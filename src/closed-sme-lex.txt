!This file contains the following  closed parts of speech

!Pronoun
!Subjunction
!Interjection
!Particles
!Numerals



! Pronoun
! =======

!Pointing to all the subgrops, presented in this order below.
LEXICON Pronoun
 Personal ;
 Interrogative ;
 Demonstrative ;
 Reflexive ;
 Reciprocal ;
 Indefinite ;


! Personal pronouns
! -----------------

!Personal pronouns, splitting in 1st, 2nd, 3rd
LEXICON Personal
mun+Pron+Pers+Sg1:m firstperspronsg ;
mun+Pron+Pers+Du1:m firstpersprondu ;
mun+Pron+Pers+Pl1:m perspronpl ;
don+Pron+Pers+Sg2:d nonfirstperspronsg ;
don+Pron+Pers+Du2:d nonfirstpersprondu ;
don+Pron+Pers+Pl2:d perspronpl ;
son+Pron+Pers+Sg3:s nonfirstperspronsg ;
son+Pron+Pers+Du3:s nonfirstpersprondu ;
son+Pron+Pers+Pl3:s perspronpl ;
dat+Pron+Pers:da datpers ;                ! Dat lea ruovttus.

!Case suffixes for 1st Sg, pointing to K lexicon
LEXICON firstperspronsg
 +Nom:un K ;
 +Nom:on K ;
 +Acc:u K ;
 +Gen:u K ;
 +Ill:unnje K ;
 +Loc:us K ;
 +Com:ujnna K ;
 +Ess:unin K ;

!Case suffixes for noŋst Sg, pointing to K lexicon
LEXICON nonfirstperspronsg
 +Nom:on K ;
 +Acc:u K ;
 +Gen:u K ;
 +Ill:utnje K ;
 +Loc:us K ;
 +Com:ujnna K ;
 +Ess:unin K ;

!Case suffixes for 1st Du, pointing to K lexicon
LEXICON firstpersprondu
 +Nom:oaj K ;
 +Acc:unno K ;
 +Gen:unno K ;
 +Ill:unnujde K ;
 +Loc:unnos K ;
 +Com:unnujn K ;
 +Ess:unnon K ;

!Case suffixes for noŋst Du, pointing to K lexicon
LEXICON nonfirstpersprondu
 +Nom:oaj K ;
 +Acc:udno K ;
 +Gen:udno K ;
 +Ill:udnujde K ;
 +Loc:udnos K ;
 +Com:udnujn K ;
 +Ess:udnon K ;

!Case suffixes for all Pl forms, pointing to K lexicon
LEXICON perspronpl
 +Nom:ij K ;
 +Acc:in K ;
 +Gen:in K ;
 +Ill:idjijde K ;
 +Loc:is K ;
 +Com:ingujn K ;
 +Ess:inin K ;

LEXICON datpers
 +Sg3:0 demcassg ;
 +Pl3:0 demcaspl ;
 +Ess:nin K ;

! Interrogative pronouns
! ----------------------

!Giving ideosyncratic Sg Nom of gii, mii lexically
!Sending the oblique forms of gii, mii to an oblique sublexicon
!Giving the stem of guhte, guhtemuš, goabbá
LEXICON Interrogative
mii+Pron+Interr+Sg+Nom:mij K-son ;
mii+Pron+Interr+Sg+Acc:májd K-son ;
gii+Pron+Interr+Sg+Nom:gij K-son ;
mii+Pron+Interr:ma oblintercas ;
gii+Pron+Interr:geaX5 oblintercas ;
guh0te+Pron+Interr:guh'te GUHTECASE ;
guhtemuš+Pron+Interr:guđemužž GUHTEMUŠCASE ;
guhtemuš+Pron+Interr:guhtemuš K-son ; !deviating Nom alternant.
goabbá+Pron+Interr:goabbá GUHTECASE ;
man% láhkái+Pron+Interr+Sg+Com:mainnalágiin K-son ; ! XXX hmm...

LEXICON oblintercas
 +Sg+Acc:jd K ;
 +Pl+Nom:t K ; 
 +Ess:nin K ;
 +Sg:0 obldemcassg ;
 +Pl:0 obldemcaspl ;
!Then going directly to the case paradigm of the demonstratives

! guhte
LEXICON GUHTECASE
 GUHTE-NE ;
 GU-GU-OBL ;

! Nom and Ess of guhte
LEXICON GUHTE-NE
 +Sg+Nom: K-son ;
 +Ess:n K-son ;

! guhtemuš
LEXICON GUHTEMUŠCASE
 GUHTEMUŠ-NE ;
 :a GU-GU-OBL ;

! Nom and Ess of guhtemuš
LEXICON GUHTEMUŠ-NE
 +Sg+Nom: K-son ;
 +Ess:an K-son ;

! Oblique paradigm for guhte and guhtemuš
LEXICON GU-GU-OBL
 +Sg+Gen:X8 K-son ;
 +Sg+Acc:X8 K-son ;
 +Sg+Ill:X3j K-son ;
!+Sg+Gen: K-son ; ! this should go out
 +Sg+Loc:X4s K-son ;
 +Sg+Com:X5jn K-son ;
 +Pl+Nom:X4t K-son ;
 +Pl+Gen:X5jd K-son ;
 +Pl+Acc:X5jd K-son ;
 +Pl+Ill:X5jde K-son ;
 +Pl+Com:X5jgujn K-son ;


! Demonstrative pronouns
! ----------------------

!Giving baseform + all demonstrative stems
!Pointing to case paradigms

LEXICON Demonstrative
dat+Pron+Dem:da demcas ;
diet+Pron+Dem:die demcas ;
dát+Pron+Dem:dá demcas ;
duot+Pron+Dem:duo demcas ;
dot+Pron+Dem:do demcas ;
dahtanaga+Pron+Dem:da Dahtanaga-par ;
dakkár+Pron+Dem:dakkár DAKKAR ;
dákkar+Pron+Dem:dákkar DAKKAR ; ! XXX substandard
dakkar+Pron+Dem:dakkar DAKKAR ; ! XXX substandard
dákkár+Pron+Dem:dákkár DAKKAR ; 
diekkár+Pron+Dem:diekkár DAKKAR ;
makkár+Pron+Dem:makkár DAKKAR ;
dakkáraš+Pron+Dem:dakkáražž DAKKARAS ;
dákkáraš+Pron+Dem:dákkáražž DAKKARAS ;
diekkáraš+Pron+Dem:diekkáražž DAKKARAS ;
makkáraš+Pron+Dem:makkáražž DAKKARAS ;
 

!Splitting into Sg, Pl and Ess, different paradigms
LEXICON demcas
 +Sg:0 demcassg ;
 +Pl:0 demcaspl ;
 +Ess:nin K ;

!Splitting into oblique and nonoblique
!This is because of the interrogatives
LEXICON demcassg
 nomdemcassg ;
 obldemcassg ;
LEXICON demcaspl
 nomdemcaspl ;
 obldemcaspl ;

LEXICON nomdemcassg
!The nominative, then pointing to clitics
!This could be conflated, but look out for interr
 +Nom:t K ;
LEXICON nomdemcaspl
 +Nom:t K ;

LEXICON obldemcassg
!The other cases, then pointing to clitics
 +Gen:n K ;
 +Acc:n K ;
 +Ill:sa K ;
 +Loc:s K ;
 +Com:jnna K ;
LEXICON obldemcaspl
 +Gen:jd K ;
 +Acc:jd K ;
 +Ill:jdda K ;
 +Loc:jn K ;
 +Loc:jnna K ;
 +Com:jguin K ;

!This case paradigm is exceptional and partial.
LEXICON Dahtanaga-par
 +Sg+Nom:htanaga # ;
 +Sg+Gen:mmanaga # ;
 +Sg+Acc:mmanaga # ;
 +Sg+Ill:sanaga # ;
 +Sg+Loc:sttánaga # ;
 +Sg+Com:jnnanaga # ;
 +Pl+Ill:jddanaga # ;
 +Pl+Loc:jnnanaga # ;

LEXICON DAKKAR
 +Sg+Nom: K ;
 +Attr: K ;
 +Ess:in K ;
 +Sg+Gen:a K ;
 +Sg+Acc:a K ;
 +Pl+Nom:at K ;
 00:i DAKKARI- ;

LEXICON DAKKARI-
 +Sg+Ill:j K ;
 +Sg+Loc:s K ;
 +Sg+Com:jn K ;
 +Pl+Gen:jd K ;
 +Pl+Acc:jd K ;
 +Pl+Ill:jdda K ;
 +Pl+Loc:jn K ;
 +Pl+Com:jgujn K ;

LEXICON DAKKARAS
 +Sg+Nom: K ;
 +Sg+Nom:X7 R ;
 +Ess:an K ;
 :a DAKKARASSA- ;

LEXICON DAKKARASSA-
  +Sg+Gen:X4 K ;
  +Sg+Gen:X8 K ;
  +Sg+Acc:X4 K ;
  +Sg+Acc:X8 K ;
  +Sg+Ill:X3j K ;
  +Sg+Loc:X4s K ;
  +Sg+Loc:X8s K ;
  +Sg+Com:X5jn K ;
  +Pl+Nom:X4t K ;
  +Pl+Gen:X5jd K ;
  +Pl+Acc:X5jd K ;
  +Pl+Ill:X5jde K ;
  +Pl+Loc:X5jn K ;
  +Pl+Com:X5jgujn K ;



! Reflexive pronouns
! ------------------

LEXICON Reflexive
!Two nominative reflexives, and pointer to the rest
!The Pl one is used for Du as well, here given two entries. 
!Should one of them be removed?
ieš+Pron+Refl+Sg+Nom:ieš K ;
ieš+Pron+Refl+Du+Nom:ieža K ;
ieš+Pron+Refl+Pl+Nom:ieža K ;
ieš+Pron+Refl+Ess:iehčanassij K ;
ieš+Pron+Refl:0 reflobl ;

LEXICON reflobl
!The V/V2 alternation due to 3rd person vocalism
!Pointing directly to 1st and 3rd person sublexica for AG & Loc.
!Pointing to exceptional Pl sublexicon for AG and Loc.
+Acc:ieža PxV2 ;
+Gen:ieža PxV2 ;
+Acc:iehča Px1V ;
+Gen:iehča Px1V ;
+Acc:iežajd PxCPlstem ;
+Gen:iežajd PxCPlstem ;
+Ill:alcces PxC ;
+Ill:alcca PxV ;
+Ill:alcce PxV2 ;
+Loc:alddi PxV ;
+Loc:aldde Px3V ;
+Loc:alddijn PxCPlstem ;
+Com:iežajn PxC ;
+Ess:iehčan PxC ;

LEXICON PxCPlstem
!This lexicon is here since it only applies to Reflexives.
!It handles the exeptional AG and Loc DuPl plural stem forms of ieš.
 +PxDu1:eame K ;
 +PxDu2:eatte K ;
 +PxDu3:easkka K ;
 +PxPl1:eamet K ;
 +PxPl2:eattet K ;
 +PxPl3:easet K ;


! Reciprocal pronouns
! -------------------

!The first four entries handle the first element of the recipr.
!The next 12 handle the 2nd part of the non-Px recipr.
!The next  point to Px lexica.
LEXICON Reciprocal
nubbi+Pron+Recipr+Du:nubbi # ;
nubbi+Pron+Recipr+Pl:nuppit # ;
goabbat+Pron+Recipr+Du:goabbat # ;
goabbat+Pron+Recipr+Pl:guhtet # ;

nuppi+Pron+Recipr+Du+Acc:nuppi K ;
nuppi+Pron+Recipr+Du+Gen:nuppi K ;
nuppi+Pron+Recipr+Du+Ill:nubbái K ;
nuppi+Pron+Recipr+Du+Loc:nuppis K ;
nuppi+Pron+Recipr+Du+Com:nuppiin K ;
nuppi+Pron+Recipr+Du+Ess:nubbin K ;
nuppi+Pron+Recipr+Pl+Acc:nuppiid K ;
nuppi+Pron+Recipr+Pl+Gen:nuppiid K ;
nuppi+Pron+Recipr+Pl+Ill:nuppiide K ;
nuppi+Pron+Recipr+Pl+Loc:nuppiin K ;
nuppi+Pron+Recipr+Pl+Com:nuppiiguin K ;
nuppi+Pron+Recipr+Pl+Ess:nubbin K ;


guoibmi+Pron+Recipr+Sg+Acc:guoibmá Px1VRecipr ;
guoibmi+Pron+Recipr+Sg+Gen:guoibmá Px1VRecipr ;
!guoibmi+Pron+Recipr+Sg+Acc:guoimmá Px2VRecipr ;
!guoibmi+Pron+Recipr+Sg+Gen:guoimmá Px2VRecipr ;
guoibmi+Pron+Recipr+Sg+Acc:guoimmi Px23VRecipr ;
guoibmi+Pron+Recipr+Sg+Gen:guoimmi Px23VRecipr ;
guoibmi+Pron+Recipr+Sg+Ill:guoibmás PxCPlstem ;
guoibmi+Pron+Recipr+Sg+Loc:guoimmist PxCPlstem ;
guoibmi+Pron+Recipr+Sg+Com:guoimmiin PxCPlstem ;

guoibmi+Pron+Recipr+Pl+Acc:guoimmiid PxCPlstem ;
guoibmi+Pron+Recipr+Pl+Gen:guoimmiid PxCPlstem ;
guoibmi+Pron+Recipr+Pl+Ill:guoimmiidasa PxVPlStemRecipr ;
guoibmi+Pron+Recipr+Pl+Loc:guoimmiin PxCPlstem ;
guoibmi+Pron+Recipr+Pl+Com:guoimmiid PxCPlComRecipr ;

LEXICON Px1VRecipr
 +PxDu1:me K ;
 +PxPl1:met K ;

LEXICON Px23VRecipr
!LEXICON Px2VPlStem
 +PxDu2:X2de K ;
 +PxPl2:X2det K ;

!LEXICON Px3VPlStem
 +PxDu3:ska K ;
 +PxPl3:set K ;

LEXICON PxVPlStemRecipr
 +PxPl1:met K ;
 +PxPl2:det K ;
 +PxPl3:set K ;

LEXICON PxCPlComRecipr
 +PxPl1:eametguin K ;
 +PxPl2:eadetguin K ;
 +PxPl3:easetguin K ;

 

! Relative pronouns
! -----------------

!LEXICON Relative
! At the moment I just omit the relative pronoun lexicon.
! From a morphological point of view, relatives and interrogatives are
! (completely?) identical, and the +Interr tag thus will serve them
! both. The issue must be reopened when we make the constraint grammar.
!mii+Pron+Rel+Sg+Nom:mij K-son ;
!gii+Pron+Rel+Sg+Nom:gij K-son ;
!mii+Pron+Rel:mo oblintercas ;
!gii+Pron+Rel:gea oblintercas ;


! Indefinite pronouns
! -------------------

! Dividing the indefinites in three groups
LEXICON Indefinite
 declindef-cl ;
 declindef ;
 declindef-idiosync ;
 indeclindef ;

! Declineable indefinite pronouns with case + clitic
! - - - - - - - - - - - - - - - - - - - - - - - - - 

LEXICON declindef-cl
mihkkege+Pron+Indef+Sg+Nom:mihkkege # ;
mihkke+Pron+Indef+Sg+Nom:mihkke # ;
giige+Pron+Indef+Sg+Nom:gijge # ;
iešgiige+Pron+Indef+Sg+Nom:iešgijge # ;
mihkkege+Pron+Indef:ma oblindefcas ;
giige+Pron+Indef:gea oblindefcas ;
iešgiige+Pron+Indef:iešgea oblindefcas ;
guhtege+Pron+Indef:guh'te GUHTEGECASE ;
iešguhtege+Pron+Indef:iešguh'te GUHTEGECASE ;
guhtemušge+Pron+Indef:guđemužž GUHTEMUSGECASE ;
guhtemušge+Pron+Indef:guhtemušge # ; !deviating Nom altarnant.
goabbáge+Pron+Indef:goabbá GUHTEGECASE ;
!makkárge+Pron+Indef (have a look at this one)
!oktage+Pron+Indef (wait with this one until numerals are treated)


LEXICON oblindefcas
 +Sg:0 oblindemcassg ;
 +Pl:0 oblindemcaspl ;

LEXICON oblindemcassg
!The other cases, then pointing to clitics
 +Gen:nge # ;
 +Acc:nge # ;
 +Ill:sage # ;
 +Loc:sge # ;
 +Loc:stege # ;
 +Com:jnnage # ;

LEXICON oblindemcaspl
 +Gen:jdege # ;
 +Acc:jdege # ;
 +Ill:jddage # ;
 +Loc:jnge # ;
!Here we do not go to the case paradigm of the demonstratives,
!since the clitic path is different. ComPl lacks (intentionally) from paradigm.


! guhtege
LEXICON GUHTEGECASE
 GUHTEGE-NE ;
 GUGE-GUGE-OBL ;


! Nom and Ess of guhtege
LEXICON GUHTEGE-NE
 +Sg+Nom:ge # ;
 +Ess:nge # ;

! guhtemušge
LEXICON GUHTEMUSGECASE
 GUHTEMUSGE-NE ;
 :a GUGE-GUGE-OBL ;

! Nom and Ess of guhtemušge
LEXICON GUHTEMUSGE-NE
 +Sg+Nom:ge # ;
 +Ess:ange # ;

! Oblique paradigm for guhtege and guhtemušGE
LEXICON GUGE-GUGE-OBL
 +Sg+Gen:X8ge # ;
 +Sg+Acc:X8ge # ;
 +Sg+Ill:X3jge # ;
!+Sg+Gen:ge # ; ! this should go out
 +Sg+Loc:X4sge # ;
 +Sg+Com:X5jnge # ;
 +Pl+Gen:X5jdge # ;
 +Pl+Acc:X5jdge # ;
 +Pl+Ill:X5jdege # ;
!+Pl+Com:X5jgujnge # ; !ComPl is out.


! Declineable indefinites with normal case paradigms
! - - - - - - - - - - - - - - - - - - - - - - - - - - 

LEXICON declindef

eará+Pron+Indef:eará indef-a-even-v ;
iežá+Pron+Indef:iežá indef-a-even-v ;
uhccánaš+Pron+Indef:uhccánažž indef-a-even-c ;
unnánaš+Pron+Indef:unnánažž indef-a-even-c ;

ea0nas+Pron+Indef:eatnas indef-a-uneven ;
ea0naš+Pron+Indef:eatnaš indef-a-uneven ;
muhtin+Pron+Indef:muhtim indef-a-uneven-gahpir ;
muhtun+Pron+Indef:muhtum indef-a-uneven-gahpir ;
uhccán+Pron+Indef:uhccán indef-a-uneven-gahpir ;
unnán+Pron+Indef:unnán indef-a-uneven-gahpir ;
veháš+Pron+Indef:veháž indef-a-uneven-gahpir ;
váháš+Pron+Indef:váháž indef-a-uneven-gahpir ;
veaháš+Pron+Indef:veaháž indef-a-uneven-gahpir ;

soa0mes+Pron+Indef+Sg+Nom:soames K ;
soa0mes+Pron+Indef+Ess:soapmásin K ;
soa0mes+Pron+Indef:soapmás indefattr ;
soa0mes+Pron+Indef:soapmásX2 indefdeclsguneven-ag ;
soa0mes+Pron+Indef:soapmás indefdeclsguneven-ilc ;
soa0mes+Pron+Indef:soapmás indu-pl ;

nuppát+Pron+Indef:nuppád indefsg-a-uneven ;

duobbá+Pron+Indef:duobbá indef-a-even-v ;
dábbá+Pron+Indef:dábbá indef-a-even-v ;
juobbá+Pron+Indef:juobbá indef-a-even-v ;
juobbágoabbá+Pron+Indef:juobbágoabbá indef-a-even-v ;
nubbi+Pron+Indef:nubbi indef-a-even-v ;
juohkehaš+Pron+Indef:juohkehažž indef-a-even-c ;

ollut+Pron+Indef:ollu indefdeclpleven ;
olut+Pron+Indef:olu indefdeclpleven ;
moanat+Pron+Indef:moana indefdeclpleven ;
gallát+Pron+Indef+Attr:gallát K ;
buohkat+Pron+Indef:buohka indefdeclpleven ;
goappašagat+Pron+Indef:goappašaga indefdeclpleven ;

ollugat+Pron+Indef:ollug indu-pl-gahpir ;
oallugat+Pron+Indef:oallug indu-pl-gahpir ;
olugat+Pron+Indef:olug indu-pl-gahpir ;
gal0lis+Pron+Indef:gal'lis indefdecluneven ;
olus+Pron+Indef:olus indefdecluneven ;
ollus+Pron+Indef:ollus indefdecluneven ; ! substandard
soamis+Pron+Indef:soamis indefdecluneven ;
 
guoktasat+Pron+Indef:guoktas indu-pl-gahpir ;
goappašat+Pron+Indef:goappaš indu-pl-gahpir ;
eanebut+Pron+Indef+Nom:eanebut K ;
eanebut+Pron+Indef:eanebu indefdeclpluneven-obl ;
eanemusat+Pron+Indef:eanemus indu-pl ;
eatnagat+Pron+Indef:eatnag indu-pl-gahpir ;
galládat+Pron+Indef:gallád indu-pl-gahpir ;


! Declension lexica for the declineable indefinites
! - - - - - - - - - - - - - - - - - - - - - - - - -

!splitting v- and c-final evensyll in two for NomEss, as usual.
LEXICON indef-a-even-v
 indefattr ;
 indefdecleven-v-ne ;
 indefdecleven-obl ;

LEXICON indef-a-even-c
 indefattr ;
 indefdecleven-c-ne ;
 :a indefdecleven-obl ;

LEXICON indefdecleven-obl
 indefdeclsgeven ;
 indefdeclpleven ;

LEXICON indefdecleven-v-ne
 +Sg+Nom: K ;
 +Ess:n K ;
 
LEXICON indefdecleven-c-ne
 +Sg+Nom: K ;
 +Ess:an K ;

LEXICON indefdeclsgeven
 +Sg+Gen:X4 K ;
 +Sg+Acc:X4 K ;
 +Sg+Ill:X3j K ;
 +Sg+Loc:X4s K ;
 +Sg+Com:X5jn K ;

LEXICON indefdeclpleven
 +Pl+Nom:X4t K ;
 +Pl+Gen:X5jd K ;
 +Pl+Acc:X5jd K ;
 +Pl+Ill:X5jde K ;
 +Pl+Loc:X5jn K ;
 +Pl+Com:X5jgujn K ;


! Intermediate lexica for uneven indefinite pronouns
! - - - - - - - - - - - - - - - - - - - - - - - - - - 

LEXICON indef-a-uneven
 indefattr ;
 indefdecluneven ;

LEXICON indef-a-uneven-gahpir
 indefattr ;
 indefdecluneven-gahpir ;
 
LEXICON indefsg-a-uneven
 indefattr ;
 indu-sg ;
 
LEXICON indefdecluneven
 indu-sg ;
 indu-pl-gahpir ;
  
LEXICON indu-sg 
 :X4 indef-sg-u-n ;
 indef-sg-u-e ;
 :X2 indefdeclsguneven-ag ;
 indefdeclsguneven-ilc ;

LEXICON indu-pl ! is this needed at all? check!!!
 :X2 indefdeclpluneven-n ;
 :iX2 indefdeclpluneven-obl ;

LEXICON indefdecluneven-gahpir
 indu-sg-gahpir ;
 indu-pl-gahpir ;

LEXICON indu-sg-gahpir
 indefdeclsguneven-ne ;
 indefdeclsguneven-ag ;
 indefdeclsguneven-ilc ;

LEXICON indu-pl-gahpir
 indefdeclpluneven-n ;
 :i indefdeclpluneven-obl ;


! Suffix lexica for uneven indefinite pronouns
! - - - - - - - - - - - - - - - - - - - - - - -

LEXICON indefdeclsguneven-ne
 indef-sg-u-n ;
 indef-sg-u-e ;

LEXICON indef-sg-u-n
 +Sg+Nom: K ;

LEXICON indef-sg-u-e
 +Ess:in K ;

LEXICON indefdeclsguneven-ag
 +Sg+Gen:a K ;
 +Sg+Acc:a K ;

LEXICON indefdeclsguneven-ilc
 +Sg+Ill:ij K ;
 +Sg+Loc:is K ;
 +Sg+Com:ijn K ;

LEXICON indefdeclpluneven-n
 +Pl+Nom:at K ;

LEXICON indefdeclpluneven-obl
 +Pl+Gen:jd K ;
 +Pl+Acc:jd K ;
 +Pl+Ill:jdda K ;
 +Pl+Loc:jn K ;
 +Pl+Com:jgujn K ;

LEXICON indefattr
 +Attr:0 K ;


! Multiword indefinites
! - - - - - - - - - - -

feara% mii+Pron+Indef+Sg+Nom:feara% mij K-son ;
feara% mii+Pron+Indef+Sg+Acc:feara% májd K-son ;
feara% gii+Pron+Indef+Sg+Nom:feara% gij K-son ;
feara% mii+Pron+Indef:feara% ma oblintercas ;
feara% gii+Pron+Indef:feara% gea oblintercas ;

! some multiword prons, according to Nickel
!feara% mii iind ;
!feara% gii iind ;
mii% nu iind ;
gii% nu iind ;
makkár% nu iind ;
makkaráš% nu iind ;


! The indeclineable indefinites
! - - - - - - - - - - - - - - - 

LEXICON indeclindef
buot iind ;
eatnat iind ;
!fal iind ; ! cf. Nickel s. 126.
gallát iind ;
goabbat iind ;
goappaš iind ;
goappašat iind ;
guhtet iind ;
ihkinassii iind ;
juohke iind ;
ollu iind ;
vaikko iind ;

 
LEXICON iind
 +Pron+Indef:0 K ;



! Separate lexica for exceptional entries
! - - - - - - - - - - - - - - - - - - - - 

LEXICON declindef-idiosync
! separate lexica for these entries:
! oktat
 earrás EARRAS ;
iešguhtet+Pron+Indef+Nom:iešguhtet K ;
iešguhtet+Pron+Indef+Gen:iešguđet K ;
iešguhtet+Pron+Indef+Acc:iešguđet K ;
 seammá+Pron+Indef:seamm SEAMMA ;
 juoga+Pron+Indef:juo JUOGA ;
 gal0le+Pron+Indef:gal'l GUOKTE ;
! okta
! juoga mii
!oktahat         +       no      OKTAHAT => indefeven / Nom=aktahat / OKTAttr
!        OKTattr:n=oktahat, aktahat, oktat, aktat
!                o=ovttahat, ovttat
!hárvvis        +       a       indef-a-      uneven  hárve, hárvves
!hárvvis        +       a       indef-a-      contr   hárve, hárvves
!guktot          p+      pd      indefdeclpl     contr                            
!Some problematic ones, one form for nsg, one for the rest
!oktat+Pron+Indef+Sg+Nom:oktat # ;
!oktat+Pron+Indef:ovttat # ;
!oktahat+Pron+Indef+Sg+Nom:oktahat # ;
!oktahat+Pron+Indef+:ovttahat # ;

LEXICON EARRAS
 +Sg+Acc:a # ;
 +Sg+Gen:a # ;
 +Sg+Ill:ij # ;
 +Sg+Loc:is # ;
 +Sg+Com:in # ;
! Essive and plural ??? 

LEXICON SEAMMA
 +Sg+Nom:á # ;
 +Sg+Acc:á # ;
 +Sg+Gen:á # ;
 +Sg+Ill:áj # ;
 +Sg+Loc:ás # ;
 +Sg+Com:ájn # ;
 +Pl+Nom:át # ;
 +Pl+Acc:ájd # ;
 +Pl+Gen:ájd # ;
 +Attr:á # ;
 +Attr:a # ;

LEXICON JUOGA
 +Sg+Nom:ga # ;
 +Sg+Acc:ga # ;
 +Sg+Acc:jdá # ;
 +Sg+Acc:jddá # ;
 +Sg+Acc:jaaájd # ;
 +Sg+Gen:ga # ;
 +Sg+Ill:sat # ;
 +Sg+Loc:stá # ;
 +Sg+Com:jdájn # ;
 +Ess:nin # ;
 +Attr:da # ;






! Subjunction
! ===========


LEXICON Subjunction

ahte Cs ;
amas Cs ;
!dahje Cs ; ! CC only (?)
danin% go Cs ;
daningo Cs ;
danne% go Cs ; 
das% go Cs ;
dasgo Cs ;
dasságo Cs ;
de Cs ;
dušše% go Cs ;
duššego Cs ;
!eará% go Cs ; ! Cs takes a clause, "eará go" can take a NP.
earágo Cs ; ! The Cs tag is not ideal, CS is a barrier in many rules.
earetgo Cs ;
!earret% go Cs ;
earretgo Cs ;
ehte Cs ;
!ele% ge Cs ; ! CC only (?)
geardde Cs ;
gearddi Cs ;
go Cs ;
nu% guhkásgo Cs ; ! Deviant, "guhkás go" expected
jes Cs ;
jis Cs ;
joas  Cs ;
!jogo Cs ; ! Moved to adv
jos Cs ;
juogo Cs ;
juos Cs ;
juos% go Cs ;
juosgo Cs ;
jus Cs ;
maŋ'ŋágo Cs ;
maŋŋágo Cs ;
maŋŋel% go Cs ;
maŋŋelgo Cs ;
maŋŋá% go Cs ;
muhte Cs ;
!muhto Cs ; ! CC instead?
!ovdal Cs ; ! Pp only without go (?)
ovdal% go Cs ;
ovdalgo Cs ;
plus Cs ;
seammás% go Cs ;
sehke Cs ; !Not in corpus.
!sihke Cs ; ! moved to adv
vái Cs ;
váikko Cs ;
vai Cs ;
vaikke Cs ;
vaikko Cs ;
váikke Cs ;

LEXICON Cs

 +CS:0 # ;





! Conjunction
! ===========


LEXICON Conjunction

ahte Cc ; ! In expressions like "heajut ahte heajut"
ja  Cc ;
ja/dahje Cc ;
dahje Cc ;
dahjege Cc ;
dahege Cc ;
dehe Cc ;
dehege Cc ;
ele Cc ;
elege Cc ;
ele% ge Cc ;
muhto Cc ;
vai Cc ;

LEXICON Cc

 +CC:0 # ;


! Interjection
! ============


LEXICON Interjection 

a-a Ij ;
á-a Ij ;
ádje-da Ij ;
ái Ij ;
ámen Ij ;
ámmen Ij ;
ámen Ij ;
aamen Ij ;
amen Ij ;
ámmozan Ij ;
áš Ij ;
báv Ij ;
bea Ij ;
bešš Ij ;
bevggo-be Ij ;
bis-bis Ij ;
bodde Ij ;
buorat Ij ;
buorát Ij ;
bures Ij ;
buse-buse Ij ;
cecce Ij ;
ci Ij ;
cilo Ij ;
cuoh:cuoh3 Ij ;
cuš Ij ;
cyš Ij ;
cys Ij ;
danheakka Ij ;
dáhkka Ij ;
dearvva Ij ;
fánten Ij ;
ficce Ij ;
fihcce Ij ;
fuoi Ij ;
fuos Ij ;
gáppo-gáppo Ij ;
gea Ij ;
geah:geah3 Ij ;
giš Ij ;
Gucco Ij ;
gus Ij ;
hái Ij ;
he-hei Ij ;
hei Ij ;
hivás Ij ;
hohoh:hohoh3 Ij ;
hohoi Ij ;
hoi Ij ;
huh:huh3 Ij ;
huf Ij ;
hurá Ij ;
hurádis Ij ;
hyš Ij ;
iš Ij ;
já Ij ;
já-já Ij ;
jo Ij ;
juo Ij ;
juo Ij ;
kyš-kyš Ij ;
ma-maid Ij ;
maid:majd Ij ;
morá Ij ;
oho Ij ;
ohoi Ij ;
skole Ij ;
skolle Ij ;
skol Ij ;
šlečč Ij ;
uš Ij ;
vuoi Ij ;

LEXICON Ij

 +Interj:0 # ;





! Particles
! =========


LEXICON Particles
abai pcle ;
ahteson pcle ;
aitto pcle ;
almma pcle ;
ama pcle ;
amal pcle ;
amat pcle ;
amma pcle ;
ammahal pcle ;
ammal pcle ;
ammat pcle ;
ammes pcle ;
appason pcle ;
ba pcle ;
bat pcle ;
!be pcle ; only in Finland as clitic?
dat pcle ;
dego pcle ; 
eaigga pcle ;
eiddo pcle ;
eigga pcle ;
eitto pcle ;
galahal pcle ;
galahan pcle ;
galhal pcle ;
ge pcle ;
ges pcle ;
gis pcle ;
go pcle ;
goit:gojt pcle ;
goitge:gojtge pcle ;
goittot:gojttot pcle ;
goittotge:gojttotge pcle ;
hás pcle ;
hal pcle ;
han pcle ;
na pcle ;
naj pcle ;
no pcle ;
noa pcle ;
son pcle ;

LEXICON pcle
+Pcle:0 # ;
! Perhaps this should be opened to  a direction to K
! and all the ge versions should be removed.
! (i.e. only goit, not goitge). This errouneously 
! permits gege, goge, etc., though, and we thus leave
! things as they are.



! Numerals
! ========

! The initial lexica
! ------------------

LEXICON Numeral
MILJON ;          ! a noun of its own
UNDERDUHAT ;      ! for generator under 1000
JUSTDUHAT ;       ! going via 1000
OVERDUHAT ;       ! for generator over 1000
OLD ;             ! for "thirteen hundred, etc.
num-ordinal ;     ! The basic ordinal numbers
!num-derived ;    ! still unimplemented
num-imprecise ;   ! still almost unimplemented
ARABIC ;          ! for the arabic numerals
ROMAN ;           ! for the roman numerals
NUM-PREFIXES ;    ! for §34 etc.
ISOLATED-NUMEXP ; ! for ½ etc.

! We now run through the alphabetic numerals, ending up with the case
! inflection of the final number. Intermediate case inflection is still
! unimplemented.


! Million, a noun of its own
! --------------------------

LEXICON MILJON
miljon+Num+Sg+Nom:miljon K ;
miljon+Num+Sg+Ess:miljovdnan K ;
miljon+Num:miljovdna OKTA-NE ;
miljon:miljovdna OKTA-OBL ;



! Overduhat, for the nuumerals over 1000
! --------------------------------------

LEXICON OVERDUHAT
O-OKTAF ;     ! 1000
O-2TO9F ;     ! 2000, 3000, ...
O-JUSTLOGIF ; ! 10000
O-2TO9LOG ;   ! 20000, 30000, ...
O-NUPPELOT ;  ! 11000, 12000, ...
O-CUODI ;     ! 100000
O-2TO9CUO ;   ! 200000, 300000, ...

! All the child lexica of OVERDUHAT have the prefix O-.
! They are directed via their respective numerals to the lexicon JUSTDUHAT.


! Thousands

LEXICON O-OKTAF
okta JUSTDUHAT ;
JUSTDUHAT ;

LEXICON O-2TO9F
guokte JUSTDUHAT ;
golbma JUSTDUHAT ;
njeallje JUSTDUHAT ;
njeallja JUSTDUHAT ;
vihtta JUSTDUHAT ;
guhtta JUSTDUHAT ;
čieža JUSTDUHAT ;
gávcci JUSTDUHAT ;
ovcci JUSTDUHAT ;


! Tens of thousands

LEXICON O-JUSTLOGIF 
logi JUSTDUHAT ;
! This lexicon is for the number 10000 only. it is separated from the
! rest to avoid forms like *logivihttaduhát, etc.


LEXICON O-LOGIF
logi JUSTDUHAT ;
logi O-OKTAF ; 
logi O-2TO9F ; 
! this lexicon is accessed only via other O-lexica, and not directly 
! from OVERDUHAT. Thus, *logivihttaduhát, etc. is avoided.

LEXICON O-2TO9LOG
guokte O-LOGIF ;
golbma O-LOGIF ;
njeallje O-LOGIF ;
njeallja O-LOGIF ;
vihtta O-LOGIF ;
guhtta O-LOGIF ;
čieža O-LOGIF ;
gávcci O-LOGIF ;
ovcci O-LOGIF ;


! Teens of thousands

LEXICON O-NUPPELOT 
okta O-NL ;
guokte O-NL ;
golbma O-NL ;
njeallje O-NL ;
njeallja O-NL ;
vihtta O-NL ;
guhtta O-NL ;
čieža O-NL ;
gávcci O-NL ;
ovcci O-NL ;

 LEXICON O-NL
 nuppelohkái JUSTDUHAT ;


! Hundreds of thousands

LEXICON O-CUODI
čuođi JUSTDUHAT ;  ! 100000
čuođi O-OKTAF ;     ! 101000
čuođi O-2TO9F ;    ! 102000, 103000, ...
čuođi O-LOGIF ;    ! 110000, 113000, ...
čuođi O-2TO9LOG ;  ! 120000, 123000, ...
čuođi O-NUPPELOT ; ! 115000, ...

LEXICON O-2TO9CUO
okta O-CUODI ;       ! #one# hundred...
guokte O-CUODI ;     
golbma O-CUODI ;
njeallje O-CUODI ;
njeallja O-CUODI ;
vihtta O-CUODI ;
guhtta O-CUODI ;
čieža O-CUODI ;
gávcci O-CUODI ;
ovcci O-CUODI ;





! Justduhat, for numrals going via 1000
! -------------------------------------

LEXICON JUSTDUHAT 
duhát:duháh DUHAT ; ! Case infl of duhát
duhát UNDERDUHAT ;  ! Thousand-and-something


! Old, for the old counting thirteen hundred etc.
! -----------------------------------------------

LEXICON OLD
okta NLX ;
guokte NLX ;
golbma NLX ;
njeallje NLX ;
njeallja NLX ;
vihtta NLX ;
guhtta NLX ;
čieža NLX ;
gávcci NLX ;
ovcci NLX ;

LEXICON NLX
nuppelohkái CUODI ;
nuppelohkái 2TO9CUO ;



! Underduhat, the numerals under 1000
! -----------------------------------


LEXICON UNDERDUHAT
OKTAF ;     ! 1, 11
2TO9F ;     ! 2-9, 12-19
LOGIF ;     ! 10
2TO9LOG ;   ! 20-99
CUODI ;     ! 100-199
2TO9CUO ;   ! 200-999

LEXICON OKTAF
ok0ta:ok'ta OKTA ;
! K ; this lead to "go +Clt" as an analysis, and was removed.
! Thus, direction to K should be made at an earlier point

LEXICON 2TO9F
guok0te:guok't GUOKTE ;
golbma GOLBMA ;
njeallje:njeallj NJEALLJE ;
njeallja:njeallj NJEALLJA ; !Does this need a lexicon its own?A variant form
vihtta:vi VIHTTA ;
guhtta:gu VIHTTA ;
čieža:čie CIEZA ;
gávcci:gák'ci GAVCCI ;
ovcci:ok'ci GAVCCI ;


LEXICON LOGIF
logi:loh'ki LOGI ;
logi OKTAF ;
logi 2TO9F ;
lot+Num+Sg+Nom:lot K ;
loh0ki:loh'ki GOAHTI ; ! vihttalohki, collective number

 LEXICON NUPPELOT 
 okta NL ;
 guokte NL ;
 golbma NL ;
 njeallje NL ;
 njeallja NL ;
 vihtta NL ;
 guhtta NL ;
 čieža NL ;
 gávcci NL ;
 ovcci NL ;

 LEXICON NL
 :nuppelohkái JUSTDUHAT ;


LEXICON 2TO9LOG
guokte LOGIF ;
golbma LOGIF ;
njeallje LOGIF ;
njeallja LOGIF ;
vihtta LOGIF ;
guhtta LOGIF ;
čieža LOGIF ;
gávcci LOGIF ;
ovcci LOGIF ;


LEXICON CUODI
!čuohti:čuođi OVERDUHAT ;
čuođi LOGI ;
čuođi LOGIF ;
čuođi 2TO9LOG ;
čuođi OKTAF ;
čuođi 2TO9F ;
čuođi NUPPELOT ;
čuoh0ti:čuoh'ti VIVVA ; ! imprecise number
čuoh0ti:čuoh'te VIVVA ; ! imprecise number

LEXICON 2TO9CUO
guokte GCUODI ;
golbma GCUODI ;
njeallje GCUODI ;
njeallja GCUODI ;
vihtta GCUODI ;
guhtta GCUODI ;
čieža GCUODI ;
gávcci GCUODI ;
ovcci GCUODI ;

LEXICON GCUODI
!čuođi OVERDUHAT ;
čuođi LOGI ;
čuođi LOGIF ;
čuođi 2TO9LOG ;
čuođi OKTAF ;
čuođi 2TO9F ;
čuođi NUPPELOT ;
čuoh0ti:čuoh'ti VIVVA ; ! imprecise number
!čuođi K ;


! Case forms of the basic digits
! ------------------------------

LEXICON OKTA
 +Num: OKTA-NE ;
 +Num: OKTA-OBL ;


LEXICON GUOKTE
 +Num:e GUOKTE-NA ;
 +Num:i GUOKTI ;

LEXICON GOLBMA
 +Num: GUOKTE-NA ;
 +Num: GUOKTI ;

LEXICON NJEALLJA
 +Num:a GUOKTE-NA ;
 +Num:a GUOKTI ;

! +Num+Sg+Gen:X4 NUPPELOHKAI ;
! +Num+Sg+Loc:X4s NUPPELOHKAI ;
! +Num+Pl+Nom:X4t NUPPELOHKAI ;
! +Num:X3 NUM-I ;
! +Num:X5 NUM-II ;

LEXICON NJEALLJE
 +Num:e GUOKTE-NA ;
 +Num:i GUOKTI ;

! +Num+Sg+Gen:iX4 NUPPELOHKAI ; ! for njealji
! +Num+Sg+Gen:eX4 NUPPELOHKAI ; ! for njealje
! +Num+Sg+Loc:X4s NUPPELOHKAI ;
! +Num+Pl+Nom:X4t NUPPELOHKAI ;
! +Num:X3 NUM-I ;
! +Num:X5 NUM-II ;

! 5 and 6, minimal stem vi-, gu- due to exceptional III-I consonant gradation
LEXICON VIHTTA
 +Num: VIHTTA-NAIE ;
 +Num: VIHTTA-OBL ;

LEXICON CIEZA
! +Num+Sg+Ill:žaX3j NUPPELOHKAI ;
! +Num+Sg+Ill:hččaX3j NUPPELOHKAI ;
 +Num+Sg+Ill:žaX3 NUPPELOHKAI ;
 +Num+Sg+Ill:hččaX3 NUPPELOHKAI ;
 +Num:h'čaX3 NUM-I ;
 +Num:h'čaX5 NUM-II ;
 +Num+Ess:hžan NUPPELOHKAI ;
 +Num:ža LOGI-WE ;


LEXICON GAVCCI ! 8,9
LOGI ;


LEXICON LOGI
 +Num:X4 LOGI-WE ;
 +Num:X3 NUM-I ;
 +Num:X5 NUM-II ;
 +Num+Ess:n K ;


! Basic digits, continuation lexica

! one

LEXICON OKTA-NE
 +Sg+Nom: NUPPELOHKAI ;
 +Ess:n NUPPELOHKAI ;

LEXICON OKTA-OBL
 :X4 OKTA-A ;
 :X4 NUM-GLN ;
 :X3 NUM-I ;
 :X5 NUM-II ;

LEXICON OKTA-A
 +Sg+Acc: NUPPELOHKAI ;

! two, three and four

LEXICON GUOKTE-NA
 +Sg+Nom: NUPPELOHKAI ;
 +Sg+Acc: NUPPELOHKAI ;
 +Sg+Gen:X4 NUPPELOHKAI ;

LEXICON GUOKTI
 :X4 NUM-GLN ;
     NUM-ESS ;
 :X3 NUM-I ;
 :X5 NUM-II ;



! Common for one through four

LEXICON NUM-GLN
 +Sg+Gen: NUPPELOHKAI ;
 +Sg+Loc:s NUPPELOHKAI ;
 +Pl+Nom:t NUPPELOHKAI ;

LEXICON NUM-ESS
 +Ess:n NUPPELOHKAI ;

LEXICON NUM-I 
 +Sg+Ill:j NUPPELOHKAI ;

LEXICON NUM-II
 +Sg+Com:jn NUPPELOHKAI ;
 +Pl+Acc:jd NUPPELOHKAI ;
 +Pl+Gen:jd NUPPELOHKAI ;
 +Pl+Ill:jde NUPPELOHKAI ;
 +Pl+Loc:jn NUPPELOHKAI ;
 +Pl+Com:jgujn NUPPELOHKAI ;


! Five and six

LEXICON VIHTTA-NAIE
 +Sg+Nom:htta NUPPELOHKAI ;
 +Sg+Acc:htta NUPPELOHKAI ;
 +Sg+Ill:httij NUPPELOHKAI ;
 +Ess:httan NUPPELOHKAI ;

LEXICON VIHTTA-OBL
 +Sg+Gen:đa NUPPELOHKAI ;
 +Sg+Loc:đas NUPPELOHKAI ;
 +Sg+Com:đain NUPPELOHKAI ;
 +Pl+Nom:đat NUPPELOHKAI ;
 +Pl+Acc:dajd NUPPELOHKAI ;
 +Pl+Gen:dajd NUPPELOHKAI ;
 +Pl+Ill:dajde NUPPELOHKAI ;
 +Pl+Loc:dajn NUPPELOHKAI ;
 +Pl+Com:dajgujn NUPPELOHKAI ;


! Seven and up

LEXICON LOGI-WE
 +Sg+Nom: K ;
 +Sg+Acc: K ;
 +Sg+Gen: K ;
 +Sg+Loc:s K ;
 +Pl+Nom:t K ;

LEXICON DUHAT
 +Num+Sg+Nom: K ;
 +Num+Sg+Acc: K ; ! Acc = Nom for duhát
 +Num+Sg+Gen: K ; ! Short genitive.
 +Num+Sg+Gen:a K ;
 +Num+Sg+Ill:ij K ;
 +Num+Sg+Loc:is K ;
 +Num+Sg+Com:ijn K ;
 +Num+Ess:in K ;
 +Num+Pl+Nom:at K ;
 +Num+Pl+Gen:ijd K ;
 +Num+Pl+Acc:ijd K ;
 +Num+Pl+Ill:ijdda K ;
 +Num+Pl+Loc:ijn K ;
 +Num+Pl+Com:ijgujn K ;

LEXICON NUPPELOHKAI
 NUPPELOHKAIGENERATOR ;
 BASICGENERATOR ;

LEXICON NUPPELOHKAIGENERATOR
      :nuppelohkáj K ; ! long -teen
 +Attr:nuppelot K ;    ! short -teen

LEXICON BASICGENERATOR
 K ;                   ! basic numbers

! This is intermediary, we need a tag moving solution to get
! vihttanuppelohkái	vihttanuppelohkái+Num+Sg+Nom instead of
! vihttanuppelohkái       vihtta+Num+Sg+Nomnuppelohkái as the old soluteion.
! Now we get vihtta+Num+Sg+Nom:vihttanuppelohkái
! the old tags:
! nuppelohkái:nuppelohkáj K ; ! long -teen
! nuppelot+Attr:nuppelot K ;  ! short -teen
! Cf relevalt chapter in B&K to fix this.


! Ordinal numbers
! ---------------


LEXICON num-ordinal
vuosttas+A+Ord:vuosttas VUOSTTAS ;
vuosttaš+A+Ord:vuosttaž VUOSTTAS ;
nubbi+A+Ord:nubbi indef-a-even-v ;
goalmmát+A+Ord:goalmmád VUOSTTAS ;
njealját+A+Ord:njealjád VUOSTTAS ;
viđát+A+Ord:viđád VUOSTTAS ;
guđát+A+Ord:guđád VUOSTTAS ;
čihččet+A+Ord:čihččed VUOSTTAS ;
gávccát+A+Ord:gávccád VUOSTTAS ;
ovccát+A+Ord:ovccád VUOSTTAS ;
logát+A+Ord:logád VUOSTTAS ;
čuođát+A+Ord:čuođád VUOSTTAS ;
duháhat+A+Ord:duháh VUOSTTAS ;
millijovnnat+A+Ord:millijovnnag VUOSTTAS ;
miljonat+A+Ord:miljonag VUOSTTAS ;

LEXICON VUOSTTAS
 GAPPUS0 ;
 GAPPUS- ;
! +A: EABBO/EAMOS ; ! No comparatives here
 +Attr: K ;


! Imprecise numbers
! -----------------

LEXICON num-imprecise
máŋga num-imprecise-distribution ;
moadde   num-imprecise-distribution ;
galle  num-imprecise-distribution ;
máŋ0ga:máŋ'ga GOLBMA ;
moadde:moadd GUOKTE ; 
gal0le:gal'le GOLBMA ;
beal-nup+Num:beal%-nup3 # ; ! ad hoc

LEXICON num-imprecise-distribution
NUPPELOHKAIGENERATOR ;  ! máŋganuppelohkái, máŋga
LOGIF ;        ! máŋgalogi, máŋgalot
CUODI ;        ! máŋgačuođi
NL ;           ! máŋganuppelohkáiduhát
O-NL ;         ! máŋganuppelohkáiduhát
O-LOGIF ;      ! máŋgalogiduhát
JUSTDUHAT ;    ! máŋgaduhát
O-CUODI ;      ! máŋgaduhát




! Arabic numerals
! ---------------


LEXICON NUM-PREFIXES
§	ARABIC ;      ! §24
§§	ARABIC ;     ! §§24
§% 	ARABIC ;    ! § 24
§§% 	ARABIC ;   ! §§ 24
%-	ARABIC ;     ! -24
%+	ARABIC ;     ! +24
%-% 	ARABIC ;	! - 24
%+% 	ARABIC ;	! + 24
%*	ARABIC ;     ! *24
%$	ARABIC ;     ! $24  ! And the Euro?
%<	ARABIC ;     ! <24
%>	ARABIC ;     ! >24
s%.	ARABIC ;     ! s.24



LEXICON ARABIC
1 ARABICLOOP ;
2 ARABICLOOP ;
3 ARABICLOOP ;
4 ARABICLOOP ;
5 ARABICLOOP ;
6 ARABICLOOP ;
7 ARABICLOOP ;
8 ARABICLOOP ;
9 ARABICLOOP ;
%0 ARABICLOOP ;

LEXICON ARABICLOOP
ARABIC ;
%-	ARABIC ;	      ! 1-2    multipart numbers
%.%- 	ARABIC ;	      ! 1.-2   multipart numbers
%-% 	ARABIC ;	      ! 1- 2   multipart numbers
% %-	ARABIC ;	      ! 1 -2   multipart numbers
% %-% 	ARABIC ;	      ! 1 - 2  multipart numbers
% %-%-%   ARABIC ;	      ! 1 -- 2 multipart numbers
% %-      ARABICDELIMITER ;   ! 1 -   multipart numbers
% %-%-	  ARABICDELIMITER ;   ! 1 --  multipart numbers
%. ARABIC ;
%, ARABIC ;
%: ARABIC ;
%  ARABIC ;	   ! Allowing for "23 500".
%/ ARABIC ;	   ! 24/12
!%' ARABIC ;	   ! Since ' denotes cons.grad. This is not optimal either.
!+Num:%- R ;	   ! Number compounds
+Num: R ;	   ! Hyphen removed bec. of "123- ja 124-viessu" 
+Num+Ord:%. # ;
,%-+Num:,%- # ;		! 10,-
%%+Num+Nom:%% # ;	! 50%. It is wrong, but 75 % of us write "75%".
% %%+Num+Nom:% %% # ;	! We must analyse the correct ones as well... 50 % etc.
+Num: ARABICDELIMITER ; ! list of number-case delimiters


LEXICON ARABICDELIMITER
  ARABICCASE ;      ! This is the 1984s case.
 :%: ARABICCASE ;    ! This is the 1984:s case.
 :%' ARABICCASE ;    ! This is the 1984's case.
 :%- ARABICCASE ;    ! This is the 1984-s case.
                     ! So, what does the lg council prefer?

LEXICON ARABICCASE
+Nom:f # ;		! s. 123f.
+Nom:ff # ;		! s. 123ff.
 # ;		! 123 is now 123+Num as well as 123+Num+Nom etc.
+Nom: # ;	
+Gen: # ;	
+Acc: # ;	
+Gen:a # ;	! But here 123a 123+Num+Gen
+Acc:a # ; 
+Ill:i # ; 
+Ill:ij # ; 
+Ill:áj # ;	! Testing over the weekend.
+Loc:s # ; 
+Com:in # ; 
+Ess:n # ; 
! No plural numbers, hmm.

LEXICON ROMAN
ROM-HUNDRED ;
M ROM-HUNDRED ;
MM ROM-HUNDRED ;
MMM ROM-HUNDRED ;

LEXICON ROM-HUNDRED

ROM-TEN ;
C ROM-TEN ;
CC ROM-TEN ;
CCC ROM-TEN ;
CD ROM-TEN ;
D ROM-TEN ;
DC ROM-TEN ;
DCC ROM-TEN ;
DCCC ROM-TEN ;
CM ROM-TEN ;


LEXICON ROM-TEN
ROM-ONE ;
X ROM-ONE ;
XX ROM-ONE ;
XXX ROM-ONE ;
XL ROM-ONE ;
L ROM-ONE ;
LX ROM-ONE ;
LXX ROM-ONE ;
LXXX ROM-ONE ;
XC ROM-ONE ;

LEXICON ROM-ONE
ROM-TAG ;
I ROM-TAG ;
II ROM-TAG ;
III ROM-TAG ;
IV  ROM-TAG ;
V ROM-TAG ;
VI ROM-TAG ;
VII ROM-TAG ;
VIII ROM-TAG ;
IX ROM-TAG ;

LEXICON ROM-TAG
+Num: # ;

LEXICON ISOLATED-NUMEXP
½+Num:½ # ;
¹+Num:¹ # ;
²+Num:² # ;
³+Num:³ # ;
¼+Num:¼ # ;
