!This file contains the following  closed parts of speech

!Pronoun
!Subjunction
!Interjection
!Particles
!Numerals



! Pronoun
! =======

!Pointing to all the subgrops, presented in this order below.
LEXICON Pronoun
 Personal ;
 Interrogative ;
 Demonstrative ;
 Reflexive ;
 Reciprocal ;
 Indefinite ;


! Personal pronouns
! -----------------

!Personal pronouns, splitting in 1st, 2nd, 3rd
LEXICON Personal
mun+Pron+Pers+Sg1:m firstperspronsg ;
mun+Pron+Pers+Du1:m firstpersprondu ;
mun+Pron+Pers+Pl1:m perspronpl ;
don+Pron+Pers+Sg2:d nonfirstperspronsg ;
don+Pron+Pers+Du2:d nonfirstpersprondu ;
don+Pron+Pers+Pl2:d perspronpl ;
son+Pron+Pers+Sg3:s nonfirstperspronsg ;
son+Pron+Pers+Du3:s nonfirstpersprondu ;
son+Pron+Pers+Pl3:s perspronpl ;

!Case suffixes for 1st Sg, pointing to K lexicon
LEXICON firstperspronsg
 +Nom:un K ;
 +Nom:on K ;
 +Acc:u K ;
 +Gen:u K ;
 +Ill:unnje K ;
 +Loc:us K ;
 +Com:ujnna K ;
 +Ess:unin K ;

!Case suffixes for non1st Sg, pointing to K lexicon
LEXICON nonfirstperspronsg
 +Nom:on K ;
 +Acc:u K ;
 +Gen:u K ;
 +Ill:utnje K ;
 +Loc:us K ;
 +Com:ujnna K ;
 +Ess:unin K ;

!Case suffixes for 1st Du, pointing to K lexicon
LEXICON firstpersprondu
 +Nom:oaj K ;
 +Acc:unno K ;
 +Gen:unno K ;
 +Ill:unnujde K ;
 +Loc:unnos K ;
 +Com:unnujn K ;
 +Ess:unnon K ;

!Case suffixes for non1st Du, pointing to K lexicon
LEXICON nonfirstpersprondu
 +Nom:oaj K ;
 +Acc:udno K ;
 +Gen:udno K ;
 +Ill:udnujde K ;
 +Loc:udnos K ;
 +Com:udnujn K ;
 +Ess:udnon K ;

!Case suffixes for all Pl forms, pointing to K lexicon
LEXICON perspronpl
 +Nom:ij K ;
 +Acc:in K ;
 +Gen:in K ;
 +Ill:idjijde K ;
 +Loc:is K ;
 +Com:ingujn K ;
 +Ess:inin K ;


! Interrogative pronouns
! ----------------------

!Giving ideosyncratic Sg Nom of gii, mii lexically
!Sending the oblique forms of gii, mii to an oblique sublexicon
!Giving the stem of guhte, guhtemus1, goabbá
LEXICON Interrogative
mii+Pron+Interr+Sg+Nom:mij K-son ;
mii+Pron+Interr+Sg+Acc:májd K-son ;
gii+Pron+Interr+Sg+Nom:gij K-son ;
mii+Pron+Interr:ma oblintercas ;
gii+Pron+Interr:gea oblintercas ;
guh0te+Pron+Interr:guh'te GUHTECASE ;
guhtemus1+Pron+Interr:gud1emuz1z1 GUHTEMUS1CASE ;
guhtemus1+Pron+Interr:guhtemus1 K-son ; !deviating Nom alternant.
goabbá+Pron+Interr:goabbá GUHTECASE ;
man% láhkái+Pron+Interr+Sg+Com:mainnalágiin K-son ; ! XXX hmm...

LEXICON oblintercas
 +Sg+Acc:jd K ;
 +Pl+Nom:t K ; 
 +Ess:nin K ;
 +Sg:0 obldemcassg ;
 +Pl:0 obldemcaspl ;
!Then going directly to the case paradigm of the demonstratives

! guhte
LEXICON GUHTECASE
 GUHTE-NE ;
 GU-GU-OBL ;

! Nom and Ess of guhte
LEXICON GUHTE-NE
 +Sg+Nom: K-son ;
 +Ess:n K-son ;

! guhtemus1
LEXICON GUHTEMUS1CASE
 GUHTEMUS1-NE ;
 :a GU-GU-OBL ;

! Nom and Ess of guhtemus1
LEXICON GUHTEMUS1-NE
 +Sg+Nom: K-son ;
 +Ess:an K-son ;

! Oblique paradigm for guhte and guhtemus1
LEXICON GU-GU-OBL
 +Sg+Gen:X8 K-son ;
 +Sg+Acc:X8 K-son ;
 +Sg+Ill:X3j K-son ;
!+Sg+Gen: K-son ; ! this should go out
 +Sg+Loc:X4s K-son ;
 +Sg+Com:X5jn K-son ;
 +Pl+Nom:X4t K-son ;
 +Pl+Gen:X5jd K-son ;
 +Pl+Acc:X5jd K-son ;
 +Pl+Ill:X5jde K-son ;
 +Pl+Com:X5jgujn K-son ;


! Demonstrative pronouns
! ----------------------

!Giving baseform + all demonstrative stems
!Pointing to case paradigms

LEXICON Demonstrative
dat+Pron+Dem:da demcas ;
diet+Pron+Dem:die demcas ;
dát+Pron+Dem:dá demcas ;
duot+Pron+Dem:duo demcas ;
dot+Pron+Dem:do demcas ;
dahtanaga+Pron+Dem:da Dahtanaga-par ;
dakkár+Pron+Dem:dakkár DAKKAR ;
dákkár+Pron+Dem:dákkár DAKKAR ; 
diekkár+Pron+Dem:diekkár DAKKAR ;
makkár+Pron+Dem:makkár DAKKAR ;
dakkáras1+Pron+Dem:dakkáraz1z1 DAKKARAS ;
dákkáras1+Pron+Dem:dákkáraz1z1 DAKKARAS ;
diekkáras1+Pron+Dem:diekkáraz1z1 DAKKARAS ;
makkáras1+Pron+Dem:makkáraz1z1 DAKKARAS ;
 

!Splitting into Sg, Pl and Ess, different paradigms
LEXICON demcas
 +Sg:0 demcassg ;
 +Pl:0 demcaspl ;
 +Ess:nin K ;

!Splitting into oblique and nonoblique
!This is because of the interrogatives
LEXICON demcassg
 nomdemcassg ;
 obldemcassg ;
LEXICON demcaspl
 nomdemcaspl ;
 obldemcaspl ;

LEXICON nomdemcassg
!The nominative, then pointing to clitics
!This could be conflated, but look out for interr
 +Nom:t K ;
LEXICON nomdemcaspl
 +Nom:t K ;

LEXICON obldemcassg
!The other cases, then pointing to clitics
 +Gen:n K ;
 +Acc:n K ;
 +Ill:sa K ;
 +Loc:s K ;
 +Com:jnna K ;
LEXICON obldemcaspl
 +Gen:jd K ;
 +Acc:jd K ;
 +Ill:jdda K ;
 +Loc:jn K ;
 +Com:jguin K ;

!This case paradigm is exceptional and partial.
LEXICON Dahtanaga-par
 +Sg+Nom:htanaga # ;
 +Sg+Gen:mmanaga # ;
 +Sg+Acc:mmanaga # ;
 +Sg+Ill:sanaga # ;
 +Sg+Loc:sttánaga # ;
 +Sg+Com:jnnanaga # ;
 +Pl+Ill:jddanaga # ;
 +Pl+Loc:jnnanaga # ;

LEXICON DAKKAR
 +Sg+Nom: K ;
 +Ess:in K ;
 +Sg+Gen:a K ;
 +Sg+Acc:a K ;
 +Pl+Nom:at K ;
 00:i DAKKARI- ;

LEXICON DAKKARI-
 +Sg+Ill:j K ;
 +Sg+Loc:s K ;
 +Sg+Com:jn K ;
 +Pl+Gen:jd K ;
 +Pl+Acc:jd K ;
 +Pl+Ill:jdda K ;
 +Pl+Loc:jn K ;
 +Pl+Com:jgujn K ;

LEXICON DAKKARAS
 +Sg+Nom: K ;
 +Sg+Nom:X7 R ;
 +Ess:an K ;
 :a DAKKARASSA- ;

LEXICON DAKKARASSA-
  +Sg+Gen:X4 K ;
  +Sg+Gen:X8 K ;
  +Sg+Acc:X4 K ;
  +Sg+Acc:X8 K ;
  +Sg+Ill:X3j K ;
  +Sg+Loc:X4s K ;
  +Sg+Loc:X8s K ;
  +Sg+Com:X5jn K ;
  +Pl+Nom:X4t K ;
  +Pl+Gen:X5jd K ;
  +Pl+Acc:X5jd K ;
  +Pl+Ill:X5jde K ;
  +Pl+Loc:X5jn K ;
  +Pl+Com:X5jgujn K ;



! Reflexive pronouns
! ------------------

LEXICON Reflexive
!Two nominative reflexives, and pointer to the rest
!The Pl one is used for Du as well, here given two entries. 
!Should one of them be removed?
ies1+Pron+Refl+Sg+Nom:ies1 K ;
ies1+Pron+Refl+Du+Nom:iez1a K ;
ies1+Pron+Refl+Pl+Nom:iez1a K ;
ies1+Pron+Refl+Ess:iehc1anassij K ;
ies1+Pron+Refl:0 reflobl ;

LEXICON reflobl
!The V/V2 alternation due to 3rd person vocalism
!Pointing directly to 1st and 3rd person sublexica for AG & Loc.
!Pointing to exceptional Pl sublexicon for AG and Loc.
+Acc:iez1a PxV2 ;
+Gen:iez1a PxV2 ;
+Acc:iehc1a Px1V ;
+Gen:iehc1a Px1V ;
+Acc:iez1ajd PxCPlstem ;
+Gen:iez1ajd PxCPlstem ;
+Ill:alcces PxC ;
+Ill:alcca PxV ;
+Ill:alcce PxV2 ;
+Loc:alddi PxV ;
+Loc:aldde Px3V ;
+Loc:alddijn PxCPlstem ;
+Com:iez1ajn PxC ;
+Ess:iehc1an PxC ;

LEXICON PxCPlstem
!This lexicon is here since it only applies to Reflexives.
!It handles the exeptional AG and Loc DuPl plural stem forms of ies1.
 +PxDu1:eame K ;
 +PxDu2:eatte K ;
 +PxDu3:easkka K ;
 +PxPl1:eamet K ;
 +PxPl2:eattet K ;
 +PxPl3:easet K ;


! Reciprocal pronouns
! -------------------

!The first four entries handle the first element of the recipr.
!The next 12 handle the 2nd part of the non-Px recipr.
!The next  point to Px lexica.
LEXICON Reciprocal
nubbi+Pron+Recipr+Du:nubbi # ;
nubbi+Pron+Recipr+PL:nuppit # ;
goabbat+Pron+Recipr+Du:goabbat # ;
goabbat+Pron+Recipr+Pl:guhtet # ;

nuppi+Pron+Recipr+Du+Acc:nuppi K ;
nuppi+Pron+Recipr+Du+Gen:nuppi K ;
nuppi+Pron+Recipr+Du+Ill:nubbái K ;
nuppi+Pron+Recipr+Du+Loc:nuppis K ;
nuppi+Pron+Recipr+Du+Com:nuppiin K ;
nuppi+Pron+Recipr+Du+Ess:nubbin K ;
nuppi+Pron+Recipr+Pl+Acc:nuppiid K ;
nuppi+Pron+Recipr+Pl+Gen:nuppiid K ;
nuppi+Pron+Recipr+Pl+Ill:nuppiide K ;
nuppi+Pron+Recipr+Pl+Loc:nuppiin K ;
nuppi+Pron+Recipr+Pl+Com:nuppiiguin K ;
nuppi+Pron+Recipr+Pl+Ess:nubbin K ;




guoibmi+Pron+Recipr+Sg+Acc:guoibmá Px1VRecipr ;
guoibmi+Pron+Recipr+Sg+Gen:guoibmá Px1VRecipr ;
!guoibmi+Pron+Recipr+Sg+Acc:guoimmá Px2VRecipr ;
!guoibmi+Pron+Recipr+Sg+Gen:guoimmá Px2VRecipr ;
guoibmi+Pron+Recipr+Sg+Acc:guoimmi Px23VRecipr ;
guoibmi+Pron+Recipr+Sg+Gen:guoimmi Px23VRecipr ;
guoibmi+Pron+Recipr+Sg+Ill:guoibmás PxCPlstem ;
guoibmi+Pron+Recipr+Sg+Loc:guoimmist PxCPlstem ;
guoibmi+Pron+Recipr+Sg+Com:guoimmiin PxCPlstem ;

guoibmi+Pron+Recipr+Pl+Acc:guoimmiid PxCPlstem ;
guoibmi+Pron+Recipr+Pl+Gen:guoimmiid PxCPlstem ;
guoibmi+Pron+Recipr+Pl+Ill:guoimmiidasa PxVPlStemRecipr ;
guoibmi+Pron+Recipr+Pl+Loc:guoimmiin PxCPlstem ;
guoibmi+Pron+Recipr+Pl+Com:guoimmiid PxCPlComRecipr ;

LEXICON Px1VRecipr
 +PxDu1:me K ;
 +PxPl1:met K ;

LEXICON Px23VRecipr
!LEXICON Px2VPlStem
 +PxDu2:X2de K ;
 +PxPl2:X2det K ;

!LEXICON Px3VPlStem
 +PxDu3:ska K ;
 +PxPl3:set K ;

LEXICON PxVPlStemRecipr
 +PxPl1:met K ;
 +PxPl2:det K ;
 +PxPl3:set K ;

LEXICON PxCPlComRecipr
 +PxPl1:eametguin K ;
 +PxPl2:eadetguin K ;
 +PxPl3:easetguin K ;

 

! Relative pronouns
! -----------------

!LEXICON Relative
! At the moment I just omit the relative pronoun lexicon.
! From a morphological point of view, relatives and interrogatives are
! (completely?) identical, and the +Interr tag thus will serve them
! both. The issue must be reopened when we make the constraint grammar.
!mii+Pron+Rel+Sg+Nom:mij K-son ;
!gii+Pron+Rel+Sg+Nom:gij K-son ;
!mii+Pron+Rel:mo oblintercas ;
!gii+Pron+Rel:gea oblintercas ;


! Indefinite pronouns
! -------------------

! Dividing the indefinites in three groups
LEXICON Indefinite
 declindef-cl ;
 declindef ;
 declindef-idiosync ;
 indeclindef ;

! Declineable indefinite pronouns with case + clitic
! - - - - - - - - - - - - - - - - - - - - - - - - - 

LEXICON declindef-cl
mihkkege+Pron+Indef+Sg+Nom:mihkkege # ;
mihkke+Pron+Indef+Sg+Nom:mihkke # ;
giige+Pron+Indef+Sg+Nom:gijge # ;
ies1giige+Pron+Indef+Sg+Nom:ies1gijge # ;
mihkkege+Pron+Indef:ma oblindefcas ;
giige+Pron+Indef:gea oblindefcas ;
ies1giige+Pron+Indef:ies1gea oblindefcas ;
guhtege+Pron+Indef:guh'te GUHTEGECASE ;
ies1guhtege+Pron+Indef:ies1guh'te GUHTEGECASE ;
guhtemus1ge+Pron+Indef:gud1emuz1z1 GUHTEMUSGECASE ;
guhtemus1ge+Pron+Indef:guhtemus1ge # ; !deviating Nom altarnant.
goabbáge+Pron+Indef:goabbá GUHTEGECASE ;
!makkárge+Pron+Indef (have a look at this one)
!oktage+Pron+Indef (wait with this one until numerals are treated)


LEXICON oblindefcas
 +Sg:0 oblindemcassg ;
 +Pl:0 oblindemcaspl ;

LEXICON oblindemcassg
!The other cases, then pointing to clitics
 +Gen:nge # ;
 +Acc:nge # ;
 +Ill:sage # ;
 +Loc:sge # ;
 +Loc:stege # ;
 +Com:jnnage # ;

LEXICON oblindemcaspl
 +Gen:jdege # ;
 +Acc:jdege # ;
 +Ill:jddage # ;
 +Loc:jnge # ;
!Here we do not go to the case paradigm of the demonstratives,
!since the clitic path is different. ComPl lacks (intentionally) from paradigm.


! guhtege
LEXICON GUHTEGECASE
 GUHTEGE-NE ;
 GUGE-GUGE-OBL ;


! Nom and Ess of guhtege
LEXICON GUHTEGE-NE
 +Sg+Nom:ge # ;
 +Ess:nge # ;

! guhtemus1ge
LEXICON GUHTEMUSGECASE
 GUHTEMUSGE-NE ;
 :a GUGE-GUGE-OBL ;

! Nom and Ess of guhtemus1ge
LEXICON GUHTEMUSGE-NE
 +Sg+Nom:ge # ;
 +Ess:ange # ;

! Oblique paradigm for guhtege and guhtemus1GE
LEXICON GUGE-GUGE-OBL
 +Sg+Gen:X8ge # ;
 +Sg+Acc:X8ge # ;
 +Sg+Ill:X3jge # ;
!+Sg+Gen:ge # ; ! this should go out
 +Sg+Loc:X4sge # ;
 +Sg+Com:X5jnge # ;
 +Pl+Gen:X5jdge # ;
 +Pl+Acc:X5jdge # ;
 +Pl+Ill:X5jdege # ;
!+Pl+Com:X5jgujnge # ; !ComPl is out.


! Indeclineable indefinites with normal case paradigms
! - - - - - - - - - - - - - - - - - - - - - - - - - - 

LEXICON declindef

eará+Pron+Indef:eará indef-a-even-v ;
iez1á+Pron+Indef:iez1á indef-a-even-v ;
uhccánas1+Pron+Indef:uhccánaz1z1 indef-a-even-c ;
unnánas1+Pron+Indef:unnánaz1z1 indef-a-even-c ;

ea0nas+Pron+Indef:eatnas indef-a-uneven ;
ea0nas1+Pron+Indef:eatnas1 indef-a-uneven ;
muhtin+Pron+Indef:muhtim indef-a-uneven-gahpir ;
muhtun+Pron+Indef:muhtum indef-a-uneven-gahpir ;
uhccán+Pron+Indef:uhccán indef-a-uneven-gahpir ;
unnán+Pron+Indef:unnán indef-a-uneven-gahpir ;
vehás1+Pron+Indef:veház1 indef-a-uneven-gahpir ;
váhás1+Pron+Indef:váház1 indef-a-uneven-gahpir ;
veahás1+Pron+Indef:veaház1 indef-a-uneven-gahpir ;

soa0mes+Pron+Indef+Sg+Nom:soames K ;
soa0mes+Pron+Indef+Ess:soapmásin K ;
soa0mes+Pron+Indef:soapmás indefattr ;
soa0mes+Pron+Indef:soapmásX2 indefdeclsguneven-ag ;
soa0mes+Pron+Indef:soapmás indefdeclsguneven-ilc ;
soa0mes+Pron+Indef:soapmás indu-pl ;

nuppát+Pron+Indef:nuppád indefsg-a-uneven ;

duobbá+Pron+Indef:duobbá indef-a-even-v ;
dábbá+Pron+Indef:dábbá indef-a-even-v ;
juobbá+Pron+Indef:juobbá indef-a-even-v ;
juobbágoabbá+Pron+Indef:juobbágoabbá indef-a-even-v ;
nubbi+Pron+Indef:nubbi indef-a-even-v ;
juohkehas1+Pron+Indef:juohkehaz1z1 indef-a-even-c ;

ollut+Pron+Indef:ollu indefdeclpleven ;
olut+Pron+Indef:olu indefdeclpleven ;
moanat+Pron+Indef:moana indefdeclpleven ;
gallát+Pron+Indef+Attr:gallát K ;
buohkat+Pron+Indef:buohka indefdeclpleven ;
goappas1agat+Pron+Indef:goappas1aga indefdeclpleven ;

ollugat+Pron+Indef:ollug indu-pl-gahpir ;
oallugat+Pron+Indef:oallug indu-pl-gahpir ;
olugat+Pron+Indef:olug indu-pl-gahpir ;
gal0lis+Pron+Indef:gal'lis indefdecluneven ;
olus+Pron+Indef:olus indefdecluneven ;
ollus+Pron+Indef:ollus indefdecluneven ; ! substandard
soamis+Pron+Indef:soamis indefdecluneven ;
 
guoktasat+Pron+Indef:guoktas indu-pl-gahpir ;
goappas1at+Pron+Indef:goappas1 indu-pl-gahpir ;
eanebut+Pron+Indef+Nom:eanebut K ;
eanebut+Pron+Indef:eanebu indefdeclpluneven-obl ;
eanemusat+Pron+Indef:eanemus indu-pl ;
eatnagat+Pron+Indef:eatnag indu-pl-gahpir ;
galládat+Pron+Indef:gallád indu-pl-gahpir ;

! Declension lexica for the declineable indefinites
! - - - - - - - - - - - - - - - - - - - - - - - - -

!splitting v- and c-final evensyll in two for NomEss, as usual.
LEXICON indef-a-even-v
 indefattr ;
 indefdecleven-v-ne ;
 indefdecleven-obl ;

LEXICON indef-a-even-c
 indefattr ;
 indefdecleven-c-ne ;
 :a indefdecleven-obl ;

LEXICON indefdecleven-obl
 indefdeclsgeven ;
 indefdeclpleven ;

LEXICON indefdecleven-v-ne
 +Sg+Nom: K ;
 +Ess:n K ;
 
LEXICON indefdecleven-c-ne
 +Sg+Nom: K ;
 +Ess:an K ;

LEXICON indefdeclsgeven
 +Sg+Gen:X4 K ;
 +Sg+Acc:X4 K ;
 +Sg+Ill:X3j K ;
 +Sg+Loc:X4s K ;
 +Sg+Com:X5jn K ;

LEXICON indefdeclpleven
 +Pl+Nom:X4t K ;
 +Pl+Gen:X5jd K ;
 +Pl+Acc:X5jd K ;
 +Pl+Ill:X5jde K ;
 +Pl+Loc:X5jn K ;
 +Pl+Com:X5jgujn K ;


! Intermediate lexica for uneven indefinite pronouns
! - - - - - - - - - - - - - - - - - - - - - - - - - - 

LEXICON indef-a-uneven
 indefattr ;
 indefdecluneven ;

LEXICON indef-a-uneven-gahpir
 indefattr ;
 indefdecluneven-gahpir ;
 
LEXICON indefsg-a-uneven
 indefattr ;
 indu-sg ;
 
LEXICON indefdecluneven
 indu-sg ;
 indu-pl-gahpir ;
  
LEXICON indu-sg 
 :X4 indef-sg-u-n ;
 indef-sg-u-e ;
 :X2 indefdeclsguneven-ag ;
 indefdeclsguneven-ilc ;

LEXICON indu-pl ! is this needed at all? check!!!
 :X2 indefdeclpluneven-n ;
 :iX2 indefdeclpluneven-obl ;

LEXICON indefdecluneven-gahpir
 indu-sg-gahpir ;
 indu-pl-gahpir ;

LEXICON indu-sg-gahpir
 indefdeclsguneven-ne ;
 indefdeclsguneven-ag ;
 indefdeclsguneven-ilc ;

LEXICON indu-pl-gahpir
 indefdeclpluneven-n ;
 :i indefdeclpluneven-obl ;


! Suffix lexica for uneven indefinite pronouns
! - - - - - - - - - - - - - - - - - - - - - - -

LEXICON indefdeclsguneven-ne
 indef-sg-u-n ;
 indef-sg-u-e ;

LEXICON indef-sg-u-n
 +Sg+Nom: K ;

LEXICON indef-sg-u-e
 +Ess:in K ;

LEXICON indefdeclsguneven-ag
 +Sg+Gen:a K ;
 +Sg+Acc:a K ;

LEXICON indefdeclsguneven-ilc
 +Sg+Ill:ij K ;
 +Sg+Loc:is K ;
 +Sg+Com:ijn K ;

LEXICON indefdeclpluneven-n
 +Pl+Nom:at K ;

LEXICON indefdeclpluneven-obl
 +Pl+Gen:jd K ;
 +Pl+Acc:jd K ;
 +Pl+Ill:jdda K ;
 +Pl+Loc:jn K ;
 +Pl+Com:jgujn K ;

LEXICON indefattr
 +Attr:0 K ;




! The indeclineable indefinites
! - - - - - - - - - - - - - - - 


LEXICON indeclindef
buot iind ;
eatnat iind ;
!fal iind ; ! cf. Nickel s. 126.
gallát iind ;
goabbat iind ;
goappas1 iind ;
goappas1at iind ;
guhtet iind ;
ihkinassii iind ;
juohke iind ;
vaikko iind ;

! some multiword prons, according to Nickel
feara% mii iind ;
feara% gii iind ;
mii% nu iind ;
gii% nu iind ;
makkár% nu iind ;
makkarás1% nu iind ;
 
LEXICON iind
 +Pron+Indef:0 K ;



! Separate lexica for exceptional entries
! - - - - - - - - - - - - - - - - - - - - 

LEXICON declindef-idiosync
! separate lexica for these entries:
! oktat
 earrás EARRAS ;
ies1guhtet+Pron+Indef+Nom:ies1guhtet K ;
ies1guhtet+Pron+Indef+Gen:ies1gud1et K ;
ies1guhtet+Pron+Indef+Acc:ies1gud1et K ;
 seammá+Pron+Indef:seamm SEAMMA ;
 juoga+Pron+Indef:juo JUOGA ;
 gal0le+Pron+Indef:gal'l GUOKTE ;
! okta
! juoga mii
!oktahat         +       no      OKTAHAT => indefeven / Nom=aktahat / OKTAttr
!        OKTattr:n=oktahat, aktahat, oktat, aktat
!                o=ovttahat, ovttat
!hárvvis        +       a       indef-a-      uneven  hárve, hárvves
!hárvvis        +       a       indef-a-      contr   hárve, hárvves
!guktot          p+      pd      indefdeclpl     contr                            
!Some problematic ones, one form for nsg, one for the rest
!oktat+Pron+Indef+Sg+Nom:oktat # ;
!oktat+Pron+Indef:ovttat # ;
!oktahat+Pron+Indef+Sg+Nom:oktahat # ;
!oktahat+Pron+Indef+:ovttahat # ;

LEXICON EARRAS
 +Sg+Acc:a # ;
 +Sg+Gen:a # ;
 +Sg+Ill:ij # ;
 +Sg+Loc:is # ;
 +Sg+Com:in # ;
! Essive and plural ??? 

LEXICON SEAMMA
 +Sg+Nom:á # ;
 +Sg+Acc:á # ;
 +Sg+Gen:á # ;
 +Sg+Ill:áj # ;
 +Sg+Loc:ás # ;
 +Sg+Com:ájn # ;
 +Pl+Nom:át # ;
 +Pl+Acc:ájd # ;
 +Pl+Gen:ájd # ;
 +Attr:á # ;
 +Attr:a # ;

LEXICON JUOGA
 +Sg+Nom:ga # ;
 +Sg+Acc:ga # ;
 +Sg+Acc:jdá # ;
 +Sg+Acc:jddá # ;
 +Sg+Acc:jaaájd # ;
 +Sg+Gen:ga # ;
 +Sg+Ill:sat # ;
 +Sg+Loc:stá # ;
 +Sg+Com:jdájn # ;
 +Ess:nin # ;
 +Attr:da # ;






! Subjunction
! ===========


LEXICON Subjunction

ahte Cs ;
amas Cs ;
!dahje Cs ; ! CC only (?)
danin% go Cs ;
das% go Cs ;
dasgo Cs ;
de Cs ;
dus1s1e% go Cs ;
dus1s1ego Cs ;
eará% go Cs ;
earágo Cs ;
earetgo Cs ;
earret% go Cs ;
earretgo Cs ;
ehte Cs ;
!ele% ge Cs ; ! CC only (?)
geardde Cs ;
gearddi Cs ;
go Cs ;
jes Cs ;
jis Cs ;
joas  Cs ;
jogo Cs ;
jos Cs ;
juogo Cs ;
juos Cs ;
juos% go Cs ;
juosgo Cs ;
jus Cs ;
man1'n1ágo Cs ;
man1n1ágo Cs ;
man1n1el% go Cs ;
man1n1elgo Cs ;
man1n1á% go Cs ;
muhte Cs ;
muhto Cs ;
!ovdal Cs ; ! Pp only without go (?)
ovdal% go Cs ;
ovdalgo Cs ;
plus Cs ;
sehke Cs ;
sihke Cs ;
vái Cs ;
váikko Cs ;
vai Cs ;
vaikke Cs ;
vaikko Cs ;
váikke Cs ;

LEXICON Cs

 +CS:0 # ;





! Conjunction
! ===========


LEXICON Conjunction

ja  Cc ;
dahje Cc ;
dahjege Cc ;
dahege Cc ;
dehe Cc ;
dehege Cc ;
ele Cc ;
elege Cc ;
ele% ge Cc ;
vai Cc ;

LEXICON Cc

 +CC:0 # ;


! Interjection
! ============


LEXICON Interjection 

a-a Ij ;
á-a Ij ;
ádje-da Ij ;
ái Ij ;
ámen Ij ;
ámmen Ij ;
ámen Ij ;
aamen Ij ;
amen Ij ;
ámmozan Ij ;
ás1 Ij ;
báv Ij ;
bea Ij ;
bes1s1 Ij ;
bevggo-be Ij ;
bis-bis Ij ;
bodde Ij ;
buorat Ij ;
buorát Ij ;
bures Ij ;
buse-buse Ij ;
cecce Ij ;
ci Ij ;
cilo Ij ;
cuoh:cuoh3 Ij ;
cus1 Ij ;
cys1 Ij ;
cys Ij ;
danheakka Ij ;
dáhkka Ij ;
dearvva Ij ;
fánten Ij ;
ficce Ij ;
fihcce Ij ;
fuoi Ij ;
fuos Ij ;
gáppo-gáppo Ij ;
gea Ij ;
geah:geah3 Ij ;
gis1 Ij ;
Gucco Ij ;
gus Ij ;
hái Ij ;
he-hei Ij ;
hei Ij ;
hivás Ij ;
hohoh:hohoh3 Ij ;
hohoi Ij ;
hoi Ij ;
huh:huh3 Ij ;
huf Ij ;
hurá Ij ;
hurádis Ij ;
hys1 Ij ;
is1 Ij ;
já Ij ;
já-já Ij ;
jo Ij ;
juo Ij ;
juo Ij ;
kys1-kys1 Ij ;
ma-maid Ij ;
morá Ij ;
oho Ij ;
ohoi Ij ;
skole Ij ;
skolle Ij ;
skol Ij ;
s1lec1c1 Ij ;
us1 Ij ;
vuoi Ij ;

LEXICON Ij

 +Interj:0 # ;





! Particles
! =========


LEXICON Particles
abai pcle ;
ahteson pcle ;
aitto pcle ;
almma pcle ;
ama pcle ;
amal pcle ;
amat pcle ;
amma pcle ;
ammahal pcle ;
ammal pcle ;
ammat pcle ;
ammes pcle ;
appason pcle ;
ba pcle ;
bat pcle ;
!be pcle ; only in Finland as clitic?
eaigga pcle ;
eiddo pcle ;
eigga pcle ;
eitto pcle ;
galahal pcle ;
galahan pcle ;
galhal pcle ;
ge pcle ;
ges pcle ;
gis pcle ;
go pcle ;
goit:gojt pcle ;
goitge:gojtge pcle ;
goittot:gojttot pcle ;
goittotge:gojttotge pcle ;
hal pcle ;
han pcle ;
na pcle ;
naj pcle ;
no pcle ;
noa pcle ;
son pcle ;

LEXICON pcle
+Pcle:0 # ;
! Perhaps this should be opened to  a direction to K
! and all the ge versions should be removed.
! (i.e. only goit, not goitge). This errouneously 
! permits gege, goge, etc., though, and we thus leave
! things as they are.



! Numerals
! ========

LEXICON Numeral
MILJON ; ! a noun of its own
UNDERDUHAT ; ! for generator under 1000
JUSTDUHAT ; ! going via 1000
OVERDUHAT ; ! for generator over 1000
OLD ; ! for "thirteen hundred, etc.
!num-basic ; ! replaced by the 5 lexica above
num-ordinal ; ! The basic ordinal numbers
!num-derived ; ! still unimplemented
num-imprecise ;! still almost unimplemented
ARABIC ; ! for the arabic numerals
ROMAN ; ! for the roman numerals
PARAGRAPHS ; ! for §34 etc.


LEXICON MILJON
miljon+Num+Sg+Nom:miljon K ;
miljon+Num+Sg+Ess:miljovdnan K ;
miljon+Num:miljovdna OKTA-NE ;
miljon:miljovdna OKTA-OBL ;


LEXICON UNDERDUHAT
OKTAF ; ! 1, 11
2TO9F ; ! 2-9, 12-19
LOGIF ; ! 10
!NUPPELOT ; ! 11-19
2TO9LOG ; ! 20-99
CUODI ; ! 100-199
2TO9CUO ; ! 200-999

LEXICON OKTAF
ok0ta:ok'ta OKTA ;
! K ; this lead to "go +Clt" as an analysis, and was removed.
! Thus, direction to K should be made at an earlier point

LEXICON 2TO9F
guok0te:guok't GUOKTE ;
golbma GOLBMA ;
njeallje:njeallj NJEALLJE ;
vihtta:vi VIHTTA ;
guhtta:gu VIHTTA ;
c1iez1a:c1ie CIEZA ;
gávcci:gák'ci LOGI ;
ovcci:ok'ci LOGI ;


LEXICON LOGIF
logi:loh'ki LOGI ;
logi OKTAF ;
logi 2TO9F ;
logi K ;

LEXICON NUPPELOT 
okta NL ;
guokte NL ;
golbma NL ;
njeallje NL ;
vihtta NL ;
guhtta NL ;
c1iez1a NL ;
gávcci NL ;
ovcci NL ;

LEXICON NL
!nuppelohkái K ;
nuppelohkái JUSTDUHAT ;
! Guoktenuppelohkáiduhát
!nuppelot K ;

LEXICON CUODI
c1uoh'ti:c1uod1i OVERDUHAT ;
c1uoh'ti:c1uod1i LOGI ;
c1uoh'ti:c1uod1i LOGIF ;
c1uoh'ti:c1uod1i 2TO9LOG ;
c1uoh'ti:c1uod1i OKTAF ;
c1uoh'ti:c1uod1i 2TO9F ;
c1uoh'ti:c1uod1i NUPPELOT ;
c1uoh'ti:c1uod1i K ;
!c1uoh'ti OVERDUHAT ;
!c1uoh'ti LOGI ;
!c1uoh'ti LOGIF ;
!c1uoh'ti 2TO9LOG ;
!c1uoh'ti OKTAF ;
!c1uoh'ti 2TO9F ;
!c1uoh'ti NUPPELOT ;
!c1uoh'ti K ;

LEXICON 2TO9CUO
guokte GCUODI ;
golbma GCUODI ;
njeallje GCUODI ;
vihtta GCUODI ;
guhtta GCUODI ;
c1iez1a GCUODI ;
gávcci GCUODI ;
ovcci GCUODI ;

LEXICON GCUODI
c1uoh'ti:c1uod1i OVERDUHAT ;
c1uoh'ti:c1uod1i LOGI ;
c1uoh'ti:c1uod1i LOGIF ;
c1uoh'ti:c1uod1i 2TO9LOG ;
c1uoh'ti:c1uod1i OKTAF ;
c1uoh'ti:c1uod1i 2TO9F ;
c1uoh'ti:c1uod1i NUPPELOT ;
c1uoh'ti:c1uod1i K ;

LEXICON 2TO9LOG
guokte LOGIF ;
golbma LOGIF ;
njeallje LOGIF ;
vihtta LOGIF ;
guhtta LOGIF ;
c1iez1a LOGIF ;
gávcci LOGIF ;
ovcci LOGIF ;

LEXICON JUSTDUHAT 
duhát:duháh DUHAT ;
duhát UNDERDUHAT ;

LEXICON OLD
okta NLX ;
guokte NLX ;
golbma NLX ;
njeallje NLX ;
vihtta NLX ;
guhtta NLX ;
c1iez1a NLX ;
gávcci NLX ;
ovcci NLX ;

LEXICON NLX
nuppelohkái CUODI ;
nuppelohkái 2TO9CUO ;

LEXICON OVERDUHAT
O-OKTAF ;
O-2TO9F ;
O-LOGIF ;
O-NUPPELOT ;
O-CUODI ;
O-2TO9CUO ;
O-2TO9LOG ;

LEXICON O-OKTAF
okta JUSTDUHAT ;
JUSTDUHAT ;

LEXICON O-2TO9F
guokte JUSTDUHAT ;
golbma JUSTDUHAT ;
njeallje JUSTDUHAT ;
vihtta JUSTDUHAT ;
guhtta JUSTDUHAT ;
c1iez1a JUSTDUHAT ;
gávcci JUSTDUHAT ;
ovcci JUSTDUHAT ;

LEXICON O-LOGIF
logi JUSTDUHAT ;
logi O-OKTAF ;
logi O-2TO9F ;

LEXICON O-NUPPELOT 
okta O-NL ;
guokte O-NL ;
golbma O-NL ;
njeallje O-NL ;
vihtta O-NL ;
guhtta O-NL ;
c1iez1a O-NL ;
gávcci O-NL ;
ovcci O-NL ;

LEXICON O-NL
nuppelohkái JUSTDUHAT ;

LEXICON O-CUODI
c1uod1i JUSTDUHAT ;
c1uod1i O-LOGIF ;
c1uod1i O-2TO9LOG ;
c1uod1i O-NUPPELOT ;

LEXICON O-2TO9CUO
guokte O-CUODI ;
golbma O-CUODI ;
njeallje O-CUODI ;
vihtta O-CUODI ;
guhtta O-CUODI ;
c1iez1a O-CUODI ;
gávcci O-CUODI ;
ovcci O-CUODI ;

LEXICON O-2TO9LOG
guokte O-LOGIF ;
golbma O-LOGIF ;
njeallje O-LOGIF ;
vihtta O-LOGIF ;
guhtta O-LOGIF ;
c1iez1a O-LOGIF ;
gávcci O-LOGIF ;
ovcci O-LOGIF ;

! Case forms of the basic digits
! ------------------------------

LEXICON OKTA
 +Num: OKTA-NE ;
 +Num: OKTA-OBL ;
! NUPPELOHKAI ;

LEXICON OKTA-OBL
 :X4 OKTA-A ;
 :X4 NUM-GLN ;
     NUM-I ;
 :X5 NUM-II ;

LEXICON OKTA-NE
 +Sg+Nom: NUPPELOHKAI ;
 +Ess:n NUPPELOHKAI ;

LEXICON OKTA-A
 +Sg+Acc: NUPPELOHKAI ;

LEXICON NUM-GLN
 +Sg+Gen: NUPPELOHKAI ;
 +Sg+Loc:s NUPPELOHKAI ;
 +Pl+Nom:t NUPPELOHKAI ;

LEXICON NUM-I 
 +Sg+Ill:j NUPPELOHKAI ;

LEXICON NUM-II
 +Sg+Com:jn NUPPELOHKAI ;
 +Pl+Acc:jd NUPPELOHKAI ;
 +Pl+Gen:jd NUPPELOHKAI ;
 +Pl+Ill:jde NUPPELOHKAI ;
 +Pl+Loc:jn NUPPELOHKAI ;
 +Pl+Com:jgujn NUPPELOHKAI ;

LEXICON GUOKTE
 +Num:e GUOKTE-NA ;
 +Num:i GUOKTI ;
! NUPPELOHKAI ;


LEXICON GUOKTE-NA
 +Sg+Nom: NUPPELOHKAI ;
 +Sg+Acc: NUPPELOHKAI ;
 +Sg+Gen:X4 NUPPELOHKAI ;

LEXICON GUOKTI
 :X4 NUM-GLN ;
 :X3 NUM-I ;
 :X5 NUM-II ;

LEXICON GOLBMA
 +Num: GUOKTE-NA ;
 +Num: GUOKTI ;
! NUPPELOHKAI ;

LEXICON NJEALLJE
 +Num:e GUOKTE-NA ;
! +Num:X4 NUM-GLN ;
 +Num+Sg+Gen:iX4 NUPPELOHKAI ; ! for njealji
 +Num+Sg+Gen:eX4 NUPPELOHKAI ; ! for njealje
 +Num+Sg+Loc:X4s NUPPELOHKAI ;
 +Num+Pl+Nom:X4t NUPPELOHKAI ;
 +Num:X3 NUM-I ;
 +Num:X5 NUM-II ;

LEXICON CIEZA
 +Num+Sg+Ill:z1aX3j NUPPELOHKAI ;
 +Num+Sg+Ill:hc1c1aX3j NUPPELOHKAI ;
 +Num:h'c1aX3 NUM-I ;
 +Num:h'c1aX5 NUM-II ;
 +Num+Ess:hz1an NUPPELOHKAI ;
 +Num:z1a LOGI-WE ;
! NUPPELOHKAI ;

! 5 and 6, minimal stem vi-, gu- due to exceptional III-I consonant gradation
LEXICON VIHTTA
 +Num: VIHTTA-NAIE ;
 +Num: VIHTTA-OBL ;
! NUPPELOHKAI ;


LEXICON VIHTTA-NAIE
 +Sg+Nom:htta NUPPELOHKAI ;
 +Sg+Acc:htta NUPPELOHKAI ;
 +Sg+Ill:httij NUPPELOHKAI ;
 +Ess:httan NUPPELOHKAI ;

LEXICON VIHTTA-OBL
 +Sg+Gen:d1a NUPPELOHKAI ;
 +Sg+Loc:d1as NUPPELOHKAI ;
 +Sg+Com:d1ain NUPPELOHKAI ;
 +Pl+Nom:d1at NUPPELOHKAI ;
 +Pl+Acc:dajd NUPPELOHKAI ;
 +Pl+Gen:dajd NUPPELOHKAI ;
 +Pl+Ill:dajde NUPPELOHKAI ;
 +Pl+Loc:dajn NUPPELOHKAI ;
 +Pl+Com:dajgujn NUPPELOHKAI ;

LEXICON LOGI
 +Num:X4 LOGI-WE ;
 +Num:X3 NUM-I ;
 +Num:X5 NUM-II ;
 +Num+Ess:n K ;

LEXICON LOGI-WE
 +Sg+Nom: K ;
 +Sg+Acc: K ;
 +Sg+Gen: K ;
 +Sg+Loc:s K ;
 +Pl+Nom:t K ;

LEXICON DUHAT
 +Num+Sg+Nom: K ;
 +Num+Sg+Gen:a K ;
 +Num+Sg+Acc:a K ;
 +Num+Sg+Ill:ij K ;
 +Num+Sg+Loc:is K ;
 +Num+Sg+Com:ijn K ;
 +Num+Ess:in K ;
 +Num+Pl+Nom:at K ;
 +Num+Pl+Gen:ijd K ;
 +Num+Pl+Acc:ijd K ;
 +Num+Pl+Ill:ijdda K ;
 +Num+Pl+Loc:ijn K ;
 +Num+Pl+Com:ijgujn K ;

LEXICON NUPPELOHKAI
 :nuppelohkáj K ;
 +Attr:nuppelot K ;
 K ;

LEXICON num-ordinal
vuosttas+A+Card:vuosttas VUOSTTAS ;
vuosttas1+A+Card:vuosttaz1 VUOSTTAS ;
nubbi+A+Card:nubbi indef-a-even-v ;
goalmmát+A+Card:goalmmád VUOSTTAS ;
njealját+A+Card:njealjád VUOSTTAS ;
vid1át+A+Card:vid1ád VUOSTTAS ;
gud1át+A+Card:gud1ád VUOSTTAS ;
c1ihc1c1et+A+Card:c1ihc1c1ed VUOSTTAS ;
gávccát+A+Card:gávccád VUOSTTAS ;
ovccát+A+Card:ovccád VUOSTTAS ;
logát+A+Card:logád VUOSTTAS ;
c1uod1át+A+Card:c1uod1ád VUOSTTAS ;
duháhat+A+Card:duháh VUOSTTAS ;
millijovnnat+A+Card:millijovnnag VUOSTTAS ;
miljonat+A+Card:miljonag VUOSTTAS ;

LEXICON VUOSTTAS
 GAPPUS0 ;
 GAPPUS- ;
! +A: EABBO/EAMOS ; ! No comparatives here
 +Attr: K ;

LEXICON num-imprecise
mán10ga:mán'ga GOLBMA ;
moadde:moadd GUOKTE ; 
gal0le:gal'le GOLBMA ;
beal-nup+Num:beal-nup # ; ! ad hoc



LEXICON PARAGRAPHS
§ ARABIC ;


LEXICON ARABIC
1 ARABICLOOP ;
2 ARABICLOOP ;
3 ARABICLOOP ;
4 ARABICLOOP ;
5 ARABICLOOP ;
6 ARABICLOOP ;
7 ARABICLOOP ;
8 ARABICLOOP ;
9 ARABICLOOP ;
%0 ARABICLOOP ;

LEXICON ARABICLOOP
ARABIC ;
!%- ARABICLOOP ;   ! multipart numbers (strange, use next line)
%- ARABIC ;        ! multipart numbers
%. ARABIC ;
%, ARABIC ;
%: ARABIC ;
!%/ ARABIC ; ! Since / denotes compounds. This is a bad solution
!%' ARABIC ; ! Since ' denotes cons.grad. This is not optimal either.

!+Num:%- R ; ! Number compounds
+Num: R ;    ! Hyphen removed bec. of "123- ja 124-viessu" 
+Num+Nom: # ;
+Num+Ord:%. # ;

+Num: ARABICCASE ;      ! This is the 1984s case.
+Num:%: ARABICCASE ;    ! This is the 1984:s case.
+Num:%' ARABICCASE ;    ! This is the 1984's case.
+Num:%- ARABICCASE ;    ! This is the 1984-s case.
                        ! So, what does the lg council prefer?
LEXICON ARABICCASE
+Gen:a # ; 
+Acc:a # ; 
+Ill:i # ; 
+Ill:ij # ; 
+Loc:s # ; 
+Com:in # ; 
+Ess:n # ; 
! No plural numbers, hmm.

LEXICON ROMAN
ROM-HUNDRED ;
M ROM-HUNDRED ;
MM ROM-HUNDRED ;
MMM ROM-HUNDRED ;

LEXICON ROM-HUNDRED

ROM-TEN ;
C ROM-TEN ;
CC ROM-TEN ;
CCC ROM-TEN ;
CD ROM-TEN ;
D ROM-TEN ;
DC ROM-TEN ;
DCC ROM-TEN ;
DCCC ROM-TEN ;
CM ROM-TEN ;


LEXICON ROM-TEN
ROM-ONE ;
X ROM-ONE ;
XX ROM-ONE ;
XXX ROM-ONE ;
XL ROM-ONE ;
L ROM-ONE ;
LX ROM-ONE ;
LXX ROM-ONE ;
LXXX ROM-ONE ;
XC ROM-ONE ;

LEXICON ROM-ONE
ROM-TAG ;
I ROM-TAG ;
II ROM-TAG ;
III ROM-TAG ;
IV  ROM-TAG ;
V ROM-TAG ;
VI ROM-TAG ;
VII ROM-TAG ;
VIII ROM-TAG ;
IX ROM-TAG ;

LEXICON ROM-TAG
+Num: # ;
