!This file contains the following  closed parts of speech

!Pronoun
!Subjunction
!Interjection
!Particles
!Numerals



! Pronoun
! =======

!Pointing to all the subgrops, presented in this order below.
LEXICON Pronoun
 Personal ;
 Interrogative ;
 Relative ;
 Demonstrative ;
 Reflexive ;
 Reciprocal ;
 Indefinite ;


! Personal pronouns
! -----------------

!Personal pronouns, splitting in 1st, 2nd, 3rd
LEXICON Personal
mun+Pron+Pers+Sg1:m firstperspronsg ;
mun+Pron+Pers+Du1:m firstpersprondu ;
mun+Pron+Pers+Pl1:m perspronpl ;
don+Pron+Pers+Sg2:d nonfirstperspronsg ;
don+Pron+Pers+Du2:d nonfirstpersprondu ;
don+Pron+Pers+Pl2:d perspronpl ;
son+Pron+Pers+Sg3:s nonfirstperspronsg ;
son+Pron+Pers+Du3:s nonfirstpersprondu ;
son+Pron+Pers+Pl3:s perspronpl ;
dat+Pron+Pers:da datpers ;                ! Dat lea ruovttus.

!Case suffixes for 1st Sg, pointing to K lexicon
LEXICON firstperspronsg
 +Nom:un K ;
 +Nom:on K ;
 +Acc:u K ;
 +Gen:u K ;
 +Gen:u# NAMAT ;
 +Ill:unnje K ;
 +Loc:us K ;
 +Com:ujnna K ;
 +Ess:unin K ;

!Case suffixes for noŋst Sg, pointing to K lexicon
LEXICON nonfirstperspronsg
 +Nom:on K ;
 +Acc:u K ;
 +Gen:u K ;
 +Gen:u# NAMAT ;
 +Ill:utnje K ;
 +Loc:us K ;
 +Com:ujnna K ;
 +Ess:unin K ;

!Case suffixes for 1st Du, pointing to K lexicon
LEXICON firstpersprondu
 +Nom:oaj K ;
 +Acc:unno K ;
 +Gen:unno K ;
 +Gen:unno# NAMAT ;
 +Ill:unnujde K ;
 +Loc:unnos K ;
 +Com:unnujn K ;
 +Ess:unnon K ;

!Case suffixes for noŋst Du, pointing to K lexicon
LEXICON nonfirstpersprondu
 +Nom:oaj K ;
 +Acc:udno K ;
 +Gen:udno K ;
 +Acc:utno K ; !SUB
 +Gen:utno K ; !SUB
 +Gen:udno# NAMAT ;
 +Ill:udnujde K ;
 +Ill:utnujde K ; !SUB
 +Loc:udnos K ;
 +Loc:utnos K ; !SUB
 +Com:udnujn K ;
 +Com:utnujn K ; !SUB
 +Ess:udnon K ;
 +Ess:utnon K ; !SUB

!Case suffixes for all Pl forms, pointing to K lexicon
LEXICON perspronpl
 +Nom:ij K ;
 +Acc:in K ;
 +Gen:in K ;
 +Gen:in# NAMAT ;
 +Ill:idjijde K ;
 +Loc:is K ;
 +Loc:ijs K ; !SUB !SOUTH
 +Com:ingujn K ;
 +Ess:inin K ;

LEXICON datpers
 +Sg3:0 demcassg ;
 +Pl3:0 demcaspl ;
 +Ess:nin K ;

! Interrogative pronouns
! ----------------------

!Giving ideosyncratic Sg Nom of gii, mii lexically
!Sending the oblique forms of gii, mii to an oblique sublexicon
!Giving the stem of guhte, guhtemuš, goabbá
LEXICON Interrogative
mii+Pron+Interr+Sg+Nom:mij K-son ;
mii+Pron+Interr+Sg+Acc:májd K-son ;
gii+Pron+Interr+Sg+Nom:gij K-son ;
gii+Pron+Interr+Sg+Ill+Foc:geasagii # ;
mii+Pron+Interr:ma oblintercas ;
gii+Pron+Interr:geaX5 oblintercas ;
guh0te+Pron+Interr:guh'te GUHTECASE ;
guhte+Pron+Interr+Sg+Nom:guhtes K ;
guhtemuš+Pron+Interr:guđemužž GUHTEMUŠCASE ;
guhtemuš+Pron+Interr:guhtemuš K-son ; !deviating Nom alternant.
goabbá+Pron+Interr:goabbá GUHTECASE ;
man% láhkái+Pron+Interr+Sg+Com:mainna#lágiin K-son ; ! XXX hmm...

! Relative pronouns
! -----------------

LEXICON Relative
mii+Pron+Rel+Sg+Nom:mij K-son ;
mii+Pron+Rel+Sg+Acc:májd K-son ;
gii+Pron+Rel+Sg+Nom:gij K-son ;
mii+Pron+Rel:ma oblintercas ;
gii+Pron+Rel:geaX5 oblintercas ;
guh0te+Pron+Rel:guh'te GUHTECASE ;
guhte+Pron+Rel+Sg+Nom:guhtes K ;
guhtemuš+Pron+Rel:guđemužž GUHTEMUŠCASE ;
guhtemuš+Pron+Rel:guhtemuš K-son ; !deviating Nom alternant.
goabbá+Pron+Rel:goabbá GUHTECASE ;
man% láhkái+Pron+Rel+Sg+Com:mainna#lágiin K-son ; ! XXX hmm...

LEXICON oblintercas
 +Sg+Acc:jd K ;
 +Pl+Nom:t K ; 
 +Ess:nin K ;
 +Sg:0 obldemcassg ;
 +Pl:0 obldemcaspl ;
!Then going directly to the case paradigm of the demonstratives

! guhte
LEXICON GUHTECASE
 GUHTE-NE ;
 GU-GU-OBL ;

! Nom and Ess of guhte
LEXICON GUHTE-NE
 +Sg+Nom: K-son ;
 +Ess:n K-son ;

! guhtemuš
LEXICON GUHTEMUŠCASE
 GUHTEMUŠ-NE ;
 :a GU-GU-OBL ;

! Nom and Ess of guhtemuš
LEXICON GUHTEMUŠ-NE
 +Sg+Nom: K-son ;
 +Ess:an K-son ;

! Oblique paradigm for guhte and guhtemuš
LEXICON GU-GU-OBL
 +Sg+Gen:X8 K-son ;
 +Sg+Gen:X8# NAMAT ;
 +Sg+Acc:X8 K-son ;
 +Sg+Ill:X3j K-son ;
!+Sg+Gen: K-son ; ! this should go out
 +Sg+Loc:X4s K-son ;
 +Sg+Loc:X4n K-son ; !SUB !SOUTH
 +Sg+Com:X5jn K-son ;
 +Pl+Nom:X4t K-son ;
 +Pl+Gen:X5jd K-son ;
 +Pl+Gen:X5jd# NAMAT ;
 +Pl+Acc:X5jd K-son ;
 +Pl+Ill:X5jde K-son ;
 +Pl+Com:X5jgujn K-son ;


! Demonstrative pronouns
! ----------------------

!Giving baseform + all demonstrative stems
!Pointing to case paradigms

LEXICON Demonstrative
dat+Pron+Dem:da demcas ;
diet+Pron+Dem:die demcas ;
dát+Pron+Dem:dá demcas ;
duot+Pron+Dem:duo demcas ;
dot+Pron+Dem:do demcas ;

dahtanaga+Pron+Dem:da Dahtanaga-par ;
dakkár+Pron+Dem:dakkár DAKKAR ;
dákkar+Pron+Dem:dákkar DAKKAR ; ! XXX substandard
dakkar+Pron+Dem:dakkar DAKKAR ; ! XXX substandard
dákkár+Pron+Dem:dákkár DAKKAR ; 
diekkár+Pron+Dem:diekkár DAKKAR ;
duokkár+Pron+Dem:duokkár DAKKAR ;
dokkár+Pron+Dem:dokkár DAKKAR ;
makkár+Pron+Dem:makkár DAKKAR ;

dakkáraš+Pron+Dem:dakkáražž DAKKARAS ;
dákkáraš+Pron+Dem:dákkáražž DAKKARAS ;
diekkáraš+Pron+Dem:diekkáražž DAKKARAS ;
duokkáraš+Pron+Dem:duokkáražž DAKKARAS ;
dokkáraš+Pron+Dem:dokkáražž DAKKARAS ;
makkáraš+Pron+Dem:makkáražž DAKKARAS ;
 

!Splitting into Sg, Pl and Ess, different paradigms
LEXICON demcas
 +Sg:0 demcassg ;
 +Pl:0 demcaspl ;
 +Ess:nin K ;

!Splitting into oblique and nonoblique
!This is because of the interrogatives
LEXICON demcassg
 nomdemcassg ;
 obldemcassg ;
LEXICON demcaspl
 nomdemcaspl ;
 obldemcaspl ;

LEXICON nomdemcassg
!The nominative, then pointing to clitics
!This could be conflated, but look out for interr
 +Nom:t K ;
LEXICON nomdemcaspl
 +Nom:t K ;

LEXICON obldemcassg
!The other cases, then pointing to clitics
 +Gen:n K ;
 +Gen:n# NAMAT ;
 +Acc:n K ;
 +Ill:sa K ;
 +Loc:s K ;
 +Com:jnna K ;
LEXICON obldemcaspl
 +Gen:jd K ;
 +Gen:jd# NAMAT ;
 +Acc:jd K ;
 +Ill:jdda K ;
 +Loc:jn K ;
 +Loc:jnna K ;
 +Com:jguin K ;

!This case paradigm is exceptional and partial.
LEXICON Dahtanaga-par
 +Sg+Nom:htanaga # ;
 +Sg+Gen:mmanaga # ;
 +Sg+Gen:mmanaga# NAMAT ;
 +Sg+Acc:mmanaga # ;
 +Sg+Ill:sanaga # ;
 +Sg+Loc:sttánaga # ;
 +Sg+Com:jnnanaga # ;
 +Pl+Ill:jddanaga # ;
 +Pl+Loc:jnnanaga # ;

LEXICON DAKKAR
 +Sg+Nom: K ;
 +Attr: K ;
 +Attr:# NAMAT ;
 +Ess:in K ;
 +Sg+Gen:a K ;
 +Sg+Acc:a K ;
 +Pl+Nom:at K ;
 00:i DAKKARI- ;

LEXICON DAKKARI-
 +Sg+Ill:j K ;
 +Sg+Loc:s K ;
 +Sg+Loc:n K ; !SUB !SOUTH
 +Sg+Com:jn K ;
 +Pl+Gen:jd K ;
 +Pl+Gen:jd# NAMAT ;
 +Pl+Acc:jd K ;
 +Pl+Ill:jdda K ;
 +Pl+Loc:jn K ;
 +Pl+Com:jgujn K ;

LEXICON DAKKARAS
 +Sg+Nom: K ;
 +Sg+Nom:X7 R ; !^C^
 +Ess:an K ;
 :a DAKKARASSA- ;

LEXICON DAKKARASSA-
  +Sg+Gen:X4 K ;
  +Sg+Gen:X8 K ;
  +Sg+Gen:X4# NAMAT ;
  +Sg+Gen:X8# NAMAT ;
  +Sg+Acc:X4 K ;
  +Sg+Acc:X8 K ;
  +Sg+Ill:X3j K ;
  +Sg+Loc:X4s K ;
  +Sg+Loc:X8s K ;
  +Sg+Loc:X4n K ; !SUB !SOUTH
  +Sg+Loc:X8n K ; !SUB !SOUTH 
  +Sg+Com:X5jn K ;
  +Pl+Nom:X4t K ;
  +Pl+Gen:X5jd K ;
  +Pl+Gen:X5jd# NAMAT ;
  +Pl+Acc:X5jd K ;
  +Pl+Ill:X5jde K ;
  +Pl+Loc:X5jn K ;
  +Pl+Com:X5jgujn K ;



! Reflexive pronouns
! ------------------

LEXICON Reflexive
!Two nominative reflexives, and pointer to the rest
!The Pl one is used for Du as well, here given two entries. 
!Should one of them be removed?
ieš+Pron+Refl+Sg+Nom:ieš K ;
ieš+Pron+Refl+Sg+Nom:ieš# NAMAT ;
ieš+Pron+Refl+Du+Nom:ieža K ;
ieš+Pron+Refl+Du+Nom:ieža# NAMAT ;
ieš+Pron+Refl+Pl+Nom:ieža K ;
ieš+Pron+Refl+Pl+Nom:ieža# NAMAT ;
ieš+Pron+Refl+Gen+PxSg3:ald3 # ; !SUB !SOUTH
ieš+Pron+Refl+Ess:iehčanassij K ;
ieš+Pron+Refl:0 reflobl ;

LEXICON reflobl
!The V/V2 alternation due to 3rd person vocalism
!Pointing directly to 1st and 3rd person sublexica for AG & Loc.
!Pointing to exceptional Pl sublexicon for AG and Loc.
+Acc:ieža PxV2 ;
+Gen:ieža PxV2 ;
+Gen+PxSg1:iežan# NAMAT ;
+Gen+PxSg1:iežam3# NAMAT ; !SUB !SOUTH
+Gen+PxSg2:iežat# NAMAT ;
+Gen+PxSg3:iežas# NAMAT ;
+Gen+PxDu1:iežame# NAMAT ;
+Gen+PxDu2:iežade# NAMAT ;
+Gen+PxDu3:iežaska# NAMAT ;
+Gen+PxPl1:iežamet# NAMAT ;
+Gen+PxPl2:iežadet# NAMAT ;
+Gen+PxPl3:iežaset# NAMAT ;
+Acc:iehča Px1V ;
+Gen:iehča Px1V ;
+Gen+PxSg1:iehčan# NAMAT ;
+Gen+PxDu1:iehčame# NAMAT ;
+Gen+PxPl1:iehčamet# NAMAT ;
+Acc:iežajd PxCPlstem ;
+Gen:iežajd PxCPlstem ;
+Gen+PxDu1:iežajdeame# NAMAT ;
+Gen+PxDu2:iežajdeatte# NAMAT ;
+Gen+PxDu3:iežajdeaskka# NAMAT ;
+Gen+PxPl1:iežajdeamet# NAMAT ;
+Gen+PxPl2:iežajdeattet# NAMAT ;
+Gen+PxPl3:iežajdeaset# NAMAT ;
+Ill:alcces PxC ;
+Ill:alcca PxV ;
+Ill:alcce PxV2 ;
+Loc:alddi PxV ;
+Loc:aldde Px3V ;
+Loc:alddijn PxCPlstem ;
+Com:iežajn PxC ;
+Com:iežajd PxCPlCom ;
+Ess:iehčan PxC ;

LEXICON PxCPlstem
!This lexicon is here since it only applies to Reflexives.
!It handles the exeptional AG and Loc DuPl plural stem forms of ieš.
 +PxDu1:eame K ;
 +PxDu2:eatte K ;
 +PxDu3:easkka K ;
 +PxPl1:eamet K ;
 +PxPl2:eattet K ;
 +PxPl3:easet K ;
 

LEXICON PxCPlCom
! This lexicon for archaic biblical forms like iežaideasetguin.
 +PxDu1:eameguin K ;
 +PxDu2:eatteguin K ;
 +PxDu3:easkkaguin K ;
 +PxPl1:eametguin K ;
 +PxPl2:eattetguin K ;
 +PxPl3:easetguin K ;


! Reciprocal pronouns
! -------------------

! The first 6 entries handle the first element of the recipr.
! The next 12 handle the 2nd part of the non-Px recipr.
! The members of the third section  point to Px lexica.

LEXICON Reciprocal
nubbi+Pron+Recipr+Du:nubbi # ;
nubbi+Pron+Recipr+Pl:nuppit # ;
goabbat+Pron+Recipr+Du:goabbat # ;
goabbat+Pron+Recipr+Pl:guhtet # ;
goabbat+Pron+Recipr+Du:goappat # ; ! Dialectal forms. Norm?
goabbat+Pron+Recipr+Pl:guđet # ;   ! Dialectal forms. Norm?

nuppi+Pron+Recipr+Du+Acc:nuppi K ;
nuppi+Pron+Recipr+Du+Gen:nuppi K ;
nuppi+Pron+Recipr+Du+Ill:nubbái K ;
nuppi+Pron+Recipr+Du+Loc:nuppis K ;
nuppi+Pron+Recipr+Du+Com:nuppiin K ;
nuppi+Pron+Recipr+Du+Ess:nubbin K ;
nuppi+Pron+Recipr+Pl+Acc:nuppiid K ;
nuppi+Pron+Recipr+Pl+Gen:nuppiid K ;
nuppi+Pron+Recipr+Pl+Ill:nuppiide K ;
nuppi+Pron+Recipr+Pl+Loc:nuppiin K ;
nuppi+Pron+Recipr+Pl+Com:nuppiiguin K ;
nuppi+Pron+Recipr+Pl+Ess:nubbin K ;


guoibmi+Pron+Recipr+Sg+Acc:guoibmá Px1VRecipr ;
guoibmi+Pron+Recipr+Sg+Gen:guoibmá Px1VRecipr ;
!guoibmi+Pron+Recipr+Sg+Acc:guoimmá Px2VRecipr ;
!guoibmi+Pron+Recipr+Sg+Gen:guoimmá Px2VRecipr ;
guoibmi+Pron+Recipr+Sg+Acc:guoimmi Px23VRecipr ;
guoibmi+Pron+Recipr+Sg+Gen:guoimmi Px23VRecipr ;
guoibmi+Pron+Recipr+Sg+Ill:guoibmás PxCPlstem ;
guoibmi+Pron+Recipr+Sg+Loc:guoimmist PxCPlstem ;
guoibmi+Pron+Recipr+Sg+Com:guoimmiin PxCPlstem ;

guoibmi+Pron+Recipr+Pl+Acc:guoimmiid PxCPlstem ;
guoibmi+Pron+Recipr+Pl+Gen:guoimmiid PxCPlstem ;
guoibmi+Pron+Recipr+Pl+Ill:guoimmiidasa PxVPlStemRecipr ;
guoibmi+Pron+Recipr+Pl+Loc:guoimmiin PxCPlstem ;
guoibmi+Pron+Recipr+Pl+Com:guoimmiid PxCPlComRecipr ;

LEXICON Px1VRecipr
 +PxDu1:me K ;
 +PxPl1:met K ;

LEXICON Px23VRecipr
!LEXICON Px2VPlStem
 +PxDu2:X2de K ;
 +PxPl2:X2det K ;

!LEXICON Px3VPlStem
 +PxDu3:ska K ;
 +PxPl3:set K ;

LEXICON PxVPlStemRecipr
 +PxPl1:met K ;
 +PxPl2:det K ;
 +PxPl3:set K ;

LEXICON PxCPlComRecipr
 +PxPl1:eametguin K ;
 +PxPl2:eadetguin K ;
 +PxPl3:easetguin K ;

 



! Indefinite pronouns
! -------------------

! Dividing the indefinites in three groups
LEXICON Indefinite
 declindef-cl ;
 declindef ;
 declindef-idiosync ;
 indeclindef ;
 indefmulti ; ! feara mii

! Declineable indefinite pronouns with case + clitic
! - - - - - - - - - - - - - - - - - - - - - - - - - 

LEXICON declindef-cl

! Exceptional nominatives
mihkkege+Pron+Indef+Sg+Nom:mihkkege # ;
mihkkege+Pron+Indef+Pl+Nom:mahkkege # ;
mihkkege+Pron+Indef+Pl+Gen:maidege # ;
mihkke+Pron+Indef+Sg+Nom:mihkke # ;
mihkket+Pron+Indef+Sg+Nom:mihkket # ;
mihkkige+Pron+Indef+Sg+Nom:mihkkige # ;
mihkkige+Pron+Indef+Pl+Nom:mahkkige # ;
mihkkige+Pron+Indef+Pl+Gen:maidige # ;
giige+Pron+Indef+Sg+Nom:gijge # ;
giinai+Pron+Indef+Sg+Nom:gijnai # ;
giijai+Pron+Indef+Sg+Nom:gijjai # ;
miinai+Pron+Indef+Sg+Nom:mijnai # ;
miijai+Pron+Indef+Sg+Nom:mijjai # ;
ieš#giige+Pron+Indef+Sg+Nom:ieš#gijge # ;
ieš#giinai+Pron+Indef+Sg+Nom:ieš#gijnai # ;
ieš#giijai+Pron+Indef+Sg+Nom:ieš#gijjai # ;

! Pointers to obl sublexicon
mihkkege+Pron+Indef:ma oblindefcas ;
giige+Pron+Indef:gea oblindefcas ;
giinai+Pron+Indef:gea oblindefcas-giinai ;
miinai+Pron+Indef:ma oblindefcas-giinai ;
ieš#giige+Pron+Indef:ieš#gea oblindefcas ;
ieš#giinai+Pron+Indef:ieš#gea oblindefcas-giinai ;
guhtege+Pron+Indef:guh'te GUHTEGECASE ;
guhtenai+Pron+Indef:guh'te GUHTENAI ;
ieš#guh0te+Pron+Indef:ieš#guh'te GUHTECASE ;
ieš#guhtege+Pron+Indef:ieš#guh'te GUHTEGECASE ;
ieš#guhtenai+Pron+Indef:ieš#guh'te GUHTENAI ;
guhtemušge+Pron+Indef:guđemužž GUHTEMUSGECASE ;
guhtemušge+Pron+Indef:guhtemušge # ; !deviating Nom altarnant.
goabbáge+Pron+Indef:goabbá GUHTEGECASE ;
goabbánai+Pron+Indef:goabbá GUHTENAI ;
!makkárge+Pron+Indef (have a look at this one)
!oktage+Pron+Indef (wait with this one until numerals are treated)

!The difference between mihkkege and mihkkige
!continues thtrough declination (maidege-maidige etc.).
!Should they be separated in different lexicons to make it logical? IK

LEXICON oblindefcas
 +Sg:0 oblindemcassg ;
 +Pl:0 oblindemcaspl ;


LEXICON oblindemcassg
!The other cases, then pointing to clitics
 +Gen:nge # ;
 +Gen:nge# NAMAT ;
 +Acc:nge # ;
 +Ill:sage # ;
 +Loc:sge # ;
 +Loc:stege # ;
 +Com:jnnage # ;

LEXICON oblindemcaspl
 +Gen:jde # ;
 +Gen:jde# NAMAT ;
 +Acc:jde # ;
 +Gen:jdege # ;
 +Gen:jdege# NAMAT ;
 +Acc:jdege # ;
 +Gen:jdige # ;
 +Gen:jdige# NAMAT ;
 +Acc:jdige # ;
 +Ill:jddage # ;
 +Loc:jnge # ;
!Here we do not go to the case paradigm of the demonstratives,
!since the clitic path is different. ComPl lacks (intentionally) from paradigm.

LEXICON oblindefcas-giinai
 +Gen:nnai # ;
 +Gen:nnai# NAMAT ;
 +Acc:nnai # ;
 +Gen:nai # ;
 +Gen:nai# NAMAT ;
 +Acc:nai # ;
 +Ill:sanai # ;
 +Ill:sai # ;
 +Loc:snai # ;
 +Loc:sai # ;
 +Loc:stenai # ;
 +Loc:stai # ;
 +Com:jnnanai # ;
 +Com:jnnai # ;
 +Gen:jdnai # ;
 +Gen:jdnai# NAMAT ;
 +Acc:jdnai # ;
 +Gen:jdai # ;
 +Gen:jdai# NAMAT ;
 +Acc:jdai # ;
 +Ill:jddanai # ;
 +Ill:jddai # ;
 +Loc:jnnai # ;
 +Loc:jnai # ;



! guhtege
LEXICON GUHTEGECASE
 GUHTEGE-NE ;
 GUGE-GUGE-OBL ;


! Nom and Ess of guhtege
LEXICON GUHTEGE-NE
 +Sg+Nom:ge # ;
 +Ess:nge # ;

! guhtemušge
LEXICON GUHTEMUSGECASE
 GUHTEMUSGE-NE ;
 :a GUGE-GUGE-OBL ;

! Nom and Ess of guhtemušge
LEXICON GUHTEMUSGE-NE
 +Sg+Nom:ge # ;
 +Ess:ange # ;

! Oblique paradigm for guhtege and guhtemušGE
LEXICON GUGE-GUGE-OBL
 +Sg+Gen:X8ge # ;
 +Sg+Gen:X8ge# NAMAT ;
 +Sg+Acc:X8ge # ;
 +Sg+Ill:X3jge # ;
!+Sg+Gen:ge # ; ! this should go out
 +Sg+Loc:X4sge # ;
 +Sg+Loc:X4nge K ; !SUB !SOUTH
 +Sg+Com:X5jnge # ;
 +Pl+Gen:X5jdge # ;
 +Pl+Gen:X5jdge# NAMAT ;
 +Pl+Acc:X5jdge # ;
 +Pl+Ill:X5jdege # ;
!+Pl+Com:X5jgujnge # ; !ComPl is out.

LEXICON GUHTENAI
 +Sg+Nom:nai # ;
 +Sg+Gen:X8nai # ;
 +Sg+Gen:X8nai# NAMAT ;
 +Sg+Acc:X8nai # ;
 +Sg+Ill:X3jnai # ;
 +Sg+Loc:X4snai # ;
 +Sg+Com:X5jnnai # ;
 +Ess:nnai # ;
 +Pl+Gen:X5jdnai # ;
 +Pl+Gen:X5jdnai# NAMAT ;
 +Pl+Acc:X5jdnai # ;
 +Pl+Ill:X5jdenai # ;



! Declineable indefinites with normal case paradigms
! - - - - - - - - - - - - - - - - - - - - - - - - - - 

LEXICON declindef

eará+Pron+Indef:eará               indef-a-even-v ;
iežá+Pron+Indef:iežá		           indef-a-even-v ;
uhccánaš+Pron+Indef:uhccánažž	   indef-a-even-c ;
unnánaš+Pron+Indef:unnánažž	       indef-a-even-c ;

ea0nas+Pron+Indef:eatnas	       indef-a-uneven ;
ea0naš+Pron+Indef:eatnaš	           indef-a-uneven ;

muhtin+Pron+Indef:muhtim	       indef-a-uneven-gahpir ;
muhtun+Pron+Indef:muhtum	       indef-a-uneven-gahpir ;
muhtun+Pron+Indef:muhtom	       indef-a-uneven-gahpir ;

muhtin+Pron+Indef+Sg+Gen:muhtin#	   NAMAT ;
muhtun+Pron+Indef+Sg+Gen:muhtun#	   NAMAT ;
muhtin+Pron+Indef+Pl+Gen:muhtimijd#	   NAMAT ;
muhtun+Pron+Indef+Pl+Gen:muhtumijd#	   NAMAT ;
muhtin+Pron+Indef+Pl+Loc:muhttuin K ;


uhccán+Pron+Indef:uhccán	   indef-a-uneven-gahpir ;

unnán+Pron+Indef:unnán		   indef-a-uneven-gahpir ;
veháš+Pron+Indef:veháž		   indef-a-uneven-gahpir ;
váháš+Pron+Indef:váháž		   indef-a-uneven-gahpir ;
veaháš+Pron+Indef:veaháž	       indef-a-uneven-gahpir ;

soa0mes+Pron+Indef+Sg+Nom:soames   K ;
soa0mes+Pron+Indef+Ess:soapmásin   K ;
soa0mes+Pron+Indef:soapmás	      indefattr ;
soa0mes+Pron+Indef:soapmásX2      indefdeclsguneven-ag ;
soa0mes+Pron+Indef:soapmás	      indefdeclsguneven-ilc ;
soa0mes+Pron+Indef:soapmásX2      indefdeclpluneven-n ;
soa0mes+Pron+Indef:soapmási	      indefdeclpluneven-obl ;

nuppát+Pron+Indef:nuppád	          indefsg-a-uneven ;

duobbá+Pron+Indef:duobbá	          indef-a-even-v ;
dábbá+Pron+Indef:dábbá		      indef-a-even-v ;
juobbá+Pron+Indef:juobbá	          indef-a-even-v ;
juobbá#goabbá+Pron+Indef: JUOBBAGOABBA ;

nubbi+Pron+Indef:nubbi		     indef-a-even-v ;
nubbi+Pron+Indef+Acc:nuppe K ;
nubbi+Pron+Indef+Gen:nuppe K ;

juohkehaš+Pron+Indef:juohkehažž	     indef-a-even-c ;

ollut+Pron+Indef:ollu		   indefdeclpleven ;
olut+Pron+Indef:olu		   indefdeclpleven ;
moanat+Pron+Indef:moana		   indefdeclpleven ;
gallát+Pron+Indef+Attr:gallát	   K ;
buohkat+Pron+Indef:buohka	   indefdeclpleven ;
gaikkat+Pron+Indef:gajkka	   indefdeclpleven ;
goappašagat+Pron+Indef:goappašaga  indefdeclpleven ;

ollugat+Pron+Indef:ollug	   indu-pl-gahpir ;
oallugat+Pron+Indef:oallug	   indu-pl-gahpir ;
olugat+Pron+Indef:olug		   indu-pl-gahpir ;
gal0lis+Pron+Indef:gal'lis	   indefdecluneven ;
olus+Pron+Indef:olus		   indefdecluneven ;
ol0lus+Pron+Indef:ol'lus		   indefdecluneven ; ! substandard
soamis+Pron+Indef:soamis	   indefdecluneven ;
eambbos+Pron+Indef:eambbos	   indefdecluneven ;

eambbogat+Prop+Indef:eambbog	   indu-pl-gahpir ; 
guoktasat+Pron+Indef:guoktas	   indu-pl-gahpir ;
goappašat+Pron+Indef:goappaš	   indu-pl-gahpir ;
goappašat+Pron+Indef+Pl+Gen:goappašijd# NAMAT ;
goappešat+Pron+Indef:goappeš		indu-pl-gahpir ;

eanebut+Pron+Indef+Nom:eanebut	   K ;
eanebut+Pron+Indef:eanebu	   indefdeclpluneven-obl ;
eanemusat+Pron+Indef:eanemusX2	   indefdeclpluneven-n ;
eanemusat+Pron+Indef:eanemusiX2	   indefdeclpluneven-obl ;

eatnagat+Pron+Indef:eatnag	   indu-pl-gahpir ;
galládat+Pron+Indef:gallád	   indu-pl-gahpir ;


! Declension lexica for the declineable indefinites
! - - - - - - - - - - - - - - - - - - - - - - - - -

!splitting v- and c-final evensyll in two for NomEss, as usual.
LEXICON indef-a-even-v
 indefattr ;
 indefdecleven-v-ne ;
 indefdecleven-obl ;

LEXICON indef-a-even-c
 indefattr ;
 indefdecleven-c-ne ;
 :a indefdecleven-obl ;

LEXICON indefdecleven-obl
 indefdeclsgeven ;
 indefdeclpleven ;

LEXICON indefdecleven-v-ne
 +Sg+Nom: K ;
 +Ess:n K ;
 
LEXICON indefdecleven-c-ne
 +Sg+Nom: K ;
 +Ess:an K ;

LEXICON indefdeclsgeven
 +Sg+Gen:X4 K ;
 +Sg+Gen:X4# NAMAT ;
 +Sg+Acc:X4 K ;
 +Sg+Ill:X3j K ;
 +Sg+Loc:X4s K ;
 +Sg+Loc:X4n K ; !SUB !SOUTH
 +Sg+Com:X5jn K ;

LEXICON indefdeclpleven
 +Pl+Nom:X4t K ;
 +Pl+Gen:X5jd K ;
 +Pl+Gen:X5jd# NAMAT ;
 +Pl+Acc:X5jd K ;
 +Pl+Ill:X5jde K ;
 +Pl+Loc:X5jn K ;
 +Pl+Com:X5jgujn K ;
 
LEXICON JUOBBAGOABBA
 +Sg+Nom:juobbá#goabbá K ;
 +Sg+Gen:juoppá#goappá K ;
 +Sg+Gen:juoppá#goappá# NAMAT ;
 +Sg+Acc:juoppá#goappá K ;
 +Sg+Ill:juobbái#goabbái K ;
 +Sg+Loc:juoppás#goappás K ;
 +Sg+Com:juoppáin#goappáin K ;
 +Ess:juobbán#goabbán K ;
 +Pl+Nom:juobbát#goabbát K ;
 +Pl+Gen:juoppájd#goappájd K ;
 +Pl+Gen:juoppájd#goappájd# NAMAT ;
 +Pl+Acc:juoppájd#goappájd K ;
 +Pl+Ill:juobbájde#goabbájde K ;
 +Pl+Loc:juoppájn#goappájn K ;
 +Pl+Com:juoppájgujn#goappájgujn K ;

! Intermediate lexica for uneven indefinite pronouns
! - - - - - - - - - - - - - - - - - - - - - - - - - - 

LEXICON indef-a-uneven
 indefattr ;
 indefdecluneven ;

LEXICON indef-a-uneven-gahpir
 indefattr ;
 indefdecluneven-gahpir ;
 
LEXICON indefsg-a-uneven
 indefattr ;
 indu-sg ;
 
LEXICON indefdecluneven
 indu-sg ;
 indu-pl-gahpir ;
  
LEXICON indu-sg 
 :X4 indef-sg-u-n ;
 indef-sg-u-e ;
 :X2 indefdeclsguneven-ag ;
 indefdeclsguneven-ilc ;

LEXICON indefdecluneven-gahpir
 indu-sg-gahpir ;
 indu-pl-gahpir ;

LEXICON indu-sg-gahpir
 indefdeclsguneven-ne ;
 indefdeclsguneven-ag ;
 indefdeclsguneven-ilc ;

LEXICON indu-pl-gahpir
 indefdeclpluneven-n ;
 :i indefdeclpluneven-obl ;
 indef-sg-u-e ; !For eatnagin

! Suffix lexica for uneven indefinite pronouns
! - - - - - - - - - - - - - - - - - - - - - - -

LEXICON indefdeclsguneven-ne
 indef-sg-u-n ;
 indef-sg-u-e ;

LEXICON indef-sg-u-n
 +Sg+Nom: K ;

LEXICON indef-sg-u-e
 +Ess:in K ;

LEXICON indefdeclsguneven-ag
 +Sg+Gen:a K ;
 +Sg+Acc:a K ;

LEXICON indefdeclsguneven-ilc
 +Sg+Ill:ij K ;
 +Sg+Loc:is K ;
 +Sg+Loc:in K ; !SUB !SOUTH
 +Sg+Com:ijn K ;

LEXICON indefdeclpluneven-n
 +Pl+Nom:at K ;

LEXICON indefdeclpluneven-obl
 +Pl+Gen:jd K ;
 +Pl+Acc:jd K ;
 +Pl+Ill:jdda K ;
 +Pl+Loc:jn K ;
 +Pl+Com:jgujn K ;

LEXICON indefattr
 +Attr:0 K ;


! Multiword indefinites
! - - - - - - - - - - -

LEXICON indefmulti ! added this lexicon, because otherwise these would be in indefattr
feara% mii+Pron+Indef+Sg+Nom:feara% mij K-son ;
feara% mii+Pron+Indef+Sg+Acc:feara% májd K-son ;
feara% gii+Pron+Indef+Sg+Nom:feara% gij K-son ;
feara% mii+Pron+Indef:feara% ma oblintercas ;
feara% gii+Pron+Indef:feara% gea oblintercas ;

! some multiword prons, according to Nickel
!feara% mii iind ;
!feara% gii iind ;
mii% nu iind ;
gii% nu iind ;
makkár% nu iind ;
makkaráš% nu iind ;


! The indeclineable indefinites
! - - - - - - - - - - - - - - - 

LEXICON indeclindef
buot iind ;
buot:buot# NAMAT ;
gait:gajt iind ;
gait:gajt# NAMAT ;
eatnat iind ;
eatnat:eatnat# NAMAT ;
!fal iind ; ! cf. Nickel s. 126.
gallát iind ;
goabbat iind ;
goabbat:goabbat# NAMAT ;
goappaš iind ;
goappaš:goappaš# NAMAT ;
!goappašat iind ; ! thus both Nom and non-Nom. XXX.
guhtet iind ;
!ieš#guhtet iind ; !XXX Nom only
ieš#guhtet:ieš#guhtet# NAMAT ;
ihkinassii iind ;
juohke iind ;
juohke:juohke# NAMAT ;
ollu iind ;
vaikko iind ;

 
LEXICON iind
 +Pron+Indef:0 K ;



! Separate lexica for exceptional entries
! - - - - - - - - - - - - - - - - - - - - 

LEXICON declindef-idiosync
! separate lexica for these entries:
! oktat
 earrás EARRAS ;
ieš#guhtet+Pron+Indef+Nom:ieš#guhtet K ;
ieš#guhtet+Pron+Indef+Gen:ieš#guđet K ;
ieš#guhtet+Pron+Indef+Gen:ieš#guđet# NAMAT ;
ieš#guhtet+Pron+Indef+Acc:ieš#guđet K ;
 simme+Pron+Indef:simme SIMME ; !SUB
 seamma+Pron+Indef:seamma SEAMMA ;
 seammá+Pron+Indef:seammá SEAMMA ;
 seamma+Pron+Indef+Attr:seamma # ;
 seammá+Pron+Indef+Attr:seammá # ; !SUB, dict says seamma ~ seammá (#seamma)
 juoga+Pron+Indef:juo JUOGA ;
 gal0le+Pron+Indef:gal'l GUOKTE ;	!^C^
guktot+Pron+Indef:gukto indefdeclplcontr ;
! okta
! juoga mii
!oktahat         +       no      OKTAHAT => indefeven / Nom=aktahat / OKTAttr
!        OKTattr:n=oktahat, aktahat, oktat, aktat
!                o=ovttahat, ovttat
!hárvvis        +       a       indef-a-      uneven  hárve, hárvves
!hárvvis        +       a       indef-a-      contr   hárve, hárvves
!Some problematic ones, one form for nsg, one for the rest
!oktat+Pron+Indef+Sg+Nom:oktat # ;
!oktat+Pron+Indef:ovttat # ;
!oktahat+Pron+Indef+Sg+Nom:oktahat # ;
!oktahat+Pron+Indef+:ovttahat # ;

LEXICON indefdeclplcontr ! so far guktot
 +Pl+Nom:t K ;
 +Pl+Gen:jd K ;
 +Pl+Gen:jd# NAMAT ;
 +Pl+Acc:jd K ;
 +Pl+Ill:jde K ;
 +Pl+Loc:jn K ;
 +Pl+Com:jn K ;

LEXICON EARRAS
 +Sg+Acc:a # ;
 +Sg+Gen:a # ;
 +Sg+Ill:ij # ;
 +Sg+Loc:is # ;
 +Sg+Loc:in # ; !SUB !SOUTH
 +Sg+Com:in # ;
! Essive and plural ??? 

LEXICON SEAMMA
 +Sg+Nom: # ;
 +Sg+Acc: # ;
 +Sg+Gen: # ;
 +Sg+Gen:# NAMAT ;
 +Sg+Ill:j # ;
 +Sg+Loc:s # ;
 +Sg+Loc:n # ; !SUB !SOUTH
 +Sg+Com:jn # ;
 +Pl+Nom:t # ;
 +Pl+Acc:jd # ;
 +Pl+Gen:jd # ;
 +Pl+Gen:jd# NAMAT ;

LEXICON SIMME !SUB
 +Sg+Nom: # ;
 +Sg+Acc: # ;
 +Sg+Gen: # ;
 +Sg+Ill:j # ;
 +Sg+Loc:n # ;
 +Sg+Com:jn # ;
 +Pl+Nom:t # ;
 +Pl+Acc:jd # ;
 +Pl+Gen:jd # ;
 +Pl+Gen:jd# NAMAT ;



LEXICON JUOGA
 +Sg+Nom:ga # ;
 +Sg+Acc:ga # ;
 +Sg+Acc:jdá # ;
 +Sg+Acc:jddá # ;
 +Sg+Acc:jaaájd # ;
 +Sg+Gen:ga # ;
 +Sg+Gen:ga# NAMAT ;
 +Sg+Ill:sat # ;
 +Sg+Loc:stá # ;
 +Sg+Com:jdájn # ;
 +Ess:nin # ;
 +Attr:da # ;






! Subjunction
! ===========


LEXICON Subjunction

ahte Cs ;
amas Cs ;
!dahje Cs ; ! CC only (?)
danin% go Cs ;
daningo Cs ;
danne% go Cs ; 
das% go Cs ;
dasgo Cs ;
dasságo Cs ;
de Cs ;
dušše% go Cs ;
duššego Cs ;
!eará% go Cs ; ! Cs takes a clause, "eará go" can take a NP.
earágo Cs ; ! The Cs tag is not ideal, CS is a barrier in many rules.
earetgo Cs ;
!earret% go Cs ;
earretgo Cs ;
ehte Cs ;
!ele% ge Cs ; ! CC only (?)
geardde Cs ;
gearddi Cs ;
go Cs ;
nu% guhkásgo Cs ; ! Deviant, "guhkás go" expected
jes Cs ;
jis Cs ;
joas  Cs ;
!jogo Cs ; ! Moved to adv
jos Cs ;
josba Cs ;
!juogo Cs ; Moved to adv
juos Cs ;
juos% go Cs ;
juosgo Cs ;
jus Cs ;
maŋ'ŋágo Cs ;
maŋŋágo Cs ;
maŋŋel% go Cs ;
maŋŋelgo Cs ;
maŋŋá% go Cs ;
mo Cs ; !XXX
mov Cs ; !XXX
movt  Cs ; !XXX
muhte Cs ;
!muhto Cs ; ! CC instead?
!ovdal Cs ; ! Pp only without go (?)
ovdal% go Cs ;
ovdalgo Cs ;
plus Cs ;
seammás% go Cs ;
sehke Cs ; !Not in corpus.
!sihke Cs ; ! moved to adv
vái Cs ;
váikko Cs ;
vai Cs ;
vaikke Cs ;
vaikko Cs ;
váikke Cs ;

LEXICON Cs

 +CS:0 # ;





! Conjunction
! ===========


LEXICON Conjunction

ahte Cc ; ! In expressions like "heajut ahte heajut"
ja  Cc ;
ja/dahje Cc ; !^C^
dahje Cc ;
dahjege Cc ;
dahege Cc ;
dehe Cc ;
dehege Cc ;
ele Cc ;
elege Cc ;
ele% ge Cc ;
muhto Cc ;
vai Cc ;

LEXICON Cc

 +CC:0 # ;


! Interjection
! ============


LEXICON Interjection 
aa Ij ;
a-a Ij ;
á-a Ij ;
ádje-da Ij ;
ái Ij ;
ámen Ij ;
ámmen Ij ;
ámen Ij ;
aamen Ij ;
amen Ij ;
ámmozan Ij ;
áš Ij ;
báv Ij ;
bea Ij ;
bešš Ij ;
bevggo-be Ij ;
bis-bis Ij ;
bodde Ij ;
buorat Ij ;
buorát Ij ;
bures Ij ;
buse-buse Ij ;
cecce Ij ;
ci Ij ;
cilo Ij ;
cuoh:cuoh3 Ij ;
cuš Ij ;
cyš Ij ;
cys Ij ;
dan#heakka Ij ;
dáhkka Ij ;
dearvva Ij ;
fánten Ij ;
ficce Ij ;
fihcce Ij ;
fuoi Ij ;
fuos Ij ;
gáppo-gáppo Ij ;
gea Ij ;
geah:geah3 Ij ;
giš Ij ;
Gucco Ij ;
gus Ij ;
halleluja Ij ;
hái Ij ;
he-hei Ij ;
hei Ij ;
hivás Ij ;
hohoh:hohoh3 Ij ;
hohoi Ij ;
hoi Ij ;
huh:huh3 Ij ;
huf Ij ;
hurá Ij ;
hurádis Ij ;
hyš Ij ;
iihii Ij ;
iš Ij ;
já Ij ;
jájáh:jájáh3 Ij ;
já-já Ij ;
jo Ij ;
jojoh:jojoh3 Ij ;
jua Ij ;
juo Ij ;
juo Ij ;
kyš-kyš Ij ;
ma-maid Ij ;
maid:majd Ij ;
morá Ij ;
oh:oh3 Ij ;
oho Ij ;
ohoi Ij ;
puh:puh3 Ij ;
skole Ij ;
skolle Ij ;
skol Ij ;
šlečč Ij ;
uhm Ij ;
uš Ij ;
voi Ij ;
vooi Ij ;
vuoi Ij ;

LEXICON Ij

 +Interj:0 # ;





! Particles
! =========


LEXICON Particles
abai pcle ;
ahteson pcle ;
aitto pcle ;
almma pcle ;
ama pcle ;
amal pcle ;
amat pcle ;
amma pcle ;
ammahal pcle ;
ammal pcle ;
ammat pcle ;
ammes pcle ;
appason pcle ;
ba pcle ;
bat pcle ;
!be pcle ; only in Finland as clitic?
dat pcle ;
dego pcle ; 
eaigga pcle ;
eiddo pcle ;
eigga pcle ;
eitto pcle ;
galahal pcle ;
galahan pcle ;
galhal pcle ;
ge pcle ;
ges pcle ;
gis pcle ;
go pcle ;
goit:gojt pcle ;
goitge:gojtge pcle ;
goittot:gojttot pcle ;
goittotge:gojttotge pcle ;
hás pcle ;
hal pcle ;
han pcle ;
na pcle ;
naj pcle ;
no pcle ;
noa pcle ;
son pcle ;

LEXICON pcle
+Pcle:0 # ;
! Perhaps this should be opened to  a direction to K
! and all the ge versions should be removed.
! (i.e. only goit, not goitge). This errouneously 
! permits gege, goge, etc., though, and we thus leave
! things as they are.



! Numerals
! ========

! The initial lexica
! ------------------

LEXICON Numeral					!^N^
MILJON ;          ! a noun of its own		!^N^
UNDERDUHAT ;      ! for generator under 1000	!^N^
JUSTDUHAT ;       ! going via 1000		!^N^
OVERDUHAT ;       ! for generator over 1000	!^N^
OLD ;             ! for "thirteen hundred, etc.	!^N^
!num-ordinal ;     ! The basic ordinal numbers	!^N^!Integrated in card.
!num-derived ;    ! still unimplemented		!^N^
num-imprecise ;   ! still almost unimplemented	!^N^
ARABIC ;          ! for the arabic numerals	!^C^
ROMAN ;           ! for the roman numerals	!^C^
NUM-PREFIXES ;    ! for §34 etc.		!^C^
ISOLATED-NUMEXP ; ! for ½ etc.			!^C^

! The tag !^N^ was left from an earlier attempt at removing the numerals
! from a potential nonrecursive tagger. It is tentatively left there, in
! case we see that our nonrecursive parser grows too big. Otherwise it may
! just be removed.

! We now run through the alphabetic numerals, ending up with the case
! inflection of the final number. Intermediate case inflection is still
! unimplemented.


! Million, a noun of its own
! --------------------------

LEXICON MILJON					!^N
miljon+Num+Sg+Nom:miljon K ;			!^N^ !Num or N?
miljon+Num+Sg+Ess:miljovdnan K ;		!^N^
miljon+Num:miljovdna OKTA-NE ;			!^N^
miljon+Num:miljovdna OKTA-OBL ;			!^N^ !hmm...
miljun+Num:miljuvdna OKTA-NE ;
miljun+Num:miljuvdna OKTA-OBL ;
millijon+Num:millijovdna OKTA-NE ;			!^N^
millijon+Num:millijovdna OKTA-OBL ;			!^N^ !hmm...
millijovnnat+A+Ord:millijovnnag VUOSTTAS ;	!^N^
miljonat+A+Ord:miljonag VUOSTTAS ;		!^N^
miljár0da+Num:miljár'da DEVNVCASE ;		!^N^ ! Num or N?
miljárdat+A+Ord:miljárdag VUOSTTAS ;		!^N^


! Overduhat, for the nuumerals over 1000
! --------------------------------------

LEXICON OVERDUHAT				!^N^
O-OKTAF ;     ! 1000				!^N^
O-2TO9F ;     ! 2000, 3000, ...			!^N^
O-JUSTLOGIF ; ! 10000				!^N^
O-2TO9LOG ;   ! 20000, 30000, ...		!^N^
O-NUPPELOT ;  ! 11000, 12000, ...		!^N^
O-CUODI ;     ! 100000				!^N^	
O-2TO9CUO ;   ! 200000, 300000, ...		!^N^

! All the child lexica of OVERDUHAT have the prefix O-.
! They are directed via their respective numerals to the lexicon JUSTDUHAT.


! Thousands

LEXICON O-OKTAF		!^N^
okta JUSTDUHAT ;	!^N^
JUSTDUHAT ;		!^N^

LEXICON O-2TO9F			!^N^
guokte JUSTDUHAT ;		!^N^	
golbma JUSTDUHAT ;		!^N^
njeallje JUSTDUHAT ;		!^N^		
njeallja JUSTDUHAT ;		!^N^
vihtta JUSTDUHAT ;		!^N^
guhtta JUSTDUHAT ;		!^N^
čieža JUSTDUHAT ;		!^N^
gávcci JUSTDUHAT ;		!^N^
ovcci JUSTDUHAT ;		!^N^


! Tens of thousands

LEXICON O-JUSTLOGIF		!^N^
logi JUSTDUHAT ;		!^N^
! This lexicon is for the number 10000 only. it is separated from the
! rest to avoid forms like *logivihttaduhát, etc.


LEXICON O-LOGIF			!^N^	     
logi JUSTDUHAT ;		!^N^
logi O-OKTAF ;			!^N^
logi O-2TO9F ;			!^N^
! this lexicon is accessed only via other O-lexica, and not directly 
! from OVERDUHAT. Thus, *logivihttaduhát, etc. is avoided.

LEXICON O-2TO9LOG		!^N^	  
guokte O-LOGIF ;		!^N^
golbma O-LOGIF ;		!^N^
njeallje O-LOGIF ;		!^N^
njeallja O-LOGIF ;		!^N^
vihtta O-LOGIF ;		!^N^
guhtta O-LOGIF ;		!^N^
čieža O-LOGIF ;			!^N^
gávcci O-LOGIF ;		!^N^
ovcci O-LOGIF ;			!^N^


! Teens of thousands

LEXICON O-NUPPELOT		!^N^
okta O-NL ;			!^N^
guokte O-NL ;			!^N^
golbma O-NL ;			!^N^
njeallje O-NL ;			!^N^
njeallja O-NL ;			!^N^
vihtta O-NL ;			!^N^
guhtta O-NL ;			!^N^
čieža O-NL ;			!^N^
gávcci O-NL ;			!^N^
ovcci O-NL ;			!^N^

 LEXICON O-NL			!^N^
 nuppelohkái JUSTDUHAT ;	!^N^


! Hundreds of thousands

LEXICON O-CUODI					!^N^
čuođi JUSTDUHAT ;  ! 100000			!^N^
čuođi O-OKTAF ;     ! 101000			!^N^
čuođi O-2TO9F ;    ! 102000, 103000, ...	!^N^
čuođi O-LOGIF ;    ! 110000, 113000, ...	!^N^
čuođi O-2TO9LOG ;  ! 120000, 123000, ...	!^N^
čuođi O-NUPPELOT ; ! 115000, ...		!^N^

LEXICON O-2TO9CUO				!^N^
okta O-CUODI ;       ! #one# hundred...		!^N^
guokte O-CUODI ;				!^N^
golbma O-CUODI ;				!^N^
njeallje O-CUODI ;				!^N^
njeallja O-CUODI ;				!^N^
vihtta O-CUODI ;				!^N^
guhtta O-CUODI ;				!^N^
čieža O-CUODI ;					!^N^
gávcci O-CUODI ;				!^N^
ovcci O-CUODI ;					!^N^





! Justduhat, for numrals going via 1000
! -------------------------------------

LEXICON JUSTDUHAT				!^N^
duhát UNDERDUHAT ;  ! Thousand-and-something	!^N^
duhát:duháh DUHAT ; ! Case infl of duhát	!^N^
duháhat+A+Ord:duháha DUHAHAT ;			!^N^


! Old, for the old counting thirteen hundred etc.	
! -----------------------------------------------

LEXICON OLD					!^N^
okta NLX ;					!^N^	
guokte NLX ;					!^N^
golbma NLX ;					!^N^
njeallje NLX ;					!^N^
njeallja NLX ;					!^N^
vihtta NLX ;					!^N^
guhtta NLX ;					!^N^
čieža NLX ;					!^N^
gávcci NLX ;					!^N^
ovcci NLX ;					!^N^

LEXICON NLX					!^N^
nuppelohkái CUODI ;				!^N^
nuppelohkái 2TO9CUO ;				!^N^



! Underduhat, the numerals under 1000
! -----------------------------------


LEXICON UNDERDUHAT				!^N^
OKTAF ;     ! 1, 11				!^N^
2TO9F ;     ! 2-9, 12-19			!^N^
LOGIF ;     ! 10				!^N^
2TO9LOG ;   ! 20-99				!^N^
CUODI ;     ! 100-199				!^N^
2TO9CUO ;   ! 200-999				!^N^

LEXICON OKTAF					!^N^
ok0ta:ok'ta OKTA ;				!^N^
vuosttas+A+Ord:vuosttas VUOSTTAS ;			 !^N^
vuosttaš+A+Ord:vuosttaž VUOSTTAS ;              !^N^ 
! K ; this lead to "go +Clt" as an analysis, and was removed.
! Thus, direction to K should be made at an earlier point

LEXICON 2TO9F					!^N^    
guok0te:guok't GUOKTE ;				!^N^
nubbi+A+Ord:nubbi indef-a-even-v ;		!^N^ !!CHECK THIS
nubbi+A+Ord+Acc:nuppe K ;			!^N^	
nubbi+A+Ord+Gen:nuppe K ;			!^N^
golbma GOLBMA ;					!^N^	
goalmmát+A+Ord:goalmmád VUOSTTAS ;		!^N^
njeallje:njeallj NJEALLJE ;			!^N^
njeallja:njeallj NJEALLJA ;			!^N^
njealját+A+Ord:njealjád VUOSTTAS ;		!^N^
vihtta:vi VIHTTA ;				!^N^
viđát+A+Ord:viđád VUOSTTAS ;			!^N^
guhtta:gu VIHTTA ;				!^N^
guđát+A+Ord:guđád VUOSTTAS ;			!^N^
čieža:čie CIEZA ;				!^N^
čihččet+A+Ord:čihččed VUOSTTAS ;		!^N^
gávcci:gák'ci GAVCCI ;				!^N^
gávccát+A+Ord:gávccád VUOSTTAS ;		!^N^
ovcci:ok'ci GAVCCI ;				!^N^
ovccát+A+Ord:ovccád VUOSTTAS ;			!^N^


LEXICON LOGIF					!^N^
logi:loh'ki LOGI ;				!^N^
logi OKTAF ;					!^N^
logi 2TO9F ;					!^N^
lot+Num+Sg+Nom:lot K ;
lot+Num+Sg+Acc+Attr:lot K ;                    	!^N^
lot NUMERALCOMPOUNDS ;      !^N^ ^C^
loh0ki:loh'ki GOAHTI ;				!^N^ ! vihttalohki, collective number
logát+A+Ord:logád VUOSTTAS ;			!^N^

 LEXICON NUPPELOT				!^N^
 okta NL ;					!^N^
 guokte NL ;					!^N^
 golbma NL ;					!^N^
 njeallje NL ;					!^N^
 njeallja NL ;					!^N^
 vihtta NL ;					!^N^
 guhtta NL ;					!^N^
 čieža NL ;					!^N^
 gávcci NL ;					!^N^
 ovcci NL ;					!^N^

 LEXICON NL					!^N^
! :nuppe#lohkái JUSTDUHAT ;			!^N^
 :nuppe#lohkái NUPPELOHKAI ;			!^N^


LEXICON 2TO9LOG					!^N^
guokte LOGIF ;					!^N^
golbma LOGIF ;					!^N^
njeallje LOGIF ;				!^N^
njeallja LOGIF ;				!^N^
vihtta LOGIF ;					!^N^
guhtta LOGIF ;					!^N^
čieža LOGIF ;					!^N^
gávcci LOGIF ;					!^N^
ovcci LOGIF ;					!^N^
      

LEXICON CUODI					!^N^
!čuohti:čuođi OVERDUHAT ;			!^N^
čuođi LOGI ;					!^N^
čuođi LOGIF ;					!^N^
čuođi 2TO9LOG ;					!^N^
čuođi OKTAF ;					!^N^
čuođi 2TO9F ;					!^N^
čuođi NUPPELOT ;				!^N^
čuoh0ti:čuoh'ti VIVVA ; ! imprecise number	!^N^
čuoh0ti:čuoh'te VIVVA ; ! imprecise number	!^N^
čuođát+A+Ord:čuođád VUOSTTAS ;			!^N^


LEXICON 2TO9CUO					!^N^
guokte GCUODI ;					!^N^
golbma GCUODI ;					!^N^
njeallje GCUODI ;				!^N^
njeallja GCUODI ;				!^N^
vihtta GCUODI ;					!^N^
guhtta GCUODI ;					!^N^
čieža GCUODI ;					!^N^
gávcci GCUODI ;					!^N^
ovcci GCUODI ;					!^N^

LEXICON GCUODI					!^N^
!čuođi OVERDUHAT ;				!^N^
čuohti LOGI ; ! testing ^N^  
!čuoh0ti:čuođi LOGI ;					!^N^
čuoh0ti:čuođi LOGIF ;					!^N^
čuoh0ti:čuođi 2TO9LOG ;					!^N^
čuoh0ti:čuođi OKTAF ;					!^N^
čuoh0ti:čuođi 2TO9F ;					!^N^
čuoh0ti:čuođi NUPPELOT ;				!^N^
!čuoh0ti:čuoh'ti VIVVA ; ! imprecise number	!^N^
! Commented out the noun čuohti and gav


! Case forms of the basic digits
! ------------------------------

LEXICON OKTA					!^N^
 +Num: OKTA-NE ;				!^N^
 +Num: OKTA-OBL ;				!^N^
 

LEXICON GUOKTE					!^N^
 +Num:e GUOKTE-NA ;				!^N^
 +Num:i GUOKTI ;				!^N^

LEXICON GOLBMA					!^N^
 +Num: GUOKTE-NA ;				!^N^
 +Num: GUOKTI ;					!^N^

LEXICON NJEALLJA				!^N^
 +Num:a GUOKTE-NA ;				!^N^
 +Num:a GUOKTI ;				!^N^

! +Num+Sg+Gen:X4 NUPPELOHKAI ;
! +Num+Sg+Loc:X4s NUPPELOHKAI ;
! +Num+Pl+Nom:X4t NUPPELOHKAI ;
! +Num: NUM-I ;
! +Num:X5 NUM-II ;

LEXICON NJEALLJE				!^N^
 +Num:e GUOKTE-NA ;				!^N^
 +Num:i GUOKTI ;				!^N^

! +Num+Sg+Gen:iX4 NUPPELOHKAI ; ! for njealji
! +Num+Sg+Gen:eX4 NUPPELOHKAI ; ! for njealje
! +Num+Sg+Loc:X4s NUPPELOHKAI ;
! +Num+Pl+Nom:X4t NUPPELOHKAI ;
! +Num:X3 NUM-I ;
! +Num:X5 NUM-II ;

! 5 and 6, minimal stem vi-, gu- due to exceptional III-I consonant gradation
LEXICON VIHTTA					!^N^
 +Num: VIHTTA-NAIE ;				!^N^
 +Num: VIHTTA-OBL ;				!^N^

LEXICON CIEZA					!^N^
! +Num+Sg+Ill:žaX3j NUPPELOHKAI ;
! +Num+Sg+Ill:hččaX3j NUPPELOHKAI ;
 +Num+Sg+Ill:žaX3 NUPPELOHKAI ;			!^N^
 +Num+Sg+Ill:hččaX3 NUPPELOHKAI ;		!^N^
 +Num:h'čaX3 NUM-I ;				!^N^
 +Num:h'čaX5 NUM-II ;				!^N^
 +Num+Ess:hžan NUPPELOHKAI ;			!^N^
 +Num:ža LOGI-WE ;				!^N^
 +Num:ža NUPPELOHKAI ;				!^N^

LEXICON GAVCCI ! 8,9				!^N^
 +Num:X4 NUPPELOHKAI ;			!^N^
     LOGI ;						!^N^


LEXICON LOGI					!^N^
 +Num:X4 LOGI-WE ;				!^N^
 +Num:X3 NUM-I ;				!^N^
 +Num:X5 NUM-II ;				!^N^
 +Num+Ess:n K ;					!^N^
 
LEXICON DUHAHAT                !^N^
 +Sg+Nom:t K ;          !^N^
 +Sg+Gen:tta K ;         !^N^
 +Sg+Acc:tta K ;            !^N^
 +Sg+Ill:ddij K ;   !^N^
 +Sg+Loc:ttas K ;            !^N^
 +Sg+Com:ttajn K ;            !^N^
 +Ess:ddan K ;                  !^N^
 +Pl+Nom:ttat K ;                 !^N^
 +Pl+Gen:ttaid K ;              !^N^
 +Pl+Acc:ttaid K ;              !^N^
 +Pl+Ill:ttaide K ;              !^N^
 +Pl+Loc:ttain K ;                !^N^
 +Pl+Com:ttaiguin K ;                 !^N^
 +Attr:t K ;					!^N^



! Basic digits, continuation lexica

! one

LEXICON OKTA-NE					!^N^
 +Sg+Nom: NUPPELOHKAI ;				!^N^
 +Ess:n NUPPELOHKAI ;				!^N^

LEXICON OKTA-OBL				!^N^
 :X4 OKTA-A ;					!^N^
 :X4 NUM-GLN ;					!^N^
 :X3 NUM-I ;					!^N^
 :X5 NUM-II ;					!^N^
 
LEXICON OKTA-A					!^N^
 +Sg+Acc: NUPPELOHKAI ;				!^N^
 
! two, three and four

LEXICON GUOKTE-NA				!^N^
 +Sg+Nom: NUPPELOHKAI ;				!^N^
 +Sg+Acc: NUPPELOHKAI ;				!^N^
 +Sg+Gen:X4 NUPPELOHKAI ;			!^N^
 +Sg+Gen#:X4# NUMERALCOMPOUNDS ;	! guovtteávjjot !^N^

LEXICON GUOKTI					!^N^
 :X4 NUM-GLN ;					!^N^
     NUM-ESS ;					!^N^
 :X3 NUM-I ;					!^N^
 :X5 NUM-II ;					!^N^



! Common for one through four

LEXICON NUM-GLN					!^N^
 +Sg+Gen: NUPPELOHKAI ;				!^N^
 +Sg+Gen#:# NUMERALCOMPOUNDS ;	! golmmagielat !^N^
 +Sg+Loc:s NUPPELOHKAI ;			!^N^
 +Sg+Loc:n NUPPELOHKAI ; !SUB !SOUTH		!^N^
 +Pl+Nom:t NUPPELOHKAI ;			!^N^

LEXICON NUM-ESS					!^N^
 +Ess:n NUPPELOHKAI ;				!^N^

LEXICON NUM-I					!^N^
 +Sg+Ill:j NUPPELOHKAI ;			!^N^

LEXICON NUM-II					!^N^
 +Sg+Com:jn NUPPELOHKAI ;			!^N^
 +Pl+Acc:jd NUPPELOHKAI ;			!^N^
 +Pl+Gen:jd NUPPELOHKAI ;			!^N^
 +Pl+Ill:jde NUPPELOHKAI ;			!^N^
 +Pl+Loc:jn NUPPELOHKAI ;			!^N^
 +Pl+Com:jgujn NUPPELOHKAI ;			!^N^


! Five and six

LEXICON VIHTTA-NAIE				!^N^
 +Sg+Nom:htta NUPPELOHKAI ;			!^N^
 +Sg+Acc:htta NUPPELOHKAI ;			!^N^
 +Sg+Ill:httij NUPPELOHKAI ;			!^N^
 +Ess:httan NUPPELOHKAI ;			!^N^

LEXICON VIHTTA-OBL				!^N^
 +Sg+Gen:đa NUPPELOHKAI ;			!^N^
 +Sg+Gen#:đa NUMERALCOMPOUNDS ;	! viđauvssot		!^N^ ^C^
 +Sg+Loc:đas NUPPELOHKAI ;			!^N^
 +Sg+Loc:đan NUPPELOHKAI ; !SUB !SOUTH		!^N^
 +Sg+Com:đain NUPPELOHKAI ;			!^N^
 +Pl+Nom:đat NUPPELOHKAI ;			!^N^
 +Pl+Acc:đajd NUPPELOHKAI ;			!^N^
 +Pl+Gen:đajd NUPPELOHKAI ;			!^N^
 +Pl+Ill:đajde NUPPELOHKAI ;			!^N^
 +Pl+Loc:đajn NUPPELOHKAI ;			!^N^
 +Pl+Com:đajgujn NUPPELOHKAI ;			!^N^


! Seven and up

LEXICON LOGI-WE					!^N^
 +Sg+Nom: K ;					!^N^
 +Sg+Acc: K ;					!^N^
 +Sg+Gen: K ;					!^N^
 +Sg+Gen#:# NUMERALCOMPOUNDS ; ! logigielat					!^N^
 +Sg+Loc:s K ;					!^N^
 +Sg+Loc:n K ; !SUB !SOUTH			!^N^
 +Pl+Nom:t K ;					!^N^

LEXICON DUHAT					!^N^
 +Num+Sg+Nom: K ;				!^N^
 +Num+Sg+Acc: K ; ! Acc = Nom for duhát		!^N^
 +Num+Sg+Gen: K ; ! Short genitive.		!^N^
 +Num+Sg+Gen:a K ;				!^N^
 +Num+Sg+Gen#:# NUMERALCOMPOUNDS ; !duhatjienat			!^N^
 +Num+Sg+Ill:ij K ;				!^N^
 +Num+Sg+Loc:is K ;				!^N^
 +Num+Sg+Loc:in K ; !SUB !SOUTH			!^N^
 +Num+Sg+Com:ijn K ;				!^N^
 +Num+Ess:in K ;				!^N^
 +Num+Pl+Nom:at K ;				!^N^
 +Num+Pl+Gen:ijd K ;				!^N^
 +Num+Pl+Acc:ijd K ;				!^N^
 +Num+Pl+Ill:ijdda K ;				!^N^
 +Num+Pl+Loc:ijn K ;				!^N^
 +Num+Pl+Com:ijgujn K ;				!^N^

LEXICON NUPPELOHKAI				!^N^
 NUPPELOHKAIGENERATOR ;				!^N^
 BASICGENERATOR ;				!^N^

LEXICON NUPPELOHKAIGENERATOR		!^N^
      :nuppe LOHKAI-END ;			!^N^
      :goalmmát LOHKAI-END ;		!^N^
      :njealját LOHKAI-END ;		!^N^
      :viđát LOHKAI-END ;			!^N^
      :guđát LOHKAI-END ;			!^N^
      :čihččet LOHKAI-END ;		!^N^
      :gávccát LOHKAI-END ;		!^N^
      :ovccát LOHKAI-END ;		!^N^
      :logát LOHKAI-END ;			!^N^
 +Attr:nuppe#lot K ;    ! short -teen	!^N^
      :nuppe#lot NUMERALCOMPOUNDS ;	!^N^	
!      :nuppe#lohkáj K ; ! long -teen		!^N^
!       :goalmmát#lohkáj K ;


LEXICON LOHKAI-END			!^N^
	:loh'kis GUOVTTIS ; 		!^N^
	:lohkáj K ;				!^N^
	:logis K ;				!^N^
	:logát VUOSTTAS ; 		!^N^

LEXICON BASICGENERATOR				!^N^
 K ;                   ! basic numbers		!^N^
! This is intermediary, we need a tag moving solution to get
! vihttanuppelohkái	vihttanuppelohkái+Num+Sg+Nom instead of
! vihttanuppelohkái       vihtta+Num+Sg+Nomnuppelohkái as the old soluteion.
! Now we get vihtta+Num+Sg+Nom:vihttanuppelohkái
! the old tags:
! nuppe#lohkái:nuppe#lohkáj K ; ! long -teen
! nuppe#lot+Attr:nuppe#lot K ;  ! short -teen
! Cf relevant chapter in B&K to fix this.


LEXICON NUMERALCOMPOUNDS ! numeral as first part !^N^
 NAMAT ;			   ! duhatjienat, logigielat, etc. !^N^
 SAS ; !^N^
 NounSecond ;	! golmmačiegahas	!^N^
! geardásaš:geardás DABALAS ;	   
! !duhat#geardásaš !^N^
! jahkásaš:jahkás DABALAS ; !^N^


! Ordinal numbers
! ---------------


!LEXICON num-ordinal ! Integrated in card
!vuosttas+A+Ord:vuosttas VUOSTTAS ;		!^N^
!vuosttaš+A+Ord:vuosttaž VUOSTTAS ;		!^N^
!nubbi+A+Ord:nubbi indef-a-even-v ;		!^N^
!goalmmát+A+Ord:goalmmád VUOSTTAS ;		!^N^
!njealját+A+Ord:njealjád VUOSTTAS ;		!^N^
!viđát+A+Ord:viđád VUOSTTAS ;			!^N^
!guđát+A+Ord:guđád VUOSTTAS ;			!^N^
!čihččet+A+Ord:čihččed VUOSTTAS ;		!^N^
!gávccát+A+Ord:gávccád VUOSTTAS ;		!^N^
!ovccát+A+Ord:ovccád VUOSTTAS ;			!^N^
!logát+A+Ord:logád VUOSTTAS ;			!^N^
!čuođát+A+Ord:čuođád VUOSTTAS ;			!^N^
!duháhat+A+Ord:duháh VUOSTTAS ;			!^N^
!millijovnnat+A+Ord:millijovnnag VUOSTTAS ;	!^N^
!miljonat+A+Ord:miljonag VUOSTTAS ;		!^N^

LEXICON VUOSTTAS				!^N^
 GAPPUS0 ;					!^N^
 GAPPUS- ;					!^N^
! +A: EABBO/EAMOS ; ! No comparatives here	!^N^
 +Attr: K ;					!^N^


! Imprecise numbers
! -----------------

LEXICON num-imprecise				!^N^
máŋga num-imprecise-distribution ;		!^N^
moadde   num-imprecise-distribution ;		!^N^
galle  num-imprecise-distribution ;		!^N^
máŋ0ga:máŋ'ga GOLBMA ;				!^N^
moadde:moadd GUOKTE ;				!^N^
gal0le:gal'le GOLBMA ;				!^N^
beal-nup+Num:beal%-nup3 # ; ! ad hoc		!^N^
beannot+Num+Sg+Nom:beannot # ;				!^N^
beannot+Num+Sg+Gen:beannot # ;				!^N^
beannot+Num+Sg+Acc:beannot # ;				!^N^

LEXICON num-imprecise-distribution		!^N^
NUPPELOHKAIGENERATOR ;  ! máŋga#nuppe#lohkái, máŋga !^N^
LOGIF ;        ! máŋga#logi, máŋga#lot		  !^N^
CUODI ;        ! máŋga#čuođi			  !^N^
NL ;           ! máŋga#nuppe#lohkái#duhát		  !^N^
O-NL ;         ! máŋga#nuppe#lohkái#duhát		  !^N^
O-LOGIF ;      ! máŋga#logi#duhát			  !^N^
JUSTDUHAT ;    ! máŋga#duhát			  !^N^
O-CUODI ;      ! máŋga#duhát			  !^N^




! Arabic numerals
! ---------------


LEXICON NUM-PREFIXES				!^C^
§	ARABIC ;      ! §24			!^C^
§§	ARABIC ;     ! §§24			!^C^
§% 	ARABIC ;    ! § 24			!^C^
§§% 	ARABIC ;   ! §§ 24			!^C^
%-	ARABIC ;     ! -24			!^C^
%‒	ARABIC ;     ! -24 U+2012 Figure line	!^C^
%–	ARABIC ;     ! -24 U+2013 en dash	!^C^
%—	ARABIC ;     ! -24 U+2014 em dash	!^C^
%―	ARABIC ;     ! -24 U+2015 horizontal l	!^C^
%+	ARABIC ;     ! +24			!^C^
%-% 	ARABIC ;	! - 24			!^C^
%‒% 	ARABIC ;	! - 24 2012		!^C^
%–% 	ARABIC ;	! - 24 2013	 	!^C^
%—% 	ARABIC ;	! - 24 2014		!^C^
%―% 	ARABIC ;	! - 24 2015 		!^C^
%+% 	ARABIC ;	! + 24			!^C^
%*	ARABIC ;     ! *24			!^C^
%$	ARABIC ;     ! $24  ! And the Euro?	!^C^
%<	ARABIC ;     ! <24			!^C^
%>	ARABIC ;     ! >24			!^C^
s%.	ARABIC ;     ! s.24			!^C^
	


LEXICON ARABIC					!^C^
1 ARABICLOOP ;					!^C^
2 ARABICLOOP ;					!^C^
3 ARABICLOOP ;					!^C^
4 ARABICLOOP ;					!^C^
5 ARABICLOOP ;					!^C^
6 ARABICLOOP ;					!^C^
7 ARABICLOOP ;					!^C^
8 ARABICLOOP ;					!^C^
9 ARABICLOOP ;					!^C^
%0 ARABICLOOP ;					!^C^

LEXICON ARABICLOOP				!^C^
ARABIC ;					!^C^
%-	ARABIC ;	      ! 1-2    multipart numbers	!^C^
%.%- 	ARABIC ;	      ! 1.-2   multipart numbers	!^C^
%-% 	ARABIC ;	      ! 1- 2   multipart numbers	!^C^
% %-	ARABIC ;	      ! 1 -2   multipart numbers	!^C^
% %-% 	ARABIC ;	      ! 1 - 2  multipart numbers	!^C^
% %-%-%   ARABIC ;	      ! 1 -- 2 multipart numbers	!^C^
% %-      ARABICDELIMITER ;   ! 1 -   multipart numbers		!^C^
% %-%-	  ARABICDELIMITER ;   ! 1 --  multipart numbers		!^C^
%. ARABIC ;							!^C^
%, ARABIC ;							!^C^
%: ARABIC ;							!^C^
%  ARABIC ;	   ! Allowing for "23 500".			!^C^
   ARABIC ;	   ! Allowing for "23 500" w/nbsp (initial char is alt-space)	!^C^
%/ ARABIC ;	   ! 24/12					!^C^
!%' ARABIC ;	   ! Since ' denotes cons.grad. This is not optimal either. 
!+Num:%- R ; !^C^	   ! Number compounds
+Num: R ; !^C^	   ! Hyphen removed bec. of "123- ja 124-viessu" !^C^
+A+Ord:%. # ;							 !^C^
,%-+Num:,%- # ;		! 10,-					 !^C^
%%+Num+Nom:%% # ;	! 50%. It is wrong, but 75 % of us write "75%".		!^C^
% %%+Num+Nom:% %% # ;	! We must analyse the correct ones as well... 50 % etc.	!^C^
+Num: ARABICDELIMITER ; ! list of number-case delimiters			!^C^


LEXICON ARABICDELIMITER								!^C^
  ARABICCASE ;      ! This is the 1984s case.					!^C^
 :%: ARABICCASE ;    ! This is the 1984:s case.					!^C^
 :%' ARABICCASE ;    ! This is the 1984's case.					!^C^
 :%- ARABICCASE ;    ! This is the 1984-s case.					!^C^
                     ! So, what does the lg council prefer?			

LEXICON ARABICCASE								!^C^
+Nom:f # ;		! s. 123f.						!^C^
+Nom:ff # ;		! s. 123ff.						!^C^
+Nom:b # ;		! § 123b						!^C^
+Nom:c # ;		! § 123c						!^C^
+Nom:d # ;		! § 123d						!^C^
+Nom:e # ;		! § 123e						!^C^
! # ;		! 123 is now 123+Num as well as 123+Num+Nom etc. Out again!	!^C^
+Nom: # ;									!^C^
+Gen: # ;									!^C^
+Acc: # ;									!^C^
+Gen:a # ;	! But here 123a 123+Num+Gen					!^C^
+Acc:a # ;									!^C^
+Ill:i # ;									!^C^
+Ill:ij # ;									!^C^
+Ill:áj # ;	! Testing over the weekend.					!^C^
+Loc:s # ;									!^C^
+Loc:is # ;									!^C^
+Loc:as # ;									!^C^
+Com:in # ;									!^C^
+Com:ain # ;									!^C^
+Ess:n # ;									!^C^
+Ess:an # ;									!^C^
! No plural numbers, hmm.


! The Roman numerals !
! ------------------ !

LEXICON ROMAN						!^C^
 ROM-THOUSAND ;						!^C^
 ROM-HUNDRED ;						!^C^
 ROM-TEN ;						!^C^
 ROM-ONE ;						!^C^

LEXICON ROM-THOUSAND              !^C^
 M ROM-THOUSAND-TAG ;						!^C^
 MM ROM-THOUSAND-TAG ;					!^C^
 MMM ROM-THOUSAND-TAG ;					!^C^
 MMMM ROM-THOUSAND-TAG ;					!^C^
 MMMMM ROM-THOUSAND-TAG ;					!^C^

LEXICON ROM-THOUSAND-TAG					!^C^
  ROMNUMTAG ;						!^C^
 ROM-HUNDRED ;					!^C^
 ROM-SPLIT	;				!^C^

LEXICON ROM-HUNDRED					!^C^
 C ROM-HUNDRED-TAG ;						!^C^
 CC ROM-HUNDRED-TAG ;						!^C^
 CCC ROM-HUNDRED-TAG ;						!^C^
 CD ROM-HUNDRED-TAG ;						!^C^
 D ROM-HUNDRED-TAG ;						!^C^
 DC ROM-HUNDRED-TAG ;						!^C^
 DCC ROM-HUNDRED-TAG ;						!^C^
 DCCC ROM-HUNDRED-TAG ;						!^C^
 CM ROM-HUNDRED-TAG ;						!^C^

LEXICON ROM-HUNDRED-TAG					!^C^
  ROMNUMTAG ;						!^C^
 ROM-TEN ;					!^C^
 ROM-SPLIT ;					!^C^

LEXICON ROM-TEN						!^C^
 X ROM-TEN-TAG ;						!^C^
 XX ROM-TEN-TAG ;						!^C^
 XXX ROM-TEN-TAG ;						!^C^
 XL ROM-TEN-TAG ;						!^C^
 L ROM-TEN-TAG ;						!^C^
 LX ROM-TEN-TAG ;						!^C^
 LXX ROM-TEN-TAG ;						!^C^
 LXXX ROM-TEN-TAG ;						!^C^
 XC ROM-TEN-TAG ;						!^C^

LEXICON ROM-TEN-TAG					!^C^
  ROMNUMTAG ;						!^C^
 ROM-ONE ;						!^C^
 ROM-SPLIT ;						!^C^

LEXICON ROM-ONE						!^C^
 I ROM-ONE-TAG ;						!^C^
 II ROM-ONE-TAG ;						!^C^
 III ROM-ONE-TAG ;						!^C^
 IV  ROM-ONE-TAG ;						!^C^
 V ROM-ONE-TAG ;						!^C^
 VI ROM-ONE-TAG ;						!^C^
 VII ROM-ONE-TAG ;						!^C^
 VIII ROM-ONE-TAG ;						!^C^
 IX ROM-ONE-TAG ;						!^C^

LEXICON ROM-ONE-TAG					!^C^
  ROMNUMTAG ;						!^C^
! +N: ROMNUMTAG ;						!^C^ !The Olav viđeš fix (Roman V now gets A)
 ROM-SPLIT ;						!^C^

! Here, we split the Roman numerals, in order to account for cases like "Kapihtal II-IV".
! We may send this first part directly to ROM-TAG below (and get the +Num tag), or we may
! send them through a second loop, identical to the first one, but marked wit "2" (the lexica
! are called 2ROMAN, etc. We do this instead of making a loop, since we do not want cases
! like "II-IV-VI-VII-IX". If that should turn out to be a good idea, a loop would do the 
! trick.

LEXICON ROM-SPLIT					!^C^
 :%- 2ROMAN ; ! II-VI, etc.				!^C^


! Here goes loop 2.

LEXICON 2ROMAN						!^C^
 2ROM-THOUSAND ;						!^C^
 2ROM-HUNDRED ;						!^C^
 2ROM-TEN ;						!^C^
 2ROM-ONE ;						!^C^

LEXICON 2ROM-THOUSAND              !^C^
 M 2ROM-THOUSAND-TAG ;						!^C^
 MM 2ROM-THOUSAND-TAG ;					!^C^
 MMM 2ROM-THOUSAND-TAG ;					!^C^
 MMMM 2ROM-THOUSAND-TAG ;					!^C^
 MMMMM 2ROM-THOUSAND-TAG ;					!^C^

LEXICON 2ROM-THOUSAND-TAG					!^C^
  ROMNUMTAG ;						!^C^
 2ROM-HUNDRED ;					!^C^

LEXICON 2ROM-HUNDRED					!^C^
 C 2ROM-HUNDRED-TAG ;						!^C^
 CC 2ROM-HUNDRED-TAG ;						!^C^
 CCC 2ROM-HUNDRED-TAG ;						!^C^
 CD 2ROM-HUNDRED-TAG ;						!^C^
 D 2ROM-HUNDRED-TAG ;						!^C^
 DC 2ROM-HUNDRED-TAG ;						!^C^
 DCC 2ROM-HUNDRED-TAG ;						!^C^
 DCCC 2ROM-HUNDRED-TAG ;						!^C^
 CM 2ROM-HUNDRED-TAG ;						!^C^

LEXICON 2ROM-HUNDRED-TAG					!^C^
  ROMNUMTAG ;						!^C^
 2ROM-TEN ;					!^C^

LEXICON 2ROM-TEN						!^C^
 X 2ROM-TEN-TAG ;						!^C^
 XX 2ROM-TEN-TAG ;						!^C^
 XXX 2ROM-TEN-TAG ;						!^C^
 XL 2ROM-TEN-TAG ;						!^C^
 L 2ROM-TEN-TAG ;						!^C^
 LX 2ROM-TEN-TAG ;						!^C^
 LXX 2ROM-TEN-TAG ;						!^C^
 LXXX 2ROM-TEN-TAG ;						!^C^
 XC 2ROM-TEN-TAG ;						!^C^

LEXICON 2ROM-TEN-TAG					!^C^
  ROMNUMTAG ;					!^C^
 2ROM-ONE ;					!^C^

LEXICON 2ROM-ONE						!^C^
 I 2ROM-ONE-TAG ;						!^C^
 II 2ROM-ONE-TAG ;						!^C^
 III 2ROM-ONE-TAG ;						!^C^
 IV  2ROM-ONE-TAG ;						!^C^
 V 2ROM-ONE-TAG ;						!^C^
 VI 2ROM-ONE-TAG ;						!^C^
 VII 2ROM-ONE-TAG ;						!^C^
 VIII 2ROM-ONE-TAG ;						!^C^
 IX 2ROM-ONE-TAG ;						!^C^

LEXICON 2ROM-ONE-TAG						!^C^
  ROMNUMTAG ;						!^C^
 
LEXICON ROMNUMTAG 						!^C^
 +Num+Nom: # ;									!^C^
 +Num+Gen: # ;									!^C^
 +Num+Acc: # ;									!^C^
 +A+Ord:%. # ;									!^C^ !III. as ordinal.
 +A+Ord: # ;								!^C^ !Looking for trouble...
 +Num:%: ROMNUMTAGOBL ;						!^C^
 +Num:%' ROMNUMTAGOBL ;						!^C^
 +Num:%- ROMNUMTAGOBL ;						!^C^
 +Num:%´ ROMNUMTAGOBL ;						!^C^
 +Num:   ROMNUMTAGOBL ;						!^C^

LEXICON ROMNUMTAGOBL 						!^C^
+Gen:a # ;	! But here 123a 123+Num+Gen					!^C^
+Acc:a # ;									!^C^
+Ill:i # ;									!^C^
+Ill:ij # ;									!^C^
+Ill:áj # ;					!^C^
+Loc:s # ;									!^C^
+Com:in # ;									!^C^
+Ess:n # ;									!^C^




! A final section with some isolated numeral expresssions !
! ------------------------------------------------------- !

      
LEXICON ISOLATED-NUMEXP					!^C^
½+Num:½ # ;						!^C^
¹+Num:¹ # ;						!^C^
²+Num:² # ;						!^C^
³+Num:³ # ;						!^C^
¼+Num:¼ # ;						!^C^
