Multichar_Symbols

@1@U.DATE.1@
@2@U.DATE.2@
@3@U.DATE.3@
@4@U.DATE.4@
@5@U.DATE.5@
@6@U.DATE.6@
@7@U.DATE.7@
@8@U.DATE.8@
@9@U.DATE.9@
@1%0@U.DATE.10@
@11@U.DATE.11@
@12@U.DATE.12@
@13@U.DATE.13@
@14@U.DATE.14@
@15@U.DATE.15@
@16@U.DATE.16@
@17@U.DATE.17@
@18@U.DATE.18@
@19@U.DATE.19@
@2%0@U.DATE.20@
@21@U.DATE.21@
@22@U.DATE.22@
@23@U.DATE.23@
@24@U.DATE.24@
@25@U.DATE.25@
@26@U.DATE.26@
@27@U.DATE.27@
@28@U.DATE.28@
@29@U.DATE.29@
@3%0@U.DATE.30@
@31@U.DATE.31@


!Distributed under the terms of the GNU General Public License version 2 
! or any later version.



LEXICON Root
< [a|b|c|d|e|f|g|h|i|j|k|l|m|n|o|p|q|r|s|t|u|v|w|q|y|z|æ|ø|å|ä|ö|á|č|đ|ŋ|A|B|C|D|E|F|G|H|I|J|K|L|M|N|O|P|Q|R|S|T|U|V|W|X|Y|Z|Æ|Ø|Å|Ä|Ö|Á|Č|Đ|Ŋ|Š|Ŧ|Ž]* > # ; ! This first line is to allow all letter strings.
NUMBERSECTION ;
COMMASECTION ;
DATESECTION ;

LEXICON NUMBERSECTION
HUNDREDSM ;		    ! 200M
čuođi:1 HUNDREDM ;	! 100M
TENSM ;			    ! 20-99M
TEENSM ;		    ! 10-19M
ONESM ;			    ! 1-9M
ONESG ;			    ! 1-9G
HUNDREDST ;		    ! 200000-999999
čuođi:1 HUNDREDT ;	! 100000-100999
TENST ;			    ! 20000-99999,10000-10999
TEENST ;		    ! 11000-19999
ONEST ;			    ! 2000-9999
duhát:1 THOUSAND ;	! 1000-1999
UNDERTHOUSAND ;		! 100-999
TENS ;			    ! 20-99
TEENS ;			    ! 10-19
ONES ;			    ! 1-9

LEXICON ONESG
miljárda:1   OVERMILLIONS ;
guokte:2     GIGA ;
golbma:3     GIGA ;
njeallje:4   GIGA ;
vihtta:5     GIGA ;
guhtta:6     GIGA ;
čieža:7      GIGA ;
gávcci:8     GIGA ;
ovcci:9      GIGA ; 

LEXICON GIGA
miljárdda: OVERMILLIONS ;

LEXICON HUNDREDSM
guokte:2   CUODIM ;
golbma:3   CUODIM ;
njeallje:4 CUODIM ;
vihtta:5   CUODIM ;
guhtta:6   CUODIM ;
čieža:7    CUODIM ;
gávcci:8   CUODIM ;
ovcci:9    CUODIM ;

LEXICON CUODIM
čuođi: HUNDREDM ;


LEXICON HUNDREDM
       TENSM ;
       TEENSM ;
:%0    ONESM ;
:%0%0  MILJON ;

LEXICON TEENSM
:1 TEENM ;

LEXICON TEENM
okta:1     LOHKAIM ;
guokte:2   LOHKAIM ;
golbma:3   LOHKAIM ;
njeallje:4 LOHKAIM ;
vihtta:5   LOHKAIM ;
guhtta:6   LOHKAIM ;
čieža:7    LOHKAIM ;
gávcci:8   LOHKAIM ;
ovcci:9    LOHKAIM ;

LEXICON LOHKAIM
nuppelohkái: MILJON ;

LEXICON TENSM
logi:1%0   MILJON ;
guokte:2   LOGIM ;
golbma:3   LOGIM ;
njeallje:4 LOGIM ;
vihtta:5   LOGIM ;
guhtta:6   LOGIM ;
čieža:7    LOGIM ;
gávcci:8   LOGIM ;
ovcci:9    LOGIM ;

LEXICON LOGIM
logi:%0  MILJON ;
logi:    ONESM ;

LEXICON ONESM
miljon:1 OVERTHOUSANDS ;
guokte:2   MILJON ;
golbma:3   MILJON ;
njeallje:4 MILJON ;
vihtta:5   MILJON ;
guhtta:6   MILJON ;
čieža:7    MILJON ;
gávcci:8   MILJON ;
ovcci:9    MILJON ; 

LEXICON MILJON
miljona: OVERTHOUSANDS ;

! =================
! Under the million
! =================

LEXICON OVERMILLIONS
                       HUNDREDSM ;
čuođi:1                HUNDREDM ;
:%0                    TENSM ;
:%0                    TEENSM ;
:%0%0                  MILJON ;
:%0%0                  ONESM ;
:%0%0%0                HUNDREDST ;  ! x00.000.000
čuođi:%0%0%01          HUNDREDT ;   ! 100.000.000
:%0%0%0%0              TENST ;	    !  20.000.000
:%0%0%0%0              TEENST ;	    !  10.000.000
:%0%0%0%0%0            ONEST ;	    !   2.000.000
duhát:%0%0%0%0%01      THOUSAND ;	!   1.000.000
:%0%0%0%0%0%0          UNDERTHOUSAND ;	
:%0%0%0%0%0%0%0        TENS ;
:%0%0%0%0%0%0%0        TEENS ;
:%0%0%0%0%0%0%0%0      ONES ;
:%0%0%0%0%0%0%0%0%0    COMMA ;	     !  x000000

LEXICON OVERTHOUSANDS
              HUNDREDST ;	! x00.000.000
čuođi:1       HUNDREDT ;	! 100.000.000
:%0           TENST ;		!  20.000.000
:%0           TEENST ;		!  10.000.000
:%0%0         ONEST ;		!   2.000.000
duhát:%0%01   THOUSAND ;	!   1.000.000
:%0%0%0       UNDERTHOUSAND ;	
:%0%0%0%0     TENS ;
:%0%0%0%0     TEENS ;
:%0%0%0%0%0   ONES ;
:%0%0%0%0%0%0 COMMA ;		!  x000000

LEXICON HUNDREDST
guokte:2   CUODIT ;	! 200000-299999
golbma:3   CUODIT ;	! 300000-399999
njeallje:4 CUODIT ;	! 400000-499999
vihtta:5   CUODIT ;	! 500000-599999
guhtta:6   CUODIT ;	! 600000-699999
čieža:7    CUODIT ;	! 700000-799999
gávcci:8   CUODIT ;	! 800000-899999
ovcci:9    CUODIT ;	! 900000-999999

LEXICON CUODIT
čuođi: HUNDREDT ;	! 100000-199999

LEXICON HUNDREDT	    ! X = 1-9, Y = 0-9
          TENST ;		! X2XYYY, X10YYY
          TEENST ;		! X1XYYY
okta:%01  THOUSANDS ;
:%0       ONEST ;		! XX0YYY
:%0%0     THOUSANDS ;	! X00YYY

LEXICON TEENST
:1 TEENT ;

LEXICON TEENT
logi:%0    THOUSANDS ;
okta:1     LOHKAIT ;
guokte:2   LOHKAIT ;
golbma:3   LOHKAIT ;
njeallje:4 LOHKAIT ;
vihtta:5   LOHKAIT ;
guhtta:6   LOHKAIT ;
čieža:7    LOHKAIT ;
gávcci:8   LOHKAIT ;
ovcci:9    LOHKAIT ;

LEXICON LOHKAIT
nuppelohkái: THOUSANDS ;

LEXICON TENST
guoktelogi:2   LOGIT ;
golbmalogi:3   LOGIT ;
njealljelogi:4 LOGIT ;
vihttalogi:5   LOGIT ;
guhttalogi:6   LOGIT ;
čiežalogi:7    LOGIT ;
gávccilogi:8   LOGIT ;
ovccilogi:9    LOGIT ;

LEXICON LOGIT
:%0     THOUSANDS ;
okta:1  THOUSANDS ;
        ONEST ;

LEXICON ONEST
guokte:2    THOUSANDS ;
golbma:3    THOUSANDS ;
njeallje:4  THOUSANDS ;
vihtta:5    THOUSANDS ;
guhtta:6    THOUSANDS ;
čieža:7     THOUSANDS ;
gávcci:8    THOUSANDS ;
ovcci:9     THOUSANDS ;

LEXICON THOUSANDS      ! x > 1
duhát: THOUSAND ;

LEXICON THOUSAND
        UNDERTHOUSAND ;	   ! 1100-1999
:%0     TENS ;	           ! 1020-1099
:%0     TEENS ;	           ! 1010-1019
:%0%0   ONES ;	           ! 1001-1009
:%0%0%0 COMMA ;	           ! 1000

!===========================
!Here starts the 999 numbers
!===========================

LEXICON UNDERTHOUSAND
HUNDREDS ;
čuođi:1 HUNDRED ;
čuođát:1 CUODAAT ;

LEXICON HUNDREDS
guokte:2   CUODI ;
golbma:3   CUODI ;
njeallje:4 CUODI ;
vihtta:5   CUODI ;
guhtta:6   CUODI ;
čieža:7    CUODI ;
gávcci:8   CUODI ;
ovcci:9    CUODI ;

LEXICON CUODI
čuođi: HUNDRED ;
čuođát: CUODAAT ;

LEXICON HUNDRED
         TENS ;
         TEENS ;
:%0      ONES ;
:%0%0    COMMA ;

LEXICON CUODAAT
:%0%0.    COMMA ;


LEXICON TEENS
:1 TEEN ;

LEXICON TEEN
okta:1     LOHKAI ;
guokte:2   LOHKAI ;
golbma:3   LOHKAI ;
njeallje:4 LOHKAI ;
vihtta:5   LOHKAI ;
guhtta:6   LOHKAI ;
čieža:7    LOHKAI ;
gávcci:8   LOHKAI ;
ovcci:9    LOHKAI ;

LEXICON LOHKAI
nuppelohkái: COMMA ;
:%. LOGAAT ;

LEXICON LOGAAT
nuppelogát: COMMA ;

LEXICON TENS
logi:1%0   COMMA ;
logát:1%0%.   COMMA ;
guokte:2   LOGI ;
golbma:3   LOGI ;
njeallje:4 LOGI ;
vihtta:5   LOGI ;
guhtta:6   LOGI ;
čieža:7    LOGI ;
gávcci:8   LOGI ;
ovcci:9    LOGI ;

LEXICON LOGI
logi:%0 COMMA ;
logát:%0 COMMA ;
logi:   ONES ;

LEXICON ONES
CARDINAL ;
ORDINAL ;

LEXICON CARDINAL
okta:1     COMMA ;
guokte:2   COMMA ;
golbma:3   COMMA ;
njeallje:4 COMMA ;
vihtta:5   COMMA ;
guhtta:6   COMMA ;
čieža:7    COMMA ;
gávcci:8   COMMA ;
ovcci:9    COMMA ;

LEXICON ORDINAL
vuosttaš:1%.     COMMA ;
nubbi:2%.   COMMA ;
goalmmát:3%.   COMMA ;
njealját:4%. COMMA ;
viđát:5%.   COMMA ;
guđát:6%.   COMMA ;
čihččet:7%.    COMMA ;
gávccát:8%.   COMMA ;
ovccát:9%.    COMMA ;

LEXICON COMMA
 ENDLEX ;
 COMMASECTION ;

LEXICON COMMASECTION 
                  ENDLEX ;
! % komma% :,      Root ;
! % čuohkkis% :%.  Root ;
! % kolon% :%:     Root ;
! % sárggis% :%-   Root ; 
 % lea% :%=       Root ; 
 % gráda% :%°     Root ; 
 % paragráfa% :§  Root ; 
! % násti% :%*     Root ; 
 % ja% :&         Root ; 


LEXICON DATESECTION

! [(0)[1-9]|[12][0-9]|3[01]]\.[3[01]]\.[([12][0-9])[0-9][0-9]]]
! 1.1.1934  = ođđajagimánu vuosttaš beaivi ovccinuppelotčuođigolbmaloginjeallje
! 3.11.2009 = skábmamánu goalmmát beaivi guokteduhátovcci
! 5.6.09    = geassemánu viđát beaivi nulla ovcci
! are there other formats in use?
! E.g. ská. 3. - ie combinations of abbr text and numbers?
! what about other orders? In Sweden, the year typically comes first:
! 2009.11.3
! Is this order found in Sámi texts, eg. from Sweden?
! and irrespective of written order, should the spoken order always be the same?

! Strategies:
! 1. make a "dumb spell-out", do the dates in one stretch, for an automaton
! with 365 lines 
! 2. Use flag diacritics: Do the dates in one batch and the months in another
!    Under this regime, we do this:
!    3.     11.          
!    @R.3@    skábmamánu  { @D.3@goalmmát | @D.4@njealjjat }
!    Then, the flag diacritic will remove non-harmonic strings @R.3@...@D.4@
!    And only the correct 3-3 string will be left intact.
!    So, strategy discussion closed :-)
!    We agree on 2? I certainly do :-)

:%0 DATEDAYMARK ; ! Eventual leading zero
    DATEDAYMARK ; ! No leading zero
    

LEXICON DATEDAYMARK
1@U.DATE.1@ DATEMONTHSEPARATOR ;
2@U.DATE.2@ DATEMONTHSEPARATOR ;
3@U.DATE.3@ DATEMONTHSEPARATOR ;
4@U.DATE.4@ DATEMONTHSEPARATOR ;
5@U.DATE.5@ DATEMONTHSEPARATOR ;
6@U.DATE.6@ DATEMONTHSEPARATOR ;
7@U.DATE.7@ DATEMONTHSEPARATOR ;
8@U.DATE.8@ DATEMONTHSEPARATOR ;
9@U.DATE.9@ DATEMONTHSEPARATOR ;
1%0@U.DATE.10@ DATEMONTHSEPARATOR ;
11@U.DATE.11@ DATEMONTHSEPARATOR ;
12@U.DATE.12@ DATEMONTHSEPARATOR ;
13@U.DATE.13@ DATEMONTHSEPARATOR ;
14@U.DATE.14@ DATEMONTHSEPARATOR ;
15@U.DATE.15@ DATEMONTHSEPARATOR ;
16@U.DATE.16@ DATEMONTHSEPARATOR ;
17@U.DATE.17@ DATEMONTHSEPARATOR ;
18@U.DATE.18@ DATEMONTHSEPARATOR ;
19@U.DATE.19@ DATEMONTHSEPARATOR ;
2%0@U.DATE.20@ DATEMONTHSEPARATOR ;
21@U.DATE.21@ DATEMONTHSEPARATOR ;
22@U.DATE.22@ DATEMONTHSEPARATOR ;
23@U.DATE.23@ DATEMONTHSEPARATOR ;
24@U.DATE.24@ DATEMONTHSEPARATOR ;
25@U.DATE.25@ DATEMONTHSEPARATOR ;
26@U.DATE.26@ DATEMONTHSEPARATOR ;
27@U.DATE.27@ DATEMONTHSEPARATOR ;
28@U.DATE.28@ DATEMONTHSEPARATOR ;
29@U.DATE.29@ DATEMONTHSEPARATOR ;
3%0@U.DATE.30@ DATEMONTHSEPARATOR ;
31@U.DATE.31@ DATEMONTHSEPARATOR ;
! must check the flag diacritic syntax

LEXICON DATEMONTHSEPARATOR
:%. MONTHZERO ;

LEXICON MONTHZERO
:%0 SMALLMONTH ; ! Eventual leading zero
    SMALLMONTH ; ! no leading zero
    LARGEMONTH ;


LEXICON SMALLMONTH
ođđajágimánu:1 DATEDAYS ;
guovvamánu:2 DATEDAYS ;
njukčamánu:3 DATEDAYS ;
cuoŋománu:4 DATEDAYS ;
miessemánu:5 DATEDAYS ;
geassemánu:6 DATEDAYS ;
suoidnemánu:7 DATEDAYS ;
borgemánu:8 DATEDAYS ;
čakčamánu:9 DATEDAYS ;

LEXICON LARGEMONTH
golggotmánu:10 DATEDAYS ;
skábmamánu:11 DATEDAYS ;
juovlamánu:12 DATEDAYS ;

LEXICON DATEDAYS
1@U.DATE.1@:@U.DATE.1@ DAYTEXT ;
2@U.DATE.2@:@U.DATE.2@ DAYTEXT ;
3@U.DATE.3@:@U.DATE.3@ DAYTEXT ;
4@U.DATE.4@:@U.DATE.4@ DAYTEXT ;
5@U.DATE.5@:@U.DATE.5@ DAYTEXT ;
6@U.DATE.6@:@U.DATE.6@ DAYTEXT ;
7@U.DATE.7@:@U.DATE.7@ DAYTEXT ;
8@U.DATE.8@:@U.DATE.8@ DAYTEXT ;
9@U.DATE.9@:@U.DATE.9@ DAYTEXT ;
1%0@U.DATE.10@:@U.DATE.10@ DAYTEXT ;
11@U.DATE.11@:@U.DATE.11@ DAYTEXT ;
12@U.DATE.12@:@U.DATE.12@ DAYTEXT ;
13@U.DATE.13@:@U.DATE.13@ DAYTEXT ;
14@U.DATE.14@:@U.DATE.14@ DAYTEXT ;
15@U.DATE.15@:@U.DATE.15@ DAYTEXT ;
16@U.DATE.16@:@U.DATE.16@ DAYTEXT ;
17@U.DATE.17@:@U.DATE.17@ DAYTEXT ;
18@U.DATE.18@:@U.DATE.18@ DAYTEXT ;
19@U.DATE.19@:@U.DATE.19@ DAYTEXT ;
2%0@U.DATE.20@:@U.DATE.20@ DAYTEXT ;
21@U.DATE.21@:@U.DATE.21@ DAYTEXT ;
22@U.DATE.22@:@U.DATE.22@ DAYTEXT ;
23@U.DATE.23@:@U.DATE.23@ DAYTEXT ;
24@U.DATE.24@:@U.DATE.24@ DAYTEXT ;
25@U.DATE.25@:@U.DATE.25@ DAYTEXT ;
26@U.DATE.26@:@U.DATE.26@ DAYTEXT ;
27@U.DATE.27@:@U.DATE.27@ DAYTEXT ;
28@U.DATE.28@:@U.DATE.28@ DAYTEXT ;
29@U.DATE.29@:@U.DATE.29@ DAYTEXT ;
3%0@U.DATE.30@:@U.DATE.30@ DAYTEXT ;
31@U.DATE.31@:@U.DATE.31@ DAYTEXT ;

LEXICON DAYTEXT
beaivi: DATEYEAR ;

LEXICON DATEYEAR 
okta:1   THOUSANDS ;
guokte:2 THOUSANDS ;

LEXICON ENDLEX
 # ;
