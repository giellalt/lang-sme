Alphabet
 a b c d e f g h i j k l m n o p q
 r s u t v w x y z å ä ö ø æ %-
 Å Ä Ö Ø Æ Á
 Á É Ó Ú Í À È Ò Ù Ì Ë Ü Ï Â Ê Ô Û Î Ã Ý þ Ñ Ð
 á é ó ú í à è ò ù ì ë ü ï â ê ô û î ã ý þ ñ ð ß ª ß ç
 e1:e i1:i u1:u c1 d1 h1 t1 n1 o1:o o3:o s1 z1
 C1 D1 N1 S1 T1 Z1 
 ':0 ¤:0 %/:0 '1:' !'1 is the real apostroph
 h2:h g2:g m2:m n2:n j3:j b3:b d3:d g3:g h3:h m3:m n3:n p3:p z3:z
                     J3:J B3:B D3:D G3:G H3:H M3:M N3:N P3:P Z3:Z
 X1:0 X2:0 X3:0 X4:0 X5:0 X6:0 X7:0 X8:0 X9:0
 Q1:0 Q2:0 Q3:0 Q4:0 Q5:0 Q6:0 Q7:0 Q8:0 Q9:0
 Y1:0 Y2:0 Y3:0 Y4:0 Y5:0 Y6:0 Y7:0 Y8:0 Y9:0 
 W1:0 W2:0 ;

Sets
  Vow = a á e e1 i i1 o o1 o3 u u1 y æ ø å ä ö A Á E I O U Y Æ Ø Å Ä Ö Å ;
  Cns = b b3 c c1 d d1 d3 f g g2 g3 h h2 h3 j j3 k l m m2 m3 n n1 n2 n3 
  p p3 r s s1 t t1 v z z1 z3
  ' '1 B C C1 D D1 F G H J K L M N N1 P R S S1 T T1 V Z Z1 ;
  StemCns = b d g g2 h h2 j j3 l m m2 n n2 r s s1 m3 n3 d3 g3 h3 b3 ;
  WeG = X4 X5 X6 X8 X9 Q4 Q5 Q6 Q7 Q8 W1 ;
  Len = Y1 Y2 Y3 Y4 Y5 Y6 Y7 Y9 ¤ ;
  Dummy = X1 X2 X3 X4 X5 X6 X7 X8 X9 Q1 Q2 Q3 Q4 Q5 Q6 Q7 Q8 Q9
  Y1 Y2 Y3 Y4 Y5 Y6 Y7 Y8 W1 W2 ¤ ;
  FinalDummy = X2 X4 X7 X8 ;
  NonMPDummy = X1 X4 X7 X8 ¤ ;
  NonMPDummy2 = X1 X4 X8 ¤ ; 
  HighVow = i u ;
  NonHighVow = a e o ;
  Stop = k p t c c1 ;
  DelCns = g2 m2 n2 h2 ;
  CntrCns = s j h z1 ;

Rules


"Postvocalic j Surfacing"
  j:i <=> Vow: ( :0 ) ( Dummy: ) _ [ Cns | ': | ( :0 - Y5: ) #: ] ;

!Added ( :0 ) because of olmmos1
!All j:i alternation is conflated into one rule. LC is vowel and optional
!(suffixinitial) dummy symbol, RC is consonant, dummy or word boundary,
!The intention is to have the strict operator (<=>), but catch the
!variation with a lot of (=all) contextual alternatives.
!exchanging DummyNotY5 with :0 - Y5: , better luck this time.
!j3:j is the j that does not turn to i in this context. It was added for
!the sake of Budejju and Stálobajjávri, to avoid Budeiju and Stálobaijávri

! Word final consonant alternations
! ---------------------------------

"Word Final Consonant Neutralization"  !smirezit : smires
  Cx:Cy <=> Vow: (( CntrCns:) Dummy:+) _ ( :0 - Y5: ) #: ;
			where Cx in (z z1 m h p d g b)
				Cy in (s s1 n t t t t t)
			matched ;

"Deletion: Final h s j z1 in Contracted Stems"
  Cx:0 <=> Vow _ [ Y5: | Y6: j: ] ;
			where Cx in (h s j z1) ;

"Word Final Cluster Simplification 1" !vuordildit : vuordil
  Cy:0 <=> Vow Cx _ #: ;
			where Cx in (l r)
				Cy in (d g) ;

"Word Final Cluster Simplification 2" !attestit : attes
  Cx:0 <=> Vow s _ #: ;
			where Cx in (t k m) ;

"Word Final Cluster Simplification 3" !beros1tit : beros1
  t:0 <=> Vow s1 _ #: ;

"Word Final Cluster Simplification 4" !bearjadahka : bearjadat 1
  Cx:0 <=> Vow h: ':0 _ #: ;
			where Cx in (t k) ;
! Changing bearjadahk to bearjadah'k, adding ':0

"Word Final Cluster Simplification 5" !bearjadahka : bearjadat 2
  h:t <=> Vow _ ':0 [t:0 | k:0] #: ;
! Splitting WFCF 4 into 2 to handle the inserted ':0 symbol.

"Word Final Double Consonant Simplification" !muitaluss : muitalus
  Cy:0 <=> Vow Cx: _ #: ;
			where Cx in (s s1 d z1)
				Cy in (s s1 d z1)
			matched ;


! Consonant gradation rules
! -------------------------


!Now that long double cons. have been analyzed having lexical triple consonants,
!many of the grad. rules of the form ':Cx may be collapsed to a single rule
!without left context: _ Cx Vow: WeG: ; where XXXX matched ; . This has not 
!been done yet (PS 29.11.1993).


"Gradation: h Loss" !johka : joga (h:0)
  h:0 <=> _ ': Cy: Vow ( StemCns:) (:StemCns) WeG: ;
			where Cy in (p t k c c1) ;

"Gradation: Prenasal Stops" !sápmi : sámi (p:0)
  Cx:0 <=> _ Cy Vow ( StemCns:) (:StemCns) WeG: ;
			where Cx in (p t k)
				Cy in (m n n1)
			matched ;

"Gradation: tnj" !latnja : lanja (t:0)
  t:0 <=> _ n j Vow ( StemCns:) (:StemCns) WeG: ;

"Gradation: dj" !vuodjit:vuoján
  d:0 <=> Vow: _ ': j Vow ( StemCns:) (:StemCns) WeG: ;

"Gradation: Double Consonant" !káffe:káfes
  Cx:0 <=> Vow: _ Cy Vow ( StemCns:) (:StemCns) WeG: ;
			where Cx in (d1 f l m n n1 r s s1 t1 v)
				Cy in (d1 f l m n n1 r s s1 t1 v)
			matched ;

"Gradation: Long Double Consonant"
  Cx:0 <=> Vow: Cy _ Cy Vow ( StemCns:) (:StemCns) WeG: ;
			where Cx in (l m n n1 r s s1 v)
				Cy in (l m n n1 r s s1 v)
			matched ;

"Gradation: Preaspirated Geminates"
  Cx:0 <=> h _ Cy Vow ( StemCns:) (:StemCns) WeG: ;
			where Cx in (k p t c c1 l r)
				Cy in (k p t c c1 l r)
			matched ;

"Gradation: Jodded Double Consonants"
  Cx:0 <=> Vow: _ Cy j Vow ( StemCns:) (:StemCns) WeG: ;
			where Cx in (l d n)
				Cy in (l d n)
			matched ; 

"Gradation: Cluster Non-nasal Sonorant + Non-sonorant"
  ':Cx <=> Vow: [ l | r | d1 | v | j: ] _ Cz Vow ( StemCns:) (:StemCns) WeG: ;
			where Cx in (f g d b k p t z z1 c c1 s s1)
				Cz in (f g d b k p t z z1 c c1 s s1)
			matched ;

!"Gradation: Cluster n1g"
!  ':g <=> Vow: n1 _ g Vow ( StemCns:) (:StemCns) WeG: ;
!Use next rule instead, including also n1k

"Gradation: Cluster n1 + Non-sonorant"
  ':Cx <=> Vow: n1 _ Cz Vow ( StemCns:) (:StemCns) WeG: ;
			where Cx in ( g k )
				Cz in ( g k )
			matched ;

"Gradation: Cluster m + Non-sonorant"
  ':Cx <=> Vow: m _ Cz Vow ( StemCns:) (:StemCns) WeG: ;
			where Cx in (f b p s s1)
				Cz in (f b p s s1)
			matched ;

"Gradation: Cluster n + Non-sonorant"
  ':Cx <=> Vow: n _ Cz Vow ( StemCns:) (:StemCns) WeG: ;
			where Cx in (d t z z1 c c1 s s1)
				Cz in (d t z z1 c c1 s s1)
			matched ;

"Gradation: Cluster j/v + Liquid"
  ':Cx <=> Vow: [ j: | v ] _ Cz Vow ( StemCns:) (:StemCns) WeG: ;
			where Cx in (l r)
				Cz in (l r)
			matched ;

"Gradation: Clusters lv, rj etc."
  ':Cx <=> Vow: [ d1 | l | r ] _ Cz Vow ( StemCns:) (:StemCns) WeG: ;
			where Cx in (v j)
				Cz in (v j)
			matched ;

"Gradation: Clusters tk, tm"
  ':Cx <=> Vow: [ t | t1 ] _ Cy Vow ( StemCns:) (:StemCns) WeG: ;
			where Cx in (k m)
				Cy in (k m)
			matched ;

"Gradation: Clusters ts, ks"
  0:s <=> Vow: [ t | k ] _ s Vow ( StemCns:) (:StemCns) WeG: ;

"Gradation: Clusters sk, sm etc."
  ':Cx <=> Vow: [ s | s1 ] _ Cz: Vow ( StemCns:) (:StemCns) WeG: ;
			where Cx in (k p t l m v)
				Cz in (k p t l m v)
			matched ;

"Gradation: Cluster vj"
  ':j <=> Vow: v _ j Vow ( StemCns:) (:StemCns) WeG: ;

"Gradation: Cluster jv"
  ':v <=> Vow: j: _ v Vow ( StemCns:) (:StemCns) WeG: ;

"Gradation: ihm, vhl etc."
  ':Cx <=> Vow: [ j: | v ] h _ Cy Vow ( StemCns:) (:StemCns) WeG: ;
			where Cx in (l m n)
				Cy in (l m n)
			matched ;

"Gradation: Clusters lbm, jdn, vdn etc."
  Cx:Cy <=> [ l | j: | v ] _ Cz Vow (Cns) ( StemCns:) (:StemCns) WeG: ;
			where Cx in (b d g)
				Cy in (m n n1)
				Cz in (m n n1)
			matched ;
! "(Cns)" is added. I do not like it. But the rule as it was blocked the 
! "olmmoz1in" essive of "olmmos1", since the "z1" was neither an underlying
! nor a surface stem consonant, I had to do something. The rule as it stands
! is meaningless, "(Cns)" contains both (StemCns:) and (:StemCns).
! Think through this.


"Gradation: Clusters ldnj, vdnj etc."
  d:n <=> [ l | v ] _ n j Vow ( StemCns:) (:StemCns) WeG: ;

"Gradation: Clusters rbm, rdn rgn1"
  Cx:Cy <=> r _ Cz Vow ( StemCns:) (:StemCns) WeG: ;
			where Cx in (b d g)
				Cy in (p t k)
				Cz in (m n n1)
			matched ;

"Gradation: Clusters rdnj"
  Cx:Cy <=> r _ Cz j Vow ( StemCns:) (:StemCns) WeG: ;
			where Cx in (b d g)
				Cy in (p t k)
				Cz in (m n n1)
			matched ;

"Gradation: ist, vsk etc."
  ':Cx <=> Vow: [ j: | v | l | r | m | n ] s _ Cy Vow ( StemCns:) (:StemCns) WeG: ;
			where Cx in (k t)
				Cy in (k t)
			matched ;

"Gradation: ms1k, rs1k"
  ':k <=> Vow: [ m | r ] s1 _ k Vow ( StemCns:) (:StemCns) WeG: ;

"Gradation: Clusters kc ks etc. 1"
  k:v <=> Vow: _ ': Cx Vow ( StemCns:) (:StemCns) WeG: ;
			where Cx in (t c c1 s s1) ;

"Gradation: Clusters kc ks etc. 2"
  ':Cx <=> Vow: k: _ Cy Vow ( StemCns:) (:StemCns) WeG: ;
			where Cx in (t c c1 s s1)
				Cy in (t c c1 s s1)
			matched ;

"Gradation: Cluster kst 1"
  k:v <=> Vow: _ s ': t Vow ( StemCns:) (:StemCns) WeG: ;

"Gradation: Cluster kst 2"
  ':t <=> Vow k: s _ t Vow: ( StemCns:) (:StemCns) WeG: ;

"Gradation: bb dd etc.1"
  Cx:Cy <=> Vow: _ Cz: Vow ( StemCns:) (:StemCns) WeG: ;
			where Cx in (b d g z z1)
				Cy in (p t k c c1)
				Cz in (b d g z z1)
			matched ;

"Gradation: bb dd etc.2"
  Cx:Cy <=> :Cy _ Vow ;
			where Cx in (b d g z z1)
				Cy in (p t k c c1)
			matched ;

"Gradation: dn to tn, etc."
  Cx:Cy <=> Vow: _ Cz: Vow ( StemCns:) (:StemCns) WeG: ;
			where Cx in (b d g )
				Cy in (p t k )
				Cz in (m n n1 )
			matched ;

"Gradation: dnj to tnj"
  d:t <=> Vow: _ n j Vow ( StemCns:) (:StemCns) WeG: ;
			

! Lengthening of consonants
! -------------------------

"Lengthening of Central Consonants hk etc."
  ':Cy <=> h: _ Cz Vow ( CntrCns:) Len: ;
			where Cy in (p t k c c1)
				Cz in (p t k c c1)
			matched ;

"Lengthening of Central Consonants pm etc."
  Cx:Cy <=> Vow: _ Cz Vow ( CntrCns:) Len: ;
			where Cx in (p t k)
				Cy in (b d g)
				Cz in (m n n1)
			matched ;

"Lengthening of Central Consonants tnj"
  t:d <=> Vow: _ n j Vow ( CntrCns:) Len: ;

"Lengthening of Central Consonants dj"
  ':d <=> Vow: d _ j Vow ( CntrCns:) Len: ;



! Lenition
! --------

"Lenition: Intervocalic Stops and Affricates"
  Cx:Cy <=> h:0 ':0 _ Vow ;
			where Cx in (k p c c1 t)
				Cy in (g b z z1 d1)
			matched ;


! Vowel Shortening
! ----------------


"Vowel Shortening in Bisyllabic Sg2 Imperatives and Connegatives" !c1uorvo
  Vx:Vy <=> Cns _ Q6: ;
			where Vx in (u i)
				Vy in (o e)
			matched ;

"Vowel Shortening in Compounds after Long 1st Syllable 1"
  Vx:Vy <=> [ Vow Vow | á ] Cns:+ Cns: _ StemCns:* X7: ;
			where Vx in (u i)
				Vy in (o e)
			matched ;
! Vx:Vy <=> [ Vow Vow | á ] Cns:+ Cns _ StemCns:* [ X7: | X8: ] ;
! the problem with this one was that it was not restricted to compounds !!
! The X7: | X8: line was commented out since no lexicon in sme-lex.txt
! pointed from X8 to R. This move should be checked for compounds later.

!"Vowel Shortening in Compounds after Long 1st Syllable 2"
!  Vx:Vy <=> [ #: | Cns ] Vz Cns+ Cns: Cns+ _ StemCns:* [ X7: | X8: ] ;
!			where Vz in (a i o u) ;
!			where Vx in (u i)
!				Vy in (o e)
!			matched ;
! XXX Remainder! This rule is commented out since including it
! spoils the i:á alternation of verbs (boahtit:boad1án, for some reason.
! This should be looked into.

"Vowel Shortening in Compounds of Contract Stems"
  Vx:Vy <=> [ Vow Vow | á ] Cns:+ Cns: _ StemCns:* X4: X7: ;
			where Vx in (u i)
				Vy in (o e)
			matched ;
! To account for boazoviessu, the u:o alternation does not work above


"Vowel lowering for buorre"
 Vx:Vy <=> [ Vow Vow | á ] Cns:+ Cns _ X8: ;
		       where Vx in (u i)
			     Vy in (o e)
			     matched ;
! This rule was actually made for one word only, buorre:buoret
! It replaces the missing "| X8:" sequence that is commented out in the
! "Vowel shortening ... 1" rule above. Why X8: does not work there is unclear.
! This rule should in any case


"Optional Vowel Shortening after Short 1st Syllable"
  Vx:Vy => [ #: | Cns ] [a|i|o|u] [ Cns: Cns | h: ': Cns: ] 
		_ StemCns:* [ X7: | X8: ] ;
			where Vx in (u i)
				Vy in (o e)
			matched ;


"Optional Vowel Shortening in Caritives"
  Vx:Vy => [ #: | Cns ] [a|i|o|u] Cns:+ _ ( DelCns:) X9: ;
			where Vx in (u i)
				Vy in (o e)
			matched ;

"Optional Shortening of á in Compounds after Long 1st Syllable 1"
  á:a => [ Vow Vow | á ] Cns:+ Cns _ StemCns:* [ X7: | X8: ] ;


"Optional Shortening of á in Compounds after Long 1st Syllable 2"
  á:a => [ #: | Cns ] Vz Cns+ Cns: Cns+ _ StemCns:* [ X7: | X8: ] ;
			where Vz in (a i o u) ;

"Vowel Shortening in Caritives"
  Vx:Vy <=> [ Vow Vow | á ] Cns:+ _ ( DelCns:) X9: ;
			where Vx in (u i)
				Vy in (o e)
			matched ;

"Vowel Shortening in Attributive Forms, V-final Adjectives"
  Vx:Vy <=> Cns:+ _  [ W1: | W2: ] s #: ;
			where Vx in (u i)
				Vy in (o e)
			matched ;
                        ! W1 for WeG Attr, W2 for non-CG Attr form.

"Vowel Shortening in Attributive Forms, s-final Adjectives"
  Vx:Vy <=> Cns:+ _  s W1: #: ;
			where Vx in (u i)
				Vy in (o e)
			matched ;
! Tentative rule.


"Allegro Forms in Compounds"
 Vx:Vy <=> Cns:+ _ X7: ;
			where Vx in (i u á )
			      Vy in (e o a )
			      matched ;
! Overlapping => constraint wrt. 'i:e', Optional Vow Shortening
! After short 1st Syllable

! Diphtong simplification
! -----------------------

"Diphthong Simplification in i-Stems before Suffixes Beginning with j:"
  Vx:0 <=> Vow _ Cns:+ [ i: | e1: ] X5: ;
			where Vx in (e o a) ;

"Diphthong Simplification in Actor Nouns from i- and a-Stems (Optional)"
  Vx:0 => Vow _ Cns:+ [ i: | a: ] ¤: X5: ;
			where Vx in (e o a) ;

"Diphthong Simplification in Certain Forms from u-Stems"
  Vx:0 <=> Vow _ Cns:+ [ u: | o1: ] [ X2: | X3: | ( DelCns:) X6: | Q8: | Y5: | Y6: ] ;
			where Vx in (e o a) ;

"Diphthong Simplification in Actor Nouns from u-Verbs (Optional)"
  Vx:0 => Vow _ Cns:+ u: ¤: [ X2: | X3: | X6: ] ;
			where Vx in (e o a) ;

"Diphthong Simplification in Diminutives etc. from Trisyllabics with 2nd Syll u/o"
  Vx:0 <=> Vow _ Cns:+ [ u: | o1: ] ( NonMPDummy:) Cns:+ X2: ;
			where Vx in (e o a) ;

"Diphthong Simplification: Conditional in -alla- etc. Derivatives from Trisyllabics"
  Vx:0 <=> Vow _ Cns:+ u:o ( NonMPDummy:) Cns:+ X1: ;
			where Vx in (e o a) ;

"Diphthong Simplification in IllSg and Certain Conjug. Forms of a-Stems"
  Vx:0 <=> Vow _ Cns:+ a: [ X3: | Q2: | Q3: | Y7: ] ;
			where Vx in (e o a) ;

"Diphthong Simplification in Certain Conjugational Forms of i- and u-Stems"
  Vx:0 <=> Vow _ Cns:+ HighVow: [ Q2: | Q3: | Q7: | Y7: ] ;
			where Vx in (e o a) ;

"Diphthong Simplification: Optional before Deverbal -alla-, -adda-, -us"
  Vx:0 => Vow _ Cns:+ o1: ( NonMPDummy:) Cns:+ X1: ;
			where Vx in (e o a) ;

"Diphthong Simplification in u-Stems before Conditional Marker"
  Vx:0 => Vow _ Cns:+ u: Q5: ;
			where Vx in (a e o) ;

"Dipthtong simplification in the attribute forms of AD-adjectives"
  Vx:0 <=> Vow _ Cns:+ Y9: ;
		       where Vx in (a e o) ;


! Vowel Rising
! ------------

"Vowel Rising"
  Vx:Vy <=> Vow: Cns:+ _ ( CntrCns:0 ) ( Dummy:+) j:i ;
			where Vx in (e o)
				Vy in (i u)
			matched ;


! Metaphony 
! ---------

"General Metaphony"
  Vx:Vy <=> Cns _ Cns:* [ X2: | X6: ] ;
			where Vx in (i e1 u)
				Vy in (á á o)
			matched ;

"Metaphony in Derivatives -us, -alla, -adda 1"
  u:o <=> Cns _ ( NonMPDummy2:) Cns:+ X1: ;
! Here I have a new set, NonMPDummy2, without X7, to avoid clash with
! the allegro shortening rule. Check for alternative solutions !!!

"Metaphony in Derivatives -us, -alla, -adda 2"
  Vx:á <=> Cns _ ( NonMPDummy2:) Cns:+ X1: ;
			where Vx in (i e1) ;

"Illative Singular Metaphony"
  Vx:Vy <=> Cns _ X3: ;
			where Vx in (i a e1)
				Vy in (á i á)
			matched ;

"Metaphony in Actor Nouns of i-, a-, and u-Verbs"
  Vx:Vy <=> Cns _ ¤: [ X2: | X3: | X6: ] ;
			where Vx in (i a u)
				Vy in (á á o)
			matched ;

"Metaphony in MUSH-Derivatives"
  i:á => Cns _ m: u: s1: s1: ;

"Potential Marker Metaphony"
  Vx:Vy <=> Cns _ Q7: ;
			where Vx in (i u)
				Vy in (e o)
			matched ;


! Stem vowel alternations
! -----------------------

"Stem Vowel in Contracted Stems"
  Vx:Vy <=> Cns _ ( CntrCns:) Y5: ;
			where Vx in (i a u)
				Vy in (á á o)
			matched ;

"Stem Vowel before Frequentative d"
  Vx:Vy <=> Cns _ Q8: ;
			where Vx in (i u)
				Vy in (a o)
			matched ;

"Stem Vowel before Frequentative l"
  i:a <=> Cns _ Q9: ;

"Stem Vowel before Reflexive s"
  Vx:Vy <=> Cns _ Y8: ;
			where Vx in (i u)
				Vy in (á a)
			matched ;

"Indicative Present Singular 3rd Final Vowel"
  Vx:á <=> Cns _ Q1: ;
			where Vx in (a i) ;

"Imperative Present 1st/3rd Person and Connegative Final Vowel"
  Vx:o <=> Cns _ Q3: ;
			where Vx in (a i u) ;

"Stem Vowel Shift a:e, i:e, u:o"
  Vx:Vy <=> Cns _ Q2: ;
			where Vx in (a i u)
				Vy in (e e o)
			matched ;

"Stem Vowel in 1st and 2nd Person Singular Present"
  i:á <=> Cns _ Q4: ;

"Stem Vowel before Conditional Marker"
  Vx:Vy <=> Cns _ Q5: ;
			where Vx in (u i)
				Vy in (o á)
			matched ;

"Stem Vowel in Dual 2nd Person Imperative"
  a:i <=> Cns _ Y1: ;

"Stem Vowel in Present Participials from i-Verbs"
  a:i <=> Cns _ ¤: [ X1: | X4: | X5: | X7: | X8: | #: ] ;

"Stem Vowel in Dual 1st Person Imperative"
  Vx:u <=> Cns _ Y2: ;
			where Vx in (a i) ;

"Stem Vowel in Plural 2nd Person Imperative"
  Vx:Vy <=> Cns _ Y3: ;
			where Vx in (a i u)
				Vy in (e e o)
			matched ;

"Stem Vowel in Imperative Plural 1st Person and Passive Stem"
  Vx:o <=> Cns _ [ Y4: | Y7: ] ;
			where Vx in (a i u) matched ;


! Special word final consonants
! -----------------------------

"Word Final Deletion of n2 m2 g2 h2"
  Cx:0 <=> Vow _ [ X4: | X8: | X6: Cns: | X9: ] ;
			where Cx in (DelCns) matched ;


"Word Final Neutralization of g2, h2, m2"
  Cx:Cy <=> Vow _ [ X2: | X7: ] #: ;
			where Cx in (g2 h2 m2)
				Cy in (t t n)
			matched ;
