! Divvun & Giellatekno - open source grammars for Sámi and other languages
! Copyright © 2000-2010 The University of Tromsø & the Norwegian Sámi Parliament
! http://giellatekno.uit.no & http://divvun.no
!
! This program is free software; you can redistribute and/or modify
! this file under the terms of the GNU General Public License as published by
! the Free Software Foundation, either version 3 of the License, or
! (at your option) any later version. The GNU General Public License
! is found at http://www.gnu.org/licenses/gpl.html. It is
! also available in the file $GTHOME/LICENSE.txt.
!
! Other licensing options are available upon request, please contact
! giellatekno@uit.no or feedback@divvun.no


!! !!!North Saami acronyms - affix part

!! !!The lexica giving tags and suffixes to the acronyms

LEXICON ACRONOUN_cons  
 ACRONOUN ;

LEXICON ACRONOUN_vow  
 ACRONOUN ;


LEXICON ACRONOUN   !!= * __@CODE@__ is the lexicon for __nouns__ (not +Prop) like ATV
 +N+ACR: acroconnector ;    
 
LEXICON UNIT   !!= * __@CODE@__  As acro, but without paradigm
+N+ABBR: ENDLEX ;

LEXICON ACRO_ACCRA  !!= * __@CODE@__ 
+N+Prop+ACR: acroconnector ;
+N+Prop:%> ACCRADECL ;

LEXICON ACRO_BERN
+N+Prop+ACR: acroconnector ;
+N+Prop:%> BERN-UCASE ;

LEXICON ACRO_LONDON
+N+Prop+ACR: acroconnector ;
+N+Prop:%> LONDON-UCASE ;

LEXICON ACRO_NYSTØ
+N+Prop+ACR: acroconnector ;
+N+Prop:%> NYSTØ-OBL ;

LEXICON ACRO_cons 
 ACRO ;    

LEXICON ACRO_vow 
 ACRO ;    


LEXICON ACRO 
 +N+Prop+ACR: acroconnector ;    

LEXICON DYN_ACRO_cons 
 DYN_ACRO ;    

LEXICON DYN_ACRO_vow 
 DYN_ACRO ;    

LEXICON DYN_ACRO
 +N+Prop+Sem/Org+ACR+Dyn: acroconnector ;
 
LEXICON acroconnector   !!= * __@CODE@__ Here comes a set of possible symbols to
                        !! put between the abbreviation and its suffix
                acronull    ;
          :%>%: acrooblique ; ! NRK:as
 +Err/Orth:     acrooblique ; ! NRKas
 +Err/Orth:%>'7 acrooblique ; ! NRK's
 +Err/Orth:%>%` acrooblique ; ! NRK`s
 +Err/Orth:%>%´ acrooblique ; ! NRK´s
 +Err/Orth:%>%- acrooblique ; ! NRK-s
                RHyph       ; ! Here we dive over to the compounds: NRK-2, NRK-bargi
 +Err/Orth:     Rreal       ; ! Without hyphen, not normative:  NRK23

LEXICON acronull   !!= * __@CODE@__  for suffixless forms, redirecting to K_only for clitic forms
 +Sg+Nom+Err/Orth: K_only ;
 +Sg+Gen+Err/Orth: K_only ;
 +Sg+Acc+Err/Orth: K_only ;
          +Sg+Nom: ENDLEX ;
          +Sg+Gen: ENDLEX ;
          +Sg+Acc: ENDLEX ;

LEXICON acrooblique   !!= * __@CODE@__
  +Sg+Gen+Err/Orth:a      K      ;
  +Sg+Acc+Err/Orth:a      K      ;
  +Sg+Ill+Err/Orth:ij     K      ;
! +Sg+Ill+Err/Orth:i      K      ; ! NSR:i
! +Sg+Loc+Err/Orth:s      K      ; ! NRK:s
  +Sg+Loc+Err/Orth:as     K      ; ! NRK:as
! +Sg+Com+Err/Orth:in     K      ;
  +Sg+Com+Err/Orth:iin    K      ; ! NRK:iin
!    +Ess+Err/Orth:n      K      ; ! NBR:n
     +Ess+Err/Orth:an     K      ; ! NBR:an ! (check!)
  +Pl+Nom+Err/Orth:at     K      ;
  +Pl+Gen+Err/Orth:ajd    K      ;
  +Pl+Acc+Err/Orth:ajd    K      ;
  +Pl+Ill+Err/Orth:ajde   K      ;
  +Pl+Loc+Err/Orth:ajn    K      ;
  +Sg+Com+Err/Orth:ajn    K      ;
  +Pl+Com+Err/Orth:ajgujn K      ;
! +Pl+Nom+Err/Orth:t      K      ;
! +Pl+Gen+Err/Orth:id9    K      ;
! +Pl+Acc+Err/Orth:id9    K      ;
! +Pl+Ill+Err/Orth:ide    K      ;
! +Pl+Loc+Err/Orth:in     K      ;
! +Pl+Com+Err/Orth:igujn  K      ;
  +Sg+Gen+Err/Orth:a      ENDLEX ;
  +Sg+Acc+Err/Orth:a      ENDLEX ;
  +Sg+Ill+Err/Orth:ij     ENDLEX ;
           +Sg+Ill:i      ENDLEX ; ! NSR:i
           +Sg+Loc:s      ENDLEX ; ! NRK:s
  +Sg+Loc+Err/Orth:as     ENDLEX ; ! NRK:as
           +Sg+Com:in     ENDLEX ;
              +Ess:n      ENDLEX ; ! NBR:n
     +Ess+Err/Orth:an     ENDLEX ; ! NBR:an ! (check!)
  +Pl+Nom+Err/Orth:at     ENDLEX ;
  +Pl+Gen+Err/Orth:ajd    ENDLEX ;
  +Pl+Acc+Err/Orth:ajd    ENDLEX ;
  +Pl+Ill+Err/Orth:ajde   ENDLEX ;
  +Pl+Loc+Err/Orth:ajn    ENDLEX ;
  +Sg+Com+Err/Orth:ajn    ENDLEX ;
  +Pl+Com+Err/Orth:ajgujn ENDLEX ;
           +Pl+Nom:t      ENDLEX ;
           +Pl+Gen:id9    ENDLEX ;
           +Pl+Acc:id9    ENDLEX ;
           +Pl+Ill:ide    ENDLEX ;
           +Pl+Loc:in     ENDLEX ;
           +Pl+Com:igujn  ENDLEX ;

!------Der/ár------
 +Der1+Der/ár+N+CmpN/SgN+CmpN/SgG+CmpN/PlG+Use/-Spell:»ár GAHPIRLONG ;  !
