!! Divvun & Giellatekno - open source grammars for Sámi and other languages
! Copyright © 2000-2010 The University of Tromsø & the Norwegian Sámi Parliament
! http://giellatekno.uit.no & http://divvun.no
!
! This program is free software; you can redistribute and/or modify
! this file under the terms of the GNU General Public License as published by
! the Free Software Foundation, either version 3 of the License, or
! (at your option) any later version. The GNU General Public License
! is found at http://www.gnu.org/licenses/gpl.html. It is
! also available in the file $GTHOME/LICENSE.txt.
!
! Other licensing options are available upon request, please contact
! giellatekno@hum.uit.no or divvun@samediggi.no

! ==========================================================================
!                     North Sámi morphological analyser                    
! ==========================================================================


! -------------------
!! !!!North Saami Possessive suffixes
! -------------------

LEXICON PxVvowch   !!= * __@CODE@__ for vowel stems, with X2, X1 with stem vowel change
 Px1Vvowch ;
 Px2Vvowch ;
 Px3Vvowch ;

LEXICON Px1Vvowch   !!= * __@CODE@__ for vowel stems with stem vowel change, 1. p
 :X2 Px1V ;
+Err/Sub: Px1V ;
 
LEXICON Px2Vvowch   !!= * __@CODE@__ for vowel stems with stem vowel change, 1. and 2. p
 :X2 Px2V ;
+Err/Sub: Px2V ;

LEXICON Px3Vvowch   !!= * __@CODE@__ for vowel stems with stem vowel change, 3. p
 :X1 Px3V ;


LEXICON PxV   !!= * __@CODE@__ for vowel stems, without stem vowel change
 Px1V ;
 Px2V ;
 Px3V ;

LEXICON Px1V   !!= * __@CODE@__ for first person vowel stems
 +PxSg1:n K ;
 +PxSg1+Err/Sub:m9 K ; ! !SOUTH
 +PxDu1:me K ;
 +PxPl1:met K ;


LEXICON Px2V   !!= * __@CODE@__ for second person vowel stems
 +PxSg2:t K ;
 +PxDu2:de K ;
 +PxPl2:det K ;

LEXICON Px3V   !!= * __@CODE@__ for third person vowel stems
 +PxSg3:s K ;
 +PxDu3:ska K ;
 +PxDu3+Err/Sub:skka K ;
 +PxPl3:set K ;


!LEXICON PxSgV   !!= * __@CODE@__ Sg only
! +PxSg1:n K;
! +PxSg1:m9 K; !SUB !SOUTH
! +PxSg2:t K;
! +PxSg3:s K;

LEXICON PxA   !!= * __@CODE@__ for a-stems
! symposiaskka
Px1A ;
Px2A ;
Px3A ;


LEXICON Px1A   !!= * __@CODE@__ for a-stems
! symposiaskka
 +PxSg1:n K ;
 +PxSg1+Err/Sub:m9 K ; ! !SOUTH
 +PxDu1:me K ;
 +PxPl1:met K ;


LEXICON Px2A   !!= * __@CODE@__ for a-stems
! symposiaskka
 +PxSg2:t K ;
 +PxDu2:tte K ;
 +PxPl2:ttet K ;

LEXICON Px3A   !!= * __@CODE@__ for a-stems
! symposiaskka
 +PxSg3:s K ;
 +PxDu3:skka K ;
 +PxDu3+Err/Sub:skka K ;
 +PxPl3:set K ;


LEXICON PxC   !!= * __@CODE@__ for consonant stems
Px1C ;
Px2C ;
Px3C ;

LEXICON Px1C   !!= * __@CODE@__ for consonant stems
 +PxSg1:an K ;
 +PxSg1+Err/Sub:am9 K ; ! !SOUTH
 +PxDu1:eame K ;
 +PxPl1:eamet K ;


LEXICON Px2C   !!= * __@CODE@__ for consonant stems
 +PxSg2:at K ;
 +PxDu2:eatte K ;
 +PxPl2:eattet K ;

LEXICON Px3C   !!= * __@CODE@__ for consonant stems
 +PxSg3:is K ;
 +PxDu3:easkka K ;
 +PxDu3+Err/Sub:easka K ;
 +PxPl3:easet K ;


LEXICON PxPlComC   !!= * __@CODE@__ for plural comitative forms of consonant stems
 +PxSg1:angujn K ;
 +PxSg2:atgujn K ;
 +PxSg3:isgujn K ;
 +PxDu1:eamegujn K ;
 +PxDu2:eattegujn K ;
 +PxDu3:easkkagujn K ;
 +PxPl1:eametgujn K ;
 +PxPl2:eattetgujn K ;
 +PxPl3:easetgujn K ;

LEXICON PxPlComV1   !!= * __@CODE@__ for first person vowel stems with vow change, directing onw
 :X2 PxPlCom12V ;
 :X1 PxPlCom3V ;

LEXICON PxPlCom12V   !!= * __@CODE@__ for first, second person comitative Px
 +PxSg1:ngujn K ;
 +PxSg2:tgujn K ;
 +PxDu1:megujn K ;
 +PxDu2:degujn K ;
 +PxPl1:metgujn K ;
 +PxPl2:detgujn K ;

LEXICON PxPlCom3V   !!= * __@CODE@__ for third person comitative Px
 +PxSg3:sgujn K ;
 +PxDu3:skagujn K ;
 +PxPl3:setgujn K ;

