! Divvun & Giellatekno - open source grammars for Sámi and other languages
! Copyright © 2000-2010 The University of Tromsø & the Norwegian Sámi Parliament
! http://giellatekno.uit.no & http://divvun.no
!
! This program is free software; you can redistribute and/or modify
! this file under the terms of the GNU General Public License as published by
! the Free Software Foundation, either version 3 of the License, or
! (at your option) any later version. The GNU General Public License
! is found at http://www.gnu.org/licenses/gpl.html. It is
! also available in the file $GTHOME/LICENSE.txt.
!
! Other licensing options are available upon request, please contact
! giellatekno@hum.uit.no or divvun@samediggi.no

! ############################## !
!This file contains the Pronouns !
! ############################## !



! Pronoun
! =======

!Pointing to all the subgrops, presented in this order below.
LEXICON Pronoun
Personal ;
Interrogative ;
Relative ;
Demonstrative ;
Reflexive ;
Reciprocal ;
Indefinite ;


! Personal pronouns
! -----------------

!Personal pronouns, splitting in 1st, 2nd, 3rd
LEXICON Personal
mun+Pron+Pers:m MUNLEX ;
don+Pron+Pers:d DONLEX ;
son+Pron+Pers:s SONLEX ;
!dat+Pron+Pers:da datpers ; ! Dat lea ruovttus.
!dat+Pron+Pers+Pl3+Use/Sub:de obldemcaspl ; ! Dat lea ruovttus.
!dat+Pron+Pers+Sg3+Com+Use/Sub:dejnna K ;

! Interrogative pronouns
! ----------------------

!Giving ideosyncratic Sg Nom of gii, mii lexically
!Sending the oblique forms of gii, mii to an oblique sublexicon
!Giving the stem of guhte, guhtemuš, goabbá
LEXICON Interrogative
mii+Pron+Interr: MIIINTERLEX ;
gii+Pron+Interr: GIIINTERLEX ;
guhte+Pron+Interr: GUHTELEX ;
guhtemuš+Pron+Interr: GUHTEMUSLEX ;
goabbá+Pron+Interr:goabbá GUHTECASE ;
goabbá+Pron+Interr+Use/Sub:goabba GUHTECASE ;
makkár+Pron+Interr:makkár MAKKARInterr ;
man% láhkái+Pron+Interr+Sg+Com+Use/Sub:mainna#lágiin K-son ; !


! Relative pronouns
! -----------------

LEXICON Relative
mii+Pron+Rel: MIIINTERLEX ;
gii+Pron+Rel: GIIINTERLEX ;
guhte+Pron+Rel: GUHTELEX ;
guhtemuš+Pron+Rel: GUHTEMUSLEX ;
goabbá+Pron+Rel:goabbá GUHTECASE ;
man% láhkái+Pron+Rel+Sg+Com+Use/Sub:mainna#lági%>in K-son ;
man% láhkái+MWE+Pron+Rel+Sg+Com:mainna% lági%>in K-son ;


! Demonstrative pronouns
! ----------------------

!Giving baseform + all demonstrative stems
!Pointing to case paradigms

LEXICON Demonstrative
dat+Pron+Dem:da%> demcas ;
dat+Pron+Dem+Use/Sub:de%> demcas ;
die-dat+Pron+Dem:die-da%> demcas ;
diet+Pron+Dem:die%> demcas ;
die-diet+Pron+Dem:die-die%> demcas ;
dát+Pron+Dem:dá%> demcas ;
dá-dát+Pron+Dem:dá-dá%> demcas ;
duot+Pron+Dem:duo%> demcas ;
duo-duot+Pron+Dem:duo-duo%> demcas ;
dot+Pron+Dem:do%> demcas ;
do-dot+Pron+Dem:do-do%> demcas ;
dot+Pron+Dem+Use/Sub:doo demcas ; !

dahtanaga+Pron+Dem:da%> Dahtanaga-par ;
dakkár+Pron+Dem:dakkár DAKKAR ;
dákkár+Pron+Dem+Use/Sub:dákkar DAKKAR ; 
dakkár+Pron+Dem+Use/Sub:dakkar DAKKAR ; 
dákkár+Pron+Dem:dákkár DAKKAR ;
diekkár+Pron+Dem:diekkár DAKKAR ;
duokkár+Pron+Dem:duokkár DAKKAR ;
dokkár+Pron+Dem:dokkár DAKKAR ;

dakkáraš+Pron+Dem:dakkáražž DAKKARAS ;
dákkáraš+Pron+Dem:dákkáražž DAKKARAS ;
diekkáraš+Pron+Dem:diekkáražž DAKKARAS ;
duokkáraš+Pron+Dem:duokkáražž DAKKARAS ;
dokkáraš+Pron+Dem:dokkáražž DAKKARAS ;
makkáraš+Pron+Dem:makkáražž DAKKARAS ;



! Reflexive pronouns
! ------------------

!Two nominative reflexives, and pointer to the rest
!The Pl one is used for Du as well, here given two entries.
!Should one of them be removed?

LEXICON Reflexive
ieš+Pron+Refl: IESLEX ;
jieš+Pron+Refl: JIESLEX ;


! Reciprocal pronouns
! -------------------

! The first 4 entries handle the first element of the recipr.
! The next 12 handle the 2nd part of the non-Px recipr.
! The members of the third section  point to Px lexica.

LEXICON Reciprocal
goabbat+Pron+Recipr+Use/-Spell: GOABBATLEX ;
guoibmi+Pron+Recipr+Use/-Spell: GUOIBMILEX ;
nubbi+Pron+Recipr+Pl: NUBBILEX ;
goabbat% guoibmi+Pron+Recipr: GOABBATGUOIBMILEX ;
goappat% guoibmi+Pron+Recipr: GOAPPATGUOIBMILEX ;
guhtet% guoibmi+Pron+Recipr: GUHTETGUOIBMILEX ;
guđet% guoibmi+Pron+Recipr: GUDETGUOIBMILEX ;

! Indefinite pronouns
! -------------------

! Dividing the indefinites in three groups
LEXICON Indefinite
declindef-cl ;
declindef ;
declindef-idiosync ;
indeclindef ;
!+Use/Sub: indefmulti ; ! feara mii

! Declineable indefinite pronouns with case + clitic
! - - - - - - - - - - - - - - - - - - - - - - - - -

LEXICON declindef-cl


dahtai+Pron+Indef+Sg+Nom:daht%>ai # ;
giiat+Pron+Indef+Sg+Nom:gii%>at # ;
giige+Pron+Indef+Sg+Nom:gii%>ge # ;
giige+Pron+Indef+Pl+Nom:geat%>ge # ;
giige+Pron+Indef+Sg+Nom:gii%>ge # ;
giige+Pron+Indef:gea%> oblindefcasge ;
giijai+Pron+Indef+Sg+Nom:gii%>jai # ;
giinai+Pron+Indef+Sg+Nom:gii%>nai # ;
giinai+Pron+Indef:gea%> oblindefcas-giinai ;
goabbáge+Pron+Indef:goabbá GUHTEGECASE ;
goabbánai+Pron+Indef:goabbá GUHTENAI ;
guhtege+Pron+Indef:guh'te GUHTEGECASE ;
guhtemušge+Pron+Indef:guhtemuš%>ge # ; !deviating Nom altarnant.
guhtemušge+Pron+Indef:guđemužž GUHTEMUSGECASE ;
guhtenai+Pron+Indef:guh'te GUHTENAI ;
iešgiige+Pron+Indef+Sg+Nom:ieš#gii%>ge # ;
iešgiige+Pron+Indef:ieš#gea%> oblindefcas ;
iešgiijai+Pron+Indef+Sg+Nom:ieš#gii%>jai # ;
iešgiinai+Pron+Indef+Sg+Nom:ieš#gii%>nai # ;
iešgiinai+Pron+Indef:ieš#gea%> oblindefcas-giinai ;
iešguhtege+Pron+Indef:ieš#guh'te GUHTEGECASE ;
iešguhtege+Pron+Indef+Use/Sub:ieš#guh'tet9 GUHTEGECASE ;
iešguhtenai+Pron+Indef:ieš#guh'te GUHTENAI ;
mihkkege+v1+Pron+Indef+Sg+Nom:mihkke%>ge # ;
mihkkege+v1+Pron+Indef:ma%> oblindefcasge-mihkkege ;
mihkkege+Pron+Indef+Sg+Nom+Use/Sub:mihkke%>t # ; !
mihkkege+v2+Pron+Indef+Sg+Nom:mihkki%>ge # ;
mihkkege+v2+Pron+Indef:ma%> oblindefcasge-mihkkige ;
mihkke+Pron+Indef+Sg+Nom:mihkke%> # ;
mihkke+Pron+Indef+Sg+Acc:ma%jde # ;
!mihkke+Pron+Indef+Pl+Acc:ma%jde # ;
miijai+Pron+Indef+Sg+Nom:mii%>jai # ;
miinai+Pron+Indef+Sg+Nom:mii%>nai # ;
miinai+Pron+Indef:ma%> oblindefcas-miinai ;
miige+Pron+Indef+Sg+Nom:mii%>ge # ;

!The difference between mihkkege and mihkkige
!continues through declination (maidege-maidige etc.).
!Should they be separated in different lexicons to make it logical? Yes - done



! Declineable indefinites with normal case paradigms
! - - - - - - - - - - - - - - - - - - - - - - - - - -

LEXICON declindef

eará+Pron+Indef:eará%>               indef-a-even-v ;
iežá+Pron+Indef:iežá%>                   indef-a-even-v ;
uhccánaš+Pron+Indef:uhccánažž       indef-a-even-c ;
unnánaš+Pron+Indef:unnánažž           indef-a-even-c ;

eanas+Pron+Indef:eatnas%>           indef-a-uneven ;
eanaš+Pron+Indef:eatnaš%>               indef-a-uneven ;

muhtin+Pron+Indef:muhtim%>           indef-a-uneven-gahpir ;
muhtun+Pron+Indef:muhtum%>           indef-a-uneven-gahpir ;
muhtun+Pron+Indef+Use/Sub:muhtom%>           indef-a-uneven-gahpir ; !

!muhtin+Pron+Indef+Sg+Gen+Use/-Spell:muhtin»#       NAMAT ;
!muhtun+Pron+Indef+Sg+Gen+Use/-Spell:muhtun»#       NAMAT ;
!muhtin+Pron+Indef+Pl+Gen+Use/-Spell:muhtim%>ijd9»#       NAMAT ;
!muhtun+Pron+Indef+Pl+Gen+Use/-Spell:muhtum%>ijd9»#       NAMAT ;
muhtin+Pron+Indef+Pl+Loc+Use/Sub:muhttuin K ; !


uhccán+Pron+Indef:uhccán%>       indef-a-uneven-gahpir ;

unnán+Pron+Indef:unnán%>           indef-a-uneven-gahpir ;
veháš+Pron+Indef:veháž%>           indef-a-uneven-gahpir ;
váháš+Pron+Indef:váháž%>           indef-a-uneven-gahpir ;
veaháš+Pron+Indef:veaháž%>           indef-a-uneven-gahpir ;
veaháš+Pron+Indef+Use/Sub:veahaž%>           indef-a-uneven-gahpir ;

soames+Pron+Indef+Sg+Nom:soames%>   K ;
soames+Pron+Indef+Attr:soames%>   K ;
!soames+Pron+Indef+Sg+Nom+Use/-Spell:soames%>#   NAMAT ;
soames+Pron+Indef+Ess:soapmás%>in   K ;
soames+Pron+Indef:soapmás%>          indefattr ;
soames+Pron+Indef:soapmás%>X2      indefdeclsguneven-ag ;
soames+Pron+Indef:soapmás%>          indefdeclsguneven-ilc ;
soames+Pron+Indef:soapmás%>X2      indefdeclpluneven-n ;
soames+Pron+Indef:soapmás%>i          indefdeclpluneven-obl ;

nuppát+Pron+Indef:nuppád%>              indefsg-a-uneven ;

duobbá+Pron+Indef:duobbá%>              JUOBBA ;
dábbá+Pron+Indef:dábbá%>              JUOBBA ;
juobbá+Pron+Indef:juobbá%>              JUOBBA ;
juobbágoabbá+Pron+Indef: JUOBBAGOABBA ;

nubbi+Pron+Indef:nubbi%>             nubbicase ;
nubbi+Pron+Recipr:nubbi%>             nubbicase ;
nubbi+Pron+Attr:nubbe # ;

juohkehaš+Pron+Indef:juohkehažž         indef-a-even-c ;

ollut+Pron+Indef:ollu%>           indefdeclpleven ;
olut+Pron+Indef:olu%>           indefdeclpleven ;
moanat+Pron+Indef:moana%>           indefdeclpleven ;
gallát+Pron+Indef+Attr:gallát%>       K ;
buohkat+Pron+Indef:buohka%>       indefdeclpleven ;
buotdivnnat+Pron+Indef:buot#divnna%> indefdeclpleven ;
gaitdivnnat+Pron+Indef:gait#divnna%> indefdeclpleven ;
gaitdiinnat+Pron+Indef:gait#dijnna%> indefdeclpleven ;
gaittiinnat+Pron+Indef:gait#tijnna%> indefdeclpleven ;
gaikkat+Pron+Indef:gajkka%>       indefdeclpleven ;
goappašagat+Pron+Indef:goappašaga%>  indefdeclpleven ;
goappašagat+Pron+Indef+Use/Sub:goappešaga%>  indefdeclpleven ;

ollugat+v1+Pron+Indef:ollug%>       indu-pl-gahpir ;
ollugat+v2+Pron+Indef:oallug%>       indu-pl-gahpir ;
ollugat+v3+Pron+Indef:ollos%>       indu-pl-gahpir ;
ollugat+v4+Pron+Indef:olug%>           indu-pl-gahpir ;
gallis+Pron+Indef:gal'lis%>       indefdecluneven ;
olus+Pron+Indef:olus%>           indefdecluneven ;
olus+Pron+Indef+Use/Sub:ol'lus%>           indefdecluneven ; ! substandard
soamis+Pron+Indef:soamis%>       indefdecluneven ;
soamis+Pron+Indef+Pl+Nom+Use/Sub:soapmasat       K ;

eambbosat+Pron+Indef:eambbos%>       indu-pl-gahpir ;
eambbogat+Pron+Indef:eambbog%>       indu-pl-gahpir ;
guoktásat+Pron+Indef:guoktás%>       indu-pl-gahpir ;
goappašat+Pron+Indef:goappaš%>       indu-pl-goappas ;
!goappašat+Pron+Indef+Pl+Gen+Use/-Spell:goappaš%>ijd9»# NAMAT ;
goappašat+Pron+Indef+Use/Sub:goappeš%>        indu-pl-gahpir ;

eanebut+Pron+Indef+Pl+Nom:eaneb%>ut       K ;
eanebut+Pron+Indef:eaneb%>u       indefdeclpluneven-obl ;
eanemusat+Pron+Indef:eanemus%>X2       indefdeclpluneven-n ;
eanemusat+Pron+Indef:eanemus%>iX2       indefdeclpluneven-obl ;

eanebat+Pron+Indef+Nom:eaneb%>at       K ;
eanebat+Pron+Indef:eaneb%>i       indefdeclpluneven-obl ;

eatnagat+Pron+Indef:eatnag%>       indu-pl-gahpir ;
galládat+Pron+Indef:gallád%>       indu-pl-gahpir ;

makkárge+Pron+Indef:makkár MAKKARGEIndef ;
oktage+Pron+Indef+Sg+Nom:okta%>ge # ;
oktage+Pron+Indef+Sg+Nom:akta%>ge # ;
oktage+Pron+Indef+Attr:okta%>ge # ;
oktage+Pron+Indef+Sg+Acc:ovtta%>ge # ;
oktage+Pron+Indef+Sg+Gen:ovtta%>ge # ;
oktage+Pron+Indef+Attr:ovtta%>ge # ;
oktage+Pron+Indef+Sg+Ill:oktii%>ge # ;
oktage+Pron+Indef+Sg+Loc:ovttas%>ge # ;
oktage+Pron+Indef+Sg+Com:ovttain%>ge # ;
oktage+Pron+Indef+Attr:ovttain%>ge # ;
oktage+Pron+Indef+Sg+Ess:oktan%>ge # ;
oktage+Pron+Indef+Pl+Nom:ovttat%>ge # ;
oktage+Pron+Indef+Pl+Acc:ovttaid%>ge # ;
oktage+Pron+Indef+Pl+Gen:ovttaid%>ge # ;
oktage+Pron+Indef+Pl+Ill:ovttaide%>ge # ;
oktage+Pron+Indef+Pl+Loc:ovttain%>ge # ;

feara% mii+MWE+Pron+Indef+Sg+Nom:feara% mij K-son ;
feara% mii+MWE+Pron+Indef+Sg+Acc:feara% májd9 K-son ;
feara% mii+MWE+Pron+Indef+Pl+Acc:feara% májd9 K-son ;
feara% gii+MWE+Pron+Indef+Sg+Nom:feara% gij K-son ;
feara% mii+MWE+Pron+Indef:feara% ma oblintercas_indef ;
feara% gii+MWE+Pron+Indef:feara% gea oblintercas_indef ;
feara% makkár+MWE+Pron+Indef:feara% makkár DAKKAR ;   

mii+Pron+Indef+Sg+Nom:mij K-son ; 
mii+Pron+Indef+Sg+Acc:májd9 K-son ;
mii+Pron+Indef+Pl+Acc:májd9 K-son ;
mii+Pron+Indef+Sg+Acc:majd9 K-son ;
mii+Pron+Indef+Sg+Nom:mij K-son ; ! only to the right of vaikko, cf. CG.
gii+Pron+Indef+Sg+Nom:gij K-son ;
mii+Pron+Indef:ma oblintercas_indef ;
gii+Pron+Indef:gea oblintercas_indef ; 
makkár+Pron+Indef:makkár DAKKAR ; 
makkár+Pron+Indef+Use/Sub:makkar DAKKAR ; 
guh0te+Pron+Interr:guh'te GUHTECASE ;
guhtemuš+Pron+Interr:guđemužž GUHTEMUŠCASE ;
goabbá+Pron+Interr:goabbá GUHTECASE ;


!vaikko% mii+Pron+Indef+Sg+Nom:vaikko% mij K-son ; 
!vaikko% mii+Pron+Indef+Sg+Acc+:vaikko% májd9 K-son ;
mii+Pron+Indef+Sg+Nom:mij K-son ; ! only to the right of vaikko, cf. CG.
vaikko% gii+Pron+Indef+Sg+Nom:vaikko% gij K-son ;
vaikko% mii+Pron+Indef:vaikko% ma oblintercas_indef ;
vaikko% mii+MWE+Pron+Indef+Sg+Acc:vaikko% májd9 K-son ;
vaikko% gii+Pron+Indef:vaikko% gea oblintercas_indef ; 
vaikko% makkár+Pron+Indef:vaikko% makkár DAKKAR ; 
vaikko% guh0te+Pron+Interr:vaikko% guh'te GUHTECASE ;
vaikko% guhtemuš+Pron+Interr:vaikko% guđemužž GUHTEMUŠCASE ;
vaikko% goabbá+Pron+Interr:vaikko% goabbá GUHTECASE ;
! Commented out vaikko mii because of cases like
!"<ahte>"
!"ahte" CS @CVP #6->8 
!"<vaikko_mii>"  <=== error: should be mii = Pron Pl1
!"vaikko_mii" Pron Indef Sg Nom @SUBJ> #7->8 
!"<eat>"
!"ii" <aux> V IV Neg Ind Pl1 @FS-OBJ #8->2 
!"<bargga>"
!"bargat" <mv> V TV Ind Prs ConNeg @FS-IMV #9->8 


goabbá% ge+Pron+Interr+Sg+Nom:goabbá% ge K ;
goabbá% ge+Pron+Interr+Sg+Acc:goappá% ge K ;
goabbá% ge+Pron+Interr+Sg+Gen:goappá% ge K ;
goabbá% ge+Pron+Interr+Sg+Ill:goabbái% ge K ;
goabbá% ge+Pron+Interr+Sg+Loc:goappás% ge K ;
goabbá% ge+Pron+Interr+Sg+Com:goappáin% ge K ;


! some multiword prons, according to Nickel
!feara% mii iind ;
!feara% gii iind ;
mii% nu+MWE+Pron+Indef+Sg+Nom:mii% nu # ;
mii% nu+MWE+Pron+Indef+Sg+Gen:man% nu # ;  
mii% nu+MWE+Pron+Indef+Sg+Acc:maid% nu # ;  
mii% nu+MWE+Pron+Indef+Sg+Ill:masa% nu # ;  
mii% nu+MWE+Pron+Indef+Sg+Loc:mas% nu # ;  
mii% nu+MWE+Pron+Indef+Sg+Com:mainna% nu # ;  
mii% nu+MWE+Pron+Indef+Sg+Ess:manin% nu # ;  
gii% nu+MWE+Pron+Indef+Sg+Nom:gii% nu # ;  
gii% nu+MWE+Pron+Indef+Sg+Gen:gean% nu # ;  
gii% nu+MWE+Pron+Indef+Sg+Acc:gean% nu # ;  
gii% nu+MWE+Pron+Indef+Sg+Ill:geasa% nu # ;  
gii% nu+MWE+Pron+Indef+Sg+Loc:geas% nu # ;  
gii% nu+MWE+Pron+Indef+Sg+Com:geainna% nu # ;  
gii% nu+MWE+Pron+Indef+Sg+Ess:geanin% nu # ;  
makkár% nu+MWE+Pron+Indef+Attr:makkár% nu # ;
makkáraš% nu+MWE+Pron+Indef+Attr:makkáraš% nu # ;

! Separate lexica for exceptional entries
! - - - - - - - - - - - - - - - - - - - -

LEXICON declindef-idiosync
! separate lexica for these entries:
! oktat
oktahat+Pron+Indef:ovttaha OVTTAHAT ;
seammahat+Pron+Indef+Use/LexSub:seammaha OVTTAHAT ; !
seammahat+Pron+Indef+Attr+Use/LexSub:seammahat # ; !
seammahat+Pron+Indef+Nom+Sg+Use/LexSub:seammahat # ; !
oktahat+Pron+Indef+Attr:oktahat%> K ;
aktahat+Pron+Indef+Attr:aktahat%> K ;
oktahat+Pron+Indef+Nom+Sg:oktahat%> K ;
aktahat+Pron+Indef+Nom+Sg:aktahat%> K ;

earrása+Pron+Indef:earrás EARRAS ;
!iešguhtet+Pron+Indef+Nom:ieš#guhtet%> K ;			    Attr
!iešguhtet+Pron+Indef+Gen+Use/Sub:ieš#guđet%> K ; !	    Attr
!!iešguhtet+Pron+Indef+Gen+Use/Sub:ieš#guđet»# NAMAT ; ! Attr
!iešguhtet+Pron+Indef+Acc+Use/Sub:ieš#guđet%> K ; !	    Attr
simme+Pron+Indef+Use/LexSub:simme SIMME ; !
seammá+Pron+Indef+Use/Sub:seamma SEAMMA ; !
seammá+Pron+Indef:seammá SEAMMA ;
!seammá+Pron+Indef+Attr+Use/-Spell:seamma»# NAMAT ;
seammá+Pron+Indef+Attr:seamma%> K ;
seammá+Pron+Indef+Attr+Use/Sub:seammá # ; !, dict says seamma ~ seammá (#seamma)
juoga+Pron+Indef:juo JUOGA ;
galle+Pron+Indef+Use/CircN:gal'l GUOKTE-pure ; !
guktot+Pron+Indef:gukto indefdeclplcontr ;
juogamii+Pron+Indef+Sg+Nom:juogamij%> K-son ;
juogamii+Pron+Indef+Sg+Acc+Use/Sub:juogamájd9 K-son ; !
juogamii+Pron+Indef:juogama%> oblintercas_indef ;




! The indeclineable indefinites
! - - - - - - - - - - - - - - -

LEXICON indeclindef
!ald+Use/LexSub:ald9 iind ; ! Refl
buot+Pron+Indef+Attr:buot K ;
buot+Pron+Indef+Nom:buot K ;
buot+Pron+Indef+Acc:buot K ;
gait+Pron+Indef+Attr:gajt K ;
gait+Pron+Indef+Nom:gajt K ;
gait+Pron+Indef+Acc:gajt K ;
eatnat+Pron+Indef+Attr:eatnat K ;
eatnat+Pron+Indef+Nom:eatnat K ;
eatnat+Pron+Indef+Acc:eatnat K ;
!buot+Use/-Spell:buot»# NAMAT ;
!gait+Use/-Spell:gajt»# NAMAT ;
!eatnat+Use/-Spell:eatnat»# NAMAT ;
!fal iind ; ! cf. Nickel s. 126.
!gallát iind ; ! Bare Attr
goabbat+Pron+Indef+Nom:goabbat K ;
goabbat+Pron+Indef+Nom+Use/Sub:goabbát K ;
goabbat+Pron+Indef+Acc:goappat K ;
goabbat+Pron+Indef+Gen:goappat K ; !Ikke ihht. N/S s. 135, men i bruk i korpus: goappat bealde
!goabbat+Use/-Spell:goabbat»# NAMAT ;
goappaš+Pron+Indef+Attr:goappaš K ;
goappaš+Pron+Indef+Attr+Use/Sub:goappeš K ;
!goappaš+Use/-Spell:goappaš»# NAMAT ;
!goappašat iind ; ! thus both Nom and non-Nom. XXX.
guhtet+Pron+Indef+Nom:guhtet K ;
guhtet+Pron+Indef+Acc:guđet K ;
iešguhtet+Pron+Indef+Nom:iešguhtet # ;
iešguhtet+Pron+Indef+Acc:iešguđet # ;
iešguhtet+Pron+Indef+Acc+Use/Sme:iešgudet # ;
iešguhtet+Pron+Indef+Gen:iešguđet # ;
iešguhtet+Pron+Indef+Gen+Use/Sme:iešgudet # ;
iešguhtet+Pron+Indef+Attr:iešguđet # ;
iešguhtet+Pron+Indef+Attr+Use/Sme:iešgudet # ;
juohke+Pron+Indef+Attr:juohke K ;
!iešguđet+Use/-Spell:ieš#guđet»# NAMAT ;
!iešguhtet+Use/-Spell:ieš#guhtet»# NAMAT ;
!juohke+Use/-Spell:juohke»# NAMAT ;
!ollu iind ; ! A og Adv
!olu iind ; ! A og Adv
visot iind ; ! A og Adv
!visot+Use/-Spell:visot»# NAMAT ;

!vaikko iind ; !This is CS, not pron


