! ############################## !
!This file contains the numerals !
! ############################## !




! Numerals
! ========

! The initial lexica
! ------------------

LEXICON Numeral
MILJON ;          ! a noun of its own
UNDERDUHAT ;      ! for generator under 1000         
JUSTDUHAT ;       ! going via 1000                   
OVERDUHAT ;       ! for generator over 1000          !^N^
OLD ;             ! for "thirteen hundred, etc.      !^N^
!num-ordinal ;     ! The basic ordinal numbers       !^N^!Integrated in card.
!num-derived ;    ! still unimplemented              !^N^
num-imprecise ;   ! still almost unimplemented       
ARABIC ;          ! for the arabic numerals          
ROMAN ;           ! for the roman numerals           !^C^
NUM-PREFIXES ;    ! for §34 etc.                     !^C^
ISOLATED-NUMEXP ; ! for ½ etc.                       !^C^

! The tag !^N^ was left from an earlier attempt at removing the numerals
! from a potential nonrecursive tagger. It is tentatively left there, in
! case we see that our nonrecursive parser grows too big. Otherwise it may
! just be removed.

! We now run through the alphabetic numerals, ending up with the case
! inflection of the final number. Intermediate case inflection is still
! unimplemented.


! Million, a noun of its own
! --------------------------

LEXICON MILJON                              
miljon+Num+Sg+Nom:miljon K ;                !Num or N?
miljon+Num+Sg+Ess:miljovdnan K ;            
miljon+Num:miljovdna OKTA-NE ;              
miljon+Num:miljovdna OKTA-OBL ;             !hmm...
miljun+Num:miljuvdna OKTA-NE ;              
miljun+Num:miljuvdna OKTA-OBL ;             
millijon+Num:millijovdna OKTA-NE ;          
millijon+Num:millijovdna OKTA-OBL ;         !hmm...
millijovnnat+A+Ord:millijovnnag VUOSTTAS ;  !^N^
miljonat+A+Ord:miljonag VUOSTTAS ;          !^N^
miljár0da+Num:miljár'da DEVNVCASE ;         !Num or N?
miljárdat+A+Ord:miljárdag VUOSTTAS ;        !^N^


! Overduhat, for the nuumerals over 1000
! --------------------------------------

LEXICON OVERDUHAT                           !^N^
O-OKTAF ;     ! 1000                        !^N^
O-2TO9F ;     ! 2000, 3000, ...             !^N^
O-JUSTLOGIF ; ! 10000                       !^N^
O-2TO9LOG ;   ! 20000, 30000, ...           !^N^
O-NUPPELOT ;  ! 11000, 12000, ...           !^N^
O-CUODI ;     ! 100000                      !^N^    
O-2TO9CUO ;   ! 200000, 300000, ...         !^N^

! All the child lexica of OVERDUHAT have the prefix O-.
! They are directed via their respective numerals to the lexicon JUSTDUHAT.


! Thousands

LEXICON O-OKTAF                               !^N^
okta JUSTDUHAT ;                              !^N^
akta JUSTDUHAT ;                              !^N^
JUSTDUHAT ;                                   !^N^
                                            
LEXICON O-2TO9F                               !^N^
guokte JUSTDUHAT ;                            !^N^    
golbma JUSTDUHAT ;                            !^N^
njeallje JUSTDUHAT ;                          !^N^        
njeallja JUSTDUHAT ;                          !^N^
vihtta JUSTDUHAT ;                            !^N^
guhtta JUSTDUHAT ;                            !^N^
čieža JUSTDUHAT ;                             !^N^
gieža JUSTDUHAT ;                             !^N^
gávcci JUSTDUHAT ;                            !^N^
ovcci JUSTDUHAT ;                             !^N^


! Tens of thousands

LEXICON O-JUSTLOGIF        !^N^
logi JUSTDUHAT ;           !^N^
! This lexicon is for the number 10000 only. it is separated from the
! rest to avoid forms like *logivihttaduhát, etc.


LEXICON O-LOGIF           !^N^    
logi JUSTDUHAT ;          !^N^
logi O-OKTAF ;            !^N^
logi O-2TO9F ;            !^N^
! this lexicon is accessed only via other O-lexica, and not directly
! from OVERDUHAT. Thus, *logivihttaduhát, etc. is avoided.

LEXICON O-2TO9LOG        !^N^    
guokte O-LOGIF ;         !^N^
golbma O-LOGIF ;         !^N^
njeallje O-LOGIF ;        !^N^
njeallja O-LOGIF ;        !^N^
vihtta O-LOGIF ;        !^N^
guhtta O-LOGIF ;        !^N^
čieža O-LOGIF ;            !^N^
gieža O-LOGIF ;            !^N^
gávcci O-LOGIF ;        !^N^
ovcci O-LOGIF ;            !^N^


! Teens of thousands

LEXICON O-NUPPELOT                                !^N^
okta O-NL ;                                        !^N^
akta O-NL ;                                        !^N^
guokte O-NL ;                                    !^N^
golbma O-NL ;                                    !^N^
njeallje O-NL ;                                    !^N^
njeallja O-NL ;                                    !^N^
vihtta O-NL ;                                    !^N^
guhtta O-NL ;                                    !^N^
čieža O-NL ;                                    !^N^
gieža O-NL ;                                    !^N^
gávcci O-NL ;                                    !^N^
ovcci O-NL ;                                    !^N^
                                                
 LEXICON O-NL                                    !^N^
 nuppelohkái JUSTDUHAT ;                        !^N^
                                                
                                                
! Hundreds of thousands                            
                                                
LEXICON O-CUODI                                    !^N^
čuođi JUSTDUHAT ;  ! 100000                        !^N^
čuođi O-OKTAF ;     ! 101000                    !^N^
čuođi O-2TO9F ;    ! 102000, 103000, ...        !^N^
čuođi O-LOGIF ;    ! 110000, 113000, ...        !^N^
čuođi O-2TO9LOG ;  ! 120000, 123000, ...        !^N^
čuođi O-NUPPELOT ; ! 115000, ...                !^N^
!Duortnus:                                        
čuohti JUSTDUHAT ;  ! 100000                    !^N^
čuohti O-OKTAF ;     ! 101000                    !^N^
čuohti O-2TO9F ;    ! 102000, 103000, ...        !^N^
čuohti O-LOGIF ;    ! 110000, 113000, ...        !^N^
čuohti O-2TO9LOG ;  ! 120000, 123000, ...        !^N^
čuohti O-NUPPELOT ; ! 115000, ...                !^N^
                                                
LEXICON O-2TO9CUO                                !^N^
okta O-CUODI ;       ! #one# hundred...            !^N^
akta O-CUODI ;       ! #one# hundred...            !^N^
guokte O-CUODI ;                                !^N^
golbma O-CUODI ;                                !^N^
njeallje O-CUODI ;                                !^N^
njeallja O-CUODI ;                                !^N^
vihtta O-CUODI ;                                !^N^
guhtta O-CUODI ;                                !^N^
čieža O-CUODI ;                                    !^N^
gieža O-CUODI ;                                    !^N^
gávcci O-CUODI ;                                !^N^
ovcci O-CUODI ;                                    !^N^





! Justduhat, for numrals going via 1000
! -------------------------------------

LEXICON JUSTDUHAT                                    
duhát UNDERDUHAT ;  ! Thousand-and-something        !^N^
duhát:duháh DUHAT ; ! Case infl of duhát            
duháhat+A+Ord:duháha DUHAHAT ;                        !^N^


! Old, for the old counting thirteen hundred etc.    
! -----------------------------------------------

LEXICON OLD                                            !^N^
okta NLX ;                                            !^N^    
akta NLX ;                                            !^N^    
guokte NLX ;                                        !^N^
golbma NLX ;                                        !^N^
njeallje NLX ;                                        !^N^
njeallja NLX ;                                        !^N^
vihtta NLX ;                                        !^N^
guhtta NLX ;                                        !^N^
čieža NLX ;                                            !^N^
gieža NLX ;                                            !^N^
gávcci NLX ;                                        !^N^
ovcci NLX ;                                            !^N^
                                                    
LEXICON NLX                                            !^N^
nuppelohkái CUODI ;                                    !^N^
nuppelohkái 2TO9CUO ;                                !^N^



! Underduhat, the numerals under 1000
! -----------------------------------


LEXICON UNDERDUHAT                                   
OKTAF ;     ! 1, 11                                  
2TO9F ;     ! 2-9, 12-19                             
BARELOGIF ;     ! 10                                 
2TO9LOG ;   ! 20-99                !^N^                  
CUODI ;     ! 100-199                                
2TO9CUO ;   ! 200-999               !^N^                 
                                                    
LEXICON OKTAF                                         
ok0ta:ok'ta OKTA ;                                    
vuosttas+A+Ord:vuosttas VUOSTTAS ;                    
vuosttaš+A+Ord:vuosttaž VUOSTTAS ;                    
! K ; this lead to "go +Clt" as an analysis, and was removed.
! Thus, direction to K should be made at an earlier point

LEXICON 2TO9F                                         
guok0te:guok't GUOKTE ;                               
nubbi+A+Ord:nubbi indef-a-even-v ;                    !!CHECK THIS
nubbi+A+Ord+Acc:nuppe K ;                                 
nubbi+A+Ord+Gen:nuppe K ;                             
golbma GOLBMA ;                                           
goalmmát+A+Ord:goalmmád VUOSTTAS ;                    
njeallje:njeallj NJEALLJE ;                           
njeallja:njeallj NJEALLJA ;                           
njealját+A+Ord:njealjád VUOSTTAS ;                    
vihtta:vi VIHTTA ;                                    
viđát+A+Ord:viđád VUOSTTAS ;                          
guhtta:gu VIHTTA ;                                    
guđát+A+Ord:guđád VUOSTTAS ;                          
čieža:čie CIEZA ;                                     
gieža:gie CIEZA ;                                     
čihččet+A+Ord:čihččed VUOSTTAS ;                      
gihččet+A+Ord:gihččed VUOSTTAS ;                      
gávcci:gák'ci GAVCCI ;                                
gávccát+A+Ord:gávccád VUOSTTAS ;                      
ovcci:ok'ci GAVCCI ;                                  
ovccát+A+Ord:ovccád VUOSTTAS ;                        


LEXICON LOGIF ! This to avoid the +log bug            
BARELOGIF ;                                           
+logi:loh'ki LOGI ;                                   
+lot+Num+Sg+Nom:lot K ;                               !^N^            
+lot+Num+Sg+Acc+Attr:lot K ;                          !^N^
+lot NUMERALCOMPOUNDS ;   !SUB                        !^N^ !changed to individual pointers Sg+Gen to avoid other cases       !^C^
+loh0ki:loh'ki GOAHTI ;                               !^N^ ! vihttalohki, collective number
+logát+A+Ord:logád VUOSTTAS ;                         

LEXICON BARELOGIF                                     
logi OKTAF ;                                          
logi 2TO9F ;                                          
logi LOGI ;                                           
                                                    
 LEXICON NUPPELOT                                     !^N^
 okta NL ;                                            !^N^
 guokte NL ;                                          !^N^
 golbma NL ;                                          !^N^
 njeallje NL ;                                        !^N^
 njeallja NL ;                                        !^N^
 vihtta NL ;                                          !^N^
 guhtta NL ;                                          !^N^
 čieža NL ;                                           !^N^
 gieža NL ;                                           !^N^
 gávcci NL ;                                          !^N^
 ovcci NL ;                                           !^N^
                                                    
                                                     
LEXICON NL                                            !^N^
! :nuppe#lohkái JUSTDUHAT ;                           !^N^
 :nuppe#lohkái NUPPELOHKAI ;                          !^N^


LEXICON 2TO9LOG                                       !^N^
guokte LOGIF ;                                        !^N^
golbma LOGIF ;                                        !^N^
njeallje LOGIF ;                                      !^N^
njeallja LOGIF ;                                      !^N^
vihtta LOGIF ;                                        !^N^
guhtta LOGIF ;                                        !^N^
čieža LOGIF ;                                         !^N^
gieža LOGIF ;                                         !^N^
gávcci LOGIF ;                                        !^N^
ovcci LOGIF ;                                         !^N^


LEXICON CUODI                                        
!čuohti:čuođi OVERDUHAT ;                            !^N^
čuođi LOGI ;                                         !^N^
čuođi LOGIF ;                                        !^N^
čuođi+Num+Sg+Gen:čuođilot NUMERALCOMPOUNDS ;         !^N^
čuođi+Num+Sg+Gen:čuođilogi NUMERALCOMPOUNDS ;        !^N^
čuođi 2TO9LOG ;                                      !^N^
čuođi OKTAF ;                                        !^N^
čuođi 2TO9F ;                                        !^N^
čuođi NUPPELOT ;                                     !^N^
čuoh0ti:čuoh'ti GOAHTILONGSHORT ; ! imprecise number           
čuoh0ti+N+Attr:čuoh'te ATTR ; ! imprecise number           
čuođát+A+Ord:čuođád VUOSTTAS ;                       !^N^
!Duortnus:                                            
čuoh0ti+Num+Attr:čuohti K ;                          
čuoh0ti+Num+Attr:čuohti NUMERALCOMPOUNDS ;           !^N^
čuohti+Num+Sg+Nom:čuohti K ;                         
čuohti+Num+Sg+Acc:čuođi K ;                          
čuohti+Num+Sg+Gen:čuođi K ;                          
čuohti+Num+Sg+Loc:čuođis K ;                         
čuohti+Num+Sg+Loc:čuođin K ; !SUB !SOUTH             !^N^
čuohti+Num+Pl+Nom:čuođit K ;                         !^N^
čuohti+Num:čuohtiX3 NUM-I ;                          !^N^
čuohti+Num:čuohtiX5 NUM-II ;                         !^N^
čuohti+Num+Ess:čuohtin K ;                           !^N^
čuohti LOGIF ;                                       !^N^
čuohti+Num+Sg+Gen:čuođilot NUMERALCOMPOUNDS ;        !^N^
čuohti+Num+Sg+Gen:čuođilogi NUMERALCOMPOUNDS ;       !^N^
čuohti 2TO9LOG ;                                     !^N^
čuohti OKTAF ;                                       !^N^
čuohti 2TO9F ;                                       !^N^
čuohti NUPPELOT ;                                    !^N^
                                                    
                                                    
LEXICON 2TO9CUO                                      !^N^
guokte GCUODI ;                                      !^N^
golbma GCUODI ;                                      !^N^
njeallje GCUODI ;                                    !^N^
njeallja GCUODI ;                                    !^N^
vihtta GCUODI ;                                      !^N^
guhtta GCUODI ;                                      !^N^
čieža GCUODI ;                                       !^N^
gieža GCUODI ;                                       !^N^
gávcci GCUODI ;                                      !^N^
ovcci GCUODI ;                                       !^N^
                                                    
LEXICON GCUODI                                       !^N^
!čuođi OVERDUHAT ;                                   !^N^
čuohti LOGI ; ! testing                              !^N^
!čuoh0ti:čuođi LOGI ;                                !^N^
čuoh0ti:čuođi LOGIF ;                                !^N^
čuoh0ti+Num+Sg+Gen:čuođilot NUMERALCOMPOUNDS ;       !^N^
čuoh0ti+Num+Sg+Gen:čuođilogi NUMERALCOMPOUNDS ;      !^N^
čuoh0ti:čuođi 2TO9LOG ;                              !^N^
čuoh0ti:čuođi OKTAF ;                                !^N^
čuoh0ti:čuođi 2TO9F ;                                !^N^
čuoh0ti:čuođi NUPPELOT ;                             !^N^
!čuoh0ti:čuoh'ti GOAHTI ; ! imprecise number          !^N^
! Commented out the noun čuohti and gav
!Duortnus:
čuoh0ti+Num+Sg+Gen:čuohtilot NUMERALCOMPOUNDS ;      !^N^
čuoh0ti+Num+Sg+Gen:čuohtilogi NUMERALCOMPOUNDS ;     !^N^
čuoh0ti+Num+Attr:čuohti NUMERALCOMPOUNDS ;           !^N^
čuoh0ti+Num+Attr:čuohti K ;                          !^N^


! Case forms of the basic digits
! ------------------------------

LEXICON OKTA                    
 +Num: OKTA-NE ;                
 +Num: OKTA-OBL ;               


LEXICON GUOKTE                  
 +Num:e GUOKTE-NA ;             
 +Num:i GUOKTI ;                

LEXICON GOLBMA                  
 +Num: GUOKTE-NA ;              
 +Num: GUOKTI ;                 

LEXICON NJEALLJA                
 +Num:a GUOKTE-NA ;             
 +Num:a GUOKTI ;                

! +Num+Sg+Gen:X4 NUPPELOHKAI ;  !^N^
! +Num+Sg+Loc:X4s NUPPELOHKAI ; !^N^
! +Num+Pl+Nom:X4t NUPPELOHKAI ; !^N^
! +Num: NUM-I ;                 !^N^
! +Num:X5 NUM-II ;              !^N^

LEXICON NJEALLJE                
 +Num:e GUOKTE-NA ;             
 +Num:i GUOKTI ;                

! +Num+Sg+Gen:iX4 NUPPELOHKAI ; ! for njealji
! +Num+Sg+Gen:eX4 NUPPELOHKAI ; ! for njealje
! +Num+Sg+Loc:X4s NUPPELOHKAI ;
! +Num+Pl+Nom:X4t NUPPELOHKAI ;
! +Num:X3 NUM-I ;
! +Num:X5 NUM-II ;

! 5 and 6, minimal stem vi-, gu- due to exceptional III-I consonant gradation
LEXICON VIHTTA                            
 +Num: VIHTTA-NAIE ;                      
 +Num: VIHTTA-OBL ;                       
                                        
LEXICON CIEZA                             
! +Num+Sg+Ill:žaX3j NUPPELOHKAI ;        
! +Num+Sg+Ill:hččaX3j NUPPELOHKAI ;        
 +Num+Sg+Ill:žaX3 NUPPELOHKAI ;           
 +Num+Sg+Ill:hččaX3 NUPPELOHKAI ;         
 +Num+Sg+Ill:žaX3 LOGIF ;                 
 +Num+Sg+Ill:hččaX3 LOGIF ;               
 +Num:h'čaX3 NUM-I ;                      
 +Num:h'čaX5 NUM-II ;                     
 +Num+Ess:hžan NUPPELOHKAI ;              
 +Num+Ess:hžan LOGIF ;                    
 +Num:ža LOGI-WE ;                        
 +Num:ža NUPPELOHKAI ;                    
 +Num:ža LOGIF ;                          
                                        
LEXICON GAVCCI ! 8,9                      
  +Num+Sg+Gen:X4 NUPPELOHKAI ;            
  +Num+Sg+Gen:X4 LOGIF ;                  
  +Num+Sg+Gen:X4lot NUMERALCOMPOUNDS ;    
  +Num+Sg+Gen:X4logi NUMERALCOMPOUNDS ;   
     LOGI ;                               


LEXICON LOGI                            
 +Num:X4 LOGI-WE ;                      
 +Num:X3 NUM-I ;                        
 +Num:X5 NUM-II ;                       
 +Num+Ess:n K ;                         
                                         
LEXICON DUHAHAT                         !^N^
 +Sg+Nom:t K ;                          !^N^
 +Sg+Gen:tta K ;                        !^N^
 +Sg+Acc:tta K ;                        !^N^
 +Sg+Ill:ddij K ;                       !^N^
 +Sg+Loc:ttas K ;                       !^N^
 +Sg+Com:ttajn K ;                      !^N^
 +Ess:ddan K ;                          !^N^
 +Pl+Nom:ttat K ;                       !^N^
 +Pl+Gen:ttaid K ;                      !^N^
 +Pl+Acc:ttaid K ;                      !^N^
 +Pl+Ill:ttaide K ;                     !^N^
 +Pl+Loc:ttain K ;                      !^N^
 +Pl+Com:ttaiguin K ;                   !^N^
 +Attr:t K ;                            !^N^



! Basic digits, continuation lexica

! one

LEXICON OKTA-NE                          
 +Sg+Nom: NUPPELOHKAI ;                  
 +Ess:n NUPPELOHKAI ;                    
 +Sg+Nom: LOGIF ;                !^N^        
 +Ess:n LOGIF ;                 !^N^         
                                         
LEXICON OKTA-OBL                         
 :X4 OKTA-A ;                            
 :X4 NUM-GLN ;                           
 :X3 NUM-I ;                             
 :X5 NUM-II ;                            
                                         
LEXICON OKTA-A                           
 +Sg+Acc: NUPPELOHKAI ;                  
 +Sg+Acc: LOGIF ;                      !^N^  
                                        
! two, three and four                    
                                        
LEXICON GUOKTE-NA                        
 +Sg+Nom: NUPPELOHKAI ;                  
 +Sg+Acc: NUPPELOHKAI ;                  
 +Sg+Gen:X4 NUPPELOHKAI ;                
 +Sg+Gen#:X4# NUMERALCOMPOUNDS ;         ! guovtteávjjot 
 +Sg+Nom: LOGIF ;                        !^N^
 +Sg+Acc: LOGIF ;                        !^N^
 +Sg+Gen:X4 LOGIF ;                      !^N^
 +Sg+Gen:X4lot NUMERALCOMPOUNDS ;        !^N^
 +Sg+Gen:X4logi NUMERALCOMPOUNDS ;       !^N^

LEXICON GUOKTI                           
 :X4 NUM-GLN ;                           
     NUM-ESS ;                           
 :X3 NUM-I ;                             
 :X5 NUM-II ;                            



! Common for one through four

LEXICON NUM-GLN                          
 +Sg+Gen: NUPPELOHKAI ;                  
 +Sg+Gen#:# NUMERALCOMPOUNDS ;            ! golmmagielat
 +Sg+Loc:s NUPPELOHKAI ;                 
 +Sg+Loc:n NUPPELOHKAI ; !SUB !SOUTH     
 +Pl+Nom:t NUPPELOHKAI ;                 
 +Sg+Gen: LOGIF ;                        !^N^
 +Sg+Gen:lot NUMERALCOMPOUNDS ;          !^N^
 +Sg+Gen:logi NUMERALCOMPOUNDS ;         !^N^
 +Sg+Loc:s LOGIF ;                       !^N^
 +Sg+Loc:n LOGIF ; !SUB !SOUTH           !^N^
 +Pl+Nom:t LOGIF ;                       !^N^

LEXICON NUM-ESS                          
 +Ess:n NUPPELOHKAI ;                    
 +Ess:n LOGIF ;                          !^N^

LEXICON NUM-I                            
 +Sg+Ill:j NUPPELOHKAI ;                 
 +Sg+Ill:j LOGIF ;                       !^N^

LEXICON NUM-II                           
 +Sg+Com:jn NUPPELOHKAI ;                
 +Pl+Acc:jd NUPPELOHKAI ;                
 +Pl+Gen:jd NUPPELOHKAI ;                
 +Pl+Ill:jde NUPPELOHKAI ;               
 +Pl+Loc:jn NUPPELOHKAI ;                
 +Pl+Com:jgujn NUPPELOHKAI ;             
 +Sg+Com:jn LOGIF ;                      !^N^
 +Pl+Acc:jd LOGIF ;                      !^N^
 +Pl+Gen:jd LOGIF ;                      !^N^
 +Pl+Ill:jde LOGIF ;                     !^N^
 +Pl+Loc:jn LOGIF ;                      !^N^
 +Pl+Com:jgujn LOGIF ;                   !^N^


! Five and six

LEXICON VIHTTA-NAIE                      
 +Sg+Nom:htta NUPPELOHKAI ;              
 +Sg+Acc:htta NUPPELOHKAI ;              
 +Sg+Ill:httij NUPPELOHKAI ;             
 +Ess:httan NUPPELOHKAI ;                
 +Sg+Nom:htta LOGIF ;                    !^N^
 +Sg+Acc:htta LOGIF ;                    !^N^
 +Sg+Ill:httij LOGIF ;                   !^N^
 +Ess:httan LOGIF ;                      !^N^

LEXICON VIHTTA-OBL                       
 +Sg+Gen:đa NUPPELOHKAI ;                
 +Sg+Gen#:đa NUMERALCOMPOUNDS ;           ! viđauvssot
 +Sg+Loc:đas NUPPELOHKAI ;               
 +Sg+Loc:đan NUPPELOHKAI ; !SUB !SOUTH   
 +Sg+Com:đain NUPPELOHKAI ;              
 +Pl+Nom:đat NUPPELOHKAI ;               
 +Pl+Acc:đajd NUPPELOHKAI ;              
 +Pl+Gen:đajd NUPPELOHKAI ;              
 +Pl+Ill:đajde NUPPELOHKAI ;             
 +Pl+Loc:đajn NUPPELOHKAI ;              
 +Pl+Com:đajgujn NUPPELOHKAI ;           
 +Sg+Gen:đa LOGIF ;                      !^N^
 +Sg+Gen:đalot NUMERALCOMPOUNDS ;        !^N^
 +Sg+Gen:đalogi NUMERALCOMPOUNDS ;       !^N^
 +Sg+Loc:đas LOGIF ;                     !^N^
 +Sg+Loc:đan LOGIF ; !SUB !SOUTH         !^N^
 +Sg+Com:đain LOGIF ;                    !^N^
 +Pl+Nom:đat LOGIF ;                     !^N^
 +Pl+Acc:đajd LOGIF ;                    !^N^
 +Pl+Gen:đajd LOGIF ;                    !^N^
 +Pl+Ill:đajde LOGIF ;                   !^N^
 +Pl+Loc:đajn LOGIF ;                    !^N^
 +Pl+Com:đajgujn LOGIF ;                 !^N^


! Seven and up

LEXICON LOGI-WE                          
 +Sg+Nom: K ;                            
 +Sg+Acc: K ;                            
 +Sg+Gen: K ;                            
 +Sg+Gen#:# NUMERALCOMPOUNDS ;             !logigielat    
 +Sg+Loc:s K ;                           
 +Sg+Loc:n K ; !SUB !SOUTH               !^N^
 +Pl+Nom:t K ;                           !^N^
                                        
LEXICON DUHAT                            
 +Num+Sg+Nom: K ;                        
 +Num+Sg+Acc: K ; ! Acc = Nom for duhát  
 +Num+Sg+Gen: K ; ! Short genitive.      !^N^ !SUB
 +Num+Sg+Gen:a K ;                       
 +Num+Sg+Gen#:# NUMERALCOMPOUNDS ;        !duhatjienat
 +Num+Sg+Ill:ij K ;                      
 +Num+Sg+Loc:is K ;                      
 +Num+Sg+Loc:in K ; !SUB !SOUTH          !^N^
 +Num+Sg+Com:ijn K ;                     
 +Num+Ess:in K ;                         
 +Num+Pl+Nom:at K ;                      
 +Num+Pl+Gen:ijd K ;                     
 +Num+Pl+Acc:ijd K ;                     
 +Num+Pl+Ill:ijdda K ;                   
 +Num+Pl+Loc:ijn K ;                     
 +Num+Pl+Com:ijgujn K ;                  

LEXICON NUPPELOHKAI                      
 NUPPELOHKAIGENERATOR ;                  !^N^
 BASICGENERATOR ;                        

LEXICON NUPPELOHKAIGENERATOR             !^N^
      :nuppe LOHKAI-END ;                !^N^
      :goalmmát LOHKAI-END ;             !^N^
      :njealját LOHKAI-END ;             !^N^
      :viđát LOHKAI-END ;                !^N^
      :guđát LOHKAI-END ;                !^N^
      :čihččet LOHKAI-END ;              !^N^
      :gávccát LOHKAI-END ;              !^N^
      :ovccát LOHKAI-END ;               !^N^
      :logát LOHKAI-END ;                !^N^
 +Attr:nuppe#lot K ;    ! short -teen    !^N^
      :nuppe#lot NUMERALCOMPOUNDS ;      !^N^
!      :nuppe#lohkáj K ; ! long -teen    !^N^
!       :goalmmát#lohkáj K ;


LEXICON LOHKAI-END                       !^N^
    :loh'kis GUOVTTIS ;                  !^N^
    :lohkáj K ;                          !^N^
    :logis K ;                           !^N^
    :logát VUOSTTAS ;                    !^N^

LEXICON BASICGENERATOR                   
 K ;                   ! basic numbers   
! This is intermediary, we need a tag moving solution to get
! vihttanuppelohkái    vihttanuppelohkái+Num+Sg+Nom instead of
! vihttanuppelohkái       vihtta+Num+Sg+Nomnuppelohkái as the old soluteion.
! Now we get vihtta+Num+Sg+Nom:vihttanuppelohkái
! the old tags:
! nuppe#lohkái:nuppe#lohkáj K ; ! long -teen
! nuppe#lot+Attr:nuppe#lot K ;  ! short -teen
! Cf relevant chapter in B&K to fix this.


LEXICON NUMERALCOMPOUNDS ! numeral as first part           
 NAMAT ;               ! duhatjienat, logigielat, etc.     
 SAS ;                                                     
 NounSecond ;    ! golmmačiegahas                          !^N^
! geardásaš:geardás DABALAS ;                              !^N^
! !duhat#geardásaš                                         !^N^
! jahkásaš:jahkás DABALAS ;                                !^N^


! Ordinal numbers
! ---------------


!LEXICON num-ordinal ! Integrated in card                 !^N^
!vuosttas+A+Ord:vuosttas VUOSTTAS ;                       !^N^
!vuosttaš+A+Ord:vuosttaž VUOSTTAS ;                       !^N^
!nubbi+A+Ord:nubbi indef-a-even-v ;                       !^N^
!goalmmát+A+Ord:goalmmád VUOSTTAS ;                       !^N^
!njealját+A+Ord:njealjád VUOSTTAS ;                       !^N^
!viđát+A+Ord:viđád VUOSTTAS ;                             !^N^
!guđát+A+Ord:guđád VUOSTTAS ;                             !^N^
!čihččet+A+Ord:čihččed VUOSTTAS ;                         !^N^
!gihččet+A+Ord:gihččed VUOSTTAS ;                         !^N^
!gávccát+A+Ord:gávccád VUOSTTAS ;                         !^N^
!ovccát+A+Ord:ovccád VUOSTTAS ;                           !^N^
!logát+A+Ord:logád VUOSTTAS ;                             !^N^
!čuođát+A+Ord:čuođád VUOSTTAS ;                           !^N^
!duháhat+A+Ord:duháh VUOSTTAS ;                           !^N^
!millijovnnat+A+Ord:millijovnnag VUOSTTAS ;               !^N^
!miljonat+A+Ord:miljonag VUOSTTAS ;                       !^N^

LEXICON VUOSTTAS                                          
 GAPPUS0 ;                                                
 GAPPUS- ;                                                
! +A: EABBO/EAMOS ; ! No comparatives here                
 +Attr: K ;                                               


! Imprecise numbers
! -----------------

LEXICON num-imprecise                                     
máŋga num-imprecise-distribution ;                        !^N^
máŋga+Num+Sg+Gen:máŋggalot NUMERALCOMPOUNDS ;             !^N^
máŋga+Num+Sg+Gen:máŋggaloht NUMERALCOMPOUNDS ;            !^N^ !SUB
máŋga+Num+Sg+Gen:máŋggalogi NUMERALCOMPOUNDS ;            !^N^
moadde   num-imprecise-distribution ;                     !^N^
moadde+Num+Sg+Gen:moattilot NUMERALCOMPOUNDS ;            !^N^
moadde+Num+Sg+Gen:moattelot NUMERALCOMPOUNDS ;            !^N^
moadde+Num+Sg+Gen:moattilogi NUMERALCOMPOUNDS ;           !^N^
moadde+Num+Sg+Gen:moattelogi NUMERALCOMPOUNDS ;           !^N^
galle  num-imprecise-distribution ;                       !^N^
galle+Num+Sg+Gen:gallelot NUMERALCOMPOUNDS ;              !^N^
galle+Num+Sg+Gen:gallelogi NUMERALCOMPOUNDS ;             !^N^
máŋ0ga:máŋ'ga GOLBMA ;                                    
moadde:moadd GUOKTE ;                                     
gal0le:gal'le GOLBMA ;                                    
beal-nup+Num:beal%-nup3 # ; !SUB ! ad hoc                 !^N^
beannot+Num+Sg+Nom:beannot # ;                            !^N^
beannot+Num+Sg+Gen:beannot # ;                            !^N^
beannot+Num+Sg+Acc:beannot # ;                            !^N^
beal#nut+Num+Sg+Nom:beal#nut # ;                          !^N^
beal#nut+Num+Sg+Gen:beal#nut # ;                          !^N^
beal#nut+Num+Sg+Acc:beal#nut # ;                          !^N^
beal#goalmmát+A+Ord:beal#goalmmád VUOSTTAS ;              !^N^
beal#njealját+A+Ord:beal#njealjád VUOSTTAS ;              !^N^
beal#viđát+A+Ord:beal#viđád VUOSTTAS ;                    !^N^
beal#guđat+A+Ord:beal#guđad VUOSTTAS ;                    !^N^
beal#čihččet+A+Ord:beal#čihččed VUOSTTAS ;                !^N^
beal#gihččet+A+Ord:beal#gihččed VUOSTTAS ;                !^N^
beal#gávccát+A+Ord:beal#gávccád VUOSTTAS ;                !^N^
beal#ovccát+A+Ord:beal#ovccád VUOSTTAS ;                  !^N^
beal#logát+A+Ord:beal#logád VUOSTTAS ;                    !^N^
beannot+Num+Attr:beannot SAS ;                            !^N^
beal#nut+Num+Attr:beal#nut SAS ;                          !^N^
beal#goalmmát+A+Ord+Attr:beal#goalmmát SAS ;              !^N^
beal#njealját+A+Ord+Attr:beal#njealját SAS ;              !^N^
beal#viđát+A+Ord+Attr:beal#viđát SAS ;                    !^N^
beal#guđat+A+Ord+Attr:beal#guđat SAS ;                    !^N^
beal#čihččet+A+Ord+Attr:beal#čihččet SAS ;                !^N^
beal#gihččet+A+Ord+Attr:beal#gihččet SAS ;                !^N^
beal#gávccát+A+Ord+Attr:beal#gávccát SAS ;                !^N^
beal#ovccát+A+Ord+Attr:beal#ovccát SAS ;                  !^N^
beal#logát+A+Ord+Attr:beal#logát SAS ;                    !^N^

                                                        
LEXICON num-imprecise-distribution                        !^N^
NUPPELOHKAIGENERATOR ;  ! máŋga#nuppe#lohkái, máŋga       !^N^
LOGIF ;        ! máŋga#logi, máŋga#lot                    !^N^
CUODI ;        ! máŋga#čuođi                              !^N^
NL ;           ! máŋga#nuppe#lohkái#duhát                 !^N^
O-NL ;         ! máŋga#nuppe#lohkái#duhát                 !^N^
O-LOGIF ;      ! máŋga#logi#duhát                         !^N^
JUSTDUHAT ;    ! máŋga#duhát                              !^N^
O-CUODI ;      ! máŋga#duhát                              !^N^




! Arabic numerals
! ---------------


LEXICON NUM-PREFIXES                                        !^C^
§    ARABIC ;      ! §24                                    !^C^
§§    ARABIC ;     ! §§24                                   !^C^
§%     ARABIC ;    ! § 24                                   !^C^
§§%     ARABIC ;   ! §§ 24                                  !^C^
%-    ARABIC ;     ! -24                                    !^C^
%‒    ARABIC ;     ! -24 U+2012 Figure line                 !^C^
%–    ARABIC ;     ! -24 U+2013 en dash                     !^C^
%—    ARABIC ;     ! -24 U+2014 em dash                     !^C^
%―    ARABIC ;     ! -24 U+2015 horizontal l                !^C^
%+    ARABIC ;     ! +24                                    !^C^
%-%     ARABIC ;    ! - 24                                  !^C^
%‒%     ARABIC ;    ! - 24 2012                             !^C^
%–%     ARABIC ;    ! - 24 2013                             !^C^
%—%     ARABIC ;    ! - 24 2014                             !^C^
%―%     ARABIC ;    ! - 24 2015                             !^C^
%+%     ARABIC ;    ! + 24                                  !^C^
%*    ARABIC ;     ! *24                                    !^C^
%$    ARABIC ;     ! $24  ! And the Euro?                   !^C^
%<    ARABIC ;     ! <24                                    !^C^
%>    ARABIC ;     ! >24                                    !^C^
s%.    ARABIC ;     ! s.24                                  !^C^
    


LEXICON ARABIC                                                                    
1 ARABICLOOP ;                                                                    
2 ARABICLOOP ;                                                                    
3 ARABICLOOP ;                                                                    
4 ARABICLOOP ;                                                                    
5 ARABICLOOP ;                                                                    
6 ARABICLOOP ;                                                                    
7 ARABICLOOP ;                                                                    
8 ARABICLOOP ;                                                                    
9 ARABICLOOP ;                                                                    
%0 ARABICLOOP ;                                                                   
                                                                                
LEXICON ARABICLOOP                                                                  
ARABIC ;                                                                            
%-    ARABIC ;                ! 1-2    multipart numbers                            !^C^
%.%-     ARABIC ;             ! 1.-2   multipart numbers                            !^C^
%-%     ARABIC ;              ! 1- 2   multipart numbers                            !^C^
% %-    ARABIC ;              ! 1 -2   multipart numbers                            !^C^
% %-%     ARABIC ;            ! 1 - 2  multipart numbers                            !^C^
% %-%-%   ARABIC ;            ! 1 -- 2 multipart numbers                            !^C^
% %-      ARABICDELIMITER ;   ! 1 -   multipart numbers                             !^C^
% %-%-      ARABICDELIMITER ; ! 1 --  multipart numbers                             !^C^
%. ARABIC ;                                                                         !^C^
%, ARABIC ;                                                                         !^C^
%: ARABIC ;                                                                         !^C^
%  ARABIC ;       ! Allowing for "23 500".                                          
   ARABIC ;       ! Allowing for "23 500" w/nbsp (initial char is alt-space)        !^C^
%/ ARABIC ;       ! 24/12                                                           
!%' ARABIC ;       ! Since ' denotes cons.grad. This is not optimal either.
!+Num:%- R ; !^C^       ! Number compounds
+Num: R ; !^C^       ! Hyphen removed bec. of "123- ja 124-viessu"                  !^C^
+A+Ord:%. # ;                                                                       !^C^
,%-+Num:,%- # ;        ! 10,-                                                       !^C^
.%-+Num:,%- # ;  !SUB  ! 10.- It is wrong, but written.                             !^C^
%%+Num+Nom:%% ARABICDELIMITER ;    ! 50%. It is wrong, but 75 % of us write "75%".  !^C^
% %%+Num+Nom:% %% ARABICDELIMITER ;    ! the correct ones as well... 50 % etc.      !^C^
+Num: ARABICDELIMITER ; ! list of number-case delimiters                            !^C^


LEXICON ARABICDELIMITER ! blank + 3 delim, what does the lg counc prefer? !^C^
     ARABICCASE ;    ! This is the 1984s case.                     !^C^
 :%: ARABICCASE ;    ! This is the 1984:s case.                    !^C^
 :%' ARABICCASE ;    ! This is the 1984's case.                    !^C^
 :%- ARABICCASE ;    ! This is the 1984-s case.                    !^C^
 :%- NUMERALCOMPOUNDS ; ! 6-jáhkasaš                               !^C^
                                 


LEXICON ARABICCASE                                       !^C^
+Nom:f # ;        ! s. 123f.                             !^C^
+Nom:ff # ;        ! s. 123ff.                           !^C^
+Sg+Nom:b # ;        ! § 123b                            !^C^
+Sg+Nom:c # ;        ! § 123c                            !^C^
+Sg+Nom:d # ;        ! § 123d                            !^C^
+Sg+Nom:e # ;        ! § 123e                            !^C^
+Sg+Nom: # ;                                             !^C^
+Sg+Gen: # ;                                             !^C^
+Sg+Acc: # ;                                             !^C^
+Sg+Gen:' # ;                                            !^C^       E00 = '
+Sg+Gen:’ # ;                                            !^C^ optio-B07 = ’
+Sg+Gen:a # ;    !SUB ! But here 123a 123+Num+Gen        !^C^
+Sg+Acc:a # ;    !SUB                                    !^C^
+Sg+Gen:ža # ;                                           !^C^
+Sg+Acc:ža # ;                                           !^C^
+Sg+Ill:i # ;                                            !^C^
+Sg+Ill:ij # ;        !SUB                               !^C^
+Sg+Ill:žij # ;                                          !^C^
+Sg+Ill:áj # ;    !SUB ! Testing over the weekend.       !^C^
+Sg+Loc:s K ;                                            !^C^
+Sg+Loc:is K ;        !SUB                               !^C^
+Sg+Loc:žis K ;                                          !^C^
+Sg+Loc:as # ;        !SUB                               !^C^
+Sg+Com:in # ;                                           !^C^
+Sg+Com:žiin # ;                                         !^C^
+Sg+Com:ain # ;    !SUB                                  !^C^
+Sg+Ess:n # ;                                            !^C^
+Sg+Ess:žin # ;                                          !^C^
+Sg+Ess:an # ; !SUB                                                
+Pl+Nom:t       K ;
+Pl+Gen:ijd       K ;  !SUB
+Pl+Gen:id       K ;
+Pl+Acc:ijd       K ; !SUB
+Pl+Acc:id       K ;
+Pl+Ill:ijde   K ; !SUB ! 100:iide čuđiide
+Pl+Ill:ijdda  K ; !SUB !1000:idda duháhiidda
+Pl+Ill:ide    K ;      ! 100:iide čuđiide
+Pl+Ill:idda   K ;      !1000:idda duháhiidda
+Pl+Loc:in       K ;
+Pl+Com:igujn  K ;
                                            !^C^
! No plural numbers, hmm.


! The Roman numerals !
! ------------------ !

LEXICON ROMAN                                !^C^
 ROM-THOUSAND ;                                !^C^
 ROM-HUNDRED ;                                !^C^
 ROM-TEN ;                                    !^C^
 ROM-ONE ;                                    !^C^

LEXICON ROM-THOUSAND                          !^C^
 M ROM-THOUSAND-TAG ;                        !^C^
 MM ROM-THOUSAND-TAG ;                        !^C^
 MMM ROM-THOUSAND-TAG ;                        !^C^
 MMMM ROM-THOUSAND-TAG ;                    !^C^
 MMMMM ROM-THOUSAND-TAG ;                    !^C^

LEXICON ROM-THOUSAND-TAG                    !^C^
  ROMNUMTAG ;                                !^C^
 ROM-HUNDRED ;                                !^C^
 ROM-SPLIT    ;                                !^C^

LEXICON ROM-HUNDRED                            !^C^
 C ROM-HUNDRED-TAG ;                        !^C^
 CC ROM-HUNDRED-TAG ;                        !^C^
 CCC ROM-HUNDRED-TAG ;                        !^C^
 CD ROM-HUNDRED-TAG ;                        !^C^
 D ROM-HUNDRED-TAG ;                        !^C^
 DC ROM-HUNDRED-TAG ;                        !^C^
 DCC ROM-HUNDRED-TAG ;                        !^C^
 DCCC ROM-HUNDRED-TAG ;                        !^C^
 CM ROM-HUNDRED-TAG ;                        !^C^

LEXICON ROM-HUNDRED-TAG                        !^C^
  ROMNUMTAG ;                                !^C^
 ROM-TEN ;                                    !^C^
 ROM-SPLIT ;                                !^C^

LEXICON ROM-TEN                                !^C^
 X ROM-TEN-TAG ;                            !^C^
 XX ROM-TEN-TAG ;                            !^C^
 XXX ROM-TEN-TAG ;                            !^C^
 XL ROM-TEN-TAG ;                            !^C^
 L ROM-TEN-TAG ;                            !^C^
 LX ROM-TEN-TAG ;                            !^C^
 LXX ROM-TEN-TAG ;                            !^C^
 LXXX ROM-TEN-TAG ;                            !^C^
 XC ROM-TEN-TAG ;                            !^C^
                                            
LEXICON ROM-TEN-TAG                            !^C^
  ROMNUMTAG ;                                !^C^
 ROM-ONE ;                                    !^C^
 ROM-SPLIT ;                                !^C^
                                            
LEXICON ROM-ONE                                !^C^
 I ROM-ONE-TAG ;                            !^C^
 II ROM-ONE-TAG ;                            !^C^
 III ROM-ONE-TAG ;                            !^C^
 IV  ROM-ONE-TAG ;                            !^C^
 V ROM-ONE-TAG ;                            !^C^
 VI ROM-ONE-TAG ;                            !^C^
 VII ROM-ONE-TAG ;                            !^C^
 VIII ROM-ONE-TAG ;                            !^C^
 IX ROM-ONE-TAG ;                            !^C^
                                            
LEXICON ROM-ONE-TAG                            !^C^
  ROMNUMTAG ;                                !^C^
! +N: ROMNUMTAG ;                            !^C^ !The Olav viđeš fix (Roman V now gets A)
 ROM-SPLIT ;                                !^C^

! Here, we split the Roman numerals, in order to account for cases like "Kapihtal II-IV".
! We may send this first part directly to ROM-TAG below (and get the +Num tag), or we may
! send them through a second loop, identical to the first one, but marked wit "2" (the lexica
! are called 2ROMAN, etc. We do this instead of making a loop, since we do not want cases
! like "II-IV-VI-VII-IX". If that should turn out to be a good idea, a loop would do the
! trick.

LEXICON ROM-SPLIT                            !^C^
 :%- 2ROMAN ; ! II-VI, etc.                    !^C^
                                            
                                            
! Here goes loop 2.                            
                                            
LEXICON 2ROMAN                                !^C^
 2ROM-THOUSAND ;                            !^C^
 2ROM-HUNDRED ;                                !^C^
 2ROM-TEN ;                                     !^C^
 2ROM-ONE ;                                     !^C^
                                            
LEXICON 2ROM-THOUSAND                       !^C^
 M 2ROM-THOUSAND-TAG ;                        !^C^
 MM 2ROM-THOUSAND-TAG ;                        !^C^
 MMM 2ROM-THOUSAND-TAG ;                    !^C^
 MMMM 2ROM-THOUSAND-TAG ;                    !^C^
 MMMMM 2ROM-THOUSAND-TAG ;                    !^C^

LEXICON 2ROM-THOUSAND-TAG                    !^C^
  ROMNUMTAG ;                                !^C^
 2ROM-HUNDRED ;                                !^C^

LEXICON 2ROM-HUNDRED                        !^C^
 C 2ROM-HUNDRED-TAG ;                        !^C^
 CC 2ROM-HUNDRED-TAG ;                        !^C^
 CCC 2ROM-HUNDRED-TAG ;                        !^C^
 CD 2ROM-HUNDRED-TAG ;                        !^C^
 D 2ROM-HUNDRED-TAG ;                        !^C^
 DC 2ROM-HUNDRED-TAG ;                        !^C^
 DCC 2ROM-HUNDRED-TAG ;                        !^C^
 DCCC 2ROM-HUNDRED-TAG ;                    !^C^
 CM 2ROM-HUNDRED-TAG ;                        !^C^

LEXICON 2ROM-HUNDRED-TAG                    !^C^
  ROMNUMTAG ;                                !^C^
 2ROM-TEN ;                                    !^C^

LEXICON 2ROM-TEN                               !^C^
 X 2ROM-TEN-TAG ;                              !^C^
 XX 2ROM-TEN-TAG ;                             !^C^
 XXX 2ROM-TEN-TAG ;                            !^C^
 XL 2ROM-TEN-TAG ;                             !^C^
 L 2ROM-TEN-TAG ;                              !^C^
 LX 2ROM-TEN-TAG ;                             !^C^
 LXX 2ROM-TEN-TAG ;                            !^C^
 LXXX 2ROM-TEN-TAG ;                           !^C^
 XC 2ROM-TEN-TAG ;                             !^C^
                                            
LEXICON 2ROM-TEN-TAG                           !^C^
  ROMNUMTAG ;                                  !^C^
 2ROM-ONE ;                                    !^C^
                                            
LEXICON 2ROM-ONE                               !^C^
 I 2ROM-ONE-TAG ;                              !^C^
 II 2ROM-ONE-TAG ;                             !^C^
 III 2ROM-ONE-TAG ;                            !^C^
 IV  2ROM-ONE-TAG ;                            !^C^
 V 2ROM-ONE-TAG ;                              !^C^
 VI 2ROM-ONE-TAG ;                             !^C^
 VII 2ROM-ONE-TAG ;                            !^C^
 VIII 2ROM-ONE-TAG ;                           !^C^
 IX 2ROM-ONE-TAG ;                             !^C^
                                            
LEXICON 2ROM-ONE-TAG                           !^C^
  ROMNUMTAG ;                                  !^C^
                                             
LEXICON ROMNUMTAG                              !^C^
 +Num+Nom: # ;                                 !^C^
 +Num+Gen: # ;                                 !^C^
 +Num+Acc: # ;                                 !^C^
 +A+Ord:%. # ;                                 !^C^ !III. as ordinal.
 +A+Ord: # ;                                   !^C^ !Looking for trouble...
 +Num:%: ROMNUMTAGOBL ;                        !^C^
 +Num:%' ROMNUMTAGOBL ;                        !^C^
 +Num:%- ROMNUMTAGOBL ;                        !^C^
 +Num:%´ ROMNUMTAGOBL ;                        !^C^
 +Num:   ROMNUMTAGOBL ;                        !^C^

LEXICON ROMNUMTAGOBL                           !^C^
+Gen:a # ;    ! But here 123a 123+Num+Gen      !^C^
+Acc:a # ;                                     !^C^
+Ill:i # ;                                     !^C^
+Ill:ij # ;                                    !^C^
+Ill:áj # ;                                    !^C^
+Loc:s # ;                                     !^C^
+Com:in # ;                                    !^C^
+Ess:n # ;                                     !^C^




! A final section with some isolated numeral expresssions !
! ------------------------------------------------------- !


LEXICON ISOLATED-NUMEXP                        !^C^
½+Num:½ # ;                                    !^C^
¹+Num:¹ # ;                                    !^C^
²+Num:² # ;                                    !^C^
³+Num:³ # ;                                    !^C^
¼+Num:¼ # ;                                    !^C^

