! Divvun & Giellatekno - open source grammars for Sámi and other languages
! Copyright © 2000-2010 The University of Tromsø & the Norwegian Sámi Parliament
! http://giellatekno.uit.no & http://divvun.no
!
! This program is free software; you can redistribute and/or modify
! this file under the terms of the GNU General Public License as published by
! the Free Software Foundation, either version 3 of the License, or
! (at your option) any later version. The GNU General Public License
! is found at http://www.gnu.org/licenses/gpl.html. It is
! also available in the file $GTHOME/LICENSE.txt.
!
! Other licensing options are available upon request, please contact
! giellatekno@hum.uit.no or divvun@samediggi.no

! ==================== !
! The North Sámi dates !
! ==================== !


! 12.8.
! Borgemánu guoktenuppelot beaivi

Multichar_Symbols

@U.MONTH.1@ 
@U.MONTH.2@ 
@U.MONTH.3@ 
@U.MONTH.4@ 
@U.MONTH.5@ 
@U.MONTH.6@ 
@U.MONTH.7@ 
@U.MONTH.8@ 
@U.MONTH.9@ 
@U.MONTH.10@ 
@U.MONTH.11@ 
@U.MONTH.12@ 

+Use/NG ! For substandard month names used in Oahpa
        ! They will be accepted but not generated.

LEXICON Root

 @U.MONTH.1@:ođđajagemánu% @U.MONTH.1@   31DAYS ;
 @U.MONTH.1@+Use/NG:garjjámánu% @U.MONTH.1@   31DAYS ; 
 
 @U.MONTH.2@:guovvamánu% @U.MONTH.2@     29DAYS ;

 @U.MONTH.3@:njukčamánu% @U.MONTH.3@     31DAYS ;

 @U.MONTH.4@:cuoŋománu% @U.MONTH.4@      30DAYS ;
 @U.MONTH.4@+Use/NG:gáranasmánu% @U.MONTH.4@      30DAYS ;
 @U.MONTH.4@+Use/NG:vuoražismánu% @U.MONTH.4@      30DAYS ;

 @U.MONTH.5@:miessemánu% @U.MONTH.5@     31DAYS ;

 @U.MONTH.6@:geassemánu% @U.MONTH.6@     30DAYS ;

 @U.MONTH.7@:suoidnemánu% @U.MONTH.7@    31DAYS ;

 @U.MONTH.8@:borgemánu% @U.MONTH.8@      31DAYS ;
 @U.MONTH.8@+Use/NG:šnjilčamánu% @U.MONTH.8@      31DAYS ;

@U.MONTH.9@:čakčamánu% @U.MONTH.9@     30DAYS ;

 @U.MONTH.10@:golggotmánu% @U.MONTH.10@    31DAYS ;
 @U.MONTH.10@+Use/NG:ragatmánu% @U.MONTH.10@    31DAYS ;

@U.MONTH.11@:skábmamánu% @U.MONTH.11@    30DAYS ;

@U.MONTH.12@:juovlamánu% @U.MONTH.12@    31DAYS ;


LEXICON 29DAYS

1   B ;
2   B ;
3   B ;
4   B ;
5   B ;
6   B ;
7   B ;
8   B ;
9   B ;
1%0 B ;
11  B ;
12  B ;
13  B ;
14  B ;
15  B ;
16  B ;
17  B ;
18  B ;
19  B ;
2%0 B ;
21  B ;
22  B ;
23  B ;
24  B ;
25  B ;
26  B ;
27  B ;
28  B ;
29  B ;

! Commenting out numeric-to-letter day, for Oahpa.
! Comment in again for use in TTS applications.
! 1:vuosttaš 		     B ;
! 2:nubbi 			     B ;
! 3:goalmmát 		     B ;
! 4:njealját 		     B ;
! 5:viđát 			     B ;
! 6:guđát 			     B ;
! 7:čihččet              B ;
! 8:gávccát              B ;
! 9:ovccát 			     B ;
! 1%0:logát 	         B ;
! 11:oktanuppelogát      B ;
! 12:guoktenuppelogát     B ;
! 13:golbmanuppelogát     B ;
! 14:njealljenuppelogát   B ;
! 15:vihttanuppelogát     B ;
! 16:guhttanuppelogát     B ;
! 17:čiežanuppelogát      B ;
! 18:gávccinuppelogát     B ;
! 19:ovccinuppelogát      B ;
! 2%0:guoktelogát 	      B ;
! 21:guoktelogivuosttaš   B ;
! 22:guokteloginubbi      B ;
! 23:guoktelogigoalmmát   B ;
! 24:guokteloginjealját   B ;
! 25:guoktelogiviđát      B ;
! 26:guoktelogiguđát      B ;
! 27:guoktelogičihččet    B ;
! 28:guoktelogigávccát    B ;
! 29:guoktelogiovccát     B ;

LEXICON 30DAYS
3%0 B ;
!3%0:golbmalogát 	     B ;
29DAYS ;

LEXICON 31DAYS
31 B ;
!31:golbmalogivuosttaš   B ;
30DAYS ;

LEXICON B
%.:%.% b%. MONTH ;        ! written form: 12. b.
%.+Use/NG:%.b%. MONTH ;   ! sloppy form: 12.b.
!%.:% beaivi  MONTH ;     ! TTS form: beaivi

LEXICON MONTH
   1@U.MONTH.1@:@U.MONTH.1@   X ;
   2@U.MONTH.2@:@U.MONTH.2@   X ;
   3@U.MONTH.3@:@U.MONTH.3@   X ;
   4@U.MONTH.4@:@U.MONTH.4@   X ;
   5@U.MONTH.5@:@U.MONTH.5@   X ;
   6@U.MONTH.6@:@U.MONTH.6@   X ;
   7@U.MONTH.7@:@U.MONTH.7@   X ;
   8@U.MONTH.8@:@U.MONTH.8@   X ;
   9@U.MONTH.9@:@U.MONTH.9@   X ;
1%0@U.MONTH.10@:@U.MONTH.10@  X ;
 11@U.MONTH.11@:@U.MONTH.11@  X ;
 12@U.MONTH.12@:@U.MONTH.12@  X ;

LEXICON X
%.: # ;
!    # ; ! sloppy punctuation
