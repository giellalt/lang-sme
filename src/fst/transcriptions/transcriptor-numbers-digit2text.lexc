! Divvun & Giellatekno - open source grammars for Sámi and other languages
! Copyright © 2000-2010 The University of Tromsø & the Norwegian Sámi Parliament
! http://giellatekno.uit.no & http://divvun.no
!
! This program is free software; you can redistribute and/or modify
! this file under the terms of the GNU General Public License as published by
! the Free Software Foundation, either version 3 of the License, or
! (at your option) any later version. The GNU General Public License
! is found at http://www.gnu.org/licenses/gpl.html. It is
! also available in the file $GTHOME/LICENSE.txt.
!
! Other licensing options are available upon request, please contact
! giellatekno@uit.no or feedback@divvun.no

Multichar_Symbols

@U.DATE.1@
@U.DATE.2@
@U.DATE.3@
@U.DATE.4@
@U.DATE.5@
@U.DATE.6@
@U.DATE.7@
@U.DATE.8@
@U.DATE.9@
@U.DATE.10@
@U.DATE.11@
@U.DATE.12@
@U.DATE.13@
@U.DATE.14@
@U.DATE.15@
@U.DATE.16@
@U.DATE.17@
@U.DATE.18@
@U.DATE.19@
@U.DATE.20@
@U.DATE.21@
@U.DATE.22@
@U.DATE.23@
@U.DATE.24@
@U.DATE.25@
@U.DATE.26@
@U.DATE.27@
@U.DATE.28@
@U.DATE.29@
@U.DATE.30@
@U.DATE.31@

  +Use/NG    ! Do not generate, for isme-ped.fst and apertium
  +String    ! Tag to denote non-numeric strings
  +NumNum    ! Tag to denote real numbers in one form or another


! Note! the diacritic marks in this file are only part of the lower
! representation. This means they can only be used from number to letter
! conversion of date and month representation, not the other way around.
! In order to get a two-way conversion, they must be added to the
! upper level as well.


LEXICON Root

! < [a|b|c|d|e|f|g|h|i|j|k|l|m|n|o|p|q|r|s|t|u|v|w|q|y|z|æ|ø|å|ä|ö|á|č|đ|ŋ|A|B|C|D|E|F|G|H|I|J|K|L|M|N|O|P|Q|R|S|T|U|V|W|X|Y|Z|Æ|Ø|Å|Ä|Ö|Á|Č|Đ|Ŋ|Š|Ŧ|Ž]* > #             ; ! This first line is to allow all letter strings.

! The regex in the previous line is now commented out. If you want this fst to
! pass through all letters untouched (but transform all numbers)
! then comment it in again. It becomes critical for the invers sme-inum.fst, that
! does not work with this line. If it really is needed, we might filter it out
! for sme-inum.fst.

  NUMBERSECTION ; ! Numerals
! COMMASECTION  ; ! Separator section - not initial?? SNM 6.6.2025
  DATESECTION   ; ! Date expressions (for TTS)




! !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! !!!!!!!!!!                     !!!!!!!!!!!!!!!!!!!
! !!!!!!!!!!   Number section    !!!!!!!!!!!!!!!!!!!
! !!!!!!!!!!                     !!!!!!!!!!!!!!!!!!!
! !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON NUMBERSECTION
         HUNDREDSM     ; ! 200M
 1:čuođi HUNDREDM      ; ! 100M
         TENSM         ; ! 20-99M
         TEENSM        ; ! 10-19M
         ONESM         ; ! 1-9M
         ONESG         ; ! 1-9G
         HUNDREDST     ; ! 200000-999999
 1:čuođi HUNDREDT      ; ! 100000-100999
         TENST         ; ! 20000-99999,10000-10999
         TEENST        ; ! 11000-19999
         ONEST         ; ! 2000-9999
 1:duhát THOUSAND      ; ! 1000-1999
         UNDERTHOUSAND ; ! 100-999
         TENS          ; ! 20-99
         TEENS         ; ! 10-19
         ONES          ; ! 1-9
         NUMJAHKASAS   ;

LEXICON ONESG
 1:miljárda OVERMILLIONS ;
 2:guokte   GIGA         ;
 3:golbma   GIGA         ;
 4:njeallje GIGA         ;
 5:vihtta   GIGA         ;
 6:guhtta   GIGA         ;
 7:čieža    GIGA         ;
 8:gávcci   GIGA         ;
 9:ovcci    GIGA         ;

LEXICON GIGA
   :miljárdda OVERMILLIONS ;

LEXICON HUNDREDSM
 2:guokte   CUODIM ;
 3:golbma   CUODIM ;
 4:njeallje CUODIM ;
 5:vihtta   CUODIM ;
 6:guhtta   CUODIM ;
 7:čieža    CUODIM ;
 8:gávcci   CUODIM ;
 9:ovcci    CUODIM ;

LEXICON CUODIM
   :čuođi HUNDREDM ;


LEXICON HUNDREDM
       TENSM  ;
       TEENSM ;
   %0: ONESM  ;
 %0%0: MILJON ;

LEXICON TEENSM
 1: TEENM ;

LEXICON TEENM
 1:okta        LOHKAIM ;
 1:akta+Use/NG LOHKAIM ;
 2:guokte      LOHKAIM ;
 3:golbma      LOHKAIM ;
 4:njeallje    LOHKAIM ;
 5:vihtta      LOHKAIM ;
 6:guhtta      LOHKAIM ;
 7:čieža       LOHKAIM ;
 8:gávcci      LOHKAIM ;
 9:ovcci       LOHKAIM ;

LEXICON LOHKAIM
 :nuppelohkái MILJON ;

LEXICON TENSM
 1%0:logi     MILJON ;
   2:guokte   LOGIM  ;
   3:golbma   LOGIM  ;
   4:njeallje LOGIM  ;
   5:vihtta   LOGIM  ;
   6:guhtta   LOGIM  ;
   7:čieža    LOGIM  ;
   8:gávcci   LOGIM  ;
   9:ovcci    LOGIM  ;

LEXICON LOGIM
   %0:logi    MILJON ;
     :logi    ONESM  ;

LEXICON ONESM
 1:miljon   OVERTHOUSANDS ;
 2:guokte   MILJON        ;
 3:golbma   MILJON        ;
 4:njeallje MILJON        ;
 5:vihtta   MILJON        ;
 6:guhtta   MILJON        ;
 7:čieža    MILJON        ;
 8:gávcci   MILJON        ;
 9:ovcci    MILJON        ;

LEXICON MILJON
 :miljovnna OVERTHOUSANDS ;

! =================
! Under the million
! =================

LEXICON OVERMILLIONS
                          HUNDREDSM     ;
                  1:čuođi HUNDREDM      ;
                 %0:      TENSM         ;
                 %0:      TEENSM        ;
               %0%0:      MILJON        ;
               %0%0:      ONESM         ;
             %0%0%0:      HUNDREDST     ; ! x00.000.000
            %0%0%01:čuođi HUNDREDT      ; ! 100.000.000
           %0%0%0%0:      TENST         ; !  20.000.000
           %0%0%0%0:      TEENST        ; !  10.000.000
         %0%0%0%0%0:      ONEST         ; !   2.000.000
        %0%0%0%0%01:duhát THOUSAND      ; !   1.000.000
       %0%0%0%0%0%0:      UNDERTHOUSAND ;
     %0%0%0%0%0%0%0:      TENS          ;
     %0%0%0%0%0%0%0:      TEENS         ;
   %0%0%0%0%0%0%0%0:      ONES          ;
 %0%0%0%0%0%0%0%0%0:      COMMA         ; !  x000000

LEXICON OVERTHOUSANDS
                    HUNDREDST     ; ! x00.000.000
            1:čuođi HUNDREDT      ; ! 100.000.000
           %0:      TENST         ; !  20.000.000
           %0:      TEENST        ; !  10.000.000
         %0%0:      ONEST         ; !   2.000.000
        %0%01:duhát THOUSAND      ; !   1.000.000
       %0%0%0:      UNDERTHOUSAND ;
     %0%0%0%0:      TENS          ;
     %0%0%0%0:      TEENS         ;
   %0%0%0%0%0:      ONES          ;
 %0%0%0%0%0%0:      COMMA         ; !  x000000

LEXICON HUNDREDST
 2:guokte   CUODIT ; ! 200000-299999
 3:golbma   CUODIT ; ! 300000-399999
 4:njeallje CUODIT ; ! 400000-499999
 5:vihtta   CUODIT ; ! 500000-599999
 6:guhtta   CUODIT ; ! 600000-699999
 7:čieža    CUODIT ; ! 700000-799999
 8:gávcci   CUODIT ; ! 800000-899999
 9:ovcci    CUODIT ; ! 900000-999999

LEXICON CUODIT
 :čuođi HUNDREDT ; ! 100000-199999

LEXICON HUNDREDT	    ! X = 1-9, Y = 0-9
                  TENST     ; ! X2XYYY, X10YYY
                  TEENST    ; ! X1XYYY
  %01:okta        THOUSANDS ;
  %01:akta+Use/NG THOUSANDS ;
   %0:            ONEST     ; ! XX0YYY
 %0%0:            THOUSANDS ; ! X00YYY

LEXICON TEENST
 1: TEENT ;

LEXICON TEENT
 %0:logi        THOUSANDS ;
  1:okta        LOHKAIT   ;
  1:akta+Use/NG LOHKAIT   ;
  2:guokte      LOHKAIT   ;
  3:golbma      LOHKAIT   ;
  4:njeallje    LOHKAIT   ;
  5:vihtta      LOHKAIT   ;
  6:guhtta      LOHKAIT   ;
  7:čieža       LOHKAIT   ;
  8:gávcci      LOHKAIT   ;
  9:ovcci       LOHKAIT   ;

LEXICON LOHKAIT
 :nuppelohkái THOUSANDS ;

LEXICON TENST
 2:guoktelogi   LOGIT ;
 3:golbmalogi   LOGIT ;
 4:njealljelogi LOGIT ;
 5:vihttalogi   LOGIT ;
 6:guhttalogi   LOGIT ;
 7:čiežalogi    LOGIT ;
 8:gávccilogi   LOGIT ;
 9:ovccilogi    LOGIT ;

LEXICON LOGIT
 %0:            THOUSANDS ;
  1:okta        THOUSANDS ;
  1:akta+Use/NG THOUSANDS ;
                ONEST     ;

LEXICON ONEST
 2:guokte   THOUSANDS ;
 3:golbma   THOUSANDS ;
 4:njeallje THOUSANDS ;
 5:vihtta   THOUSANDS ;
 6:guhtta   THOUSANDS ;
 7:čieža    THOUSANDS ;
 8:gávcci   THOUSANDS ;
 9:ovcci    THOUSANDS ;

LEXICON THOUSANDS      ! x > 1
% :% duhát%  THOUSAND ;
  :% duhát%  THOUSAND ;

LEXICON THOUSAND
         UNDERTHOUSAND ; ! 1100-1999
     %0: TENS          ; ! 1020-1099
     %0: TEENS         ; ! 1010-1019
   %0%0: ONES          ; ! 1001-1009
 %0%0%0: COMMA         ; ! 1000

!===========================
!Here starts the 999 numbers
!===========================

LEXICON UNDERTHOUSAND
          HUNDREDS ;
 1:% čuođi%   HUNDRED  ;
 1:% čuođát%  CUODAAT  ;

LEXICON HUNDREDS
 2:guokte   CUODI ;
 3:golbma   CUODI ;
 4:njeallje CUODI ;
 5:vihtta   CUODI ;
 6:guhtta   CUODI ;
 7:čieža    CUODI ;
 8:gávcci   CUODI ;
 9:ovcci    CUODI ;

LEXICON CUODI
  :% čuođi%   HUNDRED ;
  :% čuođát%  CUODAAT ;

LEXICON HUNDRED
       TENS  ;
       TEENS ;
   %0: ONES  ;
 %0%0: COMMA ;

LEXICON CUODAAT
 %0%0.: COMMA ;


LEXICON TEENS
 1: TEEN ;

LEXICON TEEN
 1:okta        LOHKAI ;
 1:akta+Use/NG LOHKAI ;
 2:guokte      LOHKAI ;
 3:golbma      LOHKAI ;
 4:njeallje    LOHKAI ;
 5:vihtta      LOHKAI ;
 6:guhtta      LOHKAI ;
 7:čieža       LOHKAI ;
 8:gávcci      LOHKAI ;
 9:ovcci       LOHKAI ;

LEXICON LOHKAI
            :nuppelohkái  COMMA    ;
            :nuppelot  JAHKASAS ;
          %.: LOGAAT   ;

LEXICON LOGAAT
 :nuppelogát COMMA ;

LEXICON TENS
   1%0:logi     COMMA    ;
   1%0:logi     JAHKASAS ;
 1%0%.:logát    COMMA    ;
     2:guokte   LOGI     ;
     3:golbma   LOGI     ;
     4:njeallje LOGI     ;
     5:vihtta   LOGI     ;
     6:guhtta   LOGI     ;
     7:čieža    LOGI     ;
     8:gávcci   LOGI     ;
     9:ovcci    LOGI     ;


LEXICON LOGI
     %0:logi  COMMA       ;
       :logi  NUMJAHKASAS ;
! logát:%0    COMMA       ;
       :logi  ONES        ;
     %0:lot   JAHKASAS    ;
     %0:lot   LOHKU       ;
    %0.:logát ENDLEX      ;

LEXICON NUMJAHKASAS   			! experiment for TTS
 2:guovtte JAHKASAS ;
 3:golmma  JAHKASAS ;
 4:njealje JAHKASAS ;
 5:viđa    JAHKASAS ;
 6:guđa    JAHKASAS ;
 7:čieža   JAHKASAS ;
 8:gávcci  JAHKASAS ;
 9:ovcci   JAHKASAS ;


LEXICON LOHKU				   ! experiment for TTS
   -logu:logu   ENDLEX ;
  -logus:logus  ENDLEX ;
 -lohkui:lohkui ENDLEX ;
 -loguin:loguin ENDLEX ;

LEXICON JAHKASAS				! experiment for TTS
   -jahkása:jahkása   JAHKASAS_CASE ;
 -gearddása:gearddása JAHKASAS_CASE ;
   -mánnosa:mánnosa   JAHKASAS_CASE ;
  -vahkkosa:vahkkosa  JAHKASAS_CASE ;

LEXICON JAHKASAS_CASE				! experiment for TTS
        š ENDLEX ;
      čča ENDLEX ;
     ččat ENDLEX ;
    ččaid ENDLEX ;
     žžii ENDLEX ;
   ččaide ENDLEX ;
     ččas ENDLEX ;
    ččain ENDLEX ;
 ččaiguin ENDLEX ;
     žžan ENDLEX ;


LEXICON ONES
  CARDINAL ;
  ORDINAL  ;

LEXICON CARDINAL
 1:okta        COMMA ;
 1:akta+Use/NG COMMA ;
 2:guokte      COMMA ;
 3:golbma      COMMA ;
 4:njeallje    COMMA ;
 5:vihtta      COMMA ;
 6:guhtta      COMMA ;
 7:čieža       COMMA ;
 8:gávcci      COMMA ;
 9:ovcci       COMMA ;

LEXICON ORDINAL
 1%.:vuosttaš COMMA ;
 2%.:nubbi    COMMA ;
 3%.:goalmmát COMMA ;
 4%.:njealját COMMA ;
 5%.:viđát    COMMA ;
 6%.:guđát    COMMA ;
 7%.:čihččet  COMMA ;
 8%.:gávccát  COMMA ;
 9%.:ovccát   COMMA ;





! !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! !!!!!!!!!!                    !!!!!!!!!!!!!!!!!!!
! !!!!!!!!!!   Comma section    !!!!!!!!!!!!!!!!!!!
! !!!!!!!!!!                    !!!!!!!!!!!!!!!!!!!
! !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!


LEXICON COMMA
  ENDLEX       ;
  COMMASECTION ;

LEXICON COMMASECTION
                  ENDLEX        ;
  ,:% rihkku%     NUMBERSECTION ;
 %.:% čuokkis%    NUMBERSECTION ; ! these will overlap with dates
 %::% kolon%      NUMBERSECTION ;
 %-:% sárggis%    NUMBERSECTION ;
 %=:% lea%        NUMBERSECTION ;
 %°:% gráda%      NUMBERSECTION ;
  §:% paragráfa%  NUMBERSECTION ;
 %*:% násti%      NUMBERSECTION ;
  &:% ja%         NUMBERSECTION ;




! !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! !!!!!!!!!!                   !!!!!!!!!!!!!!!!!!!
! !!!!!!!!!!   Date section    !!!!!!!!!!!!!!!!!!!
! !!!!!!!!!!                   !!!!!!!!!!!!!!!!!!!
! !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

! [(0)[1-9]|[12][0-9]|3[01]]\.[3[01]]\.[([12][0-9])[0-9][0-9]]]
! 1.1.1934  = ođđajagimánu vuosttaš beaivi ovccinuppelotčuođigolbmaloginjeallje
! 3.11.2009 = skábmamánu goalmmát beaivi guokteduhátovcci
! 5.6.09    = geassemánu viđát beaivi nulla ovcci
! are there other formats in use?
! E.g. ská. 3. - ie combinations of abbr text and numbers?
! what about other orders? In Sweden, the year typically comes first:
! 2009.11.3
! Is this order found in Sámi texts, eg. from Sweden?
! and irrespective of written order, should the spoken order always be the same?

! Strategy:
! Use flag diacritics: Do the dates in one batch and the months in another
! Under this regime, we do this:
!            skábmamánu   { goalmmát | njealjjat }
! 3.@U.d.3@            11.  @U.d.3@    @U.d.4@
! Then, the flag diacritic will remove non-harmonic strings @R.3@...@D.4@
! And only the correct 3-3 string will be left intact.


LEXICON DATESECTION


 %0: SMALLDATEDAYMARK ; ! Eventual leading zero for one-digit day
     SMALLDATEDAYMARK ; ! No leading zero for one-digit day
     LARGEDATEDAYMARK ; ! No leading zero for two-digit day
     DATEYEAR         ; ! Centuries only

LEXICON SMALLDATEDAYMARK
 @U.DATE.1@1: DATEMONTHSEPARATOR ;
 @U.DATE.2@2: DATEMONTHSEPARATOR ;
 @U.DATE.3@3: DATEMONTHSEPARATOR ;
 @U.DATE.4@4: DATEMONTHSEPARATOR ;
 @U.DATE.5@5: DATEMONTHSEPARATOR ;
 @U.DATE.6@6: DATEMONTHSEPARATOR ;
 @U.DATE.7@7: DATEMONTHSEPARATOR ;
 @U.DATE.8@8: DATEMONTHSEPARATOR ;
 @U.DATE.9@9: DATEMONTHSEPARATOR ;

LEXICON LARGEDATEDAYMARK
 @U.DATE.10@1%0: DATEMONTHSEPARATOR ;
  @U.DATE.11@11: DATEMONTHSEPARATOR ;
  @U.DATE.12@12: DATEMONTHSEPARATOR ;
  @U.DATE.13@13: DATEMONTHSEPARATOR ;
  @U.DATE.14@14: DATEMONTHSEPARATOR ;
  @U.DATE.15@15: DATEMONTHSEPARATOR ;
  @U.DATE.16@16: DATEMONTHSEPARATOR ;
  @U.DATE.17@17: DATEMONTHSEPARATOR ;
  @U.DATE.18@18: DATEMONTHSEPARATOR ;
  @U.DATE.19@19: DATEMONTHSEPARATOR ;
 @U.DATE.20@2%0: DATEMONTHSEPARATOR ;
  @U.DATE.21@21: DATEMONTHSEPARATOR ;
  @U.DATE.22@22: DATEMONTHSEPARATOR ;
  @U.DATE.23@23: DATEMONTHSEPARATOR ;
  @U.DATE.24@24: DATEMONTHSEPARATOR ;
  @U.DATE.25@25: DATEMONTHSEPARATOR ;
  @U.DATE.26@26: DATEMONTHSEPARATOR ;
  @U.DATE.27@27: DATEMONTHSEPARATOR ;
  @U.DATE.28@28: DATEMONTHSEPARATOR ;
  @U.DATE.29@29: DATEMONTHSEPARATOR ;
 @U.DATE.30@3%0: DATEMONTHSEPARATOR ;
  @U.DATE.31@31: DATEMONTHSEPARATOR ;


LEXICON DATEMONTHSEPARATOR
  %.:%  MONTHZERO ;

LEXICON MONTHZERO
 %0: SMALLMONTH ; ! Eventual leading zero
     SMALLMONTH ; ! no leading zero
     LARGEMONTH ;


LEXICON SMALLMONTH
 1:ođđajágimánu DATEDAYSEPARATOR ;
 2:guovvamánu   DATEDAYSEPARATOR ;
 3:njukčamánu   DATEDAYSEPARATOR ;
 4:cuoŋománu    DATEDAYSEPARATOR ;
 5:miessemánu   DATEDAYSEPARATOR ;
 6:geassemánu   DATEDAYSEPARATOR ;
 7:suoidnemánu  DATEDAYSEPARATOR ;
 8:borgemánu    DATEDAYSEPARATOR ;
 9:čakčamánu    DATEDAYSEPARATOR ;

LEXICON LARGEMONTH
 1%0:golggotmánu DATEDAYSEPARATOR ;
  11:skábmamánu  DATEDAYSEPARATOR ;
  12:juovlamánu  DATEDAYSEPARATOR ;

LEXICON DATEDAYSEPARATOR
  %.:%  DATENAME ;


LEXICON DATENAME
  @U.DATE.1@:vuosttaš              DAYTEXT ;
  @U.DATE.2@:nubbi                 DAYTEXT ;
  @U.DATE.3@:goalmmát              DAYTEXT ;
  @U.DATE.4@:njealját              DAYTEXT ;
  @U.DATE.5@:viđát                 DAYTEXT ;
  @U.DATE.6@:guđát                 DAYTEXT ;
  @U.DATE.7@:čihččet               DAYTEXT ;
  @U.DATE.8@:gávccát               DAYTEXT ;
  @U.DATE.9@:ovccát                DAYTEXT ;
 @U.DATE.10@:logát                 DAYTEXT ;
 @U.DATE.11@:oktanuppelogát        DAYTEXT ;
 @U.DATE.11@:aktanuppelogát+Use/NG DAYTEXT ;
 @U.DATE.12@:guoktenuppelogát      DAYTEXT ;
 @U.DATE.13@:golbmanuppelogát      DAYTEXT ;
 @U.DATE.14@:njealljenuppelogát    DAYTEXT ;
 @U.DATE.15@:vihttanuppelogát      DAYTEXT ;
 @U.DATE.16@:guhttanuppelogát      DAYTEXT ;
 @U.DATE.17@:čiežanuppelogát       DAYTEXT ;
 @U.DATE.18@:gávccinuppelogát      DAYTEXT ;
 @U.DATE.19@:ovccinuppelogát       DAYTEXT ;
 @U.DATE.20@:guoktelogát           DAYTEXT ;
 @U.DATE.21@:guoktelogivuosttaš    DAYTEXT ;
 @U.DATE.22@:guokteloginubbi       DAYTEXT ;
 @U.DATE.23@:guoktelogigoalmmát    DAYTEXT ;
 @U.DATE.24@:guokteloginjealját    DAYTEXT ;
 @U.DATE.25@:guoktelogiviđát       DAYTEXT ;
 @U.DATE.26@:guoktelogiguđát       DAYTEXT ;
 @U.DATE.27@:guoktelogičihččet     DAYTEXT ;
 @U.DATE.28@:guoktelogigávccát     DAYTEXT ;
 @U.DATE.29@:guoktelogiovccát      DAYTEXT ;
 @U.DATE.30@:golbmalogát           DAYTEXT ;
 @U.DATE.31@:golbmalogivuosttaš    DAYTEXT ;


LEXICON DAYTEXT
 :% beaivi%  DATEYEAR ;

LEXICON DATEYEAR
 2:guokte   THOUSANDS      ; ! 2000 ->
 3:golbma   THOUSANDS      ; ! etc.->
 4:njeallje THOUSANDS      ;
 5:vihtta   THOUSANDS      ;
 6:guhtta   THOUSANDS      ;
 7:čieža    THOUSANDS      ;
 8:gávcci   THOUSANDS      ;
 9:ovcci    THOUSANDS      ;
            YEARTEENS      ; ! 1000 - 1999
            YEARHUNDREDS   ; ! 100-999
            TWODIGITYEAR   ; ! 10 - 99
%0:nolla%   ONES ; ! 01-09
            ENDLEX         ; ! no year

LEXICON YEARTEENS
 1: YEARTEEN ;

LEXICON YEARTEEN
 1:okta        YEARLOGAI ;
 1:akta+Use/NG YEARLOGAI ;
 2:guokte      YEARLOGAI ;
 3:golbma      YEARLOGAI ;
 4:njeallje    YEARLOGAI ;
 5:vihtta      YEARLOGAI ;
 6:guhtta      YEARLOGAI ;
 7:čieža       YEARLOGAI ;
 8:gávcci      YEARLOGAI ;
 9:ovcci       YEARLOGAI ;

LEXICON YEARLOGAI
 :nuppeloh YEARHUNDRED ;

LEXICON YEARHUNDREDS
! 1:okta        YEARHUNDRED ;
! 1:akta+Use/NG YEARHUNDRED ;
 2:guokte      YEARHUNDRED ;
 3:golbma      YEARHUNDRED ;
 4:njeallje    YEARHUNDRED ;
 5:vihtta      YEARHUNDRED ;
 6:guhtta      YEARHUNDRED ;
 7:čieža       YEARHUNDRED ;
 8:gávcci      YEARHUNDRED ;
 9:ovcci       YEARHUNDRED ;

LEXICON YEARHUNDRED
     :čuođi   TWODIGITYEAR ;
 %0%0:čuođi  ENDLEX ;

LEXICON TWODIGITYEAR
     TENS     ; ! 20-99
     TEENS    ; ! 10-19
 %0: CARDINAL ; ! 1-9

LEXICON ENDLEX
  # ;
