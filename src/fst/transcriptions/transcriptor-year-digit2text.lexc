! Divvun & Giellatekno - open source grammars for Sámi and other languages
! Copyright © 2000-2023 The University of Tromsø & the Norwegian Sámi Parliament
! http://giellatekno.uit.no & http://divvun.no
!
! This program is free software; you can redistribute and/or modify
! this file under the terms of the GNU General Public License as published by
! the Free Software Foundation, either version 3 of the License, or
! (at your option) any later version. The GNU General Public License
! is found at http://www.gnu.org/licenses/gpl.html. It is
! also available in the file $GTHOME/LICENSE.txt.
!
! Other licensing options are available upon request, please contact
! giellatekno@uit.no or feedback@divvun.no

! Input fra 1 til 9999, eksempler: 
! 1987
! 1987-2000

Multichar_Symbols

  +Use/NG    ! Do not generate, for isme-ped.fst and apertium
  @P.To.add@ 
  @R.To.add@


! Note! the diacritic marks in this file are only part of the lower
! representation. This means they can only be used from number to letter
! conversion of date and month representation, not the other way around.
! In order to get a two-way conversion, they must be added to the
! upper level as well.


LEXICON Root

! < [a|b|c|d|e|f|g|h|i|j|k|l|m|n|o|p|q|r|s|t|u|v|w|q|y|z|æ|ø|å|ä|ö|á|č|đ|ŋ|A|B|C|D|E|F|G|H|I|J|K|L|M|N|O|P|Q|R|S|T|U|V|W|X|Y|Z|Æ|Ø|Å|Ä|Ö|Á|Č|Đ|Ŋ|Š|Ŧ|Ž]* > #             ; ! This first line is to allow all letter strings.

! The regex in the previous line is now commented out. If you want this fst to
! pass through all letters untouched (but transform all numbers)
! then comment it in again. It becomes critical for the invers sme-inum.fst, that
! does not work with this line. If it really is needed, we might filter it out
! for sme-inum.fst.

  DATEYEAR ; 
  PERIOD ;

LEXICON DATEYEAR
 1%0:duhát    HUNDRED  ; ! 1000 -> 1099
 1+Use/NG:   THOUSANDS     ; ! 1000 -> alternativ måte, duhátovccičuođi...
 2:guokte   THOUSANDS      ; ! 2000 ->
 3:golbma   THOUSANDS      ; ! etc.->
 4:njeallje THOUSANDS      ;
 5:vihtta   THOUSANDS      ;
 6:guhtta   THOUSANDS      ;
 7:čieža    THOUSANDS      ;
 8:gávcci   THOUSANDS      ;
 9:ovcci    THOUSANDS      ;
            YEARTEENS      ; ! 1000 - 1999
            YEARHUNDREDS   ; ! 100-999
            TENS           ;
            ONES           ;

LEXICON THOUSANDS      ! x > 1
 :duhát  THOUSAND ;

LEXICON THOUSAND
         UNDERTHOUSAND ; ! 1100-1999
     %0: TENS          ; ! 1020-1099
     %0: TEENS         ; ! 1010-1019
   %0%0: ONES          ; ! 1001-1009


!===========================
!Here starts the 999 numbers
!===========================

LEXICON UNDERTHOUSAND
          HUNDREDS ;
 1:čuođi  HUNDRED  ;

LEXICON HUNDREDS
 2:guokte   CUODI ;
 3:golbma   CUODI ;
 4:njeallje CUODI ;
 5:vihtta   CUODI ;
 6:guhtta   CUODI ;
 7:čieža    CUODI ;
 8:gávcci   CUODI ;
 9:ovcci    CUODI ;

LEXICON CUODI
  :čuođi  HUNDRED ;

LEXICON HUNDRED
       TENS  ;
       TEENS ;
   %0: ONES  ;

LEXICON TEENS
 1: TEEN ;

LEXICON TEEN
 1:okta        LOHKAI ;
 1:akta+Use/NG LOHKAI ;
 2:guokte      LOHKAI ;
 3:golbma      LOHKAI ;
 4:njeallje    LOHKAI ;
 5:vihtta      LOHKAI ;
 6:guhtta      LOHKAI ;
 7:čieža       LOHKAI ;
 8:gávcci      LOHKAI ;
 9:ovcci       LOHKAI ;



LEXICON TENS
   1%0:logi     ENDLEX    ;
     2:guokte   LOGI     ;
     3:golbma   LOGI     ;
     4:njeallje LOGI     ;
     5:vihtta   LOGI     ;
     6:guhtta   LOGI     ;
     7:čieža    LOGI     ;
     8:gávcci   LOGI     ;
     9:ovcci    LOGI     ;


LEXICON LOGI
       :logi  ONES        ;



LEXICON ONES
 1:okta        ENDLEX ;
 1:akta+Use/NG ENDLEX ;
 2:guokte      ENDLEX ;
 3:golbma      ENDLEX ;
 4:njeallje    ENDLEX ;
 5:vihtta      ENDLEX ;
 6:guhtta      ENDLEX ;
 7:čieža       ENDLEX ;
 8:gávcci      ENDLEX ;
 9:ovcci       ENDLEX ;
 %0:       ENDLEX ;


LEXICON LOHKAI
 :nuppelohkái  ENDLEX ;


LEXICON YEARTEENS
 1: YEARTEEN ;

LEXICON YEARTEEN
 1:okta        YEARLOGAI ;
 1:akta+Use/NG YEARLOGAI ;
 2:guokte      YEARLOGAI ;
 3:golbma      YEARLOGAI ;
 4:njeallje    YEARLOGAI ;
 5:vihtta      YEARLOGAI ;
 6:guhtta      YEARLOGAI ;
 7:čieža       YEARLOGAI ;
 8:gávcci      YEARLOGAI ;
 9:ovcci       YEARLOGAI ;

LEXICON YEARLOGAI
 :nuppelot  YEARHUNDRED ;
! :nuppelotčuođi%  TENS ;

LEXICON YEARHUNDREDS
 1:            YEARHUNDRED ;
 1:            YEARHUNDRED ;
 2:guokte      YEARHUNDRED ;
 3:golbma      YEARHUNDRED ;
 4:njeallje    YEARHUNDRED ;
 5:vihtta      YEARHUNDRED ;
 6:guhtta      YEARHUNDRED ;
 7:čieža       YEARHUNDRED ;
 8:gávcci      YEARHUNDRED ;
 9:ovcci       YEARHUNDRED ;

LEXICON YEARHUNDRED
   %0:čuođi  ONES    ;
     :čuođi  TENS    ;
     :čuođi  TEENS    ;
 %0%0:čuođi  ENDLEX ;

!===========================
!Here starts the period, e.g. 2010-2022
!===========================


LEXICON PERIOD
! :jagiid%  P-DATEYEAR ; !mulighet for å legge til et ord foran uttrykket
            P-DATEYEAR ; !egen automat for perioder, pga genitiv i siste del

LEXICON P-DATEYEAR
 1%0:duhát  P-HUNDRED  ; ! 1000 -> 1099
 1+Use/NG:  P-THOUSANDS     ; ! 1000 -> alternativ måte
 2:guokte   P-THOUSANDS      ; ! 2000 ->
 3:golbma   P-THOUSANDS      ; ! etc.->
 4:njeallje P-THOUSANDS      ;
 5:vihtta   P-THOUSANDS      ;
 6:guhtta   P-THOUSANDS      ;
 7:čieža    P-THOUSANDS      ;
 8:gávcci   P-THOUSANDS      ;
 9:ovcci    P-THOUSANDS      ;
            P-YEARTEENS      ; ! 1000 - 1999
            P-YEARHUNDREDS   ; ! 100-999
            P-TENS           ;
            P-ONES           ;
 1:         P-HOUNDRED-THOUSAND ;
 2:guovtte  P-HOUNDRED-THOUSAND ; ! 2000 ->
 3:golmma   P-HOUNDRED-THOUSAND ; ! etc.->
 4:njealje  P-HOUNDRED-THOUSAND ;
 5:viđa     P-HOUNDRED-THOUSAND ;
 6:guđa     P-HOUNDRED-THOUSAND ;
 7:čieža    P-HOUNDRED-THOUSAND ;
 8:gávcci   P-HOUNDRED-THOUSAND ;
 9:ovcci    P-HOUNDRED-THOUSAND ;



LEXICON P-THOUSANDS      ! x > 1
 :duhát  P-THOUSAND ;

LEXICON P-THOUSAND
         P-UNDERTHOUSAND ; ! 1100-1999
     %0: P-TENS          ; ! 1020-1099
     %0: P-TEENS         ; ! 1010-1019
!  %0%0: P-ONES          ; ! 1001-1009

!===========================
!Here starts the 999 numbers
!===========================

LEXICON P-UNDERTHOUSAND
          P-HUNDREDS ;
 1:čuođi  P-HUNDRED  ;

LEXICON P-HUNDREDS
 2:guokte   P-CUODI ;
 3:golbma   P-CUODI ;
 4:njeallje P-CUODI ;
 5:vihtta   P-CUODI ;
 6:guhtta   P-CUODI ;
 7:čieža    P-CUODI ;
 8:gávcci   P-CUODI ;
 9:ovcci    P-CUODI ;

LEXICON P-CUODI
  :čuođi  P-HUNDRED ;

LEXICON P-HUNDRED
       P-TENS  ;
       P-TEENS ;
   %0: P-ONES  ;

LEXICON P-TEENS
 1: P-TEEN ;

LEXICON P-TEEN
 1:okta        P-LOHKAI ;
 1:akta+Use/NG P-LOHKAI ;
 2:guokte      P-LOHKAI ;
 3:golbma      P-LOHKAI ;
 4:njeallje    P-LOHKAI ;
 5:vihtta      P-LOHKAI ;
 6:guhtta      P-LOHKAI ;
 7:čieža       P-LOHKAI ;
 8:gávcci      P-LOHKAI ;
 9:ovcci       P-LOHKAI ;



LEXICON P-TENS
   1%0:logi     TOflag ;
   1%0:logi     FROMflag ;
     2:guokte   P-LOGI ;
     2:guovtte  P-ONLYLOGI ; !genitiv for guovttelogi
     3:golbma   P-LOGI ;
     3:golmma   P-ONLYLOGI ; !genitiv for golmmalogi
     4:njeallje P-LOGI ;
     4:njealje  P-ONLYLOGI ; !genitiv for njealjelogi
     5:vihtta   P-LOGI ;
     5:viđa     P-ONLYLOGI ; !genitiv for viđalogi
     6:guhtta   P-LOGI ;
     6:guđa     P-ONLYLOGI ; !genitiv for guđalogi
     7:čieža    P-LOGI ;
     7:čieža    P-ONLYLOGI ;
     8:gávcci   P-LOGI ;
     8:gávcci   P-ONLYLOGI ;
     9:ovcci    P-LOGI ;
     9:ovcci    P-ONLYLOGI ;

LEXICON P-LOGI
       :logi  P-ONES ;

LEXICON P-ONLYLOGI !gir genitiv, guovttelogi, når det ikke er enere i uttrykket
 %0:logi  FROMflag ;
 %0:logi  TOflag ;

LEXICON FROMflag ! bare uttrykk med bindestrek får flagg
@P.To.add@ FROM ;

LEXICON TOflag ! bare uttrykk med flagg blir godkjent videre
@R.To.add@ TO ;


LEXICON P-ONES ! !genitiv for enere
 1:ovtta        FROMflag ;
 1:avtta+Use/NG FROMflag ;
 2:guovtti      FROMflag ;
 3:golmma       FROMflag ;
 4:njealji      FROMflag ;
 5:viđa         FROMflag ;
 6:guđa         FROMflag ;
 7:čieža        FROMflag ;
 8:gávcci       FROMflag ;
 9:ovcci        FROMflag ;
 1:ovtta        TOflag ;
 1:avtta+Use/NG TOflag ;
 2:guovtti      TOflag ;
 3:golmma       TOflag ;
 4:njealji      TOflag ;
 5:viđa         TOflag ;
 6:guđa         TOflag ;
 7:čieža        TOflag ;
 8:gávcci       TOflag ;
 9:ovcci        TOflag ;


LEXICON P-LOHKAI
 :nuppelogi  FROMflag ;
 :nuppelogi  TOflag ;


LEXICON P-YEARTEENS
 1:  P-YEARTEEN ;

LEXICON P-YEARTEEN
 1:okta        P-YEARLOGAI ;
 1:akta+Use/NG P-YEARLOGAI ;
 2:guokte      P-YEARLOGAI ;
 3:golbma      P-YEARLOGAI ;
 4:njeallje    P-YEARLOGAI ;
 5:vihtta      P-YEARLOGAI ;
 6:guhtta      P-YEARLOGAI ;
 7:čieža       P-YEARLOGAI ;
 8:gávcci      P-YEARLOGAI ;
 9:ovcci       P-YEARLOGAI ;

LEXICON P-YEARLOGAI
 :nuppelot  P-YEARHUNDRED ;

LEXICON P-YEARHUNDREDS
 1:            P-YEARHUNDRED ;
 1:            P-YEARHUNDRED ;
 2:guokte      P-YEARHUNDRED ;
 3:golbma      P-YEARHUNDRED ;
 4:njeallje    P-YEARHUNDRED ;
 5:vihtta      P-YEARHUNDRED ;
 6:guhtta      P-YEARHUNDRED ;
 7:čieža       P-YEARHUNDRED ;
 8:gávcci      P-YEARHUNDRED ;
 9:ovcci       P-YEARHUNDRED ;

LEXICON P-YEARHUNDRED
   %0:čuođi  P-ONES    ;
     :čuođi  P-TENS    ;
     :čuođi  P-TEENS    ;
             TOflag ;


LEXICON P-HOUNDRED-THOUSAND
 %0%0%0:duhát FROMflag ;
 %0%0%0:duhát TOflag ;
 %0%0:čuođi   FROMflag ;
 %0%0:čuođi   TOflag ;

LEXICON FROM
-:% rájes%  P-DATEYEAR ;

LEXICON TO
  :% rádjai ENDLEX ;

LEXICON ENDLEX
  # ;
