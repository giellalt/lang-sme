! Divvun & Giellatekno - open source grammars for Sámi and other languages
! Copyright © 2000-2010 The University of Tromsø & the Norwegian Sámi Parliament
! http://giellatekno.uit.no & http://divvun.no
!
! This program is free software; you can redistribute and/or modify
! this file under the terms of the GNU General Public License as published by
! the Free Software Foundation, either version 3 of the License, or
! (at your option) any later version. The GNU General Public License
! is found at http://www.gnu.org/licenses/gpl.html. It is
! also available in the file $GTHOME/LICENSE.txt.
!
! Other licensing options are available upon request, please contact
! giellatekno@uit.no or feedback@divvun.no

! ==================== !
! The North Sámi dates !
! ==================== !

! Input:
! 3.1.
! 03.01.
! 12.11.23
! 12.11.2023


Multichar_Symbols !flagg fordi tallet som angir måneden kommer etter dag, men i output vil vi ha månedsnavnet først

@U.MONTH.1@ ! flagg å knytte 1 til ođđajagemánnu 
@U.MONTH.2@ ! flagg å knytte 2 til guovvamánnu
@U.MONTH.3@
@U.MONTH.4@
@U.MONTH.5@
@U.MONTH.6@
@U.MONTH.7@
@U.MONTH.8@
@U.MONTH.9@
@U.MONTH.10@
@U.MONTH.11@
@U.MONTH.12@

+Use/NG ! For substandard month names used in Oahpa
        ! They will be accepted but not generated.

LEXICON Root

         @U.MONTH.1@:ođđajagemánu% @U.MONTH.1@ 31DAYS ;
  @U.MONTH.1@+Use/NG:garjjámánu% @U.MONTH.1@   31DAYS ;
  @U.MONTH.1@+Use/NG:Ođđajagemánu% @U.MONTH.1@ 31DAYS ;
  @U.MONTH.1@+Use/NG:Garjjámánu% @U.MONTH.1@   31DAYS ;

         @U.MONTH.2@:guovvamánu% @U.MONTH.2@   29DAYS ;
  @U.MONTH.2@+Use/NG:Guovvamánu% @U.MONTH.2@   29DAYS ;

         @U.MONTH.3@:njukčamánu% @U.MONTH.3@   31DAYS ;
  @U.MONTH.3@+Use/NG:Njukčamánu% @U.MONTH.3@   31DAYS ;

         @U.MONTH.4@:cuoŋománu% @U.MONTH.4@    30DAYS ;
  @U.MONTH.4@+Use/NG:gáranasmánu% @U.MONTH.4@  30DAYS ;
  @U.MONTH.4@+Use/NG:vuoražismánu% @U.MONTH.4@ 30DAYS ;
  @U.MONTH.4@+Use/NG:Cuoŋománu% @U.MONTH.4@    30DAYS ;
  @U.MONTH.4@+Use/NG:Gáranasmánu% @U.MONTH.4@  30DAYS ;
  @U.MONTH.4@+Use/NG:Vuoražismánu% @U.MONTH.4@ 30DAYS ;

         @U.MONTH.5@:miessemánu% @U.MONTH.5@   31DAYS ;
  @U.MONTH.5@+Use/NG:Miessemánu% @U.MONTH.5@   31DAYS ;

         @U.MONTH.6@:geassemánu% @U.MONTH.6@   30DAYS ;
  @U.MONTH.6@+Use/NG:Geassemánu% @U.MONTH.6@   30DAYS ;

         @U.MONTH.7@:suoidnemánu% @U.MONTH.7@  31DAYS ;
  @U.MONTH.7@+Use/NG:Suoidnemánu% @U.MONTH.7@  31DAYS ;

         @U.MONTH.8@:borgemánu% @U.MONTH.8@    31DAYS ;
  @U.MONTH.8@+Use/NG:šnjilčamánu% @U.MONTH.8@  31DAYS ;
  @U.MONTH.8@+Use/NG:Borgemánu% @U.MONTH.8@    31DAYS ;
  @U.MONTH.8@+Use/NG:Šnjilčamánu% @U.MONTH.8@  31DAYS ;

         @U.MONTH.9@:čakčamánu% @U.MONTH.9@    30DAYS ;
  @U.MONTH.9@+Use/NG:Čakčamánu% @U.MONTH.9@    30DAYS ;

        @U.MONTH.10@:golggotmánu% @U.MONTH.10@ 31DAYS ;
 @U.MONTH.10@+Use/NG:ragatmánu% @U.MONTH.10@   31DAYS ;
 @U.MONTH.10@+Use/NG:Golggotmánu% @U.MONTH.10@ 31DAYS ;
 @U.MONTH.10@+Use/NG:Ragatmánu% @U.MONTH.10@   31DAYS ;

        @U.MONTH.11@:skábmamánu% @U.MONTH.11@  30DAYS ;
 @U.MONTH.11@+Use/NG:Skábmamánu% @U.MONTH.11@  30DAYS ;

        @U.MONTH.12@:juovlamánu% @U.MONTH.12@  31DAYS ;
 @U.MONTH.12@+Use/NG:Juovlamánu% @U.MONTH.12@  31DAYS ;


LEXICON 31DAYS
 31:golbmalogivuosttaš B      ;
                       30DAYS ;

LEXICON 30DAYS
 3%0:golbmalogát B      ;
                 29DAYS ;

LEXICON 29DAYS
 %01:vuosttaš              B ; !input med null foran dag
 %02:nuppi                 B ;
 %03:goalmmát              B ;
 %04:njealját              B ;
 %05:viđát                 B ;
 %06:guđát                 B ;
 %07:čihččet               B ;
 %08:gávccát               B ;
 %09:ovccát                B ;
   1:vuosttaš              B ; !input uten null foran dag
   2:nuppi                 B ;
   3:goalmmát              B ;
   4:njealját              B ;
   5:viđát                 B ;
   6:guđát                 B ;
   7:čihččet               B ;
   8:gávccát               B ;
   9:ovccát                B ;
 1%0:logát                 B ;
  11:oktanuppelogát        B ;
  11:aktanuppelogát+Use/NG B ;
  12:guoktenuppelogát      B ;
  13:golbmanuppelogát      B ;
  14:njealljenuppelogát    B ;
  15:vihttanuppelogát      B ;
  16:guhttanuppelogát      B ;
  17:čiežanuppelogát       B ;
  18:gávccinuppelogát      B ;
  19:ovccinuppelogát       B ;
 2%0:guoktelogát           B ;
  21:guoktelogivuosttaš    B ;
  22:guokteloginuppi       B ;
  23:guoktelogigoalmmát    B ;
  24:guokteloginjealját    B ;
  25:guoktelogiviđát       B ;
  26:guoktelogiguđát       B ;
  27:guoktelogičihččet     B ;
  28:guoktelogigávccát     B ;
  29:guoktelogiovccát      B ;


LEXICON B
         %.:% beaivve  MONTH ; ! written form: 12. beaivve

LEXICON MONTH
  %01@U.MONTH.1@:@U.MONTH.1@  X ; !input med null foran månedstallet
  %02@U.MONTH.2@:@U.MONTH.2@  X ;
  %03@U.MONTH.3@:@U.MONTH.3@  X ;
  %04@U.MONTH.4@:@U.MONTH.4@  X ;
  %05@U.MONTH.5@:@U.MONTH.5@  X ;
  %06@U.MONTH.6@:@U.MONTH.6@  X ;
  %07@U.MONTH.7@:@U.MONTH.7@  X ;
  %08@U.MONTH.8@:@U.MONTH.8@  X ;
  %09@U.MONTH.9@:@U.MONTH.9@  X ;
    1@U.MONTH.1@:@U.MONTH.1@  X ; !input uten null foran månedstallet
    2@U.MONTH.2@:@U.MONTH.2@  X ;
    3@U.MONTH.3@:@U.MONTH.3@  X ;
    4@U.MONTH.4@:@U.MONTH.4@  X ;
    5@U.MONTH.5@:@U.MONTH.5@  X ;
    6@U.MONTH.6@:@U.MONTH.6@  X ;
    7@U.MONTH.7@:@U.MONTH.7@  X ;
    8@U.MONTH.8@:@U.MONTH.8@  X ;
    9@U.MONTH.9@:@U.MONTH.9@  X ;
 1%0@U.MONTH.10@:@U.MONTH.10@ X ;
  11@U.MONTH.11@:@U.MONTH.11@ X ;
  12@U.MONTH.12@:@U.MONTH.12@ X ;

LEXICON X
       %.: ENDLEX ;
  +Use/NG: ENDLEX ; !input uten punktum
      %.:%   DATEYEAR ; ! sti til årstall


LEXICON DATEYEAR
 1+Use/NG:         THOUSANDS      ; ! 1000 -> alternativ måte, duhátovccičuođi...
 2:guokte   THOUSANDS      ; ! 2000 ->
 3:golbma   THOUSANDS      ; ! etc.->
 4:njeallje THOUSANDS      ;
 5:vihtta   THOUSANDS      ;
 6:guhtta   THOUSANDS      ;
 7:čieža    THOUSANDS      ;
 8:gávcci   THOUSANDS      ;
 9:ovcci    THOUSANDS      ;
            YEARTEENS      ; ! 1000 - 1999
            YEARHUNDREDS   ; ! 100-999
            TENS           ;
            ONES           ;

LEXICON THOUSANDS      ! x > 1
 :duhát  THOUSAND ;

LEXICON THOUSAND
         UNDERTHOUSAND ; ! 1100-1999
     %0: TENS          ; ! 1020-1099
     %0: TEENS         ; ! 1010-1019
   %0%0: ONES          ; ! 1001-1009

!===========================
!Here starts the 999 numbers
!===========================

LEXICON UNDERTHOUSAND
          HUNDREDS ;
 1:čuođi  HUNDRED  ;

LEXICON HUNDREDS
 2:guokte   CUODI ;
 3:golbma   CUODI ;
 4:njeallje CUODI ;
 5:vihtta   CUODI ;
 6:guhtta   CUODI ;
 7:čieža    CUODI ;
 8:gávcci   CUODI ;
 9:ovcci    CUODI ;

LEXICON CUODI
  :čuođi  HUNDRED ;

LEXICON HUNDRED
       TENS  ;
       TEENS ;
   %0: ONES  ;

LEXICON TEENS
 1: TEEN ;

LEXICON TEEN
 1:okta        LOHKAI ;
 1:akta+Use/NG LOHKAI ;
 2:guokte      LOHKAI ;
 3:golbma      LOHKAI ;
 4:njeallje    LOHKAI ;
 5:vihtta      LOHKAI ;
 6:guhtta      LOHKAI ;
 7:čieža       LOHKAI ;
 8:gávcci      LOHKAI ;
 9:ovcci       LOHKAI ;

LEXICON TENS
   1%0:logi     ENDLEX    ;
     2:guokte   LOGI     ;
     3:golbma   LOGI     ;
     4:njeallje LOGI     ;
     5:vihtta   LOGI     ;
     6:guhtta   LOGI     ;
     7:čieža    LOGI     ;
     8:gávcci   LOGI     ;
     9:ovcci    LOGI     ;


LEXICON LOGI
       :logi  ONES        ;

LEXICON ONES
 1:okta        ENDLEX ;
 1:akta+Use/NG ENDLEX ;
 2:guokte      ENDLEX ;
 3:golbma      ENDLEX ;
 4:njeallje    ENDLEX ;
 5:vihtta      ENDLEX ;
 6:guhtta      ENDLEX ;
 7:čieža       ENDLEX ;
 8:gávcci      ENDLEX ;
 9:ovcci       ENDLEX ;
 %0:       ENDLEX ;

LEXICON LOHKAI
 :nuppelohkái  ENDLEX ;

LEXICON YEARTEENS
 1: YEARTEEN ;

LEXICON YEARTEEN
 1:okta        YEARLOGAI ;
 1:akta+Use/NG YEARLOGAI ;
 2:guokte      YEARLOGAI ;
 3:golbma      YEARLOGAI ;
 4:njeallje    YEARLOGAI ;
 5:vihtta      YEARLOGAI ;
 6:guhtta      YEARLOGAI ;
 7:čieža       YEARLOGAI ;
 8:gávcci      YEARLOGAI ;
 9:ovcci       YEARLOGAI ;

LEXICON YEARLOGAI
 :nuppelot  YEARHUNDRED ;

LEXICON YEARHUNDREDS
 1:            YEARHUNDRED ;
 2:guokte      YEARHUNDRED ;
 3:golbma      YEARHUNDRED ;
 4:njeallje    YEARHUNDRED ;
 5:vihtta      YEARHUNDRED ;
 6:guhtta      YEARHUNDRED ;
 7:čieža       YEARHUNDRED ;
 8:gávcci      YEARHUNDRED ;
 9:ovcci       YEARHUNDRED ;

LEXICON YEARHUNDRED
   %0:čuođi  ONES    ;
     :čuođi  TENS    ;
     :čuođi  TEENS    ;
 %0%0:čuođi  ENDLEX ;

LEXICON ENDLEX
  # ;
