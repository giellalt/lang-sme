! Proper nouns
! ============

! Table of content
! ----------------
! Continuation lexica
! Personal names
!  First names
!  Surnames
! Place names
! ----------------



! Continuation lexica
! ===================

! Continuation lexica for personal names
! --------------------------------------

! There are 169 doublets in the STURLA and EIDE lexica. They can be conflated, 
! but only if the (today superluous) dsitinction betwwen 1st and 2nd name is
! abandoned. (020125 TT)

! First names in vowel
LEXICON VFIRSTNAME
 +N+Sg+Nom: K ;
 +N+Sg+Gen: K ;
 +N+Sg+Acc: K ;
 +N+Sg+Ill:i K ;
 +N+Sg+Loc:s K ;
 +N+Sg+Com:in K ;
 +N+Ess:n K ;

! Surnames in vowel
LEXICON VSURNAME
 VFIRSTNAME ;

! First names in consonant
LEXICON CFIRSTNAME
 +N+Sg+Nom: K ;
 +N+Sg+Gen:a K ;
 +N+Sg+Acc:a K ;
 +N+Sg+Ill:i K ;
 +N+Sg+Loc:s K ;
 +N+Sg+Com:in K ;
 +N+Ess:n K ;



! Continuation lexica for foreign place names
! -------------------------------------------

!Now testing, giving both versions

LEXICON CPLACE
 +N+Sg+Nom: K ;
 +N+Sg+Gen:a K ;
 +N+Sg+Acc:a K ;
 +N+Sg+Ill:ii K ;
 +N+Sg+Loc+isuf:is K ;
 +N+Sg+Loc+asuf:as K ;
 +N+Sg+Com+isuf:iin K ;
 +N+Sg+Com+asuf:ain K ;
 +N+Ess+isuf:in K ;
 +N+Ess+asuf:an K ;

LEXICON LONDON ! Final foot structure (X.) and (X..) => Loc:is

 +N+Sg+Nom: K ;
 +N+Sg+Gen:a K ;
 +N+Sg+Acc:a K ;
 +N+Sg+Ill:ii K ;
 +N+Sg+Loc:is K ;
 +N+Sg+Com:iin K ;
 +N+Ess:in K ;

LEXICON BERN ! Final foot structure (X) => Loc:as

 +N+Sg+Nom: K ;
 +N+Sg+Gen:a K ;
 +N+Sg+Acc:a K ;
 +N+Sg+Ill:ii K ;
 +N+Sg+Loc:as K ;
 +N+Sg+Com:ain K ;
 +N+Ess:an K ;

LEXICON ACCRA ! Vowel final name

 +N+Sg+Nom: K ;
 +N+Sg+Gen: K ;
 +N+Sg+Acc: K ;
 +N+Sg+Ill:i K ;
 +N+Sg+Loc:s K ;
 +N+Sg+Com:in K ;
 +N+Ess:n K ;



! The lexica
! ==========




LEXICON ProperNoun
 SamiProperNoun ;
 GeneralProperNoun ;
! pointing to separate file.

LEXICON SamiProperNoun

! Personal names
! --------------

! Sami personal names
! -------------------

 Niillas:Nijllas NIILLAS "Nicholas NP" ;
 Piera PIERA "Peter NP" ;
 Heandarat:Heandarah'k HEANDARAT "Henry NP" ;
! Davvet:Davved  ?? ;
Abraham:Abraham2 CFIRSTNAME ;
 Biehta1r CFIRSTNAME ;
 Ba1vlos CFIRSTNAME ;
 Ma1rja1 VFIRSTNAME ;
 Movsses CFIRSTNAME ;
 Ma1rta1 VFIRSTNAME ;
 Johanas CFIRSTNAME ;
 Ja1hkob CFIRSTNAME ;
 Jovsset:Jovsseh'k HEANDARAT ;


