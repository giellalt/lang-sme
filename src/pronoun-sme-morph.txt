! Divvun & Giellatekno - open source grammars for Sámi and other languages
! Copyright © 2000-2010 The University of Tromsø & the Norwegian Sámi Parliament
! http://giellatekno.uit.no & http://divvun.no
!
! This program is free software; you can redistribute and/or modify
! this file under the terms of the GNU General Public License as published by
! the Free Software Foundation, either version 3 of the License, or
! (at your option) any later version. The GNU General Public License
! is found at http://www.gnu.org/licenses/gpl.html. It is
! also available in the file $GTHOME/LICENSE.txt.
!
! Other licensing options are available upon request, please contact
! giellatekno@hum.uit.no or divvun@samediggi.no

! ################################################## !
!This file contains the morphology for the  Pronouns !
! ################################################## !


! Pronoun
! =======



! Personal pronouns
! -----------------


LEXICON MUNLEX
+Sg1: firstperspronsg ;
+Du1: firstpersprondu ;
+Pl1: perspronpl ;

LEXICON DONLEX
+Sg2: nonfirstperspronsg ;
+Du2: nonfirstpersprondu ;
+Pl2: perspronpl ;

LEXICON SONLEX
+Sg3: nonfirstperspronsg ;
+Du3: nonfirstpersprondu ;
+Pl3: perspronpl ;


!Case suffixes for 1st Sg, pointing to K lexicon
LEXICON firstperspronsg
+Nom:u%>n K ;
+Nom+Use/NG:o%>n K ; ! 
+Acc:u%> K ;
+Gen:u%> K ;
+Gen:u»# NAMAT ;
+Ill:u%>nnje K ;
+Loc:u%>s K ;
+Com:u%>jnna K ;
+Ess:u%>nin K ;

!Case suffixes for non-1st Sg, pointing to K lexicon
LEXICON nonfirstperspronsg
+Nom:o%>n K ;
+Acc:u%> K ;
+Gen:u%> K ;
+Gen:u»# NAMAT ;
+Ill:u%>tnje K ;
+Loc:u%>s K ;
+Com:u%>jnna K ;
+Ess:u%>nin K ;

!Case suffixes for 1st Du, pointing to K lexicon
LEXICON firstpersprondu
+Nom:oaj%> K ;
+Acc:unno%> K ;
+Gen:unno%> K ;
+Gen:unno»# NAMAT ;
+Ill:unnu%>jde K ;
+Loc:unno%>s K ;
+Com:unnu%>jn K ;
+Ess:unno%>n K ;

!Case suffixes for non-1st Du, pointing to K lexicon
LEXICON nonfirstpersprondu
+Nom:oaj%> K ;
+Acc:udno%> K ;
+Gen:udno%> K ;
+Acc+Use/Sub:utno%> K ; !
+Gen+Use/Sub:utno%> K ; !
+Gen:udno»# NAMAT ;
+Ill:udnu%>jde K ;
+Ill+Use/Sub:utnu%>jde K ; !
+Loc:udno%>s K ;
+Loc+Use/Sub:utno%>s K ; !
+Com:udnu%>jn K ;
+Com+Use/Sub:utnu%>jn K ; !
+Ess:udno%>n K ;
+Ess+Use/Sub:utno%>n K ; !

!Case suffixes for all Pl forms, pointing to K lexicon
LEXICON perspronpl
+Nom:i%>j K ;
+Acc:i%>n K ;
+Gen:i%>n K ;
+Gen:i%>n»# NAMAT ;
+Ill+Use/Sub:i%>nsij K ; !
+Ill:i%>djijde K ;
+Ill+Use/Sub:i%>nsiidda K ; 
+Loc:i%>s K ;
+Loc+Use/Sub:i%>js K ; ! !SOUTH
+Com:i%>ngujn K ;
+Ess:i%>nin K ;

LEXICON datpers
+Sg3:0 demcassg ;
+Pl3:0 demcaspl ;
+Ess:nin K ;




! Interrogative and relative pronouns
! -----------------------------------

LEXICON MIIINTERLEX
+Sg+Nom:mij%> K-son ;
+Sg+Acc+Use/Sub:májd9 K-son ; !
:ma%> oblintercas ;

LEXICON GIIINTERLEX
+Sg+Nom:gij%> K-son ;
+Sg+Ill+Foc:gea%>sagii # ; ! Now both Interr and Rel. ok?
:gea%>X5 oblintercas ;

LEXICON GUHTELEX
:guh'te GUHTECASE ;
+Sg+Nom:guhte%>s K ;

LEXICON GUHTEMUSLEX
:guđemužž GUHTEMUŠCASE ;
:guhtemuš%> K-son ; !deviating Nom alternant.



LEXICON oblintercas
+Sg+Acc+Use/NG:jd9 K-son ; ! XXX check this, tt
+Pl+Nom:t K ;
+Ess:nin K ;
+Sg:0 obldemcassg ;
+Pl:0 obldemcaspl ;
!Then going directly to the case paradigm of the demonstratives

! guhte
LEXICON GUHTECASE
GUHTE-NE ;
GU-GU-OBL ;

! Nom and Ess of guhte
LEXICON GUHTE-NE
+Sg+Nom:%> K-son ;
+Ess:%>n K-son ;

! guhtemuš
LEXICON GUHTEMUŠCASE
GUHTEMUŠ-NE ;
:a GU-GU-OBL ;

! Nom and Ess of guhtemuš
LEXICON GUHTEMUŠ-NE
+Sg+Nom:%> K-son ;
+Ess:a%>n K-son ;

! Oblique paradigm for guhte and guhtemuš
LEXICON GU-GU-OBL
+Sg+Gen:%>X8 K-son ;
+Sg+Gen:%>X8»# NAMAT ;
+Sg+Acc:%>X8 K-son ;
+Sg+Ill:%>X3j K-son ;
!+Sg+Gen:%> K-son ; ! this should go out
+Sg+Loc:%>X4s K-son ;
+Sg+Loc+Use/Sub:%>X4n K-son ; ! !SOUTH
+Sg+Com:%>X5jn K-son ;
+Pl+Nom:%>X4t K-son ;
+Pl+Gen:%>X5jd9 K-son ;
+Pl+Gen:%>X5jd9»# NAMAT ;
+Pl+Acc:%>X5jd9 K-son ;
+Pl+Ill:%>X5jde K-son ;
+Pl+Com:%>X5jgujn K-son ;



! Demonstrative pronouns
! ----------------------




!Splitting into Sg, Pl and Ess, different paradigms
LEXICON demcas
+Sg:0 demcassg ;
+Pl:0 demcaspl ;
+Ess:%>nin K ;

!Splitting into Sg, Pl and Ess, different paradigms
LEXICON datdemcas
+Sg:0 datdemcassg ;
+Pl:0 demcaspl ;
+Ess:%>nin K ;

LEXICON datdemcassg
nomdemcassg ;
obldatdemcassg ;


!Splitting into oblique and nonoblique
!This is because of the interrogatives
LEXICON demcassg
nomdemcassg ;
obldemcassg ;
LEXICON demcaspl
nomdemcaspl ;
obldemcaspl ;

LEXICON nomdemcassg
!The nominative, then pointing to clitics
!This could be conflated, but look out for interr
+Nom:t K ;
LEXICON nomdemcaspl
+Nom:t K ;


LEXICON obldemcassg
!The other cases, then pointing to clitics
+Gen:n K ;
+Gen:n»# NAMAT ;
+Acc:n K ;
+Ill:sa K ;
+Loc:s K ;
+Com:jnna K ; !dáinna Sg Com

LEXICON obldatdemcassg
!The other cases, then pointing to clitics
+Gen:n K ;
+Gen:n»# NAMAT ;
+Acc:n K ;
+Com:jnna K ; !dainna Sg Com


LEXICON obldemcaspl
+Gen:jd9 K ;
+Gen:jd9»# NAMAT ;
+Acc:jd9 K ;
+Ill:jdda K ;
+Loc:jn K ;   !dain   Pl Loc
!+Loc:jnna K ; !dainna Pl Loc is deemed out in contemporary language.
+Com:jguin K ;

!This case paradigm is exceptional and partial.
LEXICON Dahtanaga-par
+Sg+Nom:%>htanaga # ;
+Sg+Gen:%>mmanaga # ;
+Sg+Gen:%>mmanaga»# NAMAT ;
+Sg+Acc:%>mmanaga # ;
+Sg+Ill:%>sanaga # ;
+Sg+Loc:%>sttánaga # ;
+Sg+Com:%>jnnanaga # ;
+Pl+Ill:%>jddanaga # ;
+Pl+Loc:%>jnnanaga # ;

LEXICON DAKKAR
+Sg+Nom:%> K ;
+Attr:%> K ;
+Attr:»# NAMAT ;
+Ess:%>in K ;
+Sg+Gen:%>a K ;
+Sg+Acc:%>a K ;
+Pl+Nom:%>at K ;
00:%>i DAKKARI- ;

LEXICON DAKKARI-
+Sg+Ill:j K ;
+Sg+Loc:s K ;
+Sg+Loc+Use/Sub:n K ; ! !SOUTH
+Sg+Com:jn K ;
+Sg+Com+Use/Sub:jnna K ;
+Pl+Gen:jd9 K ;
+Pl+Gen:jd9»# NAMAT ;
+Pl+Acc:jd9 K ;
+Pl+Ill:jdda K ;
+Pl+Loc:jn K ;
+Pl+Com:jgujn K ;

LEXICON DAKKARInterr
+Sg+Nom:%> K-son ;
+Attr:%> K-son ;
+Attr:»# NAMAT ;
+Ess:%>in K-son ;
+Sg+Gen:%>a K-son ;
+Sg+Acc:%>a K-son ;
+Pl+Nom:%>at K-son ;
00:%>i DAKKARI-Interr ;

LEXICON DAKKARI-Interr
+Sg+Ill:j K-son ;
+Sg+Loc:s K-son ;
+Sg+Loc+Use/Sub:n K-son ; ! !SOUTH
+Sg+Com:jn K-son ;
+Pl+Gen:jd9 K-son ;
+Pl+Gen:jd9»# NAMAT ;
+Pl+Acc:jd9 K-son ;
+Pl+Ill:jdda K-son ;
+Pl+Loc:jn K-son ;
+Pl+Com:jgujn K-son ;


LEXICON DAKKARAS
+Sg+Nom:%> K ;
+Sg+Nom:%>X7 R ;
+Ess:%>an K ;
:a%> DAKKARASSA- ;

LEXICON DAKKARASSA-
 +Sg+Gen:X4 K ;
 +Sg+Gen:X8 K ;
 +Sg+Gen:X4»# NAMAT ;
 +Sg+Gen:X8»# NAMAT ;
 +Sg+Acc:X4 K ;
 +Sg+Acc:X8 K ;
 +Sg+Ill:X3j K ;
 +Sg+Loc:X4s K ;
 +Sg+Loc:X8s K ;
+Sg+Loc+Use/Sub:X4n K ; ! !SOUTH
+Sg+Loc+Use/Sub:X8n K ; ! !SOUTH
 +Sg+Com:X5jn K ;
 +Pl+Nom:X4t K ;
 +Pl+Gen:X5jd9 K ;
 +Pl+Gen:X5jd9»# NAMAT ;
 +Pl+Acc:X5jd9 K ;
 +Pl+Ill:X5jde K ;
 +Pl+Loc:X5jn K ;
 +Pl+Com:X5jgujn K ;





! Reflexive pronouns
! ------------------



LEXICON IESLEX
+Sg+Nom:ieš%> K ;
+Sg+Nom:ieš»# NAMAT ;  ! <======= ok
+Du+Nom:ieža%> K ;
+Du+Nom+Use/Sub:ieža»# NAMAT ; !
+Pl+Nom:ieža%> K ;
+Pl+Nom+Use/Sub:ieža»# NAMAT ; !
+Gen+PxSg3+Use/Sub:ald9 # ;   ! !SOUTH
+Ess:iehča%>nassij K ;
 reflobl ;

LEXICON JIESLEX
+Sg+Nom:jieš%> K ;
+Sg+Nom+Use/NG:jieš»# NAMAT ;  ! <======= ok
+Du+Nom:jieža%> K ;
+Du+Nom+Use/Sub:jieža»# NAMAT ; !
+Pl+Nom:jieža%> K ;
+Pl+Nom+Use/Sub:jieža»# NAMAT ; !
+Ess:jiehča%>nassij K ;
:j refloblalt ;

LEXICON reflobl
!The V/V2 alternation due to 3rd person vocalism
!Pointing directly to 1st and 3rd person sublexica for AG & Loc.
!Pointing to exceptional Pl sublexicon for AG and Loc.
+Acc:ieža%> PxV2 ;
+Gen:ieža%> PxV2 ;
+Gen+PxSg1+Use/NG:ieža%>n»# NAMAT ;
+Gen+PxSg1+Use/Sub:ieža%>m9»# NAMAT ;  ! !SOUTH
+Gen+PxSg2+Use/NG:ieža%>t»# NAMAT ;
+Gen+PxSg3+Use/NG:ieža%>s»# NAMAT ;     ! <========= ??
+Gen+PxDu1+Use/NG:ieža%>me»# NAMAT ;
+Gen+PxDu2+Use/NG:ieža%>de»# NAMAT ;
+Gen+PxDu3+Use/NG:ieža%>ska»# NAMAT ;
+Gen+PxPl1+Use/NG:ieža%>met»# NAMAT ;
+Gen+PxPl2+Use/NG:ieža%>det»# NAMAT ;
+Gen+PxPl3+Use/NG:ieža%>set»# NAMAT ;
+Acc+Use/NG:iehča%> Px1V ;
+Gen+Use/NG:iehča%> Px1V ;
+Gen+PxSg1+Use/NG:iehča%>n»# NAMAT ;
+Gen+PxDu1+Use/NG:iehča%>me»# NAMAT ;
+Gen+PxPl1+Use/NG:iehča%>met»# NAMAT ;
+Acc+Use/NG:ieža%>jd PxCPlstem ;
+Gen+Use/NG:ieža%>jd PxCPlstem ;
+Gen+PxDu1+Use/Sub:ieža%>jdeame»# NAMAT ;     !
+Gen+PxDu2+Use/Sub:ieža%>jdeatte»# NAMAT ;     !
+Gen+PxDu3+Use/Sub:ieža%>jdeaskka»# NAMAT ; !
+Gen+PxPl1+Use/Sub:ieža%>jdeamet»# NAMAT ;     !
+Gen+PxPl2+Use/Sub:ieža%>jdeattet»# NAMAT ; !
+Gen+PxPl3+Use/Sub:ieža%>jdeaset»# NAMAT ;     !
+Ill:alcce%>s PxC ; ! All generated Ill forms here
+Ill+Use/NG:alcca%> Px1V ;
+Ill+Use/NG:alcca%> Px2V ;
+Ill+Use/NG:alcci%> Px3V ;
+Ill+Use/NG:alcce%> PxV2 ;
+Ill+Use/NG:alla%>s PxC ;
+Ill+PxSg1+Use/Sub:allasim9# K ; ! !SOUTH
+Loc+Use/NG:alddi%> PxV ;
+Loc+Use/NG:aldde%> Px3V ;
+Loc:aldde%>st PxC ;
+Loc+Use/Sub:alddi%>n PxCPlstem ; !
+Loc+Use/NG:alddi%>jn PxCPlstem ;
+Loc+Use/NG:alddá%>n PxCPlstem ;
+Loc+PxSg1+Use/Sub:ieža%>stan»# K ;
+Loc+PxSg1+Use/Sub:ieža%>stam»# K ;  ! !SOUTH
+Loc+PxSg2+Use/Sub:ieža%>stat»# K ;
+Loc+PxSg3+Use/Sub:ieža%>stis»# K ;
+Com:ieža%>jn PxC ;
+ComPxCPlCom+Use/NG:ieža%>jd PxCPlCom ;
+Ess:iehča%>n PxC ;

LEXICON refloblalt
!The V/V2 alternation due to 3rd person vocalism
!Pointing directly to 1st and 3rd person sublexica for AG & Loc.
!Pointing to exceptional Pl sublexicon for AG and Loc.
+Acc:ieža%> PxV2 ;
+Gen:ieža%> PxV2 ;
+Gen+PxSg1+Use/NG:ieža%>n»# NAMAT ;
+Gen+PxSg1+Use/Sub:ieža%>m9»# NAMAT ;  ! !SOUTH
+Gen+PxSg2+Use/NG:ieža%>t»# NAMAT ;
+Gen+PxSg3+Use/NG:ieža%>s»# NAMAT ;
+Gen+PxDu1+Use/NG:ieža%>me»# NAMAT ;
+Gen+PxDu2+Use/NG:ieža%>de»# NAMAT ;
+Gen+PxDu3+Use/NG:ieža%>ska»# NAMAT ;
+Gen+PxPl1+Use/NG:ieža%>met»# NAMAT ;
+Gen+PxPl2+Use/NG:ieža%>det»# NAMAT ;
+Gen+PxPl3+Use/NG:ieža%>set»# NAMAT ;
+Acc+Use/NG:iehča%> Px1V ;
+Gen+Use/NG:iehča%> Px1V ;
+Gen+PxSg1+Use/NG:iehča%>n»# NAMAT ;
+Gen+PxDu1+Use/NG:iehča%>me»# NAMAT ;
+Gen+PxPl1+Use/NG:iehča%>met»# NAMAT ;
+Acc+Use/NG:ieža%>jd PxCPlstem ;
+Gen+Use/NG:ieža%>jd PxCPlstem ;
+Gen+PxDu1+Use/Sub:ieža%>jdeame»# NAMAT ;
+Gen+PxDu2+Use/Sub:ieža%>jdeatte»# NAMAT ;
+Gen+PxDu3+Use/Sub:ieža%>jdeaskka»# NAMAT ;
+Gen+PxPl1+Use/Sub:ieža%>jdeamet»# NAMAT ;
+Gen+PxPl2+Use/Sub:ieža%>jdeattet»# NAMAT ;
+Gen+PxPl3+Use/Sub:ieža%>jdeaset»# NAMAT ;
+Com+Use/NG:ieža%>jn PxC ;
+Com+Use/NG:ieža%>jd PxCPlCom ;
+Ess+Use/NG:iehča%>n PxC ;


LEXICON PxCPlstem
!This lexicon is here since it only applies to Reflexives.
!It handles the exeptional AG and Loc DuPl plural stem forms of ieš.
+PxDu1:eame K ;
+PxDu2:eatte K ;
+PxDu3:easkka K ;
+PxPl1:eamet K ;
+PxPl2:eattet K ;
+PxPl3:easet K ;


LEXICON PxCPlCom ! +Use/NG
! This lexicon for archaic biblical forms like iežaideasetguin.
+PxDu1+Use/NG:eameguin K ;
+PxDu2+Use/NG:eatteguin K ;
+PxDu3+Use/NG:easkkaguin K ;
+PxPl1+Use/NG:eametguin K ;
+PxPl2+Use/NG:eattetguin K ;
+PxPl3+Use/NG:easetguin K ;





! Reciprocal pronouns
! -------------------


LEXICON GOABBATLEX
+Du:goabbat # ; 
+Pl:guhtet # ;  
+Du+Use/Sub:goappat # ; ! ! Dialectal forms. Norm?
+Pl+Use/Sub:guđet # ;   ! ! Dialectal forms. Norm?

LEXICON GUOIBMILEX
+Sg+Acc:guoibmá Px1VRecipr ;   
+Sg+Gen:guoibmá Px1VRecipr ;   
+Sg+Acc:guoimmi Px23VRecipr ;  
+Sg+Gen:guoimmi Px23VRecipr ;  
+Sg+Acc:guoimmi Px3VPlStem ;  
+Sg+Gen:guoimmi Px3VPlStem ;  
+Sg+Ill:guoibmás PxCPlstem ;   
+Sg+Loc:guoimmist PxCPlstem ;  
+Sg+Com:guimmiin PxCPlstem ;  

+Pl+Acc:guimmiid PxCPlstem ;   
+Pl+Gen:guimmiid PxCPlstem ;   
+Pl+Ill:guimmiidasa PxVPlStemRecipr ; 
+Pl+Loc:guimmiin PxCPlstem ;    
+Pl+Com:guimmiid PxCPlComRecipr ; 

LEXICON NUBBILEX
+Gen:nuppi%>id nubbiPxDu ;
+Acc:nuppi%>id nubbiPxDu ;
+Ill:nuppi%>idasa Px1VRecipr ;
+Loc:nuppi%>in nubbiPxDu ;
+Com:nuppi%>id nubbiPxDuCom ;

+Gen:nuppi%>id nubbiPxPl ;
+Acc:nuppi%>id nubbiPxPl ;
+Ill:nuppi%>idasa Px23VRecipr ;
+Ill:nuppi%>idasa Px3VPlStem ;
+Loc:nuppi%>in nubbiPxPl ;
+Com:nuppi%>id nubbiPxPlCom ;

LEXICON Px1VRecipr      
+PxDu1:me K ;          
+PxPl1:met K ;         

LEXICON Px23VRecipr 
!LEXICON Px2VPlStem     !SUB
+PxDu2:X2de K ;
+PxPl2:X2det K ;

LEXICON Px3VPlStem     
+PxDu3:ska K ;         
+PxPl3:set K ;         

LEXICON PxVPlStemRecipr 
+PxPl1:met K ;         
+PxPl2:det K ;         
+PxPl3:set K ;         

LEXICON PxCPlComRecipr  
+PxPl1:eametguin K ;   
+PxPl2:eadetguin K ;   
+PxPl3:easetguin K ;   

LEXICON nubbiPxDu
+PxDu1:eame K ;
+PxDu2:eatte K ;
+PxDu3:easkka K ;

LEXICON nubbiPxPl
+PxPl1:eamet K ;
+PxPl2:eattet K ;
+PxPl3:easet K ;

LEXICON nubbiPxDuCom
+PxDu1:eameguin K ;
+PxDu2:eatteguin K ;
+PxDu3:easkkaguin K ;

LEXICON nubbiPxPlCom
+PxPl1:eametguin K ;
+PxPl2:eattetguin K ;
+PxPl3:easetguin K ;




! Indefinite pronouns
! -------------------



LEXICON oblindefcas
+Sg:0 oblindemcassg ;
+Pl:0 oblindemcaspl ;


LEXICON oblindemcassg
!The other cases, then pointing to clitics
+Gen:nge # ;
+Gen:nge»# NAMAT ;
+Acc:nge # ;
+Ill:sage # ;
+Loc:sge # ;
+Loc+Use/NG:stege # ;
+Com:jnnage # ;

LEXICON oblindemcaspl
+Gen:jde # ;
+Gen:jde»# NAMAT ;
+Acc:jde # ;
+Gen+Foc/ge:jdege # ;
+Gen+Foc/ge:jdege»# NAMAT ;
+Acc:jdege # ;
+Gen+Foc/ge+Use/NG:jdige # ;
+Gen+Foc/ge+Use/NG:jdige»# NAMAT ;
+Acc+Foc/ge+Use/NG:jdige # ;
+Ill:jddage # ;
+Loc:jnge # ;
!Here we do not go to the case paradigm of the demonstratives,
!since the clitic path is different. ComPl lacks (intentionally) from paradigm.

LEXICON oblindefcas-giinai
+Gen:nnai # ;
+Gen:nnai»# NAMAT ;
+Acc:nnai # ;
+Gen+Use/NG:nai # ;
+Gen+Use/NG:nai»# NAMAT ;
+Acc+Use/NG:nai # ;
+Ill:sanai # ;
+Ill+Use/NG:sai # ;
+Loc:snai # ;
+Loc+Use/NG:sai # ;
+Loc+Use/NG:stenai # ;
+Loc+Use/NG:stai # ;
+Com:jnnanai # ;
+Com+Use/NG:jnnai # ;
+Gen+Use/NG:jdnai # ;
+Gen+Use/NG:jdnai»# NAMAT ;
+Acc+Use/NG:jdnai # ;
+Gen+Use/NG:jdai # ;
+Gen+Use/NG:jdai»# NAMAT ;
+Acc+Use/NG:jdai # ;
+Ill+Use/NG:jddanai # ;
+Ill+Use/NG:jddai # ;
+Loc+Use/NG:jnnai # ;
+Loc+Use/NG:jnai # ;

LEXICON oblindefcas-miinai
+Gen:nnai # ;
+Gen:nnai»# NAMAT ;
+Acc:nnai # ;
! +Gen:nai # ; !commented out disamb reasons
+Gen+Use/NG:nai»# NAMAT ;
! +Acc:nai # ; !commented out disamb reasons
+Ill:sanai # ;
+Ill+Use/NG:sai # ;
+Loc:snai # ;
+Loc+Use/NG:sai # ;
+Loc:stenai # ;
+Loc+Use/NG:stai # ;
+Com:jnnanai # ;
+Com+Use/NG:jnnai # ;
+Gen+Use/NG:jdnai # ;
+Gen+Use/NG:jdnai»# NAMAT ;
+Acc+Use/NG:jdnai # ; !commented out disamb reasons
! +Gen:jdai # ; !commented out disamb reasons
+Gen+Use/NG:jdai»# NAMAT ;
! +Acc:jdai # ; !commented out disamb reasons
+Ill+Use/NG:jddanai # ;
! +Ill:jddai # ; !commented out disamb reasons
+Loc+Use/NG:jnnai # ;
+Loc+Use/NG:jnai # ;




! guhtege
LEXICON GUHTEGECASE
GUHTEGE-NE ;
GUGE-GUGE-OBL ;


! Nom and Ess of guhtege
LEXICON GUHTEGE-NE
+Sg+Nom:%>ge # ;
+Ess:%>nge # ;

! guhtemušge
LEXICON GUHTEMUSGECASE
GUHTEMUSGE-NE ;
:a%> GUGE-GUGE-OBL ;

! Nom and Ess of guhtemušge
LEXICON GUHTEMUSGE-NE
+Sg+Nom:%>ge # ;
+Ess:a%>nge # ;

! Oblique paradigm for guhtege and guhtemušGE
LEXICON GUGE-GUGE-OBL
+Sg+Gen:X8ge # ;
+Sg+Gen:X8ge»# NAMAT ;
+Sg+Acc:X8ge # ;
+Sg+Ill:X3jge # ;
!+Sg+Gen:ge # ; ! this should go out
+Sg+Loc:X4sge # ;
+Sg+Loc+Use/Sub:X4nge K ; ! !SOUTH
+Sg+Com:X5jnge # ;
+Pl+Nom:X4tge # ;
+Pl+Gen:X5jdge # ;
+Pl+Gen:X5jdge»# NAMAT ;
+Pl+Acc:X5jdge # ;
+Pl+Ill:X5jdege # ;
+Pl+Loc+Use/Sub:X5jnge # ; ! because of speller
!+Pl+Com:X5jgujnge # ; !ComPl is out.

LEXICON GUHTENAI
+Sg+Nom:%>nai # ;
+Sg+Gen:%>X8nai # ;
+Sg+Gen:%>X8nai»# NAMAT ;
+Sg+Acc:%>X8nai # ;
+Sg+Ill:%>X3jnai # ;
+Sg+Loc:%>X4snai # ;
+Sg+Com:%>X5jnnai # ;
+Ess:%>nnai # ;
+Pl+Nom:X4tnai # ;
+Pl+Gen:%>X5jdnai # ;
+Pl+Gen:%>X5jdnai»# NAMAT ;
+Pl+Acc:%>X5jdnai # ;
+Pl+Ill:%>X5jdenai # ;
+Pl+Loc+Use/Sub:%>X5jnnai # ; ! because of speller













! Declineable indefinites with normal case paradigms
! - - - - - - - - - - - - - - - - - - - - - - - - - -













! Declension lexica for the declineable indefinites
! - - - - - - - - - - - - - - - - - - - - - - - - -

!splitting v- and c-final evensyll in two for NomEss, as usual.
LEXICON indef-a-even-v
indefattr ;
indefdecleven-v-ne ;
indefdecleven-obl ;

LEXICON indef-a-even-c
:%> indefattr ;
:%> indefdecleven-c-ne ;
:a%> indefdecleven-obl ;

LEXICON indefdecleven-obl
indefdeclsgeven ;
indefdeclpleven ;

LEXICON indefdecleven-v-ne
+Sg+Nom: K ;
+Ess:n K ;

LEXICON indefdecleven-c-ne
+Sg+Nom: K ;
+Ess:an K ;

LEXICON indefdeclsgeven
+Sg+Gen:X4 K ;
+Sg+Gen:X4»# NAMAT ;
+Sg+Acc:X4 K ;
+Sg+Ill:X3j K ;
+Sg+Loc:X4s K ;
+Sg+Loc+Use/Sub:X4n K ; ! !SOUTH
+Sg+Com:X5jn K ;

LEXICON indefdeclpleven
+Pl+Nom:X4t K ;
+Pl+Gen:X5jd9 K ;
+Pl+Gen:X5jd9»# NAMAT ;
+Pl+Acc:X5jd9 K ;
+Pl+Ill:X5jde K ;
+Pl+Loc:X5jn K ;
+Pl+Com:X5jgujn K ;

LEXICON nubbicase
+Attr:0 K ;
+Sg+Nom: K ;
+Ess:n K ;
+Sg+Gen:X4 K ;
+Sg+Gen+Use/NG:X4X7 K ;
+Sg+Gen:X4X7»# NAMAT ;
+Sg+Acc:X4 K ;
+Sg+Acc+Use/NG:X4X7 K ;
+Sg+Ill:X3j K ;
+Sg+Loc:X4s K ;
+Sg+Loc+Use/Sub+Use/NG:X4n K ; ! !SOUTH
+Sg+Com:X5jn K ;
+Pl+Nom:X4t K ;
+Pl+Gen:X5jd9 K ;
+Pl+Gen:X5jd9»# NAMAT ;
+Pl+Acc:X5jd9 K ;
+Pl+Ill:X5jde K ;
+Pl+Loc:X5jn K ;
+Pl+Com:X5jgujn K ;


LEXICON JUOBBAGOABBA
+Sg+Nom:juobbá%>#goabbá%> K ;
+Sg+Gen:juoppá%>#goappá%> K ;
+Sg+Gen:juoppá%>#goappá»# NAMAT ;
+Sg+Acc:juoppá%>#goappá%> K ;
+Sg+Ill:juobbá%>i#goabbá%>i K ;
+Sg+Loc:juoppá%>s#goappá%>s K ;
+Sg+Com:juoppá%>in#goappá%>in K ;
+Ess:juobbá%>n#goabbá%>n K ;
+Pl+Nom:juobbá%>t#goabbá%>t K ;
+Pl+Gen:juoppá%>jd9#goappá%>jd9 K ;
+Pl+Gen:juoppá%>jd9#goappá%>jd9»# NAMAT ;
+Pl+Acc:juoppá%>jd9#goappá%>jd9 K ;
+Pl+Ill:juobbá%>jde#goabbá%>jde K ;
+Pl+Loc:juoppá%>jn#goappá%>jn K ;
+Pl+Com:juoppá%>jgujn#goappá%>jgujn K ;

! Intermediate lexica for uneven indefinite pronouns
! - - - - - - - - - - - - - - - - - - - - - - - - - -

LEXICON indef-a-uneven
indefattr ;
indefdecluneven ;

LEXICON indef-a-uneven-gahpir
indefattr ;
indefdecluneven-gahpir ;

LEXICON indefsg-a-uneven
indefattr ;
indu-sg ;

LEXICON indefdecluneven
indu-sg ;
indu-pl-gahpir ;

LEXICON indu-sg
:X4 indef-sg-u-n ;
indef-sg-u-e ;
:X2 indefdeclsguneven-ag ;
indefdeclsguneven-ilc ;

LEXICON indefdecluneven-gahpir
indu-sg-gahpir ;
indu-pl-gahpir ;

LEXICON indu-sg-gahpir
indefdeclsguneven-ne ;
indefdeclsguneven-ag ;
indefdeclsguneven-ilc ;

LEXICON indu-pl-gahpir
indefdeclpluneven-n ;
:i indefdeclpluneven-obl ;
indef-sg-u-e ; !For eatnagin

! Suffix lexica for uneven indefinite pronouns
! - - - - - - - - - - - - - - - - - - - - - - -

LEXICON indefdeclsguneven-ne
indef-sg-u-n ;
indef-sg-u-e ;

LEXICON indef-sg-u-n
+Sg+Nom: K ;

LEXICON indef-sg-u-e
+Ess:in K ;

LEXICON indefdeclsguneven-ag
+Sg+Gen:a K ;
+Sg+Acc:a K ;

LEXICON indefdeclsguneven-ilc
+Sg+Ill:ij K ;
+Sg+Loc:is K ;
+Sg+Loc+Use/Sub:in K ; ! !SOUTH
+Sg+Com:ijn K ;

LEXICON indefdeclpluneven-n
+Pl+Nom:at K ;

LEXICON indefdeclpluneven-obl
+Pl+Gen:jd9 K ;
+Pl+Acc:jd9 K ;
+Pl+Ill:jdda K ;
+Pl+Loc:jn K ;
+Pl+Com:jgujn K ;

LEXICON indefattr
+Attr:0 K ;


! Multiword indefinites
! - - - - - - - - - - -

LEXICON indefmulti !SUB added this lexicon, because otherwise these would be in indefattr
feara% mii+Pron+Indef+Sg+Nom+Use/Sub:feara% mij K-son ; ! spellera dihte
feara% mii+Pron+Indef+Sg+Acc+Use/Sub:feara% májd9 K-son ; ! spellera dihte
feara% gii+Pron+Indef+Sg+Nom+Use/Sub:feara% gij K-son ; ! spellera dihte
feara% mii+Pron+Indef+Use/Sub:feara% ma oblintercas ; ! spellera dihte
feara% gii+Pron+Indef+Use/Sub:feara% gea oblintercas ; ! spellera dihte

vaikko% mii+Pron+Indef+Sg+Nom+Use/Sub:vaikko% mij K-son ; ! spellera dihte
vaikko% mii+Pron+Indef+Sg+Acc+Use/Sub:vaikko% májd9 K-son ; ! spellera dihte
vaikko% gii+Pron+Indef+Sg+Nom+Use/Sub:vaikko% gij K-son ; ! spellera dihte
vaikko% mii+Pron+Indef+Use/Sub:vaikko% ma oblintercas ; ! spellera dihte
vaikko% gii+Pron+Indef+Use/Sub:vaikko% gea oblintercas ; ! spellera dihte
vaikko% makkár+Pron+Indef+Use/Sub:vaikko% makkár DAKKAR ;  ! spellera dihte
vaikko% guh0te+Pron+Interr+Use/Sub:vaikko% guh'te GUHTECASE ;  ! spellera dihte
vaikko% guhtemuš+Pron+Interr+Use/Sub:vaikko% guđemužž GUHTEMUŠCASE ; ! spellera dihte
vaikko% goabbá+Pron+Interr+Use/Sub:vaikko% goabbá GUHTECASE ; ! spellera dihte

! some multiword prons, according to Nickel
!feara% mii iind ;
!feara% gii iind ;
mii% nu+Use/Sub:mii% nu iind;  ! spellera
gii% nu+Use/Sub:gii% nu iind;  ! spellera
makkár% nu+Use/Sub:makkár% nu iind;  ! spellera
makkáraš% nu+Use/Sub:makkáraš% nu iind;  ! spellera






! Separate lexica for exceptional entries
! - - - - - - - - - - - - - - - - - - - -




LEXICON indefdeclplcontr ! so far guktot
+Pl+Nom:%>t K ;
+Pl+Gen:%>jd9 K ;
+Pl+Gen:%>jd9»# NAMAT ;
+Pl+Acc:%>jd9 K ;
+Pl+Ill:%>jde K ;
+Pl+Loc:%>jn K ;
+Pl+Com:%>jn K ;

LEXICON EARRAS
+Sg+Acc:%>a K ;
+Sg+Gen:%>a K ;
+Sg+Ill:%>ij K ;
+Sg+Loc:%>is K ;
+Sg+Loc+Use/Sub:%>in K ; ! !SOUTH
+Sg+Com:%>iin K ;
+Ess:%>in K ;
+Pl+Nom:%>at K ;
+Pl+Gen:%>ijd9 K ;
+Pl+Acc:%>ijd9 K ;
+Pl+Ill:%>ijdda K ;
+Pl+Loc:%>ijn K ;
+Pl+Com:%>ijgujn K ;


LEXICON SEAMMA
+Sg+Nom:%> K ;
+Sg+Acc:%> K ;
+Sg+Gen:%> K ;
+Sg+Gen+Use/Sub:»# NAMAT ; !
+Sg+Ill:%>j K ;  ! Let's await the verdict of the jury..
! +Sg+Loc:%>s K ; ! Let's settle for the Adv reading
+Sg+Loc+Use/Sub:%>n K ;  ! !SOUTH, well..., probably this one should Adv as well
+Sg+Com:%>jn K ; ! Let's await the verdict of the jury..
+Pl+Nom:%>t K ;
+Pl+Acc:%>jd9 K ;
+Pl+Gen:%>jd9 K ;
+Pl+Gen+Use/Sub:%>jd9»# NAMAT ; !

LEXICON SIMME !SUB
+Sg+Nom:%> K ;
+Sg+Acc:%> K ;
+Sg+Gen:%> K ;
+Sg+Ill:%>j K ;
+Sg+Loc:%>n K ;
+Sg+Com:%>jn K ;
+Pl+Nom:%>t K ;
+Pl+Acc:%>jd9 K ;
+Pl+Gen:%>jd9 K ;
+Pl+Gen:%>jd9»# NAMAT ;



LEXICON JUOGA
+Sg+Nom:ga%> K ;
+Sg+Acc:ga%> K ;
+Sg+Acc:jdá%> K ;
+Pl+Acc:jdá%>jd9 K ;
+Sg+Acc+Use/Sub:jddá K ; !
+Sg+Acc+Use/Sub:jaaájd9 K ; !
+Sg+Gen:ga%> K ;
+Sg+Gen:ga»# NAMAT ;
+Sg+Ill:sa%>t K ;
+Sg+Loc:stá%> K ;
+Sg+Com:jdá%>jn K ;
+Ess:jdá%>n K ;
+Ess:ni%>n K ;
+Attr+Use/Sub:da K ; !

LEXICON OVTTAHAT
+Sg+Acc:g%>a K ;
+Sg+Gen:g%>a K ;
+Sg+Gen:t»# NAMAT ;
+Sg+Ill:hk%>ii K ;
+Sg+Loc:g%>as K ;
+Sg+Com:g%>ajn K ;
+Ess:hk%>an K ;
+Pl+Nom:g%>at K ;
+Pl+Acc:g%>ajd9 K ;
+Pl+Gen:g%>ajd9 K ;
+Pl+Ill:g%>ajde K ;
+Pl+Loc:g%>ajn K ;
+Pl+Com:g%>ajgujn K ;






! The indeclineable indefinites
! - - - - - - - - - - - - - - -


LEXICON iind
+Pron+Indef:%>0 K ;




