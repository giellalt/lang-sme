# -*- cg-pre-pipe: "$GTHOME/giella-core/scripts/preprocess --abbr=$GTHOME/langs/sme/tools/preprocess/abbr.txt | hfst-optimised-lookup  $GTHOME/langs/sme/src/analyser-disamb-gt-desc.hfstol | $GTHOME/giella-core/scripts/lookup2cg" -*-
# Divvun & Giellatekno - open source grammars for Sámi and other languages
# Copyright © 2000-2016 UiT The arctic University of Norway 
# http://giellatekno.uit.no & http://divvun.no
#
# This program is free software; you can redistribute and/or modify
# this file under the terms of the GNU General Public License as published by
# the Free Software Foundation, either version 3 of the License, or"# (at your option) any later version. The GNU General Public License
# is found at http://www.gnu.org/licenses/gpl.html. It is
# also available in the file $GTHOME/LICENSE.txt.
#
# Other licensing options are available upon request, please contact
# giellatekno@hum.uit.no or divvun@hum.uit.no

# ==================================================================== #
#    N O R T H E R N  S Á M I  D I S A M B I G U A T O R               
# ==================================================================== #
# ==================================================================== 

#  Sámi language technology 2003-2016, UiT The arctic University of Norway #



      # =========================================== #
      #      Short table of contents                #
      # =========================================== #

      # Delimiters
      # Tags and sets
      # Disambiguation rules
       # One-cohort disambiguation - cycle 0
       # Local disambiguation - cycles 1 and 2
       # Cycle 1a: Special cases 
       # Cycle 1b: Cleaning up after the special cases
       # Cycle 2: Other local disambiguation 
       # Mapping of COMP-CS<, CC and CS
       # More diambiguation
       # Verb mappings 
       # Disambiguating nouns
       # Mainly mapping-rules   
       # Disambiguating nouns
  	   # Case disambiguation
       # Cycle 3: Global disambiguation 
	   # Cycle 4: Syntactic disambiguation 
	   # Cycle 5: Post-syntactic morphological disambiguation

# =============== #
#!! # DELIMITERS 
# =============== #

DELIMITERS = "<.>" "<!>" "<?>" "<...>" "<¶>" "<..>" "<!.>" "<?.>" "<¶.>" "<❡>" (sent) sent  ; 
#!! Sentence delimiters are the following: <.> <!> <?> <...> <¶>

#PARENTHESES = "<$(>" "<$)>" "<$[>" "<$]>" "<${>" "<$}>" "<$«>" "<$»>" ;

# ================== #
#!! # TAGS AND SETS
# ================== #

SETS


#!! ## Tags


#!! This section lists all the tags inherited from the fst, and used as tags
#!! in the syntactic analysis. The next section, **Sets**, contains sets defined
#!! on the basis of the tags listed here, those set names are not visible in the output.



# Tags declared as single-membered LISTs 
# ======================================

#!! ### Beginning and end of sentence
LIST BOS = (>>>) (<s>) (sent) ;  #!! BOS 
LIST EOS = (<<<) (</s>) (sent) ("❡") ("¬") ; #!! EOS

# We define end of clause and beginning of clause in a way so that the file
# may be read both by the CG-2 and the vislcg formalisms.
# CG3 doesn´t function without >>> and <<< !


#!! ### Parts of speech tags

LIST N = N ;		    #!! * N
LIST A = A ;		    #!! * A
LIST Adv = Adv ;	    #!! * Adv
LIST V = V ;		    #!! * V
LIST Pron = Pron ;	    #!! * Pron
LIST CS = CS ;		    #!! * CS
LIST CC = CC ;		    #!! * CC
SET CC-CS = CC OR CS ;  #!! * SET CC-CS = CC OR CS ;
LIST Po = Po ;		    #!! * Po
LIST Pr = Pr ;		    #!! * Pr
LIST Pcle = Pcle ;	    #!! * Pcle
LIST Num = Num ;	    #!! * Num
LIST Interj = Interj ;  #!! * Interj
LIST ABBR = ABBR ;	    #!! * ABBR
LIST ACR = ACR ;	    #!! * ACR
LIST CLB = CLB clb ;	    #!! * CLB clb (clb lagt til for apertium, fungerer ikke uten)
LIST LEFT = LEFT ;	    #!! * LEFT
LIST RIGHT = RIGHT ;    #!! * RIGHT
LIST WEB = WEB ;	    #!! * WEB
LIST PPUNCT = LEFT RIGHT PUNCT guio ;   #!! * LEFT RIGHT because of apertium
LIST QMARK = """ ;      # "  syntax colouring reset.
SET PUNCT = PPUNCT - QMARK ;
LIST DASH = "–" "—" "-" ;

LIST COMMA = "," ;
LIST ¶ = ¶;
LIST HEADLINEMARK = "❡" ":" ;

#LIST V* = V* ;


#!! * ### Tags for POS sub-categories

LIST Pers = Pers ;	    #!! * Pers
LIST Dem = Dem ;	    #!! * Dem
LIST Interr = Interr ;  #!! * Interr
LIST Indef = Indef ;    #!! * Indef
LIST Recipr = Recipr ;  #!! * Recipr
LIST Refl = Refl ;	    #!! * Refl
LIST Rel = Rel ;	    #!! * Rel
LIST Coll = Coll ;	    #!! * Coll
LIST NomAg = NomAg ;    #!! * NomAg
LIST G3 = G3 ;		    #!! * G3
LIST Prop = Prop np ;	    #!! * Prop
LIST Allegro = Allegro ; #!! * Allegro
LIST Arab = Arab ; #!! * Arab
LIST Rom = Rom ; #!! * Romertall
LIST Subqst = Subqst ; #!! * Subqst
LIST CLBfinal = CLBfinal ; #!! * CLBfinal


#!! * ### Tags for morphosyntactic properties

LIST Nom = Nom ; #!! * Nom
LIST Acc = Acc ; #!! * Acc
LIST Gen = Gen ; #!! * Gen
LIST Ill = Ill ; #!! * Ill
LIST Loc = Loc ; #!! * Loc
LIST Com = Com ; #!! * Com
LIST Ess = Ess ; #!! * Ess
LIST Sg = Sg ;   #!! * Sg
LIST Du = Du ;   #!! * Du
LIST Pl = Pl ;   #!! * Pl
LIST Cmp/SplitR = Cmp/SplitR ;	   #!! * Cmp/SplitR
LIST Cmp/Attr = Cmp/Attr ;	   #!! * Cmp/Attr
LIST Cmp/Cit = Cmp/Cit ;	   #!! * Cmp/Cit
LIST Cmpnd = Cmpnd ;	   #!! * Cmpnd
LIST Cmp/SgNom = Cmp/SgNom ; #!! * Cmp/SgNom Cmp/SgGen
LIST Cmp/SgGen = Cmp/SgGen ; #!! * Cmp/SgGen
LIST Cmp/PlGen = Cmp/PlGen ; #!! * Cmp/PlGen
LIST Cmp/Sh = Cmp/Sh ;	   #!! * Cmp/Sh
LIST Cmp = Cmp ;	   #!! * Cmp
LIST PxSg1 = PxSg1 ;	   #!! * PxSg1
LIST PxSg2 = PxSg2 ;	   #!! * PxSg2
LIST PxSg3 = PxSg3 ;	   #!! * PxSg3
LIST PxDu1 = PxDu1 ;	   #!! * PxDu1
LIST PxDu2 = PxDu2 ;	   #!! * PxDu2
LIST PxDu3 = PxDu3 ;	   #!! * PxDu3
LIST PxPl1 = PxPl1 ;	   #!! * PxPl1
LIST PxPl2 = PxPl2 ;	   #!! * PxPl2
LIST PxPl3 = PxPl3 ;	   #!! * PxPl3
LIST Der/Comp = Der/Comp ;		   #!! * Der/Comp
LIST Der/Superl = Der/Superl ;		   #!! * Der/Superl
LIST Comp = Comp Der/Comp  Gram/Comp ;		   #!! * Gram/Comp for adverbs,  Der/Comp for adverbs  
LIST Superl = Superl Der/Superl Gram/Superl ;	   #!! * Gram/Superl for adverbs, and Der/Superl for adjectives
LIST Attr = Attr ;		   #!! * Attr
LIST Ord = Ord ;		   #!! * Ord
LIST Qst = Qst ;		   #!! * Qst
LIST IV = IV ; #!! * IV
LIST TV = TV ; #!! * TV 
LIST Prt = Prt;				   #!! * Prt
LIST Prs = Prs ;			   #!! * Prs
LIST Ind = Ind ;			   #!! * Ind
LIST Pot = Pot ;			   #!! * Pot
LIST Cond = Cond ;			   #!! * Cond
LIST Imprt = Imprt ;		   #!! * Imprt
LIST ImprtII = ImprtII ;	   #!! * ImprtII
LIST Sg1 = Sg1 (p1 sg) ;			   #!! * Sg1
LIST Sg2 = Sg2 (p2 sg) ;			   #!! * Sg2
LIST Sg3 = Sg3 (p3 sg) ;			   #!! * Sg3
LIST Du1 = Du1 (p1 du) ;			   #!! * Du1
LIST Du2 = Du2 (p2 du) ;			   #!! * Du2
LIST Du3 = Du3 (p3 du) ;			   #!! * Du3
LIST Pl1 = Pl1 (p1 pl) ;			   #!! * Pl1
LIST Pl2 = Pl2 (p2 pl);			   #!! * Pl2
LIST Pl3 = Pl3 (p3 pl);			   #!! * Pl3
LIST Inf = Inf ;			   #!! * Inf
LIST ConNeg = ConNeg ;		   #!! * ConNeg
LIST Neg = Neg ;			   #!! * Neg
LIST PrfPrc = PrfPrc ;		   #!! * PrfPrc
LIST VGen = VGen ;			   #!! * VGen
LIST PrsPrc = PrsPrc ;		   #!! * PrsPrc
LIST Ger = Ger ;			   #!! * Ger
LIST Sup = Sup ;			   #!! * Sup
LIST Actio = Actio ;		   #!! * Actio
LIST VAbess = VAbess ;		   #!! * VBAbess
LIST Known = Known ;		   #!! * Known

# The ("ge" Pcle) etc. are used in Apertium

#!! * ### Tags for clitic particles
LIST Foc/Neg-ge = Foc/Neg-ge   ;	   #!! * Foc/ge
LIST Foc/Pos-ge = Foc/Pos-ge   ;	   #!! * Foc/ge
LIST Foc/ge = Foc/Pos-ge Foc/Neg-ge   ;	   #!! * Foc/ge
LIST Foc/gen = Foc/gen   ;  #!! * Foc/gen
LIST Foc/ges = Foc/ges  ;  #!! * Foc/ges
LIST Foc/gis = Foc/gis  ;  #!! * Foc/gis
LIST Foc/naj = Foc/naj   ;  #!! * Foc/naj
LIST Foc/ba = Foc/ba   ;	   #!! * Foc/ba
LIST Foc/be = Foc/be   ;	   #!! * Foc/be
LIST Foc/hal = Foc/hal   ;  #!! * Foc/hal
LIST Foc/han = Foc/han   ;  #!! * Foc/han
LIST Foc/bat = Foc/bat   ;  #!! * Foc/bat
LIST Foc/son = Foc/son   ;  #!! * Foc/son
LIST Foc/mis = Foc/mis   ;  #!! * Foc/mis
LIST Foc/mat = Foc/mat   ;  #!! * Foc/mat

#!! * ### Derivation tags

LIST Der/PassL = Der/PassL ;
LIST Der/Pass = Der/PassL Der/PassS ; #!! * Der/PassL Der/PassS
LIST Der/NomAg = Der/NomAg ;   #!! * Der/NomAg
LIST Actor = NomAg Der/NomAg ; #!! * NomAg
LIST Der/adda = Der/adda ;		 #!! * Der/adda
LIST Der/alla = Der/alla ;		 #!! * Der/alla
LIST Der/easti = Der/easti ;	 #!! * Der/easti
LIST Der/d = Der/d ;			 #!! * Der/d
LIST Der/eamoš = Der/eamoš ;	 #!! * Der/eamoš
LIST Der/amoš = Der/amoš ;		 #!! * Der/amoš
LIST Der/geahtes = Der/geahtes ; #!! * Der/geahtes
LIST Der/h = Der/h ;				 #!! * Der/h
LIST Der/Car = Der/Car ;		 #!! * Der/Car
LIST Der/Caus = Der/Caus ;		 #!! * Der/Car
LIST Der/huhtti = Der/huhtti ;		 #!! * Der/huhtti
LIST Der/huvva = Der/huvva ;		 #!! * Der/huvva
LIST Der/halla = Der/halla ;		 #!! * Der/halla
LIST Der/l = Der/l ;				 #!! * Der/l
LIST Der/lasj = Der/lasj ;			 #!! * Der/lasj
LIST Der/las = Der/las ;			 #!! * Der/las
LIST Der/meahttun = Der/meahttun ;	 #!! * Der/meahttun
LIST Der/muš = Der/muš ;			 #!! * Der/muš
LIST Der/NomAct = Der/NomAct ;		 #!! * Der/NomAct
LIST Der/sasj = Der/sasj ;				 #!! * Der/sasj
LIST Der/st = Der/st ;				 #!! * Der/st
LIST Der/stuvva = Der/stuvva ;		 #!! * Der/stuvva
LIST Der/upmi = Der/upmi ;			 #!! * Der/upmi
LIST Der/supmi = Der/supmi ;		 #!! * Der/supmi
LIST Der/vuota = Der/vuota ;		 #!! * Der/vuota
LIST Der/InchL = Der/InchL ;		 #!! * Der/InchL
LIST Der/laakan = Der/laakan ;		 #!! * Der/laakan
LIST Der/laagasj = Der/laagasj ;		 #!! * Der/laagasj
LIST Der/jagáš = Der/jagáš ;		 #!! * Der/jagáš
LIST Der/A = Der/A Der/A* ;				 #!! * Der/A Der/A* pga av bug i lookup2cg
LIST Der/Dimin = Der/Dimin ;		 #!! * Der/Dimin
LIST Der/viđá = Der/viđá ;			 #!! * Der/viđá
LIST Der/viđi = Der/viđi ;			 #!! * Der/viđi
LIST Der/veara = Der/veara ;		 #!! * Der/veara
LIST Der/AAdv = Der/AAdv ;			 #!! * Der/AAdv
LIST Der/Adv = Der/Adv ;			 #!! * Der/Adv
LIST Der/dáfot = Der/dáfot ;			 #!! * Der/dáfot 
LIST Der/keahtta = Der/keahtta ;			 #!! * Der/keahtta
LIST Der/nuolus = Der/nuolus ;			 #!! * Der/nuolus
LIST Der/náittot = Der/náittot ;			 #!! * Der/náittot
LIST Der/seagat = Der/seagat ;			 #!! * Der/seagat
LIST Der/suttat = Der/suttat ;			 #!! * Der/suttat
LIST Der/ár = Der/ár ;			 #!! * Der/ár 
LIST <vdic> = <vdic> ;				 #!! * <vdic>
LIST Cmp/Hyph = Cmp/Hyph ;        #!! * Cmp/Hyph 
LIST <subqst> = <subqst> ;
LIST <ind> = <ind> ;

LIST Err/Orth = Err/Orth Err/Lex Err/Orth-a-á Err/Orth-nom-gen Err/Orth-nom-acc Err/DerSub Err/CmpSub Err/UnspaceCmp Err/HyphSub Err/SpaceCmp Err/Spellrelax err_orth_mt ;
LIST Err/Orth-spes = Err/Orth-a-á Err/Orth-nom-gen Err/Orth-nom-acc Err/DerSub Err/CmpSub Err/UnspaceCmp Err/HyphSub Err/SpaceCmp Err/Spellrelax err_orth_a_á_mt err_orth_nom_acc_mt err_orth_nom_gen_mt  ;


#!! ### Semantic tags

INCLUDE semsets.cg3 ; # !!≈ @CODE@ includes the list of semantic tags from semsets.cg3


SET TIME-N-SET = N + Sem/Time ;
LIST NOT-TIME = "dulvi" "lohpi" "vuorru" ;
SET TIME-N = TIME-N-SET - NOT-TIME ;



#!! ###  Syntactic tags

LIST @+FAUXV = @+FAUXV ;	         #!! * @+FAUXV : finite auxiliary verb 
									 #!!     - ferte: Son ferte oaidnit ollislaš gova. - She must see the whole picture.
LIST @+FMAINV = @+FMAINV ;	         #!! * @+FMAINV : finite main verb
									 #!!     - oaidná: Son oaidná ollislaš gova. - She sees the whole picture	
LIST @-FAUXV = @-FAUXV ;	         #!! * @-FAUXV : infinite auxiliary verb
									 #!!     - sáhte: In sáhte gáhku borrat. - I cannot eat cake.	
LIST @-FMAINV = @-FMAINV ;	         #!! * @-FMAINV : infinite main verb
									 #!!     - oaidnit: Son ferte oaidnit ollislaš gova. - She must see the whole picture.
LIST @-FSUBJ> = @-FSUBJ> ;	         #!! * @-FSUBJ> : Subject of infinite verb outside the verbal.
									 #!!     - mu: Diet dáhpáhuvai mu dieđikeahttá. - It happened without me knowing about it.
LIST @-F<OBJ = @-F<OBJ ;	         #!! * @-F<OBJ : Subject of infinite verb outside the verbal.
									 #!!     - nuppi: Ulbmil lea oažžut nuppi boagustit. - The goal is to get the other one to laugh.
LIST @-FOBJ> = @-FOBJ> ;	         #!! * @-FOBJ> : Object of infinite verb outside the verbal.
									 #!!     - váldovuoittuid: Sii vurde váldovuoittuid fasket. - They waited to grab the main prizes.
LIST @SPRED<OBJ = @SPRED<OBJ ;       #!! * @-FSPRED<OBJ
LIST @-F<ADVL = @-F<ADVL ;	         #!! * @-F<ADVL
LIST @-FADVL> = @-FADVL> ;	         #!! * @-FADVL>
LIST @-F<SPRED = @-F<SPRED ;         #!! * @-F<SPRED
LIST @-F<OPRED = @-F<OPRED ;         #!! * @-F<OPRED
LIST @-FSPRED> = @-FSPRED> ;         #!! * @-FSPRED>
LIST @-FOPRED> = @-FOPRED> ;         #!! * @-FOPRED>
LIST @>ADVL = @>ADVL ;               #!! * @>ADVL
LIST @ADVL< = @ADVL< ;               #!! * @ADVL<
LIST @<ADVL = @<ADVL ;               #!! * @<ADVL
LIST @ADVL> = @ADVL> ;               #!! * @ADVL>
LIST @>N = @>N ;		      #!! * @>N
LIST @Interj = @Interj ;    #!! * @Interj
LIST @N< = @N< ;		      #!! * @N<
LIST @>A = @>A ;		      #!! * @>A
LIST @P< = @P< ;		      #!! * @P<
LIST @>P = @>P ;		      #!! * @>P
LIST @HNOUN = @HNOUN ;	      #!! * @HNOUN
LIST @INTERJ = @INTERJ ;      #!! * @INTERJ
LIST @>Num = @>Num;		      #!! * @>Num
LIST @Pron< = @Pron< ;	      #!! * @Pron<
LIST @>Pron = @>Pron ;	      #!! * @>Pron
LIST @Num< = @Num< ;	      #!! * @Num<
LIST @OBJ = @OBJ ;			  #!! * @OBJ
LIST @<OBJ = @<OBJ ;		  #!! * @<OBJ
LIST @OBJ> = @OBJ> ;		  #!! * @OBJ>
LIST @OPRED = @OPRED ;		  #!! * @OPRED
LIST @<OPRED = @<OPRED ;	  #!! * @<OPRED
LIST @OPRED> = @OPRED> ;	  #!! * @OPRED>
LIST @PCLE = @PCLE ;		  #!! * @PCLE
LIST @COMP-CS< = @COMP-CS< ;  #!! * @COMP-CS<
LIST @SPRED = @SPRED ;		  #!! * @SPRED
LIST @<SPRED = @<SPRED ;	  #!! * @<SPRED
LIST @SPRED> = @SPRED> ;	  #!! * @SPRED>
LIST @SUBJ = @SUBJ ;		  #!! * @SUBJ
LIST @<SUBJ = @<SUBJ ;		  #!! * @<SUBJ
LIST @SUBJ> = @SUBJ> ;		  #!! * @SUBJ>
LIST @PPRED = @PPRED ;        #!! * @PPRED
LIST @APP = @APP ;			   #!! * @APP
LIST @APP-N< = @APP-N< ;	   #!! * @APP-N<
LIST @APP-Pron< = @APP-Pron< ; #!! * @APP-Pron<
LIST @APP>Pron = @APP>Pron ;   #!! * @APP>Pron
LIST @APP-Num< = @APP-Num< ;   #!! * @APP-Num<
LIST @APP-ADVL< = @APP-ADVL< ; #!! * @APP-ADVL<
LIST @VOC = @VOC ;			   #!! * @VOC : Vocative
							   #!!     - Miss Turner : Bures boahtin deike, Miss Turner! - Welcome here, Miss Turner!
LIST @CVP = @CVP ;			   #!! * @CVP : Conjunction or subjunction that conjoins finite verb phrases.
							   #!!     - go : Leago guhkes áigi dassá go Máreha oidnet? - Is it a long time since you saw Máret?
LIST @CNP = @CNP ;			   #!! * @CNP : Local conjunction or subjunction.
							   #!!     - vai : Leago nieida vai bárdni? - Is it a girl or a boy?
LIST @X = @X ;    			   #!! * @X : unknown
LIST ADVL = @ADVL @ADVL> @ADVL< @<ADVL @>ADVL @-F<ADVL @-FADVL> ;  
SET SUBJ = @<SUBJ OR @SUBJ> OR @SUBJ ;
SET SPRED = @<SPRED OR @SPRED> OR @SPRED ;
SET OPRED = @<OPRED OR @OPRED> OR @OPRED ;
SET FOBJ = @-F<OBJ OR @-FOBJ> ; 
SET FMAINV = @-FMAINV OR @+FMAINV ;
SET FAUXV = @-FAUXV OR @+FAUXV ;
SET OBJ = (@<OBJ) OR (@OBJ>) OR (@OBJ) OR (@-F<OBJ) OR (@-FOBJ>) ;
LIST <OBJ = @-F<OBJ @<OBJ ;    
LIST OBJ> = @OBJ> @-FOBJ> ;    
SET <OBJ-OTHERS = <OBJ OR (Gen) OR (Nom) OR (Ess) OR (Loc) OR (Adv) ;
SET OBJ>-OTHERS = OBJ> OR (Gen) OR (Nom) OR (Ess) OR (Loc) OR (Adv) ;
SET NOT-FAUXV = FMAINV OR OBJ + V OR ADVL + V ;
# Works after the mapping rules for verbs.
SET SYN-V = FMAINV OR FAUXV OR V + SUBJ OR OBJ + V OR ADVL + V OR (V @>N) OR (V @N<) OR (V @A<) ;

LIST WORDLEMMA = (".*"r) ;



# ========
  SETS
# ========


#!! ## Sets containing sets of lists and tags

#!! This part of the file lists a large number of sets based partly upon the tags defined above, and 
#!! partly upon lexemes drawn from the lexicon.
#!! See the sourcefile itself to inspect the sets, what follows here is an overview of the set types.



#!! ### Sets for Single-word sets

#!! OKTA and go, and the set INITIAL for initial letters 
LIST OKTA = "okta" "akta"; #!! OKTA
LIST go = "go" ; #!! go
LIST INITIAL = "a" "b" "c" "d" "e" "f" "g" "h" "i" "j" "k" "l" "m"
        "n" "o" "p" "q" "r" "s" "t" "u" "v" "w" "x" "y" "z"
        "á" "æ" "ø" "å" "ö" "ä" ; #!! INITIAL


#!! ### Sets for word or not

LIST WORD = N A Adv V Pron CS CC Po Pr Interj Pcle Num ABBR ACR  \? ; #!! WORD
 # any word
LIST WORD-NOTMISSING = N A Adv V Pron CS CC Po Pr Interj Pcle Num ABBR ACR ;
SET REAL-WORD = WORD - Num - Ord ; #!! REAL-WORD
SET REAL-WORD-NOT-ABBR = WORD - Num - Ord - (ABBR N) ; # This is former REALWORD-NOTABBR #!! REAL-WORD-NOT-ABBR
SET WORD-NOT-de = WORD - ("de") ; #!! WORD-NOT-de
SET NOT-COMMA = WORD - COMMA ;  #!! NOT-COMMA
 # Note! We also have CLB_NOT-COMMA


#!! ### Derivational affixes

LIST DER-V = Der/adda Der/alla Der/d Der/g Der/h Der/halla Der/huhtti Der/huvva Der/l Der/st Der/stuvva Der/InchL Der/Caus ; #!! DER-V

LIST DER-V2 = Der/adda Der/alla Der/halla Der/huhtti Der/huvva Der/stuvva Der/InchL  ; #!! DER-V


LIST DER-N = Der/Dimin Der/eamoš  Der/Car Der/NomAct Der/muš Der/upmi Der/vuota Der/NomAg Der/NomAct ; #!! DER-N

LIST DER-A1 = Der/sasj Der/geahtes Der/Car Der/laakan Der/laagasj Der/lasj Der/meahttun Der/t (Ex/V A) Der/A  ; #!! DER-A1

SET DER-A = DER-A1 - Adv ; #!! DER-A

LIST A-V = (Ex/V A) ; #!! A-V

SET A-NOT-V = A - A-V ; #!! A-NOT-V

#!! ### Case sets
# ---------

SET ADVLCASE = Ill OR Loc OR Com OR Ess - (Actio Ess) ; #!! ADLVCASE

# Sg Nom - Sg Nom | Pl Nom - Pl Nom
# Sg Acc - Sg Acc | Pl Acc - Pl Acc
# Sg Gen - Sg Gen | Pl Gen - Pl Gen
# Sg Gen - Sg Ill | Pl Gen/Ill - Pl Ill 
# Sg Gen - Sg Loc | Pl Loc - Pl Loc
# Sg Com - Sg Com | Pl Gen/Com - Pl Com
# Ess - Ess

LIST CASE-HALFAGREEMENT = Ill (Sg Loc) (Pl Com) Ess ; #!! CASE-HALFAGREEMENT
LIST CASE-AGREEMENT = Nom Acc Gen (Pl Ill) Loc Com Ess ; #!! CASE-AGREEMENT
LIST CASE = Nom Gen Acc Ill Loc Com Ess ; #!! CASE

SET NOT-NOM = CASE - Nom ; #!! NOT-NOM
SET NOT-GEN = CASE - Gen ; #!! NOT-GEN
SET NOT-ACC = CASE - Acc ; #!! NOT-ACC

#!! ### Verb sets
# ---------

# Verbs and their complements
# - - - - - - - - - - - - - -

SET NOT-V = WORD - V ; #!! NOT-V

#!! ### Sets for finiteness and mood
# - - - - - - - - - -

SET REAL-NEG = Neg - Sup ; #!! REAL-NEG

SET MOOD-V = Ind OR Pot OR Imprt OR ImprtII OR Cond OR (Neg Sup) ; #!! MOOD-V

LIST GC = ("gč") ; #!! GC

SET VFIN = GC OR MOOD-V - ConNeg ; #!! VFIN

SET VFIN-POS = MOOD-V - ConNeg - Neg ; #!! VFIN-POS

SET VFIN-NOT-IMPRT = VFIN - Imprt ; #!! VFIN-NOT-IMPRT

SET VFIN-NOT-NEG = VFIN - Neg ; #!! VFIN-NOT-NEG
  # this might be to strict, besides, 'iige' can be written 'ii ge'

SET NOT-PRFPRC = WORD - PrfPrc ; #!! NOT-PRFPRC


#!! ### Sets for person
# - - - - 

LIST SG1-V = (V Sg1)  (vblex p1 sg)  ;
 
LIST SG2-V = (V Sg2)  (vblex p2 sg) ;
 
LIST SG3-V = (V Sg3)  (vblex p3 sg) ;
 
LIST DU1-V = (V Du1)  (vblex p1 du) ;
 
LIST DU2-V = (V Du2)  (vblex p2 du) ;
 
LIST DU3-V = (V Du3)  (vblex p3 du) ;
 
LIST PL1-V = (V Pl1) (vblex p1 pl) ;
 
LIST PL2-V = (V Pl2) (vblex p2 pl) ;
 
LIST PL3-V = (V Pl3)  (vblex p3 pl) ;

#SET POSITIVE-V = SG1-V OR SG2-V OR SG3-V OR DU1-V OR DU2-V OR DU3-V OR PL1-V OR PL2-V OR PL3-V ;  This set doesn't function in Apertium


 # Note that imperative verbs are not included in these sets!

# Some subsets of the VFIN sets
# - - - - - - - - - - - - - - -
SET SG-V = SG1-V OR SG2-V OR SG3-V ;
SET DU-V = DU1-V OR DU2-V OR DU3-V ;
SET PL-V = PL1-V OR PL2-V OR PL3-V ;

SET DU-PL-V = DU1-V OR DU2-V OR DU3-V OR PL1-V OR PL2-V OR PL3-V ;

SET 1-2-V = SG1-V OR SG2-V OR DU1-V OR DU2-V OR PL1-V OR PL2-V ;

SET 2-V = SG2-V OR DU2-V OR PL2-V ;


#!! ### Sets consisting of forms of "leat" (these ones need to be rewritten)
# - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
SET LEAN = ("leat") + Prs + Sg1 + Ind OR ("leat") + V + Prt + ConNeg ;
SET LEAT = ("leat") + Prs + Pl1 + Ind OR ("leat") + V + Prs + Pl3 + Ind OR 
 ("leat") + V + Prs + Sg2 + Ind OR ("leat" V Inf) OR ("leat" V Prs ConNeg) + Ind ;

SET LEAT-FIN-NOT-IMPRT = ("leat" V) - Imprt;
 
LIST PRFPRC-V = (V PrfPrc) ;
LIST LEATPRFPRC = ("leat" PrfPrc) ;
SET PRC-NOT-LEAT = PRFPRC-V - LEATPRFPRC ;
    # In order to distinguish between real leat-constructions and participles of other verbs
LIST HABEO-SG3 = ("leat" Sg3) (V Neg Sg3) ("šaddat" Sg3) ("leat" p3 sg) (vblex neg p3 sg) ("šaddat" p3 sg) ;

LIST HABEO-PL3 = ("leat" Pl3) (V Neg Pl3) ("šaddat" Pl3) ("leat" p3 pl) (vblex neg p3 pl) ("šaddat" p3 pl) ;

#!! ### Pronoun sets
# ------------

LIST MUN = (Pron Pers Sg1 Nom) (pers p1 sg nom) ;
LIST DON = (Pron Pers Sg2 Nom) (pers p2 sg nom) ;
LIST SON = (Pron Pers Sg3 Nom) (pers p3 sg nom) ;
LIST MOAI = (Pron Pers Du1 Nom) (pers p1 du nom) ;
LIST DOAI = (Pron Pers Du2 Nom) (pers p2 du nom) ;
LIST SOAI = (Pron Pers Du3 Nom) (pers p3 du nom) ;
LIST MII-PERS = (Pron Pers Pl1 Nom) (pers p1 pl nom) ;
LIST DII = (Pron Pers Pl2 Nom) (pers p2 pl nom) ;
LIST SII = (Pron Pers Pl3 Nom) (pers p3 pl nom) ;

SET PPRON-NOM-NOT-DAT = MUN OR DON OR SON OR MOAI OR DOAI OR SOAI OR MII-PERS OR DII OR SII ;

SET PPRON-DU-PL = MOAI OR DOAI OR SOAI OR MII-PERS OR DII OR SII ;

SET PRON-DU = MOAI OR DOAI OR SOAI ;

SET PPRON-NOT-SII = MUN OR DON OR SON OR MOAI OR DOAI OR SOAI OR MII-PERS OR DII ;

LIST PPRON-GEN = (Sg1 Gen) (Sg2 Gen) (Sg3 Gen) (Du1 Gen) (Du2 Gen) (Du3 Gen) 
	(Pl1 Gen) (Pl2 Gen) (Pl3 Gen) (p1 sg gen) (p2 sg gen) (p3 sg gen) (p1 du gen) (p2 du gen) (p3 du gen) 
	(p1 pl gen) (p2 pl gen) (p3 pl gen) ;

SET PPRON-NOT-GEN = (Pron Pers) - PPRON-GEN ;

LIST SG-DEM = (Pron Dem Sg Nom);
LIST PL-DEM = (Pron Dem Pl Nom);
SET NOT-DEM = WORD - Dem ;

LIST SG-PRON = (Pron Sg1) (Pron Sg2) (Pron Sg3) (Pron Sg) (Pron PxSg1) (Pron PxSg2) (Pron PxSg3) (prn p1 sg) (prn p2 sg) (prn p3 sg);
LIST DU-PRON = (Pron Du1) (Pron Du2) (Pron Du3)  (Pron PxDu1) (Pron PxDu2) (Pron PxDu3) (prn p1 du) (prn p2 du) (prn p3 du);
LIST PL-PRON = (Pron Pl1) (Pron Pl2) (Pron Pl3) (Pron Pl) (Pron PxPl1) (Pron PxPl2) (Pron PxPl3) (prn p1 pl) (prn p2 pl) (prn p3 pl);
LIST DU-PRON-NOTPX = (Pron Du1) (Pron Du2) (Pron Du3) (prn p1 du) (prn p2 du) (prn p3 du) ;
LIST FIRST-PX = PxSg1 PxDu1 PxPl1 ;


LIST DU-NR = Du1 Du2 Du3 (p1 du) (p2 du) (p3 du) ; 

SET NOT-SG-PRON = DU-PRON OR PL-PRON ;

LIST DAT-PRON = ("dat") ("dát") ("diet") ("duot") ("dot") ;

LIST QUANT-PRON = "ollu" "olu" "unnán" "uhccán" "uhcán" "váháš" "veaháš" "veháš" ;
# This set is for choosing between Adv and Pron Indef.


#!! ### Adjectival sets and their complements
# -------------------------------------
SET LEX-A = A - DER-A ;

SET A-CASE = A - Attr - Adv ;

LIST A-CC = A CC ;

SET NOT-A = WORD - A ; # This is former NOT-ADJ
SET NOT-A-COMMA = WORD - A - COMMA ;
SET NOT-Attr = WORD - Attr ;
SET NOT-A-PCLE = WORD - A - Pcle ;
SET NOT-A-CC = WORD - A-CC ;

# This set was removed, for a good reason?
SET NOT-A-ADV = WORD - A - Adv OR ("maid") ;

LIST NEG-ADV-PRON = "gostege" "goassege" "ge" (Adv Foc/Neg-ge) (Pron Foc/Neg-ge)  ;

LIST NOMINAL-A = "guoktilaš" "lámis" "oasálaš" "suttolaš" "viissis";

LIST OLLU = "ollu" "olu" ;
 
 # and many others
 
#!! ### Adverbial sets and their complements
# ------------------------------------
SET LEX-ADV = Adv - (Ex/A) - Der/keahtta ;

SET NOT-ADV-DE            = WORD - Adv ;
SET NOT-ADV            = NOT-ADV-DE OR ("de" Adv) OR CLB ;
SET NOT-ADV-N          = NOT-ADV - N;
SET NOT-ADV-PCLE       = NOT-ADV - Pcle ;
SET NOT-ADV-INDEF      = NOT-ADV - Indef ;
SET NOT-ADV-PCLE-ILL-LOC-COM = WORD - Adv - Pcle - Ill - Loc - Com;
SET NOT-A-ADV-PCLE = WORD - Pcle - A - Adv OR ("maid") ; # 

LIST MOVT = "govt" ("got" Adv) "mo" "mot" "movt" ;

LIST MO-MANge = "danne" "goas" "gokko" "gos" "gosa" "govt" ("got" Adv) "makkár" "man" ("man ládje" Adv) ("man_ládje" Adv)  "manne" "guđe_ládje" "guđe ládje" ("manin" Adv) "mo" "mot" "movt" ("nugo" @CVP) (V Qst);

SET MO = MO-MANge - Foc/ge ; 
 
 # Introduce finite clauses.
 
LIST PLACE-ADV =  "bajábealde" "bajábeallai" "davábealde" "davábeallai" "lagabus" "máttabealde" "máttabeallai" "nuorttabealde" "nuorttabeallai" "oarjjábealde" "oarjjábeallai" "oarjjabealde" "oarjjabeallai" "olggobeale" "olggobeallai" "vuolábealde" "vuolábeallai" ;
# There will usually be a Gen in front. 

LIST DOPPE = "badjin" "bajil" "dakko" "davveoarjin" "davvin" "dá" "dákko" "dáppe" "diekko" "dieppe" "do" "dokko" "doppe" "duohkin" ("duo" Adv) "duokko" "duoppe" "gaskan" "guhkkin" "olgun" "lulde" "lullin" "nuortan" "oarjin" "veastan" "vuollin" ;

# Adverbs with a locativic form, but don´t get Loc as a tag. 

LIST DOHKO = "bajás" "báldii" "deike" "deikke" "diehke" "diehko" "dohko" "duohko" "gaskii" "gurrii" "lulás" "olggos" "ruoktot" "sisa" "vuovdimassii" "vulos" "vuolás" ;
# Adverbs with a illativic form, but they don´t get Ill as a tag.

LIST PO-ILL = "ala" "lusa" "vulos" "bajás" "vuollái" "geahčai" ;	


#!! ### Sets for coordinators
# ------------
SET Foc = Foc/ge  OR Foc/gen  OR Foc/ges  OR Foc/gis  OR Foc/naj OR Foc/ba  OR Foc/be  OR Foc/hal  OR Foc/han  OR Foc/bat OR Foc/son OR Foc/mis OR Foc/mat ;

LIST NEGFOC = (Neg Foc/Neg-ge) ;

LIST XGO = "dego" "dugo" "nugo" "seammaládjego" "seammaláhkáigo" ;
  # Compounds
LIST SEAMMAX = "seamma_ládje" "seamma_láhkái" "seamma ládje" "seamma láhkái" ;
  # Those combine with go
  
LIST MADE = "mađe" "mađi" ;
LIST DADE = "dađe" "dađi" ; 
  
SET CRD = COMMA OR CC OR NEGFOC OR XGO OR ("/") OR ("-") OR DADE ;
	# coordinators
	
SET NOT-CRD = WORD - CRD ; 
	
SET LCRD = @CNP OR ("asdfasdf") ; 
    # local coordinator
SET GCRD = @CVP OR ("sadfasdf") ;
    # global coordinator
    
SET CRD2 = (@CNP) OR COMMA OR NEGFOC OR XGO OR ("/") OR ("-") OR DADE ;
    # AFTER LCRD vs. GCRD disambiguation
    
#!! ### Sets for adverbs that have lookalikes
# -------------------------------------
 #!! Here come some adverbs that have identical twins in other POS. 
 #!! If these are found in Adv contexts, we treat them as adverbs.

LIST LACCAT-ADV = "absoluhtalaš" "agálaččat" "aitosaččat" "aktiivvalaš" "almmolaš" "analogalaš" "automáhtalaš" "álgoálgosaš" "álgovuolggalaš" "árbevirolaš" "beaivválaččat" "biologalaš" ("buhttet" Der/lasj) "bustávalaš" "čálalaččat" "čearddalaš" "dábálaš" "duođalaččat" ("dutkanpolitihkka" Der/lasj) "eahpedábálaš" "eahpeformálalaš" "eahpekritihkalaš" "eahpevirggálaš" "eaktodáhtolaččat" "ekologalaš" "ekologálaš" "ekonomálaš" "eksplisihtalaš" "elektrovnnalaš" ("eahpidit" VAbess) "etihkalaš" "etymologalaš" "fágalaš" "fonehtalaš" "fonologalaš" "fonotávttalaš" "formálalaš" "fysiologalaš" "gaskamearálaš" "geavatlaš" "gelbbolaš" "geográfalaš" "gielalaš" "girkorievttálaš" "globálalaččat" "gorálaš" "govttolaš" "hálddahuslaš" "historjjálaš" "ideologalaš" "iešheanálaš" "implisihtalaččat" "jahkásaččat" "jeavddalaččat" ("jietnadatoahppa" Der/lasj) "juridihkalaš" "konkrehtalaš" "kritihkalaš" "kronologalaš" "kulturhistorjjálaš" "kulturpolitihkalaš" "kultuvrralaš" "logihkalaš" "luđolaš"  "máŋggafágalaš" "mekánalaš" "metodalaš" "moralalaš" "morálalaš" "morfologalaš" "našuvnnalaš" "negatiivvalaš" "njálmmálaččat" "njuolgguslaččat" "oaiveáššálaš" "obbalaš" "obbalohkái" "oktasaččat" "oktilaš" "ollislaččat" "oppalaččat" "optimalalaččat" "ovttabealálaš" "ovttajienalaš" "ovttamielalaš" "pedagogalaš" "politihkalaš" "praktihkalaš" "prinsihpalaš" ("priváhtariekti" Der/lasj) "priváhtarievttálaš" "profehtalaš" "psykologalaš" "ráfálaččat" ("reálapolitihkka" Der/lasj) "riektehistorjjálaš" "rievttálaš" "riikkaidgaskasaš" "sámegielalaš" "riikkagottálaš" "ruđalaš" "rumašlaš" "sámepolitihkalaš" "sekundáralaččat" "semantihkalaš" "servodatávkkálaš" ("siviilariekti" Der/lasj) "sosiálalaš" ("stáhtapolitihkka" Der/lasj) "strategalaš" "teknihkalaš" "teorehtalaš" "topográfalaš" ("váldoášši" Der/lasj) "virggálaš" "virolaš" ;

LIST MOD-NP-ADV = "áibbas" "dušše" "erenoamážit" "goitge" "man" "measta" "meastui" "oalle" "oba" "oktiibuot" "oppa" "sullii" ;

LIST MOD-ADV-ADV = ("áibbas" Adv) "badjelmeare" ("eanet" Adv) "čielgasit" ("erenoamáš" Adv) ("erenoamáš" Adv) ("hirbmat" Adv) "hui" "man" ("mealgat" Adv) "measta" "menddo" "oalle" "nu" "oba" "oppa" ;

LIST EASKKA = "easkka" "easka" ; 

LIST NOT-ARG-ADV = "aiddo" "ain" "aivve" "albma" "aŋkke" "álggos" "báifáhkka" "beanta" "beare" ;

#LIST NOT-ARG-ADV2 = "aiddo" "ain" "aivve" "albma" "aŋkke" "álggos" "álgobáliid" "álo" "báifáhkka" "beanta" "beare" "dakkaviđe" "dal" "dan_dihte" "danne" "dasto" "dattetge" ("dábálaš" Adv) "dávjá" "de" "dieđinge" "dieđusge" "duođaid" "easkka" "fal" "fargga" "fas" "fárrolaga" "gal" "goas" "goasse" "gos" "gosa" "gostege" "govt" "gusto" "hedi" "juo" "justa" "loahpas" "maiddái" "manin" "manne" "nuppes" "suige" "šat" "vel" "viimmat" "vuos" ;


LIST ADV-NOT-V = "dušše" "viimmat" ;

LIST ADV-NOT-N = "easka" "várra" ;


LIST ADV-NOT-PROP = "Ain" "Anne" "Diego" "Dieppe" "Enge" "Galle" "Haga"
           "Joba" "Johan" "Liikka" "Mai" "Mannes" "Mo" "Naba"
           "Nan" "Oktan" "Sierra" "Sokka" "Villa" ;

SET ADV-NOT-OTHER-POS = ADV-NOT-V OR ADV-NOT-N OR ADV-NOT-PROP ;

LIST MANNEL = "maŋŋel" "maŋŋá"  ;


#!! ### Sets of elements with common syntactic behaviour
# ================================================

SETS

#!! ### Sets for verbs
# -----

LIST NOT-REAL-V = (Actio Nom) (Actio Gen) (Actio Loc) (Actio Com) PrsPrc Ger ;

SET REAL-V = V - NOT-REAL-V  ;
	#!! V is all readings with a V tag in them, REAL-V should
	#!! be the ones without an N tag following the V.  
	#!! The REAL-V set thus awaits a fix to the preprocess V ... N bug.
            
SET LEX-V = V - DER-V ;
       # VERB but not DER-V
 
 # The set REAL is smaller than COPULAS, made for verbs with PrfPrc complements: Seammás REAL-COPULAS son dovdan iežas...
LIST REAL-COPULAS = "dáidit" "leat" "soaitit" "veadjit" ;

#!! * The set COPULAS is for predicative constructions
LIST COPULAS = "dáidit" "gártat" "leat" "soaitit" "šaddat" "orrut" "veadjit" ;
 #  "bissut" ? 
 # 'Dáidit' can appear without 'leat'.

SET NOT-COP-V = V - COPULAS ;

LIST MOD-ASP = "áigut" "bállet" "berret" "dáidit" "fertet" "galgat" "gillet" "lávet" "sáhttit" "seahtit" "soaitit" "viggat" ;

 # Verbs that never have arguments of their own - so we leave out "áigut", for ex.

LIST AUX-OR-MAIN = "áigut" "álgit" "beassat" "galgat" "háliidit" "máhttit" "ollit" "orrut" "sihtat" "veadjit"; #"nagodit"  "nagadit" "

 # These verbs can take arguments, so they do not belong in the AUX group, 
 # but they are nevertheless mapped to (@+FAUXV). 
 # Added "álgit" to the group (Lene) - Made rules for "boahtit" which  sometimes can be AUX
 # Magga (1982: 120): áigut, dáidit, deaivat, fertet, galgat, lávet, máhttit, oažžut, sáhttit, soaitit, veadjit - in some of the meanings 
 # Halvhjelpeverb: álgit, berret, dohkket, gártat, heivet, liigut, šaddat - in some of the meanings 
 # Svonni 2015 s. 101: astat, áigut, beassat, dáidit, duostat, fertet, galgat, máhttit, oažžut, sáhttit, soaitit, šaddat, veadjit
 
SET AUX = COPULAS OR Neg OR MOD-ASP OR AUX-OR-MAIN ;
 

SET NOT-AUX-V = V - AUX ; # This is former V-NOT-AUX
 # The set of verbs that cannot be auxiliaries. 
			  # Does not include AUX-OR-MAIN.
SET VFIN-NOT-AUX = VFIN - AUX ;

SET MAIN-V = V - COPULAS - Neg - MOD-ASP OR ("orrut") ;
 
# All active verbs with a TV tag, including AUX-OR-MAIN.


LIST V-TRANS = (V TV) (Der/h V)  (Der/Caus V) ;

 
SET TRANS-ACT-NOT-AUX-V =  V-TRANS - Der/Pass - AUX ;

#!! TRANS-V is the set for verbs really taking objects

SET TRANS-V = V-TRANS - Der/Pass + REAL-V ;

#SET TRANS-V = V-TRANS - Der/Pass - (Actio Nom) - (Actio Gen)  - (Actio Loc) -  (Actio Com) - PrsPrc ;

#!! * Sets for verbs choosing oblique objects or adverbials
# - - - - - - - - - - - - - - - - - - - - - - - - - - - 
# bargat "čoallut" "borrat" lohkat "gazzat" vuoššat
#!! * **STVLIST** is the list of strictly transitive verbs. In the rules, refer not to STVLIST, but to the set STV defined below.
LIST STVLIST = (Der/h V) (Der/Caus V) "addit" "amašit" "atnit" "álggahit" "ánssášit" "árvalit" "árvidit" "árvvoštallat" "ásahit" "ávžžuhit" "bajidit" "bassat" "bearrat" "biebmat" "bidjat" "bivdit" "bilidit" "billistit" "bissehit" "bisuhit" ("bissut" der_h) "boaldit" "boktit" "borahit" ("borrat" der_h) "buhtadit" "buktit" "buoridit" "caggat" "coggalit" "coggalit" "cealkit" "cegget" "čađahit" "čatnat" "čájehit" "čiehkat" "čiegadit" "čielggadit" ("čielgat" der_d) "čilget" "čoaggit" "čoavdit" "čohkket" "čuoččuhit" ("čuožžut" der_h) "čuohppat" "čuollat" "čuovvut" "čuovvulit" ("čuovvut" der_l) "dadjat" "dagahit" ("dahkat" der_h) "dahkat" "dáhkidit" "dáhttut" "dárbbahit" "dárbbašit" "dárkkistit" "dávistit" "deaivat" "deattuhit" "deavdit" "diehtit" "dikšut" "diktit" "doahttalit" "doaimmahit" ("doaibmat" der_h) "doalahit" ("doallat" der_h) "doallat" "doalvut" "doarjut" "dohkkehit" ("dohkket" der_h) "dovdat" "dovddahit" ("dovdat" der_h) "dulkot" "duođaštit" "eahpidit" "eaiggáduššat" "eaktudit" "ealáskahttit" "ealihit" ("eallit" der_h) "eavttuhit" "evttohit" "fáhtet" "fállat" "fidnet" "fievrridit" "fuobmát" "fuolahit" ("fuollat" der_h) "fuomášit" "garvit" "gaskkustit" "gazzat" "gádjut" "gáhttet" "gáibidit" "gáržžidit" "gávdnat" ("gávnnadit" Sg1) ("gávnnadit" Sg2) ("gávnnadit" Sg3) ("gávdnat" der_d sg) "geahčadit" ("geahččat" der_d) "geassit" "geasuhit" "geatnegahttit" "geavahit" "gieđahallat" "giddet" "givssidit" "goddit" "gohččut" "gohčodit" ("gohččut" der_d) "goikadit" "gokčat" "goldit" "gozihit" ("gohcit" der_h) "guoddalit" "guođđit" "guođohit" "guorahallat" "guorrat" "guoskkahit" ("guoskat" der_h) "hábmet" "háhkat" "hálddašit" "heaittihit" ("heaitit" der_h) "heivehit" ("heivet" der_h) "hilgut" "ipmirdit" "iskat" "iskkadit" ("iskat" der_d) "jeahkit" "jođihit" ("johtit" der_h) "juksat" "juohkit" "juolludit" "kártet" "lágidit" "láhčit" "láigohit" "loahpahit" ("loahppat" der_h) "lohpidit" "loktet" "luoikat" "luoikkahit" ("luoikat" der_h) "luoitit" "massit" "mearkkašit" "mielddisbuktit" "movttiidahttit" ("movttiidit" der_ahtti) "muddet" "muitalit" "muitit" "muittuhit" "namahit" "nammadit" "namuhit" "nannet" "nanosmahttit" "njiellat" "nuppástuhttit" "oaidnit"  "oastit" "oaivvildit" "oamastit" "oažžut" "ođasmahttit" "ođastit" "olahit" "ollašuhttit" "organiseret" "ovdanbuktit" "ovdánahttit" "ovddastit" "ovddidit" "prioriteret" "ráddjet" "ráđđet" "ráfáidahttit" "ráfáiduhttit" "ráhkadit" "rávkat" "rávvet" "rievdadit" "rihkkut" "sakŋadit" "sáddet" "sáhtostit" "sávvat" "seaguhit" "seailluhit" ("seailut" der_h) "sihkkarastit" "sihtat" "siskkildit" "sisttisdoallat" "skeŋket" "stevdnet" "suodjalit" "suovvat" "unnidit" "vađastit" "vahágahttit" "váillahit" "váldit" "válljet" "várret" "vásihit" "váttásmahttit" "viežžat" "viiddidit" "virgádit" "vuhtiiváldit" "vuođđudit" "vuođđudallat" "vuohttit" "vuoitit" "vuojuhit" ("vuodjut" der_h) "vuoruhit" "vuostáiváldit" "vuosttaldit" "vuovdit" ; #removed geahččat (geahččat suorpmaid čađa)

	# Strictly transitiv-verbs - a start. This set is ment as a help choosing Acc or Gen. Example: "Mun borren eatni kafeás." - there is Gen, no Acc, even if the verb is transitive. That´s why "borrat" is not in this set.
	# These verbs: "addit", "fidnet" and "oažžut" can, when it´s about sex, be without object: "Son oaččui gonagasa bártnis." 

SET STRICT-TV-V = STVLIST + V - Der/Pass OR ("čielgat") + Der/d - Der/Pass  ; # because of apertium is čielgat here. I don't understand why it doesn't function inside the set.

#!! STRICT-TRANS-V is the set for verbs which don't let a GenAcc be a modifier of anything else than an object, e.g. Mun organiseren eatni gievkkanis. - eatni wants to be the object

SET STRICT-TRANS-V = STRICT-TV-V + REAL-V ;

LIST LOC-V = "ávkkástallat" "ballat" "beassat" "beroštit" "biehttalit" "bihtit" "ceavzit" "dinet" "dolkat" "earránit" "eastadallat" "eastadit" "fitnat" "fuolahit" "fuollat" "garvit" "gáibidit" "gažadit" "heaitit" "hehttet" "ilbmat" "jearrat" "jearralit" "luohpat" "máinnašit" "nohkkot" "oassádallat" "oassálastit" "spiehkastit" "váibat" "váruhit" "vástidit" "veaddit" ;

		#Note that "fuolahit" obviously has two homonymous variants: 
		#fuolahit1+Acc, fuolahit2+Loc

LIST COM-V = "álggahit" "álgit" "árvalit" "árvvohuššat" "ávkašuvvat" "bálddastahttit" "bálddalastit" "bártašuvvat" "birgehallat" "birget" "bivvat" "buohtastahttit" "čoahkkinastit" "deaivvadit" "deavdit" "digaštallat" "doarrut" "duhkoraddat" "ealihit" "flirtet" "gávnnadit" "gárvodit" "giksašuvvat" "gilvalit" "gulahallat" "gulaskuddat" "hilbošit" "joatkit" "juogadit" "juohkit" "kombineret" "lágastit" "láhttet" "leaikkastallat" "loahpahit" "lohpádaddat" "lohpádallat" "lonuhit" "meannudit" "malssidit" "molssodit" "náitalit" "oastit" "oktiibidjat" "oahpásmuvvat" "ovttasbargat" "ovttasdoaibmat" "ovttastahttit" "ovttastit" "ovttastuvvat" "rahčat" "ráđđádallat" "reivvestaddat" "riidalit" "riiddáskit" "rohcošit" "ságastit" "seaguhit" "servvoštallat" "servvoštaddat" "soabadit" "soahpat" "soahtat" "soardit" "stálostallat" "stoahkat" "šiehtadallat" "šiehtadit" "šiehttat" "veahkehit" "veardidit" "viiddidit" "vrd" "vuogáiduvvat" ; # vuogáiduvvat : when the choice is between Com and Loc, then it is Com
 
		#Note that "leaikkastallat" has two homonymous variants:
		#leaikkastallat1 (IV), leaikkastallat2+Com

LIST ILL-V = "áibbašit" "álgit" "ásaiduvvat" "báitit" "bahkket" "beassat" "čohkkedit" "čujuhit" "čuohcit" "deaivat" "doaškut" "dorvvastit" "došket" "duhtat" "gullat" "guoskat" "gustot" "hárjánit" "heivet" "irgidit" "irggástallat" "jáhkkit" "liikot" "luohttit" "mannat" "máhccat" "mieđihit" "miehtat" "njiedjat" "oahpásmuvvat" "ollet" "oskut" "riepmat" "ráhkkanit" "soahpat" "searvat" "suhttat" "váikkuhit" "vástidit" "vuolgit" ; 
###"geahččat" Lene: fjerna denne
 		

LIST TRANSL-V = "jápmit" "duššat" "hávkat" "náitalit" "gaskkalduvvat" "nohkat" "goarránit" "heahpanit" "boakčánit" "šlundut" "ráigánit" "vuollánit" "gopmánit" "fuotnánit" "roahppánit" "reakčanit" "bieđganit" "luoddanit" "guvggodit" "liegganit" "suorganit" "báhkkanit" "cuovkanit" "hedjonit" "lohppet" "goikat" "galbmot" "goallut" "ruostut" "buoidut" "guvggodit" "suvrut" "suddat" ;

  # Taken from Helander (2001) p. 89, might be useful in the future as those verbs typically take humans and places in illative case.
  
LIST REKS-V = "áibbašit" "deaivat" "dorvvastit" "duhtat" "gustot" "hárjánit" "jáhkkit" "liikot" "mieđihit" "ollet" "ráhkkanit"  "searvat" "vuolgit" "beroštit" "biehttalit" "bihtit" "ceavzit" "dinet" "dolkat" "eastadallat" "eastadit" "fuolahit" "fuollat" "geargat" "luohpat" "oassálastit" "váikkuhit" "váruhit" ;
# This set is for giving infitives @ADVL after the verb.   "soahpat"  "heaitit"

LIST ACC-LOC-V = "biehttalit" "čuovvut" "earuhit" "gieldit" "jearrat" ;

LIST ACC-ILL-V = "addit" "bidjat" "mearkkašit";
    # Two-place predicates that take a direct object and an illative adverbial.

LIST MIELDE-V = "addit" "bidjat" "bovdet" "čuovvut" "dáidit" "leat" "oastit" "oažžut" "váldit" ;
    # Verbs that take mielde as their complement.

# Son attii girjji mielde.

LIST TIME-ADVL-V = "ádjánit" "ássat" "bargat" "bistit" "bivdit" "eallit" "fievrridit" "guohtut" "joatkit" "láddet" "oahpahit" "ođasmahttit" ;
 
    # Do not take members of the time set as an object
    
LIST TIMEOBJECT-V = "addit" "atnit" "árvalit" "bálvalit" "čuovvut" "deavdit" "diŋgot" "evttohit" "fátmmastit" "gáibidit" "gávdnat" "geavahit" "gierdat" "guhkidit" "hábmet" "jorgalit" "juolludit" "lasihit" "liigudit" "loahpahit" "loaktit" "markeret" "mearridit" "molsut" "oanidit" "oažžut" "soahpat" "šiehttat" "várret" "viiddidit" ;

    # might take time as an object - verbs which are not members of PLACE-V: 
    
LIST DURATION-V = "ádjánit" "bargat" "bistit" "čuoigat" "eallit" "gollat" "guohtut" "leat" "sáddet" "vázzit";

LIST POINT-IN-TIME-V = "árrit" "bissehit" "caggat" "cakkadit" "cakkastallat" "doalahit" "doallat" "duostut" "dustet" "eastadit" "gieldit" "goahcat" "heađuštit" "hehttet" ;

LIST ROUTE-ADVL-V = "boahtit" "čuoigat" ;
 
    # bevegelse langsmed noe, innafor et bestemt område   
LIST ROUTEOBJECT-V = "ásahit" "bidjat" "botnjat" "basuhit" "buoridit" "čielggadit" "čuovvulit" "čilget" "čuorvut" "čurvet" "dieđihit" "duođaštit" "duorggastit" "geavahit" "geahpedit" "geahpidit" "gudnejahttit" "guorahallat" "gieđahallat" "guođđit" "gilljut" "heahtit" "huikit" "huikkádit" "juoigat" "lávlut" "lohkat" "loahpahit" "mearridit" "máidnut" "nannet" "njulget" "oahpahit" "ráhkadit" "sivdnidit" "suokkardit" "unnidit" "válmmaštit" "vurket" ;

LIST HAB-V = "boahtit" "leat" "goallut" "heaŋgát" "jápmit" "šaddat" ;
	# To be used in habitative-sentences.

#!! ### Valency sets
# - - - - - - -

LIST INF-V = "astat" "ádjánit" "áigut" "álgit" "ásahit" "bágget" "bávččagit" "beassat" "berret" "bivdit" "bivvat" "bistit" "boahtit" "bovdet" "čohkkát" "čohkkedit" "čohkánit" "čoahkkanit" "čuoččahit" "čuoččastit" "čuorvut" "čurggodit" ("dadjat" Der/PassL) ("dadjat" Der/PassS) "dagahit" "dáhttut" "dáidit" "dárbbašit" "defineret" "diktit" "doaivut" "doapmat" "duostat" "fertet" "fuobmát" "fuolahit" "galgat" "gáibidit" "gárrut" "gártat" "geahččalit" "geargat" "geatnegahttit" "gierdat" "gillet" "gohččut" "háhppehit" "háliidit" "hárjánit" "heivet" "lávet" "liikot" "lihkkat" "mannat" "máhttit" "máššat" "movttiidahttit" "muitit" "nagodit" "navdit" "nákcet" "oahpahit" "oahpástuvvat" "oahppat" "oažžut" "ollet" "ribahit" "riepmat" "sáhttit" "seahtit" "sihtat" "soaitit" "suovvat" "šaddat" "stađđat" "suovvat" "veadjit" "viggat" "viššat" "vuogáiduvvat" "vuolgit" "vuollánit" "vuordit";
 

LIST ACC-INF-V = "álgit" "bivdit" "čuorvut" "dagahit" "diktit" "gohččut" "movttiidahttit" "sihtat" "suovvat" ;

 # These are verbs taking accusative objects and infinitives at the same time;
 
 # a subset of INF-V.
 	#$ Ii suova nieiddas náitalit.

LIST OPRED-V = "alidit" "atnit" "álggahit" "ásahit" "bargat" "bidjat" "ceavzit" "dadjat" "dahkaluddat" "dahkat" "dáhttut" "doalahit" "dovdat" "dulkot" "gártat" "geavahit" "geahččat" "gohčodit" "gohččut" "kvalifiseret" "málet" "meroštallat" "nammadit" "nannet" "navdit" "oaidnit" "oaivvildit" "ovdánahttit" "ráhkadit" "sihkkarastit" "sivdnidit" "váldit" "válljet" "virgádit" "rábmot" "vihahit" ;

 # Verbs that occur with objects that have secondary predicates.
 
LIST OKTII-V = ("boallu" Der/huvva) "goallostit" "beaškkehit" "bidjat" "čatnat" "suddat" "heivet" "bidjat" "časkit" "geassit" "gullat" "rehkenastit" "ordnet" "soahpat" "gávnnadit" "heivehit" "doaibmat" "iskat" "laktit" "seaguhit" "sorrot" ;

 # Danger: This list could be too long, in certain respects. 
 # TODO: Think of counter examples, i.e. sentences where we have these verbs, but where oktii is not an adverbial.
 
LIST VGEN-V-TRIGGER = "beassat" "bidjat" "bijadit" "bivdit" "boahtit" "buktit" "čohkket" "čuovvut" "doalvut" "duolmmadit" "fidnet" "fitnat" "galgat" "girdit" "gálašit" "gávdnat" "goastit" "guođohit" "gurgalit" "gárgidit" "jávkkihit" "joatkit" "johtit" "jorgut" "loahpahit" "mannat" "mátkkoštit" "njahkalit"  "njoammut" "oasálastit" "rasttildit" "ruohttat" "sihkkelastit" "váccašit" "vájuldit" "vánddardit" "viehkat" "viehkalit" "viežžat" "vuodjit" "vuolgit" ;

LIST VGEN-V = "bassit" "bárdnat" "bealkit"  "beavkit" "biškut" "boahtit" "borrat" "bossut" "cealkit" "ciellit" "civkit" "čárvut" "čállit" "čeargut" "čierrut" "čohkkát" "čuoigat" "čuorvut" "doapmat" "doarggistit" "duorrat" "fáiput" "garrudit" "galkat" "gáhččat" "gállit" "geargat" "geavzut" "gievvut" "gilljut" "girdit" "gullat" "guoddit" "guorsat" "heahpanaddat" "hoigat" "holvut" "huikit" "hupmat" "itnut" "jápmit" "johtit" "juorbut" "jorgut" "jullat" "juoigat" "láddjet" "lávlut" "likšut" "luoibmat" "mannat" "murdit" "njáhkat" "njoammut" "njuikut" "njurgut" "oađđit" "oahkut" "oaidnit" "ohcat" "orrut" "ráhtat" "reaškit" "riehčut" "rievdat" "riidet" "rohkut" "ruohttat" "ruossut" "sáđđat" "sihkkelastit" "skeaikit" "skierbmut" "soabbut" "speažžut" "steamput" "suhkat" "suoibut" "šlivgut" "šloahtat" "šnjirgut" "vázzit" "viehkat" "vuodjat" "vuodjit" "vuohčut" "vuoššat" ;

# bassit?
# bárdnat: geat leat lahka bártna bissut riegádahttin institušuvnnaid lahkosis.
# bealkit: Son maid njuikii bealkki bealkki, 
# beavkit: Rievssat seivo fas beavkki muhtun sieđgabohttui.
# reaškit, biškut, riehčut: Mánnačora gievvuda ja gilvala reaškki, biškku ja riežu. reaškki reaškki repl. MJ
# boahtit: boađi boađi repl.
# borrat: Gaskan bora viehkalin olggos. 
# bossut: boahtá bosu sisa, bosu bosu repl. MJ
# civkit: goddesáhpán skuvihii civkki .
# ciellat: Beatnagat girde ciela olggos.
# čárvut: čárvvu čalmmiid
# čállit:  "Lávllu juoigga čáli"-girji
# čeargut, huikit: huikki ja čearggu viehkan birrat birra gohttensajis . huikki huikki repl. MJ
# čierrut: Áhkku manai čieru báhpa lusa
# čohkkát: 
# čuoigat, vázzit: rasttildit vácci dahje čuoigga
# čuorvut:  bođii čuorvvu ruovttuluotta
# doapmat: doama doama  repl. MJ
# doarggistit: Go nisu árvidii ahte ii lean čiegadeames, de bođii doarggis ovdan
# duorrat: Botta maŋŋel vas bođii duora ja dál ruovdi gieđas 
# fáiput: guhkes gákkaniin bođii fáippu	.
# galkat: galkka galkka rel. MJ
# gáhččat: lea aiddo gáhča gáhča boahtimin
# gárrudit: Juoigga bođii ja garrut	manai
# gállit:  rasttilda gáli joga
# geargat: geargga geargga  repl.
# geavzut: Bođii geavzzu
# gievvut:
# gilljut: vulggii gilju olggos
# girdit: bođii girddi
# gullat:  sii gula hallet ollesolbmuiguin
# guoddit:  bođii guotti soames olbmo 
# guorsat: guorssa guorssa repl. MJ
# heahpanaddat:  njiejai hui váddásit ja dego heahpanatta dearvvas juolggi báldii ja gessui fárus .
# hoigat: bođii sihkkeliid hoigga (Sipirjá)
# holvut: guorbmebiila bođii geaidnoráigge holvvu
# hállat: hála hála  repl. MJ
# hupmat?
# itnut: inu inu repl. MJ
# jápmit: lahka jámi
# johtit, orrut: Buoret lea	jođi go oru .
# juorbut, ráhtat: guorbmebiillat juorbbu ja ráđa ja smávva meahccebiillat daid maŋis 
# jorgut
# jullat: jula jula rep. MJ
# juoigat: sii manne juoigga
# láddjet
# lávlut: bohte lávllu
# likšut: beatnagat ruohtastedje seaibbit livššu ovddal.
# luoibmat: čurvo čieru ja luoimma	
# mannat: mana mana
# murdit: Son liikui vázzit sihke ovddos ahte murddi.
# njáhkat: váccii njága niittu rastá
# njoammut: iigo boađe njoamu
# njuikut: njuikku njuikku repl. MJ
# njurgut: manná njurggu
# oađđit: Buohkat unnán oađi ja morraša siste .
# oahkut
# oaidnit
# ohcat: gii oza gávdná
# reaškit: muitaleaba reaškki
# rievdat: boahtá olles viessu rievdda	
# riidet: bođii riide	
# rohkut:  barddii rogu
# ruohttat: Boazu manai ruohta, ruohta ruohta repl. MJ
# ruossut
# sáđđat: sáđa sáđa  repl. MJ
# sihkkalastit, viehkat: čuoigga, sihkkelastti ja viega gilvalit, viega viega repl. MJ
# skeaikit: girddii ovtta skeaikki
# skearbmut: Muhtimat bohte skierpmu soppiiguin 
# soabbut: lášmmohalle vácci, soappu ja viega
# speažžut: Mii čuožžut speačču gieđaid
# steamput: doarridii ovtta steamppu sudno
# suhkat: bohtet suga gáddái, suga suga repl. MJ
# suoibut: bohte suoibbu
# šlivgut: Bivden luosa šlivggu
# šloahtat: Son ii jođege šloađa ja vilppodala
# šnjirgut
# vuodjat: vuoja viežžat ealgga 
# vuodjit: bođii vuoji
# vuohčut: mannet vuožu
# vuoššat

# MJ : Jomppanen, Marjatta. 2015. Davvisámegiela reduplikatiiva vearbagenetiiva boađi boađi ja bosu bosu – morfologiija, syntáksa ja semantihkka. [english summary: Reduplication of the non-finite form called the verb genitive in north saami.]. Sámi dieđalaš áigečála 2015(2):33–59.

 
LIST SOURCE-V = "gáibidit" ;

     
LIST MOVEMENT-V = "ádjit" "báhtarit" "beassat" "boahtit" "čuoigat" "čuoiggadit" "fárret" "finadit" "finihit" "fitnat" "gálašit" "gállit" "girdit" "johttát" "johtit" "jorahallat" "mannat" "mátkkoštit" "njuiket" "ollit" "riidet" "ruohttat" "sáhtostit" "sihkkelastit" "soabbulit" "soabbut" "šloahtat" "váccašit" "vádjolit" "vájaldit" "vájuldit" "vánddardit" "vázzit" "viehkalit" "viehkat" "viežžat" "vuodjalit" "vuodjat" "vuodjit" "vuojadit" "vuojašit" "vuolgit" ;
 # These verbs typically combine with place adverbials in the genitive case @ADVL
 # (i.e. not accusative) I am not sure if the verbs in the second line do that too. They denote movement anyway.
 # These verbs take GEOGRAPHICAL-PLACE in Acc @OBJ.
 
LIST JOHTIT = "čuoigat" "čuoiggadit" "gálašit" "gállit" "girdit" "johttát" "johtit" "mátkkoštit" "njoammut" "riidet" "ruohttat" "sihkkelastit" "soabbulit" "soabbut" "šloahtat" "váccašit" "vádjolit" "vájaldit" "vájuldit" "vánddardit" "vázzit" "viehkalit" "viehkat" "vuodjalit" "vuodjat" "vuodjit" "vuojadit" "vuojašit" ;

  
LIST HUMAN-ACTIVITY-V = "áitit" "bassit" "báhčit" "bealuštit" "beaskidit" "beastit" "bidjat" "biebmat" "bivdit" "boaldit" "botket" "buktit" "buvttadit" "časkit" "čatnat" "čájehit" "čállit" "čoaggit" "čoavdit" "čugget" "čuohppat" "dearpalit" "dearpat" "dearvvahit" "deavdit" "devdnet" "dikšut" "divvut" "doarrádallat" "doidit" "dolastit" "čuojahit" "dovddastit" "dubmet" "duddjot" "duvdilit" "ferdnet" "gáhkket" "gárvodit" "gásttašit" "gávkat" "geahččat" "gilvit" "goarrut" "goddit" "gođđit" "gohččut" "goikadit" "goivet" "gokčat" "gottihit" "guldalit" "guolástit" "hábmet" "hervet" "hoigat" "hoitát" "hukset" "huškut" "huškkohallat" "jugahit" "juhkat" "juksat" "juogadit" "juohkit" "láddjet" "láibut" "láktadit" "loaiddastit" "luddet" "merket" "mihtidit" "murret" "nannet" "náitalit" "njuoskadit" "oahpahit" "oastit" "ohcat" "olahit" "ovddastit" "rahpat" "ráhkadit" "ráidnet" "rátkit" "registreret" "rekruteret" "riŋget" "ruohkkat" "sahát" "sáddet" "sáltet" "sárgut" "sihkkut" "siktet" "skihtardit" "skoalkalit" "tevdnet" "vadjat" "veahkehit" "vikšet" "vuođđudit" "vuosehit" "vuoidat" "vuoiddadit" "vuoitit" "vuostáiváldit" "vuoššat" "vuovdit" "vurket" ;


  # in contrast to abstract "activities" and static verbs. 
  # According to the rules choosing Loc or Com, all activity-verbs do not select TOOL, ABSTR-TOOL, CONCEPT or HUMAN in (Pl Loc).
  
LIST BODY-ACTIVITY-V = "borrat" "čiekčat" "čujuhit" "čuorbmat" "gullat" "guoskkahit" "jorahit" "lihkahit" "lihkastahttit" "loktet" "njávkkadit" "njávkkastit" "oaidnit" "seavvit" ;
  
    # These verbs can also have BODY as a tool. The verb-set belongs to the superset of dynamic-verbs, so they will also work with TOOL and ABSTR-TOOL in Com. "Giehta" is in both the TOOL-set and the BODY-set because it selects in Com to a lot of verbs.

LIST PLACING-ACTIVITY-V = "bidjat" "buonjostit" ;

LIST WRITING-ACTIVITY-V = "čállit" ;

LIST HANDICRAFT-ACTIVITY-V = "buddet" "botnit" "goarrut" "hervet" "vadjat" "vikšet" ;

SET ACTIVITY-V = BODY-ACTIVITY-V OR WRITING-ACTIVITY-V OR PLACING-ACTIVITY-V OR HUMAN-ACTIVITY-V OR HANDICRAFT-ACTIVITY-V ;

LIST DADJAT = "almmuhit" "cealkit" "čájehit" "čilget" "čuoččuhit" "dadjat" "dovdat" "gáibidit" "geatnegahttit" "jáhkkit" "láhttestit" "lohkat" "mearridit" "meroštallat" "muitalit" "navdit" "oaivvildit" ;
# This set is for  cealkkavástta: DADJAT + Acc + Inf or PrfPrc

LIST HUPMAT = "dáikit" "dárostit" "digaštallat" "digaštit" "duoldat" "háladit" "háleštit" "hállat" "hoallat" "humadit" "hupmat"  "jurddašit" "logaldallat" "máidnut" "máinnašit" "muitaladdat" "njurgut" "oaivvildit" "savkalit" "ságastaddat" "ságastallat" "ságastit" "sámástit" "sámistit" "sárdnidit" "sárdnut" "sártnodit" "smiehttat" "solžat" "suomastit" ;
# These should not take HUMAN as object

LIST VERBAL-ACTIVITY = "árvalit" "ávžžuhit" "cealkit" "čilget" "čuoččuhit" "basuhit" "bealkit" "cuoigut" "čuorvut" "čurvet" "dadjat" "deattastit" "gielistit" "gilljut" "gopmulit" "háladit" "háleštit" "hállat" "hoallat" "huikit" "huikkádit" "jearrat" "juoigat" "kommenteret" "láhttestit" "lávlut" "lohkat" "muitalit" "namahit" "namuhit" "rávvet" "sivdnidit" "váruhit" "vástidit" ;

SET VERBAL-ACTIVITY-V = VERBAL-ACTIVITY OR HUPMAT ;

#Under construction! (as most of the other LISTs) 
    # According to the rules choosing Loc or Com, the these verbs select TOOL, ABSTR-TOOL, CONCEPT or HUMAN in (Pl Loc).
    # SATNI in Com

LIST HUMAN-AGENT-V = "áŋgiruššat" "ásahit" "beahttit" "buoridit" "čielggadit" "čuovvulit" "dássidit" "deattastit" "deattuhit" "dieđihit" "dievvat" "duođaštit" "ealáskahttit" "geahpedit" "geahpidit" "gudnejahttit" "guorahallat" "illudit" "lassánit" "liekkadallat" "loahpahit" "luohpat" "mearridit" "moivašuhttit" "namuhit" "nannet" "ovddidit" "oassálastit" "suokkardit" "unnidit" "váikkuhit" "válmmaštit" "verrošit" "vurket" ;

    # According to the rules choosing Loc or Com, the these verbs do not select TOOL, ABSTR-TOOL, CONCEPT, SATNI or HUMAN in (Pl Loc).

LIST NOT-HUMAN-OBJECT-V = "bargat" "borrat" "čállit" "lohkat" "vuodjit" ;
    # transitive verbs that typically do not have humans as their object and usually have an intransitive reading

LIST USUALLY-IV =  "boradit" ;
	# TV verbs which usually are used intransitively

#LIST ABSTR-EVENT-V = "lassánit" ;

    # In contrast to the HUMAN-AGENT-V set, this one presupposes no human or human-like actor who initiates the action.

LIST INTELLECTUAL-ACTIVITY-V = "dovdat" "fuobmát" "fuomášit" "huobmát" "jurddahit" "jurddašit" "smiehttat" ;

    # According to the rules choosing Loc or Com, the these verbs do not select TOOL, ABSTR-TOOL, CONCEPT or HUMAN in (Pl Loc).

LIST VEHICLE-PLACE-V = "addit" "bearrat" "gaikkihit" "gáibidit" "geiget" "oastit" "oažžut" "suoládit" "váldit"  ;

    # According to the rules choosing Loc or Com, these verbs do not select TOOL, ABSTR-TOOL and CONCEPT in (Pl Loc), but HUMAN or ACTOR in (Pl Loc).
    # HUMAN is source and locative, VEHICLE is locative

LIST VEHICLE-TOOL-V = "beassat" "boahtit" "buktit" "doalvut" "fievrridit" "vuolggahit" ;

    # HUMAN is source and locative, VEHICLE is comitative

SET HUMAN-SOURCE-VEHICLE-V = VEHICLE-PLACE-V OR VEHICLE-TOOL-V ;

    # The two subsets differ in their choice of case for members of VEHICLE, but both choose locative for members of the set HUMAN (with the thematic role source)

LIST ABSTR-ENTITY-COM-V = "investeret" "lihkosmuvvat" "lihkostuvvat" "lihkustuvvat" "ruhtadit";

    # According to the rules choosing Loc or Com, these verbs do not select TOOL, ABSTR-TOOL, CONCEPT, ABSTR-ENTITY and ACTOR in (Pl Loc).

LIST ONLY-PLACE-LOC-V = "álggahit" "bártidit" "bargat" "čohkkedit" "dahkat" "geahččalit" "olahit" "veahkehit" ;

    # According to the rules choosing Loc or Com, these verbs only select (Pl Loc) if PLACE or ORGANIZATION.

LIST HUMAN-LOC-V = "atnit" "báhcit" "geavahit" "gohčodit" "namuhit" ;

    # According to the rules choosing Loc or Com, these verbs choose only (Sg Com) if CONCEPT or HUMAN or "sátni".
    # Abstract

#!! * **PLACE-V** Those get only not locative if the target is a member TOOL, ABSTR-TOOL or ANIMATE or CONCEPT. Selects more locatives than ONLY-PLACE-LOC-V 
LIST PLACE-V = "ássat" "buolvvastallat" "buolvvastit" "čippostallat" "čohkkát" "čohkahit" "čuožžut" "čuččodit" "morihit" "gullát" "morránit" "orrut" "orodit" "stuorrut" "veallát" "veallahit" ;
  
LIST ABSTR-PLACE-V = "váilut" "heivet" "hiehpat" "sisttisdoallat" "soahpat" "šiehttat";
    # Verbs without agent

  # TIME/AIGODAT are locative
  
LIST STATE-V = "eallit" ;

  
LIST REM-WITH-PARTS = "gaikkihit" ;

    # Belongs to the DYNAMIC-V. They also take PARTS in comitative case. 
    
  
LIST PERCEPTION-V = "áicat" "dovdat" "ealvit" "fuomášit" "fuobmát" "gullat" "haksit" "huobmát" "huomihit" "navdit" "oaidnit" ;

LIST ILLNESS-V = "buohcat" "buohccát" ; # "dearvvašmahttit" "dearvvašmuvvat" "dearvvašnuvvat" "dearvvaštuvvat" ;

LIST ADV-PX-V = "geahččat" ;
	## son geahččá birrasis
  
# HIERARCHY:

#SET STATIC-V = PLACE-V OR EMOTION-V OR PERCEPTION-V ;

SET DYNAMIC-V = ACTIVITY-V OR MOVEMENT-V OR VERBAL-ACTIVITY-V OR HUMAN-AGENT-V OR INTELLECTUAL-ACTIVITY-V OR ABSTR-ENTITY-COM-V OR HUMAN-SOURCE-VEHICLE-V OR REM-WITH-PARTS ;

    # According to the rules choosing Loc or Com, dynamic-verbs do not select TOOL, ABSTR-TOOL or CONCEPT in (Pl Loc).
    # Human becomes either Loc or Com depending on its subset membership.
    
SET N-TIMEOBJECT-V = MOVEMENT-V OR ACTIVITY-V OR VERBAL-ACTIVITY-V OR INTELLECTUAL-ACTIVITY-V OR PLACE-V ;

    # Those verb sets in addition to TIMEADVERBIAL-V do not take TIME expressions as objects

# <vdic>:
LIST MUITALIT = "árvalit" "árvidit" "ávžžuhit"  "bivdit" "boagustit" "bohkosit" "cealkit" "cuoigut" "čállit" "čilget" "čurvet" "čuorvut" "dadjat" "dahkaluddat" "deattastit" "deattuhit" "diehtit" "doaivut" "eahpidit" "geardduhit" "geažuhit" "gielistit" "gilljut" "hállat" "hoallat" "huikit" "huikkádit" "imaštallat" "jearrat" "jievžat" "joatkit" "juoigat" "jurdilit" "jurddašit" "kommenteret" "lávlut" "lasihit" "loahpahit" "lohkat" "lohpidit" "máidnut" "máinnašit" "muitalit" "muittuhit" "nágget" "njávggádit" "oaivvildit" "rávvet" "savkalit" "sárdnidit" "šuohkihit" "váidalit" "váruhit" "vástidit" ;
	#$ Deaŧalaš lea sihkkarastit eamiálbmogiid ovddastusa dákkár čállingottis, dadjá Keskitalo.

LIST JEARRAT = "dárkkistit" "diehtit" "jearrat" "mearridit" "iskat"  "imaštallat"  "imaštit" ;
	# verbs which makes the rel/interr pron  to an Interr and go to Qst: Son jearai gii boahtá odne. Isket leat go duhtavaš kollektiiva­fálaldagain.

#!! ### Adverb sets
# -------

# Adverbs as predicates
# - - - - - - - - - - -
    # in combination with verbs

LIST COM-ADV = "fárrolaga" "oktan" "oktanaga" "ovttamielas" "ovttas" "ovttasráđiid" "ruossalassii" "searválagaid" "searválaga" ;

    # Could also be perceived as particles within complexs verbs.
    
# others: vuhti combines with váldit - Com

# Other adverb sets
# - - - - - - - - - 

LIST GRADE-ADV =  "áibbas" "badjelmeare" "beare" ("duođaid" Adv) ("duođas" Adv) ("eambbo" Adv) ("eanet" Adv) "erenoamán" "erenoamáš" "hirbmat" "hui" "ila" "issoras" ("man" Adv) ("mealgat" Adv) ("mearkkašahtti" Adv) "measta" "menddo" "muhtun_muddui" "muhtun muddui" "nu" "oalle" "oba"  ("ollásit" Adv) ("oppa" Adv) "sakka" ("seammá" Adv) ("veaháš" Adv) ("uhccán" Adv) ("unnán" Adv) "vehá" "veháš" "veahá" "veaháš" "viehka"  "mearihis" ; # mearihis as adv ? "ollu" "olu"

LIST PLURALIZER = "buot" "buotlágan" "buotlágaš" "earálágan" "iežálágan" "gait" "gaitlágan" "goappaš" "goappašat" "máŋggalágan" "moattelágan" ;
    # Singular nouns can have a plural form combined with those
  
LIST BUOT = "buot" "gait" "gaitin" "gaitdivnnat" ;

LIST UNNAN = "unnán" "uhcán" "uhccán" ;

#!! ### Adjective sets
# ----------

# Lexical valency sets
# - - - - - - - - - - -
    # here we have adjectives according to their semantic properties
    
LIST NUMAG-A = "čeahppi" "viššal" "láiki" ;    
# This set for vuovdi etc., not for nisson etc.
# Here thus not ?stuorra vuovdi, etc,

# in periphrastic constructions (together with "leat") 
# these subcategorize for comitative, locative etc. depending on 
# the noun's semantic properties
    
LIST COM-A = "duhtameahttun" "duhtatmeahttun" "duđavaš" "duhtavaš" "ollis" "oppa" ;

LIST ILL-A = "gullevaš" "bahá";
	## Ja danin son lei šaddan nu bahá tálaide ja bustávaide.

LIST ACTIVITY-A = "hilbat" "oskkáldas" ;

    # denote som kind of activity, might take HUMAN comitative?
    
LIST DEHALAS = "dábálaš" "dárbbašlaš" "dehálaš" "deaŧalaš" "dohkálaš" "duhtavaš" "geatnegas" "gearggus" "lunddolaš" "mearkkašahtti" "eahpedábálaš" "oktasaš" "positiivvalaš" "veadjemeahttun" "vejolaš" "suohtas" "váttis" ;
	# When Attr = pred.form
 	#$ Lea suohtas kurssas. # not Attr 
 	#$ Lea čuovvovaš kurssas. # Attr

# Nickel: predicative adjectives (take a complement) "fargat" "heavnnas" "sáiggas" "suhtas"

# Other adjective sets
# - - - - - - - - - - -
    
LIST A-N = "buoidi" "čeahppi" "headju" "heittot" "jalla" "neavri" "nuorra" "oahpis" "oarbbis" "ráhkis" "rikkis" "váivváš" "vuoras";
    
    # nouns that have very specific semantics and are derived from adjectives
    # those nouns are animate
    
LIST A-N-CASE = "varas" ;
    # adjectives which are also nouns in other cases than nominative 

LIST NON-GRADE-A = "sápmelaš" "alternatiiva" ;
    # gradable under certain circumstances, for example if we write "nu" "hirbmat" "hui" "oalle" "oppa" "veháš" 
    # mon dagahallan NU sápmelažžan = jeg later som jeg er SÅÅ same/samisk?
    # mon anán du VIEHKA sápmelažžan 
    
LIST GRADE-A = "fávru" "headju";
    # The ones that have a strong adjective meaning, and are inclined to be adjectives rather than nouns in Ess constructions

LIST A-LEX-AS-NOUN = "bassi" "guovddáš" "nuorra" "oahpis" "váivi" "oainnus";
    # Those are quite strongly lexicalized as nouns. NBNB Earlier LEX-A, but this was another set
    
LIST TIME-A = "boahtte" "čuovvovaš" "maŋit" "maŋimus" "ovddit" ("mannat" PrfPrc) ("vássit" PrfPrc)   ;

LIST DURATION-A = "heila" "eanas" "eanet" "geažo" "geažos" "guhkki" "obba" "oba" "ollis" "oppa" "visot" ;
    
LIST POINT-IN-TIME-SPEC = "dološ" "eará" "iežá" "nubbi" ;

    

#!! ### NP sets defined according to their morphosyntactic features
# - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - 

SET NORM-LOC = Loc - (South Loc) ;

LIST N-SG-NOM = (N Sg Nom);

SET LEX-N = N - DER-N ;

LIST PROP = Prop np ;

SET COMMON-N = N - Prop;

SET HEAD-N = N - Cmp/SplitR ;

SET HEAD-N-NOM = (N Nom) - Cmp/SplitR ;

SET SUBJECTHEAD = N OR A OR Pron - Refl ; # These, can be subject heads

SET NP = N OR A ;                      # anything that can take except numerals
SET NP-HEAD        = Pron OR HEAD-N - ("buorre") ;
SET NP-HEAD-SG     = SG-PRON OR (N Sg) OR (A Sg) - Cmp/SplitR  - Dem - ("buorre");
SET NP-HEAD-PL     = PL-PRON OR (N Pl) OR (A Pl) - Cmp/SplitR  - Dem - ("buorre");
SET NP-HEAD-SG-NOM = SG-PRON + Nom OR (N Sg Nom) OR (A Sg Nom) - ("buorre") - Cmp/SplitR ;
SET NP-HEAD-PL-NOM = PL-PRON + Nom OR (N Pl Nom) OR (N Coll Nom) - ("guovttis") OR (A Pl Nom) - Cmp/SplitR  - ("buorre");
SET NP-HEAD-NOM    = NP-HEAD-SG-NOM OR NP-HEAD-PL-NOM ;
SET NP-HEAD-ACC    = (Pron Acc) OR (N Acc) OR (A Acc) - Cmp/SplitR - (Dem Attr) - ("buorre");
SET NP-HEAD-GEN    = (Pron Gen) OR (N Gen) OR (A Gen) - Der/NomAct - Cmp/SplitR - (Dem Attr) - ("buorre")   ;

#!! ### The PRE-NP-HEAD family of sets

#!! These sets model noun phrases (NPs). The idea is to first define whatever can
#!! occur in front of the head of the NP, and thereafter negate that with the
#!! expression **WORD - premodifiers**.


SET PRE-NP-HEAD = Prop + Attr OR Prop + @>N OR A + Attr OR ABBR + Attr OR ("buorre") 
         OR Pers + Gen OR N + Gen OR BUOT OR
         Num OR Cmp/SplitR OR CC OR Dem OR Refl + Gen OR Indef + Attr OR 
         PrfPrc + @>N OR PrsPrc OR A + Ord ;
         # The strict version of items that can only be premodifiers, not parts of the predicate
         
SET PRE-NP-V = PrfPrc OR PrsPrc OR Der/NomAg OR Actio OR Der/NomAct OR (V A) OR (Prs Sg3) + Ind OR (Prs Sg1) + Ind OR (Imprt Du2) ;
    # to be used together with PRE-NP-HEAD before @>N is disambiguated
         
SET NP-MEMBER   = PRE-NP-HEAD OR N ;

SET PRE-A-N     = (Pron Pers Gen) OR (Pron Pers Acc) OR (Pron Indef) OR Num OR (A Ord) OR (Pron Dem) OR (Pron Refl Gen) OR (Pron Refl Acc) ; # Acc pga av manglende disambiguering tidlig i fila

SET NOT-PRE-A-N = WORD - PRE-A-N ;

LIST PUNCT-LEFT = (PUNCT LEFT) ;
LIST PUNCT-RIGHT = (PUNCT RIGHT) ;

LIST JA/DAHJE = "ja" "dahje" "dehe" ;


SET PRE-APP   = COMMA OR PUNCT-LEFT OR PRE-NP-HEAD ;
 # This set ist not only for what can
                            # stand in front of appositions but also
                            # postmodifiers.



#!! The set **NOT-NPMOD** is used to find barriers between NPs.
#!! Typical usage: ... (*1 N BARRIER NPT-NPMOD) ...
#!! meaning: Scan to the first noun, ignoring anything that can be
#!! part of the noun phrase of that noun (i.e., "scan to the next NP head")

SET NOT-NPMOD     = WORD - PRE-NP-HEAD OR ABBR OR @CVP ;                     
# This is the previous NPNH (npnh) set.
# NOT-NPMOD  = "NOT-PRE-NP-HEAD"


SET NOT-NPMOD-ACC     = NOT-NPMOD - Acc OR ABBR ; 
SET NOT-NPMOD-ACC-ADV     = NOT-NPMOD - Acc - Adv OR ABBR ; 
# To be used in mappingrules, before the disambuation of Acc and Gen.                     

SET NOT-NPMODADV    = WORD - PRE-NP-HEAD - Adv - (Pcle) OR MO;                      
# NOT-NPMODADV = "NOT-PRE-NP-HEAD-OR-ADV"
SET NOT-NPMODADV-INDEF   = WORD - PRE-NP-HEAD - Adv - Indef ;              
## NOT-NPMODADVI = "     ...-OR-INDEF"
SET NOT-NPMODADVII  = WORD - PRE-NP-HEAD - Adv - Indef - Ill ;        
# Illative indir.obj.
SET NOT-NPMODADVIIP = WORD - PRE-NP-HEAD - Adv - Indef - Ill - Pcle ; 
# <== is this our NOT-NPMOD set?
SET NOT-NPMODCC   = WORD - PRE-NP-HEAD - COMMA - LCRD ;
SET NAPP     = WORD - PRE-APP ;

# SET TOLERANT-PRENPP = V ; (everything non-NP-head!)
# ... Most sloppy BARRIERs

# Set for NP with participle premodifier
# ... IF *-1 X BARRIER NOT-NPMOD OR 
# LocComIll are ok, but only with a PrfPcc to the right

#!! ### Other negatively defined morphosyntactic noun sets


SET NOT-N = WORD - N ;

#SET NOT-A = WORD - A ;

SET NOT-N-A = WORD - N - A ;

SET NOT-NAPrfCC = WORD - N - A - CC - PrfPrc ;

SET NOT-NP = VFIN-NOT-NEG OR ConNeg OR Inf OR Pcle OR Interj OR CS ;
 # The set NP-BOUNDARY is bigger. 

SET CLB_NOT-COMMA = CLB - COMMA OR PUNCT-LEFT OR PUNCT-RIGHT ;

SET NOT-QMARK = WORD OR CLB OR PUNCT - QMARK ; 

SET W-NOT-ACC-GEN = WORD - Acc - Gen ;



#!! ### Noun sets

#!! Nominal sets defined according to their morphophonological properties
#!! Sets for lexeme homonymy (most of them are moved to where the actual rules are.)


#!! The words in the set **N-PO** can be both N and Po, the set takes that into account.
#LIST N-PO = "áigi" "gaskan" "gaskka" "gaskkas" "guovddáš" "ovddas" "ráji" "vuostá" "guvlui" ;
LIST N-PO = "áigi" "guovddáš" "vuostá" ; 


#!! ### The LAHKA set family

LIST LAGA = (".*bargo#lága"r) "spábbačiekčan#lága" ;

LIST LAGALAHKA = (".*bargo#láhka"r) "spábbačiekčan#láhka" "sierraláhka" ; 

SET LAHKA = (".*láhka"r) - LAGALAHKA ;


LIST LAHKA-MOD = "almennet" "almmolašvuohta" "apotehka" "arkiiva" "áhpenuohtástit" "álbmotoadju" "bargi" "bargobiras" "báikenamma" "bákti" "bátnedearvvašvuođabálvalus" "bivdu" "boazodoallu" "čázádat" "dálkkas" "dásseárvu" "deatnu" "dearvvašvuođabargi" "dearvvašvuohta" "dearvvašvuođafitnodat" "dearvvašvuođabargiidláhka" "dearvvašvuođaregisttar" "divššohasvuoigatvuohta" "doaibmi" "eana" "eanasuodjaleapmi" "eanavuovdin" "elliidsuodjaleapmi" "energiija" "fápmudus" "Finnmárku" "friddjasuohkan" "fuođđu" "gávpot" "geaidnu" "gearggusvuohta" "gielddadearvvašvuođabálvalus" "gielda" "gili#oppalašeana" "girku" "golli" "guollebiebman" "háhkan#konsešuvdna" "hálddašeapmi" "heaggadieđut" "huksen" "industrikonsešuvdna" "jávrebivdu" "joatkkaoahpahus" "johtolat" "konsešuvdna" "kulturmuitu" "lavdnjeloggut" "lotnolasealáhusabargi" "luodda" "luonddusuodjalus" "luopmu" "luossa" "mánáidgárdi" "mánáidsuodjalusbálvalus" "mearraguolásteapmi" "mearraresursa" "minerála" "mohtorjohtolat" "muddet" "namma" "Norga" "norga" "oahpahallat" "oahpahus" "oahppu" "oassálastinláhka" "olgunastin" "olmmošvuoigatvuohta" "oktasašsápmi" "pasieantavuoigatvuohta" ("ráŋggáštit" Der/NomAct) "resursahálddašeapmi" "riekteveahkki" "sáivabivdu" "sápmi" "searvan" "sierra" "spesialistadearvvašvuođabálvalus" "stáhtaborgárriekti" "stáhtafitnodat" "tuollu" "uhccimusmihttonjuolggadus" "varasguolli" "várri" "vearrodahku" "vearru" "vuođđu" "vuođđoskuvla" "vuovdedoallu" "vuovdegáhttet" "vuovdi" "1789" "1842" "1863" "1888" "1899-#bivdu" "1899" "1920-#várri" "1933" "1965" "1975-#várri" ;
 
# This list is for lahka compounds written with two single words

LIST LAHKA-A = "boaris" "dálá" "dáláš" "gustojeaddji" "gustovaš" "nationála" "ovdalaš" "sorjjasmeahttun" ("suodjalit" Der/PassL PrfPrc) ;

LIST LAHKA-V = "addit" "áimmahuššat" "árvalit" "ásahit" "čállit" "čuovvulit" "čuovvut" "dohkkehit" "gáibidit" "geatnegahttit" "geavahit" "gieđahallat" "jorgalit" "kriminaliseret" "mearridit" "muhttit" "ovddidit" "ráŋggáštit" "viiddidit" ;

LIST LAHKA-V-PASS = "atnit" "ásahit" "báhkkodit" "báhkkot" "čállit" "čuoldit" "dadjat" "dahkat" "dohkkehit" "duššadit" "eaktudit" "fámuhuhttit" "geatnegahttit" "jorgalit" "jurddahit" "jurddašit" "kriminaliseret" "mearridit" "muddet" "njulget" "oaivvildit" "rievdadit" "ráŋggáštit" ;

# This one works in combination with others, f.in. LAHKA-POST.
LIST LAHKA-N = "§" "almmolašvuohta" "eanagirji" "eanagirji" "čađaheapmi" "fápmudus" "fápmuiboahtin" "gáibádus" "geatnegasvuohta" "geavaheapmi" "gonagas" "hálddašangeavat" "juksanmearri" "kap" "kapihtal" "konvenšuvdna" "láhkaásahus" "láhkaovdabargu" "láigohanvuoigatvuohta" "mearrádus" "njuolggadus" "nr" "paragráfa" "riekteáddejupmi" "riektegáldu" "riektegeavat" "riektesubjeakta" "sámekonvenšuvdna" "vuoigatvuođabargu" "vuoigatvuohta" ;

# This one does it more likely to be "láhka" in combination with f.i. LAHKA-V:
LIST LAHKA-SUBJ-N = "eiseváldi" "guovddášeiseváldi" "stáhta" ;

LIST LAHKA-GEN-N = "§" "buohkaidvuoigatvuohta" "cealkka" ("fápmu" Ill) "hápmi" "johtolatvuoigatvuohta" "kapihtal" "láhkavuođđu" "ovdabargu" "rámma" "ráhkkananbargu" "ráidu" "sátne#mearkkašupmi" "ulbmilparagráfa" "vuođđu" "vuođđoeaktu" "vuolit" "V";

# "vuolit" is an adj. but suit pragmatically to the set.

LIST LAHKA-ASAHUS = (".*riekti"r) (".*diggi"r) ;

LIST LAHKA-POST = "bokte" "olis" "mielde" "vuođul" ;
  
#!! ### Nominal sets defined according to their semantical properties
# -------------------------------------------------------------

# Here we will write what sets we have and how we divide them.

#!! * Spatial noun sets. These nouns behave like postpositions
LIST SPATIAL-N = "guovlu" "osku" "sadji" "vuođđu" ;
 
# These nouns in essiv get @ADVL:
LIST ESS-ADVL-N = "arvedálki" "arvi" "árga" "baika" "bajándálki" ("bassi" N) ("báhkka" N) "beaivvádat" "beaivvadat" "biegga" "bievla" ("buolaš" N) "borga" "čakčaseavdnjat" "čáppadálki" "čuoika" "čuovga" "cuoŋu"  (".*dálki"r) "dulvi" "fiertu" "goalki" "guoldu" "mánoheahpi" "mierká" "muohta" "njáhcu" "obbadálki" ("seavdnjat" N) "varra" "veaigi";

# These nouns/adjectives in essiv can get @ADVL:
LIST ESS-PROB-ADVL-N = ("bassi" N) "boaris" ("nuorra" N) ;
 

#!! * Time sets
# - - - - -

LIST MANNU = "ođđajagemánnu" "guovvamánnu" "njukčamánnu" "njuvččamánnu" "mársamánnu" "cuoŋománnu" "cuoŋosmánnu" "gáranasmánnu" "miessemánnu" "máimánnu" "geassemánnu" "suoidnemánnu" "borgemánnu" "šnjilčamánnu" "šnjilžamánnu" "čakčamánnu" "golggotmánnu" "skábmamánnu" "novembermánnu" "ritnemánnu" "juovlamánnu" ;

# Duomma:ii

LIST AIGODAT = "áigi" (".*áigodat"r) "álgoáigi" "bargoáigi" "soahteáigi" "soahtegaskaáigi" ;
 
# Duomma:lea      

LIST VAHKKU-DUR = "áigodat" "álgojahki" "árrageassi" "beaivi" "eallinahki" "eallenahki" "jándor" "bodda" "čakča" "čakčageassi" "čakčaseavdnjat" "čuohtejahki" "dálvi" "diibmu" "eahketbodda" "geassi" "giđđa" "idja" "iđitbodda" "jahki" "jahkebealli" "jahkečuohti" "kaleanddarjahki" "loahppajahki" ("[0-9]*-#lohku"r) "maŋŋe#giđđa" "mánnu" "minuhtta" "njealjádasjahki" "skuvlajahki" "tiibma" "vahkkoloahppa" "vahkku" ;
# these are periodes and can be Acc

# ("[0-9]*-#lohku"r) doesn't work in APERT

LIST VAHKKU-TIME = "b" "beassážat" "biidnovahkku" "bievlajahki" "bivdoáigi" (".*beaivi"r) "d-beaivi" "dienasjahki" "doaibmajahki" "eamiálbmotjahki" "earrejahki" "gaskaidja" "geardi" "juovllat" "juovlabassi" "luomejahki" "luopmojahki" "mihcamárat" "muorjejahki" "nissonjahki" "stuorradiggeáigodat" "vuoitojahki" "vuođđoskuvlavahkku" ("[0-9]*-#jahki"r) ("[0-9]*-#jahki"r) "[0-9]*-[0-9]*-#jahki" "[0-9]*-[0-9]*-#lohku" ("[0-9]*-#lohku"r) ("[0-9]*-#lohku"r) "eahket" "iđit" ;

 # The quoteless jahki here is since the preprocessor is not fine-tuned
 # yet, it still gives '"1870-" # jahki N Sg Com'. # lets see if "dbm" works
 # Duomma:lea
 
SET VAHKKU = VAHKKU-DUR OR VAHKKU-TIME ;
 
LIST JAHKI-NUM = "jahki" (".*jahki"r) ;

    # Years which can be numerated
 
LIST PARTTIME = "čavčča#bealle" ;
	# This set is for finding Gen in expressions like PARTTIME OR TIME Gen.

LIST AIGI = "áigi" (".*náigi"r) ; 

LIST SP-BEAIVI = "albmáimannan#beaivi" "bargočoahkkin#beaivi" "beassášbeaivi" "mánnodat" "vuossárga" "disdat" "maŋŋebárga" "gaskavahkku" "duorastat" "bearjadat" "juovlabeaivi" "lávvardat" "lávvordat" "sotnabeaivi" "sábbát" "skilleduorastat" ;

LIST GEN-BEAIVI = "álbmotbeaivi" "árgabeaivi" "ávvobeaivi" "bargobeaivi" "beaivi" "duopmobeaivi" "riegádanbeaivi" "skuvlabeaivi"  ;
# General-beaivi can have Gen modifier, like "skuvlla árgabeaivi".

#LIST DATE1 = ("[0-3][0-9]\.[0-1][0-9]\.[0-3][0-9][0-9][0-9]"r) ("[0-3][0-9]\.[0-1][0-9]\.[0-9][0-9]"r)  ("[0-9]\.[0-1][0-9]\.[0-3][0-9][0-9][0-9]"r) ("[0-3][0-9]\.[0-9]\.[0-3][0-9][0-9][0-9]"r) ("[0-9]\.[0-9]\.[0-3][0-9][0-9][0-9]"r) ("[0-3][0-9]\.[0-3][0-9]"r) ;

#SET DATE = DATE1 - ("[0-9]*"r) ; # En litt klosset måte å gjøre det på, fikk ikke regex i DATE1 til å fungere som de skulle

SET BEAIVI-REF = SP-BEAIVI OR GEN-BEAIVI OR Sem/Date ;

LIST BEAIVI = "beaivi" "b" ;

LIST YEAR = ("[0-2][0-9][0-9][0-9]"r) ;


SET TIME = MANNU OR VAHKKU OR BEAIVI-REF OR AIGI OR YEAR OR Sem/Time ;

  # Do not add AIGODAT, it has some other characteristics in some cases.
  # But fix!!
  # "Sámediggeráđđi   lea áigodagas doallan   5 čoahkkima."
  # Today believed to be obj.
  
SET NOTIME = N - TIME ;

LIST GEASSET = "dálvet" "dálvit" "giđđat" "geasset" ("geassit" Adv) "čakčat" ;
 # What is special here?

LIST PAST-MARKER = ("mánná" Ess) "dalle" "diibmá" "dolin" "dološ" "dovle" "duvle" "eske" "gieskat" "ikte" ("nuorra" Ess) "ovdal" ("ovddit#beaivi" Gen) ("vássánáigi" Loc) ("boaresáigi" Gen) ;

LIST PRESENT-MARKER = "dál" "ihttin" "odne" "otne" ("boahtteáigi" Loc) ("boahtteáigi" Gen)  ;

SET TIME-MARKER = PAST-MARKER OR PRESENT-MARKER ;

LIST OTHER-TIME-ADV =  "árabuš" "árrat" "dál" "diibmá" "easkka" "easka" "guhká" ("guhkit" Adv) "maŋimustá" "maŋŋel" "maŋŋá" "maŋŋelaš" "maŋŋeleappos" "maŋŋit" "muhtumin" "ovdal" ;

SET TIME-ADV = TIME-MARKER OR OTHER-TIME-ADV OR GEASSET;

LIST PLACE-PO = "duohken" ;

#!! * Amount sets
 
LIST VOLUME = "cl" "lihtar" (".*lihtter"r) "dl" "lihtter" "ml"  ;

LIST WEIGHT = "mg" "kg" "kilo" "gilo" "tonna" ;

LIST LENGTH = "čuoldabadji" "dumá" "fierbmebadji" "gaska" "miila" (".*mehter"r)  "sente" "cm" "tumá" "állan" "salla" "gaska" "km" "mm" ;

LIST TEMPERATURE = "buolašceahkki" "buolašgráda" "lieggaceahkki" "lieggagráda" ;

LIST AMOUNT = "uhcánaš" "unnánaš" ;

LIST TIME-UNIT-MEASURE =  "minuhtta" "sekunda" "diibmu" "vahkku" ;  # in MEASURE-set

LIST TIME-UNIT2 =  (".*jahki"r) "mánnu" "beaivi" ;

SET TIME-UNIT = TIME-UNIT-MEASURE OR TIME-UNIT2 ;
	# For use for rules hitting "maŋŋel" as Adv instead of Po. 

SET MEASURE = VOLUME OR WEIGHT OR LENGTH OR AMOUNT OR TEMPERATURE OR TIME-UNIT-MEASURE OR ("m2") ;

LIST CURRENCY-ABBR = "kr" "ru" "¢" "€" "$";

LIST CURRENCY = "denára" "dollár" "euro" "kruvdnu" "kr" "ru" "rubel" "ruvdno" "ruvdnu" "¢" "€" "$";

SET CURRENCY-GEN = CURRENCY + Gen ;
 	

LIST NUMUNIT = "čuohti" "duhát" "miljovdna" "mill" "milj" "miljárda" "mrd";

LIST KIND = (".*šládja"r)  ;

LIST COUNTABLE = "gáhppálat" "oassi" "kapihtal" "paragráfa" ;

LIST NUMMAR = (".*nummar"r) "njuolggotlf" "nr" "vuorkááššenr" ;

LIST KLASS = "ášši" "bálkáceahkki" "čuokkis" "s" "siidu" "§" "§§" "paragráfa" "oassi" "kap" "kapihtal";

LIST PEOPLE-NUM = "guovttis" "golmmas" "njealjis" "viđas" "guđas" "čiežas" "gávccis" "ovccis" "logis" ;

LIST GUOVTTIS = "guovttos" (".*guovttos"r)  "guovttis" ;

#!! * Sets for nouns with morpho-syntactic preferences
# ----------------------------------------

#!! * Number-related sets


LIST PL-N = "áiru" "beaivváščalbmeláset" "beassážat" "biddu" "čalbmeláset" "čázet" "fáhcca" "gállot" "gálssot" "gáma" "gistta" "goikket" "gummestevvel" "heajat" "hellodagat" "juovllat" (".*mállásat"r) "nagir" "njealjejuvllat" "njuorjjot" "nuhtat" "nuvttat" "nuvttot" "sabet" "sáppat" "sisttet" "skárrit" "skierat" "skuovva" "stevvel" "suohkku" "suorbmafáhcca" "suorpmahat" "ullofáhcca" "ullosuohkku" "vuotta" "viljjit" ("[0-9]*-#jahki"r) ("[0-9]*-[0-9]*-#jahki"r) ("[0-9]*-#jahki"r) ;
 
 # Nominals that appear with plural numerals.
 
LIST SG-WORD = ("addit" Der/upmi) "almmolašvuohta" "anistupmi" "arvi" "áigemearri" "álgu" "áŋgirvuohta" "ávvu" "ballu"   "bargguhisvuohta" "bággu" ("bávččas" Der/vuota) "boazodoallu" "borakeahttáivuohta" "boahkku" "buorredáhtolašvuohta" "ceaggáivuohta" (".*deaddu"r) "diehtemeahttunvuohta" "doaivu"  "dárogiella" "duhtavašvuohta"  "fárta" "feber" "Finnmárkoláhka" "fuolalašvuohta" "garrarohkka" "gaskkamuddu" "gávvilvuohta" "geargatvuohta" "geardun" "geavaheapmi" "gelbbolašvuohta" "giellageavaheapmi" "gihtta" "giitevašvuohta" "gudnejahttin" "guođoheapmi" "fápmu" "guossoheapmi" "guovttegielalašvuohta" "hearvásvuohta" "hoahppu" "iešdovdu" "illu" "imaštallan" "ipmilbalolašvuohta" "ipmárdus" "isitvuohta" "juonalašvuohta" "kulturduogáš" "kulturgelbbolašvuohta" "láhkavuođđu" (".*lihkku"r) "leahttu" "leaktu" "luondu" "luondduvuođđu" "loahppa" "mandáhta" "máŋggabealatvuohta" "mielki" "miella" "šlundi#miella" "miellasuvvan" "moraš" "muohta" "musihkka" "oamedovdu" "olahahttivuohta" "ovddasvástádus" "ovttasbargu" "ovttajienalašvuohta" "ovttadássásašvuohta"  (".*politihkka"r) "promilla" "ráffi" "ráfi" "rabasvuohta" "ráhkisvuohta" "rávisvuohta" "riska" "roahkkatvuohta" "rohkka" "sámegielmáhttu" "sámekultuvra" "ustitvuohta" "vanhurskkisvuohta" "vásáhusduogáš" "vázzin" "veahkaválddálašvuohta" "vuohki" ("vuordnut" Der/NomAct) "vuosteháhku" ;

# Words that are used in singular unless their plurality is stressed by words such as "goappaš", "goappašat", "earálágan", "máŋgalágan" or Num or Ord.

#!! * Sets for case, possessive, etc.


LIST LOC-N = (".*dilálašvuohta"r) "dáhpáhus" "ipmárdus"  ;

  # Those nouns appear in locative unless a head asks for comitative.
  
LIST VUOHKI = (".*vuohki"r)  ;
# Usually in comitative, see rule
  
LIST N-NOPXLIST = "atnu" "almmolašvuohta" "áibmu" "áigemearri" "ára" "álbmotjienasteapmi" "álgoálgu" "álgo#stávval" "álgu" "álki" ("árvidit" Der/NomAct) "bargoáigi" "báhkka" ("beaivádit" Der/NomAct) "bievlajahki" "boahtteáigi" "boahtu" "boazodoallu" "boraspire" ("buorre" Comp) "cealkka" "čakča" "čakčageassi" "čáppagirjjálašvuohta" "čiegus" "čiehča" "dálvi" "dáža" "divregiksi" "dollagáddi" "dološ" "duo" "eahket" "fylkkamánni" "fylkkaviessu" "Finnmárkkuopmodat" "geatnegas" "geavahanriektelaš" "geavat" "gaskkamuddu" "gárvi" "geassi" "genitiivahápmi" "guhkká" "giđđa" "giellageavaheapmi" "girkosiida" "hálddašanguovlu" "háldejaš" "hoahppu" "historjá" "jahki" "japma" "jođán" "johgáddi" "johtu" "kilo" "láhka" "leansmánne#árvvošteapmi" "loahppa" "logaheapmi" "lohku" "lohpi" "luondu" "maŋŋi" "máŋggaidlohku" "meallu" "meannudeapmi" "mielladearvvašvuođa#dikšu" "muitu" "njunuš" "nuoraidskuvladássi" "nuppádas" "nuppát" "oahpaheapmi" "oasálaš" "ođas" "ođđajahki" "oktavuohta" "olámuddu" "olle" "olmmoš" "ortnet" "ovttaidlohku" "ovttasbargu" "predikatiiva" "ráfi" "rávásmanahki" "riektegeavat" "riikkaarkivára" "sápmelaš" "sisriika" "sivva" "Skjerstad-ášši" "smávvaskuvladássi" "soaittáhat" "stáhtaráđđi" "stuorradiggi" "suohtta" "várá" "unnitlogugiella" "vahkku" "valljodat" "viiddis" "viso" ("[0-9]*-#lohku"r) Coll "ráđđeaddi" ;
# atnit muittus / árvvus

 # Nominals that probably do not have a Px ('1600-logus').

LIST N-PX = "áddjá" "áhčči" "áhkku" "bárdni" "beana" "biigá" "boadnji" "eadni" "eammi" "gáibmi" "guoibmi" "hearrá" "nieida" "oabbá" "olmmái" "ráhkis" "reaŋga" "skibir" "skihpár" "ustibaš" "ustit" "viellja" ;

 # Nominals that well may have a Px. 

LIST ALU = "alu" "gasu" "govddu" "guhku" "losu" "stuoru" "viiddu" "čieŋu" ;

	  # Those are words that only appear in one form, which Nickel calls A/G.
    # They are only in a predicative position.

# These are ABBR:
LIST REAL-TITLE =  "prof" "cand.mag" "cand.oecon" "cand.philol" "cand.real" "cand.scient" "cand.theol" "cand" "dr.art" "eksp.hoavd" "eksp.sj" "fenr" "fig" "fil.tri" "gen.lt" "genr" "genr.lt" "h.r.adv" "hr" "kapt" "korp" "lekt" "lic" "ltn" "mag.art" "mag" "merc" "miss" "Mr" "mr" "Mrs" "mrs" "odont" "oecon" "ordf" "ossod.dir" "stud.phil." "res.kap" "stud.philol" "vit.ass" "dr" "dr.med" "dr.philos" "dr.theol" ;
 
LIST TITLE-LIKE = "gásttašeaddji" "justitiárius" "majestehta" "miss" "ránnjá" ;
# Ránnjá Ole Vuolab imašta manne ....

#!! * Sets for nouns as pred


# subcategorizing for a particular case

LIST LOC-COMPL-N = "ávki" "árvalus" "dovdu" "ipmárdus" "ovdamearka" "sáhka" ;

  # Nouns that take a locative
  # There is also an idiomatic use of "ovdamearka" in the constructions "ovdamearkka dihte" and "ovdamearka" (Ess).
  

LIST COM-COMPL-N = "ákkastallan" (".*čoahkkin"r) "dahkamuš" "dárbbašmeahttun" "dearvuohta" "deaivvadeapmi" "digaštallan" "duhkoraddan" "earrodearvvuođat" "gaskavuohta" "gulahallan" "konsultašuvdna" "lihkku" "oktavuohta" "oktiigullevašvuohta" "oktiibidjan" "ovttaoaivilis" "ovttasbargočoahkkin" "ovttasbargu" "ovttasráđđi" "ovttastallan" "ovttasássi" "ovttasbargošiehtadus" "ovttastus" "rohcošeapmi" "ságastallan" "skearru" "šiehtadus" "vuostálaga" ; # "bargu"

SET LOC-HEAD = LOC-V OR LOC-COMPL-N ;

SET COM-HEAD = COM-V OR OKTII-V OR COM-COMPL-N ;

  
LIST PREGEN = "álggogeahčen" "beallemuttus" "beallemuddu" ("bealli" N Sg Loc) "gaskamuddu" "gaskkamuddu" ("gaskavuohta" N Sg Loc) "loahpageahčen" "loahpa#geahči" "oktavuohta" "ovttasbargu" ;

 # Nouns and adjectives that take a preceding genitive.
 
 

# Possessor hierarchy
#                          +            P  o  s  s  e  s  s  e  d		    |
# Possible possessors      +  FAMILY    FAMILY-ALSO-ABSTRACT    NON-FAMILY  + 
# ------------------------ +  --------    -----------------   ------------  |
# Prop Sem/Plc                 +                      x               x		    |
# HUMAN-INDIVIDUAL         +      x               x               x		    |
# FAMILY pronouns	       + 									   		    |
# HUMAN-GROUP              +                                      x		    |
# PROFESSION               +      x               x               x		    |
# OFFICE                   +      x               x               x		    |
# HUM-FUNCTION             +      x               x               x		    |
# ABSTR-TEXT               +      x               x               x		    |
# PLACE (not NATURE-PLACE) +                      x               x		    |
# NATION                   +                      x               x		    |
# ANIMAL                   +                      x			   			    |
# EDUCATION                +                      x               x		    |
# ORGANIZATION             +                      x               x         |
# INSTITUTION              +                      x               x		    |
# INDUSTRY                 +                                      x		    |

LIST FAMILY-ONLY-HUMAN = Sem/Mal (ant m) Sem/Fem  (ant f) Sem/Sur cog "áddjut" "áhkká" "áhkkut" "bárdnemánná" "bárdni" "boadnji" "čeahci" "čeahcit" "dálueamit" "eahki" "eamit" "eammi" "eanu" "goaski" "goaskit" "guoibmi" "ipmi" "ipmit" "irgi" "isit" "lunta" "mánáidmánná" "mannji" "máhka" "máttaráddjá" "máttaráhkku" "moarsi" "muoŧŧá" "muoŧŧal" "náittosguoibmi" "neahpi" "oabbá" "oabbábealle" "oambealle" "oappáš" "oarpmealle" "osku#guoibmi" "ristváhnen" (".*sássa"r) "siessal" "siessá" "vieljaš" "viellja" "vielljabealle" "vilbealle" "vuohppa" "vuonáhkku" "vuoni" ;
# These can only be possessed by HUMAN-INDIVIDUAL, HUMAN-GROUP, PROFESSION, OFFICE, HUM-FUNCTION, ABSTR-TEXT

LIST FAMILY-ALSO-ABSTRACT = "áhčči" "áddjá" "áhkku" "bearaš" "eadni" "oapmahaš" "sohka" "sohkagoddi" "váhnen" "veahka" ;
# These can be possessed by HUMAN-INDIVIDUAL, HUMAN-GROUP, PROFESSION, OFFICE, HUM-FUNCTION, ABSTR-TEXT, PLACE (minus NATURE-PLACE), Prop Sem/Plc, NATION, ANIMAL, EDUCATION, ORGANIZATION and INSTITUTION. Can also be used as metaphors.
# Giellaplánaid eadni.

SET FAMILY = FAMILY-ONLY-HUMAN OR FAMILY-ALSO-ABSTRACT ;

LIST NON-FAMILY = "bárdni" "bártnáš" "divššohas" "gánda" (".*guoibmi"r) "guossi" "kránnjá" "lagamuš" (".*mánná"r) "mánáš" (".*nieida"r) "nieiddaš" (".*nisson"r) (".*nisu"r) "nuorra" "oskkuviellja" "olmmái" (".*olmmoš"r) "ránnjá" "rumaš" "sámenuorra" "sápmelaš" "skibir" "skihpár" "smávvagánda" (".*ustit"r) "verdde" "viissis" ;
# These can be possessed by HUMAN-INDIVIDUAL, HUMAN-GROUP, PROFESSION, OFFICE, HUM-FUNCTION, ABSTR-TEXT, EDUCATION, PLACE (minus NATURE-PLACE), Prop Sem/Plc, NATION, EDUCATION, ORGANIZATION, INSTITUTION and INDUSTRY.

LIST HUMAN-LIKE = "beahtu" "beargalat" "eŋgel" "háldi" "hearrá" "ipmil" "stállu" "suodjalus#eŋgel" "ulda" ;

# This set is used in Gen/Acc-rules.
     
LIST SAPMI = "davvisápmi" "julevsápmi" "lullisápmi" "máttasápmi" "mearrasápmi" "nuortasápmi" "oarjesápmi" "sápmi" "Sápmi" ;

LIST PROP-ATTR = Sem/Mal Sem/Sur Sem/Fem (ant m)  (ant f) ;
LIST PROP-SUR = Sem/Sur Sem/Mal Sem/Fem  (ant m)  (ant f) cog ;



SET HUMAN-INDIVIDUAL = HUMAN-LIKE OR FAMILY OR NON-FAMILY OR Sem/Hum - SAPMI OR ("gii") OR ("guhte") OR PROP-SUR ;

LIST HUMAN-GROUP =  "álbmot" (".*álbmot"r) (".*bearaš"r) (".*čearda"r) "delegašuvdna" "gázzi" (".*guovttis"r) (".*guovttos"r) (".*joavku"r) (".*kommišuvdna"r) (".*komiteá"r) "ráidu" "ráđđi" ("sápmi" N Pl) "unnitlohku" (".*veahka"r) (Sem/Group_Hum) ;
  ## group of humans that connot be a building at the same time
 
LIST PROFESSION1 = (".*advokáhta"r) (".*advokáhtta"r) (".*duopmár"r) "arkeologa" (".*artista"r) (".*assisteanta"r) (".*áittardeaddji"r) "ákšoalmmái" (".*bargi"r) "buorideaddji" "ámmátolmmái" "ámmátolmmoš" (".*mánni"r) (".*hoavda"r) (".*meannudeaddji"r)  (".*ámmát"r) "boazoeaiggát"
(".*geahčči"r) (".*bagadalli"r) (".*bagadeaddji"r) (".*konsuleanta"r) "bargoveahka" "bassaleaddji" "bassi" "báhkár" "báhppa" "bálvaleaddji" (".*čálli"r) "bearremánni" "bibliotekára" (".*biigá"r) ("biila#divvut" Der/NomAg) (".*vuovdi"r) (".*terapevta"r)  (".*terapeuta"r) "bisma" "bismá" "bispa" (".*bivdi"r) (".*hábmejeaddji"r) "boanda" "boazodoalli" "boazosápmelaš" "boazosápmi" "bolesmeašttir" (".*vuoddji"r) "buvttadeaddji" 
"čalbmeáhkku" "čalbmeeadni" "čálaoahppavaš" "čiekči" "čorgejeaddji" (".*čuojaheaddji"r) 
"dáidár" (".*dáiddár"r) "dálon" "dálueamit" "dáluisit" "dárkkisteaddji" "davvisápmi" (".*dikšu"r) (".*direktevra"r) (".*dirigeanta"r) (".*divššár"r) (".*doaimmaheaddji"r) (".*doavttir"r) "dulka" "duojár" (".*duopmar"r) (".*dutki"r) 
"ealáhusdoalli" "eallo#doalli" "eanadoalli" "eananhearrá" (".*sadjásaš"r) "eatnandoalli"
"fáldi" "fitnodatdoaibmi" "fitnodatoamasteaddji" 
"gávpejas" "giellaráđđi" (".*veahkki"r) "girdi" "girko#bálvaleaddji" "girkodulka" "gollerávdi" "gonagas" "govvadáidár" "govvejeaddji" "guođoheaddji" "guolásteaddji"
"hárjeheaddji" (".*teknihkar"r) (".*jođiheaddji"r) "johttisápmi" "johttisápmelaš" (".*journalista"r) "keaisár" "kirurga" "koahkka" "kuráhtor" "korefereanta" "kursaoahppi" 
"lágideaddji" "láibu" "lávlu" "leanut" "logopeda" "luhkkár" 
"máilmmimeašttir" "máhttájeaddji" "máttasápmi" "mearraalmmái" "mearraolmmái" "mearrasápmi" "miššoneara" "museabargi" "musihkkadahkki" "musihkkár"
"návetvázzi" "neavttár" "njunuš#jurista" "noaidi" "offiseara" (".*oahpahalli"r) (".*oahpaheaddji"r) "oahppi" ("oaivadit" Der/NomAg) "oaivámuš" "ođashoavda" "ođaslohkki" "ossodatdirektevra" "ossodatdirektøra" 
"parkeren#fákta" "pedagoga" "plánejeaddji" "poliisa" "politiija" ("prentet" Der/NomAg) "profehta" "professor" 
"rádjefákta" "rádjesoalddát" (".*ráđđeaddi"r) "ráđđeolmmái" "reainnár" "rávdi" "reaŋga" "redaktevra" "rehketdoallodárkkisteaddji" "rehketdoalli" "rektor" "ruhttejeaddji" "ruvdnaprinsa" ("ruvvet" Der/NomAg)
"sámediggeráđđi" "sámeduojár" "sárdnideaddji" "sávzadoalli" "sealgeeadni" "sester" "silbarávdi" "snihkkár" "soalddát" "sosionoma" "spábbačiekčanhárjeheaddji" "spábbačiekči" "stipendiáhta" "studeanta" "sundi" "suohkanbáhppa" "suohkan#plánejeaddji"  "stáhtahálddašeaddji" 
"šibitdoalli" "táksevuoddji" "telefonvástideaddji" "tuollár" "turistaofelaš" "uksafákta" "universitehtalektor"  
"váldodoaimmaheaddji" "váldodoaimm" "viessohoiddár" "vuoktačuohppi" ;

SET PROFESSION = PROFESSION1 - Prop ;

LIST OFFICE = "áirras" "ámtamánni" "bajitbáhppa" "boazodoallohoavda" (".*čálli"r) "dekána" "farao" "fylkkaráđđi" "gonagas" "leansmánni" (".*ministtar"r) "nissonáirras" "politihkar" "prinseassa" "ráđđe#láhttu" "ruvdnaprinseassa" "ságadoalli" "sámedemokráhtta" "sámediggeáirras" "sámediggepresideanta" "sámediggeráđđi" "sátnejođiheaddji" "stivrralahttu" "ovdaolmmoš" "presideanta" "stáhtaráđđi" "várrepresideanta" ;

SET TITLE = REAL-TITLE OR TITLE-LIKE OR PROFESSION OR OFFICE OR Actor ; 

LIST HUM-FUNCTION =  "buohcci" "divššohas" "kandidáhtta" "kandidáhta" "nomáda" "oahppanolmmoš" "oktavuođaolmmoš" "olbmobárdni" "pasieanta" "ráđđealmmái" "riikkavuloš" "sámedemokráhtta" "skohtervuoddji" "soahteolmmái" "stáhtaboargár" "suttolaš" "turista" "virgeguoibmi" "virgeolmmoš" "vuorasolmmoš" ;
 #XXX bad name
 # Don´t add veahkki + Ess.
 
LIST HUM-ATTR =  "bassi" "divššohas" "eanaeaiggát" "eaiggát" "fástaássi" "gávttehas" "informánta" "lestadiánalaš" "sámegielat" "suttolaš" ;
    # adjectival

LIST HUM-VERB = "ássi" "boaris" "bálvaleaddji" "bálvalanguoibmi" "boazoeaiggát" "dállodoalli" "eanaeaiggát" "geavaheaddji" "giliássi" "muitaleaddji" "ovttasássi" "riikkaássi" "vuotnaássi" "vuostebelle" "vuostebealli" ;
    # vearbal

LIST NATIONAL-IDENTITY-NOT-SAPMI = (".*sápmelaš"r) "dáčča" "dánskalaš" "dáža" "duiskalaš" "guovdageaidnolaš" "hollándalaš" "indiána" "israellaš" "judálaš" "juvddálaš" "kvena" "maya-#indiána" "nuortalaš" "olgoriikalaš" "palestinalaš" "romalaš" "romer" "samarialaš" ;

     # Also regional identity 
 # Duomma:lea
      
LIST SUOPMA = "Davvi-Ruoŧŧa" "Davvi-Suopma" "Lulli-Ruoŧŧa" "Lulli-Suopma" "Mátta-Ruoŧŧa" "Mátta-Suopma" "Oarje-Ruoŧŧa" "Oarje-Suopma" "ruošša" "Ruošša" "ruoŧŧa" "Ruoŧŧa" "suopma" "Suopma" ;
     
      
# This set is used in Gen/Acc-rules.
 # Duomma:lea

SET NATIONAL-IDENTITY = NATIONAL-IDENTITY-NOT-SAPMI OR SAPMI ;

SET HUMAN = HUMAN-INDIVIDUAL OR HUMAN-GROUP OR PROFESSION OR HUM-FUNCTION OR HUM-ATTR OR HUM-VERB OR OFFICE OR Sem/Hum  ;

SET NOT-HUMAN = N - HUMAN ; 

#!! * Sets for animals

LIST GEN-ANIMAL = "ábegáhttu" "ásen" "beana" "bohkká" "bussá" "čeavrris" "ciiku" "čivga" "dámmá" "ealit" "fuođđu" "gáhttu" "gáica" "gálbi" "gihcci" "guigu" "gussa" "heasta" "horti" "láppis" "loddi" "máhtu" "njiŋŋálas" "ore" "sávza" "sávzabeana" "smávvafuođđu" "spiinnečivga" "spiidni" "vársá" "vársi" "vielppis" "vierca" "vuoksá" "vuoncávaris" "vuonccis" Sem/Ani ;

LIST PREDATOR = "gumpe" "guovža" "bierdna" "geatki" "albbas" ;


LIST HUNT-ANIMAL = "ealga" "rievssat" ;

LIST BOAZU = "áldu" "boazu" "čearpmat" "čora" "eallu" "heargi" "miessi" "sarvvis" "vierroboazu" "vuonjal" ;

LIST FISH = "bálddis" "čuska" "dápmot" "diddi" "diksu" "dorske#guolli" "dorski" "duovvi" "gilot" (".*guolli"r) "hávga" "juksu" "luosjuolgi" "luossa" "luossaguolli" "luossanálli" "muortu" "sallit" "sáidi" "sáivaguolli" "šákša#dorski" "stábbasáidi" "stáinnir" "stuorasáidi" "rutnot" "veajet" ;

LIST BIRD = "ákta" "báiski" "bovttáš" "cagan" "čearret" "gádde#loddi" "gáiru" "mearaloddi" "skávli" "skuolfi" "šuoršu" "albatrossa" "alitbađuš" "alitgaccet" "alitoaigaccet" "bakku" "bastesuorsi" "bađošgaccet" "beahcebáhcatloddi" "beahceloddi" "bealdocihci" "bealdo#garjá" "bealdo#vuorra" "bealgeloddi" "beallje#loađgu" "bealljerásttis" "bearalskuolfi" "beasterages" "beibboš" "beibbošcihci" "beibbošcizáš" "belddoš#vizar" "beškkoš" "beštor" "bieggafálli" "biehkan" "bigán" "bihcabasčáihni" "bihcebasgáski" "bihcebasčáihni" "bissehávka" "bižus" "boaimmáš" "boallonjukča" "boađđocivkkán" "borjjastit" "borsenjunni" "bovttáš" "bulddogas" "bumbá" "buoidegaccet" "buokčat" "buokči" "bupmálaš" "bursanjunni" "bussáskuolfi" "buvvedat" "báiski" "bákteduvvá" "bárbmofálli" "bávgoš" "cagan" "cagán" "cielláš" "cihceskuolfi" "cihrrelastavizar" "civkaleivvoš" "cizášfálli" "cizopaš" "cizot" "davvelastavizar" "davveloddi" "davvevizar" "deaggačáihni" "deagge#báiski" "deaggecihci" "dearbmerásttis" "derpmoš" "dieldá" "dielkoduvvá" "dihtti" "diksobáhppa" "dirre#lihká" "doadjosuorsi" "dokta" "dovtta" "duoršu" "duottarbižus" "duottarcivkaleivvoš" "duvvá" "duvvárásttis" "dálvebeštor" "dálveruoivil" "dálvi" "dážaboaimmáš" "dážasuorsi" "fasána" "fiehta" "fiervá#vierru" "fierváviroš" "fiskesbeštor" "fiskescihci" "fiskescižáš" "fiskesgaccet" "fiskesjievžžan" "fiskesnjungáhkkor" "fiskesvizar" "fiŋka" "gaccepaš" "gaccet" "gahperbáiski" "gahpervizar" "garjá" "gassa#njunni" "gazzaloddi" "gazzebiehtár" "geađgejorgu" "geađgerásttis" "giega#biigá" "giehka" "giehpa#vuonccis" "giellavealgu" "gierdo#duvvá" "gierdorásttis" "gierdosuorsi" "gierdo#čuonjá" "gierra" "giljobaš" "gilljobaš" "gilvabeštor" "girjebáhcatloddi" "girjelivkkár" "giron" "goahcce#loddi" "goahppil" "goahppil#gálli" "goahtegoljaš" "goalsi" "goalsoruoššehas" "goalšovizar" "goarga" "goargaloddi" "goargŋohaŋŋá" "goaski" "goaskin" "gollaš" "gollebeibboš" "gollecihci" "golle#gahpir" "golli" "golláš" "gorges#hávda" "gorgeslihkká" "govuskuolfi" "gođa#loddi" "gožu#vuoražas" "guoikgáhrit" "guoikgárri" "guoksa" "guolbba#vierru" "guolbbaviroš" "guolbba#čoavžžu" "guollefálli" "guollečearret" "guorga" "guossabáhcatloddi" "guossagaccet" "guossaruossanjunni" "guossavizar" "guovssat" "guovssoloddi" "gussagoalsi" "guškil#vierru" "guškil#viroš" "guškkástat" "gáddebuvvet" "gáddeskirvi" "gádde#vierru" "gáddeviroš" "gáhkkor" "gáiru" "gállat" "gálli" "gállit" "gáranas" "gáranas#loddi" "gárggogoalsi" "gárgo#goalsi" "gávlačuonjá" "hálkaloddi" "haŋŋá" "heartavizar" "hoaššacihci" "hurrat" "hurri" "hurri#goahppil" "hurri#čukčá" "háigir" "háigir#loddi" "hálka" "hálkagonagas" "hálka#čielkkis" "háskil" "hávda" "idja#skuolfi" "jeaggefálli" "jeaggemeahkástat" "jeagge#čoavžžu" "jeaggehávut" "kanárialoddi" "keaisár#pingviidna" "jiekŋačielkkis" "jiesmi" "jievjaskuolfi" "jievželoddi" "jávresopmir" "leahttofálli" "leahttospálfu" "leaibeloddi" "leivvoš" "lidnu" "liidn-elláš" "liin-elláš" "litnut" "livkkár" "loađgu" "loddi" "loddegonagas" "loddejievži" "luhkka#loddi" "láfol" "láhtospálfu" "látteguoksa" "látteguovssat" "láttovuonccis" "láŋgá" "meahkástat" "mearragoaskin" "mearragáiru" "mearra#čuonji" "mearračuonjá" "miellespálfu" "miesttacihci" "miesttarásttis" "miesttavizar" "miesttašurrevizar" "moldecizáš" "muhkkecizáš" "muogir" "muorracivkkán" "muorragakcu" "mákreallačearret" "niehttegarjá" "niitocivkkán" "njukča" "njurggu" "njurgut" "noaideloddi" "nuortacihci" "nuortalastavizar" "nuorti" "náhkke#soadjá" "oanehisnjunčuonjá" "paradiisa#loddi" "pelikanloddi" "pingviidna" "ribeskáiti" "riddocivkaleivvoš" "riddo#vierru" "riddoviroš" "rievssat" "rievssatcižáš" "rievssatfálli" "roavgogonagas" "rohtogaccet" "rohtovizar" "rohtočoavžu" "rohtočoavžžu" "rukses#bađoš" "ruksesbeibboš" "ruksescivkaleivvoš" "ruksesfiehta" "ruksesguškil" "ruksesjuolčoavžžu" "ruksesruivil" "ruksesruoivil" "ruksesrásttis" "ruksessoadjá" "ruksessuorrehávut" "ruoivil" "ruonábeibboš" "ruonácihci" "ruonácizáš" "ruonájuolčoavžžu" "ruoná#čáihni" "ruossalas#njunni" "ruovddagas" "ruovdecihci" "ruovdecizáš" "ruođa" "ruoššaboaimmáš" "ruoššaskire" "ruošša#skirri" "ruoššaállat" "ránescihci" "ránescizáš" "ránesháigir" "ráneslivkkár" "ránesrásttis" "ránesskuolfi" "ránes#skávli" "ránessnárttal" "ránesčuonjá" "ránesčáihni" "rásttis" "rávgoš" "salletgáiru" "sallitgáiru" "seallecižáš" "siesse#mohkki" "sieđgacivkkán" "sildegáiru" "silkebuokča" "sitronbeštor" "skierru" "skilkeskálka" "skire" "skirri" "skiŋgegaccet" "skoarra" "skuolfi" "skutnjaloddi" "skáhpeloddi" "skáiti" "skárfa" "skárfa#čielkkis" "skávli" "snárttal" "soadja#njálla" "sopmir" "spoađđo#duoršu" "spálfu" "stoarbmaloddi" "stoarbmaspálfu" "stohkkegaccet" "stohkkerievdu" "stohkkespálfu" "stohpoháigir" "stohpospálfu" "struhcca" "struhccaloddi" "stuorrafiehta" "stuorragiljobaš" "stuorraguškil" "stuorraguškkástat" "stuorrameahkástat" "stuorraskáiti" "stuorraskávli" "stuorravuodja" "stuorravuojaš" "stuorračarkkus" "stuorračoavžu" "stuorračoavžžu" "stuorračuonjá" "stálobeibboš" "stárra" "suoidnesuorsi" "suoidnečiksa" "suolaskáiti" "suorrespálfu" "suorsi" "suotnjoliráš" "suotnjočoavžžu" "suovkacihci" "suvlá" "sáhcomeahkástat" "sáhpánboaimmáš" "sáhpánskuolfi" "sáiva#hávda" "sávjahávda" "sávza#loddi" "toardnagarjá" "toardnagájon" "uhcabuokča" "uhcabuvvedat" "uhca#báiski" "uhcaciellá" "uhcacihci" "uhcameahkástat" "uhca#njukča" "uhca#saddi" "uhca#vierru" "uhcaviroš" "uhca#čoavžžu" "uhcačáihni" "ummolcizáš" "upmolcizáš" "unduláhtta" "uvlohávut" "varra#loddi" "veastareaddji" "vigláčoavžžu" "vilgesnierčuonjá" "vilges#soadja#skávli" "vilgesummolcizáš" "vilges#vierru" "vilges#viroš" "vilges#čoavžu" "vilges#čoavžžu" "vilgessealčáihni" "vintán" "vuoncáčivga" "vuoncávaris" "vizardit" "vuodja" "vuojaš" "vuoktafiehta" "vuoktagaccet" "vuoktagoalsi" "vuoktaláfol" "vuoktaskárfa" "vuonccis" "vuonccisloddi" "vuoražas" "vuorččis" "vuovdaduvvá" "vuovdecivkaleivvoš" "vuovdegaccet" "vuovdeloddi" "vuovdemeahkástat" "vuovderásttis" "vuovdevizar" "vuovde#vuorččis" "vuovde#čoavžu" "vuovdečoavžžu" "ábirievssat" "áhpedokta" "áhpedovtta" "áhpe#háskil" "áhpehávda" "áhpelihkká" "áhpespálfu" "ákta" "állat" "čarkkus" "čearret" "čiekčá" "čielkkis" "čierggesrásttis" "čiksa" "čoađgi" "čukčá" "čuohtegielas" "čuohtegiella" "čuonji" "čuonjá" "čuonjáfálli" "čáhcebávgoš" "čáhcevuonccis" "čáhppes#bađoš#guškil" "čáhppesbuokča" "čáhppessealbeštor" "čáhppesgaccet" "čáhppesrásttis" "čáhppesčoavji" "čáhppes#čoavžu" "čáhppesčoavžžu" "čáihnecizáš" "čáihnegaccet" "čáihni" "šilljocihci" ;

#LIST ANIMAL-PLANT =
    # Plants that are rather animals (lingustically)

SET ANIMAL = GEN-ANIMAL OR HUNT-ANIMAL OR BOAZU OR FISH OR PREDATOR OR BIRD ;


SET ANIMATE = HUMAN OR ANIMAL ;
 

#!! * Sets for things

LIST TOOL = "ákšodearri" "áibmi" "ákšu" "bargobierggas" "baste" "bastevuogga" "báddi" (".*rusttet"r) "beana" "bieggamillorusttet" "biebmu" "bierggas" (".*bissu"r) (".*bivddus"r)  "bivttas" "boazonjuovvan#niibi" "bohtal" "borramuš" "buiku" "buođđu" 
"čáhci" "čála" "čogu" "čujonas" "čuoibmi" "datni" "datneárpu" "dáidda" "dálkkas" "dihtor" (".*mašiidna"r) "doarta" "dolgevuogga" "dolla" "dorte" "duodji" "duorgu" "ettetviidni" "fierbmi" 
"gáddenuohtti" "gáidánuohtti" "gáffal" "gáktesoadjá" "gazirdanniibi" "giehta" "giehtaváđđu" "gievdni" "giikan" "gohppu" "golgadat" "golle#bánno" "gurutgiehta" 
"hárpa" "jávre#vuogga" "joddu" "joddu-#buođđu" "kohppa" "kruvdnu" 
"láhkki" "láibi" "liidna" "liidnesihkaldat" "liššá" "luovosbeana" "luossa#vuogga" "luovos#fierbmi" "lyra" "maŋŋečanus"
"mašiidna" "miehkki" (".*mihttu"r) "millu" (".*telefovdna"r) "mohtorsahá" "muorječoaggi" "muorravuogga" "nállu" "niibi" "njozon" "njuovvan#niibi" "oar-#juolgi"
"oaggunstággu" "oaggunstávrá" "olgešgiehta" "olju" "riffal" "roahkkefierbmi" "ruhta" "ruvdnu" 
(".*sahá"r) "sáibma" "sáiti" "sálti" "seaila" "siibma" "sihkaldat" "sirpe" "skárbban" "skierat" "snáldu" (".*soabbi"r) "sabet" "soavvelvuogga" "spánnja" "speallanmašiidna" "spihkár" "stággu" "stávrá" "stivrran" "suohpan" "suorbma" "šlivgun#boksa" 
"váđđu" "várve" "veahkke#mohtor" "veaike#baste" "vealgi" "viidni" "vuogga" "vuoiddas" "vuoigŋan#mašiidna" Sem/Tool Sem/Obj-el Sem/Obj-clo Sem/Obj-rope Sem/Tool-catch ;

   # Those could be further subdivided into: kitchen tools, working tools, hunting tools, fishing tools, writing tools...
  
#LIST FISHING-TOOL = "buođđu" "fierbmi" "gáddenuohtti" "giehtaváđđu" "goallun#stoahku" "joddu" "joddu-#buođđu" "liidna" "luovos#fierbmi" "vuogga";

#LIST HUNTING-TOOL = "bissu" "HV-#bissu" "njuolla";

#LIST KITCHEN-TOOL = "baste" "gealva" "millu" "bánno";

#LIST WORKING-TOOL = "ákšu" "goaivu" "mohtorsahá" "nibi" "sirpe" ;

#LIST COMMUNICATION-TOOL = "mobiilatelefovdna" ;

#LIST MACHINE = "bieggamillorusttet" "mašiidna" ;

#LIST EATING-TOOL = "gáffal" ;

LIST MUSIC-INSTRUMENT = "bássa" (".*čujonas"r) "gitárra" "hárpa" "lyra" "njálbmehárpa" "njurgganas" "piáno" "stráŋga" "urggut" Sem/Tool-music ;

LIST WRITING-TOOL = "bliánta" "bleahkka" "dihtor" "krihtta" "liánta" "peanna" "peanná" "tušša" ;

#LIST HANDICRAFT-TOOL = "áibmi" "datneárpu" "nálu" "suorbmagoahti" "vikšamuorra" ;

# preliminary name for the set:
LIST ABSTR-T =  "bargobearráigeahčču" "bargodearvvašvuođa#bálvalus" "bearráigeahčču" "cummá" "dánsa" "dánsu" "dearvvašvuođageahčču" "doarjja" "eallin" "fápmu" "ivdnen" "gásta" "geahčču" (".*goansta"r) "gullu" "heagga" "jahkebargu" "giellalokten" "lasseruhtadeapmi" "láhkavuođđudus" "mearka" "oahppu" "oaidninvuohki" "preassadoarjja" "prošeakta" "ruhtadoarjja" "sisabahkken" "soabadanproseassa" "sosialiserenproseassa" "suvli" "varra" "vuohki" ;

LIST SINGLE-SOUND = "fonema" "jietna" "jietnadat" "loahppa#jietnadat" "vokála" ;

    # Can be part of a word
LIST SOUND-SYSTEM = "básonšuokŋa" "čottajuoigan" "gohččun" "lávlunvuohki" "luohti" "musihkka" "riedja" "rohkos" Sem/Prod-audio ;

LIST LANGUAGE =  "jiddisch" (".*giella"r) (".*suopman"r) Sem/Lang ;

SET SOUND = SINGLE-SOUND OR SOUND-SYSTEM OR LANGUAGE ;

LIST SATNI = "adjektiiva" "advearba" "bárahisstávvalvearba" (".*tearbma"r) "doaba" "kásushápmi" "kontráktavearba" "laktapartihkkal" "laktapartihkal" "miehtan" "namma" "partihkal" "partihkkal" "postposišuvdna" "preposišuvdna" (".*tearbma"r) "sátni" "sátnemátta" "substantiiva" "tearbma" "tihttel" "vearba" (".*sátni"r) (".*namma"r) ;

    ## tool for verbal activity

LIST WORD-PARTS = "akseantamearka" (".*bustávva"r) "goallossáhcu" "gravismearka" "stávval" ;

SET ABSTR-TOOL = SOUND OR ABSTR-T ;

# Premilinary dividing of the set as a help for dimambiguating "gullat" vs. "gullát".  
 
#Those are typically comitative (instead of locative) if they go together with verbs from the LIST HUMAN-AGENT-V and probably also from INTELLECTUAL-ACTIVITY-V. The counterpart for locative is until now SITUATION, we might have to consider making a new list ABSTR-TOOL comprising SITUATION and other elements.

#!! * Sets for qualities
# - - - - -

LIST COLOUR = "alit" "čáhppat" "fiskat" "fioleahtta" "ránis" "ruoná" "ruoksat" "runta" "ruškat" "sáhppat" "vielgat" "violeahtta";

#!! * Sets for things, not necessarily tools
# - - - - - - - - - - - - - - - -

LIST MATERIAL = "ávnnas" "báhkki" (".*náhkki"r) "gámas" "gihtta" "gusanáhkki" "materiála" "muorra" "náhkki" "ullu" "sávzzanáhkki" "silba" "stálli" ;
  
LIST PLANT-PART = "lasta" "ruohtas" "upmol" ;
 
    # of plants etc. 
  
LIST SHOE = "čázet" "gállot" "gálssot" "gáma" (".*gáma"r) "goikket" "gummestevvel" (".*guohpá"r) (".*loabát"r) "njuorjjot" "nuhtat" "nuvttat" "nuvttot" "sabet" "sáppat" "sisttet" "skárri" "skuovva" "stevvel" "suohkku" (".*suohkku"r) ;

    # anything that will go on feet. IV-verbs like "vázzit" can take these as objekt.
    
LIST BEDCLOTHES = "beaska" "gávnnit" "oađđádagat" "rátnu" "roavgu" "seaŋga";

    # anything that can be used as bedclothing. IV-verbs like "oađđit" can take these as objekt. 
    
LIST CLOTHES = "avvi" "báidi" (".*báidi"r) "beaska" (".*bealjus"r) "bivttas" "boagán" (".*boagán"r)  (".*buksa"r) (".*buser"r) (".*busir"r) (".*bivttas"r) "čalbmeláset" (".*čuhppa"r) "čuvla" "dorka" "fáhcca" (".*gahpir"r) "gákti" "gárvu" (".*gákti"r) "gistta" (".*hámet"r) (".*liidni"r) (".*luhkka"r) "rahpot" (".*readdju"r) (".*reddjo"r) "T-báidi" (".*váhcca"r) (".*váhttu"r) (".*vuolpu"r) Sem/Clth;
   

LIST FOOD = (".*biebmu"r) (".*biergu"r) (".*borramuš"r)  (".*muorji"r) "fuođar" (".*guolli"r) "gordni" (".*mális"r) (".*jáffu"r) "jokŋa"  (".*láibi"r) "mális" "málli" "márfi" "niesti" "sarrit" "spelle" (".*gáhkku"r) Sem/Food ;


    # potential tool for political activity etc., potential place for other food, insects etc.
    
LIST LIQUID = "bohccomielki" "bruvsa" "buolleviidni" "buolleviidna" "buollin#viidna" "buollánviidna" "buollánviidni" "čáhci" "čáhppesmuorjesákta" "čielgamielki" "deadja" "duolvačáhci" "gáffe" "gáiccamielki" "gearra" "girkoviidna" "girkoviidni" "goden" "gusamielki" "jugus" "juhkamuš" "kirkoviidna" "kirkoviidni" "mielki" "rihppaviidna" "rihppaviidni" "ruksesviidna" "ruksesviidni" "sarritsákta" "sáivačáhci" "sákta" "sávzzamielki" "suvramielki" "viidna" "viidni" "vilgesviidna" "vilgesviidni" "vuola" "vuolla" Sem/Drink ;
    
LIST PLANT = "alášbađverássi" "alitdiehpperássi" "ávža" "báhpačehporaš" "beahci" "bastilislátnjá" "beahceguolbba" "beatnatnjuovččarássi" "biehtárguhkká" "biehtárrássi" "bievlaguolbba" "boallorássi" "biello#daŋas" "biellorássi" "cizáš#vuolpu" "duopma" "duottarbusságeahpil" "fiikkonmuorra" "fiskesrássi" "gálla" "gámasuoidni" "gáiski" "gáskálas" "gieddeguolbba" "gieddemáđir" "gollerássi" "guossa" "heasttagazzarássi" "horbmá" "jámešbátni" "jeagil" "juopmu" "lastamuorra" "lageš" "leaibi" "miestta" "muotkenjárgaguolbba" "niittoguolbba" "njálanjuovčča" "oaivejeagil" "oaksi" "rásseguolbba" "reatká" "sállja" "sieđga" "skáhpi" "skierri" "soahki" "suhpi" "uvlo#rássi" "vándádivga" "vilgesmáđir" "vuodjarássi" "vuovdeguolbba" Sem/Plant ;

    # Plants could be actors in fairytales (Alice)
    

LIST FURNITURE = "áltár" "beavdi" "hildu" "ildu" "seaŋga" "skáhppe" "soffá" "stuollu" ;

#!! * Sets for things such that people can be inside them:
LIST SPACIOUS-VEHICLE = (".*fanas"r) (".*biila"r) "báska" "borjjasvanca" (".*busse"r) "čáhcegirdi" "dámpa" "drošša" "drošše" "fievru" "gádjunhelikopter" "geaksu" "girdi" "heargeráidu" "helikopter" "láivi" "mohtorfievru" "mohtorvanca" "njealjit" "njealjejuvllat" "ruvttobiila" (".*sáhttu"r) "skuvla#biila" "suhkkivanca" "tákse" "táksi" "toga" "tráktor" "vanas" "vanca";

#!! * Sets for things such that people cannot be inside them:
LIST SMALL-VEHICLE = "ásen" "beana" "fievrošibit" "heargi" "heasta" (".*sáhttu"r) (".*sihkkel"r) "mohtorgielká" "mopeda" "njealjejuvllat" "skohter" "spárka" "skuter" "sykkel" "njealjejuvlageassi" "vuoksá" ;

SET VEHICLE = SPACIOUS-VEHICLE OR SMALL-VEHICLE OR Sem/Veh ;

LIST TEXT = "áppes" (".*listu"r) (".*čálus"r) (".*girji"r) (".*-girji"r) (".*gažaldat"r) (".*aviisa"r) (".*áviisa"r) (".*bábir"r) (".*logahallan"r) 
"bargobihttá" (".*prográmma"r) (".*lávlla"r) (".*bláđđi"r) (".*mearrádus"r) "bušeahttapoasta" (".*čála"r) (".*cealkámuš"r) "čuovus" 
(".*teaksta"r) (".*dieđáhus"r) (".*journála"r) "dokumeanta" (".*girje"r)  
"e-poasta" (".*eksamen"r) "eksámenbihttá" 
"fáddásiidu" "fáksa" "gáldu" "gullugovva" "journála#čállin" "journáladiehtu" 
"kárta" "lasta" (".*láhka"r) "láđđi" "leaksu" "mátkemuitalus" "mielddus" 
"neahttasiidu" "novealla" (".*raporta"r) (".*reive"r) "riektedieđa" "romána" "ruovttubargu" "ruovttusiidu" (".*konvenšuvdna"r)  "sárdni" "sitáhtta" "statistihkka" "šiehtadus" "telegrámma" "váidda" Sem/Txt ;

  # A potential place (locative?) for sth. written.
  # Maybe one should differentiate between written and spoken texts

LIST ABSTR-TEXT =  "áddejupmi" (".*plána"r) (".*mearrádus"r) (".*evttohus"r)  (".*njuolggadus"r) (".*ortnet"r) (".*árvalus"r) (".*cealkámuš"r) "cuvccas" "doaibmabidju" "eaktu" "eanačujuhus" "gažaldat" "gáhtten#eaktu" "gáldokoda" "geasku" "giellagažaldat" "jearaldat" "lappekodisilla" "láhkaásahus" "luođaleaktu" (".*mearrádus"r) "máinnas" "muitalus" "resolušuvdna" "sáme#kodisilla" (".*soahpamuš"r) ;

  # A potential place (locative?) for sth. written, spoken or abstract activity 
  
#!! * Part-whole sets for human

LIST BODY = "alimat" "aski" "ámadadju" "bahta" "bahtačalbmi" "bahtaráigi" "bavssa" "bállosuotna" "bátni" "beađbelađas" "bealgi" "beallji" "bierggas" "biežadákti" "billu" "buđđá" "bullječalbmi" "čalbmečiehka" "čalbmedeahkki" "čalbmegolláš" "čalbmegulbmi" "čalbmeguolga" "čalbmeluopma" "čalbmi" "čatnan#gođđu" "čeabet" "čelččen" "čeŋkkeš" "čibbeskálžu" "čibbi" "čižžebátni" "čižži" "čielgeađa" "čielgedákti" "čielgi" "čoalli" "čoamohas" "čoarbbealdeahkki" "čoarbbeallađas" "čoarbbealli" "čoavji" "čorbma" "čorbmaoassi" "čorbmaváimmus" "čorbmavuođđu" "čotta" "čuožžamasdeahkki" "čurti" "čuvdi" "dábba" "dáđvi" "dáktegođus" "dákti" "dávdi" "deahkkegođus" "deahkki" "erttet" "erttetdeahkki" "gahpárus" "gahperus" "ganjalčalbmi" "gardnjil" "gaskasuorbma" "gassačoalli" "gassaváhkká" "gáddu" "gáibi" "gállu" "gánis" "gátnis" "geađđi" "geahpis" "giegir" "giehta" "giehtadeahkki" "giehtagávva" "giehtaramas" "giehtaruohtas" "giehtasealgi" "giehtaváibmu" "gilga" "gipsajuolgi" "goapmirváimmus" "goaŧŧočotta" "goaŧŧu" "goikedákti" "gorut" "gullu" "guoggomas" "guoggomasráksá" "gurutgiehta" "hárdu" "heaggaroasmmoheapmi" "jarri" "juolgebealgi" "juolgečelččen" "juolgedeahkki" "juolgegávva" "juolgelađas" "juolgeláhpi" "juolgemátta" "juolgesuorbma" "juolgevuođđu" "juolgi" "lađas" "láhpelađas" "manin" "maŋŋebuoidi" "mánágoahtečotta" "mánágoahti" "mielganjunni" "monimuš" "monin" "monnejođas" "monneráksá" "morčesuotna" "morči" "muohtu" "náhpi" "nealgečoavji" "niehkki" "nierra" "niskegohpi" "niski" "njálbmi" "njiehcehas" "njunni" "njuovčča" "oaiveassi" "oaivečohkka" "oaivi" "oalgedeahkki" "oalgenorasdeahkki" "oalgenorasváhkká" "oalgi" "oalul" "obbalággá" "orbbeš" "raddi" "rápma" "rumaš" "rumaš#golggus" "rumašlahttu" "rumašnjalbi" "sáhppesuvri" "rumašlahttu" "rumašnjalbi" "salla" "sáhppesuvri" "siidu" "siskkitbeallji" "sotkenoalli" "spirralat" "suolbmudanoalli" "suorbma" "suorbmageahči" "šušmi" "váhkkádeahkki" "váibmu" "vuohččecuozza" "vuoivvas" "vuokta" "vuolit" "vuorggu" Sem/Body "garragiehta" ;

LIST WEATHER-ENTITY = "balva" "arvi" "muohta" Sem/Wthr ;

   
#!! * Sets for places
LIST BUILDING = "áiti" (".*skuvla"r) "barta" "badjedállu" "boasta" "buhcciidsiida" (".*viessu"r) "buvda" "buvri" 
(".*dállu"r)  (".*buvri"r)
"geaidnogievkkan" "goahti" "gávpi" "girku" "hotealla" "kaféa" "krámbuvda" "láđđu" "láhtu" "lávvu" "lonuhusgávpi" "návet" "návetgoahti" "návste" "návstu" 
"oktasaš#barta" "poasta" "rámbuvra" "rásseláđđu" "stálla" "stálle" "stállja" "stohpu" "synagoga" "šloahtta" "tempel" "universiteahtta" "visti" Sem/Build ;

LIST GENERAL-PLACE = "albmi" "almmiravda" "almmiriika" "areála" (".*guovlu"r) "ássanbáiki" "badjegeardi" "badjeeana" "báikegoddi" "báiki" "báktehárji" "bealdu" "biegga#báiki" "biras" "boazoguohtoneana" "buođđosadji" 
"čakčaeana" "čakčaeana" "čakčabáiki" "čakčaorohat" "čiegus" "čoahkkebáiki" 
"dálvebáiki" "dálveeana" "dálveguohtoneana" "dálveorohat" "davviriika" "davvisiida" "dánssa#báiki" "diggebáiki" "dimbbar#čuohppan#sadji" "dollagáddi" "duiska#eana" "eanandoallo-#eana" "eanandoalloeana" "earre#sadji" "fanasláttasadji" "fágabiras" 
"gádde#báiki" "gádde#ravda" "gávpebáiki" "gávpemárkan" "gávpot" "gávpotpoarta" "geaidnoguorra" "geaidnoravda" "geassebáiki" "geasseeana" "geasseguohtoneana" "geasseorohat" "geassesadji" "gilli" "giddagas#báiki" "giđđabáiki" "gieddeeana" "gilvvagárdi" "gilvvohallanbáiki" "girkogárdi" "goahtesadji" "goikeeana" "guoddinseaŋga" "guollebáiki" "guollebivdosadji" "guohtoneana" "guohtuneana" "guottetbáiki" "hámman" "hávdeeana" "helvet" "jeaggeeana" "jeagelguohtuneana" "jienastanbáiki" 
"kálka#eana" "kontineanta" "lassieana" "láigoeana" "luome#eana" "luossabáiki" "luonddubiras" "máilbme#čiehka" "máilbmi" "márkan"   "meahcceeana" "meahccegilli" "njáskan#báiki" "njunušsadji" "nuortaeana"
"oahppobáiki" "oaivegállobáiki" "oktasašeana" "oktasašorohat" "olgobáiki" "olgoriika" "mikroomman" "oapman" "omman" "orohat" "orrunbáiki" "priváhtaeana" 
"rasttidan#báiki" "ránnáriika" "riddo#eana" "riika" "ruoktu" "ruovdemáđidja" "ruovttueana"
"sadji" "sáidebáiki" "sámieana" "sámeeana" "sámevearroeana" "šallju" "siida" "siseana" "skoaltabáiki" "skuvlabiras" (".*šillju"r) "stáhtaeana" "stáhta#sadji" 
"TV-šearbma" "uvdna" "váhneneana" "várddobáiki" "vearroeana" "viessosadji" "viesttar#bađa#eana" "virgebáiki" "vuoiŋŋastanbáiki" "vuopmeeana" ;

LIST NATURE-PLACE = "almennet" "ávži" "bákti" "beahcevuovdi" "borri" "buollán#ávži" "coagis" "čázádat" "čolpi" "darfejeaggi" "dearbmi" "duottarjávri" "duottaroassi" "eatnu"
"fielbmá" "fiervá" "gáddi" "gáisá" "gáisi" "gárggu" "gárgu" "gieddi" "gieva#jeaggi" "gohpi" "gohppi" "gorži" "guolbba" "gurra" "jávri" "jávregáddi" "jeaggi" "jiehkki" "johka" "johka#gaska" "johkagáddi" "johkaleahki" "johkanjálbmi" 
"láddu" "láttu" "latnja" "lavdnjejeaggi" "leahki" "lokta" "luohkká" "luokta" "luondu" "luoppal" "luossajohka" "meahccegieddi" "mearra" "mielli" "mohkki" "muotki" "njárga" "njunni" "oaivi" "oalgejohka" "oalgi" "orda" "orrit" "rádja#čázádat"
"reašši" "riddogáddi" "riddu" "riidi" "ruovttugieddi" "soahkevuovdi" "stealli" "várrevielti" "várri" "vearroduottar" "vielti" "vuohppi" "vuotnabahta" "vuotnagáddi" ;

#LIST ELEMENT = "áibmu" "čáhci" "dolla" ;

    ## (probably often place) They are often tools - they are not probably Pl Loc.

LIST SETTLEMENT-PLACE = "ássanguovlu" "duottar" "eana" "eanaguovlu" "gáddi" "geassesiida" "gilli" "guovlu" "mátkeguovlu" "meahcci" "mearragáddi" "njárga" "riddu" "sámisiida" "siseana" "siseatnansiida" "sis#vuotna" "suolu" (".*jávregáddi"r) "vággi" "vuotna" "vuotnabahta" "vuotnagáddi" "vuotnaguovlu" "vuovdi" ;

SET GEOGRAPHICAL-PLACE = NATURE-PLACE OR SETTLEMENT-PLACE ;

LIST GEN-POLITICAL-PLACE = "gávpot" "gilli" (".*suohkan"r) "fylka" "orohat" "rádji" "riika" "riikkarádji" "ruovttugávpot" "sámeeana" "Sámeeana" "siseatnangilli" "stáhta" "válgabiire" "vuotnagávpot" ;

  ## can also function as an actor
  
LIST FYLKA = "Finnmárku" "Romsa" "Nordlánda" "Lulli-Trøndelag" "Davvi-Trøndelag"
       "Hedmárku" "Akershus" "Buskerud" "Vestfold" "Østfold" (".*Agder"r)
       "Rogalánda" "Hordalánda" "Opplánda" "Fjordane" "Romsdal" "Telemark" ;

LIST NORGA = (".*Norga"r) "norga" "norggabealli" ;

LIST NATION-NOT-NORGA = "Columbia" "davvi#ruoŧŧa" "Duiska" "Frankriika" "Guatemala" "Israel" "Kanada" (".*Amerihká"r) "naššuvdna" "riika"  "Ruoŧŧa" "Sápmi" "sápmi" "Skandinávia" "stáhta" "Suopma" (".*Ruošša"r) ;

    
SET NATION = NATION-NOT-NORGA OR NORGA ;

SET POLITICAL-PLACE = NATION OR GEN-POLITICAL-PLACE OR FYLKA OR (".*gielda"r) ;

    # Can be an actor

  
# This set is important for distiguishing "vuodjit" and "vuodjat", so there shouldn`t be words which can be combined with "vuodjat":
LIST CONCRETE-ROUTE = (".*máđidja"r) (".*máđii"r) (".*bálggis"r) (".*luodda"r) (".*geaidnu"r) (".*gáhtta"r) (".*guorra"r) (".*láhttu"r) (".*ráigi"r) "ráidu" "spáiddarráidu" ;
 # and others!

LIST ABSTR-ROUTE = "gaska" "mátki" "miila" "mohkki" "tuvra" ;

SET ROUTE = CONCRETE-ROUTE OR ABSTR-ROUTE ;
 

LIST WATER = "ája" "áhpi" "álddis" "basseaŋŋa" "čáhci" (".*čázádat"r)  "eatnu" "fielbmá" "jávri" "johka" "luokta" "mearra" "sálteája" "vuodjanálddis" "vuojadanálddis" "vuotna" ;

LIST GOAHTI-PLACE = "árran" "boaššu" "loaidu" "reahpenráigi" "soggi";
 

LIST ROOM = (".*kantuvra"r) (".*latnja"r) "bártniidhivsset" "feaskkir" "gámmár" "gámmir" "garáša" "gievkkan" "guovllukantuvra" "hivsset" "lášmmohallansále" "lášmmohallansálla" "lokta" "nieiddaidhivsset" "šallju" "stohpu" "studio" "šillju" Sem/Build-room ;

SET PLACE = GEOGRAPHICAL-PLACE OR POLITICAL-PLACE OR GENERAL-PLACE OR ROUTE OR BUILDING OR ROOM OR GOAHTI-PLACE OR Sem/Plc ;


LIST MAIN-ORGANIZATION = Sem/Org "alimusriekti" "ámtadiggi" (".*ásahus"r) "bargokantuvra" "báhpaskuvla" "bálvalandoaibma" "bearráigeahččanorgána" (".*bellodat"r) "buohccestohpu" "buohcceviessu" "buhcciidsiida" "dearvvašvuođafitnodat" (".*departemeanta"r) "dearvvašvuođadutkanguovddáš" "diggi" (".*direktoráhta"r) "duopmostuollu" 
"eanajuohkindiggi" "eiseváldi" "Finnmárkokommišuvdna" "firbmá" "fitnodat" "fylka" "fylkkagielda" 
"geahččo#doaibma" "geahččoeiseváldi" "gelbbolašvuođaguovddáš" "giddagas" "gieldda#diggi" "giellaguovddáš" "giellakantuvra" "giellaráđđi" "girjerájus" "girku" "girkoráđđi" "gozihankomitea" "guovddášeiseváldi" "guovllubuohcceviessu" 
"hálddašanorgána" "hállehus" "dearvvašvuođaguovddáš" "fylkkadiggi" "institušuvdna" "justiskomitea" "klinihkka" "komitea" "kommišuvdna" (".*lávdegoddi"r)  (".*listu"r)  "mánáidgárdi" "media" "meieriija" "militeara" "musea" 
"nationála#diggi" "oahpahusdoaibma" "oahpahuskantuvra" (".*organisašuvdna"r) "orgána" "parlameanta" "psykiatriijabuohcceviessu" "preassa" "ráđđehus" (".*ráđđi"r) "Sámediggi" "sámediggi" "sámeinstitušuvdna" "sámekomitea" "sámemišuvdna" "sámemusea" "sámesiida" "searvegoddi" (".*searvi"r) "servodat" "sierraduopmostuollu" (".*skuvla"r) "skuvlakantuvra" "stáhta" "stuorradiggi" "suodjalankomitea" "suohkan" (".*stivra"r) "synagoga" 
"universiteahtta" "universitehta" "universitehtabuohcceviessu"  
"dearvvašvuođastašuvdna" "gielda" "riddogielda" "ruovttugielda" "sámegielda" "vuotnagielda" ;

LIST DEPARTMENT =  "doaibmaguovddáš" "fakultehta" "etáhtta" (".*guovddáš"r)  "hálddahus" "instituhtta" "kantuvra" "giellakantuvra" "mearridanorgána"  (".*ovttadat"r) (".*ossodat"r) ;
 
SET ORGANIZATION = DEPARTMENT OR MAIN-ORGANIZATION OR Sem/Org ; #Not sure about Sem/Org
 
#!! * Sets that can both be buildings/places and represent humans

LIST INSTITUTION =  "guovddáš" "instituhtta" "kulturráđđi" "searvegodderáđđi" ;

# the previous items can both represent abstract processes and humans
 
LIST MEDIA = "media" "oahpahus#rádio" "rádio" "televišuvdna" "TV" ;

#SYMB-INSTITUTION = "njuolggadus" "vuoigatvuohta" "láhka" "ortnet" "čoahkkinortnet" ;

    # Those cannot be buildings, but 'act' , 'say' things for example..

# Two aspects: Place and institution:
# and a couple of other words that do not really have to do with that
# suits "vázzit" + Acc
LIST EDUCATION-1 = (".*skuvla"r) (".*gursa"r) (".*kursa"r) (".*oahppu"r) "badjedássi" "báhpaskuvla" "boazodoallofága" "boazodoallosuorgi" "dásseárvofága" "fága"
"gaskadássi" "giellafága" "luohkká" "luonddufága" "musihkkadiibmu" "musihkkasuorgi" "oahppofálaldat" "oahppoinstitušuvdna" "oahppoásahus" "oahppodássi" 
"sámegielfága" "sámegiel#gaskafága" "seminára" "skuvla" "stáhtainternáhtta" "stáhtainternáhta" "stuorraskuvla" "universiteahtta" "universitehta" "vuolledássi" Sem/Edu ;

SET EDUCATION = EDUCATION-1 - ("fálaldat") ;

# suits to LANGUAGE Gen
LIST LESSON =  "álgokursa" "dáiddaoahpahus" "gaskafága" "gealbu" "gelbbolašvuohta" "giellaovdáneapmi" "fálaldat" "joatkka#fáddá" "joatkkakursa" "lávdaoahppofálaldat" "lohkanbajioahppu" "máhttu" "musihkkadiibmu" "oahppa" "oahppofálaldat" "oahpahus" "váldofága" "vuođđofága" ;
 

LIST OTHER-INDUSTRY = (".*ealáhus"r) "biedjo#bivdu" "bivdu" "boazodoallu" "Deanubivdu" "duodji" "ealáhusdoaibma" "eanandoallu" "fuođđobivdu" 
"geasse#guollebivdu" "giđđa#guollebivdu" "goddebivdu" "guollebivdu" "industriija" "johka#luossabivdu" "lassiealáhus" "luossabivdu" "oapmedoallu" "riektepolitihkka" "rievssatbivdu" "ruvkedoaibma" 
"sáivaguollebivdu" "sáivabivdu" "sosiálapolitihkka" "šákša#dorske#bivdu" "sámeduodji" "smávva#bivdu" "šibitdoallu" ;

# Not related to certain political verbs
 # Duomma:lea

LIST POLITICAL-INDUSTRY = (".*politihkka"r) ;
 # Related to certain political verbs
 # Duomma:lea
 
SET INDUSTRY = POLITICAL-INDUSTRY OR OTHER-INDUSTRY ;

LIST RIEKTEDILLI = "bajithearrávuohta" "bálvalusfálaldat" "čearddalašvuohta" "diggegirji" "dilálašvuohta" "ealáhusvuođđu" "eanajuohkinsuohkan" "eanadoallobuvttadeapmi" "eanavuovdinkantuvra" "hálddašeapmi" "kultuvra" "matrihkal" "meahcceriggodat" "opmodat" "rádjemearka" "rádji" "rádjá" "resursageavaheapmi" "riektedilli" "riekteáddejupmi" "riddomearraguovlu" "sierraortnet" "stáhtaeana" "suodjalanguovlu" "váldofierpmádat" "vearronjuolggadus" "vuoigatvuođadilálašvuohta" (".*vuoigatvuohta"r) "vuovde#atnu" "vuovde#njuolggadus" "suverenitehta" "kulturdoarjja" ;

# FYLKA in front of the set is usually in Gen.

LIST STAHTA-GEN = "IT-doaibmaplána" "almennet" "bargiidgiehtagirji" "boazodoallohálddahus" "bálvalus" "dearvvašvuođageahčču" "dearvvašvuođaiskkadeapmi" "dearvvašvuođa#gozáhus" "doaibma" "doaibmaplána" "doarjja" "doarjjavuogádat" "dássi" "eaiggáduššanvuoigatvuohta" "eaiggátgáibádus" "eana" "eiseváldi" "gaskadilleortnetdoarjja" "geahččoortnet" "geatnegasvuohta" "geavat" "gáibádus" "háldu" "joatkkaskuvla" "kulturráđđi" "mearrabodni" "oahppodoaibma" "oaidnu" "oaivil" "orgána" "ovddasteaddji" "peršonalgiehtagirji" "ruhta" "sosiálapolitihkka" "spesialistadearvvašvuođabálvalus" "tariffašiehtadus" "vuostebealli" "áddejupmi" "ámmátolmmoš" "ásahus" ("jávri" Pl) "oassálastin" ("vuovdi" Pl) "áŋgiruššan" ;
 # "stáhta" in front of the set, is usually Gen.

LIST SAMEDIGGI-GEN = "autoritehta" "áirras" "árvokommišuvdna#lahttu" "ásaheapmi" "bargu" "bušeahtta" "bušeahttamearrádus" "bušeahttavuogádat" "čielggadeapmi" "čilgehus" "čoahkkin" "čoahkkinortnet" "dievasčoahkkin" "doaibma" "doaimmahat" "ealáhusfoanda" "ekonomiijahálddašeapmi" "ekonomiijastivret" "hálddašanapparáhtta" "jahkedieđáhus" "joavku" "kulturviesso#bušeahtta" "lahttu" "mearrádus" "mihttu" "njuolggadus" "nuoraidkonferánsa" "oaidnu" "oassálastin"  "ollesáigepolitihkkár" "opposišuvdna" "ovttasbargoaktøra" "politihkar" "rehketdoallu" "sadji" "ságajođiheaddji" "šiehtadusmálle" "ulbmil" "váldoášši" "váldohálddahus" ;

SET ACTOR = ANIMATE OR INSTITUTION OR ORGANIZATION ;

  # Should EVENT be part of this set?

# To be used in habitative-sentences:
SET HAB-ACTOR = ACTOR OR HUMAN-GROUP OR EDUCATION OR Coll OR ("gii") OR Pers OR ("eará") OR ("iežá") OR ("buohkat") OR ("gaikkat") OR ("juohkehaš") OR ("goappašagat") OR ("soames") OR ("okta" Indef) OR Sem/Hum - SAPMI ;

SET HAB-ACTOR-NOT-HUMAN =  INSTITUTION OR ORGANIZATION OR INDUSTRY OR EDUCATION ;


LIST STATE = "bahávuohta" "dearvvasvuohta" "gaskavuohta" "mielladearvvašvuohta" "ráfi" "váivi" "veahkki" Sem/State;

  
LIST OTHER-ABSTR-ENTITY = "ášši" (".*prošeakta"r) (".*plána"r) "hehttehus" "váttisvuohta" ;

  # can represent a concrete entity

LIST MONEY = "ánsu" "árvu" (".*bálká"r) (".*bušeahtta"r) (".*buhtadus"r) "dienas" "divat" (".*doarjja"r)
(".*gollu"r) "haddi" "juolludeapmi" "juolludus" ("liigudit" Der/NomAct) "máksu" 
 "reantu" (".*ruhta"r) "sáhkku" "sisaboahtu" "tienas" "vealgi" "vearru" "vuoitu" Sem/Money ;

    # Ambiguous with respect to concreteness, either the symbolic amount or the concrete coins and bills

SET ABSTR-ENTITY = MONEY OR STATE OR OTHER-ABSTR-ENTITY ;

LIST MEASUREADJS = "jahkásaš" "kruvdnosaš"  "ruvdnosaš" ; # more adjs denoting measure in here, blocking Num + Gen (Ožžon 10 ruvdnnosaš buhtadusa)

 
LIST GEN-CONCEPT = "anistupmi" "álgu" "áŋgirvuohta" "áigumuš" "árbevierru" "ávžžuhus" 
"ballu" "bálvaleapmi" "bálvaluskvaliteahtta" "bargomálle" "boađus" "dárbu" "dássi" "doaibma" "duođaštangeatnegasvuohta" "duogáš" "eaiggátvuohta" "eallinvuohki" "erohus" "evttohus" 
"fádda" "friddjavuohta" "fuolalašvuohta" "gaskamearri" "gaskaoapmi" "geargatvuohta" "geatnegasvuohta" "gelbbolašvuohta" "gudnejahttin" "gudni" "hállu" "hástalus" "isitvuohta"
"konflikta" "kontákta" "kulturduogáš" "kulturmáhttu" "kvaliteahtta" "loahppa" "loatnaetymologiija" "lohpi" 
"máhttu" "máŋggabealatvuohta" "miella" "mihttomearri" "molssaeaktu" "muitu" "oamedovdu" "oktavuohta" "olahahttivuohta" "ovdamearka" "ovddasvástádus" "ovttadássásašvuohta" "perspektiiva"
"ráffi" "ráfi" "ráhkisvuohta" "rámma" "riekteprinsihppa" "rolla" "sámegielmáhttu" "sámekultuvra" "sihkkarastin#strategiija" "strategiija" "šiehtadallanfáddá" "symbola#systema" 
"ulbmil" "ustitvuohta" "vanhurskkisvuohta" "váibmu" "váikkuhanfápmu" "váikkuhus" "váldosivva" "vásáhusduogáš" "vearrivuohta" "virgelohpi" "vuordámuš" (".*vuoigatvuohta"r) ;
 
  # very abstract
  # Adding words to this set, consider if they suit to the SG-WORD-set as well. 
  
    
LIST EMOTION = "gierisvuohta" "moraš" "ráhkisvuohta" "vašši" Sem/Perc-emo ;
   
LIST STRUCTURE = "álgu" "loahppa" "struktuvra" "maŋŋegeahči" "lasáhus" "loahppageahči" "álgogeahči" "álgooassi" "sisdoallu" "veršuvdna" ;

LIST PROPERTY = "burrodat" "fastivuohta" "hedjodat" "allodat" "gassodat" "kvaliteahtta" "sturrodat" "guhkkodat" "govdodat" "hápmi" "ivdni" "fearga" "dovdomearka" "iešvuohta" "viidodat" ;

LIST IDEOLOGY = "buddhisma" "geahččanguovlu" "kommunisma" "oaidninčiehka" "osku" "sosial#darwinisma" ;

SET CONCEPT = GEN-CONCEPT OR STRUCTURE OR PROPERTY OR IDEOLOGY ;

  
LIST ACTIVITY = "aktivitehta" (".*dutkamuš"r) "čađaheapmi" "dearvvašvuođadutkan" "fuolaheapmi" "geahčastat" "guorahallan" "heahtebargu"  (".*lodden"r) "lodden" "mátki" "návetbargu" "sisafárren" "šiehtadallan" ;

  # semantically comparable to Actio forms + lexicalized Der/NomAct forms.
  # probably change of state in the course of time
  
LIST PROCESS = (".*proseassa"r) ;

    # Reminds of time sets
    
LIST TOPIC = "areálaášši" "áideášši" "boazodoalloášši" (".*-#ášši"r) "buhtadusášši" "bušeahttagáržžidanášši"
"čázádathuksen#ášši" "čiekŋudanfáddá" "čoahkkin#ášši" "diggeášši" "doarjja#juohkin#ášši"
"eallinášši" "eaŋkalášši" "eanaoastinášši" 
"giellaášši" "girkoášši" "gudnerihkkun#ášši" "guolle#ášši" "guovlogáhttenášši" "hálddašanášši" "huksenášši" "individuála#ášši" "juogadanášši" "konsešuvdnaášši"
"lassiášši" "luonddugáhttenášši" "meroštan#ášši" "muddenášši" "namma#logahallan#ášši" "nággoášši" "rádjaášši" "ráfáiduhttinášši" "rávdnje#huksenášši" "riidoášši" 
"sámeášši" "sisbuksa#ášši" "suohkanstivrraášši" 
"plánaášši" "riekteášši"  
"sámeášši" "suopman#ášši" "váiddaášši" "váidinášši" "váldoášši" "vearroduottarášši" "vuoigatvuođaášši" ;

SET ABSTRACT = ABSTR-ENTITY OR CONCEPT OR ACTIVITY ;
 
  
LIST DILLI = (".*dilli"r) ;

  # Likely to be locative in most contexts
  
LIST WEATHER-SITUATION = "beaivvádat" "bievla" "buolaš" (".*dálki"r)  "fiertu" "goalki" "siivu" "jođádat" "liekkas" "linádat" "oppas" "riđđu" "muohtti" "ilbmi" "balva" "čuoika" "čoaskin" "biegga" "nuorttadat" "davádat" "oarjjádat" "luládat" "orjješbiegga" "nuortabiegga" "obbadálki" "balvadálki" "balva#ilbmi" "arvi" "savda" "savda#arvi" "liehmu" "láfubiegga" "baján" "johtti" "gállu" "jiella" "hieibma" "šlahtti" "seaŋáš" "borga" "guoldu" "dobadat" "dulvi" "moarri" "geardni" "cuoŋu" "jealahas" "jalahas" "geasádat" "dálvvádat" "jáldu" "lievhnu" "galmma" "guostta" "čođđolat" "čođđálat" "sealádat" "šearádat" "bálgu" "bálggádat" Sem/Wthr ;

SET SITUATION = DILLI OR WEATHER-SITUATION ;

SET ACTOR-ROLE = HUMAN OR ORGANIZATION OR POLITICAL-PLACE OR EDUCATION OR Sem/Org ;
 # Institution: Restricting possible actor subjects.

LIST EVENT-TOOL-ACTIVITY = Der/NomAct "addin" "dearvvašvuođaiskkadeapmi" "goddin" "hárjehallan" "jápmin" "ráđđádallan" "šiehtadallan" "vázzin" ;

LIST EVENT = (".*čoahkkin"r) "čoakkalmas" (".*márkan"r) "boazodoallošiehtadallan" "cup" "čuoigancup" "deaivvadeapmi" "doallu" "feasta" (".*festivála"r) 
"heajat" (".*gilvu"r) "konferánsa" "konsearta" "kursa" "máilmmisoahti" "mátki" "mearridanproseassa" "proseassa" 
"ruhkos" "soahti" (".*válga"r) "vuosttaščájálmas" Sem/Event ;
 #XXX bad name, too general

SET ABSTR-PLACE = ORGANIZATION OR INSTITUTION OR EVENT OR EDUCATION OR INDUSTRY OR SITUATION ;

LIST ILLNESS = "albmasuoigi" "allergiija" "anemiija" "báhti" "bátnevárka" "bátnevearka" "bárku" "bearri" "boahkku" "buohcu" "buohcuvuohta" "buollán" "buozalmas" "čoavjebávččas" "čoavjebonjahat" (".*dávda"r) "eksema" (".*feber"r) "gárrenávnnas" "gárrenávnnasváttisvuohta" "gárrendilli" "influeansa" "juhkanvuohta" "leasmi" "mánodávddat" "nuorvu" "oaivvesvuohta" "ráŋka" "ruodnu" "skihpa" "suohtta" "varrasonahat" (".*váddu"r) (".*vihki"r) (".*vuolši"r) "vuorri" Sem/State-sick ;

# LIST ANIMAL-ILLNESS = "luossadávda" "vuorri" ;


LIST MIELLA = "áigi" "áigumuš" "árbevierru" "bággu" "beroštupmi" "dárbu" "diehtu" "dilli"  "doarjja" "ealjárvuohta" "fápmu" (".*gelbbolašvuohta"r) "geatnegahtton" "geatnegasvuohta" "gudni" "hállu" "hárjáneapmi" (".*lohpi"r) "miella" "mihttu" "niehku" "ovddasvástádus" "plána" "resursa" "riekti" "searat" "sávaldat" "váldi" "vejolašvuohta" "vuođđu" "vuohki" "vuoigatvuohta" "vuorru" ;
# These give Inf @N< instead of @ADVL (like more concrete nouns: ruhta, bileahta, dálki ...)


#!! * Sets denoting relations

LIST OASSI = "gonagasoassi" "riikkaoassi" "oassi" "oasáš" "teakstaoassi" "váldooassi" ;
 
	# Might be useful for partitive constructions.	
	# Try to make a set of nouns wich not will be Gen in a AccGen string.
	
LIST HEADOFPARTS = "eanetlohku" "unnitlohku" "mearri" "nissonoassi" "oassi" "váldooassi" "bealli" "guhtemuš" "goappašat" "proseanta" "%" Sem/Part ;

  # This may be a better partitive set.	

#!! ### Miscellaneous sets
# ------------------
LIST Px = PxSg1 PxSg2 PxSg3 PxDu1 PxDu2 PxDu3 PxPl1 PxPl2 PxPl3 ;

SET N-NOPX = N-NOPXLIST OR LAHKA OR EDUCATION ;

LIST GASKAL = "gaskal" "gaskkal" "gaskii" "gaskka" "gaskkas" ;
 
 # p-positions that like coordination
 
LIST TIME-PP = "badjel" "čađa" "earret" "gaskkal" "guovddáš" "maŋŋel" "maŋŋá" "miehtá" "ovdal" ;
 

SET NUM = Num - OKTA ;

# this set does not contain ordinals, I am not sure if that is necessary.

SET NOT-NUM = WORD - Num ;

LIST MANGA = "máŋga" "galle" ;
 # Not referred to by any rule.

SET CARDINALS = Num - Ord - MANGA ;

SET NOT-CC = WORD - CC ;

SET NOT-PCLE = WORD - Pcle ;

LIST COMPAR = ("dávji" A Comp) "eambbo" "eanet" "earálágan" "eará" "iežá" "earret" "seammás" "seammalágan" "seamma láhkái" "seamma_láhkái" ;

LIST EAMBBO = "eambbo" "eanet" "eambo" "eambu" ;


#Sammallahti/Nickel sátnegirjji mielde (kommer mer)
# eambbo, eambo/eambu: 
# indef: mehr mehrere
# adv: eher, eher listig als klug
#      eambo ahte/ja eambo, 
#      eambo go dabalaččat
#
# 
 # These combine with "go" Pcle, but COMPAR stands for 'comparison' rather than
 # 'comparative'.

LIST CONTRA = "muhto" "muhte" ;
 # In lean muitalan, muhto dál muitalan.

LIST PROSEANTA = "proseanta" "%" ;

SET REAL-CLB = CLB - COMMA ;


SET NOT-INITIAL-CC = WORD - INITIAL - CC ;

#!! ### Border sets and their complements
# ---------------------------------

SET CP = (Pron Interr) OR (Pron Rel) OR MO ;

LIST BOUNDARYSYMBOLS =  "\;" ":" "-" "–" ;

# This set if for disambiguating rules after mapping @CVP and @CNP to all CS and CC readings, therefore we cannot have @CVP in the set
SET S-BOUNDARY-NOT-CVP = CP OR BOUNDARYSYMBOLS OR CONTRA OR ("de" Adv) OR (Neg Sup) ;
	
SET S-BOUNDARY = CP OR BOUNDARYSYMBOLS OR CONTRA OR ("de" Adv) OR (Neg Sup) OR (@CVP) OR ("vel" Adv Qst) ;  
	# does not include CS, because of "go" in questions, before it is disambugated.
	# includes CP
	# this one includes @CVP, the conjunction which actually connects two sentences (each with a finite verb) to each other, 
	# and not @CNP, which coordinates internal NP-/AdvP-/AP ... coordination
	# To be used only AFTER the disambiguation of @CVP and @CNP taking place in the chapter right before "Disambiguating pronouns"


SET BOC = S-BOUNDARY OR BOS ;
SET BOC-PUNCT = BOC - ("-") - ("–") ; 
SET EOC = S-BOUNDARY OR EOS ;

SET NP-BOUNDARY = BOS OR EOS OR REAL-CLB OR VFIN OR Inf OR (Actio Ess) OR ConNeg OR VGen OR Sup OR PPRON-NOT-GEN OR Recipr OR Po OR Pr OR Pcle OR ("jed") OR Interj OR CS OR CP OR @CVP ;

SET APP-BOUNDARY = REAL-CLB OR VFIN OR Inf OR (Actio Ess) OR ConNeg OR VGen OR Sup OR Recipr OR Po OR Pr OR Pcle OR Interj OR CS OR CP ;
# A special barrier used with mapping of appositions. 

# This set contains FMAINV with @, which means that it functions for all kind of mainverbs after the verb-mapping rules 
SET SV-BOUNDARY = S-BOUNDARY OR Inf - AUX OR Sup OR FMAINV ;
 # VFIN-NOT-AUX ;
 # should be MAIN-V linked to VFIN-aux to the left. (cg-3)
# This set is ment to use in rules for disambiguating due to verbs or verbsets. It contents @.

SET SVF-BOUNDARY = S-BOUNDARY OR VFIN ;

# This set is ment to use in rules for disambiguating due to verbs or verbsets. 
# Here we search for either an S-BOUNDARY or a finite verb, either aux or main.

SET CCCOMMA = CC OR COMMA ;
	# remember that those are potential sentence boundaries, too


#!! ### Syntactic sets
# --------------

LIST ALLSYNTAG = (@+FAUXV) (@+FMAINV) (@-FAUXV) (@-FMAINV) (@-FSUBJ>)(@-FOBJ)(@-F<ADVL)(@-FADVL>)(@>A) (@>ADVL) (@ADVL<) (@<ADVL) (@ADVL>) (@ADVL)  (@>N) (@APP) (@APP-N<) (@APP-Pron<) (@APP>Pron) (@APP-Num<) (@APP-ADVL<) (@VOC) (@CNP) (@CVP) (@P<) (@>P) (@HNOUN) (@INTERJ) (@Pron<) (@OBJ) (@OBJ>) (@<OBJ) (@OPRED) (@<OPRED) (@OPRED>) (@SPRED<OBJ) (@PCLE) (@COMP-CS<) (@N<) (@SPRED) (@<SPRED) (@SPRED>) (@PPRED) (@Num<) (@SUBJ) (@<SUBJ) (@SUBJ>) (@X); #!! ALLSYNTAG
SET NON-APP = ALLSYNTAG - (@APP); #!! NON-APP

#!! *These were the set types.*


BEFORE-SECTIONS

REMOVE CLBfinal ;

#!! ## Guessing: Rule for adding Sem/Date as a tag to readings which looks like dates
#SUBSTITUTE:Date1 (Num Arab Sg) (Num Sem/Date Sg) TARGET ("<[0-3][0-9][.][0-2][0-9][.][1-2][0-9][0-9][0-9]>"r) + Num ;
#SUBSTITUTE:Date1 (Num Arab Sg) (Num Sem/Date Sg) TARGET ("<[0-9][.][0-9][.][1-2][0-9][0-9][0-9]>"r) + Num ;
#SUBSTITUTE:Date1 (Num Arab Sg) (Num Sem/Date Sg) TARGET ("<[0-3][0-9][.][0-2][0-9][.][0-9][0-9]>"r) + Num ;
#SUBSTITUTE:Date1 (Num Arab Sg) (Num Sem/Year Sg) TARGET ("<[1-2][0-9][0-9][0-9]>"r) + Num ;
#SUBSTITUTE:Date1 (Num Arab Sg) (Num Sem/Year Sg) TARGET ("<[1-2][0-9][0-9][0-9][-][0-9][0-9]>"r) + Num ;

SELECT:SemYear Sem/Date (0 Num) ;
SELECT:SemYear Sem/Year (0 Num)(NOT -1 CURRENCY-ABBR)(NOT 1 CURRENCY OR MEASURE OR ("jahki")) ;
	#% Bures boahtin 2016 beaivái
SELECT:SemYear Sem/Year (0 Num)(NOT -1 CURRENCY-ABBR)(1 Po) ;
SELECT:SemYear Sem/Year (0 Num)(1 ("jahki") + Pl) ; #apertium
REMOVE:SemYear Sem/Year (0 Num) ((-1 CURRENCY-ABBR) OR (1 CURRENCY OR MEASURE)) ;
REMOVE:SemPhonenr Sem/Phonenr (0 Num) ; #vi fjerner alle inntil videre

SELECT:Sem/DateGen  Sem/Date + Gen ((1 Po) OR (-1 Pr)) ; #foreløpig, for MT 
SELECT:Sem/Date  Sem/Date + Nom ; #foreløpig, for MT 
SELECT:clock Sem/Time-clock + Nom  ;

REMOVE:alphabet (".*alphabet"r) (NEGATE 0 (abbr)) ; #apertium

#!! ## Guessing: Rule for adding Adv Sem/Adr as a tag to readings which looks addresses

#SUBSTITUTE (?inf) Adv + Sem/Adr TARGET ("<www[.].*>"r ?) OR ("http.*"r ?) OR ("<[A-Za-z][a-z].*[.][a-z][a-z].*"r ?) OR ("<[A-Za-z].*[@][a-z].*"r ?) OR ("<[A-Za-z].*[(][a][t][)][a-z].*"r ?) ; #for hfst, fungerer ikke
#
#SUBSTITUTE (?) Adv + Sem/Adr TARGET ("<www[.].*>"r ?) OR ("http.*"r ?) OR ("<[A-Za-z][a-z].*[.][a-z][a-z].*"r ?) OR ("<[A-Za-z].*[@][a-z].*"r ?) OR ("<[A-Za-z].*[(][a][t][)][a-z].*"r ?) ;
#COPY:NNom (N Nom) EXCEPT Adv TARGET Sem/Adr  ;
#	## www.samediggi.fi http://yle.fi/sapmi lene62@gmail.com anja.irina.ahola(at)gmail.com

SELECT:ArabNom (Arab Nom) (-1 BOS)(NEGATE 1 Po)(NEGATE 1 Sem/Time LINK 1 ("áigi") + Adv)(NOT 0 Prop OR Sem/ID) ;
SELECT:Arab1 (Arab Nom)(0 ("1")) ;
	#$ a) Álgogursa 1 sámegielas
#SELECT:ArabNomPlc (Arab Nom) (-1 Sem/Plc)(NEGATE 1 Po)(NOT 0 Prop) ;

SELECT:Sem/ID Sem/ID IF (-1 ("§")) ; 
REMOVE:Sem/ID Sem/ID IF (1 CURRENCY) ; 

SELECT:Arab Arab IF (NOT 0 Ord OR Prop OR Arab + Err/Orth) ; # In Apertium we get analysis both with and without the tag.

REMOVE:diNom Coll (-1 ("dii") OR ("diibmu")) ;


REMOVE:dyn  Arab (0 Prop) ; # foreløpig løsning, til vi har ny løsning for numerals.lexc

#!! * Rule for adding <vdic> to verbs denoting verbal actions like: ... dadjá Aili Kestkitalo.


ADD:muitalit <vdic> TARGET MUITALIT (0 REAL-V LINK -1 COMMA) ((*1 HUMAN-INDIVIDUAL + Nom OR Sem/Org + Nom OR PROFESSION + Nom OR Prop + Sem/Plc + Nom BARRIER SV-BOUNDARY) OR (1 Gen LINK 1 HUMAN-INDIVIDUAL + Nom OR Sem/Org + Nom OR PROFESSION + Nom) OR (1 Gen LINK 1 Gen LINK 1 HUMAN-INDIVIDUAL + Nom) OR (1 Attr LINK 1 HUMAN-INDIVIDUAL + Nom OR PROFESSION + Nom OR Prop + Sem/Plc + Nom)) ; # BARRIER NOT-A LINK *1 EOC BARRIER WORD - Prop) ;
	#$ Dál fertejit stuoridit, muitala beaivválaš jođiheaddji Eva Nordfjell.
	#$ Sii leat áŋgiruššan fágalaš sisdoaluiguin, dadjá Sámi Allaskuvlla rektor Gunvor Guttorm.
	#$ Mun jáhkán dat šaddá áibbas buorre, dadjá Deanu Mohtorsearvvi jođiheaddji Morten Blien loahpas.
	#$ Muhto eai buohkat gal čađahan, dadjá sihkkelgilvvu, jođiheaddji Marianne Balto.
	
ADD:muitalit <vdic> TARGET MUITALIT (0 REAL-V LINK 1 EOC) (-1 HUMAN-INDIVIDUAL + Nom OR PROFESSION + Nom OR Prop + Sem/Plc + Nom LINK *-1 COMMA BARRIER REAL-V OR S-BOUNDARY) ; 
	#$ Mii sávvat sutnje bures boahtin, rektor Gunvor Guttorm cealká.

#SUBSTITUTE:muitalit (V) (V <vdic>) TARGET MUITALIT (0 REAL-V LINK -1 COMMA LINK *2 HUMAN-INDIVIDUAL + Nom OR Prop + Sem/Plc + Nom BARRIER NOT-A LINK *1 EOC BARRIER WORD - Prop) ;
	#$ Sápmelaččat eai leat goassege luohpan meara riggodagain, ja danne ii sáhte eahpidit sápmelaččaid vuoigatvuođaid mearrariggodagaide, deattuha Keskitalo.
	#$ – Ii leat duohta , lohká direktora Smuk.
	#$ Mii fertet maiddái geahčadit makkár lassi guoimmuhemiid galgat fállat vai bohtet vel eanet olbmot boahtte jagi, loahpaha duhtavaš beaivválaš jođiheaddji Eriksen.
	#$ Dat čájeha ahte sámi guovllut leat geasuheaddjit ealáhusovdáneapmái, dadjá sámediggeráđđi Silje Karine Muotka.

ADD:sme (<sme>) TARGET WORD ;
ADD:sme (<sme>) TARGET PUNCT ;

#!! ###  Removing or selecting proper nouns that are lookalikes

#!! * **AvvilProp** selects Prop for Avvil
SELECT:AvvilProp Prop IF (0 ("Avvil")) ;
	#$ Avvila dulvesuodjalan áššedovdibargojoavku.


#!! * **SamediggiProp** selects Prop after Ášši 01/12
SELECT:SamediggiProp Prop IF (-2 ("ášši"))(-1 Num);
	#$ Ášši 02/13 Sámedikki čoahkkinortnet.
	
REMOVE:finnmárku ("finnmárku") IF (0 ("Finnmárku")) ;	
REMOVE:Johan ("Jo") IF (0 ("Johan")) ;	

REMOVE:Állet ("Állet") IF (-1 BOS) ;	

REMOVE:JA ACR (0 ("ja") OR ("go") OR ("illu")) ;	
REMOVE:og CC (0 ("OG")) ;
REMOVE:Gusto Prop (0 ("Gusto") LINK 0 Adv OR VFIN) ;
REMOVE:Gusto Prop (0 ("Áigi") OR ("healbat") OR ("Davvin")) ;

#ADD:subqst (<subqst>) TARGET V ; # IF (0 @+FMAINV OR @+FAUXV LINK -1 @+FMAINV OR @-FMAINV) ;

REMOVE:PropACR (Dyn) IF (0 Prop OR Adv) ;	
REMOVE:Prop Prop IF (0 ("Ai") LINK 0 Adv) ;	
REMOVE:Prop Prop IF (0 ("Alm") LINK 0 ("albmi")) ;	


#!! we don't want propernoun analysis of these words, initially in sentences
LIST NOT-PROP = "Ai" "Ain" "Áigi" "Alm" "Árran" "Beaska" "Bierdna" "Vuoksá" "Áddjá" "Áigi" "Álgu" "Barta" "Čeabet" "Daa" "Dalan" "Davvin" "Dávgi" "Dieđut" "Dronnet" "Duodji" "Duolba" "Ello" "Ellos" "Fiervá" "Fiske" "Forum" "Gáldu" "Gahpir" "Galle" "Gaskaija" "Gámas" "Geassebáiki" "Giđđa" "Gieddi" "Gielas" "Gielda" "Girjjis" "Guksi" "Gusto" "Gulli" "Guoika" "Guovssahas" "Hel" "Heasta" "In" "Internáhtta" "Jávregáddi" "Jávri" "Jo" "Johka" "Just" "Háhtta" "Li" "Loss" "Man" "Márkan" "Miestta" "Muvra" "Muitte mu" "Náhkki" "Návet" "Náhkki" "Njunni" "Nuora" "Ob" "Ohcan" "Oja" "Olle" "Ose" "Ovdal" "Profehta" "Ra" "Riidi" "Sámeskuvla" "Sápmelaš" "Sápmi" "Sieidi" "Sisa" ("Siv" Loc) ("Siva" Loc) "Skávžá" "Stállu" "Varis" "Vággi" "Váldi" "Visti" "Vuotna" "Vuoigŋa" ;

REMOVE:InitialSapmiNoun Prop IF (0 ("Sápmi") + Sg + Nom)(1 CC)(2 N + HUMAN OR N + Sem/Lang) ;
	#$ Sápmi ja dárru galgaba báldalagaid oččodit givvuid Romssa gávpogii.

SELECT:InitialÁlletNoun Neg IF (0 ("Állet") + Sg + Nom)(1 ConNeg) ;

REMOVE:InitialNeg Neg IF (0 Rom LINK -1 BOS)(NOT *1 ConNeg)(NOT 1 Indef) ;	#Apertium

REMOVE:Sokki Prop IF (0 ("soggi") + Loc LINK 1 N + Nom OR Pron + Nom) ;
	#$ Sokkis sii seailluhit diŋggaid, muhto go lávus leat ollu olbmot, de sáhttet soggái maid nohkkat.
	#% Sokki bággoheaittiheapmi maŋiduvvon.

#!! * **InitialSapmiProp** the initial Sápmi rule. 
SELECT:InitialSapmiProp Prop IF (0 ("Sápmi") + Sg + Nom LINK *-1 BOS OR (":") BARRIER WORD)(NOT 1 Prop + HUMAN);
SELECT:InitialSapmiProp Prop IF (0 ("Sápmi") + Sg + Loc LINK *-1 BOS OR (":") BARRIER WORD) ;
SELECT:InitialSapmiProp Prop IF (0 ("Sápmi") + Sg + Ill LINK *-1 BOS OR (":") BARRIER WORD) (1 Ess) ;
SELECT:InitialSapmiProp Prop IF (0 ("Sápmi") + Sg + Ill LINK *-1 BOS OR (":") BARRIER WORD) (1 ("galgat")) ;
#Comment: All initial Sápmi in Korp are Prop. In theory, they could be nouns as well, but we need authentic examples. 
#Lene: I added + Sg, but still this one is too strong, and needs some constraints, so I commented it out. There are several examples counterexamples in the goldcorpus.
#Trond: New try: demanding Nom or Loc, thereby getting correct goldcorpus. Especially Gen implies non-Prop.

SELECT:Girjjás ("Girjjás") + Gen (1 ("čearru")) ;

#!! * Rules for removing some Props which are identical to common nouns
REMOVE:Prop=noun Prop IF (0 NOT-PROP OR PROP-ATTR + Pl OR PROP-SUR + Pl LINK *-1 BOS OR (":") BARRIER WORD)(NOT 0 ("soggi"))(NEGATE 0 ("Girjjás") LINK 1 VFIN + Sg3) ;
# Comment: Min Áigi is Prop but Áigi in the beginning of the sentence is not. Unnit is not Unni+Pl


REMOVE:PropPl Prop + Pl IF (0 Sem/Mal OR Sem/Fem LINK 0 Sg); 


#!! *Removes **PropPl**, but problems with names as Davviriikkaid Ráđi, there we want Prop Pl
REMOVE:PropPl Prop + Pl IF (0 COMMON-N LINK NOT 0 Sem/Obj OR Sem/Org) (NEGATE 1 ("ráđđi"))(NEGATE 0 VGen); 
  	#$ Sápmelaččaide lea hui mávssolaš ahte sin dárbbut váldojit vuhtii. 
  	#% Davviriikkaid Ráđi - It should not hit this 
	

LIST NOT-SUR = "Kárášjohka" "Oslo" "Romsa" "Farmen" "Kenya" "Brasilia"  ;
SET NATION-NOT-SUR = NOT-SUR OR NATION OR CONCRETE-ROUTE ;

REMOVE:PlcSur Sem/Sur IF (0 NATION-NOT-SUR) ;
REMOVE:PlcSur SUB:1 Sem/Sur + NATION-NOT-SUR ;
REMOVE:PlcSurNoHyph SUB:1 Sem/Sur - Cmp/Hyph ;

REMOVE:ACRPropPlLoc Pl + Loc IF (0 Prop + ACR + Com) ;
	#$ Mii bargat NSR:in.

	
#!! *Select **PlcSur** (Sem/Plc) (Sem/Sur) 
SELECT:PlcSur0 Sem/Sur IF (0 Prop + Sem/Plc + Ill) (-1 Prop + Sem/Fem OR Prop + Sem/Mal)(*0 ("riŋget") OR ("čuojahit") BARRIER REAL-V OR S-BOUNDARY OR CC) ;
	#$ Sárá riŋgii Trond Trosterudii.
	#% Son riŋgii Kárášjohkii. 

SELECT:PlcSur1 Sem/Sur  IF (0 Sem/Plc LINK *-1 Sem/Fem OR Sem/Mal OR <vdic> BARRIER REAL-WORD-NOT-ABBR OR COMMA LINK NOT 0 Sem/Sur OR Sem/Plc)(NEGATE 0 Prop + Ill OR Prop + Loc LINK NEGATE *-1 Sem/Fem OR Sem/Mal BARRIER REAL-WORD-NOT-ABBR OR COMMA)(NEGATE 0 Prop + Ill OR Prop + Loc LINK *-1 Sem/Fem OR Sem/Mal LINK *-1 MOVEMENT-V BARRIER NOT-NPMODADV)(NEGATE 0 ("Ravna")) ;
  	#$ Linda England.
  	#$ Linda B. England.
  	#% De vuolgá Biret Trosterudii.
  	#% 1980:s manai Anny Hámmárfestii vázzit buohccedivššárskuvlla.


SELECT:PlcSur2 Sem/Sur IF (0 Prop + Sem/Plc + Com) (NEGATE 0 SAPMI OR SUOPMA)   ;
  	#$ Mun bohten Trosterudain.

SELECT:PlcSur3 Sem/Sur IF (0 Prop + Sem/Plc + Loc) (*0 ("jearrat") BARRIER Loc OR REAL-V OR S-BOUNDARY OR CC LINK NEGATE 0 ("jearahit")) ;
	#$ Mun jerren juoidá eksámena birra Trosterudas.
  	#$ Mun jerren juoidá eksámena birra Trosterudas Guovdageainnus.
  	#% Mátkkálaččat Norggas jearahit dákkár vásáhusaid.

SELECT:PlcSur4 Sem/Sur IF (0 Prop + Sem/Plc + Ill LINK NOT 0 Pron)(*0 VERBAL-ACTIVITY-V OR ACC-ILL-V BARRIER REAL-V OR S-BOUNDARY OR CC) ;
	#$ Atte reivve Trosterudii.
	
SELECT:PlcSur5 Sem/Sur IF (0 Prop + Sem/Plc + Gen) (1 ("lusa") OR ("luhtte") OR ("geahčai") OR ("geahčen"));
	#$ Biret vuolgá Trosterud lusa.
	#$ Biret lea Trosterud luhtte.
	#$ Biret vuolgá Trosterud geahčai.
	#$ Biret les Trosterud geahčen.

SELECT:PlcSur6 Sem/Sur IF (0 Prop + Sem/Plc) (-1 PROFESSION + Nom + Sg);


#!! ### Some propernouns have two parts and the first is not a genitive. We still have problems with abbr when these propernouns are inflected or are a part of a cmp. The copy rule adds Attr reading to names which not get it in the fst (Soria). The select rule selects Attr when the next word is e.g. Moria.


LIST PROP-FIRST = "Harry" "Soria" "Stuorra" "Hearrá"  ;
LIST PROP-SECOND = ("Moria.*"r) ("Potter.*"r) "Sarvvis" "Ipmil"  ;


#!! * **SoriaAttr**  Soria Attr Moria, Harry Attr Potter-girji
COPY:SoriaAttr Attr EXCEPT (Nom Gen Acc Sg) TARGET PROP-FIRST (NOT 0 Prop + Attr)(*1 PROP-SECOND BARRIER REAL-WORD-NOT-ABBR) ;

#!! * **SoriaMoria**
SELECT:SoriaMoria PROP-FIRST + Attr IF (1 PROP-SECOND) ;
	#$ Soria Moria-julggaštusas definerejuvvojedje davviguovllut Norgga deaŧaleamos nannensuorgin boahttevaš jagiid.

#!! ###  Rules for giving Attr to names, e.g. Ole Attr Kåven.



#!! * **PropAttr** 
#SUBSTITUTE:PropAttr (Sg Nom) (Attr) PROP-ATTR + Nom (NOT 0 Attr LINK *1 PROP-SUR BARRIER REAL-WORD-NOT-ABBR OR COMMA)(NEGATE 0 Sem/Sur LINK -1 Sem/Fem OR Sem/Mal LINK 2 Sem/Fem OR Sem/Mal LINK 1 Sem/Sur) ;
#SUBSTITUTE:Attr (Sg Nom) (Attr) PROP-ATTR + Nom (NEGATE -1 Prop)(1 JA/DAHJE LINK 1 Prop + Attr LINK *1 Prop + Sem/Sur) ;
#SUBSTITUTE:Attr (Prop Sem/Mal Sg Nom) (Prop Sem/Mal Attr) ("Hearrá") (1 ("Ipmil")) ;
	#$ Dá leaba Brita ja Ove Kåven.

#!! Remove unwanted analyses
REMOVE:divAttr Prop + Allegro + Attr OR Prop + Attr + Gen OR Prop + Attr + Acc OR Prop +  Pl + Attr ; # finnes det slike analyser?


SELECT:PropHyph  ("[A-Z].*-#.*"r) IF (0 Sem/Hum) ;
	#$ Máhtte-vilbealle

#!! ## Southern Locative vs. Essive

#!! * **SouthLoc** removes Southern Locative vs. Essive
REMOVE:SouthLoc (South) ;
#REMOVE:SouthLoc (N Sg Loc South) OR (A Sg Loc) IF (0 Ess)(NEGATE -1 CC LINK -1 Loc LINK NOT 0 Ess);
#REMOVE:SouthLoc (N Sg Loc South) OR (A Sg Loc) IF (0 Ess)(NEGATE 0 Actio LINK NOT 0 ESS-ADVL-N)(NEGATE -1 CC LINK -1 Loc LINK NOT 0 Ess);
 	#$ girkun # This is a rule that prevents southern dialectal forms to destroy the rest of the # parser. It should be commented out whenever analysing texts with Locative -n only. # All words with Cg will avoid this rule, since e.g. várin is southern Loc only.
	#$ Luossa-bivdin joddu-buođuin deanus.  


#!! * **Apertium-rule** we want Num as alternativ to Ord reading  
#COPY:Apertium (Num Arab Sg Nom addsent) EXCEPT (A Ord Attr) TARGET (ord) ;
#COPY:Apertium (Num Sem/Year Sg Nom) EXCEPT (A Ord Attr) TARGET (ord) ;
#REMOVE:TestNum (ord) IF (1 ("[a-z].*"r)) ;
#
#ADDCOHORT ("<.>" "." sent) AFTER (Num Arab Sg Nom addsent) ;

SELECT:seammax SEAMMAX ; # prefer mwe

SECTION

REMOVE:PropNom Acc (0 PROP-SUR + Prop + Nom)(NEGATE *0 REAL-V) ;


SELECT:min ABBR (0 ("min") LINK -1 Arab) ;
#REMOVE:abbr ABBR (NOT 0 Ord) ;

# rule for disamgibuating falli A (with Err/Orth) vs. fálli and fális N
REMOVE:falliA A (0 ("falli") LINK 0 ("fális") OR ("fálli")) ; 


#!! ## Numerals
#     -----------------------

SET NOT-ERR-NUM = Num - (Num Err/Orth) ;

REMOVE:numerrorth (Num Err/Orth) IF (0 NOT-ERR-NUM) ;

#!! * **NumRom** in beginning of sentence
SELECT:NumRom (Num Rom) (0 ACR OR ABBR LINK -1 BOS)(NOT 0 Sem/Obj-el) (NEGATE *1 ConNeg BARRIER Neg ) ;
	#$ I Áššebáhpirat
	#% CD máksá 160 ruvnnu.

#REMOVE:Rom Rom (0 Neg LINK *1 ConNeg OR NEG-ADV-PRON OR ("danin") BARRIER NOT-ADV-PCLE) ;

REMOVE:Rom Rom (0 Neg LINK NOT 1 PUNCT) ;


REMOVE:IINeg Neg (0 Rom LINK NEGATE *1 ConNeg BARRIER Neg)(NEGATE 0 Neg LINK -1 ("dahje") OR CONTRA OR ("vai")) ;

SELECT:Nom Nom (0 Arab LINK -1 (":")) ; #probl. apertium
SELECT:Nom Nom (0 Arab LINK 1 RIGHT) ;

	
SELECT:CD Sem/Obj-el (0 Rom) ;	

#!! ## Lexicalised derivations
#     -----------------------

#!! * **derVuohta** removes A Attr Der/vuota if A Der/vuota.	
REMOVE:derVuohtaAttr Attr IF (0 Der/vuota) ; 
REMOVE:vuohtan (".*#vuohtan"r) OR  (Err/Lex) IF (0 Der/vuota) ; 


#!! * **Focmat** removes Foc/mat when not Imprt  
REMOVE:Focmat Foc/mat IF (0 N OR Der/NomAct) ;
	#$ Veahá doarrumat, muhto muđuid oalle jaskes idja.

LIST DYNCMP = (".*#.*"r) ;

#!! * **eapmi** compounds with eapmi if they have Der/NomAct analysis
REMOVE:eapmi (".*#eapmi"r) (0 Der/NomAct) ;

REMOVE:cmpDer_laš DYNCMP + Der/lasj (0 Der/lasj) ;
REMOVE:cmpDer_laš DYNCMP + Der/NomAct (0 Der/NomAct) ;
	#$ Doppe lea diktatuvrralaš stivrenmálle.
	#$ .. ja álggahit ráŋggáštusaid jus ležžet lobihuššamat.

#!! * **derN** removes DER-N if lexicalised non-essives
REMOVE:derN DER-N IF (0 LEX-N LINK NEGATE 0 Ess OR ("dahkat"));
REMOVE:derN SUB:1 DER-N IF (0 LEX-N LINK NEGATE 0 Ess OR ("dahkat"));


REMOVE:derN DER-N IF (0 Der/NomAg LINK 0 (".*eaddji"r)) ; # A	
	#$ Dat lea oahpaheaddjin doppe.	
 # Since Der/NomAct was included in the DER-N set, this rule now removes
 # many Actio. This may be unfortunate. If so, the 
 # Der/NomAct is removed from the DER-N set.
 
REMOVE:semAct N + Der/NomAct IF (0 N + Sem/Act - N + Der/NomAct); 
REMOVE:semNomAg Der/NomAg IF (0 NomAg);

REMOVE:modasp-derNomAg Der/NomAg IF (0 MOD-ASP);

 
#!! * **derNEss** removes DER-N if lexicalised essives (revise this) - flytter denne til slutten av fila
#REMOVE:derNEss DER-N IF (0 DER-N LINK 0 Ess)(0 LEX-N LINK 0 Ess)(NEGATE 0 (NomAg Ess) OR (Der/NomAg Ess));
 	## Boarrásis leai dat vel eanaš ealáhussan ge.
 	# Removes DER-N if O is also a lexical noun, and both nouns are essive.	

#!! * **derA** removes DER-A if lexicalised A
REMOVE:derA DER-A IF (0 LEX-A);
 	## Dábálaš mearrádusat.


#!! * **derlasj** removes Der/lasj if lexicalised N
REMOVE:derlasj Der/lasj IF (0 Sem/Plc LINK 0 N) ;
	#$ sieiddálaš


LIST DER-A-ADV = (Der/lasj Der/AAdv Adv) ;
SET NOT-DER-A-ADV = Adv - DER-A-ADV ;

REMOVE:derAdv DER-A-ADV (0 NOT-DER-A-ADV) ;
 
#!! * **derV** removes DER-V if lexicalised V,
REMOVE:derV DER-V IF (0 LEX-V) ; 
REMOVE:TESTderV SUB:1 DER-V ; # IF (0 LEX-V OR Der/NomAct) ; 
REMOVE:A_derhuvva Der/huvva IF (0 (".*huvvat"r)) ; 
	#$ Son oaivvilda iežas bealjehuvvagoahtán.


LIST VDER-ONE = Der/h  Der/Caus Der/st Der/d Der/l ;

LIST VDER-TWO =  Der/alla Der/halla ; #OBS: se på denne!

LIST VDER-LAST = Der/NomAct Der/NomAg Der/InchL ;

#!! * **derHderAlla, derAlla, derH, derST** chosses longest Der/tag
REMOVE:DerDer1 VDER-ONE IF (0 VDER-TWO) ;
REMOVE:DerDer2 VDER-ONE OR VDER-TWO IF (0 VDER-LAST - VDER-ONE - VDER-TWO) ;
REMOVE:derPassderNomAct (Der/PassL Der/NomAct) IF (0 Der/Pass) ;  


REMOVE:derAlla Der/halla IF (0 Der/alla) ;  
REMOVE:derPass Der/Pass + Der/d ;  
	#% Prošeakta lea beroštahttán riikkalaččat ja guovdilis politihkárat leat jo fitnan galledeamen skuvlla oažžun dihtii eambbo dieđuid lážáldaga birra .

	
#!! * **derPassActio** removes Actio Nom/Gen/Acc for passive forms. I don't think they exist in Sg, we prefer the PrfPrc analysis.
REMOVE:derPassActio Der/Pass + Actio IF (0 PrfPrc) ;
REMOVE:derPassActio Der/Pass + Der/NomAct IF (0 PrfPrc) ;
	#$ Dalle galget miesit leat merkejuvvon eaiggáda merkii.
	
	
#!! ### Particular verbs

SELECT:orrut ("orrut") (1 (Actio Essive)) ;
	#$ Dat orro leamen nu ahte nissonolbmot váldet vuostá hástalusaid.

SELECT:mannan PrfPrc  (0 ("mannan") LINK 1 Sem/Time) ;
REMOVE:mannan N  (0 ("mannat") LINK 1 N) ; # too strong ?
	#$ Dalle šattašii liikka buoret dálki go mannan sotnabeaivve.

REMOVE VFIN (0 ("muhte") LINK -1 BOS OR COMMA) ; #muhte

LIST NOT-REAL-V-LEMMAS = "álbmotregistreret" ;

#!! * **notRealV** removes verb readings from verbs like álbmotregistreret
REMOVE:notRealV REAL-V (0 NOT-REAL-V-LEMMAS) ;

#!! * **notN** removes N for adjectives which have got noun analysis because of Px for Divvun 
LIST A-NOT-N = ("buorre" Der/Superl) "čeahppi" "ipmilbalolaš" "láhkásaš" "ovddeš" "rikkis" "vártnuheapme" ;
REMOVE:notN A-NOT-N + N (0 A) ;
REMOVE:notNbuorre N (0 ("buorre") LINK *-1 COPULAS BARRIER NOT-ADV-PCLE) ;
REMOVE:NAess N (0 A-LEX-AS-NOUN + Ess LINK *0 ("dovdat") OR ("doalahit") OR ("dahkat") BARRIER SV-BOUNDARY) ;

#!! * **leapmaDimin** removes it
REMOVE:leapmaDimin ("leapma" Der/Dimin) ;

#!! * **leage** removes leahki Allegro
REMOVE:leage ("leahki") + Gen (0 ("leat")) ;

#!! * **Divvun** 
SELECT:Divvun ("Divvun") (1 ("joavku")) ;

SELECT:nisu ("nisu") (0 ("nissu") LINK 0 Cmp) ;

SET VFIN-NOT-PASS = Ind - Der/Pass ;

LIST ALL-PERS = Sg1 Sg2 Sg3 Du1 Du2 DU3 Pl1 Pl2 Pl3 ;

#!! * **Der/PassS** removes some Pass-readings in favour of V not Pass
REMOVE:Der/PassS (Der/PassS) + $$ALL-PERS (0 ("guovlat") OR ("diehtit") OR (".*ut"r) + Der/Pass + $$ALL-PERS)(NOT 0 ("beahttut"))(0 VFIN-NOT-PASS) ;	
REMOVE:Der/PassS (Der/PassS) + PrfPrc (0 ("guovlat") OR ("diehtit") OR (".*ut"r) + Der/Pass + PrfPrc)(NOT 0 ("beahttut"))(0 VFIN-NOT-PASS) ;	
REMOVE:Der/PassS (Der/PassS) + ConNeg (0 ("guovlat") OR ("diehtit") OR (".*ut"r) + Der/Pass + ConNeg)(NOT 0 ("beahttut"))(0 VFIN-NOT-PASS) ;	
REMOVE:Der/PassS (Der/PassS) (0 ("oažžut")) ;	



REMOVE:beahttut  ("beahttut") (-1 ("garrasit")) ;

LIST NOT-PASS = ("oaktut" Der/PassS Sg3) ("guovlat" Der/PassS Sg3) (IV* Der/PassS)  ;

#!! * **notPass** removes som Pass readings which are not likely at all
REMOVE:notPass NOT-PASS ;
	#$ Dat gal lea maid okta fáktor muhtin guvlui, muhto gávdnojit maid eará.

LIST LEX-PASS =  "čadnot" "čirrot" "čoggot" "doddjot" "dovdot" "galbmot" "gávdnot" "gessot" "goddot" "gullot" "haksot" "láhppot" "oidnot" "rahppot" "savvot" "sorrot" "váldot"  ;

#!! * **LEX-PASS** removes passive forms of some lemmas in favour for the lexixalised one
REMOVE:LEX-PASS (Der/PassS) IF (0 LEX-PASS LINK NEGATE 0 ("geassit")) ; # geassit vs gessot

#!! * **LEX-PASSPrfPrc** selects PrfPrc when noun to the right
SELECT:LEX-PASSPrfPrc PrfPrc IF (0 LEX-PASS LINK 1 N)(NEGATE *-1 Neg BARRIER REAL-V) ;

#!! * **VGenPass** remove when Pass or LEX-PASS
REMOVE:Pass VGen IF (0 LEX-PASS) ;
REMOVE:Pass VGen + Der/Pass  ;

#!! * **Allegro** 
REMOVE:allegro Gen + Allegro (NOT 0 Sem/Time OR Sem/Plc)(NOT 1 Po)(NEGATE 0 Prop LINK 1 N) ;

LIST AVVIR = "Ávvir" ; # eller andre typiske for Cmp

SELECT:Ávvir SUB:1 Prop (0/* AVVIR +  Cmp) ;
SELECT:Ávvir ("Ávvir#.*"r) ; #xfst
	#$ Ávvirgáibmi

#!! * **LexSelpluralnouns**

LIST PL-NOUN = "boahkut" ;
LIST SG-NOUN = "boahkku" ;


REMOVE:SG-NOUN SG-NOUN  IF (*-1 PLURALIZER OR OASSI OR HEADOFPARTS BARRIER NOT-NPMOD OR Num)(0 PL-NOUN);
	#$ Badjel 200 duháha olbmo leat ožžon vuosttaš oasi boahkuin.


#!! * **LexSelbeassat**
REMOVE:LexSelbeassat ("besset") IF (0 ("beassat"))(*1 (V Actio Loc) OR Inf OR Ill BARRIER SV-BOUNDARY OR CC) ;
	#$ Mun bessen vuolgimis.
	#$ Nuppi iđida ollii mánáidsuodjalus festii ja easka de besse nuorat guođđit dán ilgadis feastta.

SELECT:Lexfas ("fas" Adv)(0 ACR)  ;


#!! * **LexSelgieldit**
SELECT:LexSelgiedit ("gieldit") IF (0 ("gieldu" Ill))(*1 Inf OR (Actio Loc) BARRIER REAL-V) ;
	#$ Go su áhčči jámii de gildui sámegiela hállat telefuvnnas.

#!! * **LexSelmuohttit**
SELECT:LexSelmuohttit ("muohttit") IF (0 ("muhttit"))(NEGATE *-1 Nom) ;
	#$ Muhtii.

#!! * LexSelvuhttot
SELECT:LexSelvuhttot ("vuhttot") IF (0 ("vuohttut"))(-1 Loc) ;
SELECT:LexSelvuhttot ("vuhttot") IF (0 ("vuohttut"))(NEGATE *0 ("geahttu")) ;


#!! * **LexSelollet**
SELECT:LexSelollet ("ollet") IF (0 ("olli"))(*1 Ill BARRIER SV-BOUNDARY OR CC) ;
	#$ Nuppi iđida ollii mánáidsuodjalus festii ja easka de besse nuorat guođđit dán ilgadis feastta.

#!! * **Lexdiehttelasaid** diehttelasaid Adv
SELECT:Lexdiehttelasaid Adv IF (0 ("diehttelasaid") OR ("čihkosis") OR ("čihkosii") OR ("árvideames") OR ("gávdnamis")) ;

#!! * **Lexmearajiekŋa** 
SELECT:Lexmearajiekŋa ("jiekŋa")(-1 Cmp) ;

#!! * **Lexmaniija** 
REMOVE:Lexmaniija ("maniija")(0 ("manin")) ;


#!! * **Lexgeassit** geassit Adv vs geassit V
#SELECT:Lexgeassit ("geassit" Adv)(NEGATE *-1 AUX OR INF-V BARRIER NO)  ;
SELECT:Lexgeassit ("geassit" Adv)(*0 GEASSET)  ;
SELECT:Lexgeassit ("geassit" Adv)(1 ("fas"))  ;
SELECT:Lexgeassit ("geassit" Adv)(-1 DOPPE OR ("dalle"))  ;
SELECT:Lexgeassit ("geassit") + Adv IF (-1 COPULAS)  ;
SELECT:Lexgeassit ("geassit") + Adv IF (1 Loc LINK 1 COPULAS)  ;
	#$ Rásit liđđot juogo geassit, čakčat dahje dálvit.

#!! * **Lexvaldot** váldot V, not váldu
SELECT:Lexvaldot ("váldot" V)(0 ("váldu") + Sg + Ill)  ;
	#$ Áššáiduhtton dievdu lei orrunbáikkis ja váldui gitta dramatihka haga.

#!! * **Lexsáhttit** sáhtašit V, sáhttit Err/Orth
SELECT:Lexsáhttit ("sáhttit") + V IF (1 Inf)  ;
	#$ Sii sáhtašedje leat cahkkehan visttiid..

	
#!! * **Ger and GER-NOTV** remove Ger-forms which are not likely at all
REMOVE:Ger Ger IF (0 ("geavat" N) OR ("vásedin") OR ("hálihit"));

LIST GER-NOTV = "barggadit" "bijadit" "čohkkedit" "dagadit" "deaivat" "dutkkadit" "geahčadit" "jugadit" "lihkkat" "logadit" "manadit" "njuovadit" "ozadit" "šlivggodit" ;
	# "bargat" Ger is more likely than "barggadit" Ger, and so on. 

REMOVE:GER-NOTV GER-NOTV + Ger IF (0 Ger - GER-NOTV); 

REMOVE (PrsPrc Qst) IF (0 VFIN + Qst) ;
REMOVE:PrsPrcAUX  PrsPrc IF (0 ("áigut")) ;  #more AUX? 

REMOVE Ord IF (0 ABBR) ;

REMOVE (A Comp) IF (0 Inf LINK *1 CS OR CC OR BOS BARRIER WORD)  ;
	# jođihit deattuhit

SELECT ("beaivádit" PrfPrc) IF (1 Sem/Date) ;

SELECT ("geahččat" Imprt Sg2) IF (0 VGen LINK NEGATE -1 Neg) ;

REMOVE:eadjit (".*#eadjit"r) ;
REMOVE:girjjis ("girjjis") IF (0 ("girji")) ;
REMOVE:giisi ("giisi") IF (0 ("giisá")) ;
REMOVE:lávke ("lávke") IF (0 ("lávka")) ;
REMOVE:olle ("olle" N) IF (0 A ) ; 
REMOVE:juoga ("juoga" N) IF (0 Pron ) ;  #what is juoga N?
REMOVE:Cmp/SplitRA  (Cmp/SplitR A) IF (0 (Cmp/SplitR N)) ;
REMOVE:Cmp/SplitR_Cit  Cmp/Cit  ;


	# Lexicalised adjectives
	# ----------------------

REMOVE:r549 VGen IF (0 A);
	#$ Na varas guolli dal lea buorre.

REMOVE:Der/A Der/A IF ((0 PrsPrc LINK 1 N) OR (0 (".*#.*"r)));  
REMOVE:r550 PrsPrc IF (0 A LINK NOT 1 N);
	#% Áššis biddjui mearrideaddji deaddu dasa ahte... 

#REMOVE:r551anotv (Ex/V) - Der/NomAct IF (0 A-NOT-V - Px) ;  
	#$ Dat lea seammá guhkki go govdat.
	#% Prošeakta lea beroštahttán riikkalaččat ja guovdilis politihkárat leat jo fitnan galledeamen skuvlla oažžun dihtii eambbo dieđuid lážáldaga birra.
	#% Son fuomášii ahte čoarveeallit johtilit iežaset luottaid mielde.
	
REMOVE:A A IF (0 ("suoma")) ;	

REMOVE:A A IF (0 PrfPrc LINK *-1 ("leat") BARRIER REAL-V)(NEGATE 0 Der/Pass) ;	


REMOVE:Der/NomAg Der/NomAg IF (0 PrsPrc LINK 1 N LINK NEGATE 0 Prop)(NEGATE 1 (N Gen) LINK 1 N) ;

#!! ## Propernouns


#!! * **PropVfin** selects propernouns which can be Vfin in the beginning of a sentence
SELECT:PropVfin_a Prop IF (0 VFIN - Qst LINK *-1 BOS BARRIER WORD)((1 VFIN + Sg3) OR (1 COMMA LINK 1 Imprt + Sg2)) ;
SELECT:PropVfin_b Prop IF (0 VFIN - Qst LINK *-1 BOS BARRIER WORD)(1 Pcle)(2 VFIN + Sg3);
SELECT:PropVfin_c Prop IF (0 VFIN - Qst LINK *-1 BOS BARRIER WORD)(1 CC OR COMMA)(2 Prop OR Pers);
SELECT:PropVfin_d Prop IF (0 VFIN - Qst LINK *-1 BOS BARRIER WORD)(1 CC OR COMMA)(2 (Pers Gen));
SELECT:PropVfin_e Prop IF (0 VFIN - Qst LINK *-1 BOS BARRIER WORD)(1 CC OR COMMA )(2 Indef)(3 N);
 	#$ Lásse dat lei sihtan mu váldit dan.
 	#$ Máhtte ii sáhttán boahtit ovdal.
 	#$ Máhtte ja Ánne eaba sáhttán boahtit ovdal.
 	#$ Sáhtán, gáidda eret!

REMOVE:pcle (pcle) OR ("dot") (0 (np) OR (n)) ; #for apertium



LIST CONF-PROP = "Lea" "Lassi" "Dan" "Hu" "Goa" "Mo" "Moa" "Man" "Naba" ;

#!! * **confProp, Lea, Man, Hui, Mo, Prop** removes Props which confuces the analyser, 
 
SELECT:confProp CONF-PROP + Attr - ("Lea") IF (-1 BOS OR PUNCT OR (":"))(*1 Sem/Sur OR Sem/Fem OR Sem/Mal BARRIER REAL-WORD-NOT-ABBR OR COMMA) ;
	#$ Dan Robert Larsen 
	#$ Lea O. Olsen
 
REMOVE:confProp CONF-PROP IF (-1 BOS OR PUNCT OR (":"))(0 Sem/Sur OR Sem/Fem OR Sem/Mal OR Attr)(NEGATE 1 SG3-V); 
  	#$ Lassi dutkandoarjagat sáhttet leat mielde... 
 	#$ Lea dušše okta Ipmil. Lea idja.  

REMOVE:confProp Prop IF (0 CONF-PROP LINK 0 Adv OR Pron OR V) ;
  	#$ De son celkkii sutnje: "Mana basadit Siloaláddos."

#!!  * **Dert** Rule for removing Der/t Prop when there are other analysis
REMOVE:Dert (Der/t) IF (0 Prop) ;

#SELECT (org) ; # IF (0 Cmp/Hyph) ; # apertium
SELECT (org) + Cmp/Hyph ; # apertium


REMOVE (Der/t) IF (0 Prop) ;
REMOVE Prop IF (0 Pron) ;
REMOVE:PropUnni Prop IF (0 ("unni" Attr))(1 Der/Dimin OR ("mánáš"));
SELECT:PropNorga ("Norga") IF (0 ("norga")); # The form "norga" is not in the dictionary...

#SELECT:Gen NP-HEAD-GEN IF (0 ("sieidi") LINK 1 ("bálvaleapmi"));
  	## Sieiddi bálvalemiin sturron eallu gohčoduvvui bálvvoseallun, mii sáhtii jávkat, go oamasteaddji jámii dahje go sieidi ii bálvaluvvon.

LIST LOHKU = ("[0-9]*-#lohku"r) ;

SELECT:lohku LOHKU IF ;
	# "1960-#lohku", not loahku or logus
SELECT:lohku-apertium ("lohku") IF ((0/1 Num) OR (-1 Sem/Year));
	
#!! ### Some adjectives are never derived as Adv
REMOVE:NeverAdv Adv IF (0 ("nuorra" Adv) ) ; 

#!! ### Rules for Prop Attr, Sem/Sur and Plc

SELECT:ProLiet ("Liet") IF (0 ("Lie"));

SELECT:ProLiet ("gurut") IF (0 ("Guro"));

SELECT:semsignNom Nom IF (0C Sem/Sign) ;

SELECT:Pro Prop + Attr IF (0 Sem/Org LINK 1 ("oasseváldi") OR ("finála") OR ("gilvu") OR ("loahppagilvu"));
SELECT:NominfrontofMissing Attr (0 Sem/Fem OR Sem/Mal LINK 1 (\?)) ;
SELECT:NominfrontofMissing Attr (0 Sem/Fem OR Sem/Mal LINK *1 Prop BARRIER WORD-NOTMISSING) ;


#!! * **PropAttrIfPropx** removes Attr if no Prop on the right side
REMOVE:PropAttrIfPropx Prop + Attr - Der/lasj IF (NEGATE *1 Prop BARRIER REAL-WORD-NOT-ABBR OR COMMA)(NEGATE 1 CC LINK 1 Prop + Attr LINK *1 Prop BARRIER REAL-WORD-NOT-ABBR OR COMMA);
	#$ Ášši 05/01 Ođđa áššiid dieđiheapmi.

#!! * **nationalOrg** removes Prop after nation
#REMOVE:nationalOrg Prop + Sem/Org IF (-1 SUOPMA + Gen)(0 N - Prop) ;
	#$ Suoma Sámedikki presideanta
	#% Buollimat namuhuvvo maiddái gaskavahkku Suoma Riikkabeivviin.

	
#!! * **PropInsideProp** Selects Prop if capital letter inside clause
SELECT:PropInside Prop IF (NEGATE *-1 BOS OR (":") BARRIER WORD - Num)(0 N LINK NEGATE 0 (Der/t) OR (Der/lasj))(NEGATE 0 Neg LINK *1 ConNeg BARRIER NOT-ADV-PCLE)(NEGATE 0 CC) ;
	#$

#!! * **AttrPropDerlaš** Selects Prop + Der/lasj + Attr if first one to the right is a noun
SELECT:AttrPropDerlaš Prop + Der/lasj + Attr IF  (1 N) ;  	
	#$ oslolaš gánda šattai Eurohpameašttirin skohtergilvvus.

#!! * **PropAttr** Removes (Prop Attr), but not if to the right is Prop or Ord OR ABBR 
REMOVE:PropAttr Prop + Attr IF (NEGATE 0 PROP-ATTR OR PROP-SUR LINK 1 PROP-ATTR OR PROP-SUR OR Ord OR INITIAL OR ("von"));
	#$ Várdobáiki Evenáššis
	#% Dat lea dehálaš, lohká Svein Ottar Helander.

# All these superseded by the remove rule above:
#!! * **PropSur** Selects (Prop Sem/Sur) if finite verb to the left. Immediately to the right is Sem/Fem OR Sem/Mal
SELECT:PropSur Prop + Sem/Sur IF (*-1 VFIN BARRIER S-BOUNDARY) (1 Sem/Fem OR Sem/Mal LINK NOT 0 Sem/Sur LINK NEGATE 1 Sem/Sur LINK 1 CRD) ; #OBS
	#$ De vuolgá Biret Turi Lene Antonsen lusa.
	#$ De vuolgá viimmat Biret Turi Lene Antonsen lusa.
	#$ Dovdágo Biret Turi Lene Antonsen?

SELECT:PropSur Sem/Sur IF (1 Sem/Fem OR Sem/Mal LINK NOT 1 Sem/Sur)(-1 Sem/Fem OR Sem/Mal) ; 
	#$ Dan muitala riektediehtaga professor ja girječálli Øyvind Ravna.


#SELECT:PropSur Sem/Sur IF (1 COMMA LINK NEGATE 1 Sem/Fem OR Sem/Mal) ; #OBS

REMOVE:PropAttr Attr IF (-1 VFIN + Sg3 LINK NOT -1 (Nom Sg))(0 Sem/Fem OR Sem/Mal OR Sem/Sur LINK 1 Sem/Plc + Ill OR Sem/Plc + Loc)  ; 
	#$ 1980:s manai Anny Hámmárfestii vázzit buohccedivššárskuvlla.


#!! * **PropAttr1** Selects Attr if you are Sem/Fem OR Sem/Mal, Sem/Sur or INITIAL and to your right is Prop which is Sem/Fem OR Sem/Mal or Sem/Sur 
SELECT:PropAttr1 Attr IF (0 Sem/Fem OR Sem/Mal OR Sem/Sur OR INITIAL - Loc) (*1C Prop BARRIER REAL-WORD-NOT-ABBR OR COMMA OR PUNCT LINK 0 Sem/Sur OR Sem/Fem OR Sem/Mal OR ("von")) (NEGATE 1 Sem/Plc + Ill OR Sem/Plc + Loc LINK -2 VFIN + Sg3) ; # LINK NEGATE 0 ADVLCASE LINK -1 Nom LINK NEGATE *-1 Nom BARRIER S-BOUNDARY); #(NEGATE 0 Acc LINK *1 Acc BARRIER NOT-NP LINK NEGATE 0 Sem/Fem OR Sem/Mal) ; Lene: kommenterte ut denne fordi den hindret regelen i å fungere på alle setninger, vi får se på hva slags constraint det bør være. 
	#$ Áirras nr. 10 Ole Henrik Magga oaččui permišuvnna.
	#$ Dat lea dehálaš, lohká Svein Ottar Helander.
	#$ Do boahtá Biret Turi Sara.
	#$ Áirras nr. 17 Randi S. Pedersen oaččui permišuvnna.
	#$ De vuolgá Biret Turi Lene Antonsen lusa.
	#$ Ánde Máhtte Somby Sandvik beasai guokte vahku áigi diehtit ahte son beassá vuolgit.
	#$ – Ii leat duohta , lohká direktora Smuk.
	#$ Norggas fuolaheigga Thomas von Westen ja Knud Leem 1700-jagiin ahte muhtun sámit ožžo oahpaheaddjeoahpa.
	
SELECT:PropAttr2a Prop + Attr IF (-1 V + Sg3  LINK 2 Prop LINK 1 Prop LINK 1 CLB);
SELECT:PropAttr2b Prop + Attr IF (-1 Prop LINK -1 V + Sg3  LINK 3 Prop - Ill - Loc LINK 1 CLB);
#SELECT:Xr1914 Prop + Attr IF (-1 Prop LINK -1 V + Sg3  LINK 3 Prop LINK 1 CLB);


SELECT:PropAttrCoord Prop + Attr IF (NEGATE -1 Prop)(1 JA/DAHJE LINK 1 Prop + Attr LINK *1 Prop + Sem/Sur);
	#$ Dá leaba Brita ja Ove Kåven.

REMOVE:PropAttr1 Prop + Attr IF (NEGATE 1 Sem/Fem OR Sem/Mal OR Sem/Sur OR INITIAL OR Ord); 
	#$ De vuolgá Biret Merete Trosterudii. #This one gets Sem/Sur

SELECT:firstname Sem/Fem OR Sem/Mal (0 Sem/Plc)((1 FAMILY OR NON-FAMILY) OR (*1 ("gii") OR ("guhte") BARRIER WORD)) ;
	#$ Salmai riegádii Boas, gean eadni lei Rahab.
	#$ Rama bártnit ledje Saba ja Dedan.



SELECT:PropAttr2 Attr IF (0 Sem/Fem OR Sem/Mal OR INITIAL)(NEGATE 0 Loc)(1 INITIAL LINK *1C Sem/Sur BARRIER REAL-WORD-NOT-ABBR)(NEGATE 0 Acc LINK *1 Acc BARRIER NOT-NP LINK NOT 0 Sem/Fem OR Sem/Mal);

#SELECT:Xr1920- Attr IF (0 Sem/Sur)(NEGATE 0 Loc)(1C Sem/Sur)(NEGATE 0 Acc LINK *1 Acc BARRIER NOT-NP  LINK NOT 0 Sem/Fem OR Sem/Mal);
	#$ Áirras nr. 17 Randi S. Pedersen oaččui permišuvnna.
	#$ Áirras nr. 10 Ole Henrik Magga oaččui permišuvnna. # Two rules in order to avoid Sem/Sur Attr to Sem/Fem OR Sem/Mal.
  	
SELECT:PropGen_a Gen IF (0C Prop LINK NEGATE 0 Sem/Fem OR Sem/Mal)(1 ORGANIZATION - Prop - ("stuorradiggi"))(NEGATE -1 TRANS-V LINK NOT 3 Acc); #Lene: la inn C for å unngå at den treffer Lea osv.
SELECT:PropGen_b Gen IF (0C Prop LINK NEGATE 0 Sem/Fem OR Sem/Mal)(1 PROFESSION OR SAPMI LINK 1 ORGANIZATION); #Lene: la inn C for å unngå at den treffer Lea osv.
	#$ Oslo universiteahta namas Oslo namma lea genitiivvas.
	#$ 1982:s gitta 1993 rádjái jođihii son Norgga Álbmotmusea sámi ossodaga Oslos.
	#% Mávsseža go Ánde aviissa ihttin?
	#% World Indigenous Higher Education Consortium, WINHEC, dohkkehii Sámi allaskuvlla eamiálbmotásahussan áigodahkii 2008–2018.

SELECT:nationalaAttr Attr IF (0 ("internationála") OR ("nationála"))(1 SAPMI LINK 1 ORGANIZATION);
	#$ Son sihke buvttada ja bagadallá dan, ja filbma lea sámegillii, muitala Internašunála Sámi Filbmainstituhta direktevra Anne Lajla Utsi.


#!! * Removes **PropAttr** if no Prop on the right side
REMOVE:HLPropAttr Prop + Attr - Der/lasj IF (NEGATE *1 Prop BARRIER REAL-WORD-NOT-ABBR) ;

#!! * Removes **PropEss** if no Der/lasj
REMOVE:PropEss Prop + Ess - Der/lasj  ;
	#$ Tveiten

#!! * Removes **HearránEss** we want Px for Voc (we should we add it to the Prop version)
REMOVE:HearránEss Ess IF (0  ("Hearrá" Prop))  ;
	
#!! * Selects **PropNom** 
SELECT:PropNom Nom IF (0 Sem/Sur LINK 1 PROFESSION LINK 1 EOS)(NEGATE *-1 REAL-V BARRIER (":"))  ;
	#$ Govven: Inga Marie Guttorm Journalista


#!! ### MISC

#!! * **NotConNegII** removes ConNegII if no Neg Imprt around. This is important, as the homonym forms are common. - 30850
REMOVE:NotConNegII (ConNegII) IF (NEGATE *-1 (Neg Imprt) BARRIER SV-BOUNDARY);
 	#$ Dasgo mii oinniimet su nástti badjáneamen ja leat boahtán...
 	#$ Stáhta galgá fuolahit ahte gávdno divššohasbearráigeahčči juohke fylkkas.

SELECT:PropNom Nom IF (0 ("samas.no") LINK -1 Sem/Org)  ;
	#$ Dieđit Sámi Allaskuvlii samas.no

SELECT:nisuCmp ("nisu") (0 ("nisut") + Cmp LINK 1 Sem/Hum) ; #apertium
	#$ nisuvuovdaleaddji

REMOVE:VerbIll VFIN (0 (N Ill) LINK -1 (Dem Ill)) ;
	#$ Huksenteknihkalaš konstrukšuvnnat čatnasit njuolga elektrihkalaš rusttegiidda ja daidda doaimmaide.

LIST UVVAT = (".*uvvat"r) ;


#!! * __errsub_uvvo__ removes -uvvat Err/Orth Sg3 if Der/PassL, e.g. čujuhuvvo
REMOVE:errsub_uvvo UVVAT IF (0 Der/PassL) ; 
	#$ Das čujuhuvvo lága doaibmaviidodaga máinnašupmái bajábealde.
#REMOVE:uvvat (".*uvvot"r) IF (0 (".*uvvat"r) LINK 0 Der/PassL) ;   referanse

REMOVE:Vbearaš Pot (0 ("bearaš"))  ;


#!! * sutnje is not verb
REMOVE ("suotnjat") + Ind IF (0 Pers LINK -1 VFIN) ;

#!!  * **ABBR** Removes ABBR in favour of Adv, Pcle or Pron, e.g. "dii" when there is no punctuation
REMOVE:Badv ABBR + Adv IF ( 0 ("b") LINK -1 Sem/Fem OR Sem/Mal) ;
REMOVE:ABBR ABBR IF (0 Adv OR Pcle OR Pron) ;

#!! * **ollit** removes ollit when ollu - move this one?
REMOVE:ollit ("ollit" V) IF (0 OLLU) ;

#!! * **FocbaDu3** removes Foc/ba when Du3 verbs like máhttiba and Adv like juoba and Prop like Jáhkoba (Acc) 
REMOVE:Focba Foc/ba IF (0 Du3 OR ("juo") OR Prop) ;

#!! * **Focmis** removes Foc/mis when Loc  
REMOVE:Focmis Foc/mis IF (0 Loc) ;
	#$ ruovttubuohccedikšumis

#!! * **Focson** removes Foc/son when Sur  
REMOVE:Focson Foc/son OR Pcle IF (0 Sem/Mal LINK 0 Sem/Sur) ;
	#$ Johnson



#!! * **Fochan** removes Foc/han when adp  
REMOVE:Fochan Foc/han IF (0 Pr OR Po OR Adv) ;
	#$ čađahan


#!! * **Focbe** removes Foc/be when juobe Adv  
REMOVE:Focbe Foc/be IF (0 ("juobe") OR ("healbat")) ;
	#$ juobe, Helbe


#!! * **Focge** removes Foc/ge when Adv like dieđusge
REMOVE:Focge Foc/ge IF (0 ("dieđusge") OR ("galgat") + Du1 OR ("galgat") + Pl3) ;

#!! * **Focge-dis** disambiguation Foc/Neg-ge and Foc/Pos-ge
REMOVE:Foc-dis1 Foc/Pos-ge IF (0 Foc/Neg-ge LINK *-1 Neg BARRIER S-BOUNDARY OR CS) ;
REMOVE:Foc-dis2 Foc/Pos-ge IF (0 Foc/Neg-ge LINK *1 Neg BARRIER NOT-ADV) ;
SELECT:Foc-dis3 Foc/Pos-ge (NOT 0 Imprt + ("leat")) ;


#!! # ONE-COHORT DISAMBIGUATION - CYCLE 0

#!! The idea behind "cycle 0" is to have safe rules without context first.
#!! These rules typically chose lexicalisations over derivations, 
#!! Saami words instead of marginal names, etc.


#!! ## Lexicalised derivations


#!! *Removes **derN** if lexicalised.
REMOVE:derN DER-N IF (0 LEX-N LINK NEGATE 0 Ess OR ("dahkat"));

#!! *Removes **derNEss** if lexicalised, and both nouns are essive.
REMOVE:derNEss DER-N + Ess IF (0 LEX-N + Ess) ; #(NEGATE 0 Actio); # Vi hadde denne constrainten tidligere, vi får se på om vi trenger den
	#$ Dat lea oahpaheaddjin doppe.	
 		
#!! *Removes **derA** or PrsPrc or VGen if lexicalised. VGen is a chance.
REMOVE:derA DER-A IF (0 LEX-A); 
REMOVE:VGen PrsPrc OR VGen IF (0 LEX-A LINK NOT 1 N); 
 	#$ Dábálaš mearrádusat.
 	#$ Áššis biddjui mearrideaddji deaddu dasa ahte... 
 	#$ Na varas guolli dal lea buorre.

#!! *Removes **derAdv**  when Adv is lexicalised.  
REMOVE:derAdv (Ex/A Adv) IF (0 LEX-ADV); 

#!! *Removes **VAbess**  when Adv is lexicalised.  
REMOVE:VAbess VAbess IF (0 (".*keahttá"r)) ; 
REMOVE:Derkeahtta Der/keahtta IF (0 LEX-ADV) ; 
	#$ Dat ii vealttakeahttá gáibit ruhtajuolludemiid.


#!! * Removes **derVhmm** Does this function?
REMOVE:derVhmm DER-V IF (0 LEX-V) ; 

#!! * **derHderAlla** removes Der/h Der/alla if Der/halla.
REMOVE:derHderAlla (Der/h Der/alla) IF (0 Der/halla) ; 
	#$ Mii bovdehalaimet heajaide.

#!! * **derAlla** removes Der/halla if Der/alla.
REMOVE:derAlla Der/halla IF (0 Der/alla) ;
	#$ Mii bovdehalaimet heajaide.
  
#!! * Removes **derH** if Der/InchL.  
REMOVE:derH Der/h OR  Der/Caus IF (0 DER-V2) ; 
	#$ Nillá-mága gasttihišgođii.

#!! * Removes **derST** if Der/ahtti #OBS se på denne	
#REMOVE:derST Der/st IF (0 Der/h OR Der/Caus) ; 
	#$ Heasta vuoiŋŋastahttá juolggi go čuožžu golbma juolggi nalde.

REMOVE Der/Car IF (0 (".*heapme"r)) ; 


REMOVE Der/Comp IF (0 Der/Car OR (".*heapme"r))(0 N OR V) ; 
	#$ lasihit
	#$ Dárbu lea eanet deattuhit dáid vuođđohástalusaid.

REMOVE:ihttin ("ihtit") IF (0 ("ihttin")) ;
REMOVE:olgu ("olgu") IF (0 ("olgun")) ;

#!! ## Fragments and headliners

#!! * **foto** 
SELECT:foto1 PrfPrc IF (0 ("čállit") OR ("govvet") OR ("filbmet") OR ("čuohppat") LINK 1 (":") OR (ant)) ;
SELECT:foto2 PrfPrc IF (0 ("čállit") OR ("govvet") OR ("filbmet") OR ("čuohppat") LINK 1 ("ja") LINK 2 (":") OR (ant)) ;
SELECT:foto3 PrfPrc IF (0 ("čállit") OR ("govvet") OR ("filbmet") OR ("čuohppat") LINK 1 EOS LINK -2 HUMAN + Nom LINK NOT 0 Sg1) ;
SELECT:foto4 N IF (0 ("govva") + Loc LINK 1 (":") OR (ant)) ;

	#$ Filbmen ja čuohppan: Bengt Roger Kåven

SELECT:čállit N + Nom - Der/NomAg IF (0 Inf OR Imprt LINK 1 EOS)(-1 BOS) ;

SELECT:imprt N + Nom (0 Der/NomAg OR NomAg)(1 PrfPrc + ("gávdnot")) ;
	#$ Goargŋu gávdnon jápmán.

REMOVE:passl Sg1 IF (0 Der/Pass + PrfPrc LINK 1 (":") OR EOS) ;

SELECT:PropNom Nom IF (0 Prop LINK *-1 (":") BARRIER WORD - Attr) ;



#!! * **Sem/Act** selects lexicalised NomAct in fragments (instead of looking for VFIN). 
SELECT:Sem/Act Sem/Act + Nom IF (*-1 BOS BARRIER WORD)(NOT 0 Prop)(NEGATE *1 WORD) ;
SELECT:Sem/Act Sem/Act + Nom IF (*-1 BOS BARRIER WORD)(1 CC LINK *1 (N Nom) BARRIER NOT-NPMOD LINK NEGATE *1 WORD)(NEGATE 0 Cmp/SplitR) ;
SELECT:Sem/Act Sem/Act + Nom IF (-1 Sem/Time-clock) ;
	#$ Dutkan  # dutkan has Err/Orth reading as VFIN
	#$ Dutkan ja alit oahpahus  # dutkan has Err/Orth reading as VFIN
	#$ Dii. 10:00 Levgen olggobealde Diehtosiidda.

#!! * **AnomInf** initial adjectiv or ceartain nouns
SELECT:AnomInf Inf IF (NOT 0 COPULAS LINK -1 A + Nom OR ("bággu") + Nom OR Sem/Plc + Ill LINK *-1 BOS BARRIER WORD)(NEGATE *1 VFIN BARRIER ("ahte")) ;
SELECT:AnomInf Inf IF (NOT 0 COPULAS LINK -1 A + Nom OR ("bággu") + Nom LINK -1 Adv LINK *-1 BOS BARRIER WORD)(NEGATE *1 VFIN BARRIER ("ahte")) ;
SELECT:AnomInf Inf IF (0 TRANS-V LINK -1 Acc LINK -1 A + Nom - OLLU OR ("bággu") + Nom LINK *-1 BOS BARRIER WORD)(NEGATE *1 VFIN BARRIER ("ahte")) ;
	#$ Váttis goastadit buot vuoittuid.
	#$ Alaskai gazzat oahpu máttuid bálgáid maŋis.
	#$ – Dehálaš čájehit ahte guldalit eamiálbmogiid.
	#$ Ain vejolaš ohcat Sámi allaskuvlii
	#$ Bággu beassat hivssegii.
	#$ Váttis dievdoolbmuid oažžut bargat dásseárvoáššiiguin.
	#% Davviriikkalaš dearvvašvuođadirektevrrat galledit Sámi ja Sámedikki.

#!! * **ACompPl** adjective plural nomitative, not comp sg nor adv
SELECT:ACompPl Pl + Nom IF (0 DEHALAS LINK -1 Nom)(NEGATE *0 VFIN) ;
	#$ – Eanandoallu ja boazodoallu dehálaččat mátkeealáhussii.

#!! *  **viimmatAdv** 
SELECT:viimmatAdv ("viimmat" Adv) IF  (NEGATE 1 Inf);


REMOVE:bivnnuheapme Comp IF (0 ("bivnnut") + Pl + Nom) ;

REMOVE:bárdnat V  IF (0 ("bárdni")) ;
 	#$ bárdni joavkkuinis ráhkadan Formel 1 gilvobiilla

#!! * **SA kurssat**  
SELECT ("DUO") + Nom OR ("BOA") + Nom OR ("JUO") + Nom IF (0 (np));
	#$ DUO 100

SELECT ("duddjon") + Nom IF (-1 ("DUO"));
	#$ DUO Duddjon ja sámegiella.


#!! * **NotGen** 
SELECT:NotGen N + Nom IF (*-1 BOS BARRIER WORD)(0 Gen LINK NOT 0 PrfPrc OR Attr LINK 1 Sem/Time LINK NEGATE *1 REAL-V) ;
	#$ Giđđajohtin minuvttas minuktii.
	#% Mannan ija dáhpáhusat.
	#% Vuosttáš báddenbeaivi.

#Trying out generalisations:
SELECT:HLNomAct N IF (0 Sg1 LINK NEGATE -1 Pers + Sg1 OR DASH)(0 Sem/Act + Nom LINK 1 CC LINK 1 N + Nom)(*1 HEADLINEMARK) ;
REMOVE:HLSg1 Sg1 IF (0 PrfPrc LINK NEGATE *-1 Pers + Sg1 OR DASH BARRIER SV-BOUNDARY OR REAL-V)(*1 HEADLINEMARK) ;
REMOVE:HLSg1exep Sg1 IF (0 PrfPrc LINK -1 ("álo"))(*1 HEADLINEMARK) ;
#SELECT:HLPrfPrcSg1Acc PrfPrc IF (0 Sg1 LINK 1 Pers + Sg1 + Acc) ;
	#$ Siidda organiseren ja váldi.
SELECT:HLXXPrfPrc PrfPrc IF ((0 BOS) OR (-1 N + Nom LINK -1 BOS))(*1 HEADLINEMARK BARRIER REAL-V) ;
SELECT:HLPrfPrcCoor PrfPrc IF (1 JA/DAHJE LINK 1C PrfPrc LINK 1 HEADLINEMARK) ;
	#$ CUVKON, MOLLEN JA BOALDÁN ❡ .


SELECT:HLInfmearridit Inf IF (-1 ("mearridit") + PrfPrc) ;

SELECT:duššeAdv Adv IF (0 ("dušše"))(1 Num LINK *1 PrfPrc BARRIER REAL-V) ;
SELECT:duššeAdv2 Adv IF (*-1 VFIN)(0 ("dušše"))(1 Num);
#SELECT:PassPrfPrc PrfPrc IF (-1 PrfPrc + Der/Pass LINK *-1 BOS BARRIER WORD)(1 EOS OR ("–"))(NEGATE 0 A) ;
#SELECT:PassPrfPrc PrfPrc IF (0 Sg1 + Der/Pass LINK NEGATE *-1 Sg1 OR Neg BARRIER SV-BOUNDARY)(NEGATE 0 A + Attr LINK 1 N)(NEGATE 0 A) ;
#SELECT:PrfPrc PrfPrc IF (-1 Nom LINK *-1 BOS BARRIER NOT-NPMODADV) (1 Acc OR Adv LINK *1 HEADLINEMARK) ;
#SELECT:PrfPrc PrfPrc IF (-1 Nom LINK -1 Attr OR Gen LINK *-1 BOS BARRIER WORD) (1 Acc OR Adv LINK *1 HEADLINEMARK) ;
#SELECT:PrfPrc PrfPrc IF (-1 Nom LINK *-2 BOS BARRIER WORD) (1 ADVLCASE OR A OR Adv LINK *1 HEADLINEMARK) ;
#SELECT:PrfPrc PrfPrc IF (-1 BOS) (1 Attr LINK 1 Acc LINK *1 EOS BARRIER WORD) ;
#SELECT:PrfPrc PrfPrc IF (-1 Gen LINK -1 Num LINK *-1 BOS BARRIER NOT-NPMODADV)(*1 HEADLINEMARK OR EOS OR (":") BARRIER REAL-V) ;
#SELECT:PrfPrc PrfPrc IF (-1 Com LINK -1 Nom LINK *-1 BOS BARRIER Sg1)(*1 HEADLINEMARK BARRIER Sg1) ;
#SELECT:PrfPrc PrfPrc IF (-1 ADVLCASE LINK *-1 BOS BARRIER WORD)(1 ADVLCASE LINK *1 HEADLINEMARK) ;
SELECT:HLNom Nom IF (-1 Gen LINK -1 Gen LINK -1 CC LINK -1 Nom LINK *-1 BOS BARRIER WORD)(1 HEADLINEMARK) ;
SELECT:HLPl Pl IF (0 A + Comp LINK 0 Sem/Hum)(1 ADVLCASE LINK *1 HEADLINEMARK) ;
#REMOVE:HLSg1 Sg1 IF (0 N OR PrfPrc LINK 1 HEADLINEMARK) ;
SELECT:HLCopAdvPrfPrc Adv IF (1 PrfPrc - ("leat") LINK *1 HEADLINEMARK)(NEGATE 0 Po) ;
REMOVE:HLattrNum Attr IF (0 ("miljovdna") LINK 1 Ill LINK *1 HEADLINEMARK)(-1 Num) ;
REMOVE:HLáddjá A IF (0 ("áddjá")  LINK *1 HEADLINEMARK) ;
REMOVE:HLattrA (A Attr) IF (1 PrfPrc LINK *1 HEADLINEMARK BARRIER V) ;
        #$ Son muitalii maid ahte sin fanashoteallas leamašan dán jagi dušše vihtta guossi
REMOVE:HLadv Adv IF (0 (A Pl) LINK *1 HEADLINEMARK BARRIER V) (-1 Pl + N LINK *-1 BOS BARRIER V) ;
REMOVE:HLImprtDu2 Du2 (0 Imprt LINK 0 PrsPrc LINK 1 N LINK *1 HEADLINEMARK)  ;

	#$ Dieđihuvvon láhppon – gávdnon dearvan. 
	#$ Olgeš čohkken olbmuid buot válgabiriin.
	#$ Golbma olbmo duššan biilalihkohisvuođain dán rádjái.
	#$ Sámi bárdni joavkkuinis ráhkadan Formel 1 gilvobiilla.
	#$ Heargeráidduin vuoddján soagŋuide.
	#$ Terminologiijadieđa ja sámi fágagielaid dutkan ❡ .
	#$ Vel okta nuorra boazodoalli massán heakkas ❡ .

SELECT:HLSem/Act Nom IF (*-1 BOS BARRIER WORD)(0 Sem/Act) (0 PrfPrc LINK 1 CC LINK 1 Sem/Act LINK 1 Ill LINK 1 HEADLINEMARK) ; #TEST
SELECT:HLSem/Act Nom IF (-1 CC LINK -1C Nom LINK *-1 BOS BARRIER WORD)(0 PrfPrc LINK *1 HEADLINEMARK) ; #TEST
	#$ Ovtta­stallan ja suohta­stallan nuoraide ❡ .
SELECT:HLIVPrfprc1 Nom IF (*-1 BOS BARRIER SV-BOUNDARY)(0 N + Acc) (1 PrfPrc + IV LINK *1 HEADLINEMARK) ; 
SELECT:HLIVPrfprc2 Nom IF (0 N + Acc LINK -1 PrfPrc + IV) ((-2 BOS) OR (-2 ADVLCASE OR Adv LINK -1 BOS)) (*1 HEADLINEMARK) ;
SELECT:HLIVPrfprc3 Nom IF (0 N + Acc LINK -1 PrfPrc + IV) ((-2 BOS) OR (-2 ADVLCASE OR Adv LINK -1 BOS)) (1 EOS) ;


#SELECT:PrfPrc PrfPrc IF (0 ("giddet") LINK 1 ("luodda") OR Sem/Route) ;
#
#SELECT:PrfPrcHealiner PrfPrc IF (*-1 BOS BARRIER WORD)(0 PrfPrc LINK *1 HEADLINEMARK BARRIER VFIN) ; #TEST
#SELECT:PrfPrcHealiner PrfPrc IF (*-1 BOS BARRIER WORD)(0 PrfPrc LINK *1 ("–") BARRIER VFIN LINK *1 HEADLINEMARK) ; 
#SELECT:PrfPrcHealiner PrfPrc IF (*-1 BOS BARRIER WORD)(0 PrfPrc LINK *1 Rel OR CS OR CC BARRIER VFIN LINK *1 HEADLINEMARK) ; #TEST
	#$ Čavgen gearggusvuođa uđasvára geažil.

#SELECT:PrfPrcHealiner PrfPrc IF (-1 CC LINK *-1 BOS BARRIER VFIN)(0 PrfPrc LINK *1 HEADLINEMARK BARRIER VFIN) ; #TEST
	#$ Gidden sisabuktinvejolašvuođa ja maŋidan áigemeari.

SELECT:HLInf Inf IF (-1 Acc LINK -1 TRANS-V + PrfPrc LINK *-1 BOS BARRIER WORD)(*1 HEADLINEMARK BARRIER VFIN) ;
SELECT:HLInf Inf IF (-1 Ess LINK -1 Acc LINK -1 TRANS-V + PrfPrc LINK *-1 BOS BARRIER WORD)(*1 HEADLINEMARK BARRIER VFIN) ;
	#$ Váldán advokáhta čielggadit iežas vuoigatvuođaid ❡ .
	#$ Váldán advokáhta veahkkin oažžut vástádusa gielddas  ❡ .

#SELECT:HLInf Inf IF (1 CC LINK 1 Inf LINK *1 HEADLINEMARK) ;
#SELECT:HLInf Inf IF (-1 CC LINK -1 Inf LINK *1 HEADLINEMARK) ;
#SELECT:HLInf Inf IF (*-1 BOS BARRIER WORD)(1 ADVLCASE OR Adv LINK *1 HEADLINEMARK)(NEGATE 0 Pl1 LINK -1 PUNCT LINK -1 BOS) ;
	#$ Oassálastit ja suohtastallat deháleappot go vuoitit čiekčamiid.
	#$ – Háleštit earáin gii ádde lea buoremus dálkkas man goassige lean dovdan ❡ .

SELECT:HLPl1 Pl1 IF (-1 DASH LINK -1 BOS)(0 Inf OR Sg2 LINK *1 HEADLINEMARK BARRIER ("leat")) ;


#!! * **compgo** 
SELECT:compgo A + Comp IF (*-1 BOS BARRIER WORD)(1 go) ;
	#$ Buoret go goassege ovdal.

SELECT:HLNom Nom IF (0 HUMAN LINK *1 Ill BARRIER NOT-NPMOD LINK 1 HEADLINEMARK)(NOT 0 A + Attr)(*-1 BOS BARRIER VFIN) ;
SELECT:HLNom Nom IF (0 HUMAN LINK 1 Gen OR Attr LINK 1 Ill LINK 1 HEADLINEMARK)(NOT 0 A + Attr)(*-1 BOS BARRIER VFIN) ;
	#$ Forfang máilmmicupa rahpamii ❡ .

	
SELECT:NomPunctRight ABBR + Nom IF (*-1 BOS BARRIER WORD)(1 (PUNCT RIGHT)) ;
	#$ a) Álgogursa 1 sámegielas

SELECT:NomFragm KLASS + Nom IF (*-1 BOS BARRIER WORD)(1 ACR OR Num OR PUNCT) ;
	#$ Ášši DC 002/12

#REMOVE:NotVfinFragm VFIN (0 PrfPrc LINK *-1 BOS BARRIER WORD)(*1 EOS BARRIER REAL-V OR Adv) ;
	#$ Čuvvon ášši.

SELECT:§Attr Attr (0 ("§") LINK 1 Num) ;
	#$ Njuolggadusat nággolága § 204:s § 209 rádjái ožžot seammá geavahusa.

SELECT:HLNom N IF (0 Imprt LINK *-1 BOS BARRIER WORD)(*1 PrfPrc BARRIER SV-BOUNDARY LINK *1 HEADLINEMARK) ;
	#$ Juoigi bargošiehtadus lebbejuvvon almmolašvuhtii.

#MAP:HL  (<ind>) TARGET N IF (*1 HEADLINEMARK BARRIER WORD - Po) ;


SELECT:NomKlass Nom IF (0 ACR OR Num)((-1 ACR OR Num LINK -1 KLASS + Nom LINK -1 BOS) OR  (-1 KLASS) )(NEGATE -1 ("§") LINK 2 Po)(NEGATE 0 Loc)  ;
	#% Dearvvašvuođaásahusas mearriduvvo divvun dahje sihkkun dearvvašvuođabargiidlága § 44 mielde.
SELECT:NomKlass Nom IF (0 ACR OR ABBR)(-1 Num LINK -1 KLASS) ;
	#$ Ášši DC 002/12.
REMOVE:NomACR Acc IF (0 ACR OR ABBR)(*-1 BOS BARRIER WORD) ;
SELECT:NomACR Nom IF (0 ACR OR ABBR)(*-1 BOS BARRIER WORD)(1 PUNCT) ;
	#$ Ovttas.no - Sámi oahpponeavvut neahtas

#SELECT:PrfPrcFragm PrfPrc (0 Prs + Sg1 LINK *-1 BOS BARRIER WORD)(1 (":") OR EOS) ;
#SELECT:PrfPrcFragm PrfPrc (0 Prs + Sg1 LINK -1 Nom - Sg1 LINK *-1 BOS OR LEFT BARRIER WORD)(1 (":") OR RIGHT OR EOS) ;
#SELECT:PrfPrcFragm PrfPrc (0 Prs + Sg1 LINK -1 Gen LINK -1 Num LINK -1 BOS)(1 (":") OR EOS) ;
#SELECT:PrfPrcFragm PrfPrc (0 Der/NomAct LINK -1 Nom LINK -1 BOS)(1 (":") OR EOS) ;
#SELECT:PrfPrcFragm PrfPrc (0 Der/NomAct LINK -1 Nom LINK -1 CC LINK -1 N LINK -1 BOS)(1 (":") OR EOS) ;
SELECT:N N + Nom (0 Der/NomAct LINK -1 CC LINK -1 N LINK -1 BOS)(1 PrfPrc LINK 1 (":") OR EOS) ;
	#$ Gásttašeapmi njiedjan.
	#$ Gásttašeapmi ja konfirmeren njiedjan.


SELECT:PrfPrcFragm PrfPrc (0 Prs + Sg1 LINK -1 (N Nom) LINK -1 BOS)(1 CC OR EOS) ;
	#$ Sámekonvenšuvdna almmuhuvvon ja gaskaboddosaččat dohkkehuvvon

SELECT:Coor PrfPrc (0 Prs + Sg1)((-1 CC LINK -1C PrfPrc LINK NEGATE *-1 VFIN) OR (-1 Adv LINK -1 CC LINK -1C PrfPrc LINK NEGATE *-1 VFIN)) ;
	#$ Sámekonvenšuvdna almmuhuvvon ja gaskaboddosaččat dohkkehuvvon


#!! ## Adjectives or nouns, not adverbs

#!! * **Aifeambbo** selects A after eambbo
SELECT:Aifeambbo A - Px IF (-1 EAMBBO)(NEGATE 0 Pl OR Inf OR Adv);
	#$ Muhto eambbo eahpidahtti lea čielggadusa s. 17 cealkámuš.
	
#!! * **muhtunlagan** removes lága Ess if Indef ja lágan A
SELECT:muhtunlagan A IF (-1 Indef)(0 ("lága" Ess));
 	#$ muhtun lágan

#!! * **aiggePo** removes áigge Po, which belongs to MT and thu
REMOVE:aiggePo ("áigge" Po) ;

#!! Adjective plural, not comparative

#!! * **positivepl** Pos Pl not Comp Pl for man A sii leat
REMOVE:positivepl Comp IF (-1 ("man"))(0 A + Sg + Nom)(1 Pl OR Pl3 + Nom)(2 V + Pl3) ;
        #$ Olbmot eai dieđe man boarrásat sii leat.

#!! ## Adverbs

#!! * IFF **buotAdv** : buot Adv in front of Superl
IFF:buotAdv ("buot" Adv)  IF ((1 Superl) OR (-1 Acc LINK 2 Inf) OR (-1 TRANS-V LINK 2 ("dat" Acc) OR (Dem Acc)) OR (-1 COPULAS LINK 2 (N Ess)) OR (-1 REAL-V + IV LINK NOT 0 Pron LINK 2 COMMA OR EOS) OR (-1 (Nom Pl) LINK 2 VFIN + Pl3)) ;
 	#$ Buot buoremus lea ieš vuolgit.
 	#$ Son dáhtui buot dan ollašuvvat, vai eatnat olbmot bisošedje heakkas.	
 	#$ Son lea njuoskan buot, mii lea fuones ášši.
 	#$ Skávžá lea buot soalsin ja njuoskkas.

SELECT:rahpasitAdv ("rahpasit" Adv)(*0 DADJAT BARRIER SV-BOUNDARY) ;


REMOVE:rajesAdv ("rájes" Adv)(-1 Gen) ;

SELECT:AdvLACCAT1 Adv IF (NEGATE *-1 COPULAS BARRIER NOT-ADV-PCLE LINK *-1 (N Pl) OR PPRON-DU-PL BARRIER Nom LINK 0 Nom)(NEGATE 0 (Der/Comp Sg) LINK *-1 COPULAS BARRIER NOT-ADV-PCLE LINK *-1 (N Sg) OR SG-PRON BARRIER Nom LINK 0 Nom)(0 LACCAT-ADV) ;
SELECT:AdvLACCAT2 Adv IF (0 LACCAT-ADV LINK NEGATE 0 Pl LINK *-1 COPULAS BARRIER NOT-A-ADV LINK *-1 NP-HEAD-NOM + Pl BARRIER NOT-ADV-PCLE)(1 A OR N LINK NEGATE 1 go) ;    
	#$ Guovddáš lea fágalaččat iešbirgejeaddji ásahus.
	#$ Divššohas lea duođalaččat roasmmuhuvvan.

#!! ### Lexicalised adverbs
#!! It is useful to select early the adverbial reading for potensial nouns or verbs.

#!! * **aibbasAdv** áibbas dolin
SELECT:aibbasAdv ("áibbas" Adv)  IF (1 A OR Adv LINK *1 VFIN BARRIER S-BOUNDARY) ;
	#$ Áibbas dolin nissonat botne láiggi snálduin, dál sii botnet dorttiin.

#!! ***aloGen** removes állu Gen,  álo  Adv vs. N Gen
REMOVE:aloGen ("állu" Gen) IF (NEGATE 1 Po) ;

#!! * **aiddo**
SELECT:aiddo Adv (0 ("aiddo") OR ("áidu")) ((1 CC LINK 1 ("áidu") OR ("aiddo")) OR ((-1 CC LINK -1 ("áidu") OR ("aiddo")))) ;

#!! ***bealisAdv** 
SELECT:bealisAdv ("bealis" Adv) IF (0 ("bealli")) ;
	#$ Bargiidebellodaga joavkojođiheaddji Magnhild Mathisen fas bealistis doarju ollásit ráđđehusa ekonomalaš politihka . 

#!! ***bearreAdv** beare vs bearri
SELECT:bearreAdv ("beare" Adv) IF (0 ("bearri")) ;
	#$ Dál lohká iežas garrasit behtton fylkamánnái , go oaivvilda beare unnán bohccuid ovddas ožžon buhtadusa . 

#!! ***ilusAdv** 
SELECT:ilusAdv ("ilus" Adv) IF (0 ("illu")) ;
SELECT:balusAdv ("balus" Adv) IF (0 ("ballu")) ;
SELECT:jáhkkimisAdv ("jáhkkimis" Adv) IF (0 ("jáhkkit")) ;


SELECT:beallemudduAdv Adv IF (0 ("beallemuddu") OR ("gaskamuddu")) ;

SELECT:geavadisAdv ("geavadis" Adv) ;

#!! ***rámisA** 
SELECT:rámisA ("rámis" A) IF (0 ("rápmi")) ;


#!! * **mannelTimeAdv** golbma jagi maŋŋel
SELECT:mannelTimeAdv MANNEL + Adv IF (-1 VAHKKU-DUR OR ("dat") + Sg + Loc - Attr) ; 
SELECT:mannelTimeAdv MANNEL + Adv IF (1 go ) ; 
	## Dat dáhpáhuvai golbma jagi maŋŋil.
	#$ Logi minuvtta maŋŋel nagodii Sørøy Glimt čiekči Stig Martinsen dulbet bohtosa ovttain moalain.
	#$ Go jagi maŋŋil álggahuvvui Kárášjoga gymnása, de fárrejin ruoktot ja vázzen guokte maŋimus jagi dáppe.

#!! * **Advbadjelii** nahkehit badjelii
SELECT:Advbadjelii Adv IF (-1 Acc LINK *-1 ("nahkehit") OR ("coggat") BARRIER SV-BOUNDARY OR Acc LINK NOT -1 Acc); 	
	#$ Dan oktavuođas lei njunuš, Lene Nystrøm, nahkehan gávtti badjelii mainna šleŋggodii. 


#!! * **AdvSTV** váldit mielde, oahppat bajil. eará? STRICT-TRANS-V is too strong
SELECT:AdvSTV1 Adv IF ((-1 Acc LINK *-1 ("váldit") OR ("oahppat") OR ("čurvet")  OR ("dáhttut")  OR ("goastadit") BARRIER SV-BOUNDARY OR Acc LINK NOT -1 Acc) OR (-1 Gen LINK -1 Num LINK *-1 ("váldit") OR ("oahppat") OR ("čurvet")  OR ("dáhttut")  OR ("goastadit") BARRIER SV-BOUNDARY OR Acc LINK NOT -1 Acc)) (0 Po LINK 0 ("bajil") OR ("mielde") OR ("fárrui") LINK NEGATE *1 Acc BARRIER NOT-A-ADV); 	
SELECT:AdvSTV2 Adv IF (-1 Acc LINK -1 (Pron Gen) LINK *-1 ("váldit") OR ("oahppat") BARRIER SV-BOUNDARY OR Acc LINK NOT -1 Acc)(0 Po LINK NEGATE *1 Acc BARRIER NOT-A-ADV); 	
	#$ Váldot beatnaga mielde!
	#$ Oahpa lávlaga bajil! 	
SELECT:mieldeAdv Adv IF (0 ("mielde") LINK 1 (Actio Ess))(NEGATE -1 N + Gen - Prop) ; 
	#$ Sanila-Aikio mielde ođasmahttimin sámediggelága.	
	#% Dasgo leavssuid čállin orru vástádusaid mielde leamen dábáleamos čálamáhtolaš doaibma,
SELECT:mieldeAdv Adv IF (0 ("mielde") LINK -1 Gen LINK -1 Num LINK *-1 COPULAS BARRIER NOT-ADV-PCLE) ; 

SELECT:Advleat2 Adv IF (*-1 ("leat") BARRIER SV-BOUNDARY OR Nom LINK -1 Loc + HAB-ACTOR) (0 Po LINK 0 ("mielde") LINK NEGATE -1 Sem/Time + Gen); 	
SELECT:AdvNum3 Adv IF (-1 N + Gen LINK *-1 MEASURE + Gen BARRIER NOT-A LINK -1 Num + Nom LINK NOT 0 Sem/Year) (0 Po LINK 0 ("mielde") LINK NEGATE -1 Sem/Time + Gen); 
	#$ golbma kilo hášša mielde	

SELECT:HLAdvleatFarmen Adv IF (0 Po LINK 0 ("mielde") LINK 1 ("Farmen") OR Sem/Prod-vis OR Sem/Event LINK 0 Loc LINK 1 HEADLINEMARK) ; 	

	
#!! * **cadaAdv** if oažžut juoidá čađa
SELECT:cadaAdv ("čađa" Adv)  IF (*-1 ("oažžut") BARRIER REAL-V OR S-BOUNDARY)(NEGATE -1 ("geardi") OR TIME);
	#$ Mii oažžut daid áššiid čađa.

#!! * **cohkkutAdv** čohkkut
REMOVE:cohkkutAdv Der/NomAg IF (0 ("čohkkut") + Adv)  ;
	#$ Geahččal oččodit pasieantta čohkkut dahje veallut.


#!! * **dussaiAdv**  
SELECT:dussaiAdv ("duššái" Adv) IF (*0 ("golahit") OR ("mannat") BARRIER REAL-V OR S-BOUNDARY) ;


#!! * **gaskanAdvVGen**  
SELECT:gaskanAdvVGen ("gaskan" Adv)  IF (1 VGen);
 	#$ Gaskan bora viehkalin olggos.

#!! * **gotAdv**  
SELECT:gotAdv ("got" Adv)(-1 ("jur")) ; #eksempler på CS?

#!! * **ovdalgoCS**  
#SELECT:ovdalgoCS CS IF (0 ("ovdalgo")) ;
REMOVE:ovdalCS ("ovdal") + CS ; #venter på brukseksempler
REMOVE:ovdalPo ("ovdal") + Po (NEGATE -1 Gen) ; 

#!! * **ikteAdv**  
SELECT:ikteAdv ("ikte" Adv) IF  (*0 VFIN BARRIER S-BOUNDARY) ;
	#$ Mu mielas lei heajut dálki ikte.

#!! * **miehtaV**  
SELECT:miehtaV VFIN IF (NEGATE *-1 VFIN BARRIER S-BOUNDARY)(NEGATE -1 Gen LINK NOT 0 Nom)(0 ("miehtá"))(NEGATE 1 Gen LINK 0 TIME OR PLACE);
 	#$ Jus divššohas dasa miehtá, dahje dilálašvuohta lea nu,...
	## Jus divššohas dasa miehtá,... 
	
#!! * **mannelAdv**  
SELECT:mannelAdv_a Adv IF (1 TIME-UNIT + Acc LINK 1 MANNEL);
SELECT:mannelAdv_b Adv IF (1 Num LINK 1 TIME-UNIT + Acc LINK 1 MANNEL);	

#!! * **miehtaPr**  
SELECT:mannelPr Pr IF (0 VFIN)(1 PLACE or Sem/Plc LINK 0 Gen) ;
	#$  Ealgabivdu miehtá fylkka.


#!! * **aigiAdv**  guokte vahku áigi
SELECT:aigiAdv Adv IF (NEGATE -2 (Num Gen))(-1 TIME-UNIT + Acc)(0 ("áigi"));	
	#$ Ánde Máhtte Somby Sandvik beasai guokte vahku áigi diehtit ahte son beassá vuolgit.


SELECT:duppalitAdv ("duppalit" Adv);


#!! * **dalleAdv**
SELECT:dalleAdv ("dalle" Adv);

#!! *  **dusseAdv** 
SELECT:dusseAdv1 ("dušše" Adv) IF ((*-1 VFIN OR (Neg Foc/Neg-ge) BARRIER NOT-ADV) OR (*-1 go OR ("eará_go") BARRIER REAL-V OR S-BOUNDARY LINK -1 VFIN) OR (-1 NP-BOUNDARY OR S-BOUNDARY LINK 2 Num LINK *1 N BARRIER NOT-A-ADV LINK 1 VFIN) OR (*1 Num BARRIER NOT-ADV LINK *-2 VFIN BARRIER S-BOUNDARY)) ; # OR (1 N OR Pron LINK 1 Rel) OR (1 Nom - Pl - Pl3));
	#$ Dan mun muitalan dušše dutnje.
	#$ Áiggun geahččat dušše moadde filmma. # This function because geahččat is homonym to VFIN
	#$ Lea go dus dušše okta mánná?
	#$ Dušše ovtta lanjas leat jorba beavddit.
	#$ Buot álbmogiid hearvásvuođat ja riggodagat buktojuvvojit gávpogii , muhto ii mihkkege buhtismeahttumiid beasa sisa , ii ge oktage fastivuođa ja giellása bálvaleaddji , muhto dušše sii geaid namma lea čállojuvvon Lábbá eallima girjái.

SELECT:dusseAdv2 ("dušše" Adv) IF (1 (N Pl) LINK *1 VFIN + Pl3 BARRIER S-BOUNDARY);
	#$ Dušše inuihtat ja sápmelaččat sáhttet adnojuvvot Davviriikkaid eamiálbmogin.

SELECT:dusseAdv3 ("dušše" Adv) IF (-1 COMMA LINK *-1 ConNeg BARRIER (Pl Nom)) ;
	#$ Čilgehusat eai gávdnon, dušše sámegiel giehtačállosiid kopiijat.

SELECT:dusseAdv4 ("dušše" Adv) IF (NEGATE *-1 ANIMATE + (Pl Nom) BARRIER VFIN OR S-BOUNDARY OR COMMA)(NEGATE *-1 ANIMATE + (Sg Gen) BARRIER VFIN OR S-BOUNDARY OR COMMA LINK -1 Num)(NEGATE *1 ANIMATE + (Pl Nom) OR (A Pl Nom) BARRIER NOT-ADV-PCLE LINK NOT 1 VFIN + Pl3)(NEGATE -1 Coll LINK 2 ("johtolat")) ;
#(NEGATE *1 (Num Nom) BARRIER NOT-ADV-PCLE LINK 1 ANIMATE + (N Sg Gen)) ;
	#% 13:s dušše johtolagas.

SELECT:dusseAdv5 ("dušše" Adv) IF ((1 VFIN + Pl3) OR (-1 Inf OR PrfPrc)) ;
	# Buohkat dušše hállet ahte Liissá lei diekkáraš ja diekkáraš, dajai Soldatkin ja moaráhuvai iežas sániin.


SELECT:negadvpron Indef + Foc/ge OR ("goste" Foc/Neg-ge) IF (-1 Neg - Sup) ;
SELECT:goste Adv (0 ("goste") LINK 1 ("ge")) ;
	#$ ii maninge
	#$ ii leat jietna goste ge


#!! * **alggageAdv**  
SELECT:alggageAdv ("álggage" Adv) IF  ((*-1 Nom BARRIER S-BOUNDARY) OR (*1 Nom BARRIER S-BOUNDARY));
	#$ In mun leat álggage heajut go dát.
	#$ Sis, geain eai lean álggage njuovvanbohccot, ...

#!! * **bearraiAdv**  
SELECT:bearraiAdv ("bearrái" Adv) IF  (*-1 ("geahččat") BARRIER NOT-ADV) ;	
	#$ geahččat bearrái

#!! * **boaittobealeAdv**  
SELECT:boaittobealeAdv ("boaittobeale" Adv) IF  (1 REAL-V) ;
	#$ Guhkkelis boaittobeale orru mánát bohte Gárdima ja Mollešjoga, Šuoššjávrri ja Iešjohleagi duoddarstobuin.	
REMOVE:boaittobealeAdv ("boaittobeale" Adv) IF  (1 N) ;


#!! * **buresAdv**  
SELECT:buresAdv ("bures" Adv) IF  (*1 ("mannat") BARRIER REAL-V) ;
	#$ Bures dat manná.	

#!! * **cadatAdv**  
SELECT:cadatAdv ("čađat" Adv)  IF ((-1 VFIN - Neg - Pl3) OR (1 VFIN - Pl3)) ;
	#$ Lea čađat dárbu ođasmahttit jorgalusaid.
	#$ Son muitala ahte dii čađat jurddašehpet min iluin.

#!! * **cuozzutAdv**  
SELECT:cuozzutAdv Adv IF (0 Inf)((-1 ("bissut")) OR (-1 Nom LINK -1 ("bissut"))) ; 	
	#$ Sávvamis bissot logut dássidit ná, vaikke logut dieđusge veahá rievddadit jagis jahkái.

#!! * **dadjatAdv**  
SELECT:dadjatAdv ("dadjat" Adv)  IF ((-1 COMMA) OR (1 ("juo")) OR (*-1 VFIN - AUX - INF-V BARRIER NOT-ADV-PCLE)) ;
	#$ Muhto joddobuođuiguin bivdojuvvo luossa miehtá deanu, dadjat njálmmi rájes gitta gierragii.
	#$ Háhkan lea šaddan dadjat juo cieggan riektedilli.
	#$ Dan ii máhte ipmirdit eambbo dat geat dainna sohtet measta dadjat beaivválaččat.
	#$ Leat badjánan dadjat garra oaivilat manin bohccobiergu lea nu jođuheapmi.

#!! * **dadjatAdv2** 
SELECT:dadjatAdv2 ("dadjat" Adv)  IF (*-1 V - AUX OR COPULAS BARRIER NOT-ADV)(*1 (N Ess) OR (A Ess) BARRIER NOT-NPMODADV) ;
	#$ Dat šattai duođai dadjat ođđa girjin vuosttaš klássa várás. 
	#$ Eanavuovdinorgánat ledje maid ovdal dulkon mearrádusa áddejumi viidát, dan ektui go Direktoráhta dulkui njuolggadusa vuosttasvuoigatvuohtan árjjalaš eanadolliide ja guollebivdiide dadjat juo njuolggadussan mas ii spiehkastuvvo.
	
REMOVE:KillDadjatAdv ("dadjat" Adv) ;
	#Comment: Kills all remaining "dadjat" adverbials. 

#!! * **dainnaAdv**  
IFF:dainnaAdv ("dainna" Adv)  IF (*1 ("ahte") BARRIER WORD);
	#$  ...muhto sáhttá vuordit dainna go ferte vuoruhit dikšuma. #Pers
	#$  Kvaliteahtta sihkkarasto dainna, ahte bargit dovdet kultuvrra.

#!! * **danin** (Pron Ess OR Adv)
SELECT:danin (Pron Ess)  IF (*-1 COPULAS BARRIER NOT-ADV-PCLE)(0 ("danin"))(1 EOS OR CLB);
	#$ ...jos oapmi fierpmit leat danin,...
#!! * **daninAdv** selects danin Adv. It is a special rule, only negative restrictions.
SELECT:daninAdv ("danin" Adv) IF (NEGATE *1 (N Ess) BARRIER NOT-A-ADV)(NEGATE 0 Ess LINK NEGATE *1 Nom BARRIER NOT-ADV LINK *-1 Ess LINK *-1 COPULAS BARRIER NOT-ADV)(NEGATE 1 CLB)(NEGATE *-1 OPRED-V BARRIER NOT-ADV)(NEGATE *1 OPRED-V BARRIER NOT-ADV);
	#$ Danin badjánii jurdda lága ráhkadit.
	#$ Sierralágan diehtojuohkingeahččaleamit leage danin dehálaččat.
	#$ Návetruittu lávejit danin atnit. #Essive - hence the restrictions. Special rule, only negative restrictions.

	
#!! * Select Ess, and then kill?  
#SELECT:r1286 Ess IF (*-1 COPULAS BARRIER NOT-ADV)(0 ("danin"))(1 CLB); 
#	## ...jos fierpmit leat danin,...
#SELECT:r1287 Ess IF (0 ("danin"))(*1 OPRED-V BARRIER NOT-ADV-PCLE);
#SELECT:r1288 Ess IF (0 ("danin"))(*-1 OPRED-V BARRIER NOT-ADV-PCLE);  
#	## Návetruittu lávejit danin atnit. 

#!! * **dassaAdv**  
SELECT:dassaAdv_a Adv IF (0 ("dassá") OR ("dassái"))(1 go LINK *1 VFIN BARRIER S-BOUNDARY);
SELECT:dassaAdv_b Adv IF (0 ("dassá") OR ("dassái"))(-1 VAHKKU-DUR);
	#$ Dassá go son jámii, lea guhkes áigi.
	#$ Vuosttažettiin adnui sámi kultuvra árvvoheabbon go dáža kultuvra 100 jagi dassá.

#!! * **dakkoAdv**  
SELECT:dakkoAdv ("dákko" Adv)  IF (*0 VFIN BARRIER S-BOUNDARY) ;
	#$ ... oaiviliid ja ákkaid bokte mat dákko ovddiduvvojit,...
	#$ Hammerfeastta suohkanbáhppa Harboe lea dákko maid, nu go eará oktavuođain, buorre gáldu.


#!! * **jusCS**  
SELECT:jusCS ("jus" CS)  IF (NEGATE 1 COMMA OR EOS);
	#$ jus, 


#!! * **duoAdv**  
SELECT:duoAdv ("duo" Adv)  IF (NEGATE -1 Dem or (A Attr))(0 ("duo"));
	#$ Áddjá go duo lea boahtime?

#!! * **duoN**  
REMOVE:duoN ("duo" N) IF (0 Pron LINK NEGATE 1 NP-BOUNDARY);


#!! * **duodaidAdv**  
SELECT:duodaidAdv1 ("duođaid" Adv) OR ("duođas" Adv) OR ("golgosis" Adv) ; # IF  ((-1 BOC LINK 2 COMMA) OR (-1 Nom LINK 2 VFIN));
	#$ Duođaid, mun cealkkán didjiide:...
SELECT:duodaidAdv2 ("duođaid" Adv) OR ("duođas" Adv)  IF (-1 COPULAS)(1 Ess OR Nom);
	#$ Eatnandoallu šattai duođaid buressivdnádussan sámiide, nugo dat lei iežáge álbmogiidda.
	#$ Muhto lei duođas ovddasmannibargu.
SELECT:duodaidAdv2 ("duođaid" Adv) OR ("duođas" Adv)  IF ((*-1 ("váldit") BARRIER SV-BOUNDARY) OR (1 ("váldit")));

LIST ADV-NOT-ESS = "olahahttin" ;

#!! * **plcadv** words like nuortan adv (DOPPE) not N Ess
SELECT:plcadv Adv IF (0 (N Sem/Plc Ess) OR DOPPE OR ADV-NOT-ESS LINK NEGATE 0 Po LINK -1 Gen) ;
	#$ várit nuortan



#!! * **AdvNotNA** Adverbs, not nouns or adjectives
LIST ADV-NOT-NOMEN = "bealleheggii" "beare" "birrasiid" "čoahkkái" "čoahkis" "fárrui" "fárus" "gárrenoaivvis" "ihttážii" "jávkosis" "goalmmádassii" "namalassii" "nuppádassii" "oidnosii" "ođđasis" "olámuddui" "oppalohkái" "ovdagihtii" "ovttasráđiid" "ruossalassii" "ruovttoguvlui" "sullii" "válbmasii" "lihkus" ; 

#!! * **biras** is noun and not adverb if in GN context
SELECT:biras N IF (-2 CS OR BOS)(-1 Gen)(0 ("birrasiid")); # Vi bør generalisere denne.

REMOVE:AdvNotNA N OR A IF (0 ADV-NOT-NOMEN) ;

#!! * **AComp** remove A Comp when Adv
REMOVE:AComp (A Der/Comp) IF ((-1 DYNAMIC-V) OR (-1 Ill LINK -1 DYNAMIC-V))(0 Adv LINK NOT 0 ("unnit") OR Indef OR ("buorre"))(NEGATE *1 go BARRIER SV-BOUNDARY);         
	#$ Eadni ráhkadii johtilit ruovttusiiddu.  
	#$ Son sártnui sidjiide garrasit ja jearai:
	#% Mun čállen romit reivve go don.

#SELECT:ACompAdv Avd IF (0 ("eanet") OR ("unnit") LINK 0  A + Comp); # legg til constraint

REMOVE:ACompAdv (A Der/Comp) IF (-1 BOS OR CLB)(0 Adv)(1 EOS OR CLB);
REMOVE:ACompAdv (A Der/Comp) IF (-1 V - COPULAS)(0 Adv)(1 (N Ess) OR Inf); 

REMOVE:buoretAdv ("buoret" Adv)(0 (A Der/Comp) LINK 1 N) ;

#!! * **birrasii** removes birrasii N
REMOVE:birrasii N IF (0 ("birrasii") LINK 1 Num OR HEADOFPARTS OR Sem/Year OR Sem/Date OR Sem/Amount)(NEGATE -1 Gen)  ;	

#!! * dieđusge chooses adv
SELECT:dieđusge ("dieđusge") + Adv  ;	
SELECT:dieđus  ("dieđus") + Adv  ;	

#!! * **sávvamis** chooses adv
SELECT:sávvamis ("sávvamis") + Adv  ;	



#!! * **beali** chooses adv
SELECT:beali ("beali") + Adv (1 Comp)  ;	


#!! * **doarvaiAdv** removes birrasii N
SELECT:doarvaiAdv ("doarvái" Adv) ; # IF  ((-1 COPULAS LINK 2 Nom OR CS) OR (-1 TRANS-V LINK 2 (N Acc)) OR (-2 TRANS-V LINK 3 (N Acc)) OR (-1 Neg + Ind LINK 2 Adv) OR (-1 Der/Pass LINK *2 Nom BARRIER NOT-A)) ;
	#$ Vaikko mánáin lei doarvái bargu skuvllas ja bihtáidbargamiin beaivvi mielde, de berre liikká dál maŋŋá fuomášuhttit ahte máŋggaid jagiid ii lean makkárge lágiduvvon asttuáigefálaldat mánáide.	


#!! * **doložat** removes doalut N
REMOVE:doložat N IF (0 ("dološ")) ;


#!! * **eanasAdv**  
SELECT:eanasAdv Adv IF (*-1 NP-HEAD-NOM BARRIER S-BOUNDARY)(0 ("eanas") OR ("eanaš"))(NEGATE *1 N BARRIER NOT-A-ADV);
	#$ Muhto báinnan dat gal eanaš lea.
	#$ Áššemeannudeapmi lea eanaš dárogillii.
	#$ Nuppádassii leat čielga gáibádusat eaktodáhtolaš oskui, mii guoská giddodahkii, eanaš vuođđuduvvon registrerema oktavuođas.

#!! * **eambbogo** selects Adv
SELECT:eambbogo Adv IF (0 EAMBBO LINK -1 REAL-V)(1 ("go") OR COMMA) ;
	#$ Gittabivdosiin ii leat lohpi bivdit deanu eambbo go beallái.
	#$ Eamiálbmogat soitet gillát eambbo, danne go oallugat ellet geafivuođas.


SELECT:eambboN Attr IF (0 EAMBBO LINK 1 N LINK 1 Inf) ;
	#$ Lullisámi juoigi Marja Mortensson lohká Spellemann-nammadeami addit vel eambbo movtta bargat sámi musihkain.
SELECT:eambboN Adv IF (0 EAMBBO LINK 1 ADVLCASE)(-1 REAL-V) ;
	#$ iige oktage oahppi lohkan ahte son čállá eanet sámegillii go eanetlogugillii vaikko livččiige vástidan ahte hállá gal eanet sámegiela go eanetlogugiela.

#!! * **eanetAdv**  
SELECT:eanetAdv1 ("eanet" Adv) IF ((-1 ("ja") OR ("ahte") LINK -1 ("eanet")) OR (1 ("ja") OR ("ahte") LINK 1 ("eanet") LINK NEGATE 1 N + Pl))(NEGATE 1 N + Pl) ; #(*1 A OR REAL-V BARRIER S-BOUNDARY);
	#$ Dutkangaskkusteapmi ja jearru dutkanbohtosiidda šaddá eanet ja eanet guovddáš oassi dutkama árgabeaivvis.
SELECT:eanetAdv2_a Adv IF (0 ("uhcit") OR ("unnit") OR ("eanet")) (1 ("uhcit") OR ("unnit") OR ("eanet"));
SELECT:eanetAdv2_b Adv IF (0 ("uhcit") OR ("unnit") OR ("eanet")) (-1 ("uhcit") OR ("unnit") OR ("eanet"));
SELECT:eanetAdv3 Adv IF (0 ("uhcit") OR ("unnit") OR ("eanet")) (1 go LINK 1 Num);
	#$ ...muhto dál lea eanet uhcit jávkan. 
	#$ Go vierroopmodaga geavaha ja eaiggát unnit eanet čielgasit dasa leat miehtan,...
	#$ Go eaiggát eanet unnit čielgasit dasa lea miehtan.
	#$ Mearrádusaid gittaopmodaga luobaheamis, mat leat dahkkojuvvon uhcit go njealji stivrralahtu doarjagiin, ferte Sámediggi ja Finnmárkku fylkkadiggi dohkkehit.

SELECT:eanetAdv3 ("eanet" Adv) IF (1 A-CASE OR REAL-V - ("leat") LINK *1 N BARRIER NOT-A-ADV)(NEGATE 1 (N Pl))(NEGATE 0 Pl + Nom LINK 1 VFIN + Pl3) ;
	#$ Báhkkodeamis ferte leat sáhka geavaheamis mii lea oktoráđđejeaddji dahje ainjuo čielgasit eanemus čalbmáičuohcci.
	#% 2010 rájes leat ceggejuvvon eanet sajit go dan mii lei vurdojuvvon.
	#% Eanebut ožžot barggu.
	
SELECT:eanetAdv4 ("eanet" Adv) IF (1 Inf OR ConNeg LINK NEGATE 1 N)(NEGATE 0 (A Pl) OR (Pron Pl) LINK 1 VFIN + Pl3) ;

REMOVE:eanetAdv3 Adv IF (0 EAMBBO LINK *1 (N Pl) BARRIER NOT-A) ;
	#$ 2010 rájes leat ceggejuvvon eanet bargosajit go dan mii lei vurdojuvvon.

	
REMOVE:eanetV V IF (0 ("eanet" Adv))((*0 COPULAS BARRIER S-BOUNDARY) OR (*1 N BARRIER NOT-A-ADV LINK *-1 ("eanet") LINK -1 VFIN)) ;	
	#$ Dál go ruhtadilli lea buoret go ovdal, de leat gávttis eanet čiŋat.
	#$ Ovdal ledje eanet láttagat Olmmáivákkis, muhto boanddat leat goikadan ollu jekkiid ja gilván daidda rásiid.

SELECT:IndefAdv Adv (0 Indef LINK -1 ("vel")) ;
	#$ Dát láhka nanne mánáideamet rievtti iežaset gillii vel eanebut.


SELECT:eanemusatAdv ("eanemusat" Adv) (1 Num) ;
	#$ Juohke mánná oaččui guokte ja eanemusat golbma mánu oahpahusa jahkái.

#!! * **AdvComp**  
#SELECT:AdvComp Adv IF (0 Comp)(NEGATE 0 Pron)(NEGATE 1 Dem OR N OR A);
	#$ Dat lea eanet go 10.
	#$ Lea 43 olbmo unnit go diibmá dán muddui.
	# Ambiguous: 	## ...hálida universitehta almmuhit eanet dieđalaš artihkkaliid sámegillii. #- hence the condition (NEGATE 1 A).

#!! * **easkkaAdv**  
SELECT:easkkaAdv1 EASKKA + Adv IF (0 EASKKA)(1 ("dalle") OR go OR ("de") OR Num OR MANNEL);   
	#$ ...easkka maŋŋel 1529 ásaiduvve olbmot duođas báikkiide.

SELECT:easkkaAdv2 Adv IF (0 EASKKA)(*1 TIME BARRIER NOT-A-ADV);
	#$ ...mii easkka maŋimuš áiggiid lea jávkágoahtán. 
	#$ Dat lei boahtán ruoŧagielain easkka jagi 1863.
	#$ Muhto easkka 1898:s dovdagohte Láhppiolbmot dan mii maŋŋelis bođii.
 # Ambiguous: 	## Dat čielgá dálá oasálaččaide easkka proseassas. 

SELECT:easkkaAdv3 Adv IF (0 EASKKA)(-1 MOD-ADV-ADV);
 	#$ Lea hui easkka čuožžilan gažaldat.

SELECT:easkkaAdv4 Adv IF (0 EASKKA)(1 PrfPrc);
 	#$ dat ebmos vuoksá lei oaffaruššojuvvon easkka huksejuvvon áltára alde.


LIST CUT-V = "boatkanaddat" "botket" "boatkanit" "oažžut" "gáskkistit" "čuohpastit" "čuolastit" "dodjit" "doadjit" "čuohppat" "gáskit" "mannat" ;

#!! * **gaskatAdv**  
SELECT:gaskatAdv ("gaskat" Adv)  IF (*-1 CUT-V BARRIER S-BOUNDARY) ;

#!! * **goassigeAdv**  
SELECT:goassigeAdv ("goassige") OR ("goassege")  IF (*-1 Neg BARRIER VFIN) ;
	#$ Ii leat stáhtas várra goassige leamaš ruđalaš ávki dán ámmahis. 

#!! * **gosaAdv**  
SELECT:gosaAdv ("gosa" Adv) IF  (*1 VFIN BARRIER S-BOUNDARY);
	#$ Gosa don čihket dan?
	#$ Son oinnii báikkiid gosa juo ledje ásaiduvvan. 

#!! * **gustoAdv**
SELECT:gustoIsAdv ("gusto" Adv) IF (-1 ConNeg);
        #$ In leat gusto dan guovllu áidna turista.

REMOVE:gustoAdv ("gusto" Adv) IF (NEGATE *-1 VFIN - Neg BARRIER NOT-ADV-PCLE)(NEGATE 1 VFIN - ("duššat")) ;
	#$ Juos evttohusa meahcceeatnamiid nuppástuvvan geavaheami birra, mii juogo gusto dušše Kárášjoga, Guovdageainnu, Unjárgga, Porsáŋggu ja Deanu gildii dahje gusto dušše restii Finnmárkkus, dorjot golbma ja dušše golbma stivrralahtu, de sáhttet golbma stivrralahtu ovtta oli gáibidit ášši gieđahallat oktii vel.

#!! * **gustoAdvláhka**
REMOVE:gustoAdvláhka ("gusto" Adv) IF (*0 LAHKA) ;
	#$ Guđe fylkkasuohkana doaimmaide gusto láhka?

#!! * **guhkasAdv**  
SELECT:guhkasAdv Adv IF (*-1 REAL-V BARRIER NOT-ADV-N)(0 ("guhkká") LINK 0 Loc OR Gen)(NEGATE 1 COPULAS OR Acc)(NEGATE 1 (V Neg) LINK 1 COPULAS) ;
SELECT:guhkasAdv Adv IF (-2 REAL-V)(-1 (A Nom) OR (N Nom) OR Ess)(0 ("guhkká") LINK 0 Loc OR Gen) ;
SELECT:guhkasAdv Adv IF (-1 BOS OR CC OR CS)(0 ("guhkká") LINK 0 Loc OR Gen)(*1 REAL-V LINK -1 NP-HEAD-NOM) ;
	#$ Dilli joatkašuvvá dákkárin guhkás boahtteáigái.
	#$ Son bálkestii spáppa guhkás.
	#$ Guhkás son bálkestii spáppa.

SELECT:guhkaAdv Adv IF (0 ("guhkká") + Gen)(-1 ("šat")) ;


#!! * **VifVFIN** removes V 
#REMOVE:VifVFIN V ((0 Adv LINK 1C VFIN OR PrfPrc) OR (0 Adv LINK -1C VFIN OR PrfPrc));
	#$ Don gal gusto beroštat čuovvut.

#!! * **harveAdv**
SELECT:harveAdv ("hárve" Adv)  IF (NEGATE *-1 PLANT OR ("bealdu") OR ("eana") OR ("gieddi") OR ("tráktor") BARRIER S-BOUNDARY)(0 Adv)(NEGATE *1 PLANT OR ("bealdu") OR ("eana") OR ("gieddi") OR ("tráktor") BARRIER S-BOUNDARY);
	#$ Boazoealáhusas ovddasteaddjit hárve vuosttaldit viessohuksema.

#!! * **juogoQst**
IFF:juogoQst ("jogo" Qst) IF  (NEGATE *1 ("dahje") OR ("dehe"));
	#$ Jogo gikse dat guolit?

#!! * **justeAdv**
SELECT:justeAdv ("juste" Adv) IF  ((-1 ("jur")) OR (1 PAST-MARKER OR PRESENT-MARKER OR MO));
	#$ jur juste
	#$ juste dalle

#!! * **jámasAdv**
SELECT:jámasAdv ("jámas" Adv) IF (*-1 FMAINV BARRIER SV-BOUNDARY LINK 0 ("bargat") OR ("heahpanit") OR ("gálbmot"));


	# láhkai
	# - - - -
SELECT:láhkaiAdv Adv IF (-1 Attr OR Indef LINK NOT 0 Gen)(0 ("láhkai"));
	#$ Seammá láhkai go váldojournála ja lassijournála geavaheami oktavuođas...
	#$ Juohke láhkai ..

#!! * **lihkusAdv**
SELECT Adv IF (0 ("lihkus"));



#!! * **loahpasAdv**
SELECT:loahpasAdv Adv IF (*-1 MUITALIT BARRIER SV-BOUNDARY)(0 ("loahpas"));
SELECT:loahpasAdv Adv IF (NEGATE -1 Gen)(0 ("loahpas"));

#!! * **liikkaAdv**
SELECT:liikkaAdv ("liikka" Adv)(0 N LINK -1 VFIN)(1 A OR Adv) ;
SELECT:liikkaAdv Adv (0 ("lihkká")) (*1 VFIN BARRIER SV-BOUNDARY OR REAL-V) ;
	#$ Dalle šattašii liikka buoret dálki go mannan sotnabeaivve.
 	#$ muhto lihkká dat manne golbma dievas beaivvi ovdal válmmasteimmet. (LIA)


LIST SLIP-V = "luoitit" "beassat" "gaikkihit" "luovvanit" "oažžut" "nuollat" ;

#!! * **luovosAdv**
SELECT ("luovos" Adv)  IF (*0 SLIP-V BARRIER S-BOUNDARY) ;


#REMOVE Interj ((-1 WORD - Interj) OR (1 WORD - Interj)) ;

#!! * **maninAdv**
SELECT:maninAdv ("manin" Adv) IF (NEGATE *-1 REAL-V BARRIER S-BOUNDARY)(NEGATE 0 Ess LINK *1 OPRED-V LINK -1 Acc) ;
	#$ Manin don leat heittohit nohkkan?
	#% Manin don geavahat dan?

SELECT:LIAmannetV V IF (0 ("mannat") LINK *1 PLACE + Ill OR DOHKO OR PO-ILL BARRIER SV-BOUNDARY) ;
	#$ dáppe bohte ovdamearkka dihte skábman dat bohte dáppe diekko Leavvajoga Deanu ala ja em . (LIA)

SELECT:LIAmannetV V IF (0 ("mannat") LINK 1 ("jo") LINK 1 Inf OR DOHKO OR PLACE + Ill) ;
	#$ na dološolbmot dikšo dainna lágiin ahte juohke geasi go bohte nuohttái manne jo iskat vuohččanaga jávreoivoša leat go riba ja ruska buktán giđaža (LIA)


#!! * **manneAdv**
SELECT:LIAmanneAdv1 ("manne" Adv)  IF (1 ("bat")); 


#!! * **manneAdv**
SELECT:manneAdv1 ("manne" Adv)  IF (*-1 BOC OR PUNCT OR COMMA OR Interj BARRIER NOT-ADV-PCLE)(*1 VFIN BARRIER S-BOUNDARY OR CC LINK NOT 0 N)(NEGATE 0 Qst)(NEGATE 1 Qst)(NEGATE -1 ("de")); 
	#$ Ja manne moraštehpet biktasiid dihtii? 
	#$ Naba dii, manne dii ieža rihkkubehtet Ipmila báhkkomiid?
	#$ Manne dis leat diekkár bahás jurdagat din váimmuin?
	#$ Manne rievddadit luonddudilálašvuođat?
	#% Sii manne isideaset lusa ja muitaledje buot. # BARRIER CC!


SELECT:LIAmannemanne ("manne" Adv) IF (1 ("manne" Adv) LINK *1 VFIN BARRIER NOT-ADV-PCLE) ; 
	#$ ahte manne manne dál eai sáhte muhto na go juo stáhtas leat ruđat ahte arbeidsledighetstrygdaid ja diekkáriid máksit jaja na dat manná dalle bures


SELECT:LIAmanneAdv2 ("manne" Adv) IF (*-1 VFIN BARRIER S-BOUNDARY OR CC OR CLB OR Du1 + Nom OR Pl + Nom LINK NOT 0 Neg)(*-1 VFIN BARRIER S-BOUNDARY OR CC OR CLB)(NEGATE 0 VFIN LINK *1 DOHKO OR PLACE + Ill BARRIER NOT-ADV-PCLE)(NEGATE 0 VFIN LINK *1 Gen BARRIER NOT-ADV-PCLE LINK 1 PO-ILL) ;
	#$ Jos oktage jearaš dudnos manne doai dahkabeahtti dan,...
	#% dat manne dat albmát manne vuos deikke bygget viesuid ja . (LIA)
	#% muhto de lei dálvi lávededje dien dábálaččat die februar mánus go dat dážaid diggehearrát de manne manne duon duo Kárášjohkii . (LIA)
	#% na dat gal dieđus ge njigui go dat oinnii dat manne buot dat olbmot. (LIA)

SELECT:LIAmannemanne ("manne" Adv) IF (-1 CONTRA)(1 VFIN ) ; 
	#$ dat máđut leamašan das muhto manne bohte dalle áhkkorat



SELECT:manneAdv3 ("manne" Adv)  IF (-1 BOC)(1 N OR Adv LINK 1 ("?")) ;
	#$ Manne Sámi allaskuvlii?

	
#!! * **muhtuminAdv3**
SELECT:muhtuminAdv ("muhtumin" Adv) ;
	#$ Muhtumin lea deaŧalaš čállit journála dalán.

#!! * **njuolgaAdv**
SELECT:njuolgaAdv ("njuolga" Adv)  IF ((*1 Ill BARRIER NOT-A-ADV) OR (1 Gen LINK 1 N + Ill)) ;
	#$ Nuppi vuoru goalmmát minuvttas Anette nivkalii čiehkačievččastaga čábbát njuolga mollii.

#!! * **oddasitAdv**
SELECT:oddasitAdv1 ("ođđasit" Adv)  IF (-1 ("fas")) ;
	#$ Elia gal boahtá dahkat buot fas ođđasit.

SELECT:oddasitAdv2 ("ođđasit" Adv) IF (*-1 Nom BARRIER S-BOUNDARY)(*1 NOT-AUX-V BARRIER S-BOUNDARY);
	#$ Eat mii sáhte ođđasit ásahit vuođu.

#!! * **oktanAdv**
SELECT:oktanAdv Adv IF (0 ("ovttas") OR ("oktan"))(*1 Com BARRIER NP-BOUNDARY);
	## Divššohasas lea vuoigatvuohta iežas journála geahččat oktan mildosiiguin. 

#!! * **ollengeAdv**i
SELECT:ollengeAdv ("ollenge" Adv) IF (*-1 Neg);
        #$ Ii mus lean gal ollinge jurdda searvat juoiganoassái.

#!! * **ovttasAdv**
REMOVE:ovttasColl ("ovttas" Coll) ;
REMOVE:oktaLoc OKTA + Loc - Attr IF (NEGATE 1 COPULAS + Pl3 OR COPULAS + Sg3) ; 

SELECT:ovttasAdv1 ("ovttas" Adv) IF (-1 (Pl Nom))(1 PL3-V);
	#$ Ferte oaidnit ollislaš gova maid soahpamušat ovttas vuođđudit.

SELECT:ovttasAdv2 ("ovttas" Adv) IF (-1 N LINK *-1 NUM + Nom BARRIER NOT-A-ADV)(*1 PL3-V BARRIER NOT-ADV-PCLE);
	#$ Ferte oaidnit ollislaš gova maid dát guokte soahpamuša ovttas vuođđudit.

LIST OKTII-ADV = "oktii" "guktii" "golbmii" "njelljii" "vihttii" "guhttii" "čihččii" ;
	
#!! * **oktiiV** remove
REMOVE:oktiiV SG3-V OR Num IF  (*-1 VFIN BARRIER S-BOUNDARY)(-1 ("dušše"))(0 OKTII-ADV);
	#$ Vuoigatvuohta gusto dušše oktii.
	#$ Dušše oktii lean oaidnán čájáhusa mii čájehii álgoálbmoga rávdebarggu.
	#$ Ja dušše oktii lean oaidnán čájáhusa mii čájehii álgoálbmoga rávdebarggu.
	#$ Lulli-sámiguovllu biirret heajumusat go sis lea dušše oktii leamaš listu mas nisu lea njunnožis.
	#$ Oktii lei gotka gii ii jáhkkán Kilimanjaro gávdnot.

#!! * **oktiiAdv** select
SELECT:oktiiAdv1 OKTII-ADV IF (-1 BOC)(1 go OR (N Pl) OR VFIN);
	#$ Oktii go leimmet mannamin rohkadallanbáikái, bođii min ovddal muhtun šlávvanieida. 
	#$ Oktii cuohpaid galgá biddjot luossa go sáltejuvvo.

SELECT:oktiiAdv2 OKTII-ADV IF (1 ("njunni" N Pl Gen) OR ("juolgi" N Pl Gen) OR ("sealgi" N Pl Gen) OR ("geahči" N Pl Gen) OR Sem/Time);
	#$ Dat čohkkáiga oktii njuniid.
  
SELECT:oktiiAdv3 OKTII-ADV IF (*-1 OKTII-V OR COM-V) ;
	#$ Porsáŋggu loguin leat buot klássaid submi biddjon oktii.

SELECT:oktiiAdv4 OKTII-ADV IF (*1 TIME + Loc BARRIER N OR V OR Pron)(*1 Ill OR Loc BARRIER NOT-A-ADV);  
	#$ Oktii stuorradiggeáigodagas ovddiduvvo dieđáhus daid doaibmabijuid birra mat galget sihkkarastit ja ovddidit sámi giela.

SELECT:oktiiAdv5 OKTII-ADV IF (-1 ("vel") OR REAL-V) ; 
	#$ Lea go Sámedikki mielas Microsoft duođaid ánssášan vel oktii nuvttá máidnosa, mii ii leat duohta ge?

REMOVE:oktiiAdv Num IF (0 ("oktii")) ; #waitingfor examples of Num Ill

#!! * **ollasitAdv** selects
SELECT:ollasitAdv1 Adv IF (0 ("ollásit" V))((*-1 REAL-V - INF-V BARRIER SV-BOUNDARY) OR (*1 Loc OR Ill BARRIER NOT-A-ADV) OR (*1 NP-HEAD-GEN BARRIER NOT-A-ADV LINK 1 Po) OR (-1 MOD-ADV-ADV LINK 2 Adv OR ADVLCASE) OR (1 REAL-V)) ;
	#$ ...ahte historjjálaš ovdáneapmi čielggaduvvo ollásit. 
	#$ Nu  leatge geasseguohtumat, mat leat váris, measta ollásii norggabealde.
	#$ Gaup gal devddii dán rolla ollásit.

#!! * **radjaiPo** selects
SELECT:radjaiAdv Po IF (-1 Num OR Ord)(0 ("rádjai") OR ("rájes"));
	#$ Njuolggadusat nággolága § 204:s § 209 rádjái ožžot seammá geavahusa.
# # This rule should be extended to all postpositions likely to occur after
# # number expressions. Todo: Make a set.

#!! * **rabasAdv** selects
SELECT:rabasAdv Adv IF (-1 COPULAS)(0 ("rabas") LINK NEGATE *1 N BARRIER NOT-ADV);
	#$ Guktot uvssat leat rabas.

#!! * **rabasAttr** selects
SELECT:rabasAttr Attr IF (0 ("rabas") LINK 1 Sem/Event OR ("reive"))(NEGATE -1 COPULAS LINK *-1 Nom BARRIER WORD)  ;
	#$ rabas seminára
	#% Sajosa restauráŋŋa Caiju lea rabas festivála áigge.


#!! * **rabasANom** selects
SELECT:rabasANom Nom IF (0 ("rabas") LINK -1 COPULAS LINK *-1 Nom BARRIER WORD)  ;
	#% Sajosa restauráŋŋa Caiju lea rabas festivála áigge.


#!! * **sámásAdv** selects
SELECT:sámásAdv Adv IF (0 ("sámás") LINK -1 BOS)  ;
SELECT:sámásAdv Adv IF (0 ("sámás") LINK 1 (":"))  ;
	#% Sajosa restauráŋŋa Caiju lea rabas festivála áigge.


#!! * **soaittáhagasAdv** selects
SELECT:soaittáhagasAdv Adv IF (0 ("soaittáhagas"));
	#$ sierralávdegoddi ii dieđe daid luottaid ja meannudit soaittáhagas gos bivdit besset vuodjit.
# The rule has no restrictions, so keep an eye out, it removes loc of soaittáhat




#!! * **seahkáPl** selects Pl
SELECT:seahkáPl N + Pl (1 ("seahká"));
	#$ Ja soames báikkiin lea smávva sieđga, muhto jos eai leat goikesieđggat, de lea headju buollit, muhto go leat goikan sieđggat seahká, de lea buorre buollit.


#!! * **seammaAdv** selects
SELECT:seammaAdv ("seammá" Adv) IF (1 OLLU OR go)(NEGATE -1 Dem) ;
	#$ Soabadanráđis galget leat golbma miellahtu ja seamma ollu várrelahtut.
	#$ Seamma go ovdalis namahuvvon riektehistorjjálaš ovdanbuktimis...

#!! * **unnanAdv** selects
SELECT:unnanAdv UNNAN + Adv IF (1 VFIN OR PrfPrc LINK NEGATE 0 N);
REMOVE:unnanV V IF (0 UNNAN + Adv LINK *-1 BOC BARRIER WORD);
	#$ Eanaš duopmárat unnán dovdet eamiálbmogiid historjjá.
	#$ Dat orru oppanassii unnán čilgejuvvon 1800-logu gaskamuttu.
SELECT:unnánAdv Adv IF (-1 MOD-ADV-ADV)(0 UNNAN)((1 REAL-V) OR (-2 REAL-V)) ;
SELECT:unnánAdv Adv IF (-1 COPULAS)(0 UNNAN)(1 PrfPrc) ;
	#$ Sii leat hui unnán dahkan guorahallat "sámi vieruid ja riekteáddejumiid".
	#$ Eanetlohku lea menddo unnán bidjan Finnmárkku mihtilmas diliid vuođđun.


#!! * **varraAdv** selects
SELECT:varraN ADV-NOT-N + N IF (0 Adv LINK -1 Superl OR Comp)(1 ("ahte"));
SELECT:varraN ADV-NOT-N + N IF (0 Adv LINK -1 Superl OR Comp)(*1 COPULAS + Sg3 BARRIER NOT-ADV-PCLE);
SELECT:varraN ADV-NOT-N + N IF (0 Adv LINK -1 Superl OR Comp LINK *-1 COPULAS + Sg3 BARRIER NOT-ADV-PCLE);

SELECT:varraAdv1 ADV-NOT-N + Adv IF (*0 (N Nom) BARRIER S-BOUNDARY OR CC);
	#$ Mearkkašahtti Ruonáeatnama ruovttustivrii dál várra lea váttisvuohta ruhtadit buot áššesurggiid hálddašeami.
SELECT:varraAdv2 ADV-NOT-N + Adv IF (-1 CLB)(NEGATE *1 SG3-V);
	#$ Sápmelaččat geat ledje birgegoahtán dušše boazodoaluin, várra ledje buot friddjamusat.
SELECT:varraAdv3 ADV-NOT-N + Adv IF (-1 COPULAS LINK 2 A + Nom);
	#$ Muhtun diliin lea várra váttis beassat eret das.
SELECT:varraAdv4 ADV-NOT-N + Adv IF ((1 Adv LINK 1 ("ahte")) OR (1 ("nu ahte")));
	#$ Dat lea várra nu ahte ...
SELECT:varraAdv5 ADV-NOT-N + Adv IF (*1 VFIN BARRIER NOT-ADV-PCLE LINK NOT 1 Sg3);
REMOVE:varraN N (0 ("várra") + Adv) ; #inntil vi finner flere kontekster for N


#!! * **valjisAdv** selects
SELECT:valjisAdv ("valjis" Adv) IF (*1 N + Pl + Nom BARRIER NOT-NPMODADV) ;
	#$ Čáziin bákŋe valjis guolit.


#!! * **vehaziidAdv** selects
SELECT:vehaziidAdv ("vehážiid" Adv) IF ((-1 ("vehážiid")) OR (1 ("vehážiid"))) ;
	#$ ...mii easkka maŋimuš áiggiid lea vehážiid vehážiid jávkagoahtán.

#!! * **visotdAdv** selects
SELECT:visotAdv ("visot" Adv) IF (1 Dem) ;
	#$ Visot dat stuora mánát leat boahtán.

SELECT:visotAdv ("visot" Adv) IF (1 ("mielde")) ;
		#$ Illá goastadit visot mielde. #this one is ambigious


SELECT:r1426 ("visot" A Attr) IF (0 ("visot"))(1 (A Attr));
	## Jus ulbmilat politihkain galget šaddat duohtan fertejit visot náššuvnnalaš unnitálbmogiid dárbbut ja beroštumit váldojuvvot vuhtii juohke oasis riikkas ja buot servodaga dásiin.
SELECT:r1427 ("visot" A Attr) IF (0 ("visot"))(*1 (N Pl Gen) BARRIER NOT-PRE-A-N);
SELECT:r1428 ("visot" A Attr) IF (0 ("visot"))(*1 (N Pl) BARRIER NOT-NPMOD);
SELECT:r1429 ("visot" A Attr) IF (0 ("visot") LINK 1 Refl LINK *1 (N Pl) BARRIER NOT-NPMOD);
	## Visot mánát leat boahtán.
	## Ii leat vel visot čohkkejuvvon.
	## Visot dan maid NRK sádde digitála fierpmis sáhttá maid gullat neahttaradios. 
	# This one doesn´t work. 

SELECT:visotIndef ("visot" Indef) (*-1 ("leat") + HABEO-SG3 OR ("leat" ConNeg) BARRIER Nom)(1 PrfPrc) ;	



#!! * **vuhtiiAdv**
SELECT:vuhtiiAdv ("vuhtii" Adv) IF ((*-1 ("váldit") BARRIER FMAINV OR CLB) OR (1 ("váldit"))) ;
	#$ ...muhto ferte váldit vuhtii máná agi ja láttasvuođa.
    #$ Eai váldde man ge láhkái vuhtii ahte …
    #$ Sii čállet ahte luhttet dasa ahte eiseválddit šaddet váldit sin oainnu vuhtii.
    #$ … eai leat váldán sámi oahpaheddjiid oainnu vuhtii.

#!! ## Pronouns 

#!! * **recipr, reciprPl** select Recipr

SELECT:recipr Recipr IF ((-1 Recipr) OR (1 Recipr)); 
SELECT:reciprAttr (Recipr Attr) IF (1 Recipr) ; 
SELECT:reciprPl Pl IF (-1 (Recipr Pl))(0 Recipr);
REMOVE:reciprPl Pl IF (-1 (Recipr Du))(0 Recipr);
 	#$ ...de dadje guhtet guoibmáseaset:... 
 	#$ Gielddat berrejit ásahit fásta dulkaveaga, kánske ovttasbarggus nubbi nuppiin.

REMOVE:Killrecipr Recipr IF ;
 	#$ Jos dajan nubbái: Mana! de son manná.

LIST PLUR-LEMMA = "deadja" "gáffe" "gávdni" "muohtu" "spahká" "noađđe#spahká" "mánná#guoddi-#spahká" "urgu" ;

REMOVE:SgPlurlemma PLUR-LEMMA + Pl (0 PLUR-LEMMA) ;

REMOVE:derCar (Der/Car) IF (0 Der/NomAct OR Inf) ;

#!! ### Nouns, not verbs
#    ----------------

#!! * **álbmotN**, ii V.

SELECT:álbmotN ("álbmot" N) (0 V)(-1 Gen) ;

#!! * **headisge**, ii heađisge.
SELECT:headisge ("heahti" Loc) + Foc/ge IF (0 ("heahtit")) ;
	## ii heađisge

#!! * **loahppa** after TIME Gen.
SELECT:loahppa ("loahppa") IF (0 REAL-V OR Adv LINK -1 TIME LINK 0 Gen) ;

REMOVE:r595 VAbess IF (0 ("orohat") OR ("vuojahat"));
	## Guovlu lea orohaga olggobealde.
	
REMOVE:fatnasit ("fatnasit") IF (0 ("fanas"));	 #waiting for examples with the verb

LIST FIRSTPERS = Du1 Sg1 Pl1  (p1 du)  (p1 sg)  (p1 pl) ;


REMOVE:Imprt1p Imprt + FIRSTPERS (0 (N Nom) LINK NOT 1 Inf OR Adv)(NEGATE 0 TRANS-V LINK 1 Acc) ;
	#$ Giitu mu oasis!
	#$ Gilvu golmma joavkku gaskka
	#$ Bivdu ja guollebivdu
	#% Váldot beatnaga mielde!
	#% Gárvodeahkku ja mannu geahččat dien nieidda, gii váccii stuora gápmagiiguin.

REMOVE:HLvfin VFIN IF (0 N + Nom) ((1 (":") OR Rel OR EOS OR HEADLINEMARK LINK -2 BOS) OR (0 (N Gen) LINK 1 ("guovttos"))) ;
	#$ Ohcci: Per M. Buljo , Guovdageaidnu
	#$ Ohcci geas lea earenoamáš gelbbolašvuohta sáhttá árvvoštallojuvvot seniorráđđeaddin.
	#$ Ohcci guovttos áigguiga ovdánahttit áibbas ođđa ja konkrehta buktagiid .
	#$ Álbmot mii ballá ii leat friija álbmot.
	#% Ohcci áhči!


LIST N-NOT-V = "deasta" "viessu" "giella" "lávlla" "muddu" "nieida" "oadju" "skuvla" "guovlu" "golli" ;

REMOVE Imprt (0 N LINK *1 ("leat") - Imprt BARRIER REAL-V OR S-BOUNDARY)  ;
	#$ Giela boahtteáigái lea erenoamáš dehálaš ahte nuorain stuorát proseantaoassi go gaskaleamos buolvvas máhttet hállat kymrigiela .
	
REMOVE (Imprt Sg2) (-1 BOC LINK 2 (Pers Nom) LINK NOT 0 Sg2) ;
	#$ Giela sii gal besse gullat dievva beljiid .

REMOVE Imprt (0 Pron)(1 CS) ;

REMOVE Imprt (0 ("bargi") LINK 1 Sem/Org + Loc) ;
	#$ Bargit Dutkanhálddahusas.


REMOVE Imprt (0 N-NOT-V LINK -1 BOC)(1 VFIN) ;
	#$ Giela oahpat go guldalat, humat, čálát, neavttát ja ovttastalat earáiguin, lohkaba girječállit.

SELECT:Vgielaheapme ("giellat" V) (0 ("gielaheapme")) ;

SELECT:afterSami N (-1 NATIONAL-IDENTITY + Gen)(0 ("dutkan")) ;
	#$ Sámi dutkan
	
REMOVE:LIAABBR ABBR (*1 ("¬")) ;
REMOVE:LIAfiskat Imprt (0 ("fiskat")) ;
IFF:Jos ("jos") + ABBR (0 ("jos") LINK 1 BOS OR Arab) ; 
	
# Rules made especially for LIA spoken corpus: the words often has little context. The rules should be safe.
REMOVE:LIAbivvatDer ("bivvat") (0 ("bivastat")) ;
REMOVE:LIAdassatDer ("dassat") (0 ("dassái") OR ("dat")) ;
REMOVE:LIAmearratDer ("mearrat") (0 ("mearra")) ;
REMOVE:LIAviessutDer ("viessut") (0 ("visot")) ;
REMOVE:LIAolletDer ("ollet") (0 ("olles")) ;
REMOVE:LIAgarratDer ("garrat") (0 ("garas")) ;
REMOVE:LIAviessutDer ("viessut") (0 ("viessu")) ;
REMOVE:LIAáibatDer ("áibat") (0 ("áibbas")) ;
REMOVE:LIAmannetDer ("mannet") (0 ("mannat")) ;
SELECT:LIAoažžudadjat Sg3 (0 ("oažžut") LINK 1 ("dadjat") + Inf) ;
SELECT:LIAoažžu Sg3 (0 ("oažžut") LINK 0 Der/NomAg) ;
REMOVE:LIAváibatDer Der/NomAct (0 ("váibat") LINK 0 Der/NomAct LINK 1 LEAT) ;
REMOVE:LIAlávetDer Der/NomAct (0 ("lávet")) ;
REMOVE:LIAfertetDer Der/NomAct (0 ("fertet")) ;
REMOVE:LIAnuorrat ("nuorrat") (0 ("nuorra")) ;
REMOVE:LIAvarraPx Px (0 ("varra") LINK 0 ("varas")) ;
REMOVE:LIAviessuPx Px (0 ("viessu") LINK 0 ("visot")) ;
REMOVE:LIAoaktutV V (0 ("oaktut") LINK 0 ("okto")) ;
REMOVE:LIAviessatV V (0 ("viessat") LINK 0 ("viessu")) ;
REMOVE:LIAunniditV V (0 ("unnidit") LINK 0 ("unnit")) ;
REMOVE:LIAbuoriditV V (0 ("buoridit") LINK 0 A) ;
REMOVE:LIAgossatV V (0 ("gossat") LINK 0 ("gosa")) ;
REMOVE:LIAovttastitV V (0 ("ovttastit") LINK 0 ("ovttas")) ;
REMOVE:LIAsilditV V (0 ("sildit") LINK 0 ("sildi")) ;
REMOVE:LIAdiehtitV Imprt (0 ("diehtit") LINK 0 ConNeg LINK NOT 1 ("juo")) ;
REMOVE:LIAballatV Imprt (0 ("ballat") LINK NOT 0 ConNeg) ;
REMOVE:LIAeanášitV V (0 ("eanašit") LINK 0 ("eanaš")) ;
REMOVE:LIAdiehtitV Imprt (0 ("ollit") LINK 0 ConNeg) ;
REMOVE:LIAdietnutV V (0 ("dietnut") LINK 0 ("dieno")) ;



SELECT:gáhkkuNom Pl (0 ("gáhkut")) ((-1 CC OR COMMA LINK -1 Nom) OR (1 CC OR COMMA LINK *1 Nom BARRIER NOT-A)) ;
SELECT:gáhkkuAcc Acc (0 ("gáhkut")) ((-1 CC OR COMMA LINK -1 Acc) OR (1 CC OR COMMA LINK *1 Acc BARRIER NOT-A)) ;
SELECT:gáhkkuNom Pl (0 ("gáhkut")) ((-1 V + Pl3) OR (1 V + Pl3)) ;
SELECT:gáhkkuNom N (0 ("gáhkut")) (*-1 Num BARRIER NOT-A) ;
SELECT:skuvla Ill (0 ("skuvlet") LINK *-1 ("mánáidgárdi") + Loc OR ("váikkuhus") BARRIER NOT-NPMODADV) ;
SELECT:skuvlii Ill (0 VFIN LINK 1 Ger) ;
SELECT:filbmii Ill (0 ("filbmet") LINK -1 CC LINK -1 ("teáhter")) ;
	#$ Sámi girjjálaš muitalanárbi ja dan oktavuohta teáhterii ja filbmii.
	
SELECT:duorastat N (0 ("duorastat") LINK NOT -1 ("mii")) ;	

SELECT:ollis A (0 ("ollis") LINK *-1 BOC BARRIER WORD) ;
	#$ Olles Sámis dárbbašuvvojit oahppan oahpaheaddjit mánáidgárddiin.

REMOVE:lean PrfPrc (0 ("leat") + ConNeg)(NEGATE -1 ("leat")) ;


SELECT:logahat N (0 ("logahat") LINK *0 Sem/Edu) ;
SELECT:logahat N (0 ("logahat"))((-1 ("/")) OR (1 ("/"))) ;
	#$ Sisabeassanvuođđu sáhttá leat juogo oppalaš lohkangelbbolašvuohta (čađahan joatkkaskuvlla/logahaga) dahje realagelbbolašvuohta.


SELECT:Vnotverb1 N-NOT-V IF (0 V LINK -1 HUMAN + Gen OR (Pron Gen) OR SAPMI + Gen OR PLACE + Gen OR Attr OR ("buorre" A) OR PrsPrc OR PrfPrc LINK NOT 0 N + Nom) ;
	#$ Geafes olbmá viessu.
	#$ Ođđa viessu boahtte jagi.
	#$ Seamma ládje lea maiddái go Kristus hábme máhttájeddjiid vuoiŋŋalaš viessun ja ealli tempelin.
	#$ – Mu	niidii Sire .
	#% Ráhkkerávjju sámegieloahpaheaddji skuvlii maid eará fágain go sámegielas.

SELECT:Vnotverb2 N-NOT-V IF (0 V LINK -1 go LINK -1 VFIN) ;
	#$ Iskka massá go viessu ollu lieggasa.

SELECT:Vnotverb3 N-NOT-V + Sg IF (0 V LINK -1 VFIN + Sg3) ;
	#$ NVE lea geavahan viesu go leat barggus Kárášjogas dahje Deanus , ja maŋemus lei viessu anus mannan čavčča go Válljoga guovllus ledje barggus.

SELECT:Vnotverb4 N-NOT-V IF (0 $$CASE LINK 0 N OR Pron)(*-1 JA/DAHJE OR COMMA OR NEGFOC BARRIER NOT-NPMOD LINK -1 N + $$CASE) ;
SELECT:Vnotverb4 N-NOT-V IF (0 Imprt LINK 0 N OR Pron)(1 JA/DAHJE OR COMMA OR NEGFOC LINK *1 N + Nom BARRIER NOT-NPMOD) ;
SELECT:Vnotverb4 N-NOT-V IF (0 $$CASE LINK 0 N OR Pron)(1 JA/DAHJE OR COMMA OR NEGFOC LINK *1 N + $$CASE BARRIER NOT-NPMOD) ;
	#$ Dákkár guovlluin ii galgga álggahit doaibmabijuid mat sakka sáhttet muhttit eanadaga hámi dahje mihtilmasvuođa , numo omd. viesut, rusttegat, geainnut ja čievravieččahagat.
	#$ Lean čuoldán searrobáttiid sihke gándii ja niidii.
	#$ Alcces ja niidii, čieža jahkásaš Sarai, ii astta báljo goarrut šat maidege .

SELECT:Vnotverb6 N-NOT-V IF (0 V LINK 1 ("vuovdimassii") OR Rel OR MO OR Po LINK NOT 0 Adv) ;
	#$ Viessu vuovdemassii Kárášjogas.
	#$ Viesus gos jápmán dievdu gávdnui galge lean eambbo olbmot , muhto Guovdageainnu leansmánni Klemetsen ii hálit duođaštit ledje go olbmot viesus ii ge gallis de livčče leamaš.
	#$ Giela dáfus maid leat beahtahallan.
	#% Ráhkkerávjju sámegieloahpaheaddji skuvlii maid eará fágain go sámegielas.
	
SELECT:Vnotverb6 N-NOT-V IF (0 N + Ess LINK -1 Ess) ;	
	#$ Lea go dus leamaš sámegiella nubbin giellan skuvllas álggo rájes?
	
SELECT:Nniidii N (0 ("nieida") OR ("skuvla")) (0 Ill LINK *0 ILL-V OR ("addit") BARRIER REAL-V OR Ill) ;	
SELECT:Nvierut N (0 ("vierru") LINK -1 ("vuoiŋŋalaš")) ;	

SELECT:LexSelsahttit ("sáhttit") (0 ("sáhtašit") LINK *1 Inf BARRIER SV-BOUNDARY) ; # sáhttit Err/Orth
	#$ Sámegiela oahpahusa Roavvenjárggas sáhtašii ain lasihit.	


SELECT:LexSelgullat ("gullat") IF (0 ("gullát") OR ("gullet"))(*1 Ill OR ("vuollai") OR Acc OR ("ahte") BARRIER SV-BOUNDARY LINK NOT 0 SOUND) ;
SELECT:LexSelgullat ("gullat") IF (0 ("gullát") OR ("gullet"))(*-1 Ill OR ("gosa") BARRIER SV-BOUNDARY) ;
	#$ Redaktevrapláhkát gieldá váldodoaimmaheddjiid diktimis eaiggádiid stivret iežaset, ja eanaš friddja áviisat gullet dán šiehtadussii.
	#$ Gosa dat gullá?

SELECT:ealliPrsPrc PrsPrc IF (0 ("ealli") LINK 1 Sem/Lang OR Sem/Org OR Sem/Plc) ;
	#$ Mii dárbbašat ollislaš ja oktasaš giellapolitihka, mii láhčá sámegielaide saji vai šattašedje lunddolaš, servodatguoddi ja ealli giellan min servodagas.


REMOVE:PrsPrcProp PrsPrc IF (0 N LINK 1 Prop) ;

REMOVE:PrsPrc PrsPrc IF (0 LEX-N + Sg)(NEGATE -1 ("nu") LINK 2 go)(NEGATE *1 Acc BARRIER NOT-A-ADV)(NEGATE -1 PLACE + Loc OR Adv OR Po)(NEGATE -1 ("leat") OR BUOT LINK 2 N)(NOT 0 ("gullat")) ;
#(NEGATE 1 CC LINK 1 Attr LINK 1 N)

SELECT:PrsPrc PrsPrc IF (0 LEX-N + Sg)((-1 ("nu") LINK 2 go) OR (-1 ("leat") OR BUOT LINK 2 N) OR (1 CC LINK 1 Attr LINK 1 N) OR (0 ("gullat") LINK *1 N BARRIER NOT-A)) ;
	#$ Dat mearkkaša ahte badjelaš bealli buot oassálasti ohppiin hállet, gullet ja geavahit sámegiela boarrásut olbmuiguin ságastallamis.
	#$ Dainna ii leat vissis mearri galljodagas.
	#% Rusttegat mat fievrridit dahje nuppástuhttet elektrihkalaš energiija oktan dasa gulli elektrihkalaš huksehusaiguin ja huksenteknihkalaš konstrukšuvnnaiguin ...
	# Should not hit cealkkavástaga with PrsPrc + Obj.
	#% Son láhčá diliid nu ahte mis lea ealli ja girjás sámi kultureallin mas lea buorre dássi.

REMOVE:NomcopNom V - AUX (0 (N Sg Nom) LINK NOT 0 VFIN LINK 1 COPULAS LINK 1 (Sg Nom))(NEGATE 0 PrfPrc LINK *-1 ("leat") BARRIER NOT-ADV-PCLE) ;
	#$ Ii ge ságastallan leat vejolaš jus eai leat journalisttat geat máhttet sámegiela bures.
	#% Mun livččen sáhttán leat dronnet.
	#% Dán jagi ii sáhttán leahkit buoret vuoiti Min Áiggi lihkkoluossa gilvvus.

REMOVE:mánnatV V  (0 ("mánnat")) ((-1 OKTA) OR (*0 VFIN + Sg3 BARRIER REAL-V OR (N Sg3)) OR (1 Inf OR Rel)) ;
	#$ Agivuloš mánná gean váhnemat eai eale ovttas, oažžu almmotge eaiggáduššat bohccuid siidaoasis sihke eatni ja áhči bealde .
	#$ Ja de gávnnahin ahte dán ii galgga munno mánná vásihit.
	#$ Mii leimmet njeallje ollesolbmo ja okta mánná geat galggaimet orrut internáhtas juovllaid .
	#$ Dasgo mánná mii lea su siste, lea sahkanan Bassi Vuoiŋŋa bokte.
	#$ Guovllu ja agi mielde, hállá go mánná sámegiela vai ii.

	# čilget vs čielgat 
	# -------------------
SELECT:LexSel ("čilget") IF (0 ("čielgat"))((1 Acc OR ("makkár")) OR (1 CC LINK 1 V + Sg3  LINK 1 Acc OR ("makkár")))  ;
	#$ Sámediggedieđáhus sámegiela birra čilge ja ságaškuššá makkár rolla ja politihkalaš váldi Sámedikkis galgá leat giellagažaldagain.

	
#!! ### Lexical selection - nouns

    # ambigous lemma
    # - - - - - - - -
    # ambigous with respect to case, in which case one should not simply remove lemma2 (such as AMB-LEMMA2 Nom could be AMB-LEMMA1 Gen/Acc)
            
LIST AMB-LEMMA1 =  (".*Afrihká"r) (".*Amerihká"r) (".*bearal"r) (".*bovra"r) 
(".*fenomena"r) (".*gámmir"r)  (".*guolbba"r) ("Juhán") ("Ravna")
(".*kaleandar"r) "keaissir" "lassá" (".*lassá"r) (".*kapihtal"r)(".*meašttir"r) 
(".*partihkal"r) "savu" "skážir" (".*skibir"r) (".*speajal"r) (".*spigir"r) "varis" (".*varis"r)  ;

LIST AMB-LEMMA2 =  (".*Afrihkká"r) (".*Amerihkká"r) (".*bearral"r)  (".*bovrra"r) 
(".*fenomen"r) (".*gámmár"r)  (".*guolban"r)  ("Juhá") ("Ravn")
(".*kaleanddar"r) (".*kapihtal"r) "keaisár" "lasá" (".*lasá"r) (".*meaštár"r) 
(".*partihkkal"r) "savvon" (".*skihpár"r) "skáhčir" (".*spihkár"r) (".*speadjal"r) "varris" (".*varris"r) ;

REMOVE:amblemma AMB-LEMMA2 IF (0 AMB-LEMMA1 LINK NEGATE 0 (Sg Nom) LINK NOT 0 Px);

LIST LEMMA-ADJ1 = "asehaš" "čáppat" "eahpesihkar" "guorus" "njálggat" "oppalaš" "sihkar" ;

LIST LEMMA-ADJ2 = "aseheapme" "čáppis" "eahpesihkar" "guoros"  "njálggis" "oppal" "sihkar" ;

REMOVE:lemmaAdj LEMMA-ADJ2 + A IF (0 LEMMA-ADJ1);

	# nisson, skihpár, spihkár jnv. 
	# - - - - - - - - - - - - - - -
SELECT:r622 (".*nisu"r) (0 (".*nisson"r) LINK NOT 0 (Sg Nom)) ;
SELECT:r623 (".*nisu"r) (0 (".*nisson"r) LINK 0 (Sg Nom) LINK 0 Px) ;
  	#$ Gielddat sáhttet eambbo ulbmillaččat stivret iežaset servodaga ealáhusovdáneami, nu ahte sin gielda šáddá buore gieldan maiddái nissoniidda orrut.

	# girjái A vs. girjjis N
	# - - - - - - - - -	
SELECT:LexSel Attr (0 ("girjái") LINK 1 N)  ;	 

	# guhkki A vs. guhkkat V
	# - - - - - - - - -	
REMOVE:LexSel V (0 ("guhkki"))  ;	 

	# áddjá N vs. áddjái A
	# - - - - - - - - -	
SELECT:LexSel ("áddjá")(0 A LINK -1 Attr)  ;	
	## ja dat lei jábálaš áddjá das lei guokte návet šibihat dievva . (LIA)
REMOVE:LexSel A (0 ("áddjá") LINK NOT -1 ("nu") OR GRADE-ADV)  ;	 


	# albmi vs. almmái
	# - - - - - - - - -	
SELECT:LexSel ("albmi")(0 Ill LINK *-1 ("čohkket") OR ("ihtit") OR ("bajás") OR ("máhccat") OR ("váldit") OR ("mannat") OR ("čállit" Der/PassL) BARRIER SV-BOUNDARY) ;	
	## Muhto čohkkejehket alccceseattet dávviriid albmái.

SELECT:LexSel ("albmi")(0 Ill LINK *1 ("rahpat") BARRIER SV-BOUNDARY) ;	
SELECT:LexSel ("almmái")(-1 ("noađđi")) ;	

    # áitit vs. náitit
    # - - - - - - - - -
SELECT:r626 ("goddin#áitit") IF (0 ("goddi#náitit")); 


    # bassi vs bassi N G3
    # - - - - - - - - -
REMOVE:bassiEss NomAg OR Der/NomAct IF (0 ("bassi" Ess) LINK 1 VFIN);    
REMOVE:bassi1 N IF (0 (A Ess) LINK NOT 0 Der/NomAct LINK *0 OPRED-V)(0 ("bassi")) ;  
	## Maŋŋá jápmimis de aliduvvui Gonagas Olav bassin. 
	#% Son atná iežas sápmelažžan. #Needs one for N Ess aswell?  
	# Try to make a general rule.
	 
	
SELECT:bassi2 (A Attr) IF (0 ("bassi") LINK 1 (N Ess) OR ("vuoigŋa") OR ("báiki") OR ("guovlu")) ;
	## Suomagiella šattai bassi giellan.
	## Bassi Vuoiŋŋas dievva Jesus máhcai Jordanis

SELECT:bassi5 (A Attr) IF (0 ("bassi") LINK 1 N )(NEGATE -1 Gen) ;

	
SELECT:bassi3 NomAg IF (0 ("bassi" Sg Nom) LINK *-1 (N Gen) BARRIER NOT-NPMODADV LINK NOT 0 NATIONAL-IDENTITY) ; 
	## Gulan go Bienne Káre-Ánne, skuvlla bassi, boahtá. 

	
SELECT:bassi4 NomAg IF (0 ("bassi" N Ess) LINK *-1 ("bargat" V TV)) ;
	## Mun lean bargan bassin. 
  	
    # bára vs. bárra
    # - - - - - - - -
SELECT:bar ("bára") IF (0 ("bárra"))(*0 ("čohkkát") OR ("juhkat") OR LIQUID);
SELECT:par ("bárra") IF (0 ("bára"))(*0 DU-V);

    # biile vs. biila
    # - - - - - - - -
SELECT:r638 VEHICLE IF (0 (".*biile"r));
    
    
    # boahtu vs. boađđu vs. boađus
    # - - - - - - - - - - - - - - -

SELECT:r660 ("boađus") IF (-1 COPULAS)(0 ("boahtu"))(1 Num);
SELECT:r661 ("boađus") IF (0 ("boahtu"))(1 COPULAS)(2 Num);
	## Maŋŋil láivves vuosttaš vuoru lei boađus 0-0.

	# borri
	# - - - -
REMOVE NomAg (0 ("borri") LINK *-1 (Num Gen))	;


    # čiekči vs. čiekčá
    # - - - - - - - - -
SELECT:r665 ("čiekči") (0 ("čiekčá") LINK *0 ("spábba") BARRIER S-BOUNDARY);



	# čuođi vs čuohti
	# - - -
SELECT:čuohti1 Num IF (0 ("čuđđi") OR ("čuhti"))(1 MEASURE OR ("mielde") OR ("miel")) ;
SELECT:čuohti2 ("čuohti" Num) IF (-1 ("beannot")) ;
  	## Sin jahkásaš johtolagat sáhtte leat čuđiidge kilomehteriid, ja olle dálá Suomabeale vuovdeguovlluid dálveorohagain Jiekŋameara rittu geasseorohagaide.

REMOVE:davviEss Ess (0 ("davvi") OR ("máddi") OR ("lulli") OR ("oarji") OR ("nuorti") LINK 0 Adv) ;  	

    # eamit vs. eapmi
    # - - - - - - - -
REMOVE:r675 ("eapmi" Pl) IF (*1 SG-V BARRIER VFIN OR S-BOUNDARY);

	# eana
	# - - - 	
LIST EANAN = "eana"  ;    

REMOVE:r677 Sg IF (*-1 ("stáhta") OR ("priváhta") OR ("matrikuleret"))(0 EANAN + (Pl Loc));
	#$ Stáhta eatnamiin.
SELECT ("eana") (0 EANAN) ; 


	# eallin
	# - - - 	
SELECT:eallin ("eallin") (-1 ("agálaš")) ; 

	# fáddá
	# - - - 	
SELECT:LexSelfáddá ("fáddá") IF (0 ("fáddá")); # fádda	fáddá+Err/Orth-a-á+N+Sg+Nom Foreløpig regel, fádda er veldig sjeldent ord

 
	# fálli
	# - - - 	
SELECT:LexSelfálli ("fálli" NomAg) IF (*0 ("fitnodat"));
SELECT:LexSelfálli ("fálli" NomAg) IF (-1 ("vulobeale"));
	#$ Sis leat olu Finnmárkku fitnodagat fárus vulobeale fállin.

    # geassi vs. geasis vs. geassi
    # - - - - - - - - -
REMOVE NomAg (0 ("geassi") LINK -1 DURATION-A OR TIME-A) ;
REMOVE NomAg (0 ("geassi")) ; #  This is preliminary

	# gielda vs gieldda
	# - - - - - - - - -
SELECT:LexSelgielda1 ("gielda") IF (-1 Prop + Sem/Plc);
SELECT:LexSelgielda1 ("gielda") IF (0 Gen LINK 1 N);
SELECT:LexSelgielda2 ("gielda") IF (1 JA/DAHJE LINK 1 ACTOR-ROLE OR Prop OR ("fylkkadiggeválga") OR ("válga"));
	#$ Gieldda ja Finnmárkkuopmodaga áššegieđahallamii gusto hálddašanláhka.
  	#$ Deanu gieldda

SELECT:r682 ("gielda") IF (-1 FYLKA + Gen);

     
	# gilvu vs. gilvvu Adv
	# - - - - - - - - - - 
SELECT:LexSelgilvu ("gilvu") (0 Adv LINK *0 ("vuoitit") BARRIER SV-BOUNDARY);

	# goddi vs. goddi NomAg
	# - - - - - - - - - - 
REMOVE:LexSelgilvu NomAg (0 ("goddi") LINK *1 Sem/Ani-fish);

	# guovddáš 
	# - - - - -
SELECT:r693 Attr IF (0 ("guovddáš"))(1 N LINK NOT 0 TIME OR PLACE OR VFIN); 
	## Guovddáš doaibmabidju lea vuođđudit sámegiela álgooahpahusa.
REMOVE:guovddasAttr Attr IF (0 ("guovddáš") LINK -1 Attr) ; 
	#$ Ođđa alit guovddáš Romsii.

    # gurra
    # - - -
SELECT:r697 ("bákte#gurra") IF (0 ("bákte#guorra"));
SELECT:r698 ("lássa#gurra") IF (0 ("lássa#guorra"));
 
    	
    # juogus vs. jugus
    # - - - - - - - - -

LIST JUGUS-V = "juhkat" "návddašit" "šlaggat" "vuoššat" ;

SELECT:jugus1 ("jugus") (0 ("juogus" Acc) LINK *0 JUGUS-V BARRIER S-BOUNDARY) ;
	## Áhčis biográfiijas Vest geažida mo duon erenomáš juhkosa návddašeapmi skihpáriiguin lei šaddagoahtán dehálaš áigegollun maŋŋá go lei máhccan ruoktot.

SELECT:jugus2 ("jugus") (0 ("juogus") + $$CASE LINK *0 FOOD + $$CASE BARRIER S-BOUNDARY) ;
 	## Mun válddán juhkosa ja márffiid mielde. 

SELECT:jugus3 ("juogus") IF (-1 NATION + Gen OR SUOPMA + Gen OR SAPMI + Gen)(0 ("jugus")) ;
# LINK NEGATE *0 ("juhkat"));
	## Maŋŋel bođii Suoma juhkosa evttohus.

    # juste
SELECT:juste1 Adv IF (-1 V) (0 ("juste"));    
SELECT:juste2 Adv IF (0 ("juste"))(1 Nom);    

	# lága vs láhka
	# - - - -
  
SELECT:r712 ("lága") IF (0 ("lága"))(-1 ("eará") OR ("iežá"))(*-1 BOS LINK *1C ("lága")) ;
  	## Lága gáibida menddo olu eará lágaid ektui.
  	## Sii bidjet menddo stuora gáibádusaid min lágii eará lágaid ektui.
SELECT:r713 LAGA IF (0 LAGA) ;

	# láhka
	# - - -
 # The point here is to disambiguate "láhka" from "lága". 

SELECT:r714 LAHKA - Px IF (0 LAHKA LINK NOT 0 Der/laakan);
  	## Čujuhuvvo maiddái sierramotiivvaide dearvvašvuođalága 45. paragráfii čuolbmačilgehusaid birra jávohisvuođageatnegasvuođa ja geigenlobi ektui epikriissa oktavuođas (oasi dearvvašvuođabargiidlágas oktan mearkkašumiiguin).

SELECT:r716 ("láhka") IF (0 ("lága"))(*1 ("fápmu") BARRIER V - COPULAS LINK 1 ("boahtin"));

#SELECT:r717 ("láhka") IF (0 ("lága") LINK *1 LAHKA-N OR ("kriminaliseret") BARRIER V - COPULAS);
	## Norgga stáda lea iešguđet lágaid ja láhkaásahusaid bokte geatnegahtton.
	## Sosiálabálvalusaid lága § 4-3.
  	
#SELECT:r718 ("láhka") IF (0 ("lága"))(*-1 LAHKA-N OR ("gč") OR ("kriminaliseret") BARRIER V - COPULAS) ;
	## Gč. lága suoidnemánu b. 1992 nr. 97.	
  	## Fápmuiboahtin ja eará lágaid rievdadusat.
  	## Gonagas addá láhkaásahusa lága geavaheami birra.

#TEMPLATE lahkanoun = (*-1 LAHKA-N OR ("gč") OR ("kriminaliseret") BARRIER V - COPULAS) OR (*1 LAHKA-N OR ("kriminaliseret") BARRIER V - COPULAS) ;

#SELECT ("láhka") IF (T:lahkanoun)(0 ("lága")) ;

#SELECT ("láhka") IF ((*-1 LAHKA-N OR ("gč") OR ("kriminaliseret") BARRIER V - COPULAS) OR (*1 LAHKA-N OR ("kriminaliseret") BARRIER V - COPULAS)) ;


SELECT:r719 ("láhka") IF (0 ("lága"))(-1 ("rievdadit") OR ("jorgalit") LINK 0 Der/Pass);
SELECT:r720 ("láhka") IF (0 ("lága"))(*1 ("rievdadit") OR ("jorgalit") BARRIER NOT-AUX-V LINK 0 Der/Pass);
	## Rievdaduvvon láhka....
	## Ođđa lágat galget jorgaluvvot sámegillii.

SELECT:r721 ("láhka") IF (*-1 MANNU LINK 1 Ord LINK 1 ABBR LINK 1 Num)(0 ("lága"));
	## Dat lea dahkkon skábmamánu 28. b. 1898 lága mielde. 	

SELECT:r722 ("láhka" Loc) IF (0 ("lága"))(*-1 BOC LINK *1 LAHKA-V-PASS LINK 0 Der/Pass) ;
	## Dat lea mearriduvvon lágas 33.
  	## Ii adnon norgga lágas daid attáldagaid birra maid dáččat mákse iežaset gonagassii.

SELECT:r723 ("láhka") IF (0 ("lága" Loc))(*-1 ("reguleret") OR ("sihkkarastit") OR ("ovdanboahtit") OR Der/Pass + ("oaidnit") BARRIER REAL-V OR S-BOUNDARY) ;
	## Dan ferte reguleret lágain.
     
SELECT:r724 ("láhka") IF (0 ("lága"))(1 ("guoskat") OR ("gustot")) ; 
SELECT:r725 ("láhka") IF (0 ("lága"))(*1 Rel BARRIER WORD LINK 1 ("guoskat") OR ("gustot")) ;        
	## Lágat gusket moadde gielddaide Norrbottena leanas.  
	## Vuođđun leat maiddái dálá lágat, mat gustojit mánáid- ja nuoraidsuorgái.
     
SELECT:r726 ("láhka") IF (*1 ("mearridit") OR ("čuoldit") BARRIER VFIN)(0 ("lága" Sg Gen))(*1 Po BARRIER V);
	## Dat lea mearriduvvon lága 3 mielde. 
SELECT:r727 ("láhka") IF (*-1 Gen BARRIER NOT-A LINK 0 LAHKA-MOD OR NATION-NOT-NORGA)(0 ("lága"));
  	## Ráđđehus háliida lágidit dearvvašvuođa- ja sosiálabálvalusaid oktagaslaš dárbbuid mielde, earret eará pasientavuoigatvuođaid lágain ja dearvvašvuođabargiid lágain.
  
SELECT:r728 ("láhka") IF (-1 LAHKA-A)(0 ("lága"));  
  
SELECT:r729 ("láhka") IF (0 ("lága"))(1 LAHKA-POST)(*-1 BOC LINK *1 LAHKA-N OR LAHKA-V-PASS) ;
SELECT:r730 ("láhka") IF (0 ("lága"))(1 LAHKA-POST)(*1 LAHKA-N OR LAHKA-V-PASS BARRIER S-BOUNDARY) ;
  # In addition to a LAHKA-POST, there is a noun or pass.verb which make it more likely to be "láhka".
  	## Sis lea leamaš geatnegasvuohta čállit journálaid maiddái ovddeš lágaid mielde.
  	## Lága mielde lei vejolaš oažžut albma eanangirjji mii dáhkidii  oamastanvuoigatvuođa gávpe- ja árbeoktavuođain ge.

SELECT:r731 ("láhka") IF (0 ("lága"))(*-1 BOS LINK *1 LAHKA-SUBJ-N LINK 0 NP-HEAD-NOM LINK *1 LAHKA-V) ;
  	## Guovddášeiseválddit árvaledje lága vai sidjiide livčče vejolaš váikkuhit Finnmárkku čearddalašvuođadiliide.
  
SELECT:r732 ("láhka") IF (0 ("lága"))(*-1 BOS LINK *1 LAHKA-V LINK *1 LAHKA-ASAHUS LINK 0 Ill OR Loc) ;
  	## Dáidá šaddat váttis ovddidit lága Stuoradiggái dál.  
  
SELECT:r733 ("láhka") IF (0 ("lága"))(-1 ("čuovvut") OR ("čuovvulit")) ;
  # If it´s "lága", then one should expect a Gen., like "sin lága, gávpoga lága".   
  	## Olbmot eai čuovo lága.  

SELECT:r734 ("láhka") IF (0 ("lága" Gen))(*1 LAHKA-GEN-N BARRIER NOT-A) ; 
  	## Dan oaidná lága ráhkkananbarggus.

SELECT:r735 ("láhka") IF (0 ("lága"))(1 LAHKA-POST)(2 ("lohpi") OR LAHKA-N) ; 
SELECT:r736 ("láhka") IF (-1 ("lohpi") OR LAHKA-N)(0 ("lága"))(1 LAHKA-POST) ;    
  	## Sis lei lága mielde lohpi vuosttaš vuovdimis prioriteret dážaid sámiid ja láddelaččaid ovddabeallái. 
  	## Sis lei lohpi lága mielde.    
  
SELECT:r737 ("láhka") IF (0 ("lága"))(1 ("olis") OR ("vuođul") OR ("ovdal")) ;
  	## Ii mihkkege hehtte nammadeames ovddasvástideaddji maiddái dakkár doaimmain mat eai adno 
  	## dearvvašvuođaásahussan lága olis.  
  	
SELECT:r738 ("láhka") IF (0 ("lága") LINK *1 ("vuostá" Po) BARRIER NOT-N LINK 1 ("váldit") LINK 1 ("vuhtii"));
	## Mii geahččat bearrái ahte lágat vealaheami vuostá váldojuvvojit vuhtii.
 
  	  
SELECT:r739 ("láhka") IF (0 ("lága" Acc))(*-1 BOC LINK *1 LAHKA-V) ;
  	## Ja de bohciida jearaldahkan leat go sii geat dán lága čálle, bidjan sápmelaččaid sisriikalažžan.
  	## Stáhta galgá jorgalit lága sámegillii. 
	# láhka in coordination

SELECT:r740 ("láhka") IF (0 ("lága"))(-1 ("eará") OR ("iežá"))(*-1 BOS LINK *1C LAHKA) ;
  	## Dan áiggi rájes go láhka fápmui biddjo, dahkkojit čuovvovaš njuolggadusat eará lágain.
  
SELECT:r741 ("láhka") IF (0 ("lága"))(*-1 LAHKA) ; 
SELECT:r742 ("láhka") IF (0 ("lága"))(*1 LAHKA) ; 
  	## Go teavsttas lea guhkes láhkanamahus, de sáhttá lága namahusa bidjat aisttonmearkkaid sisa.
  # This one might make trouble for "lága".  

SELECT:r743 ("láhka") IF (0 ("lága"))(1 CC OR COMMA LINK 1 ("konvenšuvdna") OR ("láhkaásaheapmi") OR ("láhkaovdabargu") OR ("politihkka") OR ("ekonomiija") OR ("riektegeavat") OR ("hálddašit" Der/NomAct));
SELECT:r744 ("láhka") IF (0 ("lága"))(-1 CC OR COMMA LINK -1 ("konvenšuvdna") OR ("láhkaásaheapmi") OR ("láhkaovdabargu") OR ("politihkka") OR ("ekonomiija") OR ("riektegeavat") OR ("hálddašit" Der/NomAct));  	
	## Gihpa gieđahallá maid vuoigatvuođaid, mat vuođđuduvvet lágaide ja konvenšuvnnaide.
  	## Sámi perspektiiva lea mielde dehálaš dearvvašvuođa- ja sosiálpolitihkalaš guorahallamiin ja mearrádusdokumeanttain, nugo lágain, láhkaásahusain ja bagadusmateriálain.
  
SELECT:r745 ("láhka") IF (*-1 CC BARRIER NOT-NPMOD LINK -1C LAHKA OR TEXT OR ("stáhta"))(0 ("lága"));
SELECT:r746 ("láhka") IF (*1 CC LINK *1C LAHKA OR TEXT OR ("stáhta") BARRIER NOT-NPMOD)(0 ("lága"));
  	## Máŋggabealatvuohta ja ovttadássásašvuohta čuovvula maiddái ođđa pasientavuoigatvuođalágaid ja lága dearvvašvuođabargiid birra.
     

    # lávka/lávki vs. lávke
    # - - - - - - - - - - -
SELECT:LexSel ("jiehtanas#lávki") IF (0 ("jiehtanas#lávke"));
SELECT:LexSel ("lávki") IF (0 ("lávke" Com) LINK -1 (Num Com)) ;

  	# logi
  	# - - -
SELECT:r749 ("logi" Pl) IF (0 ("loahki") LINK *1 (N Pl) BARRIER NOT-A);  
	## Almmiriika sáhttá veardiduvvot logiin nuorra nieiddain geat válde lámppáset ja vulge olggos irggi ovddal.
	#$ Son lea logiid fáhcaid juo láhppán.
 

    # luohkká vs. luohká vs. luohkki
    # - - - - - - - - - - - - - - - -
LIST LUOHKKA = "gártnet#luohkká"  ;

SELECT:r757 LUOHKKA IF (0 LUOHKKA);

	# luopmu vs. lupmu (we don´t know the semantics of the latter one)
	# - - - - - - - - -
SELECT ("luopmu") (0 ("lupmu")) ;	
   	
  	# mális vs málli
  	# - - - - - - - -
REMOVE:r3586 ("málli") IF (*0 ("duoldat") BARRIER SV-BOUNDARY)(0 ("mális" Nom));  
REMOVE:r3586 ("málli") IF (*-1 go BARRIER NOT-NPMOD LINK -1 ("duoldat"))(0 ("mális" Nom));  

	## Duolddaža go mális vel?	
  
    # mánni vs. mánná vs mánná G3
    # - - - - - - - -
REMOVE G3 (0 ("mánná") LINK NEGATE *-1 ("borrat") BARRIER NOT-ADV-PCLE LINK *1 NP-HEAD-NOM BARRIER NOT-ADV-PCLE) ; # Vi må lage flere regler for når det kan være G3. i NT er det mánna

    # mánnán N or PrfPrc, because of sub-path -án PrfPrc
    # - - - - - - - -
REMOVE ("mánnat" PrfPrc)(0 ("mánná" Ess))((NEGATE *-1 ("leat") BARRIER NOT-ADV-PCLE) OR (1 N)) ;

	# meassut
	# - - - - -
SELECT ("meassut") (0 ("meassu")); 	


	# meastagis
	# - - - - -
SELECT:r764 N IF (0 ("measta" Adv Foc/gis)); 	
SELECT:r764 A IF (0 ("sámegielat" A) LINK 0 Foc/gis) ; 	


    # moalla vs. molla
    # - - - - - - - - 
LIST MOALLA-V = "báhčit" "boahtit" "bidjat" "bidjalit" "coggalit" "čiekčat" "čievččastit" "deaddilit"  "dearpalit" "fieraldahttit" "galkat" "gárrut" "joavdat" "loktestit" "nahkehit" "nivkalit" "ollet" "roaiskalit" "siktet" "viehkat" ;  
# when "moalla" Ill

SELECT ("moalla") IF (-1 ("rabas"));  
SELECT:r766 ("moalla") IF (*-1 MOALLA-V BARRIER SV-BOUNDARY)(0 ("moalla" Ill));  
SELECT:r768 ("moalla") IF (*-1 ("bidjat") OR ("mannat") BARRIER SV-BOUNDARY)(0 ("moalla" Ill));  

    # násti vs. nástá
    # - - - - - - - - - - 
REMOVE ("nástá") IF (0 ("násti") LINK *1 ("čuovgat") BARRIER SV-BOUNDARY) ;
	## Nieida bođii ráhkkásis mielde.


    # ráhkis vs. ráhkká
    # - - - - - - - - - - 
REMOVE ("ráhkká" Ill) (0 ("ráhkis")) ;
	## Nieida bođii ráhkkásis mielde.

SELECT Attr (0 ("ráhkis") LINK *-1 BOS BARRIER WORD)(1 Indef + Nom OR N + Nom LINK 1 EOS OR COMMA) ;
	#$ Ráhkis buohkat,	

    # reavri vs. revre
    # - - - - - - - - -
SELECT:r773 ("ruovde#revre") IF (0 ("ruovde#reavri"));   

    # riggi vs. rikkis
    # - - - - - - - - -
SELECT:r774 ("riggi") IF (*0 ("dolla") OR ("lávvu") OR ("báhti") OR ("árran") OR ("heaŋgát") BARRIER S-BOUNDARY)(0 ("rikkis")) ;
 

    # ruoktu vs. rukto/ruktu
    # - - - - - - - - - - - -
#SELECT:r777 ("ruoktu") + Px ;
	## Ale bala váldimis Márjjá ruktosat áhkkán.

SELECT:r778 ("poasta#ruktu") IF (0 ("poasta#ruoktu"));
SELECT:r779 ("turnea#ruvtto") IF (0 ("turnea#ruktu"));

    # ruvke vs ruvki
    # - - - - - - - -
SELECT:r780 ("urána#ruvke") IF (0 ("urána#ruvki"));

	
	# Prop Plc Der/t 
	# - - - - - -
REMOVE:r782 Prop + (Der/t) IF (0 ("sápmi") OR ("ruošša") OR ("duiska"));

    # sáhka vs. sáhkat
    # - - - - - -- - -
#!! * **sahkaEss** if Mii lea sáhkan.
SELECT:sahkaEss ("sáhka" Ess) IF (0 PrfPrc LINK -1 COPULAS LINK -1 ("mii" Nom)) ;    

#!! * **sahkaPl** after PLURALIZER in NP
IFF:sahkaPl ("sáhka" Pl) IF (0 TV LINK *-1 PLURALIZER BARRIER NOT-NPMOD) ;    


	# sissi vs siessá
	# - - - - - - - -
REMOVE:r783 ("siessi") + Px IF (0 ("siessá"));
REMOVE:r783 ("siessi") + Ill IF (0 ("siessá") LINK NEGATE *0 MOVEMENT-V OR ("čohkkedit") BARRIER SV-BOUNDARY);
		

	# soadji vs. sodji
	# - - - - - - - - -
SELECT:r785 ("soadji") IF (0 ("sodji"))(1 BIRD LINK 0 Gen);
	## Eará symbola lei Gáissáža soajit, ja manin dás geavahedje juste lotti sojiid?

    # soahti vs. sohti/sođđi soađis
    # - - - - - - - - - - - -
SELECT:r787 ("Krim#soahti") IF (0 ("Krim#sohti"));
SELECT:soahti ("soahti") IF (0 ("soađis") LINK *0 ("goddot") OR ("goddit"));
  
 	# suorgi vs. suorggis
	# - - - - - - - - - -
SELECT:r789 ("suorgi") IF (-1 ("priváhta"))(0 ("suorggis"));
SELECT:r790 ("áittardan#suorgi") IF (0 ("áittardan#suorggis"));
  	## Mot lea sámi nissoniid posišuvdna obbalaččat bargoeallimis (vuođđoealáhusain, almmolaš- ja priváhta suorggis , smávva fitnodagain je.) ja erenoamážit go leat bargin sámi ásahusain ja organisašuvnnain ?
  # A coordination rule might work in this case too.


	# testameanta vs. testamentet V
	# - - - - - - - - - -
REMOVE:r792 V IF (-1 ("ođas"))(0 ("testameanta"));	

	# vihtta Num vs. vihtta N
SELECT:vihtta Num IF (*-1 ("diibmu"))(0 ("vihtta"));	



	# viergi vs. virgi
	# - - - - - - - -
LIST VIRGI =  (".*virgi"r) ;

LIST VIRGI-V = "almmuhit" "árvalit" "ásahit" "bealljemerket" "dagahit" "deavdit" "geahpedit" "gilvalit" "gustot" "guoskat" "hálddašit" "heaittihit" "lasihit" "nannet" "ohcat" "ruhtadit" ;

LIST VIRGI-ADJ = "almmolaš" "bealle" "bissovaš" "bistevaš" "dieđalaš" "fásta" "girkolaš" "hálddahuslaš" "lassi" "mearrideaddji" "rabas" "sadjásaš" "2/3" "7,5" ;

# Verbs are in Der/NomAct:
LIST VIRGI-N = "ásaheapmi" "bálká" "deavdin" "doaibmagollu" "gollu" heaittiheapmi "lohku" "virgáibidjan" ;

LIST VIRGI-LOC-N = "almmuhusteaksta" "vuorrobargu" ;

SELECT:r796 VIRGI IF (0 VIRGI) ;

SELECT:r797 ("virgi") IF (*-1 ORGANIZATION OR INSTITUTION BARRIER NOT-A LINK 0 Gen)(0 ("viergi"));
	## Nuoraidskuvlla virggiin lea buorre bálká. 
SELECT:r798 ("virgi") IF (0 ("viergi"))(1 ORGANIZATION OR INSTITUTION LINK 0 Loc);	
	## Dat sáhtášedje maid viššaleappot čuovvulit báikegotti studeanttaid ja fállat daidda virggiid ruovttugielddas. 

SELECT:r799 ("virgi") IF (-1 PROFESSION)(0 ("viergi"));
  	## Eai leat lahkage doarvái logopedat, geat dikšot geavaheaddjiid klinihkain, eaige sámiid ássanguovllus leat galle gieldda, mat leat ásahan logopeda virggiid.
 
SELECT:r800 ("virgi") IF (-1 VIRGI-ADJ)(0 ("viergi")) ;
	## ... almmolaš virggiid .. 
SELECT:r801 ("virgi") IF (*-1 VIRGI-V BARRIER SV-BOUNDARY)(0 ("viergi" Acc)) ; 
SELECT:r802 ("virgi") IF (*1 VIRGI-V BARRIER SV-BOUNDARY)(0 ("viergi" Acc)) ;
	## Olbmot eai oza diekkár virggiid. 
	## Hálddahus berre nannet suorggi ovttain virggiin.

SELECT:r803 ("virgi") IF (0 ("viergi") LINK *-1 ("bidjat") OR ("oažžut") OR ("gáibidit") OR ("beassat") OR  ("háliidit") BARRIER SV-BOUNDARY LINK *-1 ("olmmoš") OR ("fágaolmmoš") OR ("kandidáhta") OR EDUCATION OR HUMAN BARRIER NOT-A-ADV) ;
SELECT:r804 ("virgi") IF (0 ("viergi") LINK *1 ("bidjat") OR ("oažžut") OR ("gáibidit") OR ("beassat") OR  ("háliidit") BARRIER SV-BOUNDARY LINK *1 ("olmmoš") OR ("fágaolmmoš") OR ("kandidáhta") OR EDUCATION OR HUMAN BARRIER NOT-A-ADV) ;
	## virggiide biddjojit olbmot
	## oažžut olbmuid virggiide
    
SELECT:r805 ("virgi") IF (*0 ("fállat") OR ("beassat"))(0 ("viergi" Ill)) ;  

SELECT:r807 ("virgi") IF (1 ("guoskevaš") LINK 1 ("resursa"))(0 ("viergi" Ill)) ; 
SELECT:r808 ("virgi") IF (*1 ("hálddahus") BARRIER NOT-NPMOD)(0 ("viergi" Ill)) ;   
SELECT:r809 ("virgi") IF (0 ("viergi" Ill) LINK *-1 ("ohcat") BARRIER SV-BOUNDARY);  
    
SELECT:r810 ("virgi") IF (0 ("viergi") LINK *-1 BOS LINK *1 ("virgáibidjat") OR ("váldo#bidjan") OR ("bálká")) ;    

SELECT:r811 ("virgi") IF (0 ("viergi" Gen))(*1 VIRGI-N BARRIER NOT-A) ;
	## Mii váldit Finnmárkku olggobeale virggiid goluid, maiguin ásahit dakkár gelbbolašvuođa...
  
SELECT:r812 ("virgi") IF (*-1 ("luohpat") OR ("bissut") OR ("bidjat") BARRIER NOT-ADV)(0 ("viergi" Loc));
SELECT:r813 ("virgi") IF (0 ("viergi" Loc))(*1 ("luohpat") OR ("bissut") OR ("bidjat") BARRIER NOT-NPMOD);
   	## Virgeloguid lea suohkan dássážii geahpedan dainna lágiin go olbmot luhpet virggiin.
   	## Doavttir lea máŋgii biddjon eret virggiin miehtá Suoma.
   
SELECT:r814 ("virgi") IF (0 ("viergi" Loc))(1 COPULAS)(*2 HUMAN OR VIRGI-LOC-N BARRIER NP-HEAD-NOM LINK 0 Nom); 
SELECT:r815 ("virgi") IF (*-1 HUMAN OR VIRGI-LOC-N BARRIER NOT-NPMOD OR REAL-V LINK 0 NP-HEAD-NOM)(0 ("viergi" Loc));  
	## Virggiin leat nu ollu sámegielat doaktárat go vejolaš. 
	
SELECT:r816 ("virgi") IF (*0 ("resursavátnivuohta") OR ("servodatsuorgi") BARRIER NOT-NPMOD)(0 ("viergi" Gen))(1 ("ektui"));	

SELECT:r818 ("virgi") IF (0 ("viergi" Loc) LINK 1 COPULAS LINK 1 VIRGI);
	## Okta dain virggiin lea giellamielbargivirgi.
	
	# vuorru
	# - - - -
REMOVE G3 (0 (".*#vuorru"r))	; 
#REMOVE G3 (0 ("vuorru") LINK 0 (<cmp>) OR Cmp)	; 
#REMOVE G3 (0 ("vuorru") LINK SUB:1 (<cmp>))	; #apertium 
REMOVE G3 (0 ("vuorru") LINK -1 HAB-ACTOR + Gen)	;
	## Odne lea mu vuorru.
	## Vuorru lea leamaš jagi jogas.
SELECT G3 + Nom (0 ("vuorru") LINK *0 MOVEMENT-V BARRIER SV-BOUNDARY)	;


    # vuovdi
    # - - - -
    
LIST FOREST-V = "boaldit" "hukset" "šaddadit" "njáskat" "njiedjat" "njeaidit" "čuollat" "čorget" "njiedjat" "ođasmuvvat" "ritnut" "čáhpodit" "ávkkástallat" "šuvvat" ;
 # "hukset" does possibly not belong to this set, same goes for "šaddadit". May need spesific rules.

LIST FOREST-ADJ = "galmmas" "suohkat" "viiddis" "stuoris" "smávis" "goikkis" "ruonas" ;

LIST FOREST-PREP = "guovdu" ; 

LIST FOREST-WORD = "Amazonas" ;
    
SELECT:vuovdiHumanActivity NomAg (0 ("vuovdi" Sg Nom) LINK *0 HUMAN-ACTIVITY-V + Sg3 OR DADJAT + Sg3 OR VERBAL-ACTIVITY + Sg3 OR HUMAN-AGENT-V + Sg3 BARRIER SV-BOUNDARY OR COMMA) ;
	## Vuovdi bolddii buot báhpáriid ovdalgo politiijat bohte.
	## Vuovdi dajai munnje dan.
	
SELECT:vuovdiPlc Sem/Plc IF (*-1 MOVEMENT-V BARRIER NOT-ADV-PCLE)(0 ("vuovdi" Sg Ill)) ;
	## Luonddufága láide dien guvlui ahte galgá váldit vuođu báikkálaš luonddus, muhto buot ovdamearkkat váldet gis vuođu máttanorgga diliin, oahppit galget mannat vuovdái geahččat giđđaliđiid ja čoaggit hasselnihtiid ja ruvsumurjjiid.
	## Go Niillas olle vuovdái ja čuollá ovtta muora, de son oaidná ahte ii doppe nagot čuollat buot muoraid.

REMOVE:vuovdiNomAg ("vuovdi" NomAg) IF (0* RIEKTEDILLI BARRIER S-BOUNDARY) ;
	
SELECT:vuovdiPlc2 Sem/Plc IF (*-1 FOREST-ADJ + Attr BARRIER NOT-NPMODADV)(0 ("vuovdi" Sg Nom) OR ("vuovdi" Sg Ill)) ;
	## Diekko galgá šaddat nu stuora ja suhkkes vuovdi ahte ii mihkkege beasa čađa.	
	## Áhkku manai stuora, sevdnjes vuovdái.
	
SELECT:vuovdiCOP ("vuovdi" Nom NomAg) IF (*1 COPULAS BARRIER V OR S-BOUNDARY LINK *1 NUMAG-A + Nom BARRIER NOT-ADV-PCLE) ;
	## Vuovdi lea čeahppi.
	
SELECT:vuovdiPlcCOP ("vuovdi" Nom Sem/Plc) IF (*1 COPULAS BARRIER V OR S-BOUNDARY LINK *1 FOREST-ADJ + Nom OR FOREST-V + (TV Der/PassS PrfPrc) OR FOREST-V + (TV Der/PassL PrfPrc) OR FOREST-V + (IV PrfPrc) BARRIER NOT-ADV-PCLE) ;	
	## Vuovdi lei goikkis dego geassit láviige.
	## Vuovdi lea njeidon.

REMOVE:vuovdi1 Actor IF (*-1 FOREST-V OR ("báhcit") BARRIER S-BOUNDARY)(0 ("vuovdi" Ill)) ;
	## Áhčči njiejai váris vuovdái.
	## Piera bázii vuovdái, muhto áhčči manai ruoktot.

REMOVE:vuovdi2 NomAg IF (*1 ("buollit") OR ("šuvvat") BARRIER S-BOUNDARY)(0 ("vuovdi" Nom));
	## Vuovdi bulii mannan vahkkus Ivgubađas.
	
REMOVE:vuovdi3 NomAg IF (*-1 FOREST-PREP BARRIER S-BOUNDARY)(0 ("vuovdi" Sg Nom)) ;
	## Dii galgabehtet čuohppat aivve guovddáš vuovddi das go guovdu lea buoremus vuovdi.
	
REMOVE:vuovdi4 NomAg IF (*1 FOREST-V BARRIER S-BOUNDARY)(0 ("vuovdi" Sg Nom)) ; 
	## Vuovdi ii bálle ođasmuvvat go ealgavallji lea buot ođđa muoraid jávkadan. 
	
REMOVE:vuovdiEss Sem/Plc IF (*-1 ("bargat") BARRIER S-BOUNDARY)(0 ("vuovdi" Ess)) ;
	## Mun barggan vuovdin. 

#REMOVE:vuovdiNUMAG-A Sem/Plc IF (-1 NUMAG-A)(0 ("vuovdi"));
#REMOVE:vuovdiLOCATION NomAg IF (-1 ADJ-LOCATION)(0 ("vuovdi"));
	#I suspect these do not have any function at this stage. Concider removing them. 
 

REMOVE:vađastit NomAg IF (*-1 ("vađđa"))(0 ("vuovdi" Nom)) ;
	##Vađas goasii vuovdi jávká.
	
REMOVE:vađastit2 (V "vađastit") IF (0 ("vađđa" Sem/Plc)) ;
	##Vađas goasii vuovdi jávká.
	#Not a good rule if the tag "Sem/Plc" is given an other name in the future. 



	
#!! * **UsImprt** removes Imprt Sg3 for all nouns in -us	
SET UN-N = (".*us"r) + N ;
REMOVE:UsImprt Imprt IF (0 UN-N OR ("oažžut") LINK NOT 0 ("šaddat")) ;

#!! * **SUBImprt** removes Imprt when it can be a part of an NP	
REMOVE:SUBImprt Imprt IF (0 (Sg Nom) OR Attr) ((*1 V + Sg3  BARRIER S-BOUNDARY OR REAL-V OR CS) OR (*1 (Rel Nom) LINK *1 VFIN BARRIER S-BOUNDARY OR REAL-V LINK *1 COMMA LINK 1 V + Sg3 )) ;
	#$ Bargu sámi fanasduddjonvieruid ektui lea dakkár bargu masa Sámediggi sáhtášii váikkuhit eanet dutkama bokte .

REMOVE:SUBImprt2 (TV Imprt) IF (0 Attr LINK NEGATE *1 Acc BARRIER S-BOUNDARY)(1 N)   ;
	#$ Njuolggadusat meahccegeavaheami rievdamis – rabas gulaskuddan.

REMOVE:Imprt STRICT-TV-V + Imprt IF (0 N LINK NEGATE *1 Acc OR Inf OR ("ahte") BARRIER REAL-V)   ;
	#$ Eavttut gánnáhahtti boazodollui leat iešguđetláganat guovllus guvlui.


REMOVE:r599 VFIN IF (-1 ("makkár"))(0 N);
REMOVE:r600 Inf IF (-1 ("makkár"))(0 N);
	## Makkár gáhkut dis leat?	
	
#!! * **oahppit**, ii Imprt.
SELECT:oahppit ("oahppi" Pl Nom) (0 Imprt LINK *1 VFIN + Pl3 BARRIER REAL-V) ;
	#$ Oahppit leat čeahpit.

#!! * **bargi**, ii Imprt.
SELECT:bargi ("bargi") (0 Imprt) ; # LINK *1 VFIN + Pl3 BARRIER REAL-V) ;


#!! ### mánnu vs mánus
REMOVE ("mánus") (0 ("mánnu" Loc) LINK 0 Gen) ;

#!! ### Not noun


#!! ## Adposition or not

#!! * The rules **Pooaivai, Pogiedas** removes oaivái and gieđas as Po
REMOVE:Pooaivai Po IF (0 ("oaivái") LINK NOT -1 HUMAN) ;	
	#$ Eksempel fra N/S som skal ha Po: Válden vealggi vielljan oaivái.
REMOVE:Pogiedas Po IF (0 ("gieđas") LINK 1 ("eret")) ;	
	#$ Eksempel fra N/S som skal ha Po: Go bohccot leat olbmuid gieđas.

#!! *  **aldatV1, aldatPo, KillaldatV** for the problem aldat V vs. alde Po
REMOVE:aldatV1 V IF (0 ("aldat") LINK 0 ("alde"))((-1 Gen) OR (-1 CC LINK -1 Po)) ;
SELECT:aldatPo Po IF (0 ("alde")) ((-1 Gen OR Acc) OR (-1 CC LINK -1 Po)) ;
	#$ Doaibmabijut maid alde šiehtaduvvo , mearkkašit hui ollu sámi guovlluid eanadollui.	
	#$ – Olbmot berrejit muitit ahte joga lahka dahje alde, berre leat hirbmat várrugas.

REMOVE:KillaldatV ("aldat" V) ; #foreløpig regel til vi finner ut hva aldat betyr


#!! ## Not Qst

#!! * **AdvQst** removes dego/nugo Qst 
REMOVE:AdvQst (Adv Qst) OR (Po Qst) OR (Pr Qst) IF (0 CS)(NEGATE 1 ("?")); 
 	#$ Su ámadadju báittii dego beaivváš. 
 	#$ Nugo dieđát, de lean dál.


#!! ## Interjections

#!! * **Interjlemma** voja voja nana nana select interj if repeated 
#SET INTERJ-LEMMA = (".*"r) + Interj  ;

SELECT:Interjlemma_a $$WORDLEMMA + Interj IF (*1 $$WORDLEMMA + Interj BARRIER WORD);
SELECT:Interjlemma_b $$WORDLEMMA + Interj IF (*-1 $$WORDLEMMA + Interj BARRIER WORD) ;
	#$ Guohttehehket dál dearvan mu bohccuid, nana nana voja voja.
SELECT:vuoi ("vuoi" Interj) IF (*-1 BOS OR ("vuoi") OR (":") BARRIER WORD)(1 Acc OR ("surgat") OR COMMA) ;
SELECT:juo ("juo" Interj) IF (*-1 BOS OR ("juo") OR (":") BARRIER WORD)(1 PUNCT OR CLB OR EOS OR Interj) ;

#!! * **Interj** or not
IFF:Interj Interj  IF (*-1 BOS BARRIER WORD LINK *1 Interj LINK 1 COMMA OR EOS);
 	#$ Maid, iigo leat boahtán?



#!! ## Px-rules for special nouns

#!! * **NnoPx** Remove Px for special nouns
REMOVE:NnoPx Px IF (0 N-NOPX) ; # + Px OR Num + Px OR Der/NomAct + Px OR MANNU + Px OR WEATHER-SITUATION + Px)(0 Loc OR Nom + Px OR Ess + Px);
	#$ Čoahkkin lea eahkedis.
	#$ Mun gal ceavzzán du goargŋumis.
	#$ Badjel 800 ođđa njoammuma vahkus.
	
#!! * **gaskaneaset** selects Po for gaskaneaset	
SELECT:gaskaneaset Po IF (0 ("gaskan") + Px)(NEGATE 1 VGen); 	
	#$ Sii árvališgohte gaskaneaset.


#!! ## Some verb rules

#!! * **vfingo** selects VFIN in front of go Qst
SELECT:vfingo VFIN - Imprt IF (-1 BOC)(NEGATE 0 OLLU OR Adv)(1 go + Qst LINK NOT 1 VFIN) ; 
SELECT:jearrat VFIN - Imprt IF (-1 JEARRAT)(1 go); 
SELECT:vfingo VFIN - ConNeg IF (-1 BOS)(NEGATE 0 OLLU OR Adv)(0 Qst) ; 
	#$ Dáhtut go min gaikut dan eret?
	#$ Lea go dus ruhta?
	#$ Isket leat go duhtavaš kollektiiva­fálaldagain.
	#% Ođđa giellaláhka: – Illudahtti go sámegielat nannejuvvojit álgoálbmotgiellan lága bokte.

#!! * **buoritV** removes buorit as V
REMOVE:buoritV V IF (0 ("buorre" Pl Nom) LINK 1 (N Pl)) ;
	#$ Das leat buorit lanjat.

REMOVE:muhtoNotConNeg ConNeg IF (NEGATE *-1 Neg BARRIER CONTRA);

	
#!! * Some brave rules for removing Imprt
REMOVE:TESTImprt Imprt - ConNeg - Neg IF (NEGATE -1 CC OR BOS OR COMMA OR (":") OR (";") OR ("de") OR PUNCT OR ("""))(NEGATE 0 (Imprt Sg3) LINK -1 (Pron Sg3) OR (N Sg) OR ("nu"))(NEGATE 0 (Imprt Sg3) LINK 1 (Pron Sg3) OR (N Sg));
	#% Geas ležžet bealjit, son gullos!
	#% Nu báitos din čuovggas ge olbmuide.
	#% Na čájet mat.

REMOVE:ImprtInf Imprt IF (-1 BOS)(NEGATE 0 INF-V) (1 Inf) ;
	#$ Govas oaidnit bátnedivššáriid Marit ja Inger.



SELECT:RelVFIN VFIN (0 Pl3 OR Du3 LINK -1 (Rel Pl Nom)) ;
	#$ Oahpaheaddjit geat leat leamaš dán suorggis leat sihke sámit ja dážat, muhto dat geat bohte olggobealde ledje ovdal bargan dearvvašvuođabálvalusas sámi guovlluin


REMOVE:ImprtCase Imprt IF (0 $$CASE - Px LINK -1 COMMA OR CC LINK -1 $$CASE - Px )(NEGATE 0 (Imprt Sg3))(NEGATE -2 Imprt LINK -1 BOS) ;
REMOVE:ImprtCase Imprt IF (0 A + Nom OR Ess LINK -1 COMMA OR CC LINK -1 A + Nom OR Ess)(NEGATE 0 (Imprt Sg3))(NEGATE -2 Imprt LINK -1 BOS) ;
	#% Fina iežat báikkálaš báŋkkus dahje huma minguin telefuvnnas de beasat eambbo diehtit.
	
REMOVE:ImprtNomAg Imprt IF (0 Actor LINK 1 PROP-SUR) ; # PROP-SUR has also Mal and Fem

#REMOVE:ImprtCoor Imprt IF (-1 JA/DAHJE LINK -1 ConNeg OR VGen) ; 
#


#!! * **ImprtCopPrfPrc** removes imperative readings in front of coopulas and PrfPrc
REMOVE:ImprtCopPrfPrc Imprt IF (-1 BOS)(*1 COPULAS BARRIER NOT-ADV LINK 1 PrfPrc) ;
	#$ Oahppit leat lohkan ollu girjjiid.

#!! * **FocV** revmoves Foc when Actio, PrfPrc, VGen, e.g. čađahan, ovttasge
REMOVE:FocV Foc IF (0 Actio OR PrfPrc OR Indef LINK NOT 0 Pron + Foc);
	#$ Studeanttat geat leat čađahan váldofágaeksámena... 
	#$ Ii han ovttasge du sogas leat dat namma.
	#% Mo son Duommá lea dahkan? 




#!! ### Particular CS

#!! * **madeCS** for mađe/mađi and dađe/dađi
IFF:madeCS MADE + CS IF  (*1 DADE) ;

#!! * **dadeCS** for mađe/mađi and dađe/dađi
IFF:dadeCS DADE + CS IF (*-1 MADE) ;
	#$ Mađe mielde oaivi, dađe mielde ságat.
	#$ Mađe stuorit RAM-muitu du dihtoris lea, dađe eanet dihtorprográmmaid sáhtát geavahit oktanaga.	

#!! ### Verb or Noun?

SELECT:buresboahtinIll Ill IF (*-1 ("boahtin" Nom) BARRIER REAL-V OR S-BOUNDARY LINK -1 ("bures")) ;
	#$ Bures boahtin Čáhppes Bearralii.


SELECT:VFINSg3TEST VFIN-NOT-IMPRT + Sg3 IF 
(*-1 NP-HEAD-SG-NOM BARRIER NOT-NPMODADV LINK NOT 0 VFIN LINK NEGATE *-1 VFIN BARRIER S-BOUNDARY)(NEGATE *1 VFIN - Foc/Neg-ge BARRIER S-BOUNDARY OR Foc/Neg-ge)(NEGATE 0 Pl3 LINK *-1 NP-HEAD-PL-NOM)(NEGATE 0 Gen LINK 1 Po)(NEGATE 0 VFIN + Pl3 LINK *-1 NP-HEAD-PL-NOM BARRIER NOT-NPMODADV) ; ;

	#$ Fápmu čiehká iežas luottaid.
	#$ Go čálalaš kultuvra lea eahpedábálaš minoritehtaid gaskkas ja go oskkáldasvuohta ja heahpu lea leamaš dábálaš, de sáhttá dás oaidnit ráhkadusdovdomearkka, nammalassii ahte fápmu čiehká iežas luottaid.
	#$ Eat atnán sahá, muhto ákšu vai muorra bissu divttisin iige mieskka nu jođánit.
	#% Bures boahtin Čáhppes Bearralii.
	#% Nuorat ođđe buvrriin geasset, muhtumin dálvet maid.
	#% dat manne dat albmát manne vuos deikke bygget viesuid ja . (LIA)


#!! * **Včiehká** selects V instead of N when nomintive to the right and accusative to the left *fápmu čiehká luottaid*
SELECT:Včiehká (V TV)  IF (*-1 (N Nom) BARRIER SV-BOUNDARY OR NP-BOUNDARY)(0 ("čiehká"))(*1 (N Acc) BARRIER NOT-NPMODADV) ;
	#$ Go čálalaš kultuvra lea eahpedábálaš minoritehtaid gaskkas ja go oskkáldasvuohta ja heahpu lea leamaš dábálaš, de sáhttá dás oaidnit ráhkadusdovdomearkka, nammalassii ahte fápmu čiehká iežas luottaid.


REMOVE NomAg (0 ("veahkaváldi")) ;

#!! ## Adpositions

#!! ### Adpositions, not verbs

#!! * **bealisPo** removes imperatives when Po lookalikes
REMOVE:bealisPo Imprt IF (-1 Gen)(0 Po);
	## Dan bealis.

SELECT:ektuiPo Po IF (-1 ("diibmá"))(0 ("ektui")) ;
	#$ Sámi allaskuvlla ohcciidlogut leat lassánan 37 proseanttain diimmá ektui.
	




#!! # Section 2: LOCAL DISAMBIGUATION - CYCLE 1
# ==================================
# ==================================

SECTION


#!! ## FAMILY pronouns

#!! ### Pron Pers 1. p.

#!! * **moai** This rule is not in use because of REMOVE:Prop
#SELECT:moai MOAI ( *1 DU1-V);   	
	#$ Moai čilgejetne dan ášši.

#!! * __miiPersLeft1, miiPersLeft2, _miiPersRight__ select mii Pers 
SELECT:miiPersLeft1 Pers IF (0 MII-PERS) ((-1 go LINK -1 PL1-V LINK -1 BOC) OR (-1 (V Prs Pl1 Qst) + Ind) OR (-1 (V Neg Pl1))) ; 
	#$ Eat go mii leat profehtastallan du nammii?
	#$ Eat mii sáhte vuolgit.

#SELECT:miiPersLeft2 Pers (*-1 PL1-V BARRIER NOT-ADV OR ("seammá"))(0 MII-PERS)(NEGATE 0 Rel LINK *1 SG3-V BARRIER NOT-ADV);
SELECT:miiPersLeft2 Pers IF ((-1 PL1-V LINK -1 MO) OR (-1 ("go" Qst) LINK -1 PL1-V) OR (-1 PL1-V + TRANS-V LINK -1 Acc) OR (-1 PL1-V LINK -1 Acc LINK 3 PrfPrc + TRANS-V)) ;
	#$ Goas oinniimet mii du nelgon dahje goikan?
	#$ Dán leat mii bargan ovttas guoskevaš čearuid, boazoorohagaid, ja eiseválddiid áirasiiguin.
	#% Dán geasi ledjen doppe menddo guhká , go gergen láittastuvvagoahtit ja ohcalit fas iežan barggu , de olles dat prosessa bijai mu smiehtadit mii dat lea mii čatná olbmo báikkiide

SELECT:miiPersRight Pers IF (0 MII-PERS)((*1C Rel BARRIER WORD) OR (*1 PL1-V BARRIER S-BOUNDARY OR VFIN LINK NOT 0 Adv) OR (1 CC LINK 1 (N Nom)))(NEGATE 0 Foc/ge LINK 1 N + Pl + Acc)(NEGATE -1 ("vaikko") OR ("vaikke") OR ("feara") LINK NOT -1 CS) ;
SELECT:miiPersRight Pers IF (0 MII-PERS)(1 Neg + Pl1) ;
	#$ .. de mii, guđet leat eatnagat, leat okta rumaš. #funker ikke pga 1C Rel
	#$ Dasgo mii oinniimet su násti badjáneamen.
	#$ Mii ja farisealaččat atnit borakeahttáivuođa.

#!! ### Pron Pers 2. p.

#!! * **donDem** selecst don as Dem instead of Pers
SELECT:donDem Dem IF (0 ("dot") + Gen)(1 Po OR ("bealli")); 
 	#$ Don beale ceakkus váriid ihtet ruoná guolbanat.


#!! * **donPers** selecst don as Pers instead of Dem
SELECT:donPers Pers IF (0 DON)((*-1 V + Sg2  BARRIER SV-BOUNDARY LINK NOT 0 Adv) OR (*1 SG2-V BARRIER PUNCT)); 
 	#$ Liikotgo don Abbai?
 	#$ Don it leat nama čállán.

SELECT:donPersEllipse Pers IF (0 DON)((**1 (A Sg Nom) OR (N Sg Nom) BARRIER NOT-A LINK 1 ("!")) OR (*1 (N Com) BARRIER NOT-NPMODADV) LINK 1 ("?")) ; 
	#$ Son celkkii: Don ipmilmeahttun jalla!


#!! ### Pron Pers 3. p.

#!! * **sonSG3V, sonRel, goson** select son as Pers, Rel or Pcle 
SELECT:sonSG3V Pers + Sg3 + Nom IF (0 ("son"))(NEGATE -1 Qst OR go) ((*-1 SG3-V BARRIER SV-BOUNDARY) OR (*1 SG3-V BARRIER N)) ; 
SELECT:sonSG3V Pers + Sg3 + Nom IF (0 ("son"))(NEGATE -1 Qst OR go) (1 PrfPrc LINK 1 COPULAS + Sg3) ; 
	#$ Ii son liikon dohko.
	#$ Son fertii vuolgit. 
	#$ Eatnistis son árben lea dan luonddu čáppa moji.

SELECT:sonRel Pers IF (0 ("son" Pron Pers Sg3 Nom))(1 (Pron Rel Sg));
	#$ Son gii addá veahki galgá mearridit.

SELECT:goson Pcle IF (NEGATE -2 CLB)(-1 Qst OR go)(0 ("son"))(1 Nom LINK NEGATE 0 Refl OR Inf OR Cmp/SplitR);
	#$ Lea go son Máret boahtán?

SELECT:nuPlce Pcle IF (-1 ("mii" Indef) OR ("gii") OR ("gos"))(0 ("nu")) ;  #because of Apertium
REMOVE:nuPlce Pcle IF (0 ("nu")) ; 
	
#!! * **dePcle** de as Pcle
SELECT:dePcle ("de" Pcle) IF ((-1 REAL-V LINK 2 Adv) OR (-1 ("dál") LINK 2 REAL-V)) ;
	#$ Buohkat gilvalit de vuostálaga, ii leat sierra luohkká mánáide.
	#$ Dáidda heŋgejuvvojit gurpasat, ja maid dál de guottehit lea áigumin.

IFF:dePcle ("de" Pcle) IF (-1 N LINK NEGATE *-1 REAL-V) ;

#!! * **sutnje** ( = forms of the verb "suotnjat")
SELECT:sutnje_a (Pron Ill) IF (0 ("son"))(1 (Pron Rel Sg));
SELECT:sutnje_b (Pron Ill) IF (0 ("son"))(*-1 V + Ind); 
	#$ Dearvvašvuođabálvalus galgá addit sutnje gii ohcá ...
	#$ Son galgá addit sutnje dárbbašlaš dieđuid.


#!! * **datPlIll** selects dát Pron Dem Pl Ill
SELECT:datPlIll Ill IF (0 ("dáidda"))((-1 NP-HEAD-NOM OR ("lassi") LINK NOT 0 Attr) OR (*1 (N Pl Ill) BARRIER NOT-A-ADV));
	#$ ...ja dáidda gielaide sáhttá čállit journálaid. 
	#$ Sus lea álgovuorus dušše guohtunvuoigatvuohta dáidda. 
	#$ Lassin dáidda lea Sámi vuoigatvuođalávdegotti ovdaolmmoš...

#!! * **daiddaVerb** removes dáidda N Sg Nom 
REMOVE:daiddaVerb Nom IF (0 ("dáidda"))((*-1 TRANS-V OR ILL-V BARRIER SV-BOUNDARY OR CS) OR (*1 ("bidjat") OR ("lasihit") BARRIER NOT-AUX-V)) ;
	#$ ...lea čađahan iskamiid muhtun orohagain ja ávžžuhan boazologu dáidda.
	#$ ...mat gullet dáidda 1972-konvenšuvnna mielde.
	#$ Ferte mearridit makkár deattu dáidda galgá bidjat.
	#% Guovlulaš ja báikkálaš eiseválddiin lea maiddái ovddasvástádus váikkuhit dan ahte sámi dáidda, kultureallin ja sámi ásahusat ovddiduvvojit ovttas.


#!! * **dasaVGen, dasaLassin dasa,datSg3, datSg3PrfPrc** ( = forms of the verb "dassat"): 
REMOVE:dasaVGen VGen IF (0 Pron);
	#$ Na muhto don it leat dasa nama čállán! 

#!! * **dasaILLV** choses dasa to the left of verbs like duhtat, suhttat, luohttit
SELECT:dasaILLV (Pron Ill) (0 REAL-V) (1 ILL-V - TV)  ;
	#$ Muhto ii Emil dasa duđa.

SELECT:dasaLassin Ill IF (0 DAT-PRON)(1 ("lassi" Ess));
	#$ Dasa lassin buvttán dás lasseargumenttaid. 


SELECT:IllvsAdj N + Ill IF (0 (A Nom) LINK NOT 0 Comp OR Superl OR Px LINK NEGATE *-1 COPULAS BARRIER REAL-V)(NEGATE 0 VFIN) ;
	#$ Ii fal runiidan jalges gittiide, muhto meahccái, gosa návddit dávjá ja lunddolaččat fievrredit ráppiid. 
	#% Norgga guđát buoremus boarrásat junior báhčči.


# selects if there is no Pl3-verb, and in front of Rel Sg, and if the next word is not N 
SELECT:datSg3 Sg3 IF (NEGATE *0 PL-V BARRIER S-BOUNDARY)(0 ("dat" Sg3 Nom) LINK NOT 1C (Pron Pl))(NEGATE 0 Pcle LINK -1 (N Nom) OR (Pron Nom))(NEGATE *1 (N Nom) BARRIER VFIN OR S-BOUNDARY LINK NOT 0 VFIN)(*0 SG3-V BARRIER PL-V) ;
	#$ Dat movt sámi oahpahus lea doaibman...
	#$ Dat orru leamen buorre fanas.
	#$ Dat gal lei beare unnán.

# selects dat in front of leamaš
SELECT:datSg3PrfPrc Sg3 IF (NEGATE *-1 PL-V BARRIER S-BOUNDARY)(0 ("dat" Sg3 Nom))(1 ("leat" PrfPrc) LINK *1 Sg BARRIER NOT-A-ADV) ;
	#$ Dat leamaš hui stuorra noađđi midjiide.


# selects dat Sg in front of Rel Sg
SELECT:datSgRel (Pron Sg Nom) IF (*1 (Rel Sg) BARRIER WORD) ;
	#$ Dat mii sáhttá háhkkojuvvot, lea ovdalis juo namuhuvvon..
	#% Čorgestallat mii maid várra veahážiid juohke beaivvi, ..

# selects dat Sg in front of ahte
SELECT:datSgAhte ("dat" Pron Sg Nom) IF (1 COMMA)(2 ("ahte" CS));
	#$ Duopmokapihttalis lea álot vuođđojurddan dat, ahte nisson- ja almmáiolbmuin buohkain leat seammalágan vejolašvuođat.

# selects dat Pl in front of Rel Pl
SELECT:datPlRel (Pron Pl Nom) IF (*1 ("mii" Rel Pl) OR ("gii" Rel Pl) BARRIER WORD) ;
	#$ Dat mat sáhttet háhkkojuvvot, leat ovdalis juo namuhuvvon..


#!! * **DemPlLoc** selects Dem when Dem Pl Loc and agreement, perhaps no need for it here because we have agreement-rules later. Men viktig: her blir vi kvitt duo N.
SELECT:DemPlLoc Dem IF (*1 (N Pl Loc) OR (A Pl Loc) BARRIER NOT-A-ADV)(0 (Pl Loc));
  	#$ Fertet vázzilit vai it báze duoin earáin. # viktig: her blir vi kvitt "duo" N
  	#$ Miessegietti dállu lei okta dain dáluin Davvi-Romssas maid duiskkat eai boaldán. #denne er det ikke hast med
  	#$ Dain hirbmat váriin ja mehciin sin guovllus lei earenoamáš vallji sihke elliid ja eará dáfus. #denne disambigueres først seinere med agreement-regler pga av at her er "hirbma" N

#!! * **DemPlCom** selects Dem when Dem Pl Com and agreement, perhaps no need for it here because we have agreement-rules later.
SELECT:DemPlCom Dem IF (*1 (N Pl Com) BARRIER NOT-A-ADV)(0 (Pl Com));
	#$ Gal mun goasttan daiguin sabehiiguin.

#!! * **datPersCopulas** select Pers in front of copula. I setninger som Riššat dat gal leat musge, jus eai leačča njuoskan. tolker jeg dat som Pcle. Derfor constraint hva som kommer etter.
SELECT:datPersCopulas Pers IF (-1 Nom)(NEGATE -1 Pers)(0 ("dat" Nom))(*1 REAL-COPULAS BARRIER NOT-ADV-PCLE LINK 1 CLB OR EOS);
	#$ Hei, soiddas dat lea, máistte don dan gáfe!
	#$ Gean mearkka bohccot dat ledje?

#!! * **datPcle1** selects dat Pcle between N and finite, even if there is agreement between verb and dat .
SELECT:datPcle1 ("dat" Pcle)  IF (-1 (N Nom) OR (Pers Nom) OR (Pers Loc) LINK NOT 0 A OR Interr OR PrfPrc LINK NEGATE *-1 Gen BARRIER WORD - Acc - Gen)((*1 VFIN BARRIER NOT-ADV-PCLE) OR (1 Adv LINK *1 VFIN BARRIER NOT-ADV-PCLE))(NEGATE 1 ("bohtti")) ;
	#$ Máret dat boahtá.
	#$ Mun dat lean.
	#$ Nissonat dat eanaš duddjojit sámi biktasiid.
	#% Ođđa plána dat lea.
	#% Gean mearkka bohccot dat leat?

#!! * **datPcle2** selects dat Pcle when there is no agreement between verb and dat .
SELECT:datPcle2 Pcle IF (NEGATE *-1 V + Sg3  OR V + Du3  OR V + Pl3  BARRIER SV-BOUNDARY)(NEGATE -1 BOC)(0 ("dat" Nom))(NEGATE *1 Rel BARRIER WORD)(NEGATE *1 N BARRIER NOT-NP)(NEGATE *1 V + Sg3  OR V + Du3  OR V + Pl3  BARRIER SV-BOUNDARY);
	#$ Na vuolggán dat gal mun ge.	

SELECT:datPcle3 ("dat" Pcle)  IF (1 ("dat") + Nom);


#!! * **KilldatPcle** removes the remaining dat Pcle
REMOVE:KilldatPcle ("dat" Pcle) ;

SELECT:nuPcle ("nu") + Pcle (-1 ("gii")) ; #for apertium with other preprocess (not gii_nu)


#!! * **PersAcc** selects Pers Acc in accusativ infinitive clauses with object
SELECT:PersAcc (Pers Acc)  IF (-1 Acc LINK *-1 VERBAL-ACTIVITY-V BARRIER NOT-A-ADV)(0 Dem)(1 REAL-V LINK NOT 0 VFIN) ;  
	#$ Son logai oappás dan bargan.


#!! * **datPers** selects Pers. I made it stronger than it was. ref. r897 in sme-dis.rle
#SELECT:datPers Pers IF (0 DAT-PRON)(NEGATE 0 ("dasgo") OR ("danin") OR ("dainna"))(NEGATE 1 A OR Num OR N OR Indef OR Attr)(NEGATE 1 PrfPrc LINK *1 N BARRIER NOT-A-ADV)(NEGATE 0 Nom LINK 1 ("gal") OR Pcle LINK 1 (N Nom))(NEGATE 1 NP-HEAD-GEN LINK 1 (Actio Nom) LINK 1 N)(NEGATE 1 ("seammás"))(NEGATE 0 Acc LINK *1 STRICT-TRANS-V BARRIER S-BOUNDARY LINK NEGATE *1 Acc BARRIER NOT-A-ADV);
	#$ Dá dat leat.
	#$ Moai soabaime dasa.
	#$ Aiddo sii movttidahttojuvvojit daid buot smiehttat sámegillii.
 
#!! * **datDemSg** selects Dem from Pron Pers Sg3 Gen
#SELECT:datDemSg (Dem Sg)  IF (NEGATE *-1 STRICT-TRANS-V LINK NEGATE *2C Acc BARRIER N)(0 (Pron Pers Sg3 Gen))(NEGATE 1 Po)(NEGATE *1 STRICT-TRANS-V BARRIER S-BOUNDARY OR Acc)(NEGATE *1 Pl + N OR Ess OR Com BARRIER N)(NEGATE -1 BOS LINK 2 Prop) ;
	#$
	#% Nubbi noađđeheargi goallostuvvo giehtahearggi lávžái ja goalmmát fas dan nuppi lávžái. #It shouldn´t take this one.
	#% Jearahallan ii goassege deaddiluvvon áviissas, daningo daláš doaimmaheaddji Bjarne Store Jakobsen biehttalii dan deaddileames.
    # It shouldn´t hit: Mun gohčodan dan eadnin. Sii čurvot dan dáhkiid alde. De dearvvahehket dan ráfiin. Son dasto attii dan eadnásis. Disse vil bli tatt hånd om i More pronouns  

#!! * **datPersPl3** selects dat Pl3 in front of V Pl3 and V Du3 and Rel Pl
#SELECT:datPersPl3 DAT-PRON + (Pers Pl3) IF (*1 V + Pl3  OR V + Du3  OR (Rel Pl) BARRIER NOT-ADV-PCLE OR Neg)(NEGATE -1 Neg) ; 
	#$ Dan guovtto dat leat.

SELECT:DemAccNum (Dem Sg Acc) (1 Num LINK 1 TIME-N + Gen LINK NOT 1 N) ; 
	#$ Mun ledjen man nu láhkai oktavuođas internáhttaohppiin dán 22 jagi maid ledjen skuvllas.

#!! ## An early rule for "eanaš"/"eanas"
SECTION

#!! * **eanasPron** selects Pron in front of Pron Loc
SELECT:eanasPron Pron IF (0 ("eanaš") OR ("eanas"))(1 (Dem Loc) OR (Pers Loc));
	#$ Eanaš dáin gálduin leat hui unnán dieđut.

#!! ## Px constraints 

SET NOTPXACC-V = LOC-V OR MUITALIT OR HUPMAT OR VERBAL-ACTIVITY-V OR ("bargat") ;

#!! First select Px, then remove all remaining Px

#!! * Set with adjectives, which are documented to have Px in our corpus
LIST ADJ-PX = "boaris" "ipmilbalolaš" "nuorra" "ráhkis" "vistelágaš" ;

#!! * **APxifN** Remove A Px if N:
REMOVE:APxifN A + Px (0 N LINK NOT 0 ADJ-PX) ;

#!! * **PxAlone** Remove Px if it is only word in the sentence, and not a typical px-term
REMOVE:PxAlone Px (NEGATE 0 N-PX)(NEGATE -1 WORD)(NEGATE 1 WORD) ; 
	#$ Dieđihančállosat.



#!! * **APx** Remove A Px if Adv of A Ess og A Attr og PrfPrc or Loc
REMOVE:APx A + Px (0 Adv OR (A Ess) OR (A Attr) OR PrfPrc OR Loc)(NEGATE 0 ADJ-PX) ;
	#$ Son čuoččastii ja geahčadii vehá	dobbelaččas	Sipirjá-Liissá českes goruda.
	#$ Dáikkihin ahte lei juo golggotmánu loahpageahči, ja dat šlundudii ovddežisge.
	#$ Rásit liđđot juogo geassit, čakčat dahje dálvit: dat ii mearkkaš olus , dasgo dain alddiineaset ii leat goiku iige nealgi, iige eallin.

LIST ANYLEMMA = (".*"r) ;

#!! * **PxLocIll** Remove Px if viesus vissui or similar
REMOVE:PxLocIll Px (0 $$ANYLEMMA + Loc LINK 1 $$ANYLEMMA + Ill) ; 

#!! * **NPxPrfPrc** Remove Px if PrfPrc with  leat to the left
REMOVE:NPxPrfPrc N + Px (0 PrfPrc LINK *-1 ("leat") BARRIER NOT-ADV-PCLE) ;


#!! * Nouns: **NomPxSg1** (not Ess) as the only word in a sentence. Needs no disambiguation.
SELECT:PxSg1Nom N-PX + (N Nom PxSg1) IF (-1 BOS)(NEGATE 0 VFIN)(*1 EOS OR VFIN - Der/Pass BARRIER WORD);  
	#$ Ustibažžan! 
	#$ Vielljan sáhttá vuoitit gilvvu.
	
SELECT:ahccamet N-PX + FIRST-PX (-1 BOC OR COMMA)(*1 EOC OR COMMA) ;
	#$ Áhččámet, don guhte leat almmis. # also "áhččámet" N Sg Nom
 
#!! * Nouns:  **AccPxSg1** after a TV verb. Exception for Aux.
SELECT:PxSg1Acc (N Acc PxSg1)  IF ((-1 TRANS-V + Sg1 - OPRED-V LINK NOT 0 AUX) OR (-1 TRANS-V + PrfPrc - OPRED-V LINK -1 COPULAS + Sg1) OR (-1 TRANS-V + ConNeg - OPRED-V LINK -1 Neg + Sg1))(NEGATE *-1 VFIN - Sg1 BARRIER S-BOUNDARY OR CS)(NEGATE *1 Acc BARRIER REAL-V LINK NOT 1 Po)(NEGATE *1 ("ahte") BARRIER WORD);

#!! * Nouns:  **AccPxSg1** after a TV Inf verb.
SELECT:PxSg1Acc (N Acc PxSg1)  IF ((*-1 TRANS-V + Inf - OPRED-V BARRIER NOT-NPMOD LINK *-1 SG1-V BARRIER NOT-ADV-PCLE LINK NOT 0 ("leat")) OR (1 ("ruovttoluotta") LINK *-2 OPRED-V + Inf BARRIER NOT-NPMOD LINK *-1 SG1-V BARRIER NOT-ADV-PCLE LINK NOT 0 ("leat")))(NEGATE 0 Dem) ;
SELECT:PxSg1Acc (N Acc PxSg1)  IF (*-1 TRANS-V + Inf - OPRED-V BARRIER SV-BOUNDARY OR CC OR CS OR COMMA OR VFIN OR Acc LINK *-1 SG1-V BARRIER NOT-ADV-PCLE LINK NOT 0 ("leat"))(NEGATE 0 Dem) ;
	#$ Mun oidnen vielljan.
	#$ Mun lean oaidnán vielljan.
	#$ Mun in oaidnán vielljan.
	#$ Dál mun ferten gaikkodit visson. # visson is also PrfPrc
	#$ Mun attán heaggan sávzzaid ovddas.
	#$ Addet munnje eatnama hávdebáikin, vai mun beasan hávdádit jápmán áhkkán.
  	#% Mun válddán ovdamearkan Buolbmága. # It shouldn`t take this example.
  	#% Son lei hárjánan ohcat doarjalusa iežas eallimii vássán beivviin .
  

#!! * **PxSg1LocAcc** is Acc to the right.
SELECT:PxSg1LocAcc (Loc PxSg1) IF (-1 SG1-V + TRANS-V)(1 Acc); 	
	#$ Mun roggen ozastan njálgáid mánáide.

#!! * **PxSg1Acc** is Acc to the right.
SELECT:PxSg1Acc (N Acc PxSg1) IF (-1 TRANS-V) ((*-1 SG1-V BARRIER VFIN OR Acc - Actor OR S-BOUNDARY LINK NEGATE 0 PrfPrc LINK -1 ("leat")) OR (-2 COPULAS LINK -1 SG1-V))(NEGATE 0 PrfPrc)(NEGATE 1 Acc)(NEGATE 0 Ess LINK *-1 OPRED-V BARRIER NOT-NPMOD) ;   	
	#$ Mun háliidan mánnán boahtit ruoktot!
	#$ Mun in oaidnán vielljan jahkái.
	#$ Mun in leat oaidnán vielljan jahkái.
	#$ Mun in oaidnán vielljan.
	#% geaid bellodagat ja jienasteaddjijoavkkut leat ásahan evttohassan.
	#% juo go geađggi lea ipmil sivdnidan láibin dat han gal


#!! * coordination **PxSg1coord** 
SELECT:PxSg1coord1 (PxSg1)  IF (*-1 SG1-V - ("leat") LINK NOT 0 PrfPrc)(NEGATE 0 Dem OR PrfPrc)(1 JA/DAHJE OR COMMA LINK 1 PxSg1) ;
	#% Guolban mánáidgárddis lei karnevalbeaivi , ja danin ledje mánát gárvodan prinseassan , politiijan ja tigerin .

#!! * **PxSg1coordLast** for the last word of a coordination
SELECT:PxSg1coordLast (PxSg1)  IF (*-3 SG1-V - ("leat"))(-1 JA/DAHJE LINK -1C PxSg1)(NEGATE 0 Dem);
	#$ Mun attán heaggan, áhkkán ja ruhtan sávzzaid ovddas.  

#!! * **ReflPxSg1** lean oahppan alddán
SELECT:ReflPxSg1 (Refl PxSg1)  IF (*0 Sg1 BARRIER VFIN)(0 Adv); 
	#$ Dan lean oahppan alddán.  


#!! * Nouns: **PxSg2** if SG2-V. The rule needs no disambiguation. The DON-constraint because of homonymi with (N Pl)
SELECT:PxSg2 (PxSg2)  IF (-1 SG2-V - REAL-COPULAS LINK -1 DON)(NEGATE 0 Attr OR Adv LINK *1 N BARRIER NOT-PRE-A-N)(NEGATE 0 VFIN + Sg2)(NEGATE 0 Acc LINK *-1 SG2-V + IV)(NEGATE 0 REAL-V) ; 
	#$ Don attát heakkat mu ovddas?
	#% Don galggat mannat árkii, don ja du bártnit, du áhkká ja bártniidat áhkát oktan duinna.
	#% Don fertet oahppat vuoruhit ja árvvoštallat makkár bovdehusain oaččut eanemus ávkki.

#!! * **PxSg2Acc** if  TV to the right
SELECT:PxSg2Acc (Acc PxSg2) IF (0 (N Pl Nom)) 
((*-1 TRANS-V + Sg2 BARRIER SV-BOUNDARY OR CS OR COMMA OR Acc OR ADVLCASE LINK NOT 0 A OR Imprt OR Inf OR Pl3 OR (N Pl Nom) LINK NEGATE *-1 Sg1 OR Pl1) OR (*-1 TRANS-V + ConNeg BARRIER NOT-A-ADV OR COMMA LINK -1 (Neg Sg2)) OR (*-1 TRANS-V + PrfPrc BARRIER NOT-A-ADV LINK *-1 COPULAS + Sg2 BARRIER NOT-ADV) OR 
(-1 Qst LINK -1 AUX + Sg2 LINK *3 TRANS-V + Inf BARRIER NOT-ADV))
(NEGATE 0 Inf)(NEGATE 0 Attr LINK *1 N - Actor BARRIER NOT-A)
;
	#$ Máhtát go namat čállit?
	#$ Leat go oaidnán oappát? 
	#$ Ale cáme beatnagat, ii dat leat sivalaš.
	#% Mánat ja nuorat besset deaivvadit ja oahpásmuvvat.
	#% dat giellapolitihkka nu ahte eai leat duostan hállat?

#!! * **PxSg2AccImprt** if  TV Imprt to the left
SELECT:PxSg2AccImprt (Acc PxSg2)  IF 
((*-1 (TV Imprt Sg2) - AUX BARRIER NOT-NPMODADV LINK NOT 0 Adv OR Loc LINK *-1 BOC BARRIER WORD) OR 
(*-1 (TV Imprt Sg2) - AUX BARRIER NOT-NPMODADV LINK NOT 0 Adv OR Loc LINK -1 COMMA OR ("ja" CC) LINK *-1 (Imprt Sg2) - AUX BARRIER NOT-ADV))
(NEGATE 0 Adv)(NEGATE 0 (N Pl Nom) LINK *-1 CC OR CLB BARRIER NOT-NPMOD LINK -1 Nom)(NEGATE 0 (N Pl Nom) LINK -1 Attr LINK 0 Imprt)(NEGATE 0 Gen LINK 1 Po)(NEGATE 0 Sem/Plc OR Sem/Txt LINK *1 Acc BARRIER NOT-NPMOD) ; # (NEGATE 0 (N Pl Nom) LINK *1 PL3-V BARRIER NOT-ADV-PCLE) ; 
	#$ Váldde mánát sallasat!
	#$ Buvtte dasto oaffarskeaŋkkat!
	#$ Lihka bajás, váldde seaŋggat ja vázzil!
	#$ Viečča vieljat boradit!
	#$ Lihka bajás, váldde seaŋggat ja vázzil!
	#% Nuppi iđida ollii mánáidsuodjalus festii ja easka de besse nuorat guođđit dán ilgadis feastta.
	#% Čále girjásat bártniid namaid.

#!! * **PxSg2AccPrfPrc** after PrfPrc 
SELECT:PxSg2AccPrfPrc (Acc PxSg2) IF (-1 PrfPrc + TRANS-V - NOTPXACC-V LINK *-1 SG2-V BARRIER NOT-AUX-V)(0 N-PX - Nom);
	#$ Leat go oaidnán oappát?

#!! * **NotPxSg2** if no Sg2 
REMOVE:NotPxSg2 (N PxSg2) IF (0 Adv LINK NEGATE *-1 Sg2) ;
	#$ Danne sáhttá váttisvuođaid mat čuožžilit gaskasaš konvenšuvnna beaivválaš dilis, buoremusat čoavdit riikkaidgaskasaš orgánain, mas leat loahpalaš mearridanváldi.

#!! * **PxSg2GenPo** if in front of Po, after til verb
SELECT:PxSg2GenPo (Gen PxSg2)  IF (*-1 Sg2 BARRIER S-BOUNDARY OR COMMA LINK NEGATE 0 Pl3)(0 (N Pl Nom))(1 Po LINK NOT 0 N OR ("fárus") OR ("mielde"))(NEGATE *2 NP-HEAD-GEN BARRIER NOT-NPMOD);
	#$ Go don rohkadalat, de mana gámmárat sisa.

#!! * **PxSg2Loc** after habitivconstruction
SELECT:PxSg2Loc (N Loc PxSg2) IF  (-1 COPULAS + VFIN LINK -1 HAB-ACTOR) ;
	#$ Mii dus lea ozastat?


#!! * **ánsuPx** 
SELECT:atnitPx Px (0 Loc LINK 0 ("ánsu")  LINK *0 ("juogadit") BARRIER SV-BOUNDARY) ;
	#$ Juogada ánssus iežas álbmogiin.


#!! * **atnitPx** removes Px for for atnit muittus, gudnis, árvvus, čalmmis
REMOVE:atnitPx Px (0 Loc LINK 0 ("muitu") OR ("gudni") OR ("árvu") OR ("čalbmi") LINK *0 ("atnit") BARRIER NOT-ADV) ;
	#$ Lea dehálaš atnit čalmmis iežat ruhtadili mii ii leat nu buorre juste dál.
REMOVE:doallatPx Px (0 Loc LINK 0 ("čalbmi") OR ("fápmu") OR ("dássedeaddu") OR ("guovddáš") OR ("árvu") OR ("sadji") OR ("salla") OR ("miella") LINK *0 ("doallat") BARRIER NOT-ADV) ;
	#$ Go mánáid álo galgá čalmmis doallat, amaset eai šatta biilla vuollai, váidala Gerd ja Biret-Áhkku lea ovtta oaivilis.

#!! * Nouns: **PxSg3Acc** if Sg3 or Sg to the left
SELECT:PxSg3Acc (Acc PxSg3) IF (0 HUMAN LINK *1 PrfPrc OR Inf BARRIER WORD - Pron - Acc - Gen)(-1 MUITALIT) ;
	#$ Son logai oappás dan bargan.


#!! * Nouns: **PxSg3Acc** if Sg3 or Sg to the left
SELECT:PxSg3Acc (Acc PxSg3) IF (
(*-1 TRANS-V + Sg3 - AUX BARRIER Nom OR S-BOUNDARY OR CS OR Acc OR COPULAS LINK *-1 HUMAN + (Nom Sg) OR (Sg3 Nom) BARRIER NOT-ADV-PCLE LINK NEGATE *-1 Acc BARRIER S-BOUNDARY OR REAL-V OR CC) OR 
(*-1 TRANS-V + ConNeg - AUX BARRIER HUMAN + Nom OR S-BOUNDARY OR Acc OR COPULAS OR CS LINK NOT 0 NOTPXACC-V LINK *-1 (Neg Sg3) BARRIER NOT-ADV LINK NEGATE *-1 Acc BARRIER S-BOUNDARY OR REAL-V OR CC) OR 
(*-1 TRANS-V + Sg3 - AUX BARRIER Nom OR S-BOUNDARY OR Acc OR COPULAS OR CS LINK -1 go OR CC)
)
(0 Loc LINK NOT 0 PLACE OR Sem/Org LINK NOT -1 Num)(NEGATE 0 Po)
(NEGATE *-1 NOTPXACC-V - ("čurvet") BARRIER SV-BOUNDARY)
(NEGATE 0 Attr LINK *1 N - SAPMI BARRIER NOT-NPMOD)
#(NEGATE 0 Attr LINK *1 SAPMI + Gen BARRIER NOT-A LINK 1 N)
(NEGATE 0 (N Gen) LINK *1 (N Acc) BARRIER NOT-A LINK NOT 0 Po OR TIME-N OR SAPMI LINK NOT 1 Loc)
(NEGATE 1 Po LINK NEGATE 0 Pr LINK 1 Gen)
(NEGATE 0 TEXT OR ABSTR-TEXT LINK *-1 MUITALIT BARRIER REAL-V)
(NEGATE *1 ("ahte") BARRIER N - ADVLCASE OR REAL-V)
(NEGATE 0 Loc LINK 1C (Indef Acc)) ;
	#$ Son beastá álbmogis sin suttuin.
	#$ Son hárve gullá eatnigielas. # This one needs verbdisambiguation of hárve
	#$ Dat guhte ráhkista bártnis dahje nieiddas eanebut go mu, ii dohkke munnje.
	#$ Jesus leai sullii golmmalotjahkásaš go álggahii doaimmas. 
	#$ Bargojoavku ovddidii árvalusas čakčamánu 8. b. 2003.
	#$ Vearrámus lea go nu váttis lea oažžut eret dan gova, lohká nisu, gii ii hálit namas aviisii. 
	#$ Bárdni ozai beatnagis miehtá gili.
	#$ Juos mearriduvvo meahcceeatnamiid nuppástuvvan geavaheapmi golmma ja dušše golmma stivrralahtu doarjagiin ja olles uhcitlohku vuođđudallá oainnus sámi kultuvrra, boazodoalu, meahcásteami, ealáhusdoaimmaheami ja servodateallima destii.
	#$ Son vuolggahii muhtun bálvaleaddji lusa vai son oččošii oasis viidnegárddi šattuin.
	#$ Juos mearriduvvo meahcceeatnamiid nuppástuvvan geavaheapmi golmma ja dušše golmma stivrralahtu doarjagiin ja olles uhcitlohku vuođđudallá oainnus sámi kultuvrra, boazodoalu, ealáhusdoaimmaheami ja servodateallima destii.
	#% Mun muittán ahte mun máŋgii ijas fertejin lihkkat deavdit parafiinna petromávssaide ja pumpet daid.
	#% Mari Boine juoigá jiekŋašloahtas.
	#% Son šálloša go máilmmidálkkádatčoahkkimis ii boahtán buoret čoavddus.
	#% Muhto sárgu mielas dárbbaša sámi servvodat eambbo ironiija.

#!! * Nouns: **PxSg3AccPrfPrc** if PrfPrc and Sg3 to the left
SELECT:PxSg3AccPrfPrcInf (N Acc PxSg3)  IF ((-1 PrfPrc + TRANS-V - NOTPXACC-V LINK *-1 SG3-V BARRIER NOT-AUX-V OR Nom OR Acc LINK *-1 (Sg Nom)) OR (*-1 Inf + TRANS-V - NOTPXACC-V BARRIER NOT-NPMODADV LINK *-1 SG3-V BARRIER NOT-AUX-V OR Nom OR Acc LINK *-1 (Sg Nom)) OR (-1 Inf + TRANS-V - NOTPXACC-V LINK -1 (Sg Nom) LINK *-1 SG3-V BARRIER NOT-AUX-V OR Nom OR Acc))(NEGATE 0 PLACE + Loc OR EVENT + Loc OR Adv)(NEGATE 0 Loc LINK 1 Acc LINK NOT 0 Sem/Time)(NEGATE 0 Sem/Time OR Sem/Mat OR ("ruovdi") OR Sem/Substnc) ;
	#$ Nu geavai ovdal go Márjá lei riegádahttán bártnis.
	#$ Gozihanlávdegoddi galgá čilget gozihandoaimmas, árvvoštallat stivrra jahkemuitalusa ja dilálašvuođaid maid dat siskkilda, ja árvvoštallat mo dát láhka doaibmá ja leago sávahahtti dan nuppástuhttit.
	#$ Raporttas galgá gozihanlávdegoddi čilget gozihandoaimmas, árvvoštallat stivrra jahkemuitalusa ja dilálašvuođaid maid dat siskkilda, ja árvvoštallat mo dát láhka doaibmá ja leago sávahahtti dan nuppástuhttit.
	#% UNTAK boahtá lávlut márkanšilljus ja Juhlsa silbarávddis beaivet, ovdalgo dollet konseartta kulturviesus.
	#% Divvunprográmma ii máhte jorgalit , vaikko dan sihkkarit áiggi vuollái sáhttá oahpahit jorgalit sánis sátnái.

SELECT:PxSg3AccPrfPrcInf (N Acc PxSg3)  IF (1 Inf + TRANS-V - NOTPXACC-V LINK NEGATE *1 Acc BARRIER REAL-V)(*-1 SG3-V + AUX - ("leat") BARRIER NOT-AUX-V OR Acc)(NEGATE -1 Num) ;
	#$ Dalle fertejit buotlágan ávnnaslaš eavttut maid dárbbaša vai duođalaččat galgá sáhttit kultuvrras dikšut.

SELECT:PxSg3AccSTV (N Acc PxSg3) IF (*-1 STRICT-TRANS-V - ("muitalit") BARRIER SV-BOUNDARY OR REAL-V OR Acc OR CLB LINK NOT 0 Attr LINK NEGATE *-1 Acc BARRIER NOT-NPMOD)(NOT 0 ("bealis") OR Sem/Time OR Sem/Event OR Sem/Org OR Sem/Plc)
#(NEGATE *-2 Acc BARRIER SV-BOUNDARY OR CC LINK NOT 0 Loc)
(NEGATE 0 Sem/Plc OR Sem/Org OR Sem/Ani LINK -1 ("viežžat") OR ("oaidnit") OR ("oažžut") OR ("fidnet") OR ("oastit") OR ("gávdnat") OR ("ohcat") OR ("deaivat")) ;
	#$ Juos mearriduvvo meahcceeatnamiid nuppástuvvan geavaheapmi golmma ja dušše golmma stivrralahtu doarjagiin ja uhcitlohku vuođđudallá oainnus sámi kultuvrra, boazodoalu, meahcásteami, ealáhusdoaimmaheami ja servodateallima destii mii lea árvvoštallojuvvon Sámedikki válddahusaid vuođul, de sáhttá uhcitlohku ovtta oli stivrračoahkkima áigge gáibidit ášši Sámedikki gieđahallamii.
	#$ 70 jagi čiegadii son sámevuođas.
	#% na ja mii oaččuimet rávddus gitta kruvnnu vihttalot evrre kilos alimus.

SELECT:Pxoidnosis N-PX + PxSg3 + Gen (1 ("oainnus")) ;
	#$ Gállábárdni geahčái buoremussan livkkihit eret áhčis oidnosis.
	
SELECT:oidnosis ("oainnus" N) (0 Adv LINK -1 HUMAN + Gen) ;

LIST NOT-PXPO = "beali" "dievva" "áigi" "maŋŋel" "maŋŋá" "ovdal" ;


#!! *  **PxSg3GenPo1** in front of Po, to the left of the owner
SELECT:PxSg3GenPo1 (Gen PxSg3)  IF (*-1 BOC OR COMMA BARRIER NOT-NPMOD)(1 Po - NOT-PXPO LINK NOT 1 go)(*2 SG3-V BARRIER NOT-ADV-PCLE LINK NOT 0 A LINK *1 NP-HEAD-NOM BARRIER NOT-NPMOD)(NEGATE 0 Loc LINK 1 (Adv Sem/Plc));
	#$ Raporttas bokte dáhttu bargojoavku čalmmustahttit Sámedikki.

#!! *  **PxSg3GenPo2** in front of Po, to the left of the owner
SELECT:PxSg3GenPo2 (Gen PxSg3) IF (1 Po - NOT-PXPO OR ("veara") LINK NOT 0 CS LINK NEGATE 0 Pr LINK 1 Gen)(NEGATE 0 Loc LINK 1 (Adv Sem/Plc) OR ("fárus") OR ("mielde"))
(NEGATE 0 N LINK NOT 0 HUMAN LINK 1 ("mielde") LINK *-1 COPULAS BARRIER REAL-V)
(NEGATE 1 ("ovdal"))
(NEGATE 0 PLACE + Loc LINK 1 PLACE + Ill)
(NEGATE 0 EVENT + Loc LINK 1 Pr LINK *1 Gen BARRIER NOT-NPMOD)
(0C N) ; 
	#$ Mánná bođii eatnis mielde.
	#$ Ábo logai eatnis maŋis eahketrohkosas.
	#$ Terje hárve hupmá mediain, muhto son áinnas hupmá sápmelašvuođas birra.
	#% Heahtečoahkkimis gaskal Eanandoallo- ja biebmodepartemeantta duorastaga ii leat boahtán čielga boađus .

#!! * **Genguossis** is  selection Gen, not only with Px. The FAMILY-set would be better than Sem/Hum-tag, but there is often a propernoun connected to the noun. guossái and guossis should have Po analysis?
SELECT:Genguossis Gen IF (0 Sem/Hum LINK 1 ("guossi" Ill) OR ("guossi" Loc)) ;	
	#$ Odne lea Joneš-bojá vuolgimin Biette-eagis guossái.

#!! * **GenNPFinal** selects Gen as the modifier of a noun in the end of a sentence.
SELECT:GenNPFinal Gen + Px IF (-1 COPULAS)(0 HUMAN + Loc LINK NOT 0 Attr OR A)(1C (N Nom) - Prop LINK 1 EOC) ; 
	#$ Ábo lei eatnis čalbmebealle.

#!! * **PxSg3Nom**
SELECT:PxSg3Nom (Nom PxSg3) IF  (-1 go LINK -1 Comp LINK *-1 NP-HEAD-NOM BARRIER Nom OR S-BOUNDARY OR COMMA)(NEGATE 0 Attr LINK *1 N BARRIER NOT-NPMOD);
	#$ Máhttájeaddji ii leat stuorit go oahpaheaddjis, ii ge bálvaleaddji stuorit go hearrás. 
	#% Mávssut leat adnon eambbo go bušeahtas.
	
#!! * **PxGenNorPo**  
SELECT:PxGenNorPo (Gen PxSg3) IF (0 N OR A) (*-1 (Sg Nom) OR (Sg3 Nom) BARRIER Pers OR S-BOUNDARY OR NOTPXACC-V OR Num OR Dem LINK NOT 0 Attr)(NEGATE *-1 (Imprt Sg2) BARRIER SV-BOUNDARY)(NEGATE -1 (N Nom) OR (N Acc))(0 N-PX)(NEGATE 0 Loc LINK 1 (N Ill) OR ("leat") + VFIN)(*1 N OR Po BARRIER NOT-A OR COMMA) ; # (NEGATE *-1C (V TV) + Ind BARRIER SV-BOUNDARY) ;
SELECT:PxGenNorPo (Gen PxSg3) IF (0 N OR A) (-1 Adv LINK *-1 (Sg Nom) OR (Sg3 Nom) BARRIER Pers OR S-BOUNDARY OR NOTPXACC-V OR Num OR Dem LINK NOT 0 Attr)(NEGATE *-1 (Imprt Sg2) BARRIER SV-BOUNDARY)(NEGATE -1 (N Nom) OR (N Acc))(0 N-PX)(NEGATE 0 Loc LINK 1 (N Ill) OR ("leat") + VFIN)(*1 N OR Po BARRIER NOT-A) ; # (NEGATE *-1C (V TV) + Ind BARRIER SV-BOUNDARY) ;
	#$ Nieida bođii ráhkkásis mielde.
	#$ Son ii váiban goassege guldaleames áhčis muitalusaid.
	#% Dat ferte addojuvvot dan dihte mánnái buoidi njammat, go ii eatnis leat mielki. 

#!! * **PxGenNum**  
SELECT:PxGenNum (Acc PxSg3) IF (0 N OR A) (-1 Num LINK *-1 (Sg Nom) OR (Sg3 Nom) OR (Sg3 V)  BARRIER Pers OR S-BOUNDARY OR NOTPXACC-V OR Num OR Dem LINK NOT 0 Attr) (NEGATE *-1 (Imprt Sg2) BARRIER SV-BOUNDARY)(NEGATE -1 (N Nom) OR (N Acc))(0 N-PX)(NEGATE 0 Loc LINK 1 (N Ill) OR ("leat") + VFIN) ; #(1 N OR Po)  ;
	#$ Son vulggii árra iđit meahccái ja čurvii vel golbma beatnagis fárrui.

#!! * **PxGenPr**  
SELECT:PxGenPr (N Gen PxSg3)  IF (-1 Pr LINK -1 BOC)((*1 VFIN + Sg3 OR (Pers Nom Sg3) OR (N Nom Sg) BARRIER NOT-ADV-PCLE) OR (1 Adv LINK 1 VFIN + Sg3 OR (Pers Nom Sg3) OR (N Nom Sg))) ;
	#$ Maŋŋá jápmimis de aliduvvui Gonagas Olav bassin. # "de" er en del av NOT-ADV-PCLE, derfor to regler

#!! * **PXGenoaivai**  for oaivái Po, there could be more Po for this rule?
SELECT:PXGenoaivai FAMILY + Gen IF (1 ("oaivái")) ;
	#$ Válden vealggi vielljan oaivái.

#!! * **eallitAcc** Selects Acc for eallit IV if you are *eallin* or *eallinahki*
SELECT:eallitAcc Acc IF (*0 ("eallit") BARRIER S-BOUNDARY)(0 ("eallin") OR ("eallinahki")) ;
	#$ Son elii eallimis Norggas.


#!! * **PXAccCoor**  
SELECT:PXAccCoor Px + Acc IF (-1 JA/DAHJE LINK -1C Acc) ;
	#$ Dat guhte ráhkista bártnis dahje nieiddas eanebut go mu, ii dohkke munnje.

#!! *  **PxSg3CC** in coordination with the owner
SELECT:PxSg3CC (PxSg3)  IF (-1 JA/DAHJE LINK -1C N-PX + Px + Sg);
	#$ Mun lean boahtán bidjat soahpameahttunvuođa bártni ja áhčis gaskii.

REMOVE:PxSg3Loc (PxSg3) IF (0 Sem/Plc + Loc)(NOT 0 ("bargu"));
	#$ Ruovttus hálaimet eanaš sámegiela, muhto mis ledje ollu dárogielat ránnját.


#!! * **PxSgIllPx**  
SELECT:PxSgIllPx Ill + Px IF (0 Sem/Plc OR Sem/Txt)(1 Acc OR EOS) (NEGATE 0 Po LINK -1 Gen)(NEGATE 0 (N Pl Nom) LINK -1 V + Pl3 ) ;
	#$ Čále girjásat bártniid namaid.
	#% Dan duođaštit čállosat..



#!! * **gaskaAcc**  
REMOVE:gaskaEss ("gaska" Ess)(0 ("gaskan" Adv) + Px) ;

REMOVE:PxPlaceV PxSg3 (0 Loc OR Adv LINK 0* PLACE-V OR ("leat") BARRIER SV-BOUNDARY) ;
	#$ Muhto čohkkájin ovtta sajis livttisin gollan duolba geađggi alde ja gehččen čáhcái.
	#$ Čuđiid jagiid vásáhusat ja duođašteamit leat buori muddui oidnosis , ahte álbmot ii sáhte šaddat kristtalažžan giela haga maid Ipmil oktii lea bidjan sin váimmuide ja sin baksamiidda .


SELECT:heakkaAcc Acc (0 ("heagga") + Px LINK *-1 ("massit")) ;
	#$ Vel okta nuorra boazodoalli massán heakkas ❡ .
SELECT:heakkaAcc Acc (0 ("ulbmil") + Px LINK -1 TRANS-V) ;


#!! We end section 2 by removing all remaining Px

#!! * **KillPx** removes all remaining Px readings
REMOVE:KillPx  Px (NEGATE 0 Adv) ;


#!! ## Section 3: Certain verb readings
SECTION


#!! * **FinGoInf** for  vai áigu go njulget.. Lene:  we don't need this
#REMOVE N OR PrsPrc IF (1 ("go" Qst))(2 Inf);
	## vai áigu go njulget boares boasttuvuođaid

LIST ADV_INF = "čohkut" "veallut" ;

#!! verb or adv	
SELECT:adv_inf Adv IF (0 ADV_INF LINK 1 CC LINK 1 ADV_INF) ;	
SELECT:adv_inf Adv IF (0 ADV_INF LINK -1 CC LINK -1 ADV_INF) ;	
	#$ Dás galget nuorat, čohkut dahje veallut duorggaid alde buolli árrandola ovddas vuostáiváldit oahpahusa sis.


#!! * **NotVGenIfDer** removes VGen if 0 = Der/Pass or Der...(r947)
REMOVE:NotVGenIfDer VGen IF (0 Der/Pass OR Der/InchL OR Der/st OR Der/l OR MOD-ASP);

#!! * **NotVGenIfDer** selects Actio Ess 
SELECT:ActioEss (Actio Ess) IF (0 (N Ess) LINK *-1 COPULAS OR ("fitnat") BARRIER REAL-V OR Attr OR Num);

#!! * **NotActio** selects Actio Ess 
REMOVE:Acc (Actio Ess) IF (0 Acc LINK -1 (Actio Ess));


	#!! ### All imperatives
	
#!! For imperative disambiguation we need the following:
#!! Pick imperative contexts, and thereafter remove imperative.
#!! Such contexts are: Imperative verb sentence-initially with exclamation mark

# Imperative examples:
	## ..., geahčá sámediggeválgalága láhkaásahusas.

# Non-imperative examples:
	## Rievdaduvvo leat ná: 
	## Doaimma birra¶
	## Evttohus 1¶
	## Bargu sámi fanasduddjonvieruid ektui lea dakkár bargu masa Sámediggi sáhtášii váikkuhit eanet dutkama bokte .
	## 
	
#!! * **NotEmbeddedImprt** removed Imprt after CS
REMOVE:NotEmbeddedImprt Imprt IF (-1 CS)(NEGATE 0 ("geahččat") LINK -1 COMMA) ;
	#$ Go ollet sáhpánvári ala, de muitte čuovvolit bálgá.
	#% Dán njuolggadusa deaŧalaš eaktu lea ahte ođđa siidaosiid ásaheapmi ii galgga dagahit ahte boazolohku šaddá alit go siidii mearriduvvon lohku, geahča dás maŋŋelis. 


#!! * **NotImprtWhenInd** removes Imprt if part of an Ind domain
#REMOVE:NotImprtWhenInd Imprt IF (*-1C Ind OR Rel BARRIER SV-BOUNDARY)(NEGATE -1 COMMA);
	#$ Beana jávkkai guhká ovdalgo bođii njága suolggaid, vilppui munnje ja manai fas. #denne funker med VGen-regel
	#% Olles máilmmis ii lean dakkár gievrras boles, jurddaš.
	#% Geahča, dá lea áhkkát, váldde su ja mana!
	
#!! * **NotImprtWhenIndCoor** removes Imprt when coordination of an Ind domain - a very special case
REMOVE:NotImprtWhenIndCoop Imprt IF (0 ConNeg LINK -1 ("ja" CC) OR ("dahje" CC) OR ("dehe" CC) LINK **-1 ConNeg LINK -1 Neg);
	#$ Bearehan dal son ii geige vel gieđa ja váldde maiddái eallima muoras ja bora ja beasa nu eallit agálaččat.

	
#!! * **NotImprtIfAttrLeft** removes Imprt after attribute
REMOVE:NotImprtIfAttrLeft Imprt IF (NEGATE -1 BOC OR COMMA)(NEGATE -1 CC LINK -1 Imprt LINK -1 BOC)(0 (N Nom) OR PrsPrc);
	#$ Buot oahppit leat mannan.	
	#% Gárvodeahkku ja mannu geahččat dien nieidda, gii váccii stuora gápmagiiguin.

#!! * **NotImprtIfRel** removes Imprt after Rel, unify this with other left context (r948)
REMOVE:NotImprtIfRel Imprt IF (*-1C Rel BARRIER S-BOUNDARY OR CC OR COMMA);

#!! * **ImprtDADJAT** removes  DADJAT
REMOVE:ImprtDADJAT Imprt IF (0 DADJAT + VFIN LINK *1 HUMAN + Acc LINK 1 Inf) ;
REMOVE:Sg2DADJAT Sg2 IF (0 DADJAT + Pl3 LINK *1 HUMAN + Acc LINK 1 Inf)(*-1 BOS BARRIER WORD) ;
	#$ Jáhkket Elin Kåvena vuoitit



	#!! ### Sg1 - early cycle, safe rules

LIST NOT-SG1-V = "lassánit" "geahppánit" "čoagganit" "čoahkkanit" ;

REMOVE:lassanan V + Sg1  IF (0 NOT-SG1-V + PrfPrc LINK NOT -1 Pron + Sg1) ;
	#$ Lassánan doarjagat sámi aviissaide.

#!! * **VSg1IfLeftMun** selects Sg1 when "mun" is to the left (r949)
SELECT:VSg1IfLeftMun V + Sg1  IF (*-1 MUN BARRIER Nom OR SG1-V OR S-BOUNDARY LINK NEGATE *-1 SG1-V BARRIER NOT-ADV-PCLE)(NEGATE 0 PrfPrc LINK *-1 REAL-COPULAS BARRIER S-BOUNDARY OR CLB)(NEGATE 0 PrfPrc LINK *-1 SG1-V BARRIER NOT-ADV-PCLE);
 	#$ Dego mun dás vuollelis mottiin ovdamearkkain čájehan,...

SELECT:VSg1NoMun V + Sg1  IF (-1 Po LINK -1 Gen LINK -1 BOS) ; 
 	#$ Telefuvnna bokte ožžon Álttá Johtolatstašuvnna ságaide.

#!! * **VSG1IfRightMun** selects Sg1 when "mun" is to the right (r950)
SELECT:r950 V + Sg1  IF (*1 MUN BARRIER NOT-ADV-PCLE);
 	#$ Man guhká ferten mun gierdat din?


	#!! ### Sg2 - early cycle, safe rules


#!! * **VSG2IfLeftDon** selects Sg2 when "don" is to the left (r951)
SELECT:VSG2IfLeftDon SG2-V IF ((*-1 DON BARRIER SG2-V OR ConNeg OR S-BOUNDARY OR VFIN LINK NEGATE *-1 SG2-V BARRIER S-BOUNDARY) OR (-1 (Rel Nom) LINK -1 DON));
 	#$ ...jos don fal luoitádat ja gudnejahtát mu.
 	#$ Rohkadallet dasto ná: Áhččámet, don guhte leat almmis.



#!! * **VSG2IfRightDon** selects Sg2 when "don" is to the right (r952)
SELECT:VSG2IfRightDon SG2-V IF (NEGATE 0 Num)(*1 DON BARRIER S-BOUNDARY LINK NEGATE *1 SG2-V BARRIER S-BOUNDARY OR CC OR CS);
 	#$ Leat go don dat guhte galgá boahtit?
 	#$ Reason for last LINK condition: Don sáhtát válljet galle alit sáni don háliidat.

#!! * **VInfIfAhte** removes Inf if there is no other VFIN between BOS and "ahte" (r953)
REMOVE:VInfIfAhte Inf IF (*-1 BOS BARRIER VFIN)(*1 ("ahte" CS) BARRIER NOT-ADV);
 	#$ Dainna lágiin fuobmát jođánit ahte sámi gielladilis lea árvu maid eará riikkain.


	#!! ### Sg3 - early cycle, safe rules

#!! * **VSG3IfLeftSon** selects Sg3 when "son" is to the left (r954)
SELECT:r954 V + Sg3  IF (*-1 SON BARRIER Nom OR SG3-V OR CS OR PUNCT OR CC LINK NEGATE *-1 SG3-V BARRIER NOT-ADV-PCLE)(NEGATE 0 Adv LINK *1 SG3-V);
 	#$ Geas ležžet bealjit, son gullos!

#!! * **VSG3IfRithgSon** selects Sg3 when "son" is to the right (r954)
SELECT:r955 V + Sg3  IF (*1 SON BARRIER NOT-ADV-PCLE OR go LINK NOT 1 V + Sg3 )(NEGATE 0 CS)(NEGATE 0 Adv LINK *1 V + Sg3  BARRIER SV-BOUNDARY);
 	#$ ...oažžu son atnit dušše dan čázis mii sutnje lea mearriduvvon.

#!! * **VNotSg3When12Left** removes Sg3 if 12 Pron immediate left (r955)
REMOVE:VNotSg3When12Left V + Sg3  IF (-1 (Pron Nom))(NEGATE -1 Sg OR Sg3);
 	#$ Sii manne vissui.

#!! * **VNotSg3IfCom** removes Sg3 in X with Y is... (r957)
REMOVE:VNotSg3IfCom V + Sg3  IF (-1 (Sg Com))(-2 (N Nom))(0 Du3);	
 	#$ Biret-guovttos Juffáin leaba čeahpit duddjot.

#!! * **Sg3vdic** selects Sg3 if VERBAL-ACTIVITY between comma and Nom
SELECT:Sg3 (Ind Sg3)(-1 COMMA)(0 VERBAL-ACTIVITY)(*1 (N Nom Sg) OR (?) BARRIER NOT-NPMOD) ;
	#$ Dáinna galggat don vuodjit, čilge Vegard ja cuige skutterii.
	#$ Dáinna galggat don vuodjit, čilge Veggard. # ? unknown names


REMOVE:r958 V + Pl3  IF (-1 (Pron Rel Sg))(0 V + Sg3 );
 	#$ Kapihttalis mas čilge orrunlanjadagaid latnjačovdosa nannejuvvo ahte lanjadat leat plánejuvvon orruide geat dárbbašit divššu guhkit.

#!! * **NegSg3BeforeFoc** selects Neg before Foc/ge or ConNeg (r959)
SELECT:NegSg3BeforeFoc (V IV Neg Sg3) + Ind IF (0 ("ii"))(*1 Foc/ge OR ConNeg BARRIER COMMA OR PUNCT);
 	#$ Ii oktage lea boahtán.

#!! * **vfin** removes verb reading when the reading should be noun  
REMOVE:vfin1 VFIN - Qst IF (0 (N Sg Nom) LINK *1 VFIN + Sg3 BARRIER NOT-ADV-PCLE OR Qst) ; 
REMOVE:vfin2 VFIN IF (0 Acc LINK *-1 TRANS-V BARRIER NOT-ADV-PCLE)(*-1 VFIN BARRIER S-BOUNDARY LINK NOT 0 Po)(NEGATE 1 Acc OR go)(NEGATE 0 VFIN LINK -1 Inf) ; 
	#% Galgabehtet muitit, ahte báhčit oažžu dáppe meahcis...
REMOVE:vfin3 VFIN IF (0 N LINK 1 ("vai" CC) LINK 1 N LINK NOT 0 VFIN) ;
	#$ Birasgáhttenlihkadus ja álgoálbmogat – ovttasbargit vai vuostálagaid?
REMOVE:vfin4 REAL-V IF (0 N LINK 1 ("dat" Pcle)) ;
	#$ Riššat dat gal leat musge, jus eai leačča njuoskan.
REMOVE:vfin5 VFIN IF (0 (N Pl Nom) LINK *1 VFIN + Pl3 BARRIER NOT-ADV-PCLE LINK NOT 0 Inf) ; 

REMOVE:vfin4 VFIN - Qst IF (0 N LINK *-1 AUX BARRIER NOT-ADV-PCLE OR Qst)(*1 Inf BARRIER NOT-ADV-PCLE) ; 
	#$ Áigugo fas skuvlii álgit?

SELECT:vfin5 VFIN + LOC-V IF (0 Ill LINK -1 CC)(1 Loc) ; 
	#$ Tommi geahčai boksii dugo heajuid vihkume ja jearai sus.

REMOVE:vfinPrfPrc Der/Pass + Sg1 IF (0 PrfPrc LINK *1 N)(NEGATE *-1 Pron + Sg1) ;


#!! Negative verb, not abbreviation or roman numeral Ii.

	#!! ### Du1 - early cycle, safe rules

#!! These Du1, Du2 rules are (almost) not in use in our corpus, but we keep them for completeness.

#!! * **VDu1IfMoaiLeft** selects Du1 when "moai" left (r960)
SELECT:VDu1IfMoaiLeft V + Du1  IF (*-1 MOAI BARRIER DU1-V OR S-BOUNDARY OR PUNCT LINK NEGATE *-1 DU1-V BARRIER NOT-ADV-PCLE);
 	#$ Moai dihte ahte dát lea munno bárdni.

#!! * **VDu1IfMoaiRight** selects Du1 when "moai" right (r961)
SELECT:VDu1IfMoaiRight V + Du1  IF (NEGATE 0 Adv LINK *-1 BOS OR PUNCT BARRIER NOT-CC)(NEGATE 0 Adv LINK *-1 V + Du1 )(*1 MOAI BARRIER S-BOUNDARY);


      
	#!! ### Du2 - early cycle, safe rules

#!! The next two rules are not found in the corpus, but logically
#!! they belong, to cover the whole paradigm. There is no verb-internal
#!! homonymy here, but there is homonymy with e.g. Illative for certain verbs.

#!! * **VDu2IFDoaiLeft** selects Du2 if "doai" to the left (r962)	 
SELECT:VDu2IFDoaiLeft V + Du2  IF (*-1 DOAI BARRIER DU2-V OR SV-BOUNDARY OR PUNCT LINK NEGATE *-1 DU2-V BARRIER NOT-ADV-PCLE);
 	#$ Manne doai ozaide mu?

#!! * **VDu2IFDoaiRight** selects Du2 if "doai" to the right (r963)	 
SELECT:VDu2IFDoaiRight V + Du2  IF (*1 DOAI BARRIER NOT-ADV-PCLE);
 	#$

	#!! ### Du3 - early cycle, safe rules

#!! The competitor to Du3 is -ba Foc.

#!! * **VDu3IfSoaiLeft** selects Du3 when "soai" left (r964)
SELECT:VDu3IfSoaiLeft V + Du3  IF (*-1 SOAI BARRIER DU3-V OR SV-BOUNDARY OR PUNCT LINK NEGATE *-1 DU3-V BARRIER NOT-ADV-PCLE);
 	#$ Soai leaba dahkan čielga oavdudagu.
 	#$ Soai ferteba mannat. 

#!! * **VDu3IFSoaiLeft** selects Du2 if "doai" to the right (r965)	 
SELECT:VDu3IFSoaiRight V + Du3  IF (*1 SOAI BARRIER NOT-ADV-PCLE);
 	#$ Fertebago soai mannat? 

#!! * **VDu3IfGuokteLeft** selects Du3 if "guokte" left (r966) - 15
SELECT:r966 V + Du3  IF (*-1 ("guokte" Nom) BARRIER SV-BOUNDARY) ; 
 	#$ Guokte nissona ferdneba milluin.

#!! * **VDu3IfGuokteRight** removes Sg3 if "guokte" right and 0 Du3 (r967)
REMOVE:VDu3IfGuokteRight V + Sg3  IF (0 Du3)(*1 ("guokte" Nom) BARRIER NOT-NPMODADV);
 	#$ Mus leat dušše dát guokte oappá geaid don dás oainnát.

#!! * **VDu3IfNjaNLeft** selects Du3 as verb with coordinated subject to the left (r968) - 43
SELECT:VDu3IfNjaN V + Du3  IF (*-1 (N Nom) BARRIER NOT-ADV-PCLE LINK *-1 ("ja") BARRIER NOT-NPMOD LINK -1 (N Nom));
 	#$ Per Berg ja Rolf Olsen leaba doppe.
 	#$ Deanu Searat ja Sámi Siida leaba ovttas ordnen dáid gilvvohallamiid.
	#$ Stuorra nieida ja unna gánda ferteba leat doppe.
 # Note that 'Searat' is Pl, hence no number requirement in this rule.
  	#$ Mu vánhemat, sihke eadni ja áhčči, leaba jápmán.

#!! * **VDu3IfNjaNRight** selects Du3 as verb with coordinated subject to the right (r969) - 12
SELECT:VDu3IfNjaNRight V + Du3  IF (*1 (Sg Nom) BARRIER NOT-NPMODADV LINK 1 ("ja") LINK *1 (Sg Nom) BARRIER NOT-NPMOD);
 	#$ Dá leaba Brita ja Ole Kåven.

#!! * **VDu3IfCollLeft** hmm, remove this?
REMOVE:r970 V + Sg3  IF (*-1 (Coll Nom) BARRIER SV-BOUNDARY )(0 V + Du3 );
 	#$ Biera-guovttos Márehiin leaba daid mánáid váhnemat.

	#!! ### Pl1 - early cycle, safe rules

#!! The competitor here is obviously Inf, but also Pl3 and Prt Sg2.

#!! * **goasbeareInf** goas beare Inf
SELECT:goasbeareInf Inf (-1 ("beare") LINK -1 ("goas")) ;
	#$ Su rašes eallinláigi sáhtii goas beare boatkanit.

#!! * **VPl1IfMiiLeft** selects Pl1 if "mii" Pron to the left (r971) - 3163
SELECT:VPl1IfMiiLeft V + Pl1  IF (*-1 MII-PERS BARRIER VFIN OR PUNCT LINK NEGATE *-1 PL1-V BARRIER NOT-ADV-PCLE)(NEGATE *-1 AUX + Pl1 OR INF-V + Pl1 BARRIER REAL-V OR COMMA)(NEGATE -1 MII-PERS LINK -1 ConNeg + INF-V OR AUX + ConNeg LINK -1 Neg + Pl1)(NEGATE 0 Inf LINK -2 ("vaikko") OR ("vaikke")); # underlesning pga Apertium: Miihan čoahkkanit dohko ávvudit su barggu
 	#$ Mii sápmelaččat gal leat vásihan dán...
 	#$ Miihan čoahkkanit dohko ávvudit su barggu.
 	#% – Juo dávjá sáhttá nu, muhto dan eat dáhtu mii árvvoštallat.

#!! * **VPl1IfMiiRight** selects Pl1 if "mii" Pron to the right (r972) - 272
SELECT:VPl1IfMiiRight V + Pl1  IF (*1 MII-PERS BARRIER NOT-ADV-PCLE OR ("seammá") OR S-BOUNDARY LINK NEGATE *1 SG3-V BARRIER NOT-A-ADV OR CLB)((*-1 BOS BARRIER WORD - Adv) OR (-1 Acc LINK *-1 BOS BARRIER WORD));
#SELECT:VPl1IfMiiRight V + Pl1  ((1 ("go" Qst) LINK 1 MII-PERS) OR (-1 MO OR ("danne") LINK 2 MII-PERS) OR (0 TRANS-V LINK -1 Acc LINK 2 MII-PERS)) ;
 	#$ Galgat go mii vuordit muhtun eará? 
 	#$ Čorgestallat mii maid várra veahážiid juohke beaivvi.
SELECT:VPl1IfMiiRight2 V + Pl1  IF (0 AUX LINK *1 MII-PERS BARRIER NOT-ADV-PCLE OR ("seammá") OR S-BOUNDARY LINK 1 PrfPrc);
 	#$ Min cealkámušas universitehta ja allaskuvlalága rievdadeapmái leat mii fuomášuhttán ahte UA-lágas galggašii sierra mearrádus sámegiela fágagiela ektui, dadjá rektor Laila Susanne Vars.


#!! * **VPl1NotImprIfMiiLeft** removes Imprt if if "mii" Pron to the left and 0 = "mii" (r973) - 557
REMOVE:r973 Imprt IF (-1 MII-PERS OR (N Pl Nom))(0 (V Prs Pl1));
 	#$ Mii áigut maiddái leat aktiivvalaččat mielde juohke dásis.

	#!! ### Pl2 - early cycle, safe rules

#!! These rules are not used when disambiguating the corpus
#!! * **VPl2IfDiiLeft** selects Pl2 if "dii" Pron to the left (r974) - 0
SELECT:r974 V + Pl2 IF (*-1 DII BARRIER NOT-ADV-PCLE);

#!! * **VPl2IfDiiRight** selects Pl2 if "dii" Pron to the right (r975) - 0
SELECT:r975 V + Pl2 IF (*1 DII BARRIER NOT-ADV-PCLE OR MO);
 	#$ Addet dii sidjiide borramuša!


#!! ### Pl3 - early cycle, safe rules

#!! Select...

#!! * **r976** SE V Pl1 if *-1 SII
SELECT:r976 V + Pl3  IF (*-1 SII BARRIER PL3-V OR S-BOUNDARY OR PUNCT LINK NEGATE *-1 PL3-V BARRIER NOT-ADV-PCLE)(NEGATE 0 Adv LINK *1 PL3-V BARRIER S-BOUNDARY);
 	#$ ...vai sii áiggi mielde oččoše doarvái gelbbolašvuođa.

#!! * **r977** SE V Pl1 if *1 SII
SELECT:r977 V + Pl3  IF (*1 SII BARRIER NOT-ADV-PCLE OR S-BOUNDARY)(NEGATE 0 Adv OR Po LINK *1 PL3-V BARRIER S-BOUNDARY)(NEGATE 0 Sg3 LINK -1 Nom + Sg) ;	
 	#$ Máhttego sii čilget áššiset dárogillii?
 	#% Trygg Trafikk bivdá váhnemiid jurddašit movt sii sihkkarastet mánáideaset biillas geassejohtolagas.

#!! * **VPl3jaPl3** selects Prt Pl3 in coordination (r978)
SELECT:VPl3jaPl3 (Prt Pl3) IF (*-2C (Prt Pl3) BARRIER VFIN)(-1 JA/DAHJE);
 	#$ Ja buot olbmot imaštalle sakka ja jerre. 

#!! * **muVPl3** removes Prs Pl1 after mu
REMOVE:muVPl1 Prs + Pl1 IF (-1 ("mun") + FIRSTPERS + Acc);
 	#$ Manne mu doalahit olggobealde dákkár áššis man birra mun nai liikošin gullat 


#!! The following two may be joined:

#!! * **VPl3IfPronRelLeft1** selects Pl3 if -1 Rel is linked to -2 Pl (r979) - 7801
SELECT:VPl3IfPronRelLeft1 V + Pl3  IF (-1 (Pron Rel Pl Nom) OR MOVT)(-2 (N Pl));


#!! * **VPl3IfPronRelLeft2** selects Pl3 if -1 Rel is linked via COMMA to -3 Pl (r980) - 853
SELECT:VPl3IfPronRelLeft2 V + Pl3  IF (-1 (Pron Rel Pl) OR MOVT)(-2 COMMA)(-3 (N Pl));
 	#$ Njuolggadusat movt meannudit oanehisáigge vealgegáibideami, beaivádan 25.04.00.
 	#$ Vai bálvalusas lea buohccedikšofágalaš dássi eaktuduvvo ahte leat fágabargit geat virgáibiddjojit.

#!! * **VPl3IfCSLinkPl3Left** selects Pl3 if -1 Rel is linked via COMMA to -3 Pl (r979) - 341
SELECT:VPl3IfCSLinkPl3Left V + Pl3  IF (-1 CS)(*-2 V + Pl3  BARRIER VFIN LINK -1 (N Pl Nom) OR (Pron Pers Pl3 Nom))(NEGATE 0 Inf LINK 1 COMMA OR CC LINK 1 Inf);
 	#$ Sii ožžot smiehttanáiggi jos háliidit muitalit dan birra.
 	#% Diet vuoigatvuođat leat ráddjejuvvon guoskat dan vuoigatvuhtii ahte oamastit, atnit, ovddidit ja mearridit daid eatnamiid ja riggodagaid mat sis leat odne árbevirolaš čanastumi dihte.


#!! Remove...


#!! The following two may be joined:

#!! * **r982** removes Prt Sg2 if Pl3 subject - 6002
REMOVE:r982 (V Prt Sg2) IF (*-1 NP-HEAD-PL-NOM BARRIER VFIN OR S-BOUNDARY)(0 V + Pl3 );
 	#$ Daid gažaldagaide, mat dán čielggadeamis galget gieđahallot, ...

#!! * **r983** removes Prt Sg2 if Pl3 subject via CS - 305
REMOVE:r983 (V Prt Sg2) IF (-1 CS LINK *-1 NP-HEAD-PL-NOM)(0 V + Pl3 );
 	#$ Hálddašanásahusat maid várra fertejit dákkár deasttaid atnit rievttálaš árvvoštallamis go galget mearridit mii dan sisdoallu berrešii leat.

#!! * **VPl3Lookalikes** removes "verbs" like "manne" and "dušše" (r984) - 274
REMOVE:VPl3Lookalikes V + Pl3  + Ind IF (1C VFIN)(NEGATE 1 Pl3) ;
 	#$ Manne ii leat áviisa boahtán?
 	#$ Máhtte manai čuoigga skuvlii.
 	#$ Vihtta bivdovuogi mun gal dušše namahin. 

REMOVE:vissalit V IF (0 ("viššal") + Adv LINK -1 REAL-V ) ;

#!! * **VSg3Lookalikes** removes "verbs" like "skuvlii" 
REMOVE:VSg3Lookalikes V + Sg3  + Ind IF ((*-1C VFIN BARRIER S-BOUNDARY OR REAL-V OR CC OR CS OR COMMA LINK NOT -1 Rel) OR (0 (N Ill) LINK -1 Gen LINK NOT 0 TIME))(NEGATE 1 (Nom Sg3) OR (N Nom Sg) OR ("leat")) (NEGATE -1 (Nom Sg3) OR (Nom Sg)) ;
	#$ Moai vázze ruovttus skuvlii.
	#% Vai bálvalusas lea buohccedikšofágalaš dássi eaktuduvvo ahte leat fágabargit geat virgáibiddjojit.


#!! * **VPl3NotSg2BefPassive** removes Sg2 for Pl3 and Inf before passive (r985)
REMOVE:r985 Prt + Sg2 IF (NEGATE *-1 (Pron Pers Sg2))(0 Prs + Pl3 )(*1 Der/Pass BARRIER NOT-ADV);
 	#$ Reivelonuhallan sámi institušuvnnaiguin galgá dahkkot sáme- ja dárogillii. 
	# Probably not prodrop if passivized Sg2.

#!! * **EssNotV** selects Ess instead of VFIN
SELECT:EssV Ess IF (0 ESS-ADVL-N)(*-1 VFIN BARRIER S-BOUNDARY) ;
	#$ Beaskkain bivvá buolašin.

#!! * **Esscoor** selects Ess instead of NomAct
SELECT:Ess Ess IF (0 N)(*-1 JA/DAHJE BARRIER NOT-A LINK -1 Ess) ;
	#$ Rolf Morten Anti Amundsen beakkihii beaškasiin nuhttejeaddjin ja čállin joavkkuin rOlfFa jagi 2006.


#!! * nuorra (vs. nuorrat V)
REMOVE:nuorraV V IF (0 ("nuorra") LINK *-1 NOT-COP-V BARRIER SV-BOUNDARY);
	#$ Son bođii dohko nuorran.
REMOVE:nuorraV V IF (0 ("nuorra") LINK *1 V BARRIER SV-BOUNDARY);
	#$ Min nuorat leat mielde nu olu doaimmain, sii leat oidnosis sápmelažžan.
#	
#LIST DU2-OR-NOUN = "áitit" "besset" "busset" "čuovgat" "doaibmat" "dulljat" "girdnut" "girjet" "gođđit" "latnjat" "lavdnjet" "láhtut" "oassálastit" "nuorrat" "njallat" "sáhkat" "válgat" "váret" "vierrut" "vistit" ;	
#
REMOVE:Du2 Ind + Prt + Du2 IF (0 N - DER-N LINK NEGATE *-1 (Nom Du2))(NOT 0 ("leat") OR ("váldi")) ;
#REMOVE:Sg2 Ind + Prs + Sg2 IF (0 N - DER-N LINK NEGATE *-1 (Nom Sg2))(NOT 0 ("leat")) ;
#!! * **PlNomCoor** Selects (N Pl Nom)
REMOVE:Sg2 Prs + Sg2 IF (0 (N Pl Nom) LINK *-1 CC BARRIER NOT-A LINK -1 Nom + Pl) ;
	# gođiide nuoraide nuorat
	#$ De lei erohus oahpahusas «dáččamánáide» ja «sámi nuoraide».
	#$ Dákko leat sihke mánát ja nuorat hearkkit.
	#% Dát lea dat beaivi go meastui válddiide gitta Jack Sparrowa!
	#% Makkár lágán diŋggaid lávet gávdnat?
	#% Go ollet Sáhpánvári ala, de muitte čuovvulit bálgá.
	#% Maid siđat odne?
	#% Bora vai ealát.
	#% Lávet go vuovdit guliidat?

#!! * **johtilit og bastilit** removed johtit + Der/l 
SELECT:Vjohtilit VFIN + Pl3 IF (0 Adv) (-1 N + Nom + Pl)(NEGATE *1 REAL-V BARRIER SV-BOUNDARY) ;
REMOVE:johtit Der/l IF (0 ("johtilit") + Adv OR ("bastilit") + Adv OR ("ceavzilit") + Adv OR ("spáitilit") + Adv OR ("bahččagit") + Adv) ;
	#$  Maŋŋá bohten diehtit athe son lei ieš cealkán iežas eret barggus ja vuolgán nu johtilit ahte ii Ullage lean gullan maidige.
REMOVE:johtit Der/l + Inf IF (0 Adv) ((-1 Inf OR VFIN) OR (-1 Acc LINK -1 Inf + TV OR VFIN + TV)) ;



	
	#!! ### PrsPrc
	# - - - -

#!! * **PrsPrc** selects PrsPrc if coordinated with A - 10
SELECT:PrsPrcA PrsPrc IF ((-1 GRADE-ADV LINK 2 N) OR (1 JA/DAHJE OR COMMA LINK 1 A LINK 1 N)) (NEGATE 0 Sem/Hum OR ("goargu"));
 	#$ Soahpamušas váilot njuolggadusat smávit vahágahtti ja geavatlaš bággolonistemiide. 
 	#$ Mearridanváldi berre leat nu čadni go vejolaš.
 	#$ Mudden ferte dagahit janza ja hui vahágahtti meassamiid.
#!! Early rule since many PrsPrc readings are removed later.

SELECT:PrsPrcAdvl PrsPrc IF ((-1 PLACE + Loc) OR (-1 PLACE-PO LINK -1 Gen) OR (-1 Adv LINK *-1 BOS OR (":") BARRIER NOT-ADV-PCLE))(NEGATE 0 VFIN LINK *-1 (Nom Sg)) (1 N)(NEGATE 0 ("orrut") + VFIN LINK *1 PrfPrc BARRIER REAL-V)(NEGATE 0 VFIN LINK 1 (Nom Sg) LINK NEGATE *1 VFIN + Sg3)(NEGATE 0 ("oažžut")) ; ;
	#$ Finnmárkkus ássi olbmuid divat ii galgga leat alit go guovtti dan mađe go gielddas ássi olbmuid divat.
	#$ Uvnna duohken orru sáhpánat rikkerejit beaivvi.
	#$ Guhkkelis boaittobeale orru mánát bohte Gárdima ja Mollešjoga, Šuoššjávrri ja Iešjohleagi duoddarstobuin.
	#$ Ledje gal geahččalan, vaikko unnán lihkostuvve, ásahit smávva skuvlastobožiid, gos johtti oahpaheaddji ja skuvliiohcaleaddji nuorat sáhtte gávnnadit moadde vahkku ain háválassii.
	#% Seammás orru Alimusriekti Skjerstad-áššis dovdan iežas čadnojuvvon dupmui.
	#% Dál bissu čájáhus Davvi-Norgga Dáiddamuseas borgemánu 27. beaivái.

#!! OBS: denne er ikke helt bra
#SELECT:PrsPrcAdv2 PrsPrc IF (-1 COPULAS)((-2 PLACE + Loc OR Adv) OR (-2 Po LINK -1 Gen))(1 N);
	#$ Muđui ledje johtti oahpaheaddjit, muhto eai doarvái.
REMOVE:NomAgPrsPrc PrsPrc IF (0 NomAg LINK NOT 0 ("johtti") OR ("ealli") OR ("orru")) ;

SELECT:PrsPrcealli PrsPrc IF (0 ("eallit") LINK 1 Sem/Org) ;  
	#$ báikkit gos lea ealli álbmotstivra

SELECT:gradeadv Adv IF (0 GRADE-ADV LINK 1 PrfPrc) ;  
	#$ Ballagohten, muitala Astrid Johnskareng ja lohká iežas mealgat váfon go šattai geahpedit lášmmohallama.
	
	

SELECT:PrsPrcImprt PrsPrc IF (0 (Imprt Du2) LINK 2 (Neg Pl3)) ;  # Bør utvides
	#$ Leahkki vuoigatvuođat eai lihkahallojuvvo.

SELECT:bealliGen Gen (0 ("bealli") LINK -1 Prop OR Sem/Plc)(1 N) ;


SELECT:ovddidan Actio + Nom IF (0 ("ovddidit") LINK 1 ("arena")) ;
	#$ Sámi mánáidgárddioahpaheaddji studeanttat čállet mánáidgárddi giellaealáskahttima birra ja ovddidan arenan.


#!! * Actio Gen
SELECT:GenVeara_a (Actio Gen) IF  (1 ("veara") OR ("dihte"));
SELECT:GenVeara_b (Actio Gen) IF  (1 CC LINK **1 Gen BARRIER NOT-NPMOD-ACC LINK 1 ("veara") OR ("dihte")) ;
	#$ Dasgo bargi lea bálkkás veara.
	#$ Guoimmuhan ja lassiruđa háhkama dihte lágiduvvojedje maiddái eará doalut, nugo iešguđet lágan smávit gilvvožat.

SELECT:ActioNom (TV Actio Nom) IF (0 N - Der/Pass LINK -1 HAB-ACTOR + Gen OR (Pers Gen) LINK NOT 0 NATIONAL-IDENTITY LINK NEGATE *-1 COPULAS OR DADJAT BARRIER SV-BOUNDARY)((*1 N BARRIER NOT-A LINK NOT 0 ("dihte")) OR (1 Attr LINK 1 N)) ; 
	#$ Dán gieđahallamii ii searvit Finnmárkku fylkkadikki válljen maŋimuš stivrralahttu.
	#% Lea maiddai ohppiid lonohallan suohkaniid gaskkas ja oahppit leat sirddihuvvon skuvllaide nuppe beallai rája.

REMOVE:ActioNom (Actio Nom) OR (Actio Com) ; # until we have examples of Actio Com

REMOVE:ActioGen (Actio Gen) IF (NOT 1 Po OR ("veara") OR ("miella") + Loc OR Sem/Org)(NEGATE 1 CC LINK 1 Gen LINK 1 Gen LINK 1 Po OR ("veara") OR Sem/Org) (NEGATE 0 ("boaittobealli") OR ("geardi") OR Sem/Time) (NEGATE -1 Num) ;
	#$ Sii lohket fatnasa gopmánan.
	#$ Sii lohket bártni lápmašuvvan.

SELECT:GenMielas (gen) (1 ("miella") + (loc)) ; #apertium rule, in apertium smenob we don't go for mielas Po


SELECT:ENr2996b PL3-V IF (NEGATE *-1 VFIN BARRIER CS OR CC)((*1 (Sg Nom) BARRIER NOT-NPMODADV LINK NOT 0 Adv LINK 1 CC LINK 1 Nom) OR (*1 (Pl Nom)  BARRIER NOT-NPMODADV LINK NOT 0 Adv OR Inf) OR (1 Attr LINK 1 (Pl Nom) LINK NOT 0 Adv OR Inf) OR (1 ("leat" PrfPrc) LINK *1 (Pl Nom) BARRIER NOT-NPMODADV LINK NOT 0 Adv OR Inf) OR (1 ("leat" PrfPrc) LINK *1 NUM + Nom BARRIER NOT-NPMODADV LINK NOT 0 Adv OR Inf) OR (*1 NUM + Nom BARRIER NOT-NPMODADV LINK NOT 0 Adv OR Inf)) (NEGATE 0 ("leat" Imprt Pl2))(NEGATE 0 Inf LINK -1 MOVT)(NEGATE 0 Imprt LINK -1 CC OR BOS OR COMMA OR (":") OR ("de") OR PUNCT)(NEGATE 0 Po LINK -1 Gen)(NEGATE 0 Sg3 LINK -1 Nom + Sg)  ;
	## Muhto dorskit gal leat varrasat.
	## Ja dis leat velá buot oaivvi vuoktačalmmit lohkkojuvvon.
	#$ Son lea čállán lohkameahttun čállosiid sámi historjjás ja sus leat leamaš ollu luohttámušdoaimmat.
	#$ Dáhpáhusa lágidit Sámemusea ja luondduguovddáš.
	#$ Maŋŋel koronapandemiija de norgalaččat ostet unnit bálvalusaid ja eanet gálvvuid, dan čájehit Statistalaš guovddášdoaimmahaga logut.
	#% Mii doarjut Sámelávdegotti evttohusa oainnu ahte sámegiela berre sáhttit válljet fágan dan sadjái go duiskkagiela gymnásaráhkkaneaddji linnjás nuoraidskuvlla ovccát jagis.

SELECT PL3-V IF (NEGATE *-1 VFIN BARRIER CS OR CC)(1 Loc OR DOPPE LINK *1 NUM + Nom BARRIER NOT-NPMODADV LINK NOT 0 Adv OR Inf) (NEGATE 0 ("leat" Imprt Pl2))(NEGATE 0 Inf LINK -1 MOVT)(NEGATE 0 Imprt LINK -1 CC OR BOS OR COMMA OR (":") OR ("de") OR PUNCT)(NEGATE 0 Po LINK -1 Gen)(NEGATE 0 Sg3 LINK -1 Nom + Sg)  ;
	#$ Dál leat 28 jagi vássán ja 2017 čavčča leat Sámi allaskuvllas 270 studeantta.


SELECT:ENleatPl3Rel ("leat" Pl3) IF (*-1 PL3-V - Neg BARRIER VFIN OR S-BOUNDARY LINK -1 (Rel Nom Pl) LINK -1 NP-HEAD-PL-NOM) (*1 NP-HEAD-PL-NOM BARRIER NOT-NPMODADV) ;
	#$ Oahpaheaddjit geat leat leamaš dán suorggis leat sihke sámit ja dážat, muhto dat geat bohte olggobealde ledje ovdal bargan dearvvašvuođabálvalusas sámi guovlluin ja ledje integrerejuvvon sosiálalaččat ja dađistaga maiddái gielalaččat. # leat sihke

#!! * **BeallileatPl3** when bealli or oassi + Pl Loc
SELECT:BeallileatPl3 ("leat" Pl3) IF (*1 ("bealli") OR ("oassi") BARRIER NOT-NPMODADV LINK 1 Pl + Loc) ;
	#$ Doaimmahusgoddi lea mearridanválddálaš, juos das leat bealli miellahtuin ja ságajođiheaddji.


#!! * **ENInf1** 
SELECT:ENInf1 Inf IF (*-1 INF-V OR MOD-ASP BARRIER S-BOUNDARY OR CS OR Neg OR COMMA OR CC LINK NOT 0 N - Der/NomAg OR PrfPrc)(NEGATE *1 Inf BARRIER SV-BOUNDARY OR Acc)(NEGATE 0 Adv);
  	#$ Bissuin galgá heahkkahuhttit, ii nie váigadit goddit; sáhttá leat váiga.
	#$ Dearvvašvuođabargit galget divššohasa agi, silolaš dilálašvuođa, láddama ja vásáhusduogáža ektui láhčit dilálašvuođaid buoremus lági mielde.
	#$ Vuoittut mat galge čohkket olbmuid básárii ja oažžut sin rahpat sin juovlaburssaid, ledje gal anolaš ja ođđaáigásaš diŋggat.
	#% Jos galggaš sáhttit ovddidit mánáidgárddiid orgánan gos oahppat ja ollašuhttit oahppogáibádusa mánáidgárdelága mielde,

#!! * **ENInf2** selects Inf (NOTE, this was further down in sme-dis)
SELECT:ENInf2 Inf IF (*-1 INF-V + V BARRIER SV-BOUNDARY OR Neg OR CS OR COMMA OR CC)(NEGATE -1 Attr)(NEGATE -1 Attr OR PrfPrc LINK *-1 CC BARRIER NOT-ADV)(NEGATE 0 Adv LINK 1 A) ;
	#$ Skuvla galggai oahpahit sámemánáide dárogiela.
	#$ Prošeavtta áigut čađahit.
	#% Mearrasápmelaččat ledje áŋgiris ja oahppan bivdit.

SELECT:goInf Inf IF (-1 (Sem/Measr_Time) LINK *-1 go BARRIER NOT-NPMOD LINK -1 AUX LINK -1 Neg) ;
	#$ Son ii galgan go jagi lohkat, muhto studeanttaeallin geasuhii su eambbo ja nu son jotkkii studeremiin.

#!! * **ENInf** selects Inf  
SELECT:ENInf Inf IF (-1 ("ahte") LINK *-1 ("bargat") BARRIER SV-BOUNDARY OR Neg OR CS OR COMMA OR CC)(NEGATE -1 Attr) ;
	#$ Sii barget dan nammii ahte eastadit illasteami.

#!! * **ENInf** selects Inf  
SELECT:ENInf Inf IF (*-1 ("namalassii") OR ("dan dihte") BARRIER SV-BOUNDARY OR Neg OR CS OR COMMA OR CC OR Nom)(NEGATE -1 Attr) ;

#!! * **InfgoInf** selects Inf  
SELECT:InfgoInf Inf IF (-1 go LINK -1C Inf - AUX) ;
	#$ Mun in sáhttán dalle eará dahkat go bissut dien virggis, go ballen Ovddádusbellodaga beassat dasa de čohkkedit.

#!! * **ENInfcoor1** selects Inf coor
SELECT:ENInfcoor1 Inf IF (*-1 INF-V + V BARRIER SV-BOUNDARY OR Neg OR CS OR COMMA OR CC)(NEGATE -1 Attr)(NEGATE -1 Attr OR PrfPrc LINK *-1 CC BARRIER NOT-ADV)(NEGATE 0 Adv LINK 1 A) ;
	#$ Guovlluide mat leat árbevirolaš sámi geavahan- ja ássanguovllut, ferte dasto sihkkarastojuvvot dárbbašlaš gelbbolašvuohta ja resurssat čuovvolit dan sámepolitihka maid stáhta, guovlulaš ja báikkálaš hálddahusdásit hábmejit.

#!! * **ENInfcoor2** selects Inf coor
SELECT:ENInfcoor2 Inf IF (-1 CC OR COMMA LINK *-1 Inf BARRIER SV-BOUNDARY OR REAL-V OR CS OR CC OR COMMA)(0 AUX OR INF-V + V LINK 1 Inf) ;
	#$ Seammás bohte eanet ja eanet gáibádusat dasa ahte boazosápmelaš galggai dovdat lágaid, máhttit doaimmahit rehketdoalu ja eará báberbargguid ja čállit reivve.

#!! ***listInf** in lists
SELECT:listInf Inf (0 COPULAS LINK *-1 BOS BARRIER WORD)(1 Nom OR Ess) ;
SELECT:listInf Inf (0 HUMAN-AGENT-V LINK *-1 BOS BARRIER WORD) ;
	#$ Leat diehtoguovddáš studeanttaide.

#!! # Section 4: CYCLE 1B: REMOVING THE READINGS THAT WERE LEFT FROM THE 1A RULES 
# =====================================================================
SECTION

#!! We don't need more Px sections, it's done alrady

#!! ## Noun, adjectiv, PrsPrc or not?
SELECT:NCoor (N Nom) (0 VFIN LINK -1 JA/DAHJE OR COMMA LINK -1 (N Sg Nom))((1 EOS) OR (1 JA/DAHJE LINK 1 (N Sg Nom)))(NEGATE 0 Cmp/SplitR) ;
	#$ Seamma gusto uhcit fuđđožiidda nu go neahti, čeavrris, mievri ja rieban.

LIST NAMMADIT = "nammadit" "válljet" "virgádit" ;

SELECT:Prof N + Ess IF (0 PROFESSION LINK *-1 OPRED-V BARRIER SV-BOUNDARY OR COMMA) ;  
	#$ Sámediggepresideanta Aili Keskitalo nammadii bearjadaga Guovddášbellodaga Anne Toril Eriksen Balto (40) ođđa politihkalaš ráđđeaddin.


SELECT:NounNotEará N IF (-1 (A Attr) OR (Indef Attr) LINK NOT 0 OLLU)(NEGATE -1 Adv OR Interr)(NEGATE 0 Inf OR VFIN OR Der/veara OR ("veara") OR ("leat" PrfPrc) OR Indef OR Foc/gis)(1 NP-BOUNDARY)(NEGATE 0 PrfPrc LINK -1 GRADE-ADV);  
	#$ Dat lea čuovvovaš sajis.
	#$ Áhku vuostá lea buoremus vuostá maid mun dieđán.
	#$ Jáhkán livččii váddáseabbu leamaš jus ii livčče juogalágan bearašoktavuohta, lohká Budeaju nieida gii lea bajásšaddan gávpogis. 
	# It should not take the latter one.
	#% Maŋimuš jagiid áigge lea almmuhusdienas mearihis olu njiedjan
	#% Sii leat hui unnán dahkan guorahallat "sámi vieruid ja riekteáddejumiid".
	#% Ballagohten, muitala Astrid Johnskareng ja lohká iežas mealgat váfon go šattai geahpedit lášmmohallama.


#!! * **NnotAcoord** removes A instead of N (earlier: selects N instead of A), based on coordination with N, and a vfin-verb
REMOVE:NnotAcoord A IF (NEGATE 0 PrfPrc LINK *0 REAL-COPULAS + PL-V BARRIER SV-BOUNDARY)(0 N LINK NOT 0 A LINK 1 COMMA OR JA/DAHJE LINK *1 HEAD-N BARRIER NOT-NPMOD OR CC LINK 1 JA/DAHJE OR EOC OR VFIN);
#SELECT:NnotAcoord N (NEGATE 0 PrfPrc LINK *0 REAL-COPULAS + PL-V BARRIER SV-BOUNDARY)(0 A LINK 1 COMMA OR JA/DAHJE LINK *1 HEAD-N BARRIER NOT-NPMOD OR CC LINK 1 JA/DAHJE OR EOC OR VFIN);
	#$ Sápmelaččat ja dážat leat ovdal eallán ovttas.
	#$ ...ležžet sii dál sápmelaččat, dážat dahje kveanat.

#!! * **NPlbeforeRel, NSgbeforeRel** select N in front of Rel and MO
SELECT:NPlbeforeRel N + Pl IF (NEGATE 0 Inf OR Adv)(*1 Rel + Pl - ("mii" Rel Pl) BARRIER WORD LINK NOT 0 Sg) ;  
SELECT:NSgbeforeRel N + Sg OR Pron + Sg IF (NEGATE 0 Adv OR Po OR VFIN OR PrfPrc)(NEGATE -1 HEADOFPARTS OR (Dem Pl Loc))(*1 Rel + Sg BARRIER WORD LINK NOT 0 Pl) ;  # Lene: fjerna MO og la til not Sg/Pl pga av maid som kan være både Sg og Pl
SELECT:NbeforeRel N IF (NEGATE 0 Adv OR Po OR VFIN OR Inf)(NEGATE 0 PrfPrc LINK -1 ("leat") + VFIN LINK NEGATE *-1 Nom)(NEGATE -1 HEADOFPARTS OR (Dem Pl Loc))(1 Rel LINK *1 VFIN) ;
	#$ Dat leat doaimmat, maiguin mun barggan.
 	#$ Gažaldat lea dušše, man ollu ... # NOT 0 Adv!
 	#$ Lea miellagiddevaš lohkat, maid ...# NOT 0 Inf!
	#% Nuba áddjá árvala ahte eiseválddit galggaše bálkáhit baicce vuorrolohkkiid, vai rehkenasto vuorruid mearri maiddái, iige dušše dat mearri guliin mii goddo deanus.
	#% Lea registreren maid ja man ollu lea borran.

#!! ## Adjectives and adverbs

#!! ### Adv or not?
#!! maid has many readings and as Rel it is a member of S-BOUNDARY. Therefore we need to disambiguate is early in this file. Most important is to select Adv. Because of that A ang N still can have Vfin readings, it is difficult to make very general rules.

SELECT:manAdv Indef + Gen IF (0 ("man") LINK 1  N + Ill OR N + Loc) (NEGATE 1 Nom LINK 1 VFIN) ;

#!! * **vaikkomii**
IFF:vaikkomiiIndef ("mii") + Indef - Foc/ge OR ("gii") + Indef - Foc/ge OR ("goabbá") + Indef - Foc/ge OR ("makkár") + Indef - Foc/ge IF ((-1 ("vaikko") OR ("vaikke") OR ("feara")) OR (1 ("nu")))(NEGATE 0 ("man") LINK 1 A)(NEGATE 0 (Sg Nom) LINK 1 Neg + Pl1) ; 
#(NEGATE 0 (Sg Nom) LINK *1 VFIN + Pl1 BARRIER SV-BOUNDARY)
SELECT:vaikkomiiAdv_a ("vaikko" Adv)  IF (1 ("mii" Indef) OR ("gii" Indef) OR ("makkár" Indef) LINK NEGATE 0 (Sg Nom) LINK *1 VFIN + Pl1 BARRIER SV-BOUNDARY) ;
SELECT:vaikkomiiAdv_b ("vaikko" Adv)  IF (1 ("man") LINK NEGATE *1 VFIN) ;
SELECT:vaikkomiiIndefSg ("mii" Indef Acc Sg)  IF (-1 ("vaikko") OR ("vaikke")) ;
SELECT:miiPllAcc ("mii") + Indef OR ("gii") + Indef  IF (1 N + Pl + Acc) ;
SELECT:vaikkoman Gen (0 ("vaikko mii") LINK 1 A) ;

#!! * **giitu** or not
REMOVE:giituV VFIN (0 ("giitu")) ;


#!! * **gilvu** or not
SELECT:gilvu N (0 ("gilvu") LINK -1 ("viidáset")) ;


#!! * **AdvPx**
SELECT:AdvPx Adv + Acc IF (0 Px LINK -1 ADV-PX-V) ;
	#$ Son geahččá birrasis.

#!! * **comparAdv**
SELECT:comparAdv Adv IF (0 COMPAR LINK 1 Dem OR (A Attr) OR go OR PrfPrc) ;
	#$ Viđát siiddus lea eanet dien ášši birra.

#!! * **badjelisAdv**
SELECT:badjelisAdv Adv IF (0 ("badjelis") LINK -1 Nom OR Sem/Measr + Gen OR Sem/Measr + Acc) ;
	#$ Mus lea gákti badjelis.
	#$ Diagrámma badjelis čájeha ...
	#$ Mis ledje ránnját moadde kilomehtera badjelis, geat maiddái barge eanandoaluin.

#!! * **bálddasAdv**
SELECT:bálddasAdv Adv IF (0 ("bálddas") LINK -1 Dem + Loc) ;
	#$ Mus lea gákti badjelis.


#!! * **guhkáAdv**
SELECT:guhkaAdv Adv IF (0 ("guhká") LINK 1 PrfPrc) ;
SELECT:guhkaAdv Adv IF (0 ("guhká") LINK -1 ("nu") OR ("man")) ;
	#$ Guhká bargan journalistan.

#!! * **lasiAdv**
SELECT:lasiAdv Adv IF (0 ("lasi") LINK 1 N + Pl OR ("gáffe") OR Sem/Drink OR Sem/Food);
	#$ Vižže lasi stuoluid.

#!! * **loanasAdv**
IFF:loanasAdv ("loanas") IF (0 Adv LINK *-1 ("oažžut") OR ("váldit") OR ("jearrat") OR ("addit") BARRIER SV-BOUNDARY);
	#$ don oaččut loanas ...

#!! * **oaivvisAdv**
IFF:loanasAdv ("oaivvis") IF (0 Adv LINK -1 ("leat"));


#!! * **guossaiAdv**
IFF:guossaiAdv ("guossái") IF (0 Adv LINK *0 ("bovdet") OR ("boahtit") BARRIER SV-BOUNDARY)(NEGATE -1 Gen + HUMAN);


#!! * **AdvinfrontofPrfPrc**
SELECT:AdvinfrontofPrfPrc Adv IF (0 A LINK 1 PrfPrc - COPULAS LINK NOT 0 N)(NEGATE 0 Po LINK -1 Gen) ;
	#$ Jouni lea hárjánan bivdi ja lohká iežas hirbmadit illudan go vuittii váldovuoittu.


#!! * **viidáseappotAdv**
SELECT:viidáseappotAdv Adv IF (0 ("viidáseappot")) ;
	#$ Válljejin dán fáttá go vuhtten ahte dáppe Álttás ii leat nu álki fievrridit sámegiela viidáseappot mánáide.

#!! * **viidásetAdv**
SELECT:viidáseappotAdv Adv IF (0 ("viidáset") LINK -1 ("ain")) ;
SELECT:viidáseappotAdv Adv IF (0 ("viidáset") LINK 1 ("gilvu")) ;
	#$ Duopmárat rámidedje su ja ieš sávvá ahte olbmot jienastit su ain viidáset gilvvus.


#!! * **vuostálagaAdv**
SELECT:vuostálagaAdv Adv IF (0 ("vuostálaga") OR ("vuostálagaid")) ; # LINK *0 ("gilvalit") OR ("bargat") OR ("bidjat") BARRIER SV-BOUNDARY) ;
	#$ Márjjábeivviid oktavuođas lágiduvvo njoarostangilvu, ja dán jagi galget sihke mánát, nuorat, nissonolbmot ja almmáiolbmot gilvalit vuostálaga.
	#$ Birasgáhttenlihkadus ja álgoálbmogat – ovttasbargit vai vuostálagaid ?
	#$ Nugo oalle máŋgga eará gilisge, de leat Kárášjogasge leamaš sogat geat árbevirolaččat leat leamaš vuostálagaid.



#!! * **maidAdv1** selects maid Adv when there is no vfin to the right.
SELECT:maidAdvA ("maid" Adv) IF ((NEGATE *1 VFIN BARRIER S-BOUNDARY - ("de") OR CS) OR (*1 COMMA BARRIER VFIN LINK 1 CC OR Neg) OR (1 Nom LINK -2 Loc LINK *-1 COPULAS BARRIER NOT-PCLE LINK NEGATE *-1 Nom))(NEGATE *1 NP-HEAD BARRIER NOT-NPMOD LINK 1 Rel LINK *1 VFIN LINK *1 VFIN)(NEGATE 1C CC)(NEGATE -1 BOC LINK *1 EOS BARRIER VFIN) ;
 	#$ Dán oktavuođas čujuhuvvo maid oanáduslistui ja gáldolistui.
 	#$ Sápmelaččat oasálaste maid dasa, muhto lassin ledje sis vel eará resursat.
 	#$ Dá lea reive maid.
 	#$ Dattetge bohciida maid sierra gažaldat, ...
 	#$ Boahtágo son maid?
 	#$ Riggodagaide gullet maid fuođđut ja lottit.
  	#$ Dat njammá visot gavjjaid ja várra njammá maid dan gopmi, ii láve čiehkadit eahkedis Bireha seaŋgga vuollái.
  	#% Maid mii dál vihtaniiguin?

#!! * **maidAdv** selects maid Adv when there is a comma to the right.
SELECT:maidAdvA ("maid" Adv) IF (1 COMMA LINK NEGATE 1 Rel) ;


#!! * **maidAdv2** selects maid Adv copulas and PrfPrc or Actio Ess. We need this rule because of that there can be an Inf to the right which also has Vfin reading.
SELECT:maidAdv ("maid" Adv)  IF (*-1 COPULAS BARRIER NOT-ADV)(*1 PrfPrc OR (Actio Ess) OR DEHALAS BARRIER S-BOUNDARY OR ("leat"));
 	#$ Lea maid lassin gávnnahan metodalaččat váttisin mearridit...
 	#$ Min mielas lea maid dehálaš rekrutteret eambbosiid geain lea sámi giellagelbbolašvuohta dehálaš bargguide mánáidgárdefálaldagas, sámegieloahpahusas ja sámi deaivvadanbáikkiin.


#!! * **maidAdv3** selects maid Adv even if there is a vfin to the right.
SELECT:maidAdv ("maid" Adv) IF (*-1 CS - go - ("amas") OR BOS OR Rel OR ("muhto") BARRIER VFIN OR CLB)(NEGATE -1 go)(NEGATE 0 Rel LINK -1 Acc LINK -1 BOS)(NEGATE 0 Rel LINK 1 Po OR ("de"))((*1 VFIN - Pot LINK NEGATE *1 VFIN BARRIER CC OR COMMA) OR (NEGATE *1 TRANS-V BARRIER CC OR COMMA OR REAL-V) OR (*1 COPULAS BARRIER REAL-V LINK 1 PrfPrc OR (Actio Ess) LINK NEGATE *1 VFIN)) (NEGATE *1 ("?"))(NEGATE 1 VFIN LINK *-1 PrfPrc)(NEGATE -1 Nom + N OR Nom + Superl LINK *2 TRANS-V BARRIER Acc LINK NOT 1 CS) ;
SELECT:maidAdv ("maid" Adv) IF (-1 MO) ; 
 	#$... nu ahte dat maid siskkilda ávnnaslaš kultureavttuid.  
   	#$ Maid moarsi lea borran.
   	#$ ... mat maid leat politihkalaš bellodagat.
 	#$ Suomas maid lei dilli váttis.
 	#$ Sidjiide maid lei Ruija.
 	#$ Lea go stáhtas maid geatnegasvuohta doarjut dán kultuvrra.
 	#$ Leago stáhtas maid geatnegasvuohta doarjut dán kultuvrra.
	#$ Maid moarsi ja irgi leaba čiŋadan gávttiiguin.
	#$ Lea garas mu ektui, muhto maid buohkaide geat eai leat ožžon doarjaga.
	#% Na maid dal juo dagaža ja maid ii dagaš. # derfor VFIN - Pot
	#% Dan maid dalle dárbbaša fievrridit, ferte olmmoš noađuhit.
	#% Dat materiála maid giellaguovddážat ráhkadit giellaoahpahusa váste, ...
	#% Oahpahusoktavuođas geavahuvvon dan giela birra maid vállje váldogiellan.
	#% Stuorimus maid sápmelaš sáhttá oažžut

#!! * **maidAdv4** selects maid Adv between two verbs or the verb after is IV
SELECT:maidAdv ("maid" Adv) IF (NEGATE -1 TIME OR MEASURE) (1 VFIN + IV LINK NEGATE 0 AUX LINK *1 Inf OR PrfPrc OR (Actio Ess) OR ConNeg BARRIER S-BOUNDARY OR COMMA);
SELECT:maidAdv ("maid" Adv)  IF (*-1 INF-V BARRIER REAL-V OR N)(*1 Inf BARRIER REAL-V);
 	#$ Mun háliidan maid mannat.
 	#$ Sápmelaččat galge maid vearu máksit Danmárku-Norgii. 

#!! * **maidAdv5** selects maid Adv in front of Comp which at this stage can have vfin analysis.
SELECT:maidAdv ("maid" Adv)  IF (-1 COPULAS)(*1 Nom BARRIER SV-BOUNDARY);
 	#$ Geainnut ledje maid guhkit rabas.

#!! * **maidAdv6** selects maid Adv between copulas Pl3 and N Pl.
SELECT:maidAdv ("maid" Adv) IF  (-1 COPULAS + Pl3)(*1 (N Pl Nom) BARRIER NOT-NPMODADV);
 	#$ Romssa universitehtas leat maid plánat álggahit fálaldagaid.

#!! * **maidAdv7** in a special construction with geahččat
SELECT:maidAdv ("maid" Adv) IF (*1 ("geahččat") BARRIER SV-BOUNDARY LINK 0 VFIN OR ConNeg OR PrfPrc OR Inf LINK 1 Ill);	
 	#$ Maid don geahčat munnje?

#!! * **maidAdv8** selects maid Adv after a Pers
SELECT:maidAdv8 ("maid" Adv) IF  (-1 (Pron Pers Nom) - ("dat")) ;
	#$ Mun maid ferten málestit guktii vahkus.
	#$ Jus lean váiban ja don maid leat váiban, de manne nohkkat.

#!! * **maidAdv9** selects maid Adv even 
SELECT:maidAdv9 ("maid" Adv) IF  (-1 (N Sg) LINK NOT 0 Sem/Group LINK NEGATE *-1 REAL-V)(1 VFIN LINK NEGATE *1 VFIN + Sg3) ;
	#$ Lágamánneriekti maid dubmii dievddu máksit 130 000 kruvdnosaš buhtadusa gillájeaddjái.

#!! * **maidAdv10** selects maid Adv iežas 
SELECT:maidAdv10 ("maid" Adv) IF  (-1 DADJAT)(1 (Refl Acc))((2 Inf OR PrfPrc) OR (2 Adv OR Acc LINK 1 Inf OR PrfPrc)) ;
	#$ Patrika eadni, Anna Margrethe Bæhr, lohká maid iežas hirbmadit suorganan go gulai mii lea dáhpáhuvvan.

#!! * **maidAdv11** selects maid Adv iežas 
SELECT:maidAdv11 ("maid" Adv) IF  (-1 COPULAS)(1 Inf LINK 1 ("ahte")) ;
	#$ Stuora vuoitu sutnje lea maid ahte olbmot geat eai oainne iežaset sápmelažžan, leat álgán boahtit festiválii, dán sii eai leat ovdal dahkan.

#!! * **maidAdv12** selects maid Adv for Lea maid A Inf
SELECT:maidAdv12 ("maid" Adv) IF  (-2 BOS)(-1 ("leat") + Sg3)(1 A)( 2 Inf OR ("ahte")) ;
	#$ Lea maid vejolaš oastit liegga  borramušaid ja bruvssa.

#!! * **maidAdv13** selects maid Adv for 
SELECT:maidAdv13 ("maid" Adv) IF  (*1 MIELLA BARRIER NOT-A LINK 1 Inf)(-1 VFIN) ;
	#$ Lea maid vejolaš oastit liegga  borramušaid ja bruvssa.
 	#$ Oahpaheaddjeoahppu addá maid vejolašvuođa oažžut gitta 60 000 ruvnno stipeandda Sámedikkis.

#!! * **maidAdv14** selects maid Adv for 
SELECT:maidAdv14 ("maid" Adv) IF  (-1 Nom)(1 Acc LINK 1 TRANS-V) ;
	#$ Dál leage juste dat áigi go skire maid beasi ráhkadišgoahtá.

#!! * **maidAdvProp** selects maid Adv for 
SELECT:maidAdvProp ("maid" Adv) IF  (-1 Nom + Prop OR HUMAN)(1 VFIN OR Inf) ;
	#$ Go Johan Áilu maid devddii daid eará eavttuid, de han son maid lei dat buoremus virgái.
	#$ Ávžžuha eará nuoraid maid čálihit Sámedikki jienastuslohkui.


#!! * **AdvPlc** selects Adv for 
REMOVE:AdvPlc N IF  (0 PLACE-ADV)(NEGATE 0 ("olggobealli") + Gen LINK 1 Sem/Hum) ; ;
REMOVE:Po Po IF (0 PLACE-ADV LINK NOT -1 Gen) ;


#!! * **KillmaidAdv** removed the remaining maid Adv
REMOVE:KillmaidAdv ("maid" Adv) ;

SELECT ("maŋŋeleappos" Adv) ;
SELECT ("máŋgii" Adv) ;

#!! * **mielasAdv** 
SELECT:mielasN Po IF (0 ("mielas") LINK -1 Gen + HUMAN LINK NOT 0 A) ;
SELECT:mielasN N IF (0 ("mielas") LINK -1 Attr OR ("buorre")) ;
SELECT:mielasA A IF (0 ("mielas") LINK *-1 COPULAS BARRIER NOT-ADV-PCLE)(1 Inf OR CS OR Ill) ;
SELECT:mielasAdv Adv IF (0 ("mielas")) (*-1 REAL-V BARRIER Gen OR Attr OR S-BOUNDARY) ;


#!! **matPcle**
IFF:matPcle ("mat" Pcle) IF (-1 Imprt OR ("gea") OR ("dego") OR ("gal") LINK *-1 BOC OR COMMA BARRIER WORD - Pcle - Adv) ;
	#$ Dán mii gávnnaimet, dutkka mat leago dat du bártni gákti.

#!! The following two rules are omitted. They only inflect on the disambiguation of mat pcle, a wackernagel, which is done in the rule over here, I think.


# * __miiRel__ selects maid Rel when there is vfin to the right and comma
#SELECT:miiRel ("mii" Rel)(*1 VFIN BARRIER S-BOUNDARY LINK *1 COMMA BARRIER S-BOUNDARY LINK 1 VFIN)(NEGATE -1 (N Pl) LINK *2 PL3-V BARRIER NOT-ADV);
 	#$ Dan maid dalle dárbbaša fievrridit, ferte olmmoš noađuhit.
   	#$ Buot maid dii dahkabehtet, lea buorre.
 	#$ Don galggat doallat dan maid leat vurdnon Hearrái.

# * __miiRelcoord__ selects maid Rel in a coordination with Rel or MO
#SELECT:miiRel ("mii" Rel) (1 JA/DAHJE LINK 1 MO OR Rel);
 	#$ Lea registreren maid ja man ollu lea borran. 


SELECT:manAdv ("man" Adv) IF (1 (A Nom) OR UNNAN OR Adv OR PrfPrc OR OLLU OR ("muddu") LINK NOT 0 Der/sasj) (NEGATE 1 Nom LINK 1 VFIN)(NOT 1 Po)(NOT -1 N) ;
 	#$ Man stuoris mánná lea? 
	#$ Man ollu sudnos lea alddiska ruhta?
	#$ Girkonjárggas fuomášin man ollu negatiiva oainnut dan áigge ledje sámiid vuostá.


REMOVE:manAdv ("man" Adv) IF ((1 N + ADVLCASE) OR (-1 COMMA LINK -1 N)  OR (-1 N));    

REMOVE:erenoamšA A (0 ("erenoamážit")) ;

#!! * **olluNom** 
SELECT:olluNom OLLU + Nom IF ((0 Qst) OR (1 go))(*1 COPULAS)(NOT -1 ("nu") OR ("na")); 
	#$ Ollugo diibmu lea?
	#$ Ollu go diibmu lea?
	#$ Man ollu lea diibmu?
	#$ Bargiid guottut mearkkašit seamma ollu go fáktadieđut.


REMOVE:manV-N V OR N IF (-1 ("man" Adv) OR ("mihá"))(NEGATE 0 Po)(NEGATE 0 N LINK 1 VFIN);
	#$ Man guhkki lea girkui?
	#$ In dieđe man buorit dat leat.
	#$ Muhtun riggodagaid geavahit mihá unnit dál.

REMOVE:seammaV V IF (-1 ("seammá"))(*1 go BARRIER NOT-N-A LINK NOT 1 VFIN); 
SELECT:Vihkinassii REAL-V IF (-1 ("ihkinassii"));
 	#$ Mun áiggun čuovvut du ihkinassii manažat.

SELECT:vissásit Adv (0 ("vissásit") LINK NEGATE 1 N) ;
 	#$ Muhto ii goassege dieđe vissásit

SELECT:bealli Acc (0 ("bealli") LINK 1 Loc)(*-1 TRANS-V) ;	
	#$ Válden oktavuođa givssideddjiin vai son gulašii mu beali áššis.


SELECT:AdvPrfPrc Indef IF (0 ("olus") LINK -1 COPULAS + Pl3)(1 PrfPrc);
	#$ Guovdageainnus leat olus čadnon gillii ja kultuvrii.


SELECT:CopAdvPrfPrc Adv IF (-1 COPULAS)(1 PrfPrc - ("leat"));
SELECT:ovttasAdv Adv IF (-1 REAL-V OR Com)(0 COM-ADV)(NEGATE 0 Foc/ge OR Num); 
 	#$ Divššohas lea duođalaččat roasmmohuvvan.
 	#$ Bátnedoavttir galgá ovttas eará bargiiguin mearridit.

#!! * **olluAdv**
SELECT:olluAdv1 OLLU + Adv IF ((-1 AUX LINK 2 Inf OR PrfPrc) OR (1 Adv LINK NOT 0 N) OR (1 go LINK 1 (A Nom) OR VFIN) OR (-1 REAL-V LINK NEGATE 2 A OR N OR EOC)) ;
	#$ Dolin olbmot lávejedje ollu dulkot nieguid.
	#% Dát dieđusge mearkkaša ollu dan olbmui gii lea vuoitán.
	
SELECT:olluAdv2_a OLLU + Adv IF (NEGATE *-1 REAL-V BARRIER S-BOUNDARY)(1 Pron LINK *1 VFIN);
SELECT:olluAdv2_b OLLU + Adv IF (NEGATE *-1 REAL-V BARRIER S-BOUNDARY)(1 VFIN LINK NOT 0 N) ;
	#$ Nissonat duddjojedje dieđinge biktasiid, gávnniid ja biergasiid maid ieža atne, muhto ollu sii vuvde márkaniin, sihke Bossegohpis ja Ivgubađas.
	#$ Ollu eará maŋŋá dollojuvvon boazosápmelaččaid riikkačoahkkimiin leat skuvladilálašvuođat leamaš áššin.

SELECT:olluAdv3 OLLU + Adv IF (-1 GRADE-ADV LINK -1 REAL-V - AUX) (1 N + Loc OR Pron) ;
	#$ Ja go geahččá maŋás historjjás, de lea Bargiidbellodat bargan hui ollu sámepolitihkain, dadjá Ørnebakk.

SELECT:olluAdv4 OLLU + Adv IF (-1 GRADE-ADV)(1 REAL-V - AUX)  ;
	#$ Maŋimuš jagiid áigge lea almmuhusdienas mearihis olu njiedjan.

SELECT:ovdamearkkadihte ("ovdamearkka dihte") ;



IFF:ruovttusAdv ("ruoktu" Sg Loc)((*1 Ill BARRIER NOT-NPMOD) OR (-1 Gen LINK NEGATE -1 TRANS-V)) ;
	## Adv

REMOVE:sajisAdv ("sajis") IF (0 ("sadji") LINK *-1 Num BARRIER NOT-A-ADV);
 	#$ Dat lovttohalai leabuheamet julggiid ja vikkai jorggáhallat ovtta sajis.

LIST ADV-NOT-PL = "digitálalaš" "vejolaš" ;

#!! * **valjitAdv**
SELECT:valjitAdv ADV-NOT-PL + Adv (-1 S-BOUNDARY OR go)(1 VFIN) ; #more to come
#!! * **vejolaččatAdv**
SELECT:vejolaččatAdv ("vejolaččat" Adv)(NOT 1 ("go")) ; #more to come
	#$ hirbmat buorre go digitálalaččat besse searvat doaluide

#!! * **aččatAttr**
REMOVE:aččatAttr Attr + Comp (0 (".*aččat"r) + Adv LINK NEGATE *1 ("go")) ; #more to come


SELECT:vuoiA (A Nom) IF (-1 ("vuoi"));
 	#$ Vuoi surgat dan gándda! 

SELECT:eanasAdv Adv IF (-1 COPULAS)(0 ("eanas") OR ("eanaš"))(1 N)(NEGATE 1 TIME)(*2 EOC BARRIER N); 
 	#$ Dál lea eanaš ovttamielalašvuohta, ahte...
 	#$ Ovdal ledje eanas vuonaid bivdán, muhto go skoavdefatnasat bohte... 
 	#$ Ealus leat eanaš álddut.

	
SELECT:eanasAttr (A Attr) IF  (-1 V OR Acc)(0 ("eanas" A) OR ("eanaš" A))(1 N LINK NOT 0 Ill);
	#$ Ná sáhttá geavvat eanas geasi.

SELECT:ovdditAttr Attr IF  (0 ("ovddit" A) LINK *1 N BARRIER NOT-NPMOD LINK 1 VFIN);
	#$ Ovddit Sámediggeráđis ledje guokte ráđđeaddi.


#SELECT:Aeambbo Attr IF (0 $$A-COMP LINK -1 ("ahte") OR ("ja") LINK -1 $$A-COMP) ;
#SELECT:Aeambbo Attr IF (0 $$A-COMP LINK 1 ("ahte") OR ("ja") LINK 1 $$A-COMP) ;
#

    
SELECT:CompahteComp Comp (0 $$ANYLEMMA + Comp LINK -1 ("ahte") OR ("ja") LINK -1 $$ANYLEMMA + Comp) ;     
SELECT:CompahteComp Comp (0 $$ANYLEMMA + Comp LINK 1 ("ahte") OR ("ja") LINK 1 $$ANYLEMMA + Comp) ;     
	#$ Dat šattai heajut ahte heajut.
    
REMOVE:A A IF (*-1 IV BARRIER SV-BOUNDARY OR CC LINK NOT 0 COPULAS OR ("áibbas") OR Indef)(0 Adv)(1 EOC);
REMOVE:Amannit A IF (*-1 IV BARRIER SV-BOUNDARY LINK NOT 0 COPULAS)(0 ("maŋŋit" Adv))(1 Ill OR DOHKO)(2 EOC);    
	#$ Boađán maŋŋeleappos.
	#$ Eatnandoallu bođii maŋŋit Sápmái.



SELECT:gradeAdv GRADE-ADV IF (0 REAL-V LINK 1 A OR Adv) ;
SELECT:gradeAdvA GRADE-ADV + Adv IF (0 A OR Indef LINK 1 (A Nom) OR (A Ess) OR (A Attr)) ;
SELECT:gradAdvAdv MOD-ADV-ADV IF (1 Adv OR Indef LINK NOT 0 Po OR N OR ("maiddái") OR ("maid")) ;
	#$ Dii uhccán oskkolaččat.
	#$ Man uhccán osku dus lea!

SELECT:galmmasAdv ("galmmas" Adv)(*-1 ("mannat") BARRIER NOT-ADV-PCLE) ;


SELECT:guhkkinAdv ("guhkkin" Adv)(1 ("eret") OR ("meahcci" Loc)) ;

SELECT:hoahpusAdv ("hoahpus" Adv)((-1 REAL-V) OR (1 REAL-V)) ;

#!! * **jogoAdv** jogo and juoga as adverbs
SELECT:jogoAdv Adv (0 ("jogo") OR ("juogo"))(NEGATE *-1 VFIN BARRIER S-BOUNDARY) ;
	#$ Jogo gikse dat guolit?	
SELECT:jogoAdv Adv (0 ("jogo") OR ("juogo"))(*1 ("dahje") BARRIER S-BOUNDARY) ;
	#$ Rásit liđđot juogo geassit, čakčat dahje dálvit.

#!! * **AdvPx** selects Adv Px instead of N Px
SELECT:AdvPx Adv + Px (0 N + Px) ;
	#$ Atnet sálttiid duohkineattet, ja dollet ráfi gaskaneattet.

#!! * **AdvwhenAPl** selects A Pl instead of Adv
REMOVE:AdvwhenAPl Adv (0 (A Pl Nom) OR (N Pl Nom)) ((1 VFIN + Pl3 LINK NOT 0 Inf) OR (-1 ("leat" Pl3)) OR (-1 ("leat" Inf) LINK -1 AUX + Pl3))(NEGATE *-1 NP-HEAD-PL-NOM OR Sem/Group BARRIER S-BOUNDARY) ;
	#$ Riggámusat vulget lupmui juohke jagi.
	#$ Álgoálbmogat ellet hui iešguđetlágán eavttuiguin ja sáhttet leat riggát dehe geafit.
	#% Ii fal runiidan jalges gittiide, muhto meahccái, gosa návddit dávjá ja lunddolaččat fievrredit ráppiid.

SELECT:APl Pl (0 (A Pl Nom) LINK -1 Refl + PxPl3 LINK -1 DADJAT) ;



SELECT:LIAboahtitV V IF (*-1 Sem/Veh + Com OR ("linnjá") + Com OR Pl + Nom BARRIER VFIN OR PrfPrc OR SV-BOUNDARY OR CS) (0 ("boahtit")) ;
	#$ ii hal dađi eambbo gal maid dainna biillain bohte die dat lei buot gal . (LIA)
	#$ dat bohte dat bohte dat bohte dalle Ruŧŧii dat bohte soađi áigge .

SELECT:LIAboahtitV V IF (0 ("boahtit") LINK *1 ("boahtit") BARRIER SV-BOUNDARY) ;
	#$ dáppe bohte ovdamearkka dihte skábman dat bohte dáppe diekko Leavvajoga Deanu ala ja em .

SELECT:LIAboahtitV V IF (0 ("boahtit") + Pl3 LINK 1 Neg + Pl3) ;
	#$ ja dohko bohte sii dušše bivdit muhto gáddebargit maid ? (LIA)

	
#!! ## Disambiguating abbreviations

#!!	Abbreviations are also disambiguated via normal case disambiguation rules,  
#!!	but some contexts are special to abbreviations, and therefore dealt with
#!!	separately here. Keep in mind: perhaps the placement of these rules is to early.

#!! * **AttrABBRNum**
SELECT:AttrABBRNum Attr IF (*-1 BOS LINK NEGATE *1 V)(0 (N ABBR))(1 Num);
	#$ Sd.prp.nr. 63:s.
	
SELECT:AttrNr Attr IF (0 NUMMAR OR CURRENCY-ABBR LINK 1 Num);
	#$ Áirras nr. 10 Ole Henrik Magga oaččui permišuvnna.
SELECT:NrNom Nom (0 ("nr")) ; 
    	 
#!! ## Disambiguating particles

#!! * **sonPcle** selects son Pcle, the remaining Pcle are removed
SELECT:sonPcle Pcle IF (0 ("son"))(1 NP-HEAD-NOM)((NOT -1 PUNCT LINK -1 BOS) OR (-1 N LINK *-1 BOS BARRIER NOT-NPMOD)) ;
	#$ Mo son Duommá lea dahkan?
	#$ Dan viesu son mun dovddan.

REMOVE:KillSonPcle Pcle IF (0 ("son"));
 	#$ Son mu nama ipmirdii boastut.

SELECT:misPcle Pcle IF (0 ("mis"))(-1 Imprt LINK 0 ("gullat") OR ("árvidit") OR ("guovlalit") OR ("jurddašit")) ;
	#$ Gula mis dál, logai boanda.
	#$ Árvit mis dáid.
SELECT:misPcle Pcle IF (0 ("mis"))(-1 ("dat") LINK 2 ("leat") + Foc/Pos-ge) ;
SELECT:misPcle Pcle IF (0 ("mis"))(-1 ("dal") OR ("amma"))(NEGATE 1 ("leat")) ;
	#$ Dat mis leige.
	#$ Čájet dal mis.

REMOVE:KillmiPcle Pcle IF (0 ("mis"));

#!! ## Disambiguating rom attr
REMOVE:RomAttr Attr IF (0 Rom LINK NOT 1 COUNTABLE);
 	#$ Prošeavtta II. oasi jođiha ođđasisnammaduvvon sámi vuoigatvuođalávdegoddi.



#!! ## Disambiguating clitics

REMOVE:Dego Qst IF (0 ("dego") OR ("dugo"));
	## Su ámadadju báittii dego beaivváš. 

REMOVE:Nugo Qst IF (NEGATE *-1 REAL-V BARRIER WORD)(0 Adv)(NEGATE *1 ("?")); 
	## Nugo dieđát, de lean dál.

REMOVE:leagis Foc/gis IF (0 ("leahki")) (1 EOC OR CCCOMMA);
	## Mánát bohte Kárášjoga leagis, Deanu leagis ja Anárjoga leagis.
# Philosophy: "Dat leagis duohta" (if possible?): -gis modifies something to the right.
# If correct, the rule might be generalised.

#!! ## Disambiguating numerals

REMOVE (Num Rom) IF (0 (N ACR))(NEGATE -1 BOS) ;

REMOVE Coll IF (0 Num LINK -1 ("diibmu") OR KLASS OR ("sullii") OR ("birrasii") OR Sem/Time + Adv) ;

#!! ## Disambiguating adpositions

#!! ### čađa
#!! * **caddaN** if čađa and movement-v
REMOVE:caddaN ("čađđa") IF (0 ("čađa") LINK 0* MOVEMENT-V BARRIER SV-BOUNDARY) ;
REMOVE:caddaN ("čađđa") IF (0 ("čađat")) ;
	#$ Gal sii besset čađa.
	

#!! Commented out som adp-rules we don't need anymore:

# čađa Po and Pr if TIME-N Gen
#SELECT:cadaPoTime ("čađa" Po) (-1 TIME + Gen)(NOT 1 TIME + Gen);
	#$ Ii sáhte dieđusge dušše suohtastallat go rievtti mielde leat dát meassut áiggiid čađa leamaš girkostallanmeassut.
#SELECT:cadaPo1 Po (0 ("čađa") LINK -1 Gen)(1 MANNU + Gen LINK 1 Ord);
#SELECT:cadaPrTime ("čađa" Pr) ((1 ("gaska" Gen)) OR (1 TIME-N + Gen LINK NOT -3 TIME-N + Gen));
	#$ Nu dáhpáhuvai čađa gaskka.


#!! ### geahčai
#!! * **geahcaiPP** not geahččat V
REMOVE:geahcaiPP ("geahčai")  IF ((0 ("geahččat") LINK NOT -1 Gen) OR (0 ("geahččat") LINK *1 Ill BARRIER NOT-NPMOD));
 	#$ Áhkku geahčai iežas speadjalis.
 	#$ Tommi geahčai boksii.


#!! ### guovddaš
#!! * **guovddasPP** or not 
REMOVE:guovddasPo ("guovddáš" Po) IF (NEGATE -1 GENERAL-PLACE OR VAHKKU-DUR) ;
REMOVE:guovddasPr ("guovddáš" Pr) IF (NEGATE 1 GENERAL-PLACE OR VAHKKU-DUR) ;
REMOVE:guovddasA ("guovddáš" A Sg) OR ("guovddáš" A Ess) IF (NEGATE -1 COPULAS) ; #Needs constraints
	#% Dat váccaša guovddáš márkana.

#!! ### mađe
#!! * **madePo** after Num Gen
SELECT:madePo MADE + Po IF (-1 (Num Gen)) ;

#!! * **NumMade** Num before mađe
SELECT:NumMade Num IF (1 MADE);

#!! ### miehta
#!! * "miehtá" is also VFIN, and miehtá needs special treatment 
#!!     - **miehtaPo** after place or time Gen
SELECT:miehtaPo ("miehtá" Po)  IF (-1 PLACE + Gen OR VAHKKU-DUR + Gen OR Sem/Plc + Gen)(NEGATE 1 Sem/Plc OR ("riika")) ;
	#$ Áldu mii dálvvi miehtá njamaha miesi gohčaduvvo čearpmateadni.
	#$ Beaivvi miehtá galget earát lohkat galle geardde viehká mearriduvvon guhkkodaga, ja galgá sáhttit árvidit galle geardde háhppeha viehkat 12 diimmus.

#!!     - **miehtaPr** before place or time Gen
SELECT:miehtaPr ("miehtá" Pr)  IF (1 PLACE + Gen OR VAHKKU-DUR + Gen OR Sem/Plc + Gen) ;
 	#$ Dalle bisttii luossaguolli miehtá dálvvi.
 	#$ Sii dieđihedje dan miehtá guovllu, ja sii bukte su lusa buot buhcciid.
 	#$ Searvegottiin lei dál ráfi miehtá Judea ja Galilea ja Samaria.
 	#$ Áldu mii dálvvi miehtá njamaha miesi gohčoduvvo čearpmateadni.
 	#$ Dat sáhttet leat ovttasbargošiehtadusat oanahis áigái, dahje oktasašdoallu muhtin áiggiid jagis, dahje vaikko miehtá jagi.
 	#$ Beaivvi miehtá galget earát lohkat galle geardde viehká mearriduvvon guhkkodaga, ja galgá sáhttit árvidit galle geardde háhppeha viehkat 12 diimmus.
	## Olbmuin bisttii luossaguolli miehtá dálvvi. 

#!! * **oidnosisAdv**
SELECT:oidnosisAdv ("oidnosis" Adv) ; # IF (*0 ("leat") BARRIER SV-BOUNDARY) ;	

#!! * "ovddas" has many readings and needs special treatment 
#!!     - **ovddasPo**  - commented out because we don't need it
#SELECT:ovddasPo ("ovddas" Po) (-1 Gen LINK *-1 NP-HEAD-NOM BARRIER S-BOUNDARY)(1 V); #we don't need it anymore
  	#$ Láilá Susanne Vars lea ge dál Sámedikki ovddas bargamin njuolggadusaiguin Finnmárkoláhkii.

#!! * special rules for rastá because it often is Adv, and it can be an object connected to the PP
#!!     - **rastaAdv**  čuohppat/časkit/sahet rastá
SELECT:rastaAdv ("rastá" Adv) IF (*0 ("čuohppat") OR ("časkit") OR ("čuohpastit") OR ("sahet") BARRIER SV-BOUNDARY) ;	

#!!     - **rastaPo, rastaPr**  fievrridit olbmo man nu rastá
REMOVE:rastaPo ("rastá" Po) IF (-1 HUMAN + Acc OR (Pers Acc) LINK *0 ("doalvut") OR ("fievrridit") OR ("guoddit") OR ("láidet") BARRIER SV-BOUNDARY) ;	
REMOVE:rastaPr ("rastá" Pr) IF (1 HUMAN + Acc OR (Pers Acc) LINK *0 ("doalvut") OR ("fievrridit") OR ("guoddit") OR ("láidet") BARRIER SV-BOUNDARY) ;	

#!!     - **rastaPr**  rastá ráji/rájá
SELECT:rastaPr Pr IF (0 ("rastá") OR ("badjel"))(1 ("rádji" Gen) OR ("rádjá" Gen)) ;


#!!     - **sisaAdv**   sisa
SELECT:sisaAdv Adv IF (0 ("sisa"))(-1 Acc LINK *-1 ("viežžat") OR ("guoddit") OR ("oažžut") BARRIER REAL-V) ;
	#$  vižžen vel muoraid sisa

#!!     - **unnimusatAdv**   
SELECT:unnimusatAdv Adv IF (0 ("unnimusat"))((1 Num) OR (1 ("árvosátni") LINK 1 Num)) ;
	#$  unnimusat 35 čuoggá


#!! * **birraPo, birraPr** special rules for birra because it often is Adv, and it can be an object connected to the PP
SELECT:birraPo ("birra" Po) IF ((-1 ("máilbmi" Gen)) OR (-1 NP-HEAD-GEN LINK *-1 VERBAL-ACTIVITY-V OR ("diehtit") OR MUITALIT BARRIER NOT-ADV-PCLE)) ;
	#$ Dattetge ii leat muitaluvvon geavahanriiddu birra dálolaččaid ja boazodoalu gaskkas.

#!!	* **muitalitPo** muitalit X birra
SELECT:muitalitPo ("birra" Po) (-1 Gen LINK *-1 MUITALIT + REAL-V OR ("gullat") + REAL-V BARRIER SV-BOUNDARY OR Pr) ;
	#$ Son muitala dás iežas skuvllafearaniid birra Buolbmát skuvllas. 
	#$ Dattetge ii leat muitaluvvon makkárge namahan veara geavahanriiddu birra dálolaččaid ja boazodoalu gaskkas. 


REMOVE:searvvis Po IF (0 ("searvvis") LINK -1 NATION + Gen)  ;
	#$ E-boasta lea hui álkes ja jođánis vuohki sáddet reivviid birra máilmmi.


SELECT:birraPr ("birra" Pr) IF (1 ("máilbmi" Gen))  ;
	#$ E-boasta lea hui álkes ja jođánis vuohki sáddet reivviid birra máilmmi.

#!! * "vuostá" has many readings and needs special treatment 
#!!     - **vuostaAdv**  váldit vuostá/vuostái
SELECT:vuostaAdv Adv IF (*-1 ("váldit") OR ("jienastit") BARRIER SV-BOUNDARY)(0 ("vuostá") OR ("vuostái"));
 	#$ Sámi servodagas váilu árbevierru váldit vuostá almmolaš diehtojuohkima dakkár hámis.
#!!     - **vuostaPr**  váldit vuostá/vuostái	
REMOVE:vuostaPr Pr IF (0 ("vuostá") OR ("vuostái"))(*-1 REAL-V BARRIER S-BOUNDARY LINK NOT 0 REAL-COPULAS) ; 


#!! * vuollel ja badjel as Adv in front of Num
SELECT:vuollelbadjelAdv Adv IF (0 ("vuollel") OR ("badjel"))(1 Num LINK *1 Loc OR Gen BARRIER NOT-NPMOD-ACC OR COMMA) ;
	#$ Vuollel 12 jahkásaš mánáin ii leat iešheanalis geahččanvuoigatvuohta.
	#$ Vuosttasnaššuvnnaid čoagganeapmi ovddasta badjel 300 indiánaservodaga miehtá Kanada ja gáibida dál iešstivrejumi Davvi-Amerihká buot eamiálbmogiidda.
	#% Lohku ii ožžon heađisge njiedjat vuollel viđa, dasgo dalle gielddas dahje Skuvlaráđđehusas lei vuoigatvuohta loahpahit diekkár gánnetmeahttun fálaldaga. 

#!!	### Adpositions that take modifiers

SELECT:GenPrGen Pr IF (-1 Gen)(0 ("bealde") OR ("beale") OR ("beallai") OR ("geahčai") OR ("geahčen") OR ("geaže"))(1 Gen)(NEGATE 1 TIME);
 	#$ Daid sáhttá heaŋggastit dan heargái mii daid eará goahtemuoraid lea guoddimin, guokte bealji goappáge beallái hearggi. 
	
SELECT:gaskalPo GASKAL + Po IF (-1 NP-HEAD-GEN LINK -1 CC LINK -1 NP-HEAD-GEN) ;
 	#$ Dat lei min ja din gaskkas.

SELECT:gaskalPrNum GASKAL + Pr IF (1 Num LINK 1 CC LINK 1 Num);
 	#$ Gaskal 12 ja 16 jahkásaš mánáin lea muhtun muddui iešheanalis.

SELECT:gaskalPoNum GASKAL + Po IF (-1 Num LINK -1 CC LINK -1 Num) ;
 	#$ Alimusriekti veadjá leat jagiid 1854 ja 1897 gaskka doallan siskkáldas čoahkkima.

SELECT:gaskalPr GASKAL + Pr IF (*1 NP-HEAD-GEN BARRIER SV-BOUNDARY LINK 1 CC LINK *1 NP-HEAD-GEN BARRIER SV-BOUNDARY);
 	#$ Ferte navdit ahte dáža duopmostuolut ja eará riektegeavaheaddjit dábálaččat dulkojit dáža siskkáldas rievtti nu ahte garvá vejolaš vuostálasvuođa gaskal dákkár álbmotriektenorpma ja siskkáldas norgga njuolggadusa.
 	#$ Lága mearrádusat galget váikkuhit luohttevašvuođa gaskal divššohasa ja dearvvašvuođabálvalusa.

SELECT:gaskalPr GASKAL + Pr IF (*1 (N Gen) BARRIER NOT-NPMOD LINK *1 CC BARRIER NOT-NP LINK *1 (N Gen) BARRIER NOT-NPMOD LINK 0 HEAD-N);
 	#$ Dán láhkai lea leamaš muhtun muddui lotnolas doahttaluvvon dássádatdilálašvuohta gaskal stáhta almenneteaiggádin ja geavahanvuoigaduvvon giliássiid.

SELECT:gaskalPrNum GASKAL + Pr IF (*1 NUM BARRIER NOT-NPMOD);
 	#$ Ferte oaidnit oktavuođa gaskal dan guovtti soahpamuša.

SELECT:gaskalPrA GASKAL + Pr IF (1 A LINK 1 CC LINK 1 A LINK *1 (N Gen) + HEAD-N); 
 	#$ Galgá válljejuvvot gaskal vejolaš ja dohkálaš iskkadan- ja dikšunvugiid.

SELECT:gaskalPo GASKAL + Po IF (-1 (N Gen) LINK *-1 CC BARRIER NOT-NPMOD LINK -1 (N Gen)) ;
 	#$ Riiddut boazodolliid ja eará vuoigatvuođaoamasteddjiid gaskkas livčče lassánan. # Does not function because of barrier

#!! ### LIST LG-MATERIAL = Inf Adv Nom ;
LIST LG-MATERIAL = Inf Adv Nom ; # Eará?

#!! * **gaskasPosticky, gaskasPrsticky** selects Po after coordinating language materials
SELECT:gaskasPosticky GASKAL + Po IF (-1 $$LG-MATERIAL LINK -1 JA/DAHJE LINK -1 $$LG-MATERIAL) ;
SELECT:gaskasPrsticky GASKAL + Pr IF (1 $$LG-MATERIAL LINK 1 JA/DAHJE LINK 1 $$LG-MATERIAL) ;
 	#$ Sániid oamastit ja eaiggáduššat gaskkas geavahuvvo "ja".

#!! * **PoParantes** selects Po after paranteces
SELECT:PoParantes Po IF (-1 RIGHT LINK *-2 LEFT LINK -1 Gen);
	## ...sáhttá dulkot telestudio dahje govvatelefovnna (ruoná nummira) bokte. 

#!!	### Requiring Genitive



SELECT:punct Po IF (-1 RIGHT)(0 ("dihte") LINK 1 VFIN) ;
SELECT:punct Po IF (-1 Inf)(0 ("dihte") LINK 1 VFIN) ;
	#$ «Cihppa, cinná ja anašit» dihte bálkestuvvui sátnegirji.

SELECT:mieldeadv Adv (0 ("mielde") LINK -1 ("beassat") OR ("boahtit")) ;


SELECT:PoCompl Po IF (*-1 NP-HEAD-GEN OR (A Gen) OR (Num Gen) OR (Actio Gen) OR (Refl Gen) BARRIER WORD LINK NOT 0 Rel + Acc)(NEGATE 0 N OR A OR ("mielde"))(NEGATE 0 Pr LINK *1 Gen BARRIER NOT-NPMOD) ;
REMOVE:PoCompl Po IF (NEGATE *-1 NP-HEAD-GEN OR (A Gen) OR (Num Gen) OR (Actio Gen) OR (Refl Gen) OR (Pers Gen) BARRIER WORD) ;


#!! * **PoNomCompl** removes Po if no possible complement to the left
#REMOVE:PoNoCompl Po IF (NEGATE *-1 NP-HEAD-GEN OR (A Gen) OR (Num Gen) OR (Actio Gen) OR (Refl Gen) BARRIER WORD)(NEGATE 0 ("gaskan"))(NEGATE -1 CC LINK -1 Po LINK -1 Gen) ;
	#$ Jus divššohas lea badjel 16 jagi boaris,... 
	#$ Dat lea dušše badjelaš állanbeali guhku.
	#$ Jesus manai fatnasii ja vulggii rastá jávrri iežas gávpogii.
	#$ ... muhto ovdal dan áiggi ledje "sápmelaččat boahtán dán guvlui". 
	#$ Ovdal dan áiggi ledje sápmelaččat boahtán dán guvlui.
	#$ Vuollel 12 jahkásaš mánáin ii leat iešheanalis geahččanvuoigatvuohta.
	#$ Dát dáhpáhuvvá dávjjit go dat, maid ovdal namuhin.
	#$ Seamma go ovdalis namahuvvon riektehistorjjálaš ovdanbuktimis leat dán barggus maid muhtun buhtes historjjálaš dilit, ja almmuhuvvo danne joavkku čielggadeami mielddusin (2. mielddus).
	#% Dorkka geavahedje gávtti vuolde.
	#% Beaskka geavahedje gávtti badjel.
	#% Vuittii bártniid badjel.



#!! * **PoMeasure** removes Po when MEASURE to the left
REMOVE:PoMeasure Po IF (-1 MEASURE LINK -1 Num)(NOT 0 ALU)(1 Gen);
	#$ Ádjá viessu lea guokte miilla bajábeal márkana. # Contraints?

#!! * **PrGen1** selects Pr
SELECT:PrGen Pr - Foc IF (NEGATE -1 NP-HEAD-GEN OR (A Gen) OR (Num Gen))(*1 NP-HEAD-GEN OR (A Gen) OR Num + Gen BARRIER NOT-NPMOD)(NEGATE 1 (Loc Attr) LINK 1 Loc)(NEGATE *-1 STRICT-TRANS-V BARRIER SV-BOUNDARY OR Acc) ;
	#$ Maŋŋegiđa ja árrageasi guohtu boazu lulábeali rámaid, čohkaid ja jekkiid.
	#$ Jus divššohas lea badjel 16 jagi boaris,... 
	#$ Jesus manai fatnasii ja vulggii rastá jávrri iežas gávpogii.
	#$ Ovdal dan áiggi ledje sápmelaččat boahtán dán guvlui.
	#$ Dat váccaša guovddáš márkana.
	#$ Vuosttasnaššuvnnaid čoagganeapmi ovddasta badjel 300 indiánaservodagaid miehtá Kanada ja gáibida dál iešstivrejumi Davvi-Amerihká buot eamiálbmogiidda.
	#$ 1978 láhka balansere gaskal sámi vuoigatvuođaid boarásmuvvan oainnu,...  
	#$ Maŋŋil láivves vuosttaš vuoru lei boađus 0-0.
	#% Son čuoččastii ja geahčadii vehá dobbelaččas Sipirjá-Liissá českes goruda.

#!! * **PrGen2** selects Pr
SELECT:PrGen Pr - Foc IF (NEGATE -1 NP-HEAD-GEN OR (A Gen) OR Num + Gen)(**1 Attr BARRIER NOT-NPMOD LINK *1 NP-HEAD-GEN OR (A Gen) BARRIER NOT-NPMOD);
	#$ Loahpageahčen ovddit jahkečuođi bohte ollu láddelaččat Sápmái, ja sis lei buorre máhttu eatnandoalu birra.
	#$ Maŋŋá jápmimis de aliduvvui Gonagas Olav bassin.
	#$ Gánda: "Birra buot oppa máilmmi."

SELECT:Pr Pr IF (1 Gen LINK 1 EOC)(0 Adv OR Pr LINK -1 Acc LINK *-1 ("ohcat") BARRIER NOT-NPMOD ) ; #TRANS-V
	#$ Ohcet dievddu maŋŋel viessobuollima.

#!! * **PrNoCompl** removes Pr if no complement to the right	
REMOVE:PrNoCompl Pr IF (NEGATE *1 NP-HEAD-GEN OR (A Gen) OR Num + Gen BARRIER NOT-NPMOD) ;
	#$ Na mun gal hupmen dan birra.
	#$ Dán vuostá sáhttá dadjat, ahte ...
	#$ Dát dáhpáhuvvá dávjjit go dat, maid ovdal namuhin.



#!! * **PoGen** selects Po
SELECT:PoGen Po - Foc - N-PO IF (-1 NP-HEAD-GEN - Qst LINK NEGATE 0 Acc LINK *-1 STRICT-TRANS-V BARRIER NOT-ADV-PCLE)(NEGATE -1 VAHKKU-DUR + Acc LINK -1 REAL-V) 
(NEGATE 0 Pr LINK *1 NP-HEAD-GEN - (Rel) OR (A Gen) OR Num + Gen BARRIER NOT-NPMOD LINK NEGATE *1 Po BARRIER SV-BOUNDARY) 
(NEGATE 1 STRICT-TRANS-V LINK NEGATE *1 Acc BARRIER NOT-NPMODADV)(NEGATE -1 Dem LINK 2 ADVLCASE)
(NEGATE 0 ("mielde") LINK -1 Nom LINK 0 Err/Orth + Gen OR Err/Orth-spes + Gen)
(NEGATE 0 ("mielde") LINK *-1 MIELDE-V BARRIER NOT-ADV-PCLE)
(NEGATE 0 ("mielde") LINK -1 REAL-V)
(NEGATE 0 ("mielde") LINK 1 (Actio Ess))
(NEGATE 0 ("sisa") LINK *-1 ("guoddit") OR ("viežžat") BARRIER SV-BOUNDARY)
;
	#$ Vuoi dan olbmo gean bokte fillehusat bohtet.
	#$ Nieida bođii ráhkkásis mielde.
	#$ Tommi ja Annika dárkilit geahččaba Pippi oaidnin dihte mo diŋggaohccit barget.
	#$ Mun gal attán dutnje bealji vuollái. 
	#$ Mii bidjat boaldinmuoraid bearpmehiid gaskii.
	#$ Váldde biergasiid iežat fárrui!
	#$ Su vuosttaš girji bođii jagi 1986, ja girji lea muitalus nuorra sámenieidda birra gii šaddá bajás internáhtas.
	#$ Lávvomuoraid nala bidjat álo loavdaga.
	#$ Dattetge ii leat muitaluvvon makkárge namahan veara geavahanriiddu birra dálolaččaid ja boazodoalu gaskkas.
	#% Ii galgga leat vejolaš addit nuppi áššebeallái viidát vuoigatvuođaid dahje geatnegasvuođaid go alddis leat.
	#% Áhku vuostá lea buoremus vuostá maid mun dieđán.
	#% NBR čuovvu mielde.

SELECT:PoGenLahka Po - Foc - N-PO IF (-1 Num LINK -1 KLASS LINK -1 TEXT + Gen OR LAHKA + Gen) ;
	#$ Dearvvašvuođaásahusas mearriduvvo divvun dahje sihkkun dearvvašvuođabargiidlága § 44 mielde.

####SELECT:PoGenSTV Po - Foc - N-PO IF (-1 NP-HEAD-GEN - Qst LINK *-1 STRICT-TRANS-V BARRIER NOT-ADV-PCLE LINK -1 Acc LINK NOT 0 Nom LINK NEGATE *-1 TRANS-V BARRIER Acc)(NEGATE -1 VAHKKU-DUR + Acc LINK -1 REAL-V)(NEGATE *1 NP-HEAD-GEN OR (A Gen) OR Num + Gen BARRIER NOT-NPMOD) (NEGATE 1 STRICT-TRANS-V LINK NEGATE *1 Acc BARRIER NOT-NPMODADV);
SELECT:PoGenSTV Po - Foc - N-PO IF (-1 NP-HEAD-GEN - Qst LINK *-1 STRICT-TRANS-V BARRIER NOT-ADV-PCLE LINK -1 Acc LINK NOT 0 Nom LINK NEGATE *-1 TRANS-V BARRIER Acc)(NEGATE -1 VAHKKU-DUR + Acc LINK -1 REAL-V) (NEGATE 1 STRICT-TRANS-V LINK NEGATE *1 Acc BARRIER NOT-NPMODADV);
	#$ Beaskka geavahedje gávtti badjel.
	#$ Dorkka geavahedje gávtti vuolde.
	#% Sámediggi lea bivdán ráđđehusa váldit Sámedikki mielde árvoháhkanprográmmabargui.

#!! ### Diambiguation  Noun vs. Po or Pr:

#!! * **vuollaiPo** selects
SELECT:vuollaiPo Po IF (*-1 Gen BARRIER WORD)(1 CC)(NEGATE -1 Acc LINK *-1 CC BARRIER NOT-NP LINK -1 Acc LINK *-1 TRANS-V BARRIER NOT-NPMODADV)(NEGATE 0 ("mielde") OR ("fárrui"));
	#$ Jos sáddo deavdá buođu vuollái dahje jos sáddo hávddarda buođu vuolde... 

#!! * **beallaiPo** selects
SELECT:beallaiPo ("beallai" Po) IF (-1 Gen LINK NEGATE 0 Loc LINK *2 NP-HEAD-GEN BARRIER NOT-NPMOD)(NEGATE -1 REAL-V OR Adv)(NEGATE 0 TIME-PP)(1 TIME); 
	#$ Sámi guovlluin ožžo Sámeparlameantta Suoma beallái jagi 1976.   

#!! * **PrTime** 
SELECT:PrTime Pr IF (0 Po LINK 1 Sem/Time + Gen - ("jahki")) ; #needs probably more constraints
	#$ Go Sámedikki dievasčoahkkin ovddabealde juovllaid bijai eret NSRa presideantta ja NSRa ráđi, de dat dáhpáhuvai ortnega mielde.


#!! * **ovdalPr** selects
SELECT:ovdalPr Pr IF (0 TIME-PP)(1 TIME OR ("soahti") LINK NEGATE 1 Num)(NEGATE 2 Po)(NEGATE 0 ("čađa") LINK 1 MANNU LINK 1 Ord LINK -3 Gen);
	#$ Bođeža go Ánne ovdal gaskaija?

#!! * **gaskanPo** selects
SELECT:gaskanPo ("gaskan" Adv) IF  (-1 ("guovttá")) ;
	## Mun áiggošin duinna guovttá gaskan hupmat.
	
#!! * **gaskkasPo** selects
SELECT:gaskkasPo Po IF (-1 Gen)(0 ("gaskkas") OR ("vuostá"))(NEGATE 0 ("vuostá") LINK *-2 BOC BARRIER NOT-NPMOD);
	#$ Son lea buoremusaid gaskkas.


#!! * **lassinPo** removes
REMOVE:lassinPo ("lassin" Po) IF (NEGATE -1 Gen) ;
		
#!! * **ovddasPo1** selects
SELECT:ovddasPo1 ("ovddas" Po)  IF (-1 Gen)(*1 ("máksit") OR ("máksu") BARRIER SV-BOUNDARY);
	#$ Spider-Man-gova ovddas máksojuvvo bures!
	#$ Daid ovddas gal in máksán maidege.

#!! * **ovddasPo2** selects
SELECT:ovddasPo2 ("ovddas" Po) IF (-1 Gen LINK *-1 ("máksit") OR ("máksu") BARRIER NOT-NPMOD) ;
	#$ Logut čájehit makkár kilohatti gaskamearálaččat Finnmárkkus ferte máksit ealggabierggu ovddas.

#!! * **ovddasPo3** 	selects
SELECT:ovddasPo3 ("ovddas" Po) IF  (*-2 Acc BARRIER NOT-NPMOD LINK *-1 ("máksit") OR ("máksu") BARRIER NOT-NPMOD)(-1 Gen) ;
	#$ ...maid ovddas sihtá máksit bálkámassima 30 beaivvi ovddas.

#!! * **ovddasPocoord** selects
SELECT:ovddasPocoord ("ovddas" Po) IF  (-1 Gen LINK *-1 (V Neg) BARRIER V OR Pr OR Po LINK *-1 ("ovddas" Po) BARRIER NOT-NPMOD);
	#$ Sámediggi lea miehtan sutnje máksit 800 ruvnno juohke árgabeaivvi ovddas, muhto ii fal lávvordagaid, sotnabeivviid ja bassebeivviid ovddas.
 
#!! * **NwhenPo** removes N if Po
REMOVE:NwhenPo N IF (-1 Gen LINK NOT 0 Num)(0 Po)(NEGATE 0 N-PO)(NEGATE 1C Po);
	#$ Ábo logai eatnis maŋis eahketrohkosa.

#!! * **VwhenPo** removes V if Po
REMOVE:VwhenPo V IF (-1 Gen)(0 Po)(NEGATE 1C Po);
	#$ ... maid ektui sáhtii leat buori oskkus.
	#$ Gávpot mii lea vári alde, ii sáhte leat čihkosis.
	
SELECT:GengoGenPo Gen (0 Acc LINK 1 go LINK **1 Gen BARRIER NOT-NPMOD-ACC LINK 1 Po LINK 1 EOS) ; 
	#$ Dieđálin eanet Oslo go iežan gili birra.
	
SECTION 
# 9  


#!! ## Some particular subjunctions and Neg Sup

#!! * **amasCS** selects CS, not A or Neg Sup
SELECT:amasCS CS IF (0 ("amas"))(*1 Neg BARRIER WORD - Nom) ;
	#$ Prinsihpa maid lean čuovvon lea juohkehačča ektui fuolahan, amas ii vuovdi vahágahtto.

#!! * **amasA** selects A, not CS or Neg Sup
SELECT:amasA A IF (NEGATE -1 Neg LINK NEGATE *2 N BARRIER NOT-NPMOD)(0 ("amas") LINK NEGATE *1 Inf OR ConNeg BARRIER S-BOUNDARY-NOT-CVP) (NEGATE *1 Neg BARRIER REAL-V OR S-BOUNDARY);
	#$ Máŋgasat leat sakka fuolastuvvan amas ja dovddus vahágiin.
	#$ Ollu sápmelaččaide lea dál ain amas jurdda.
	#$ Eai dovdda amas olbmo jiena.
	#$ Amas olbmo jiena ii dovdda.
	#$ Sáhpánvuovddi bajil lea máŋga beaivvi girddašan amas, stuora loddi.

#!! * **amasNegSup** selects Neg Sup, not CS or A
SELECT:amasNegSup (Neg Sup) IF (0 ("amas") LINK -1 COMMA)(1 Nom LINK 1 Inf) ;
	#$ Doamit juo čoaggit duhkorasaid, amas noavki njamistit daid, bivdá eadni.

#!! * **amasNegSup** selects Neg Sup, not CS or A
SELECT:amasNegSup (Neg Sup) IF (0 ("amas") LINK NEGATE *-1 REAL-NEG BARRIER ConNeg)(*1 Inf BARRIER S-BOUNDARY-NOT-CVP)(NEGATE 0 A LINK 1 CC)(NEGATE 0 Attr LINK 1 N LINK NOT 0 Inf LINK *1 VFIN BARRIER REAL-V)(NEGATE 1 Nom + Pl LINK 1 VFIN + Pl3) ;
	#$ Bávlos lei nappo mearridan gárvit Efesosa, amas ájihuvvot Asia eanangottis. 
 	#$ ...amas lojes hearggit livvut. 
 	#$ Oahpaheaddji šattai geavahit buot čehppodagas, amas massit vátna oahppiidis.
 	#% Ii amas olmmoš soaitte álo leat buoret .
 	#% Ávvir čálii ivttáš aviissas 7-jahkásačča birra gean amas dievdu geahččalii fillet ruđaiguin iežas biilii .
 	#% – Biilavuddjiide geat eai leat oahppásat guovllus orru mohkki mii lea ovdalaš šalddi amas ja váttis vuodjit.

#!! * **amasNegSup** selects Neg Sup, not CS or A
SELECT:amasNegSup (Neg Sup) IF (0 ("amas") LINK NEGATE *-1 REAL-NEG BARRIER ConNeg)((1 ConNeg) OR (1 WORD - REAL-NEG LINK 1 ConNeg)) ;
 	#$ Dasgo stáhtaossodagat mearrašuvvet oahppiidlogu mielde, de fertešii logahagas leat dás ovddosge 35-40 oahppovázzi, amas bajásdoallan šatta rásehit gieldda ekonomiija.


#!! * **amatNegSup** selects Neg Sup, not CS
SELECT:amatNegSup (Neg Sup) IF (0 ("amat") LINK *1 Inf BARRIER NOT-ADV); 
	#$ Sii guddet du gieđaideaset alde, amat norddastit juolggát geađgái.

#!! * **dasgoCS** selects CS, not Qst
SELECT:dasgoCS CS IF (0 ("dasgo"))((NEGATE *1 ("?")) OR (1 Interr)) ;
	#$ Dasgo Herodes lea ohcamin máná goddin dihtii dan.
	#$ Dasgo goabbá lea stuorit,...

#!! * Select and remove **vaikkoAdv** , 
SELECT:vaikkoAdv Adv IF (0 ("vaikko")) ((1 MO-MANge OR Interr OR ("goabbá")) OR (0 Foc/ba) OR (-1 AUX + VFIN LINK *3 Inf BARRIER NOT-ADV-PCLE)) ;
SELECT:vaikkoAdv Adv IF (0 ("vaikko") LINK NEGATE *1 VFIN) ;
SELECT:vaikkoAdv Adv IF (0 ("vaikko") LINK 1 ("ieš") LINK 1 N OR Inf) ;
SELECT:vaikkoAdv Adv IF (0 ("vaikko") LINK 1 Inf)(-1 AUX) ;
	#$ Áigot vaikko nealgudit jus eai oaččo buohcciviesu Áltái.

REMOVE:vaikkoAdv Adv IF (0 ("vaikko") LINK NOT 0 Foc/ba) ;
	#$ Ásahusa journálaovddasvástideaddji sáhttá liikká rievdadit journála vaikko gustojit vuolláičállojuvvon journálamerkesteamit.	
	#$ Lea ráfálaš ja seammás sáhttá vaikko mii dáhpáhuvvat.
	#% Ja moai boazodoallu áirasat áigguime vaikko guovttá joatkit barggu.

#!! ## go as CS and Qst Pcle
#!! First select all "go" Qst Pcle, then remove them so the rest will be "go" CS

#!! * **standQst** selects Pcle in standard questions with question mark. Also without question mark if the verb is in 2. person.
SELECT:standQst Pcle IF (-1 VFIN LINK -1 BOS OR CLB OR PUNCT)(0 go LINK *1 ("?")); 
SELECT:standQst Pcle IF (-1 VFIN + 2-V LINK NOT 0 ("buorre") LINK -1 BOC OR CLB OR PUNCT)(0 go); 
SELECT:standQst Pcle IF (-1 VFIN + 2-V LINK -1 JEARRAT)(0 go); 
	#$ Muhto boađát go?                         
	#$ Eai go báhkinat ge daga aiddo nu?
	#$ Judas, beahtát go Olbmobártni cummáin?
	#$ Addet go máŋga dearvvašvuođabargiidjoavkku veahki ja fállojuvvo go divššohasaide birrajándororrun?
	#$ Ja hálida go Uhcastáloš duođaid stálostallat nieiddain?
	#$ Isket leat go duhtavaš kollektiiva­fálaldagain.

#!! * **standQst** selects Pcle in standard questions without question mark
SELECT:standQst Pcle IF (-1 VFIN + Sg3 LINK -1 BOS OR CLB OR PUNCT OR CC OR ConNeg)(0 go LINK 1 (Sg Nom) LINK NEGATE *1 V + Sg3 ); 
	#$ Ja hálida go Uhcastáloš duođaid stálostallat nieiddain.
	#$ Son dadjá ahte lea dušše geavvan oktii ja ahte sii manne dohko, go eai dieđe lea go duohta vai ii.
	#% Ovttasráđiid oapmahaččaiguin almmuhit politiijat mohtorsihkkelvuoddji nama, gii duššai go mohtorsihkkel ja guorbmebiila beaškkehedje oktii mánnodaga Porsáŋggus.


SELECT:iiQst Pcle IF (-1 Neg LINK -1 CS) ;
	#$ Son de jearrá guovžžas , ahte ii go gille rastá eanu doalvut?

#!! * **objQst** selects Pcle in questions which function as object in the clause
SELECT:objQst Pcle IF (**-2 VFIN BARRIER S-BOUNDARY-NOT-CVP LINK NOT 0 Inf)(-1 VFIN - COPULAS LINK NOT 0 Inf OR Comp LINK NOT -1 Neg + Ind)(0 go)(NEGATE *1 VFIN BARRIER S-BOUNDARY-NOT-CVP LINK NOT 0 Inf OR PrfPrc)(NEGATE 1 (A Nom) LINK NOT -2 COPULAS); 
	#$ Dalle ferten iskat lea go dus feber.
	#$ De oaidná olmmoš dárbbaša go oalgebáddi luoitiluvvot. 
	#$ Dalle mun ferten iskat lea go dus feber, ja mihtidit dus varradeattu.
	#$ Farisealaččat atne Jesusa čalmmis oaidnit áigu go buoridit sábbáhin.
	#$ Politihkas lei 1866 rájes sáhka ahte galggai go stivrregoahtit gii oažžu eatnama láigohit stáhtas.
	#$ Lea eahpečielggas lea go mánáidgárddiid lohku lassánan vai unnon.
	#% Mearridanváldi berre leat nu stuoris go vejolaš
	#% Oidnen man váttis dilli lei mánáide geat eai máhttán go sámegiela.
	#% – Dat mii lea stuorámus hástalus lea go leat válljen dan guhttasa, de lea gávdnat dan geainnu sin váras, mii sidjiide heive buoremusat.

SELECT:objQst Pcle IF (**-2 REAL-V BARRIER S-BOUNDARY-NOT-CVP LINK 0 JEARRAT OR MUITALIT OR ("oaidnit"))(-1 VFIN LINK NOT 0 Inf OR Comp LINK NOT -1 Neg + Ind)(0 go); #(1 NEG-ADV-PRON) ;
	#$ Mii háliidit diehtit dáhpáhuvvá go mihkkege, ja jos nu dagaš, makkár áigemeriid siste dán áššis.
	#$ Son ii áiggo gal dadjat ledje go Álttás dohkálaš ákkat hilgut doarjagiid.


#!! * **objQst2** selects Pcle in standard questions where an object follows VFIN
SELECT:objQst2 Pcle (-1 VFIN LINK -1 BOS OR CLB OR PUNCT)(0 go LINK 1 Acc OR Gen) ;
	#$ Oinnežan go su ihttin, dan in dieđe.
 

#!! * **subQst** selects Pcle in questions as subordinated clause
SELECT:subQst Pcle IF (*-2 VFIN-NOT-IMPRT LINK NEGATE 0 Inf LINK *-1 AUX BARRIER REAL-V)(-1 VFIN LINK NOT 0 Ill OR COPULAS LINK -1 COMMA OR CC)(0 go LINK NEGATE *1 VFIN) ;
	#$ Mun jurddašin iežainan, leažžá go duohta?
	#$ Eat dieđe, bođeža go Elle ihttin.
	#$ Addet go máŋga dearvvašvuođabargiidjoavkku veahki ja fállojuvvo go divššohasaide birrajándororrun.

#!! * **vaiQst** selects Pcle in questions with vai
SELECT:vaiQst Pcle IF (-2 ("vai"))(0 go);
	#$ Boahtá go eadni, vai ii go boađe?                         
	#$ ...vai geahččaluvvo go gávdnat čovdosiid bissovaš rievttis,... 
	#$ Dárbbahat go dálkasa vai hálidat go doaktáriin sárdnut?

#!! * **auxQst** selects Pcle in questions as subordinated clause, starting with AUX
SELECT:auxQst Pcle IF (-1 (V Neg) OR MOD-ASP OR COPULAS)((*-2 Ger OR ConNeg OR VFIN BARRIER WORD LINK NEGATE *-1 Rel BARRIER COMMA) OR (*-2 (Refl Nom) LINK *-1 ConNeg OR VFIN BARRIER WORD))(0 go LINK *1 PrfPrc OR Inf OR ConNeg BARRIER AUX) (NEGATE *-2 Neg BARRIER NOT-V ) ;  
	#$ Árvvoštaladettiin galgá go addit lobi lea dattege gažaldat das,... 
	#$ In mun gal dieđe sáhtežit go vuolgit dál.      
	#$ Son jearrá it go boađe?
	#$ Sámedikki eará orgánat mearridit ieža galget go čoahkkimat leat rabas vai gitta.
	#$ Dan ferte oaidnit makkár diliin guovllut leat, leat go áitojuvvon dakkár stuorát sisabáhkkemiin.
	#% Eará molssaeaktuhan mis ii leat leamašge go ceavzit.

#!! * **refQst** selects Pcle in two main clauses, the first one a question which is referred to in the second.
SELECT:refQst Pcle IF (-1 VFIN LINK -1 BOS OR REAL-CLB OR PUNCT OR COMMA)(0 go)(*1 COMMA BARRIER VFIN LINK 1 ("dat" Pers));	
	#$ Oinnežan go su ihttin, dan in dieđe.
	
#!! * **nounQst** selects Pcle for go after NP
SELECT:nounQst Pcle IF (-1 N LINK *-1 BOS BARRIER NOT-NP OR Interr OR MO)(NEGATE -1 ("hávvi"))(0 go)(*1 VFIN BARRIER S-BOUNDARY-NOT-CVP)(*1 ("?"));
	#$ Eatni goarrun gákti go dus lea die?
	#% Mii ávkkiid go lásat eai mana gitta?

#!! * **poQst** selects Pcle for go after Po
SELECT:poQst Pcle IF (-2 (N Gen) LINK *-1 BOS BARRIER NOT-NP)(-1 Po)(0 go)(*1 VFIN BARRIER S-BOUNDARY-NOT-CVP);
	#$ Girdi mielde go don manat Helssegii? 
	
#!! * **negQst** selects Pcle for go after Neg
SELECT:negQst Pcle IF (-1 Neg);
	#$ It go don gula die leat oappážat golbma geat jogŋet leat vuolgán.
	

#!! * **AdvQst** selects Pcle for go after WORD
SELECT:wordQst Pcle IF (-1 Adv OR A LINK *-1 BOS BARRIER NOT-PCLE)(0 go)(*1 VFIN BARRIER S-BOUNDARY-NOT-CVP)(*1 ("?"));
	#$ Na geasset go bođii diehtit ahte ii beasa skuvlii?

#!! * **killPcle** removes all remaining Pcle for go 
REMOVE:killPcle Pcle IF (0 go) ;

REMOVE:ConNegIfAttr ConNeg IF (-1 (A Attr))(0 PrfPrc LINK 1 N) ;

REMOVE:ConNegIfAdv ConNeg IF (0 Adv LINK 1 ConNeg LINK NOT 0 Adv) ;
	#$ Gállá diđii, ahte son ii okto birge Riibmagállásiin.

REMOVE:LIAConNegIfNeg ConNeg IF (*-1 REAL-NEG BARRIER ConNeg OR PrfPrc OR SV-BOUNDARY OR CS) (0 N + Gen LINK 1 Po LINK 1 ConNeg) ;
	#$ duottarjávrriin mun gal sáhtán vatnasin leahkit muhto in in meara nalde liiko riekta . (LIA)


SELECT:ConNegIfNeg ConNeg IF (*-1 REAL-NEG BARRIER ConNeg OR PrfPrc OR SV-BOUNDARY OR CS) (NEGATE 0 N OR PrfPrc OR Pron OR A LINK *1 ConNeg BARRIER NOT-ADV-PCLE LINK NOT 0 VFIN OR A)(NEGATE 0 Loc LINK -1 NEGFOC LINK -1 Loc)(NEGATE 0 Inf LINK *-1 REAL-NEG LINK -1 ConNeg LINK -1 REAL-NEG)(NEGATE 0 Comp LINK1 go)(NEGATE 0 Attr LINK 1 N LINK *1 ConNeg BARRIER REAL-V)(NOT 0 ("galle")) ; #(NEGATE *1 ("leat") + FMAINV );
	#$ Jearranskovis ii jerrojuvvon leatgo mánáin vieljat dahje oappát geat servet seammá dutkamuššii.
	#% Máhttájeaddji ii leat stuorit go oahpaheaddjis, ii ge bálvaleaddji stuorit go hearrás.
	#% Ii amas olmmoš soaitte álo leat buoret .
	#% Ahte ii ovttage amas dahje joavdelas olbmui addo lohpi čuollat makkárge muorraávdnasiid almennetvuovddis ii alcces iige earáide , go buot murrema galget guovllu fásta ássit dahkat.

#SELECT:ConNegIfNeg ConNeg IF (*-1 REAL-NEG BARRIER ConNeg) ; # OR PrfPrc OR SV-BOUNDARY)(NEGATE 0 N OR PrfPrc LINK *1 ConNeg BARRIER NOT-ADV-PCLE)(NEGATE 0 Loc LINK -1 NEGFOC LINK -1 Loc)(NEGATE 0 Inf LINK *-1 REAL-NEG LINK -1 ConNeg LINK -1 REAL-NEG) ;
 	
 	#$ Iige njuovvan ja bierggu rádjan leat mihkkege dearvvašvuođaváttisvuođaid.Lea erenoamáš dehálaš movttiidahttit doaktáriid bargat gielddain.

REMOVE:ConNeg ConNeg IF (-1 ConNeg) ;


#!! # Section 9 WORD-SPECIFIC RULES

	# ============== #
SECTION  
	# ============== #


#!! ## Some particular subjunctions

#!! ## Adverb rules

#!! # MAPPING OF COMP-CS< , COMPLEMENTS OF PARTICLES IN COMPARISON
#!! First map all COMP-CS<, then remove the other readings


#!! * **compInf** Inf go Inf
MAP:compInf (@COMP-CS<) TARGET Inf ((*-1 ("go" CS) BARRIER NOT-ADV LINK -1 Inf) OR (*-1 ("go" CS) BARRIER NOT-ADV LINK *-1 Comp OR ("baicca") BARRIER SV-BOUNDARY LINK -1 Inf))(*-1 ("go" CS) LINK *-1 Comp BARRIER SV-BOUNDARY)(NEGATE *1 VFIN BARRIER NOT-ADV-PCLE)(NEGATE 0 AUX + VFIN LINK *1 PrfPrc OR (Actio Ess)) ;
	#$ Anán buorebun odne vuolgit go ihttážii vuordit. 
	#$ Čállit lea buoret go lohkat.
	#% Álget fas sáddet go vuojahit.
	
MAP:compInf2 (@COMP-CS<) TARGET Inf (*-1 N BARRIER NOT-NPMOD LINK -1 ("go" CS) LINK -1 Inf LINK *-1 Comp OR ("baicca") BARRIER SV-BOUNDARY)(NEGATE *1 VFIN BARRIER NOT-ADV-PCLE)(NEGATE 0 AUX + VFIN LINK *1 PrfPrc OR (Actio Ess)) ;
	#$ Son lea neavttártaleanta gii niegadivččii baicca Beaivváš Sámi Našunálateáhteris neaktit go Hollywood-nástin šaddat.
 	
#!! * **ComptimeAdvl** buoret go ovdal
MAP:ComptimeAdvl (@COMP-CS<) TARGET TIME-ADV (-1 ("go" CS) LINK -1 A OR COMPAR)(NEGATE *1 REAL-V BARRIER S-BOUNDARY) ;
	#$ Dál go ruhtadilli lea buoret go ovdal, de leat gávttis eanet čiŋat.

#!! * **ComptimeAdvl** ii nu ollu go dál
MAP:ComptimeAdvl (@COMP-CS<) TARGET TIME-ADV (*-1 ("go" CS) BARRIER NOT-ADV LINK *-1 A OR COMPAR BARRIER REAL-V LINK *-1 Neg OR ConNeg BARRIER NOT-ADV-PCLE)(NEGATE *1 REAL-V) ;
	#$ Eai lean boraspiret, eai ealggat, eaige nu ollu bohccot dain duovdagiin go dál.

#!! * **Compadvlcase** eará sivas go fuorrávuođas
MAP:Compadvlcase (@COMP-CS<) TARGET $$ADVLCASE (*-1 ("go" CS) BARRIER NOT-NPMOD LINK *-1 $$ADVLCASE BARRIER ADVLCASE)(1 COMMA OR EOC)(NEGATE 0 Gen LINK -1 Num) ;
	#$ Juohkehaš guhte earrána áhkástis mange eará sivas go fuorrávuođas, dagaha su rihkkut náittosdili.

#!! * **CompNumP** uhcit go njealji stivrralahtu doarjagiin
MAP:CompadvlcaseNum (@COMP-CS<) TARGET ADVLCASE + N (*-1 Num BARRIER NP-BOUNDARY LINK -1 ("go" CS) LINK -1 Comp) ;
	#$ Mearrádusaid gittaopmodaga luobaheamis, mat leat dahkkojuvvon uhcit go njealji stivrralahtu doarjagiin, ferte Sámediggi ja Finnmárkku fylkkadiggi dohkkehit.

MAP:CompAccNum (@COMP-CS<) TARGET Acc + N (*-1 Num + Acc BARRIER NP-BOUNDARY LINK -1 ("go" CS) LINK -1 Comp) ;

#!! * **CompNumP** numerals
MAP:CompNomNum (@COMP-CS<) TARGET Num + Nom (-1 ("go" CS) LINK -1 ConNeg)(1 Gen) ;
	#$ Máilmmis leat máŋga duhát giela, ja dat lohku sorjá das maid mii oaivvildit gielain, ja stáhtat eai leat go guokte čuođi. 

MAP:Compadvl (@COMP-CS<) TARGET ADVLCASE + N (*-1 ("go" CS) BARRIER NOT-NPMOD LINK -1 Comp)(1 EOC) ;
	#$ Mávssut leat adnon eambbo go bušeahtas.
MAP:Compadvl (@COMP-CS<) TARGET ADVLCASE + N (*-1 ("go" CS) BARRIER NOT-NPMODADV LINK *-1 ("earálágan") OR ("seammalágan") OR ("seammá")) (1 EOC) ;

MAP:Comp>P (@COMP-CS<) TARGET Gen (*-1 ("go" CS) BARRIER NOT-NPMOD LINK -1 Gen LINK *-1 Comp BARRIER NOT-NPMOD)(1 Po LINK 1 EOS OR COMMA) ;
	#$ Dieđálin eanet Oslo go iežan gili birra.

MAP:Compadvl (@COMP-CS<) TARGET ADVLCASE + N (*-1 ("go" CS) BARRIER NOT-NPMODADV LINK *-1 ("earálágan") OR ("seammalágan") OR ("seammá") LINK -1 BOC)(1 NOT-NP) ;
	#$ Seamma go ovdalis namahuvvon riektehistorjjálaš ovdanbuktimis leat dán barggus maid muhtun buhtes historjjálaš dilit.
	
#!! * **CompEanet** dohko eanet go 
MAP:Compadvleanetgo (@COMP-CS<) TARGET $$ADVLCASE - Rel - Interr (*-1 ("go" CS) BARRIER CS LINK -1 ("eanet") LINK *-1 COMMA LINK *-1 $$ADVLCASE) ;
	#$ Internáhta váhnenčoahkkimiidda bohte ollu váhnemat, ollu eanet go lágiduvvon biirečoahkkimiidda maidda bohte váhnemat geat orro skuvlla lahkosis.
	#% Muhto sii eai leat nu gallis, eai eanet go ahte sin nagoda lohkat ovttain gieđain.


#!! * **Compvejolas** go vejolaš
MAP:Compvejolas (@COMP-CS<) TARGET ("vejolaš") - Attr OR  ("vejolaččat") (-1 ("go" CS) LINK -1 A OR Adv OR ("ollugat") OR ("máŋggas") OR PrsPrc LINK -1 ("nu")) ;
	#$ Nu guhkás go vejolaš galgá dat čuovvoluvvot.
	#$ Sin mearridanorgánain berre mearridanváldi leat nu viiddis go vejolaš ja nu čadni go vejolaš.
	#$ Oahppit geahččaleigga sámástit nu ollu go vejolaš.
	#$ Dat galgá čuovvoluvvot nu guhkás go vejolaš.
	#$ dat álo galgá nu bures go vejolaččat doaimmahuvvot
 
#!! * **compNomHead** NP-HEAD-NOM (ADVL) go NP-HEAD-NOM (ADVL). VFIN-NOT-IMPRT pga manglende disamgiguering
MAP:compNomHead (@COMP-CS<) TARGET NP-HEAD-NOM - Comp ((*-1 ("go" CS) BARRIER NOT-NP OR Nom OR REAL-V LINK NOT -1 VFIN-NOT-IMPRT OR CC OR BOC OR ("nu")) OR (-1 Attr LINK -1 ("go" CS) LINK NOT -1 VFIN-NOT-IMPRT OR CC OR BOC OR ("nu")))(*-1 ("go" CS) LINK *-1 Comp OR ("seammá") OR ("ovdal") OR ("earálágan") OR ("seammalágan"))(0 Nom OR ADVLCASE LINK NEGATE 0 Gen LINK 1 N)(NEGATE 0 Attr OR PrfPrc OR TITLE LINK 1 N OR A)(NEGATE *1 VFIN BARRIER SV-BOUNDARY OR CC OR COMMA)(NEGATE 0 Gen LINK -1 Num); 
 	#$ Ovdabarggut állanit seamma beallái go "láhkateavstta lunddolaš lohkan". 
 	#$ Ovdabarggut állanit seamma beallái go "láhkateavstta lohkan". 
 	#$ Sápmelaččat guđet orrot Norgga bealde Deanu, geavahit seammá gávtti go sápmelaččat Suoma bealde Deanu.
	#$ Son lea viššaleabbo go mun.
	#$ Dalle dat viežžá čieža eará vuoiŋŋa vel, bahábuid go son ieš.
	#$ Máhttájeaddji ii leat stuorit go oahpaheaddjis.
	#% Dutket eambbo go 120 veagalváldinášši.

#!! * **CompNomHead** Comp NP-HEAD-NOM leat go NP-HEAD-NOM  
MAP:CompNomHead (@COMP-CS<) TARGET NP-HEAD-NOM (*-1 ("go" CS) BARRIER NOT-NPMOD LINK *-1 NP-HEAD-NOM BARRIER S-BOUNDARY-NOT-CVP OR CS LINK -1 Comp)(NEGATE *-1 go BARRIER NOT-NPMOD LINK -1 VFIN LINK *-1 VFIN BARRIER S-BOUNDARY-NOT-CVP OR CS)(0 Nom OR ADVLCASE)(1 EOS OR CLB) (NEGATE 1 CLB LINK *1 VFIN BARRIER CC) (NEGATE 1 CLB LINK *1 NP-HEAD-NOM BARRIER NOT-NPMOD) (NEGATE 2 Interr LINK *1 VFIN LINK *1 VFIN BARRIER S-BOUNDARY-NOT-CVP OR CS); 
	#$ ...muhto galjibut dat lea go guv-čalmmit. 
	#$ Dán njuolggadusa deaŧalaš eaktu lea ahte ođđa siidaosiid ásaheapmi ii galgga dagahit ahte boazolohku šaddá alit go siidii mearriduvvon lohku, geahča dás maŋŋelis. ## Ii máhte vuos dán 'mearriduvvon' dihte


#!! * **compMisc** go geassebuođut, go dán áigge
MAP:compMisc (@COMP-CS<) TARGET NP-HEAD-NOM - Attr - Comp OR TIME + Gen  (*-1 ("go") BARRIER NOT-NP OR Nom LINK NOT -1 VFIN OR BOS OR ("nu"))((1 S-BOUNDARY) OR (1 CLB OR CC LINK NEGATE *1 VFIN - Imprt BARRIER CC))(NEGATE 0 VFIN); 
	#$ Muhto buođđogeažit ja giđđabuođut ráhkaduvvojit muđui seammá vugiid mielde go geassebuođut, ja danne heive buođđun birra obbalohkái juo muitaluvvot.
	#$ Šibihat ledje ovdal unnibut go dán áigge.
	#$ Viššalat ohppet eanet go láikkit.
	#% – Turisten nu guhká go sáhtán.

MAP:compmade (@COMP-CS<) TARGET ("mađe") OR (A Nom) (*-1 ("go" CS) BARRIER NOT-NPMOD LINK -1 Comp OR ("seammá"))(1 go); 
	#$ Finnmárkkus ássi olbmuid divat ii galgga leat alit go guovtti dan mađe go gielddas ássi olbmuid divat.


MAP:Compgo (@COMP-CS<) TARGET NP-HEAD-NOM - Px OR (Num Nom) ((*-1 ("go" CS) BARRIER NOT-NPMOD-ACC LINK NOT -1 ("nu")) OR (-1 Gen LINK -1 ("go" CS)) OR (-1 TITLE + Nom LINK -1 ("go" CS))) (*-1 ("go" CS) LINK *-1 Comp LINK *-1 VFIN - TRANS-V BARRIER S-BOUNDARY OR TRANS-V)(NEGATE *1 VFIN BARRIER S-BOUNDARY)(NEGATE 0 Attr OR Gen LINK 1 N)(NEGATE 0 Attr OR Gen LINK 1 Attr LINK 1 N)(NEGATE 0 TITLE LINK 1 Prop + Nom) ; 


#!! * **Compdego** dego @COMP-CS<
MAP:Compdego (@COMP-CS<) TARGET NP-HEAD-NOM - Px OR (Num Nom) OR (ABBR Nom) ((*-1 XGO BARRIER NOT-NP OR CC LINK NEGATE *-1 (N Pl Nom) BARRIER WORD) OR (*-1 XGO BARRIER NOT-NP OR CC LINK -1 Inf LINK -1 COPULAS + VFIN))(NEGATE *1 VFIN-NOT-IMPRT OR AUX BARRIER S-BOUNDARY OR CC OR CS)(NEGATE 0 Attr OR (N Gen) LINK 1 N)(NEGATE 0 Attr OR Gen LINK 1 Attr LINK 1 N)(NEGATE 0 OKTA LINK 1 (N Nom))(NEGATE 0 (Refl Nom) LINK -1 NP-HEAD-NOM)(NEGATE *-1 XGO LINK -1 COMMA) ; 
	#$ Sii leat dego eŋgelat almmis.
	#$ Son oahppaladdá min allagasas dego beaivváža badjáneapmi.
	#$ Dat lea dego olmmái gii manai olgoriikii.
	#$ Dego njamaheaddji eadni dikšumin mánáidis.
	#$ Dat guhte ii biso mu siste, bálkestuvvo olggos dego oaksi ja son goldná.
	#$ Ráhkkásiiddán, allet vajáldahte ahte Hearrái lea okta beaivi dego duhát jagi ja duhát jagi dego okta beaivi.

#MAP:CompdegoAdvl (@COMP-CS<) TARGET $$ADVLCASE - Px (*-1 XGO BARRIER NOT-NP LINK *-1 $$ADVLCASE BARRIER WORD)(NEGATE *1 VFIN-NOT-IMPRT OR AUX BARRIER S-BOUNDARY OR CC OR CS)(NEGATE 0 Attr OR (N Gen) LINK 1 N)(NEGATE 0 Attr OR Gen LINK 1 Attr LINK 1 N)(NEGATE 0 OKTA LINK 1 (N Nom)); 
	#$ Dolin sámit leat geavahan raddeleahpi eanaš guovlluin, nugo dán áigge julevsámi ja oarjesámi guovlluin.	

#!! * **compAccdego** Acc dego Acc
MAP:compAccdego (@COMP-CS<) TARGET NP-HEAD-ACC - Px (-1 XGO - ("nugo") LINK -1 NP-HEAD-ACC OR (Pron Acc))(NOT 1 Po)(NEGATE *1 VFIN-NOT-IMPRT OR AUX BARRIER S-BOUNDARY OR CS OR CC OR COMMA)(NEGATE 0 Attr OR Gen LINK 1 N LINK NOT 1 Po)(NEGATE 0 Attr OR Gen LINK 1 Attr LINK 1 N); 
	#$ Mun vuolggahan din dego sávzzaid gumppiid gaskii.

#!! * **compAccgo** Acc go Acc
MAP:compAccdego (@COMP-CS<) TARGET NP-HEAD-ACC (-1 go LINK -1 Comp LINK -1 NP-HEAD-ACC)(1 EOS OR COMMA); 
MAP:compAccdego (@COMP-CS<) TARGET NP-HEAD-ACC (-1 go LINK -1 NP-HEAD-ACC LINK -1 Comp)(1 EOS OR COMMA); 
	#$ Dat guhte ráhkista bártnis eanebut go mu, ii dohkke munnje.

	
#!! * **compNum** TRANS-V eambbo go Num  
MAP:compNum (@COMP-CS<) TARGET Num (-1 ("go" CS) LINK -1 Comp)(NEGATE 1 N + ADVLCASE)(NEGATE 1 Gen LINK 1 ("mađe")) (NEGATE *1 VFIN BARRIER S-BOUNDARY OR CS OR CC);
	#$ Ii heargi gal nagot guoddit eambbo go guokte diekkára. 
	#$ Ii oktage sáhte leat lahttun eambbo go logi jagi oktilassii.
	
MAP:compEss (@COMP-CS<) TARGET (N Ess) (-1 ("go" CS) LINK -1 Nom LINK -1 Comp LINK *-1 (N Ess) BARRIER S-BOUNDARY) ;
	#$ Noađđeheargin dárbbašuvvojit sakka lojit hearggit go geresgeassin.
 	
#!! * **compCoord** coordination	
MAP:compCoord (@COMP-CS<) TARGET NP-HEAD-NOM (*-1 CRD BARRIER NOT-NPMOD-ACC LINK -1 (@COMP-CS<))(NEGATE 0 Attr LINK 1 NP-HEAD-NOM)(NEGATE 1 VFIN) ; #(1 CRD) (*2 Nom BARRIER NOT-NPMOD);
	#$ Seamma gusto uhcit fuđđožiidda nu go neahti, čeavrris, mievri ja rieban.
	#$ Dat ledje beakkán ja dovddus olbmot nu go Diaz, Maria, Anne, Erik ja vel muhtin eará dovddus olbmot. # ii máhte maŋemuš oassi
 	#$ Dat ledje beakkán ja dovddus olbmot nu go Diaz, Maria Hætta, Anne Gaup, Erik ja vel muhtin eará dovddus olbmot. # ii vuos dán

#!! * **compCoordAttr** coordination again, now with Attr. Speacial rule because of that Attr also has other readings.	
MAP:compCoordAttr (@COMP-CS<) TARGET NP-HEAD-NOM (-1 Attr LINK -1 CRD LINK -1 (@COMP-CS<))(NEGATE 0 Attr LINK 1 NP-HEAD-NOM) ;
	#$ Jesus lea gievrrat go Sáhtán ja bahá vuoiŋŋat.

#!! * **compInf** 
MAP:compInf (@COMP-CS<) TARGET Inf (-1 ("go" CS) LINK -1 Nom LINK -1 Comp OR COMPAR) ;
	#$ Ja de son riegádahtii máná jođidettiin, ja de ii lean iežá dorvu go coggat ohcii máná ja vuojehit ein bohccuid.

#!! * **compInf** 
MAP:compInf (@COMP-CS<) TARGET Inf (-1 ("go" CS) LINK -1 ConNeg LINK -1 Neg) ;
	#$ Ledje láikkes olbmot geat eai veadján go bargat oasi jagis, ja muđui oaggut, bivdit ja murjet.


#!! * **compInfCoor** 
MAP:compInfCoor (@COMP-CS<) TARGET Inf (-1 JA/DAHJE LINK *-1 @COMP-CS< BARRIER Nom OR REAL-V) ;
	#$ Ja de son riegádahtii máná jođidettiin, ja de ii lean iežá dorvu go coggat ohcii máná ja vuojehit ein bohccuid.


SECTION
 	
#!! * **killAllnotComp** Removes analysis which are not @COMP-CS<
SELECT:killAllnotComp  @COMP-CS< ;	

#!!     - *This was the kill all not Comp rule!!*

#!! * **goCSbeforeComp** Selects CS analysis in front of @COMP-CS<
SELECT:goCSbeforeComp ("go" CS) IF (*1 @COMP-CS< BARRIER REAL-V OR CC) ;

#!! * **ACompgo** Selects Comp analysis in front of go and @COMP-CS<
SELECT:AdvCompgo Adv + Comp IF (1 go LINK *1 @COMP-CS< BARRIER REAL-V OR CC)(*-1 REAL-V BARRIER NOT-ADV-PCLE LINK NOT 0 COPULAS OR AUX) ;
SELECT:Compgo A + Comp IF (1 go LINK *1 @COMP-CS< BARRIER REAL-V OR CC)(NOT -1 ("seammá") OR ("ovtta")) ;



SECTION

#!! # MAPPING OF CC AND CS

#!! Mostly we map both @CNP and @CVP, then we select @CNP, after that we remove them so @CVP remains

#!! * **cnpCompSC** Map @CNP if @COMP-CS< or COMPAR ahte
MAP:cnpCompSC (@CNP) TARGET ("go" CS) (*1 @COMP-CS< BARRIER S-BOUNDARY OR CC OR CS);
	#$ Son lea buoret go mun.
	#$ Internáhta váhnenčoahkkimiidda bohte ollu váhnemat, ollu eanet go lágiduvvon biirečoahkkimiidda.

#!! * **cnpCompSpec** special rule because of PrfPrc = VFIN
MAP:cnpCompSpec (@CNP) TARGET ("go" CS) (*1 PrfPrc BARRIER REAL-V LINK 1 NP-HEAD-NOM - Attr - Comp) ;
	#$ Dat ii galgga dagahit ahte boazolohku šaddá alit go siidii mearriduvvon lohku.

#!! * **CSasCNPCVP** Map some CSs both @CNP @CVP
MAP:CSasCNPCVP (@CNP @CVP) TARGET CS (0 ("go") OR ("vai") OR XGO OR DADE);

#!! * **CSasCVP** Map @CVP to CS
MAP:CSasCVP (@CVP) TARGET CS (NEGATE 0 Indef) ;
	#$ Leago guhkes áigi dassá go Máreha oidnet?

#!! * **CCasCNPCVP** Map (@CNP @CVP) to CC
MAP:CCasCNPCVP (@CNP @CVP) TARGET CC ;

#!! * **ahteCNP** ahte CC @CNP, remove the rest
IFF:ahteCNP ("ahte" CC @CNP) IF (-1 COMPAR OR Comp)(0 ("ahte"))(1 COMPAR OR Comp);
	#$ Dárogiel sánit bohtet eanet ahte eanet sámegiela sisa.
	#$ Dat šattai heajut ahte heajut.
	#$ Áiggi mielde eanet ahte eanet lohkagohtet sámegillii.
	
#!! * **killAllahtenotCS** All other occurrences of "ahte" are CSs.
SELECT:killAllahtenotCS ("ahte" CS) ; 

SELECT:degoCNP (@CNP) + XGO ((*1 @COMP-CS< BARRIER NOT-NPMOD) OR (NEGATE *1 VFIN BARRIER S-BOUNDARY) OR (-1 COPULAS LINK 2 Inf) OR (-1 COPULAS LINK 2 ADVLCASE OR Adv LINK 1 Inf))(NEGATE *-1 BOS BARRIER WORD) ;
	#$ Dat guhte ii biso mu siste, bálkestuvvo olggos dego oaksi ja son goldná.


#!! * **RelCNPRel** maid ja gos
SELECT:RelCVPRel (CC @CNP) IF (0 JA/DAHJE) ((-1 Rel LINK 2 Rel) OR (-1 Interr LINK 2 Interr OR ("man" Adv)) OR (-1 Rel LINK 2 ("man" Adv) LINK 1 A OR Adv)) ;
	#$ Lea registreren maid ja man ollu lea borran.


#!! * **vaiCCCNP** vai as CC or CS
SELECT:vaiCCCNP (CC @CVP) IF (0 ("vai")) (-1 Inf)((1 Inf) OR (1 Adv OR N OR A LINK 1 Inf));
	#$ Buriid dahkat vai baháid dahkat.
	#% Sii fertejit dahkat juoga vai ođđa čállit álggášedje čállit girjjiid sámi mánáide ja nuoraide.

#!! * **vaiCC** remove vai as CC 
REMOVE:vaiCC CC IF (0 ("vai"))(NEGATE *-1 Qst OR Interr OR ("go" Pcle))(NEGATE 1 Qst OR Neg)(NEGATE 2 ("go" Pcle))(*1 VFIN LINK NOT 0 N)(NEGATE *1 ("?"));
	#$ Árranis sii vušše liepmasa ealuide vai sestet goikerásiid.
	#$ Vai miehtan galgá leat lágalaš, ferte divššohas leat ožžon ...

#!! * **vaiCCNegQst1** vai CC @CVP before Neg or question
SELECT:vaiCCNegQst1_a (CC @CVP) IF (0 ("vai")) (1 Neg LINK 1 Qst OR ("?"));
SELECT:vaiCCNegQst1_b (CC @CVP) IF (0 ("vai")) (1 VFIN + Qst);
SELECT:vaiCCNegQst1_c (CC @CVP) IF (0 ("vai")) (1 VFIN LINK 1 ("go" Pcle));
	#$ Leago sámeálbmogis vuoigatvuođat vai eai?
	#$ Dáhpáhuvvá go dat ođđa lágaid bokte, vai geahččaluvvo go gávdnat čovdosiid bissovaš rievttis? 
	#$ Dáhpáhuvvá go dat ođđa lágaid bokte, vai geahččaluvvogo gávdnat čovdosiid bissovaš rievttis? 

#!! * **vaiCCNegQst2** vai CC @CNP in question about two alternatives
SELECT:vaiCCNegQst2 (CC @CNP) IF (-1 N OR A OR Adv)(0 ("vai"))(*1 N OR A OR Adv BARRIER NOT-NPMOD LINK NOT 0 VFIN LINK NEGATE *2C VFIN BARRIER S-BOUNDARY OR CS) ; # (*1 ("?")); 
	#$ Leago nieida vai bárdni?
	#$ Odne vai ihttin?
	#$ ...čállogo journála elektrovnnalaččat vai báhpárii?
	#$ Sámedikki eará orgánat mearridit ieža galget go čoahkkimat leat rabas vai gitta.


#!! * **vaiCCPrfPrcInfQst** vai CC @CNP in question about two alternatives
SELECT:vaiCCPrfPrcInfQst (CC @CNP) IF (0 ("vai") LINK -1 REAL-V LINK *-1 Qst BARRIER VFIN)((-1 PrfPrc LINK 2 PrfPrc) OR (-1 Inf LINK 2 Inf) OR (-1 Actio + Ess LINK 2  Actio + Ess)) ;
	#$ Lea eahpečielggas lea go mánáidgárddiid lohku lassánan vai unnon.

#!! * **killAllvainotCSCVP** Select all vai CS @CVP	
SELECT:killAllvainotCSCVP (CS @CVP) IF (0 ("vai"));

#!! * **dadeCNP** removes dađe @CNP, so @CVP remains
REMOVE:dadeCNP (@CNP) IF (0 DADE) ; 
	#$ Mađe mielde oaivi, dađe mielde ságat. # ii leat VFIN   

#!! * **CVPNPron** No finite verb or verbalactivity in front N/Pron @CNP N/Pron	
REMOVE:CVPNPron (CC @CVP)  IF (
(-1 NP-HEAD-NOM OR (Pron Nom) LINK NEGATE *-1 REAL-V BARRIER S-BOUNDARY-NOT-CVP OR CS) OR 
(-1 NP-HEAD-NOM LINK *-1 VERBAL-ACTIVITY-V BARRIER NOT-NPMOD))
((*1 NP-HEAD-NOM BARRIER NOT-NPMOD-ACC-ADV) OR (1 Num + Nom LINK 1 N + Gen) OR (1 Gen LINK 1 Num + Nom LINK 1 N + Gen)) (NEGATE -1 @COMP-CS<)
;
	#$ Maid moarsi ja irgi leaba čiŋadan gávttiiguin.
	#$ Mun ja mu ustibat manaimet kinoi geahččat buot ođđaseamos Harry Potter-filmma.
	#$ Go mu isit ja mu guokte bártni gárvodit, mun ráhkadan niestti.
	#% Dat guhte ii biso mu siste, bálkestuvvo olggos dego oaksi ja son goldná.


#!! * **CVPnoVfin** No potential finite verb following
REMOVE:CVPnoVfinAttr (@CVP) IF (0 CC LINK *-1 N BARRIER WORD - COMMA) (1 Adv OR A LINK 1 N LINK NOT 0 VFIN LINK NEGATE 1* VFIN BARRIER S-BOUNDARY OR CS OR PUNCT-RIGHT OR COMMA)(NEGATE *1 ("leat" PrfPrc) BARRIER S-BOUNDARY-NOT-CVP OR PUNCT-RIGHT OR CCCOMMA LINK NEGATE *-1 PrfPrc BARRIER REAL-V)(NEGATE 1 MO - ("nugo") OR ("de") OR Rel LINK *1 VFIN)(NEGATE -1 BOS)  ; 
	#$ Mu givssidedje danin go hállen sámegiela ja heajos dárogiela.
	#$ Mu givssidedje danin go hállen sámegiela, ja heajos dárogiela.
	#$ Rusttegat mat fievrridit dahje nuppástuhttet elektrihkalaš energiija oktan dasa gulli elektrihkalaš huksehusaiguin ja huksenteknihkalaš konstrukšuvnnaiguin, leat earret eará fápmojođđasat...
	#% Ledjen optimista ja dollen logaldallamiid ášši birra.
	#% Eanaš internáhtat ledje gieldda hálddus, ja maiddái Finnmárkku rittus ledje muhtun gieldda internáhtat.
	

#!! * **CVPnoVfin** Infitive following
REMOVE:LocLoc (@CVP) IF (0 CC) (-1 (N Loc) OR ("birra" Po)) (*1 (N Loc) BARRIER NOT-A-ADV LINK 1 Inf)  ; 
	#$ Dás son muitala iežas vásáhusaid birra Cizáš mánáidgárddis Oslos ja muđuid rahčamušain oažžut mánáguovtto guovttegielalažžan.

REMOVE:CVPnoVfin1 (@CVP) IF (NEGATE *1 VFIN BARRIER S-BOUNDARY OR CS OR PUNCT-RIGHT) (NEGATE 1 S-BOUNDARY)(NEGATE -1 BOC) (NEGATE 0 CONTRA LINK *1 Rel BARRIER REAL-V OR SV-BOUNDARY LINK *1 VFIN LINK *1 VFIN)(NEGATE *1 ("leat" PrfPrc) BARRIER S-BOUNDARY-NOT-CVP OR PUNCT-RIGHT OR CCCOMMA LINK NEGATE *-1 PrfPrc BARRIER REAL-V)(NEGATE 1 MO - ("nugo") OR ("de") OR Rel LINK *1 VFIN)(NEGATE -1 BOS)(NEGATE 1 CC OR CS LINK *1 VFIN)(NEGATE *1 Inf BARRIER REAL-V OR S-BOUNDARY LINK *-2 Inf BARRIER REAL-V LINK *-1 MOD-ASP OR INF-V BARRIER REAL-V OR S-BOUNDARY)  ; 
	#$ Mun máhtán láibut sihke šuhkoládagáhku ja lákcagáhku.
	#$ Mii bidjat dávviriid skáhpiide ja lođáide.
	#$ Mii vuolgit Londonii golmmain mánáin ja ovttain beatnagiin.
	#$ Dain hirbmat váriin ja mehciin sin guovllus lei earenoamáš vallji sihke elliid ja eará dáfus.
	#$ Bargiidbellodaga Per Nils Saari beasai suohkanstivrii ja válljii ovttasbargat olgešbellodagain, gurutbellodagain ja johttisámi listtuin.
	#$ Eai galgga sii geat gullet dakkáraš minoritehtaide ovttas eará láhttuiguin mat gullet sin jovkui, manahit vuoigatvuođaideaset dikšut iežaset kultuvra, "mihkkege" Pron Indef Sg Acc ja geavahit iežaset oskku, dahje geavahit iežaset giela. # This does not function yet because it needs verb disambiguation.
	#$ Muhto go Martha Hoffmann dáppe oastigođii ránuid maid son vuvddii Lulli-Norgii, sihke museaide ja olbmuide, de šattai fas gánnáhahtti gođđit ránuid.
	#$ Ja nugo olmmoš gohčodii guđege ealli, nu dat šattai navdojuvvot.	

#!! * __CVPnoVfin_iige__ didn't succeed including iige in barrier in the last rule
REMOVE:CVPnoVfin_iige (@CVP) IF (1 N LINK *1 Neg + Foc/Neg-ge BARRIER WORD )(NEGATE 1 S-BOUNDARY)(NEGATE -1 BOC) 
(NEGATE 0 CONTRA LINK *1 Rel BARRIER REAL-V OR SV-BOUNDARY LINK *1 VFIN LINK *1 VFIN)
(NEGATE *1 ("leat" PrfPrc) BARRIER S-BOUNDARY-NOT-CVP OR PUNCT-RIGHT OR CCCOMMA LINK NEGATE *-1 PrfPrc BARRIER REAL-V)(NEGATE 1 MO - ("nugo") OR ("de") OR Rel LINK *1 VFIN)
(NEGATE -1 BOS)(NEGATE 1 CC OR CS LINK *1 VFIN)(NEGATE *1 Inf BARRIER REAL-V OR S-BOUNDARY LINK *-2 Inf BARRIER REAL-V LINK *-1 MOD-ASP OR INF-V BARRIER REAL-V OR S-BOUNDARY)  ; 
	#$ Vibeke Larsen muitala ožžon fálaldagaid Olgežis ja Árjjas, iige leat hilgon goappáge vuos.
	
#!! * **CVPInfInf** between to Inf
REMOVE:CVPInfInf (@CVP) IF  ((-1 Inf LINK NOT -1 N) OR (-1 Inf LINK -1 Acc LINK -1 REAL-V))(0 CC)(1 Inf); 
REMOVE:CVPInfInf (@CVP) IF  ((-1 Inf LINK NOT -1 N) OR (-1 Inf LINK -1 INF-V) OR (-1 Inf LINK -1 Acc LINK -1 REAL-V))(0 CC)(1 Inf); 
	#$ Dii oahppabehtet lohkat ja čállit sámegillii.
	#$ Liná liikui sárgut ja tennjet.

#!! * **CVPadvladvl** between to ADVL
REMOVE:CVPadvladvl (@CVP) IF (-1 ADVLCASE)(1 ADVLCASE LINK 1 COMMA LINK 1 VFIN LINK *1 Nom BARRIER SV-BOUNDARY ) ; 	
	#$ Láhkaásahusa rievdadus dagaha dan ahte dál ferte árjjalaččat ohcalit máná kultuvrralaš ja gielalaš vuoigatvuođaid goziheami dalle go biddjojuvvo biebmoruktui ja mánáidsuodjalanásahussii, cealká Sámediggeráđi lahttu Jåma loahpas.


#!! * **CVPAdvAdv** between to Adv
REMOVE:CVPAdvAdv (@CVP) IF  (-1C Adv)(0 CC LINK 1C Adv); 
	#$ čohkut dahje veallut


#!! * **CVPActioNom** 
REMOVE:CVPActioNom (@CVP) IF  (-1 (N Nom) LINK -1 (Actio Nom) LINK -1 HAB-ACTOR + Gen)(*1 VFIN) ;
	#$ Stáhta nammadan lahttu ja sadjásašlahttu galgaba deavdit gáibádusaid.

REMOVE:CVPPrfPrc (@CVP) IF (-1 PrfPrc)(0 CC)(1 PrfPrc LINK NEGATE 0 VFIN + AUX LINK *1 Inf BARRIER NOT-ADV-PCLE); 
	#% Jus lean váiban dahje ferten lihkkat árrat, de manan nohkkat oalle árrat.

REMOVE:CVPCase (@CVP) IF  (-1 Com)(0 CC)(1 Com); 
	#$ Son oažžu vuokkain ja luovosfirpmiin bivdit miehtá deanu.	
	
REMOVE:CVPAPrcPrf (@CVP) IF  (-1 A)(0 CC)(1 PrfPrc LINK *1 N BARRIER NOT-NPMOD-ACC); 
	#$ Sámeskuvla šattai oktasaš deaivvadanbáikin hárvelohkosaš ja hádjánan minoritehtii.	
	
	
REMOVE:CVPNomNom (@CVP) IF (-1 (N Nom))(0 CC) (*1 (N Nom) BARRIER NOT-NPMODADV-INDEF LINK 1 EOS); 
	#$ Pippi viesu bálddas lei nubbi gárdin ja nubbi viessu.
	#% Oabbá válddii biillas ja vuoddjái.
	
REMOVE:CVPnoVfin3 (@CVP) IF  (-1 N LINK NEGATE *-1 VFIN BARRIER BOS)(0 CC)(*1 N BARRIER NOT-A LINK *1 VFIN BARRIER S-BOUNDARY) ; 
	#$ Grammatihkkavuđot giellateknologalaš reaiddut teakstaanalyseremii ja giellaoahpahus ja digitálalaš sátnegirjjit leat buot gielaide eaktun dasa ahte sáhttet ceavzit geavahangiellan ođđaáigásaš servodagas.

#!! * **CVPnoVfinAdvl** No finite verb in front ADVLCASE @CNP ADVLCASE	
REMOVE:CVPnoVfinAdvl (@CVP) IF  (-1C ADVLCASE OR Adv - ("easka") LINK NEGATE *-1 VFIN BARRIER S-BOUNDARY-NOT-CVP OR CS)(1 ADVLCASE OR Adv LINK 1 VFIN);
	#$ Ii fal runiidan jalges gittiide, muhto meahccái, gosa návddit dávjá ja lunddolaččat fievrredit ráppiid.
    # Should not hit sentences like this:  
 	#% Lávdegoddi lea dasa lassin guorahallan báikkálaš riekteáddejumiid Finnmárkkus ja vuordimis áigu dán ilbmadit iežas boahtte čielggadeami mielddusin.
 	#% Dat lea mainna lean olahan bohtosiid herggiin ja seamma ládje geahččalin heasttain.

	#$ Grammatihkkavuđot giellateknologalaš reaiddut teakstaanalyseremii ja giellaoahpahussii ja digitálalaš sátnegirjjit leat buot gielaide eaktun.	#% Dállu lea divrras midjiide danne go dat vuosiha govt mearrasámit elle ovdal ja makkár viesut sis ledje.	


REMOVE:CVPMiella (@CVP)  IF (-1C ADVLCASE OR Adv LINK *-1 COPULAS BARRIER NOT-ADV-PCLE)(*1 ADVLCASE OR Adv BARRIER NOT-NPMOD-ACC-ADV) ;
REMOVE:CVPMiella (@CVP) IF  (-1C ADVLCASE OR Adv LINK *-1 COPULAS BARRIER NOT-ADV-PCLE)(*1 Attr BARRIER NOT-NPMOD-ACC-ADV LINK *1 ADVLCASE OR Adv BARRIER NOT-NPMOD-ACC-ADV) ;
	#$ De lea divššohasas ja su lagamus oapmahaččas vuoigatvuohta oažžut dieđuid.
 
#!! * **CVPAdvNom** Nom @CNP Adv Nom	
REMOVE:CVPAdvNom (@CVP)  IF (-1 NP-HEAD-NOM LINK *-1 VERBAL-ACTIVITY-V BARRIER NOT-NPMOD)(*1 NP-HEAD-NOM BARRIER NOT-NPMODADV);
	#$ Son lohká bargit ja maiddái fáŋggat fertejit oažžut sámegiel oahpahusa.

#!! * **CVPCopNomInf** COPULAS Nom @CNP Nom Inf	
REMOVE:CVPCopNomInf (@CVP) IF  (-1C (N Nom) LINK *-1 VFIN BARRIER NOT-NPMODADV)(1C (N Nom) LINK 1 Inf);
	#$ Jagi 2005 Sámediggi álggahii Divvuma, mii lei prošeakta ja bargojoavku ráhkadit sámegielaid sátnedivvunprográmmaid.


#!!  ***CVPoppramsing** Lásse, Iŋgá ja mun
REMOVE:CVPoppramsing (@CVP) IF  (-1 Nom LINK *-1 COMMA BARRIER NOT-NPMOD LINK -1 Nom)(0 CC)(*1 Nom BARRIER NOT-NPMOD);
	#$ Lásse, Iŋgá ja mun leimmet Romssas.
	#  "Lásse" also have VFIN reading.


#!! ***CVPCmp/SplitR** Cmp/SplitR @CNP
REMOVE:CVPCmp/SplitR (@CVP) IF  (-1 Cmp/SplitR);
	#$ Ulbmil seammádássásaš bohtosiid juksamis galgá gustot álo beroškeahttá makkár giella- dahje kulturduogáš dárbbašeaddjis lea.

#!! * **CVPwrongCmpnd** wrongly formatted compounds
REMOVE:CVPwrongCmpnd (@CVP) IF  (-1 ("\-") LINK -1 (\?) LINK -1 BOS)(0 CC LINK 1 N);
	#$ Eanandoallo- ja biebmoministtar Lars Sponheim lohká ahte goas boazodoalloláhka biddjo Stuoradikki ovdii mearrideapmái, lea dan duohken man muddui Oarje-Finnmárkkus lea dohkálaš boazolohku cuoŋománu 1. beaivvi.

#!! * **CVPAAttr** A Attr @CNP A Attr	
REMOVE:CVPAAttr (@CVP)  IF (-1 (A Attr) OR Gen LINK -1 CC OR CS)(0 CC)(1 (A Attr) OR Gen OR (@>N));
	#$ Olbmuin geain leat lihkadanváttut leat váttisvuođat go boares ruovdemáđiija vovnnaid oláheapmi ii leat nu buorre, ja ođđa ja stuorit stášuvnnain ges lea heajos čalmmálaš.

#!! * **CVPA** A @CNP A 	
REMOVE:CVPA (@CVP)  IF (-1C A)(0 CC)(1C A LINK *1 N BARRIER NOT-NPMOD);
	#$ E-boasta lea hui álkes ja jođánis vuohki sáddet reivviid birra máilmmi.


#!! * **CVPAccAdv** Acc @CNP Adv Acc	
REMOVE:CVPAccAdv (CC @CVP) IF  (-1 Acc)(*1 Acc BARRIER NOT-NPMODADV LINK NOT 0 VFIN)(NEGATE 1 Num LINK 1 Gen LINK 1 VFIN);
#REMOVE:CVPAccAdv (CC @CVP) IF (-1 Gen)(*1 Gen BARRIER NOT-NPMODADV LINK 1 Po OR ADVLCASE);
	#$ Juos gorreválggain lea dárbbašlaš deavdit gáibádusa, man mielde lahtuid ja sadjásašlahtuid gaskkas galget leat sihke nissonat ja dievddut.
	#% Beassášmárkaniin fitne sullii 9000 guossi ja 3500 guossi ledje čakčamárkaniin.

#!! * **CVNFauxcFmainv** 
SELECT:CVNFauxcFmainv @CNP IF (-1 N + Nom LINK NOT 0 REAL-V LINK *-1 VFIN + AUX BARRIER NOT-ADV-PCLE)((*1 N BARRIER NOT-NPMOD LINK 1 Inf - AUX) OR (*1 Gen BARRIER NOT-NPMOD LINK 1 N + Nom LINK 1 Inf - AUX)) ;
	#$ Mearrádusaid gittaopmodaga luobaheamis, mat leat dahkkojuvvon uhcit go njealji stivrralahtu doarjagiin, ferte Sámediggi ja Finnmárkku fylkkadiggi dohkkehit.
	#% Jus lean váiban dahje ferten lihkkat árrat, de manan nohkkat oalle árrat.

SELECT:CVP (@CVP)  IF (*-1C VFIN BARRIER S-BOUNDARY)(*1C NP-HEAD-NOM BARRIER NOT-NPMOD LINK *1 VFIN BARRIER S-BOUNDARY)(0 @CNP)(NEGATE 0 go LINK *1 Inf BARRIER VFIN) ;
	#$ Áhčči vulggii, ovdalgo mun bohten.
	#$ Mun mannen maŋŋilgo mii leimmet čorgen. # fungerer ikke enda pga manglende verbdisambiguering

REMOVE:CS (@CVP)  IF (-1 CS)(1 CS) ;
	#$ Jos ja go suodjalus ii šat dárbbaš guovllu, dat galgá máhcahuvvot sámi geavahussii, Skum cealká.

REMOVE:Prop (@CVP)  IF (-1 Prop + Gen)(1 Prop + Gen LINK 1 Po) ;
	#$ Sotnabeaivve guolddut dagahedje bárttiid, Šuoššjávrre ja Gievdneguoikka gaskkas ledje máŋga biilla darvánan.


REMOVE:ja (@CVP)  IF (-1 Num LINK -1 Pr)(1 Num) ;
	#$ Sihke ovdal ja maŋŋil dan leat gaskkal vihtta ja logi olbmo ožžon dávdda jagis.

REMOVE:ja (@CVP) IF (-1 Prop + Gen LINK -1 Inf OR (Actio Ess) LINK -1 AUX) (1 Prop + Gen LINK 1 N) ;
REMOVE:ja (@CVP) IF (-1 Prop + Gen LINK -1 VFIN) (1 Prop + Gen LINK 1 N) ;
	#$ Guovdageainnu suohkan áigu ovttasbargat Eanodaga ja Álttá suohkaniiguin, ovdánahttit mátkkoštanealáhusa.


#!! * **killAllCNP** removes all remaining @CNP
REMOVE:killAllCNP @CNP IF (0 @CVP) ;

#!! * **XCC-CS** removes CC and CS with no synttag
MAP:CC-CS (@X) TARGET CC-CS ;
REMOVE (@X) ;    



#!! ## PRONOUNS

#!! ### Plural?

#!! * **PlSg3V** removes plural in front of Sg3 verb (and **SgPl3V** does the opposite)
REMOVE:PlSg3V NP-HEAD-PL-NOM IF (0 (Inf) OR NP-HEAD-SG-NOM)(*1C SG3-V BARRIER NOT-ADV-PCLE);
REMOVE:PlSg3V NP-HEAD-PL-NOM IF (0 (Inf) OR NP-HEAD-SG-NOM)(-1C SG3-V)(NOT 1 VFIN + Pl3);
	## Dát lei deháleamos ágga.

REMOVE:SgPl3V (Pron Sg Nom) IF  (*1C PL3-V OR DU3-V BARRIER NOT-ADV-PCLE);
	## Dát lei deháleamos ágga.
	#$ Dathan han gal leat buorit guolit.


#!! ## Interrogative and relative pronouns


#!! * **Interr** selects interrogative pronouns in questions

SELECT:Interr Interr IF ((-1 COMMA LINK *-1 CS LINK *-1 BOS BARRIER NOT-PCLE) OR (*-1 BOC OR LEFT BARRIER NOT-PCLE LINK NOT 0 ("maid") OR Interr) OR (-1 COMMA LINK -1 HUMAN + Nom LINK *-1 BOC BARRIER WORD))(0 Rel OR Indef LINK NOT 0 ("man" Adv)) ((*1 ("?")) OR (2 Pot));
	#$ Juos Ipmil lea min beale, gii lea min vuostái?
	#$ Maid don doaivvut, mun gielistan?
	#$ Na maid don diŋgojit?
	#$ - Áhčči, mii lea duot čáppa alit násti doppe guhkkelis?
	#% Maid mii dál vihtaniiguin?
	#% Maid de barggažan, dehaleamos lea ahte oaččun bargat maiddái sámegillii ja sámi áššiiguin.

SELECT:Interr Interr IF (-1 JEARRAT)(0 Rel OR Indef LINK *1 VFIN);
SELECT:Interr Interr IF (0 ("goabbá") + Sg)(NOT 0 Foc/Pos-ge) ;


#!! * **InterrIfPot** selects interrogative pronouns in potential sentences, and after that we remove the remaining Interr
SELECT:InterrIfPot Interr IF (*-1 BOC BARRIER NOT-PCLE)(0 Rel)(*1 TRANS-V + Pot BARRIER S-BOUNDARY OR VFIN-POS);
	#$ Na maid dal juo dagažat ja maid it dagaš.

SELECT:miiPlAcc Interr IF (0 ("mii") OR ("gii") OR ("mihkke" Foc/Neg-ge) OR ("mii" Foc/Neg-ge) OR ("guhte" Pl))(1 Acc + Pl LINK 1 VFIN) ; 
	#$ Mii gielaid gullo ja geavahuvvo ruovttus.

REMOVE:KillInterr Interr ;

#!! * **munPl3** removes Pron Pers Pl3 if there is no verb agreement
REMOVE:munPl3 Pers IF (NEGATE *-1 PL1-V BARRIER NOT-ADV-PCLE)(0 ("mii"))(*1 SG3-V BARRIER PL1-V OR SV-BOUNDARY LINK NOT 0 Pl1);
	#$ Dát ii guoskka dikšui mii boahtá psykiatriija vuollái.

#!! * **Rel** selects Rel
SELECT:Rel Rel IF (*-1 NOT-AUX-V BARRIER S-BOUNDARY LINK *-1 NP-HEAD-NOM BARRIER S-BOUNDARY)(*1C NP-HEAD-NOM BARRIER S-BOUNDARY);
	#$ ...ahte olbmot besset oaidnit maid sii sáhttet vuordit.

#!! * **RelSg1, RelSg2** select Rel
SELECT:RelSg1 (Rel Sg) IF  (-1C NP-HEAD-SG OR (Num Sg))(NEGATE *-2 NUM BARRIER NOT-NPMOD)(*1 VFIN)(*1 TRANS-V);
REMOVE:RelSg2 (Rel Sg)  IF ((*-1 (N Sg) BARRIER WORD LINK *-1 CC BARRIER NOT-NPMOD LINK -1 N) OR (-1 Interr));
	#$ Mun oasttán peanna ja bliántta, maid mun dárbbašan skuvllas.
	#$ Nubbi maid fuobmáimet lei ahte... 

#!! * **RelPl** selects Rel
SELECT:RelPl (Rel Pl) IF  ((-1 NP-HEAD-PL LINK NOT 0 NP-HEAD-SG) OR (-1 NP-HEAD-SG LINK -1 Num)) (*1 VFIN);
	#$ ...go lei sáimmaiguin bivdán jávrris maid stáhta lei láigohan. 
	#$ Doaibma gullá doaimmaide maid ulbmil lea movttiidahttit sámi nuoraid... 

#!! * **RelPl** removes Rel
REMOVE:RelPl (Rel Pl) IF  (NEGATE *-1 (N Pl) OR NOT-SG-PRON BARRIER N OR Pron)(NEGATE *-1 N LINK *-1 Num OR CC BARRIER V)(0 (Rel Sg));
	#$ Lávvui gullá maid uvssot.

REMOVE:miiPl Pl IF (*-1 BOC-PUNCT OR (":" CLB) BARRIER NOT-ADV-PCLE)(0 ("mii" Pron Acc) LINK 0 Rel OR Interr);
	## Maid don doaivvut, mon gielistan?

SELECT:manAcc Known + Rel  IF (0 Acc)(*-1 N + Sg BARRIER WORD) (1 VFIN) ;
	#$ Sámi allaskuvlla lektor Lovisa Mienna Sjöberg bealušta iežas doavttergrádadutkamuša sivdnideamis, man lea dahkan Oslo Universitehta Teologalaš fakultehtas.


#!! ## Emphatic ieš     

#!! * **ies1Pl, ies2Pl** select Pl for ieža

SELECT:ies1Pl Pl IF (*-1 PL-V OR (V Imprt Pl2) OR MII-PERS OR DII OR SII OR (Pron Rel Pl Nom) BARRIER SV-BOUNDARY OR PUNCT)(0 ("ieš" Pron Refl Pl Nom))(NEGATE *1 DU-V BARRIER NOT-ADV-PCLE);
	#$ Allet dubme, amadet ieža dubmejuvvot!
	#$ ...muhto leat maiddái muhtumat geat ieža leat dahkan iežaset 
	#$ dohkketmeahttumin...

SELECT:ies2Pl Pl IF (0 ("ieš" Pron Refl Pl Nom))(*1 PL-V BARRIER SV-BOUNDARY);
	#$ ...ja didjiide mihtiduvvo seammá mihtuin mainna ieža ge mihtidehpet.

#!! * **iesDu** select Pl for ieža
SELECT:iesDu Du IF (*0 DU-V OR MOAI OR DOAI OR SOAI BARRIER SV-BOUNDARY OR PUNCT)(0 ("ieš" Pron Refl Du Nom));
	#$ Doai galgabeahtti ieža mannat.
	#$ Ieža soai muitaleigga mii lei dáhpáhuvvan mátkkis.


#!! ## Numerals



#!! * **NifNum** 
REMOVE:NifNum N IF (0 Num - Rom)(*1 (N Sg Gen) OR Superl + Gen BARRIER NOT-A - Indef - PrfPrc)(NEGATE 0 (N ACR))(NEGATE 0 Coll LINK 1 PrfPrc OR Num)(NEGATE 0 Nom LINK *1 VFIN BARRIER NOT-A);
	#$ Mii finaimet doppe guđa geardde.

SELECT:Num1 Num IF (0 Loc LINK 1 Num + Ill - Attr) ;
SELECT:Num2 Num IF (0 Loc LINK 1 Num + Gen LINK 1 ("rádjai")) ;
SELECT:Num3 Num IF (0 Ill LINK -1 Num + Loc - Attr) ;
SELECT:Num4 Num IF (0 Gen LINK 1 ("rádjai") OR ("áigi")) ;
	#$ Nullas čuođi rádjai, de galgá ádjánit sullii 2,1 sekundda.

	
#!! * **AdvOvtta** 
REMOVE:AdvOvtta Adv IF ((0 OKTA + Acc LINK 1 (Sg Acc)) OR (0 OKTA + Attr LINK *1 N + Sg + Loc OR N + Sg + Ill BARRIER NOT-A)); 
	#$ Soai vácciiga ovtta gaskka.
	
#!! * **AdvNumEss** 
REMOVE:AdvNumEss Adv IF (0 OKTA + Ess)(1 Ess); 
	#$ Mii barggaimet oktan joavkun.

#!! * **NumCurrency** Selects Num
SELECT:NumCurrency Num IF (1 CURRENCY OR MEASURE);
	#$ Dainna čuđiin ruvnnuin áiggun bierggu oastit.

#!! * **NumNomJahki** Selects (Num Nom) 
SELECT:NumNomJahki (Num Nom) IF (1 ("jahki" Pl Gen)) ;
	#$ 1970-1980 jagiid ásahedje maiddái sámi kulturguovddážiid dáid skuvllaid olis, ja maid stáhta ja fylkkagielda ruhtadedje.

#!! * **NumDassa** Selects (Num Nom)
SELECT:NumDassa (Num Nom) (1 TIME-N + (Sg Gen) LINK 1 ("dassá") OR ("dassái"))  ;
	#$ 

#!! * **NumAccCurrency** Selects (Num Acc)	
SELECT:NumAccCurrency (Num Acc) IF (NEGATE *-1 COPULAS OR Der/Pass BARRIER NOT-ADV - CLB)(NEGATE -1 CC LINK -1 N LINK *-1 COPULAS BARRIER NOT-ADV - CLB)(NEGATE *-1 Nom BARRIER NOT-ADV LINK *-1 COPULAS BARRIER NOT-NPMOD)(NOT -1 Num)(1 CURRENCY + Acc OR MEASURE + Acc LINK NEGATE 0 Gen LINK 1 COPULAS OR Po) ;
	#$ 
	#% Ráhkkásiiddán, allet vajáldahte ahte Hearrái lea okta beaivi dego duhát jagi ja duhát jagi dego okta beaivi.

#!! * **árvosátniNum** Selects (Num Nom) 
SELECT:árvosátniNum Nom IF  (-1 ("árvosátni"))(0 Arab)(NEGATE 1 Po);


#!! * **NumNom** Selects (Num Nom) 
SELECT:NumNom (Num Nom) IF  (-1 ABBR OR ACR OR ("árvosátni") OR ("§") OR ("paragráfa") OR ("kapihtal") OR ("govus") OR ("tabealla") OR HEADOFPARTS OR JAHKI-NUM OR TEXT OR LAHKA LINK NOT 0 Ess)(NEGATE -1 ("b" ABBR))(NEGATE -1 ("nr"))(0 Num)(NEGATE 1 Ill - Attr OR Loc - Attr)(NEGATE -1 JAHKI-NUM OR TEXT OR LAHKA LINK 2 Po)(NEGATE 1 Po);
	#$ ... maid KITH gárvvistii ja almmuhii geasset 2001.
	#$ Son vuittii Davviriikkaid Ráđi girjjálašvuođabálkkašumi jagi 1991 dán girjji ovddas.

	
#!! * **NumNomCoord** Selects (Num Nom) 
SELECT:NumNomCoord (Num Nom) IF  (-1 JA/DAHJE LINK -1 Num LINK -1 ABBR OR ACR OR ("árvosátni") OR ("§") OR ("paragráfa") OR ("kapihtal") OR HEADOFPARTS OR JAHKI-NUM OR TEXT OR LAHKA LINK NOT 0 Ess)(NEGATE -1 ("b" ABBR))(NEGATE -1 ("nr"))(0 Num)(NEGATE 1 Ill - Attr OR Loc - Attr);
	#$ Riekti oaivvilda dán vuoigatvuođadilálašvuođa dál sáhttit lea ain eanet cieggan almmá vuostálastima haga nu go leamaš vássán áiggis gitta das rájes go advokáhta Schiefloe almmuhii girjjiidis jagiin 1952 ja 1955.
 # These rules leave only the Nom Num reading, 

#!! * **r1082** Selects (Num Nom) 
SELECT:r1082 (Num Nom) IF (-1 COUNTABLE - Ess OR ("nr"))(0 ABBR OR ACR OR Num) ;
	#$ Dieđáhusa oasis I ovdanbuktojuvvo Bb:a sámepolitihkalaš vuođđolávdi. 
	#$ Áirras nr. 10 Ole Henrik Magga oaččui permišuvnna.
	#$ Luonddumáŋggabealatvuođalága paragráfa 15 mielde ii leat lohpi lottibeasi bilidit man dat lea juo álgán ráhkadit.

#!! * **year** Selects (Num Gen)
SELECT:year (Num Gen)  IF (-1 GEASSET) ;
	#$ ...maid KITH gárvvistii ja almmuhii geasset 2001.

#!! * **numunit** Selects (Num Gen) + NUMUNIT 
SELECT:numunit (Num Gen) + NUMUNIT IF  (-1 (Num Acc)) ;
	## Dat máksá guokte duhát ruvnno.

#!! * **NumGenPo** Selects Gen if you are Num and there is a Gen following the first Gen to the right *gávcci máná njuni ovddas* 
SELECT:NumGenPo Gen IF (0 Num LINK **1 (N Gen) BARRIER NOT-NPMODADV - Acc - Indef - Pcle LINK 1 Po) ;	
	#$ Miehtá čájáhuslanja lea lebbejuvvon plastihkkagovččas ja gávcci máná njuni ovddas lea čáhcemála.

SELECT:geaidnuNum (Num Arab Sg Nom) IF (-1 (".*geaidnu"r)) ;
SELECT:jagiGen (Num Sg Nom) IF (-1 ("jahki" Gen)) ;
REMOVE:Locattr (Num Loc Attr) IF (NEGATE *1 (N Loc) BARRIER NOT-NPMOD-ACC  - Indef) ;
REMOVE:Illattr (Num Ill Attr) IF (NEGATE *1 (N Ill) OR Num + Ill BARRIER NOT-NPMOD-ACC) ;
REMOVE:LocIllattr NUM + (Loc Attr) OR NUM + (Ill Attr) IF (1 (N Gen)) ;
SELECT:numPlAccn (Num Pl Acc)(1 MEASURE + Pl) ;

SELECT:WWNumOrdGen Gen IF (0 Num OR Ord LINK NOT 0 Acc)(1 (N Gen) - PROSEANTA LINK *1 (N Acc) OR (N Gen) OR (N Nom) BARRIER NOT-A)(NEGATE -1 TRANS-V LINK NEGATE *3 Acc)(NEGATE -1 TRANS-V LINK *3 Acc LINK 1 Po);
	## Dan eatnamis oažžu ovtta gusa fuođđara.

SELECT:nubbiAttr Ord + Attr (0 ("nubbi") LINK 1 ("jođiheaddji")) ; # vanlig med særskriving
SELECT:nubbiAttr Ord (0 ("nubbi") + Ess LINK 1 ("jođiheaddji")) ; # vanlig med særskriving

#!! * **WWNumOrdIllAttr** selects Ill Attr and Loc Attr for numerals and ordinals
SELECT:WWNumOrdIllAttr (Ill Attr) IF (0 Num OR Ord) (*1 (N Ill) - Arab OR (Num Ill) - Arab BARRIER NOT-A);	
	#$ Mun attán girjji ovtta oahppái.
	#$ Skuvlaáigi lei gáržžiduvvon golmma vahkkui.
	#$ Spábba guoskkai ovtta Snøgga čiekčái ja manai mollii.
	#Comment: We are lacking some rules for: 'Skuvlaáigi lei gáržžiduvvon 3 vahkkui.' and also: 'Skuvlaáigi lei gáržžiduvvon guovtti dahje golmma vahkkui, jus juo obanassiige čađahuvvui oahpahus.' the last one beeing a coordinated sentence.
SELECT:WWNumOrdIllAttr (Ill Attr) IF (0 NUM OR Ord OR ("nubbi")) (1 (N Gen) LINK *1 (N Ill) BARRIER NOT-A)(NEGATE 0 (Num Nom) LINK 1 (N Sg Gen));	
SELECT:WWNumOrdLocAttr (Loc Attr) IF (0 NUM OR Ord OR ("nubbi") LINK NOT 0 Sem/Date OR Sem/Year) (*1 (N Sg Loc) BARRIER NOT-A - Indef OR Ord) ;	
SELECT:WWNumGen Gen IF (0 ("nubbi") LINK NOT 0 Sem/Date OR Sem/Year) (*1 (N Gen) BARRIER NOT-A - Indef OR Ord) ;	

#SELECT:WWNumOrdLocAttr (Loc Attr) IF (0 Num - Sem/Date - Sem/Year OR Ord)(*1 (N Sg Loc) BARRIER NOT-A - Indef OR Ord) ;	
	## Ovtta ovddasteaddjis divššohasa ovddas lea vuoigatvuohta...
	## Na gal don dal oaččut dan beannot čuohtái, go juo válddežat.	
REMOVE:WWNumOrdAttr (Num Attr) + ADVLCASE OR Indef + Attr + ADVLCASE   IF (NEGATE *1 ADVLCASE + N BARRIER NOT-NPMOD-ACC OR Ord) ;	
	#$ Nuppi vuoru goalmmát minuvttas Anette nivkalii čiehkačievččastaga čábbát njuolga mollii.

SELECT:SUBNumOrdAttr Loc + Attr IF (0 NUM LINK NOT 0 Sem/Date OR Sem/Year) (*1 (N Pl Loc) BARRIER NOT-A - Indef OR Ord) ;	
	#$ Son lei okta dain 20 nissoniin.
SELECT:SUBNumOrdAttr Ill + Attr IF (0 NUM LINK NOT 0 Sem/Date OR Sem/Year) (*1 (N Pl Ill) BARRIER NOT-A - Indef OR Ord) ;	



SELECT:galleIndef ("galle" Indef)  IF (*-1 Neg BARRIER S-BOUNDARY) ;
	#$ Eaige sámiid ássanguovllus leat galle gieldda, mat...
 
REMOVE:galleIndef ("galle" Indef) ;

SELECT:galleNum ("galle" Num) IF (NEGATE -1 Neg)(*1 N BARRIER NOT-NPMOD);
	#$ ...ja govve galle joga leat Deanučázádaga njuoskkadagas.

SELECT:nubbiGen ("nubbi" Pron Indef Gen)  IF (1 ("dáfus" Po) OR ("beali" Po) OR ("hávvi") OR ("beallai" Po) OR ("bealli") OR ("sohkabealli") OR ("bealde") OR ("beales") OR ("láhkai") OR ("riika") OR ("máilbmi"));
	#$ Nuppe dáfus deattuha suohkan, ahte...

SELECT:nubbiIndef ("nubbi" Pron)  IF ((*-1 ("nubbi") OR ("guokte") BARRIER ("goalmmát") OR ("vuosttas") OR ("vuosttaš")) OR (*1 ("nubbi") BARRIER ("goalmmát"))) ;
	#$ Ja go iđit šaddá, de fas reainnárat bohtet ealuin ja de álget fas rátkit seammáládje go vuosttaš beaivve : čuldet nuppit vuohččan, ja go lea unnon eallu, dat bihttá mii lea searvelahkki, de álgojuvvo guovtte guovllus čuoldit. #Comment: <==galggašii deaivat, muhto ii nagat BARRIER dihtii

SELECT:nuppiIndefCom ("nubbi" Pron Com) IF  (1 ("sátni" Com));

SELECT:IndefLoc (Indef Loc)  IF (-1 COMMA OR CC LINK -1 (N Nom) LINK *-1 COPULAS BARRIER REAL-V LINK -1 (N Pl Loc));
	#$ Muhtun siiddain leat moanat goađit, nuppiin unnánat, dan duohken leago rájá siskkobealde stuorra dahje unna eanaguvlloš. 
	
#SELECT:nubbiIndefPl ("nubbi" Pl)(0 ("nubbi" Indef Pl) LINK NOT 1 PL-N); #not good	 

SELECT:nubbiOrd ("nubbi" Ord)  IF (*0 ("vuosttas") OR ("vuosttaš") OR ("goalmmát") OR ("listu")) ;
	#$ Vuosttaš vuojahaga fávllimus čuolda lea goaratčuoldan ja nubbi čuolda fas gáddeoal-čuoldan.
	#$ Nubbi noađđeheargi goallostuvvo giehtahearggi lávžái ja goalmmát fas dan nuppi lávžái.
	#$ Sii leat Ohcejogas golggotmánu vuosttaš beaivvi, Gáregasnjárggas nuppi beaivvi ja Njuorggámis ges goalmmát beaivvi.

SELECT:nubbiOrdManu ("nubbi" Ord) IF  (-1 MANNU + Gen)(1 ("beaivi") OR ("b") OR ("vahkku"));
	#$ Son lei doppe Ohcejogas golggotmánu nuppi beaivvi.

SELECT:nubbiOrdSuperl ("nubbi" Ord) IF ((-1 ("juohke")) OR (1 A LINK 0 Superl OR Loc OR ("maŋit" Superl)));
	#$ Listu lea gieldda nubbi stuorámus bellodat.
	#$ Nubbin vuolemusas lea Guovdageaidnu Finnmárkku listtus.

SELECT:nubbiIndefIll ("nubbi" Indef) IF  (-1 (N Sg Loc))(0 ("nubbi" Ill));
	#$ Son lea viehkan čoahkkimis nubbái maŋŋebárgga rájes.

SELECT:nubbiOrdLex ("nubbi" Ord) IF (1 ("geardi") OR ("buolva" Loc) OR ("giella") OR ("máilbmesoahti") OR ("lađas") OR ("čuokkis") OR ("vuorru") OR ("moalla"));
SELECT:nubbiOrdLex ("nubbi" Ord) IF (1 Gen LINK 1 ("moalla"));
	#$ Henrik oaččui buot gussiid dearvan nuppi gearddi čađa, gos ii buollán.
	#$ Soai leigga fulkkežat nuppi buolvvas.
		
SELECT:nubbiIndefOkta ("nubbi" Indef)  IF (*-1 OKTA BARRIER S-BOUNDARY OR REAL-V);
	#$ Okta nubbi vuodjá tráktor máđiiráigge.
	#$ Riektegeavadis sáhtášii danin geavahit guokte váldolinjjá, okta ovdal ja nubbi fas maŋŋá 1970.
	
SELECT:nubbiOrdEss ("nubbi" Ord Ess) IF  (*-1 MOVEMENT-V OR COPULAS BARRIER REAL-V) ;
	#$ Sii olihedje diibmá nu movt dán jagi nubbin.	
	
SELECT:nubbiIndefAcc ("nubbi" Indef Acc)  IF (*-1 TRANS-V BARRIER SV-BOUNDARY)(1 Inf);
	#$ Ulbmil lea oažžut nuppi boagustit.	

SELECT:nubbiIndefUnnimusat Indef IF (-1 ("unnimustá") OR ("unnimusat"))(0 ("nubbi"));
	#$ Mánáin lea ovttastallanvuoigatvuohta unnimustá nuppiin váhnemiin.

REMOVE:nubbiAttr Attr IF (0 ("nubbi"))(1 Dem OR Adv);
	#$ ... ja nubbi dán suorggi siskkabeale hálddahusortnegiid birra.
	#$ ...nubbi fas rávesolbmuid psykiatriijas.

SELECT:KillnubbiOrd Indef IF (0 ("nubbi" Ord));
	#$ Okta ášši lea mii lágas mearriduvvui, nubbi lea man kommišuvnnat čuvvo mearrádusaid.



#!! ## Indefinite pronouns
#!! The rules are not documented yet

#!! * **IndefAttr1** Selects (Indef Attr) 	
REMOVE:IndefAttr1 (Indef Attr) IF (*-1 LEAT-FIN-NOT-IMPRT LINK NEGATE *1 PRC-NOT-LEAT BARRIER VFIN)(NEGATE *-1 COM-HEAD BARRIER NOT-NPMOD)(0 ("muhtun") OR ("soames"))(*1 Loc BARRIER NOT-NPMOD)(NEGATE *1 VFIN);
	#$ 

#REMOVE:r1568 (Pron @>N) IF (*-1 LEAT-FIN-NOT-IMP LINK NEGATE *1 PRC-NOT-LEAT BARRIER VFIN)(NEGATE *-1 COM-HEAD BARRIER NOT-NPMOD)(0 ("muhtun") OR ("soames"))(*1 Loc BARRIER NOT-NPMOD)(NEGATE *1 VFIN);
	#$ Son lea profehta, aiddo dego muhtun boares profehtain.
    #Comment: Both rules are necessary in exactly this order
#OBS

#!! * **IndefAttr2** Selects (Indef Attr) 
SELECT:IndefAttr2 (Indef Attr) IF (0C Pron - ("nubbi"))(NOT 0 Ill OR Loc)((*1 N - Prop BARRIER NOT-A-PCLE - Indef OR go) OR (1 (Actio Gen) LINK 1 ("veara") LINK 1 N - Prop))(NEGATE 0 ("eará") LINK -1 ("earret"));
	#$ Dan muitalii muhtin mátkkošteaddji gean namma lei Keilhau.
	#$ Njuolggadusain ledje eará ge mearrádusat.
	#$ Dattetge ii leat muitaluvvon makkárge namahan veara geavahanriiddu birra dálolaččaid ja boazodoalu gaskkas.

#!! * **IndefAttr3** Selects (Indef Attr)
SELECT:IndefAttr3 (Indef Attr) IF (1 (A Attr))(*2 HEAD-N BARRIER NP-BOUNDARY);
	#$ ...ja eará almmolaš dahje priváhta doaibma. Comment: Please write the whole sentence as an example. 

#SELECT:r1571 (Indef Attr) IF (1 ("láhkai"));
	#$ Seammá láhkái go váldojournála ja lassijournála geavaheami oktavuođas...
	
#!! * **NoAttr** Removes Attr if you are Pron and first one to your right is (Pron Rel)	
REMOVE:r1572 Attr IF (0 Pron)(1 (Pron Rel));
	#$ Lea unnán maid sáhttit dahkat. 
	
#!! * **NoIndefAttr** Removes (Indef Attr) if first one to the right is (Pron Pers Loc)
REMOVE:r1573 (Indef Attr) IF (1 (Pron Pers Loc));
  	#$ Ja muhtun sis viegai dakkaviđe su lusa. 
  	
#!! * **NoIndefGen** Removes (Pron Gen Indef) or (Pron Acc Indef) if intransitive mainverb to the left and end of sentence to the right *muhto gávdnojit maid eará*
REMOVE:NoIndefGen (Pron Gen Indef) OR (Pron Acc Indef) IF (*-1 FMAINV + IV BARRIER Nom OR SV-BOUNDARY)(1 EOS) ;
	#$ Dat gal lea maid okta fáktor muhtin guvlui, muhto gávdnojit maid eará.
   
#!! * **IndefAttr4** Selects Indef if you are Interr, and to the left is *jus*
SELECT:IndefAttr4 Indef IF (*-1 ("jus") OR ("jos") OR ("juos") OR ("joas") BARRIER S-BOUNDARY)(0 Interr); 
	#$ Jus guhtege dadjá áhččásis dahje eadnásis:...
	#$ ...ahte doaimmat eai juogaduvvo menddo ollugiidda.

#SELECT:r1579 Indef IF (*-1 BOC BARRIER NOT-ADV)(*1 N BARRIER NOT-NPMOD);
	## Ollu amerihkálaččat doivot Oslo Ruoŧa oaivegávpogin.



	# Adv 

REMOVE:r1580 Adv IF (*-1 REAL-COPULAS BARRIER S-BOUNDARY OR CC OR REAL-V)(0 Indef)(NEGATE 1 A)(NEGATE *1 REAL-V BARRIER S-BOUNDARY OR CC);
	## Doppe ledje ollu olbmot.
	## Diet gal lei beare unnán.
 # Indefinite pronouns, not adverbs, if there is no verb other than 'leat'.

REMOVE:r1581 Adv IF (*-1 CC OR CLB BARRIER NOT-ADV)(0 Indef)(NEGATE 0 Loc)(*1 N BARRIER NOT-NPMOD);
	## Dán áššis, nugo eanaš eará áššiin,...

REMOVE:r1583 Adv IF (0 Indef)(1 Interr);
  	## Attášii sutnje buot maid dárbbašit.
  	## Danne lea váttis čohkket barggu buot surggiide oktanaga.
  # Possibly sentences such as: 
  	## Son lea njuoskan buot, mii lea fuones ášši.
  # could be counterexamples, but until now a comma is supposed to be inbetween.
  	#% Dávjá ii boađe ovdan makkár gielladuogáš sis lea.
	
	
	# Particular indefinite pronouns
	# ------------------------------

	# This is an important section. It should first be expanded
	# and then probably generalised according to classes of 
	# indefinite pronouns.

	# buot
	# - - -

#!! * **AttrBuot** IFF-rule
IFF:AttrBuot BUOT + Attr IF (*1 N OR (A Pl) BARRIER NOT-NPMOD);
	#$ Sii meannudit buot velggolaččaid máksingeahpádusohcamiid. 
	#$ Mii addit bálvalusfálaldaga buot álbmogii.
	#$ De fertejit buot vihtta miellahtu oassálastit.
	#$ ahte buot náitalan olbmuin galggai leat guolástanvuoigatvuohta.

#!! * **IndefNom** Selects (Pron Indef Nom) if you are BUOT and first one to the right is PL3-V 	
SELECT:IndefNom (Pron Indef Nom) IF (0 BUOT)(1 PL3-V);
	#$ Vuollásaš ráđit buot heaittihuvvojedje oktasaš hálddahuslaš jođiheapmái. 
	
#!! * **IndefNom2** Selects Indef Nom if you are BUOT and there is no transitive verb to your left or roght in the clause
SELECT:IndefNom2 (Indef Nom) IF (NEGATE *-1 TRANS-V BARRIER REAL-V OR S-BOUNDARY)(0 BUOT)(NEGATE *1 TRANS-V BARRIER S-BOUNDARY LINK 0 MAIN-V OR Inf);
	#$ Buot lea lávkkas.
	
SELECT:r1597 Nom IF (-1 ("gullat" Pl3) LINK -1 Ill)(0 BUOT); # trenges ikke
	## Dat mearkkaša ahte dása gullet buot mat eai leat spiehkastuvvon.		

SELECT:gula Nom IF (0 Acc LINK 0 Sem/Hum OR PROP-ATTR LINK -1 ("gullat") + Imprt + Sg2) ;  
	## - Gula Biret, jeđđe eadni.	


#SELECT:r1598 Indef IF (*-1 TRANS-V BARRIER NOT-ADV-PCLE)(0 BUOT LINK 1 S-BOUNDARY OR REAL-CLB); # trenges ikke
	## Mun borren buot.

#SELECT:r1599 Indef IF (0 BUOT)(*1 TRANS-V BARRIER Acc OR S-BOUNDARY); # trenges ikke
	## Sii fertejit buot earret eatnamiid gohčodit danin.


# eará
# - - -

SELECT:r1601 (Indef Attr) IF (0 ("eará") OR ("iežá"))(1 PrfPrc)(2 N);
	## ...ja eará vuostáiváldojuvvon dokumeanttaid...
	
SELECT ("eará" Gen) IF (-1 ("earret")) ;

# earri
# - - -

REMOVE:r1602 ("earri" N Sg Acc PxSg2) IF (-2 V + Pl3 )(-1 COMMA)(0 ("eará") OR ("iežá"))(1 V + Pl3 );
	## Muhtimat čállet, earát ožžot jorgalan- ja bagadallanveahki dán bargui.

REMOVE:r1603 ("earri" N Sg Acc PxSg2) IF (-1 BUOT OR ("máŋga"));
	## Buot earát.

REMOVE:r1604 ("earri" N Sg Acc PxSg2) IF (NEGATE *-1 Sg2)(*1 V + Pl3 );
	## ... go dat maid earát vásihit.

REMOVE:r1605 ("earri" N Sg Acc PxSg2) IF (1 (Pron Rel Pl));
	## ... ja earát geat háliidit ...

# eatnat
# - - - 
SELECT:r1606 Indef IF (-1 ("giittos"))(0 ("eatnat"));

# juoga
# - - -
SELECT:r1607 (Indef Nom) IF (-1 CLB)(0 ("juoga"))(1 Rel);
	## ..., juoga mii boahtá ovdan Romssa universitehta plánain.

#!! * **miiIndef** it vaikko mii or mii beare
#IFF:miiIndef ("mii" Indef) IF ((-1 ("vaikko")) OR (1 ("beare")))(NEGATE *0 V + Pl1  BARRIER SV-BOUNDARY) ;

IFF:miiPl ("mii" Rel Pl) IF (*-1 (N Pl) BARRIER WORD) ;

# ollu
# - - -
REMOVE:killOLLUAdv OLLU + Adv ;
SELECT:r1608 Pron IF (-2 Pron)(-1 CC)(0 OLLU);
	## Váilos unnán dahje ollu.

SELECT:r1609 Pron IF (-1 ("seammá"))(0 OLLU)(*1 N BARRIER NOT-NPMOD);
	## Jus eanebut leat ožžon seamma ollu jienaid, de mearriduvvo boađus vuorbádemiin.

# seamma/seammá
# - - - - - - - 

SELECT:seammaAdv Adv IF (0 ("seammá"))(1 A OR Adv);
SELECT:r1610 Attr IF (0 ("seammá"))(*1 N BARRIER NOT-NUM);
	## Dasto smiehttatge fas seamma sullasaš jearaldagaid. 
	## Sáhttá leat ulbmillaš, ahte seamma olmmoš áimmahuššá máŋga dain doaimmain.

SELECT:r1611 Attr IF (0 ("seammá"))(1 Adv OR OLLU + Pron)(NEGATE 1 REAL-V OR ("gusto")) ;
	## Mu oainnu mielde sáhttá seammá bures oaidnit dan láhkai, ahte... 
  # The "gusto" here should be removed by a set of sentence adeverbials

	# Then we kill the rest of the attributes...
	# But did we catch enough attributes?  

	#REMOVE Attr IF (0 ("seammá")) ;
	## Seammá gusto poliklinihkalaš divššus. 
  	## Seamma gusto poliklinihkalaš divššus. 

# unnán
# - - -

SELECT:r1612 Indef IF (*-1 COPULAS BARRIER NOT-AUX-V)(0 UNNAN)(NEGATE 1 PrfPrc LINK *1 NP-HEAD-ACC BARRIER NOT-NPMOD);
	## Dat gal lei beare unnán.
  

#!! ## Demonstrative pronouns - should have a look at these



#!! * **DemPlIll** removes Dem Ill and Dem Loc in front of Acc
#REMOVE:DemPlIll Dem (0 (Pers Ill) OR (Pers Loc))(1 (N Acc));
	#$ Muhto boanddat leat goikadan ollu jekkiid ja gilván daidda rásiid.


#!! * **DemSgNom** selects Dem Nom Sg if VFIN Sg3
SELECT:DemSgNom (Dem Sg)  IF (*-1 VFIN + Sg3 BARRIER NOT-ADV-PCLE)(NOT 0 Pers LINK NEGATE *1 N BARRIER NOT-A-ADV OR PL-V)(NEGATE 0 Pl LINK 1 Pl3) ;


#SELECT:DemPl (Dem Pl Acc) IF (0 Pers)(*1 PrfPrc BARRIER NOT-A-ADV LINK 1 (N Pl Acc) - TIME);
	#$ Guovtti dehálaš ja guovddaš rapporttain oaččuimet daid maŋimus divoduvvon veršuvnnaid 12.03.2010, dan beaivvi go mis lei dat maŋimus ovttasbargočoahkkin Finnmárkku fylkkain gos gárviimet min oktasaš evttohusa.

#!! * **DemIndefAttr** selects Dem in front of Indef Attr, no verb to the left
#SELECT:DemIndefAttr Dem (NEGATE -1 REAL-V)(1 (Indef Attr));
	#$ Nuppi geahči dan seammá lávžžis fas biddjojuvvo bađa birra.

#!! * **DemGenSeammas** selects dat Dem Gen in front seammás
#SELECT:DemGenSeammas (Dem Gen) IF (0 ("dat" Sg) LINK 1 ("seammás"));

#!! * **DemSg** removes Dem Sg when there is no Sg N to the right
#REMOVE:DemSg (Dem Sg) IF (0 ("dat" Sg))(NEGATE *1 (N Sg) BARRIER NOT-NP LINK 0 HEAD-N)(NEGATE 1 PrfPrc LINK *1 (N Sg) BARRIER NP-BOUNDARY)(NEGATE 1 (Indef Sg) OR ("seammás")); 
	#$ .. ja dan golut leat govttolaččat veahki ektui.

#!! * **datPersSg3** selects dat Pers Sg3 when there is no N to the right
#SELECT:datPersSg3 ("dat" Pers Sg3)(NEGATE *1 (N Pl) OR (Indef Pl) OR NUM BARRIER NOT-NP)(NEGATE 1 NP-HEAD-GEN LINK 1 (Actio Nom) LINK 1 (N Pl))(NEGATE 0 Dem LINK *1 N BARRIER NOT-NPMOD-ACC) ;
	#$ ...ja ahte dat dáhttu čielgasit berre doahttaluvvot. 
	#$ Harry viežžá báđi návsttis ja dainna vuoššat guliid dolas.
	#$ Dat, ahte Nielsenii eai lean čájehan makkárge hávdesajiid, sáhttá čuovvut máŋgga áššis.
	#$ In dieđe, vaikko vel sáhkohattažan dainna.


#!! * **PersNRel** selects Pers Sg3 when there is a N and a Rel to the right
#REMOVE:PersNRel Pers (0 (Dem Gen))(1 N LINK *1 Rel BARRIER WORD);

#!! * **DemMeasure** removes Dem in front of a Num and MEASURE or NUMUNIT in Ill
#REMOVE:DemMeasure Dem (0 Pers)(1 (Num Gen))(2 MEASURE + Ill OR NUMUNIT + Ill) ; 
	#$ Na gal don dal oaččut dan beannot čuohtái, go juo válddežat.

SELECT:Attr Attr IF (*-1 Neg OR Qst OR ("go" Pcle))(0 (Pron Attr) + Foc/ge)(*1 N - Prop BARRIER NOT-NPMOD); # In use?

LIST MAKKAR = "makkár" "dakkár" "dákkár" "diekkár" ;

SELECT:DemAttr Attr IF (0 MAKKAR LINK *1 N BARRIER NOT-A) ;
	#$ Jáhkát go dakkár balddonasmuitalusaide?

#!! ## Disambiguating adjectives 

#!! ### Rules related to specific adjectives

#!! * jagáš
SELECT:DerDiminA ("jagáš") + Attr IF  (-1 Dem)(0 Der/Dimin LINK *1 N BARRIER NOT-NPMOD) ;
	#$ Dán jagáš mánát gal leat buohkat fitnan Sámis ovdal.

#!! * boaris A or N
SELECT:boarisA A IF (0 ("boaris") LINK -1 TIME-N) ;
	#$ Son lea vihtta sáhpánjagi boaris.

SELECT:Comp Comp IF (0 ("boaris") OR ("nuorra") LINK 1 ("geađgeáigi"r)) ;
	#$ boarrásat geađgeáigi


#!! * dáláš
REMOVE Imprt IF (0 ("dáláš"))(1 N + Nom);
	## Dáláš ortnet ahte dieđut juogaduvvojit soaittáhagas ii leat dohkálaš.
	
#!! * dološ 
SELECT:r1684 (A Attr) IF (0 ("dološ"))(*1 N BARRIER NOT-NPMOD);
	#$ ...go lea gažaldat háhkamis dološ áiggi rájes geavaheami...

#!! * garra N vs. garas A
SELECT:garasAttr1 Attr IF (0 ("garas"))(1 (N Nom)); 
SELECT:garasAttr2 Attr IF (NEGATE *-1 SG3-V)(0 ("garas" A Attr))(1 A OR N)(NEGATE *1 SG3-V)(NEGATE -1 VFIN);
	#$ Norggas lea leamaš gal garra digaštallan sámi oahppoplána hárrái.
	#$ Beatnagat eai biva viššat garra buollašiid.

#!! * nanus
SELECT:nanusA A IF (-1 GRADE-ADV)(0 ("nanus"));
	#$ Dat lea hui nanus. 

#!! * adjective or noun?
SELECT:adj A IF (-1 GRADE-ADV - Foc)(0 N + Sg + Nom);
	#$ Beaivvádagas lei oalle liekkas. 

SELECT:adj A IF (0 N + Sg + Nom LINK *1 Inf BARRIER NOT-ADV-PCLE) (*-1 COPULAS BARRIER NOT-ADV-PCLE) ;
	#$ Rággasa vuolde lea liekkas nohkkat, eaige nu olu čuoikkat.
	
#!! * sierra
SELECT:sierraAAttr (A Attr) IF (0 ("sierra"))(*1 N BARRIER NOT-NPMOD);
	#$ Sámelága vuođul lea ásahuvvon sierra ovddasteaddji orgána.

#!! * surgat
SELECT:surgatA (A Sg Nom) IF (0 ("surgat"))(-1 Interj);
	#$ Hei surgat, lea divrras!

#!! * veara
SELECT:vearaANom (A Nom)  IF (-1 Gen OR ("eanet"))(0 ("veara"));
	#$ De bođii geafes leaska ge gii bijai guokte smávvaruđaža, moatte šlántte veara.
	#$ ...buot dan mii leat ráhkistan ja gudnejahttin veara.

REMOVE:vearaN N IF (0 Der/veara OR ("veara"));
	#$ Buhttenrievttálaččat ii nuppástuvvon mearkkašanveara...
  
#!! * **vulitAttr**
SELECT:vulitAttr Attr IF (0 ("vuolit"))(1 ("gráda") OR ("dássi"));
	#$ Skuvla dušše addá oahpu vuolit dásis. 

#!! * **Comp** rules select Comp A
SELECT:Comp Comp IF (NEGATE -1 ("nu") OR ("seammá") OR CC)(NEGATE 0 (Pl Nom) LINK -1 Comp OR COMPAR LINK *-1 COPULAS BARRIER NOT-ADV-PCLE)(NEGATE 0 VFIN)(1 N LINK *1 go BARRIER NOT-N);
 	#$ Dážat navde Finnmárkku buoret guovlun go iežaset ruovttubáiki.

SELECT:Comp Comp IF (-1 ("mihá"));
SELECT:Comp Comp IF (-1 ("olu"))(NEGATE 0 (Pl Nom) LINK *1 PL3-V BARRIER NOT-ADV-PCLE);
	#$ Nuppe dáfus ledje mihá eanebut geat bivde lagabus gátti.
	#$ ...ja danin lea buot nu olu álkit inuihtaide. 

#!! ### Attribute disambiguation
	
SELECT:AttrTitle REAL-TITLE + Attr IF (0 N)(*1 Sem/Fem OR Sem/Mal OR Sem/Sur BARRIER REAL-WORD-NOT-ABBR);
	#$ Gilette, Mr. Sparrowis lea diibmu harcamuoras ja lea vahát jus boahtá maŋŋit.	
	
#!! * **AttrVFIN** removes Attr in front of VFIN
REMOVE:AttrVFINx Attr - DAT-PRON IF (1 VFIN LINK NOT 0 N OR A OR PrfPrc);
	#$ Lea dehálaš deattuhit, ahte IKT ii doaimma.
 	#$ Seamma guoská geassemánu.
	#$ Dan gal juohkehaš fuobmá dakkaviđe.

SELECT:AttrVFINx (A Sg Nom) IF ((-1 COPULAS + Sg3 LINK -1 NP-HEAD-SG) OR (-1 COPULAS LINK -1 AUX + Sg3 LINK -1 NP-HEAD-SG))(1 MANNU + Gen LINK 1 Ord);	
	#$ CD galgá leat válmmas geassemánu 10. beaivvi 2005.

#!! * **AttrnotNA** removes Attr when no N or A to the right
REMOVE:AttrnotNAx Attr - DAT-PRON IF (NOT 0 Indef OR Num OR ABBR)(NEGATE *1 N OR A OR (\?) OR  PrfPrc BARRIER NOT-NPMODCC)(NEGATE 1 CC LINK *1 (A Attr) OR PrfPrc BARRIER NOT-ADV-PCLE LINK *1 N BARRIER NOT-NPMODCC)(NEGATE 1 PUNCT-LEFT LINK *1 PUNCT-RIGHT LINK *1 N BARRIER NOT-NPMODCC);
   	#$ Dalle sáhttá beaktilit háhkat guovtte ja maiddái golmmagielat (maiddái suomagielat) gelbbolašvuođa.
	#$ Ollu go dus lea ruhta?
	#$ Maŋimus maid Will Turneris oinniimet lei go son vuojui meara bodnái.
	#$ Jos viessu lea dohkálaš, de ráfi orru dan badjel.	


#!! * **AttrnotNA** removes Attr when no N or A to the right
REMOVE:AnomInf Attr IF (0 DEHALAS + Nom LINK 1 Acc LINK 1 TRANS-V LINK 1 Inf)(*-1 BOS BARRIER WORD) ;
	#$ Váttis dievdoolbmuid oažžut bargat dásseárvoáššiiguin.
REMOVE:AnomInf Attr IF (0 DEHALAS + Nom LINK 1 Prop) ;
REMOVE:AnomInf Attr IF (0 DEHALAS + Nom LINK *1 Inf + COPULAS BARRIER SV-BOUNDARY OR AUX) ;
	#$ Dehálaš Sámediggái leat mielde nannemis heargegilvvuid dál.

	
#!! * **ANomILLA** selects Nom when ILL-A	
SELECT:ANomILLA ILL-A + Nom IF (1 Ill);
	#$ Ja danin son lei šaddan nu bahá tálaide ja bustávaide.

#!! ### Rules for Attr between Dem and N

#!! * **AAttrDemSg1, AAttrDemPl1** 
SELECT:AAttrDemSg1 (A Attr)  IF (*-1 (Dem Sg Nom) BARRIER NOT-NPMOD)(*1 (N Sg Nom) BARRIER NOT-NPMOD);
SELECT:AAttrDemPl1 (A Attr) IF  (*-1 (Dem Pl Nom) BARRIER NOT-NPMOD)(*1 (N Pl Nom) BARRIER NOT-NPMOD);
 	#$ Dalle dat geahppaset gurppas daddjojuvvo veajan.
 	#$ ...jus dát máinnašuvvon árbevirolaš dilálašvuođat eai geahččaluvvo čuvgejuvvot.

#!! * **AAttrDemSg2, AAttrDemPl2** 
SELECT:AAttrDemSg2 (A Attr)  IF (*-1 (Dem Sg Acc) BARRIER NOT-NPMOD)(*1 (N Sg Acc) BARRIER NOT-NPMOD);
SELECT:AAttrDemPl2 (A Attr) IF  (*-1 (Dem Pl Acc) BARRIER NOT-NPMOD)(*1 (N Pl Acc) BARRIER NOT-NPMOD);
 	#$ ...ja son doaimmaha dán dábálaš dahje eanaš geavaheami.
 	#$ Nuppe beales geahččalit dahkat dáid siskkáldas áddejumiid duođaštanfáddán.

#!! * **AAttrDemSg3, AAttrDemPl3** 
SELECT:AAttrDemSg3 (A Attr) IF  (*-1 (Dem Sg Gen) BARRIER NOT-NPMOD)(*1 (N Sg Gen) BARRIER NOT-NPMOD);
SELECT:AAttrDemPl3 (A Attr)  IF (*-1 (Dem Pl Gen) BARRIER NOT-NPMOD)(*1 (N Pl Gen) BARRIER NOT-NPMOD);
 	#$ Dán konkrehta háve gustojedje gáržžideamit nappo earáide go boazosápmelaččaide. 
 	#$ No cases left after the Dem Pl Acc-Attr-N Pl Acc rule? 

#!! * **AAttrDemSgIll, AAttrDemPlIll** 
SELECT:AAttrDemSgIll (A Attr) IF  (*-1 (Dem Sg Ill Attr) BARRIER NOT-NPMOD)(*1 (N Sg Ill) BARRIER NOT-NPMOD);
SELECT:AAttrDemPlIll (A Attr)  IF (*-1 (Dem Pl Ill) BARRIER NOT-NPMOD)(*1 (N Pl Ill) BARRIER NOT-NPMOD);
 	#$ Dás lei maiddái gažaldat geavahanvuoigatvuođas vuovdái ja dan vejolaš viidodahkii. 
 	#$ ...mat gullet dáidda ja eará lunddolaš sullasaš doaimmaide. 

#!! * **AAttrDemSgLoc, AAttrDemPlLoc** 
SELECT:AAttrDemSgLoc (A Attr)  IF (*-1 (Dem Sg Loc Attr) BARRIER NOT-NPMOD)(*1 (N Sg Loc) BARRIER NOT-NPMOD);
SELECT:AAttrDemPlLoc (A Attr)  IF (*-1 (Dem Pl Loc) BARRIER NOT-NPMOD)(*1 (N Pl Loc) BARRIER NOT-NPMOD);
 	#$ Sápmelaččat ledje vuosttasin dahje okta dain vuosttaš álbmot-joavkkuin,... 
 	#$ ...dainna váilevaš vejolašvuođain...

#!! * **AAttrDemComPl** 
SELECT:AAttrDemComPl (A Attr) IF  (*-1 (Dem Pl Com) BARRIER NOT-NPMOD)(*1 (N Pl Com) BARRIER NOT-NPMOD);
 	#$ Geainnuin livččii eambbo turistajohtolat suohkana čoahkkeássanguovddážiid čađa daiguin ekonomalaš oalgeváikkuhusaiguin.

#!! * **AAttrDemdakkar** 
SELECT:AAttrDemdakkar (A Attr)  IF (*-1 MAKKAR BARRIER NOT-NPMOD)(*1 N BARRIER NOT-A-CC)(NEGATE 0 N LINK *1 VFIN BARRIER NOT-ADV)(NEGATE 0 Gen LINK -1 (Dem Gen)) ;
 	#$ Mo Alimusriekti meannudii vaikkoba dakkár vuolit dási olbmuid vihtančilgehusaiguin?
	#$ Muhto dakkár radikála rievdadus eahpitkeahttá buktá garra reakšuvnnaid.
	#% Sii lohket iežaset leat oalle duhtavaččat dán jagáš básárdoaluin.

#!! ### Other attribute rules 

#!! * Not attribute in front of Ess: dovddus sánálaš nissonin
SELECT:dovddusEss (A Nom) IF (0 ("dovddus") OR ("beakkan") OR ("deaŧalaš"))(1 Attr OR Gen LINK *1 (N Ess) BARRIER S-BOUNDARY OR REAL-V)(NEGATE 0 Attr LINK 1 Nom LINK 1 Ess) ;
	#$ Son lea dovddus sánálaš ja girjjálaš nissonin.
	#$ Dovddus juoigi váldoartistan.

SELECT:ANomCop (A Nom) IF (0 DEHALAS + Attr LINK 1 Sem/Hum + Gen LINK 1 N + Ill) ;
SELECT:ANomCop (A Nom) IF (0 DEHALAS + Attr LINK 1 ("ollis")) ;
	#$ Mánáidgárdi - deaŧalaš mánáid giellaovdánahttimii.

SELECT:accANom A + Nom + Pl (-1 Pron + PL-PRON + Acc LINK -1 DADJAT) ;	
SELECT:accANom A + Nom + Pl (-1 Pron + DU-PRON + Acc LINK -1 DADJAT) ;	
SELECT:accANom A + Nom + Sg (-1 Pron + SG-PRON + Acc LINK -1 DADJAT)(NOT 1 Sem/Txt OR Sem/Org LINK 0 Ill OR Loc) ;	
	#$ Buvttadeaddji Elle Mari Dunfjell Oskal lohká sin duđavaččat doaluiguin.
	#% Ella Marie Hætta Isaksen almmuhii su sosiála medias ahte son neaktá Estera, mii lea ge váldorolla  «Ellos Eatnu» filmmas.

	


SELECT:ANomCop (A Nom) IF (*-1 COPULAS BARRIER NOT-ADV LINK *-1 BOC BARRIER Nom)(0 DEHALAS + Attr LINK 1 N - Nom - Gen LINK NEGATE *1 REAL-V BARRIER S-BOUNDARY)(NEGATE 0 Attr) ;
	#$ Lea suohtas kurssas.
SELECT:ANomCop (A Nom) IF (*-1 COPULAS BARRIER NOT-ADV LINK *-1 BOS BARRIER Nom)(0 DEHALAS + Attr LINK 1 N + Gen LINK 1 Ill) ;
	#$ Lea dárbbašlaš sámegiela boahtteáigái ahte sámegielagiid lohku lassána.
SELECT:ANomCop (A Nom) IF (*-1 COPULAS BARRIER NOT-ADV)(0 Superl + Attr LINK 1 Ill) ;
SELECT:ANomCop (A Nom) IF (*-1 Dem + Sg BARRIER NOT-ADV LINK -1 COPULAS)(0 Superl + Attr LINK 1 Ill) ;
	#$ Go Johan Áilu maid devddii daid eará eavttuid, de han son maid lei buoremus virgái.
	#$ Go Johan Áilu maid devddii daid eará eavttuid, de han son maid lei dat buoremus virgái.
SELECT:Dem Sg IF (0 Dem + Nom LINK 1 Superl OR ("áidna") LINK NEGATE *1 Pl + Nom) ;


#!! * **AAttrN** no copulas close to the left
SELECT:AAttrN (A Attr) IF (NEGATE *-1 COPULAS BARRIER NP-HEAD-NOM OR NOT-AUX-V)(NEGATE 0 Num OR Inf OR Comp OR ("nubbi") OR V) (NEGATE 0 Adv LINK 1 Gen LINK 1 N)(NEGATE 0 Adv LINK 1C (N Gen))((1 N OR A LINK NOT 0 TIME - Ess OR PLACE - Ess) OR (1 Num LINK 1 N LINK NOT 0 TIME OR PLACE))(NEGATE 1 VFIN OR Inf OR PREGEN OR Prop)(NEGATE 0 (N Sg Nom) LINK *-1 SG3-V BARRIER NOT-NPMODADV LINK NEGATE *-1 NP-HEAD-NOM BARRIER S-BOUNDARY)(NEGATE *-1 ("go") BARRIER NOT-NP LINK NOT -1 VFIN LINK NOT 1 EOS)(NEGATE 0 Gen LINK -1 (Dem Gen));
	#$ Oainnán ollu olbmuid.
	#$ Boahtte golbma vuoruhansuorggi leat suorggit ...
	#$ Gonagas sáhttá mearridit dábálaš njuolggadusaid báikkálaš dilálašvuođaid ektui.
	#$ Sis-Finnmárkkus lea vuolit gaskamearálaš bruttoboahtu go fylkkas...  # gaskamearálaš
	#$ ...go galgá válljejuvvot gaskal vejolaš ja dohkálaš iskkadan- ja dikšunvugiid. # dohkálaš
	#$ Olbmot geat ellet náitoslágan dahje guimmešvuođalágan gaskavuođain. # guimmešvuođalágan
	#$ Máttimus sápmelaččat eahpidedje dáža dutkiid eanet go davit sápmelaččat.
	#$ Golbma čáppa, doaimmalaš nieidda livžo áhčiset riegádanbeaivve.
	#$ Dan dihte ohcat guokte lassi stipendiáhttavirggi sámi/eamiálbmotguoskevaš dutkamiidda.
	#$ Olles doaibmaviessu lea sullii 2300 njealjehasmehtera stuoru. 
	#$ Lea erenoamáš dehálaš movttiidahttit sámegielat doaktáriid bargat gielddain.
	#$ Olggosaddin sáhttá liikká dáhpáhuvvat jos gávdnojit dasa beaktilis sivat.
	#$ Sámedikki vuosttamuš dábálaš dievasčoahkkima maŋŋá... 
	#$ Oktasaš vuoigatvuođat ledje šaddan dološ áiggi rájes...
	#$ ...galget divššohasa lagamus oapmahaččat oažžut dieđu...
	#% Sii lohket iežaset leat oalle duhtavaččat dán jagáš básárdoaluin.

#!! * **AAttrCop**  copulas close to the left
SELECT:AAttrCop (A Attr) IF (*-1 COPULAS BARRIER NOT-NPMODADV)(NEGATE 0 Adv LINK 1 A) 
(NEGATE 0 (A Pl) LINK *-1 COPULAS LINK -1 Pl OR Pl1 OR Pl2 OR Pl3)
(1 A OR N LINK NOT 0 Superl OR Ord)
(NEGATE 1 Gen LINK *1 Po OR Loc OR Inf BARRIER NP-BOUNDARY OR Indef)
(NEGATE 1 Adv OR REAL-V OR ADVLCASE)(*2 go OR EOC OR Neg BARRIER NOT-NP)
(NEGATE 0 Pl + Nom LINK -1 COPULAS + Pl3 OR COPULAS + Du3) ;
	#$ Sis-Finnmárkkus lea vuolit gaskamearálaš bruttoboahtu go fylkkas...  # vuolit
	#$ Interneahtta lea máilmmiviidosaš oktavuohta dihtoriid gaskkas.
	#$ Dieđuid diehtogieđahallama ovddasvástideaddjin leat guovlluguovdasaš dearvvašvuođadoaimmat.f6513
	#$ Dán áiggi sii johtet biillaiguin, jos headjavissui lea guhkit mátki.
	#$ Sámegiella ja dárogiella leat ovttaárvosaš gielat. 
	#% Fiettar orohaga jođiheaddji Ann Catharina Lango ja orohatolmmoš Nils Mathis Sara leaba duhtavaččat oaiviliskkadeami loguiguin.



#!! * **AttrPlacelaš** This rule selects Sem/Plc Der/lasj A Attr in front of Prop or N
SELECT:XAttrPlacelašr1718 (Sem/Plc Der/lasj A Attr) IF (1 Sem/Fem OR Sem/Mal OR Sem/Sur OR N);
	#$ Oslolaš Jostein Johnsen šattai Eurohpameašttirin skohtergilvvus.
	#$ Oslolaš gánda šattai Eurohpameašttirin skohtergilvvus.


#!! * **AttrCord** 
SELECT:AAttrCord A + Attr IF (-1 COMMA LINK -1C A + Attr)(1 N) ; 
	#$ Sihke ohppiide ja váhnemiidda lei skuvla sosiálalaš, oktasaš báiki.

  
REMOVE:Xr1719 VFIN IF (NEGATE *-2 Rel OR CS BARRIER S-BOUNDARY)(-1C VFIN)(NEGATE 0 ConNeg); 
	#$ Mun áiggun oastit biilla.
	#$ Gos dáppe lea máddi?
	#$ Ánne-Sofe buvttii varas biergguid.
	#$ Máhtte manai čuoigga skuvlii.
	#$ Dan dihte go lea čađat dárbu ođasmahttit jorgalusaid.
	#$ Tommi geahčai skuvlii.
 	#$ Bargu lei árvvoštallat dan.
 # As we see, this rule can lead to a verbal, a nominal, an adjectival or an
 # adverbial reading. It comes here because this is where it is first needed.
	
#!! * **AdvManimus** 
SELECT:AdvManimus Sem/Plc + Adv IF (NEGATE *0 VFIN)(-1 N)(1 EOS) ;
	#$ NSR-Henrik bajimusas.
		
REMOVE:AdvManimus Adv IF (NEGATE *-1 REAL-V BARRIER S-BOUNDARY)(0 A LINK 1 Rel)(NEGATE 1C A OR Dem)(NEGATE *1 REAL-V BARRIER S-BOUNDARY); # This rule makes sense to this sentence.
	#$ Maŋimus maid Will Turneris oinniimet lei go son vuojui meara bodnái.
	#% Unnimusat 90 oahppočuoggá ovtta fágasuorggis.


#!! * **Advovdalaš** 
SELECT:Advovdalaš Adv IF (0 ("ovdalaš") LINK 1 Num) ;
	#$ ovdalaš njealji


#!! * **AttrIllCop** 
SELECT:AttrIllCop Attr IF (-1 (N Nom) LINK *-1 COPULAS LINK -1 HUMAN + Ill OR (Pers Ill))(0 A)(1 (Nom)) ;	
	#$ Sidjiide lei maid liegga Ruija geasuheaddji guovlu.

#!! * **AttrAdv** 
SELECT:AttrAdv (A Attr) IF (-1 Adv LINK NOT 0 GRADE-ADV)(NEGATE 0 Inf OR VFIN OR Cmp/SplitR)(*1C N BARRIER NOT-A)(NEGATE 1 CLB LINK 1 N)(NEGATE 1 Prop)(NEGATE 0 (A Pl) LINK *-1 PL-V OR DU-V BARRIER NOT-ADV-PCLE);
	#$ Mánná viehkalii olggos álás julggiid.
	#$ Universitehtas lea maiddái čielga nášunála ovddasvástádus...
	#$ Dalle sáhttá beaktilit háhkat guovtte ja maiddái golmmagielat (maiddái suomagielat) gelbbolašvuođa.Gonagas sáhttá mearridit dábálaš njuolggadusaid báikkálaš dilálašvuođaid ektui.

REMOVE:AttrVejolaš ("vejolaš" Attr)  IF (*-1 COPULAS)(1 REAL-V) ;
#!! * **Cop** 
SELECT:CopANom (A Nom) (-1 COPULAS LINK -1 HUMAN + Nom OR Pers + Nom)(0 Attr)(NEGATE 1 Nom OR Loc OR Gen OR Cmp/SplitR) ;	
	#$ Son lei suollemas dainna doaivvuin ahte galgá leat nu..

SELECT:AttrAdv Adv IF (0 GRADE-ADV LINK 1 Adv);

REMOVE:attrcom Attr (0 COM-A + Nom LINK 1 Com)(NEGATE 0 ("ollis") OR ("oppa")) ;
	#$ Sara advokáhta Trond Biti lea hui duhtavaš bohtosiin.
 
#!! * **ANom**  removes A Nom
REMOVE:ANom (A Nom) IF (NEGATE -1 COPULAS LINK 1 DEHALAS LINK *2 ADVLCASE - Ess BARRIER NOT-NPMOD)(0 (A Attr))(NEGATE 0 (Pl Nom) LINK *-1 PL-V OR DU-V BARRIER NOT-ADV-PCLE OR CS LINK NEGATE *-1 (Pl Nom) BARRIER S-BOUNDARY)(NEGATE 0 (Pl Nom) LINK *-1 COPULAS BARRIER NOT-ADV-PCLE LINK 0 Pl3)(NEGATE *1 NP-HEAD-NOM OR Ess BARRIER NOT-A-ADV)(NEGATE *-1 (Refl Acc) BARRIER NOT-ADV)(*1C N OR Cmp/SplitR BARRIER NOT-NPMOD OR (PUNCT RIGHT) OR CC LINK NOT 1 Po OR PREGEN)(NEGATE 1 Prop OR Ill)(NEGATE 1 COMMA LINK 1 N)(NEGATE 1 Gen LINK *1 Po OR PREGEN BARRIER NP-BOUNDARY)(NEGATE 0 Pl LINK *-1 Pl3 BARRIER VFIN)(NEGATE -1 COPULAS LINK 2 Com) ;
	#$ Dat lea čuovvovaš sajis.
	#$ Ánne-Sofe buvttii varas biergguid.
	#$ Mun čállen romit reivve go don.	
	#$ Vuoigatvuohta leat Sámedikkiid jienastusloguin lea lassin dábálaš jienastanvuoigatvuođa eavttuide.
	#% Dálut eai lean nu ráhpadat ođđasishuksenáigge.
	#% Suoma Sámedikki presideanta, Tiina Sanila-Aikio lea duhtavaš konvenšuvnnain.

REMOVE:AifProf (A Attr) IF (0 PROFESSION LINK 1 PROP-SUR) ;
	#$ Várrelahttu: Ráđđeaddi Berit Ellen J. Eira

REMOVE:ASup A + Attr IF (0 Superl LINK 1 Sem/Plc + Loc) ;
	#$ Inger Alette gávccádin buoremus Norggas


#!! * **AAttr**  selects A Attr
SELECT:AAttr (A Attr) - ("nubbi") IF (NOT 0 (Ex/V A) OR Adv - Superl OR Inf)(NEGATE 0 PrfPrc OR DEHALAS LINK *-1 REAL-COPULAS BARRIER REAL-V)(NEGATE 1 PREGEN)(NEGATE *-1 COPULAS BARRIER NOT-A-ADV-PCLE) (*1 N - Ess BARRIER NOT-NPMOD OR Dem OR Indef OR Num LINK NOT 1 Po)(NEGATE 0 Gen LINK -1 (Dem Gen))(NEGATE 1 CC LINK 1 Num OR N)(NEGATE 0 (Pl Nom) LINK 1 VFIN + Pl3) (NEGATE 0 (Neg Sup) LINK 1 Inf)(NEGATE 0 N LINK 1 REAL-V)(NEGATE 0 DEHALAS LINK *1 Inf BARRIER REAL-V)(NOT 1 Prop) ;
	#$ Máttimus sápmelaččat eahpidedje dáža dutkiid eanet go davit sápmelaččat. # davit
	#$ ...main leat mielde dušše priváhta oasálaččat, ii leat...
	#$ Sámedikki ásaheapmi lea eanet ideologalaš go čielga mearrádus. # čielga
	#$ Lávži goallostuvvo dan ovddit noađđehearggi čeabehii.
	#$ Dábálaččat earuha gaskal dábálaš (oppalaš) geavahanvuoigatvuođaid ja sierranas (partiála) geavahanvuoigatvuođaid.
	#$ Váillui guhkit áiggi heivvolaš oaivil. 
	#% lea dehálaš vuovdingálvun
	#% Dán girjji vuođul ja ságastallamiiguin lullisámiin lea munnje báhcán millii ahte internáhttadoaibma ii lean gal alla dásis, muhto internáhttaskuvlii dattetge bohte viššalit lullisámi bearrašat.
	#% Konferánsa atná hui dárbbašlážžan ahte buot sámi mánát ja nuorat buot golmma davviriikkas besset oahppat ovddidit eatnigielaset.
	#% Oahpaheaddji šattai geavahit buot čehppodagas, amas massit vátna oahppiidis.
	#% Dasgo stáhtaossodagat mearrašuvvet oahppiidlogu mielde, de fertešii logahagas leat dás ovddosge 35-40 oahppovázzi, amas bajásdoallan šatta rásehit gieldda ekonomiija.

LIST TYP-ADV = "garrasit" "čielgasit" ;

#!! * **ASuperlAttr**  selects A Superl Attr
SELECT:ASuperlAttr (A Superl Attr) IF (1 Num LINK 1 N) ;
	#$ Son lea leamaš oahpaheaddji maŋimuš 15-20 jagi.


SELECT:AAttrNotAdv (A Attr) - ("nubbi") IF (0 Adv - Superl OR Inf LINK NOT 0 TYP-ADV)(-1 VFIN - COPULAS)(1 N)(NEGATE 0 Comp LINK 1 Gen OR Attr ) ;
SELECT:TYPAdv TYP-ADV ((-1 REAL-V - COPULAS) OR (-2 REAL-V  - COPULAS) OR (1 REAL-V  - COPULAS)) ;
	#$ Dattetge lea dárbu dovddahit dan čielgasit láhkaásahusain.

SELECT:OrdAttr (Ord Attr)  - ("nubbi") IF (1 N) ;
	#$ Son lea viđát luohkás.


#!! * **AdvN** removes Adv   
REMOVE:AdvN Adv IF (NEGATE -2 NOT-AUX-V)(-1 Gen OR Dem OR Indef LINK NEGATE -1 PrfPrc)(0 Attr LINK 1 N);
	#$ Muhto dán geasi gal áiggun bargat vai dinen veháš ruđaid.

#!! * **AAttrPunct**    
SELECT:AAttrPunct (A Attr)  IF (1 PUNCT LINK *1 Attr BARRIER NOT-ADV LINK *1 N BARRIER WORD) ;
	#$ Dalle sáhttá beaktilit háhkat guovtte ja maiddái golmmagielat (maiddái suomagielat) gelbbolašvuođa.Gonagas sáhttá mearridit dábálaš njuolggadusaid báikkálaš dilálašvuođaid ektui.

#!! * **AAttrgoAAttr**    
SELECT:AAttrgoAAttr (A Attr) IF (-1 COMPAR)(1 go)(2 (A Attr) LINK NOT 0 Nom)(*3 N BARRIER NOT-A);
	#$ Sámedikki ásaheapmi lea eanet ideologalaš go čielga mearrádus. # ideologalaš

#!! * **AttrTIME**    bad rule
#SELECT:AttrTIME (A Attr) IF (NEGATE 0 Num OR PrfPrc)(NEGATE 0 Adv LINK *-1 REAL-V BARRIER NOT-ADV-PCLE)(NEGATE 0 Pl LINK *-1 Pl3 BARRIER VFIN)(*1 TIME BARRIER WORD - Num);  
	#$ Guovdageaidnu čájehii fámuid vuosttaš minuvtta rájes.
	#$ Ollu jagiid mielde lei dáhpin šaddan ahte stuorra gánddat bohte min geahčái lávvardaga ja sotnabeaivve go radios ledje sáddagat skeittástan- ja čuoigangilvvuin.	
	#% Dálut eai lean nu ráhpadat ođđasishuksenáigge
 
#!! * **AAttrCoord1** coordination, first part
SELECT:AAttrCoord1 (A Attr) IF (1 CC OR COMMA LINK *1 (A Attr) OR ("nubbi") OR PrfPrc OR PrsPrc BARRIER NOT-ADV-PCLE LINK 1 N)(NEGATE *-1 ("leat") BARRIER NOT-ADV-PCLE LINK *3 Attr OR PrfPrc LINK NOT 1 Nom)(NEGATE 0 Der/A OR ("dávjjit")) ;
	#$ Duodji lei deaŧalaš sivva manne olbmot Gáivuonas birgejedje nu bures váttes jagiid vuosttaš ja nuppi máilbmesoađi gaskkas.
	#$ ...go galgá válljejuvvot gaskal vejolaš ja dohkálaš iskkadan- ja dikšunvugiid. # vejolaš
	#$ Olbmot geat ellet náitoslágan dahje guimmešvuođalágan gaskavuođain. # náitoslágan
	#$ ...go viidáset gaskkusta oppalaččat álbmoga historjjálaš, kultuvrralaš ja politihkalaš dieđuid. # kultuvrralaš
 	#$ Ráđđehus hálida erenoamažit buktit ovdan ILO-konvenšuvnna nr. 169 eamiálbmogiid ja čearddaid birra iehčanas stáhtain, jagi 1966 ON-konvenšuvnna siviila ja politihkalaš vuoigatvuođaid birra artihkal 27,...
	#% 1613 rájes ledje rittut ja vuonat ja lagamus eatnamat siskelis, visot Danmárkku-Norgga vuolde.

#!! * **AAttrCoord2** coordination, first part
SELECT:AAttrCoord2 (A Attr) IF (NEGATE 0 Prop)(1 COMMA LINK 1 Attr LINK 1 JA/DAHJE OR ("vai") LINK *1 HEAD-N BARRIER NOT-NPMODADV);
	#$ ...go viidáset gaskkusta oppalaččat álbmoga historjjálaš, kultuvrralaš ja politihkalaš dieđuid. # historjjálaš

#!! * **AAttrCoord2** coordination, second part
SELECT:AAttrCoord3 (A Attr) IF (NEGATE 0 Prop)(-1C JA/DAHJE OR ("vai") LINK -1C Attr ) ;
	#$ Leat go Suomas eanet dahje unnit ássit go Norggas?



#!! * **PrfPrcCoordA** selects PrfPrc in coordination with an A
SELECT:PrfPrcCoordA PrfPrc IF (*-1 JA/DAHJE OR ("vai") OR COMMA BARRIER NOT-ADV LINK -1 A);
	#$ Dilálašvuođaid álgu sáhttá leat dovdameahttun dahje jávkan historjjá seavdnjatvuhtii.
	#$ Johtalus rabas dahje galbmon mearas lea muddejuvvon. # galbmon

#!! * **ACoordPrfPrc** selects A itn coordination with PrfPrc 
SELECT:ACoordPrfPrc (A Attr) IF (NEGATE -1 ("leat"))(1 JA/DAHJE OR ("vai") OR COMMA)(*2 PrfPrc BARRIER NOT-ADV)(*3 N BARRIER NOT-NPMOD);
	#$ Johtalus rabas dahje galbmon mearas lea muddejuvvon. # rabas

#!! * **AAttrContra** selects A itn coordination with PrfPrc 
SELECT:AAttrContra (A Attr) IF (*-1 Neg BARRIER S-BOUNDARY OR VFIN)(*1 CONTRA BARRIER WORD LINK 1C Attr);
	#$ ILO-konvenšuvdna nr 169 ii vuođđuduva formála, muhto funktionála oamastanvuoigatvuođa doahpagii.

IFF:sapmelas ("sápmelaš") + A IF (0 Attr LINK 1 N LINK NOT 0 VFIN) ;	

	#$ Dušše inuihtat ja sápmelaččat sáhttet adnojuvvot Davviriikkaid eamiálbmogin. 

#!! ### Special rules for 'buorre' (the only adjective showing case agreement)

REMOVE Attr IF (0 ("buorre") OR Dem OR ("nubbi")) (1 Num LINK *1 (Sg Com) BARRIER NOT-NPMOD LINK 0 N) ;

SELECT:SeCaseAgrBuorre $$CASE-AGREEMENT + A IF (0 ("buorre") OR OKTA OR Dem LINK NOT 0 Superl OR Comp) (*1 $$CASE-AGREEMENT BARRIER NOT-NPMOD LINK 0 N)(NEGATE -1 Nom LINK NOT 0 Gen) ;
SELECT:SeComAgrBuorre Com IF (0 ("buorre") OR OKTA OR Dem OR ("nubbi")) (0 Sg LINK *1 (Sg Com) BARRIER NOT-NPMOD LINK 0 N)(NEGATE -1 Gen LINK -1 OKTA)(NEGATE -1 LOC-V) ;
SELECT:SeLocAgrBuorre (Pl Loc) IF (0 ("buorre") OR OKTA OR Dem OR ("nubbi")) (*1 (Pl Loc) BARRIER NOT-NPMOD LINK 0 N) ;
	## Son dadjá dan nuppiin sániin.
	#$ Báhppa fertii dávjá konfirmeret sin dáiguin unnán dieđuiguin mat sis ledje.
	#$ Givssideapmi sáhttá boahtán das, go earránin nuppiin mánáin ja nuorain.

SELECT:SeCaseHalfagrBuorre (A Gen) IF (0 ("buorre") + Sg OR OKTA OR Dem) (*1 CASE-HALFAGREEMENT + N BARRIER NOT-NPMOD);

SELECT:ovttaAgreem OKTA + Attr IF (1 N + Sg + Loc OR N + Sg + Ill);


SECTION

#!!	### Comparatives V Pl3
#!! This block of rules is there to ensure case agreement for comparatives.

#!! * Select Pl Nom if V Pl3
SELECT:PlNomifVPl3 (N Pl Nom) IF (NEGATE -1 DADE OR MADE)(0 Comp LINK *1 V + Pl3  BARRIER VFIN OR S-BOUNDARY);	
	#$ Lea buoret borramuš go dat maid nuorat dán áigge njillet.

#!! * Remove Nom, Acc and Gen if Comp
REMOVE:GenifComp Gen IF (0 Comp)(*1 HEAD-N BARRIER NOT-NPMOD LINK 0 NOT-GEN); 
	#$ Sámedikki doarjjastivra lea doallan čoahkkima Ákšovuonas árat dán mánus. # árat
REMOVE:AccifComp Acc IF (0 A + Comp LINK NOT 0 EAMBBO)(*1 HEAD-N BARRIER NOT-NPMOD LINK 0 NOT-ACC);
	#$ Ja ožžot eanet buhtaduvvot jávkan elliid ektui go boazodoallu vaikko eai leat luođus guhton eambbo snoagga ¼ oasi jagis, goit dáppe davvin. # eambbo

REMOVE:NomifComp Nom IF (0 Comp)(*1 HEAD-N BARRIER NOT-NPMOD OR PUNCT LINK 0 NOT-NOM LINK NOT 0 Inf)(NEGATE 1 COMMA OR CC)(NEGATE 0 (Pl Nom) LINK *-1 PL-V OR DU-V BARRIER NOT-NPMODADVIIP)(NEGATE 0 (Pl Nom) LINK -1 CC LINK -1 (Pl Nom) LINK *-1 PL-V OR DU-V BARRIER NOT-NPMODADVIIP)(NEGATE 0 (Pl Nom) LINK *-1 ConNeg OR PrfPrc OR (Actio Ess) BARRIER NOT-NPMODADVIIP LINK -1 PL-V OR DU-V)(NEGATE 0 (Pl Nom) LINK *1 V + Pl3  BARRIER NOT-NPMODADV)(NEGATE 0 (Sg Nom) LINK *-1 SG3-V BARRIER NOT-NPMODADV)(NEGATE 1 Gen LINK *1 Po OR PREGEN BARRIER NP-BOUNDARY)(NEGATE 1 Dem);
	#$ Finnmárkkuopmodat sáhttá dárkileappot albmaduvvon guovlluide mearridit gáržžádusaid beassat ávkkástallat ođasmuvvi valljodagain nugo namahuvvo vuosttas lađđasis, juos deasta valljodagas gáibida dan. # dárkileappot
	#$ Finnmárkkus ledje ovdal sihke doaktárat ja buohccedivššárat dábálaččat Lulli-Norggas eret, ja máŋggas ledje dušše leamaš moadde jagi dahje vel oanibut davvin. # oanibut
	## Álggahit eará doaibmabijuid gáibida baicca lassi árjja...  #  álggahit
	#% Go dii lehpet šiegat ealibiin.
	#% Soai vácciiga seamma luohká go Liná, ja leigga čeahpit eanas fágain.
	#% Buvttadeaddji Elle Mari Dunfjell Oskal lohká sin duđavaččat doaluiguin.
	
#!! ### alit vs. allat Comp Attr

LIST ALLAT = "boazolohku" "dáiddaoahpahus" "dássi" "divišuvdna" "dutkan" "eallindássi" "gealdda" "gráda" "iešmássa" "lohku" "oahppa" "oahppolágádus" "oahppu" "oahpahus" "oahppat" "riekti" "sápmi" "lávdaoahppofálaldat" "virgi" ;

#!! * **allat** in front of ALLAT OR MONEY OR EDUCATION OR go
SELECT:allat ("allat") IF (0 ("alit"))(1 ALLAT OR MONEY OR EDUCATION OR go);
	#$ Finnmárkkus ássi olbmuid divat ii galgga leat alit go guovtti dan mađe go gielddas ássi oblmuid divat. #PrsPrc needs a mapping rule
	#$ Dán njuolggadusa deaŧalaš eaktu lea ahte ođđa siidaosiid ásaheapmi ii galgga dagahit ahte boazolohku šaddá alit go siidii mearriduvvon lohku, geahča dás maŋŋelis.
SELECT:allatMeasure ("allat") IF (-3 ("badjel") OR ("vuollil"))(-2 Num)(-1 MEASURE)(0 ("alit"));
	
#!! * **alitColour** in coordination with COLOUR
SELECT:alitColour ("alit") IF (0 ("allat"))(*0 COLOUR OR (".*ivdni"r) BARRIER NOT-CRD);
	#$ Fiskes, rukses, alit, ruoná ha muđuid buotlágaš ivnnit.

#!! * **alitN** in front of VEHICLE, CLOTHES, BEDCLOTHES, BUILDING and more
SELECT:alitN ("alit") IF (0 ("allat"))(*1 ("Volvo") OR VEHICLE OR ("čalbmi") OR ("dielku") OR ("russa") OR ("čuovga") OR ("albmi") OR ("ivdni") OR ("fearga") OR ("násti") OR ("muorji") OR ("lieđđi") OR ("girji") OR ("loddi") OR CLOTHES OR BEDCLOTHES OR BUILDING OR ("guovddáš") BARRIER WORD - Attr );
	#$ Min bussás leat alit čalmmit.

#!! * **alitEOS** in the end of a sentence
SELECT:alitEOS ("alit") IF (-1 go OR Qst)(0 ("allat" Nom))(1 EOS);
 
#!!	### Comparative or A Pl 

#!! * **APlNomafterCop** selects A Pl Nom after copulas and Pl Nom OR Pl Pron
SELECT:WAPlNomafterCop (A Pl Nom) IF (*-1 COPULAS + Inf BARRIER NOT-ADV-PCLE LINK -1 PL-PRON + Refl + Acc)(NEGATE 0 N)(NEGATE 1 A)(NEGATE 1 Adv LINK 1 A);
SELECT:WAPlNomafterCop (A Pl Nom) IF (*-1 COPULAS OR ("fas") BARRIER NOT-ADV-PCLE LINK *-1 (N Pl Nom) BARRIER S-BOUNDARY OR (Sg Nom))(NEGATE 0 N)(NEGATE 1 A)(NEGATE 1 Adv LINK 1 A);
SELECT:WAPlNomafterCop (A Pl Nom) IF (*-1 COPULAS + Du3 BARRIER NOT-ADV-PCLE) (NEGATE 0 N)(NEGATE 1 A)(NEGATE 1 Adv LINK 1 A);
	#$ ...ja dan golut leat govttolaččat veahki ektui.
	#$ ...jus dat leat čuožžovaččat ja dárbbašlaččat.
	#$ Riekteháhkama eavttut geavahusa njuolggadusaid mielde eai oro absoluhtat.
	#$ Muhtimat ledje stuorábut go earát, já muhtimat fas unnibut: muhtimat ledje viššalat.
	#$ Biret-guovttos Juffáin leaba čeahpit duddjot.

#!! * **APlNomafterCop2** selects A Pl Nom after copulas and Pl Nom OR Pl Pron
SELECT:WAPlNomafterCop2 (A Pl Nom) IF (*-1 ("leat") + Inf OR ("leat") + PrfPrc BARRIER NOT-ADV-PCLE LINK -1 V + Pl3 )(NEGATE 0 N)(NEGATE 1 A)(NEGATE 1 Adv LINK 1 A);
	#$ Álgoálbmogat ellet hui iešguđetlágán eavttuiguin ja sáhttet leat riggát dehe geafit.

#!! * **APlNomafterDu** selects A Pl Nom after copulas and Du
SELECT:WAPlNomafterDu (A Pl Nom) IF (*-1 COPULAS BARRIER NOT-ADV-PCLE LINK *-1 (Du1 Nom) OR (Du2 Nom) OR (Du3 Nom) BARRIER S-BOUNDARY)(NEGATE 0 N)(NEGATE 1 A)(NEGATE 1 Adv LINK 1 A);
	#$ Vuoi, doai leahppi nu čeahpit!

#!! * **ASgNomNoSubj** selects A Sg Nom after copulas Sg3 or Neg Sg3
SELECT:WASgNomNoSubj (A Sg Nom) IF ((-1 COMMA LINK NEGATE *2 VFIN) OR (*-1 ("leat") + Sg3 BARRIER NOT-ADV-PCLE) OR (*-1 ("leat") BARRIER NOT-ADV-PCLE LINK -1 (Neg Sg3))) (NEGATE 0 N OR Inf OR Indef)(NEGATE 1 Adv LINK 1 A)(1 NP-BOUNDARY OR PrfPrc)(NEGATE 0 Attr LINK 1 N)(NEGATE 0 GRADE-ADV LINK 1 A); 
	#$ Lea ártet go it gallet fulkkiid.
	#$ Ii leat vuogas geavahit hálduiváldima dálá fásta opmodagaide.
	#$ Gávpoga geaidnu lei ráhkaduvvon gollis, buhtis dego čađačuovgi láse.


#!! * **ASgNomNoSubj** selects A Sg Nom also when no copulas 
SELECT:WASgNomNoSubj (A Sg Nom) IF (*-1 BOS BARRIER NOT-ADV)(*1 CS BARRIER WORD - Inf); 
	#$ Hui somá oaidnit ahte lea beroštupmi, dadjá Dunfjell Oskal.

#!! * **ASgNomafterCop** selects A Sg Nom after copulas and Sg Nom, not so strong constraint for the target
SELECT:WASgNomafterCop (A Sg Nom) IF (*-1 COPULAS BARRIER NOT-ADV-PCLE LINK *-1 (Sg Nom) BARRIER NOT-ADV-PCLE LINK *-1 BOC OR CP BARRIER NOT-ADV-PCLE)(1 NP-BOUNDARY OR PrfPrc LINK NOT 0 N);	
	#$ Vuoigŋa gal lea geargat, muhto oažži lea headju. # geargat, headju
	#$ ...dan dihte go geassegollebivdu lei áibbas heittot leamaš,... # heittot
	#$ Joddu suhppejuvvo go doaris lea gárvvis.	
	#$ Buođđu lea dál gárvvis.
	#% Dat lea guhkimus fanas.


#!! * **ASgNomEssCopNeg** selects A Sg Nom after copulas Sg3 or Neg Sg3s, 
SELECT:WASgNomEssCopNeg A + Sg + Nom IF (*-1 COPULAS BARRIER SV-BOUNDARY LINK *-1 Neg BARRIER NOT-ADV-PCLE LINK 0 Sg1 OR Sg2 OR Sg3)(NEGATE 0 N OR Indef)(NEGATE 1 N OR A)(NEGATE 1 GRADE-ADV LINK 1 A);
	#$ Boazosápmelaččain ii leat bassin vejolaš fievrridit gálvvu gerresiiguin.
	#$ Jos dat ii leat vejolaš, de mii fertet mannat.
	#%  Son lei oainnat plánen irggis lusa vuolgit ovdal ođđabeairuohta, muhto dálki ii orron gal dasa riekta miehtame.

#SELECT:WASgNomifCopEOC (A Sg Nom) IF (*-1 COPULAS BARRIER NOT-ADV-PCLE LINK *-1 (Sg Nom) BARRIER NOT-ADV-PCLE LINK *-1 BOC OR CP BARRIER NOT-ADV-PCLE)(*1 COPULAS BARRIER NOT-ADV-PCLE LINK *1 CLB OR EOS BARRIER NOT-ADV-PCLE);
#SELECT:Wr1769 (A Pl Nom) IF (*-1 COPULAS BARRIER NOT-ADV-PCLE LINK *-1 Nom BARRIER S-BOUNDARY LINK *-1 CC BARRIER NOT-NPMOD LINK -1 Nom)(NEGATE 0 N)(NEGATE 1 REAL-V OR A);

#!! * dsfa
SELECT:Wr1772xc (A Sg Nom) IF (*-1 COPULAS BARRIER NOT-ADV-PCLE LINK *-1 (N Sg Nom) OR (Pron Sg Nom) OR (Sg1 Nom) OR (Sg2 Nom) OR (Sg3 Nom) BARRIER S-BOUNDARY LINK NOT 0 Ess)(NEGATE 0 N OR Inf OR Adv)(NEGATE 1 REAL-V)(NEGATE 1 CC LINK 1 Adv)(NEGATE *1 N BARRIER NOT-NPMOD);	
	#$ ... berre mearridanváldi leat nu viiddis go vejolaš ja nu ...
	#$ Dilálašvuođaid álgu sáhttá leat dovdameahttun dahje jávkan historjjá seavdnjatvuhtii.

#!! * **AcompGo** Selects (A Der/Comp Nom) even if there is no verb (ellipse)
SELECT:AcompGo (A Der/Comp Nom) IF (*-1 (N Sg Nom) OR (Pron Sg Nom) OR (Sg1 Nom) OR (Sg2 Nom) OR (Sg3 Nom) BARRIER NOT-ADV-PCLE LINK NOT 0 Ess)(NEGATE 0 N OR Inf OR Adv)(1 go);	
	#$ Máhttájeaddji ii leat stuorit go oahpaheaddjis, ii ge bálvaleaddji stuorit go hearrás.

#!! * **Wr1775xc** Selects (A Sg Nom) if you are (N Sg Loc), Der/NomAg or (Ex/N A). Copulas is to the left. EOS or CLB is to the right
SELECT:Wr1775xc (A Sg Nom) IF ((*-1 COPULAS + Sg3 BARRIER NOT-A-ADV) OR (*-1 COPULAS + ConNeg BARRIER NOT-A-ADV LINK -1 @+FAUXV + Neg ))(NEGATE 0 Inf)(0 (N Sg Loc) OR Der/NomAg OR (Ex/N A))(*1 EOS OR CLB BARRIER NOT-ADV-PCLE); 
	#$ Dudno mielas lea suohtas.
	#$ Mu mielas ii leat suohtas dáppe.

#!! * **Wr1776xc** selects (A Sg Nom) 
SELECT:Wr1776xc (A Sg Nom) IF (-1 NP-HEAD-SG-NOM LINK NOT 0 (A Attr) OR Adv)(*-2 COPULAS BARRIER NOT-ADV-PCLE LINK NEGATE *-1 Nom BARRIER S-BOUNDARY)(NEGATE 0 Adv LINK 1 Adv)(NEGATE 0 Attr LINK *1 (N Sg Nom) BARRIER CLB OR NOT-NPMOD)(NEGATE 0 (TV Inf) LINK *1 Acc) ;
	#$ Dábálaččat lea čuollu guhkit, golbma, njeallje fierbmebaji guhku. # guhkit
	#% Skuvlaráđđehusas lei vuoigatvuohta loahpahit diekkár gánnetmeahttun fálaldaga.

SELECT:Wr1777c (Sg Nom) IF (*-1 Ill BARRIER NOT-ADV-PCLE LINK *-1 COPULAS BARRIER NOT-ADV-PCLE)(*-1 SG3-V BARRIER NOT-AUX-V)(NEGATE 0 PrfPrc OR ILL-A)(NEGATE 0 NUM LINK *1 ALU OR ("velggolaš") BARRIER NP-BOUNDARY);
	#$ ... muhtun gii lei sutnje velggolaš logi duhát taleantta.
	#$ Mus lea dutnje juoga dadjamuš.

SELECT:Wr1778xc (A Pl Nom) IF (*-1 (N Pl Nom) OR ("visot") BARRIER NOT-ADV-PCLE LINK *-1 COPULAS BARRIER SV-BOUNDARY)(NEGATE 0 PrfPrc);
	#$ Mánnáguoddi-spagáin leat spahká-oaivvit sakka guhkibut. # guhkibut
	#$ Dán oktavuođas leat dološ geavahusa njuolggadusat miellagiddevaččat. # miellagiddevaččat

SELECT:Wr1779xc (A Pl Nom) IF (*-1 COPULAS BARRIER SV-BOUNDARY LINK *-1 (V Neg) BARRIER NOT-ADV-PCLE LINK 0 DU-PL-V)(NEGATE 0 N)(NEGATE 1 REAL-V OR A); 

SELECT:Wr1780c (A Sg Nom) IF (*-1 BOS OR REAL-CLB BARRIER NOT-CC)(NOT 0 Adv)(1 (Dem Sg Nom) OR MUN OR DON OR SON); 
	#$ Ja ávdugas son guhte oskkui.

SELECT:Adv Adv IF (*-1 BOS OR REAL-CLB BARRIER NOT-CC)(0 A)((1 VFIN) OR (1 Nom LINK 1 VFIN)); 
	#$ ja rahpasit muitala endometriosa dávdda birra.

 
#SELECT:Wr1781c (A Pl Nom) IF (*-1 BOS OR REAL-CLB BARRIER NOT-CC)(*1 NP-HEAD-PL-NOM BARRIER NOT-NPMOD);
	#$ Ávdugasat sii geat moraštit.

SELECT:W (A Nom)  IF (-1 go LINK -1 (A Nom) LINK *-1 COPULAS BARRIER S-BOUNDARY)(0 REAL-V LINK NEGATE 1 N) ;
    
REMOVE:Wr1782 REAL-V IF (-1 COPULAS)(0 Nom)(NEGATE 0 PrfPrc)(1 (Pron Rel));
	#$ Dat geaidnu lea govdat mii doalvu gáđohussii. 
SELECT:Wr1783 (A Nom) IF (-1 COPULAS)(0 Nom)(1 (Pron Rel)); 
	#$ Dát dáidá leahkit duohta maid boazodoalli lohká.
	#$ Dat geaidnu lea govdat mii doalvu gáđohussii.

REMOVE:Wr1784 VFIN IF (*-1 COPULAS BARRIER NOT-ADV-INDEF)(0 Attr)(1 N OR EOS);

SELECT:Wr1785 A IF (*-1 COPULAS BARRIER NOT-ADV-INDEF)(NEGATE 0 REAL-V OR Ord OR N)(1 N OR EOS);
	#$ ...de dilli orru leamen earalágan.

SELECT:Wr1786xc A IF (-2 (N Sg Nom))(-1 ("leat" Sg3))(NEGATE 0 N OR PrfPrc OR Inf)(NEGATE 0 Ill LINK 1 PrfPrc)(NEGATE 0 Adv LINK 1 A); 
	#$ Sin váilevaš oahppu ja giellamáhttu lea guhkit áiggi leamaš heađušteaddjin. 	 # guhkit

REMOVE:Wr1787c (A Attr) IF (0 A)(1 COMMA)(NEGATE 2 A OR PrsPrc)(NEGATE 2 CC LINK 1 Attr);

REMOVE:Wr1788xc (A Attr) IF (0 A)(NEGATE 1 A OR N OR Num OR Actio OR PrfPrc OR PrsPrc OR CC OR COMMA OR (\?)); 

REMOVE:Wr1789xc (A Attr) IF (0 A-CASE OR Adv)(1 CC LINK *1 A-CASE BARRIER NOT-ADV-PCLE);
	#$ Dan dihte leage riekta ja dehálaš go skuvla čielga vugiin dovddasta dat lea maiddái sámi ohppiid skuvla. # riekta

SELECT:Wr1791xxc (A Sg Nom) IF (*-1 COPULAS BARRIER NOT-ADV LINK 0 SG3-V LINK NEGATE *-1 Loc BARRIER N)(NEGATE -1 Gen)(NOT 0 WEATHER-SITUATION OR Inf OR Attr)(NEGATE 0 Adv LINK *1 REAL-V BARRIER NOT-ADV)(NEGATE 1 PrfPrc)(NEGATE 1 CC LINK 1 Adv) (NEGATE 0 Adv LINK *1 (A Sg Nom) BARRIER NOT-ADV-PCLE) ;
	#$ Lea veahá eahpidahtti, ahte eanaš oassi ođđa govvadáidagis,...  # eahpedahtti

SELECT:APlNom (A Pl Nom) - Der/lasj IF ((*-1 COPULAS + DU-PL-V BARRIER NOT-ADV-PCLE LINK -1 NP-HEAD-PL LINK 0 Nom) OR (*-1 COPULAS - SG-V BARRIER NOT-ADV-PCLE LINK -1 DU-PL-V LINK 0 Neg OR COPULAS))(0 Sg);
	#$ Biktasat leat nannosat.
	#$ Bártnit ledje čeavlát.
	#$ Sii eai lean duhtavaččat priváhta olbmuid luhtte orrumiin ja háliidedje skuvlainternáhta nugo lei leamaš Guovdageainnus ja Buolbmágis.

SELECT:Wr1793c (A Pl Nom) IF (1 (Pron Pl2))(*2 COPULAS BARRIER REAL-V)(*2 PL2-V BARRIER NOT-ADV-PCLE);
	#$ Man čeahpit dii lehpet hilgut Ipmila báhkkomiid! 

SELECT:Wr1795 (A Pl Nom) IF (*-1 ("orrut") BARRIER NOT-ADV-PCLE)(*-1 PL3-V BARRIER SV-BOUNDARY);

SELECT:Wr1796xc (A Pl Nom) IF (-2 (A Pl Nom) LINK *-1 COPULAS BARRIER NOT-ADV-PCLE)(-1 CC);
	#$ ...jus dat leat čuožžovaččat ja dárbbašlaččat. # dárbbašlaččat

REMOVE:Wr1797 (A Pl Nom) IF (*-1 COPULAS BARRIER REAL-V)(*-1 SG3-V BARRIER NOT-AUX-V)(*1 (A Sg NOM) BARRIER SV-BOUNDARY OR Nom);
	## No hits?

	# Coordination
	# ------------
SELECT:Wr1798c (A Sg Nom) IF (-2 (A Sg Nom))(-1 JA/DAHJE OR COMMA)(1 JA/DAHJE OR COMMA)(2 (A Sg Nom));
	#$ e. e. man bissovaš, dovddus ja bindil dat lea. # dovddus
SELECT:Wr1799 (A Sg Nom) IF (-2 (A Sg Nom))(-1 JA/DAHJE)(1 CS OR EOS)(NEGATE 0 Inf LINK -2 Inf);
	## Vare livččet jogo galmmas dahje liekkas! # liekkas

REMOVE:Wr1800c (A Attr) IF (1 COMMA OR JA/DAHJE)(2 A)(NEGATE *1 N BARRIER NP-BOUNDARY);
	#$ ...man bissovaš, dovddus ja bindil dat lea. # bissovaš
	#$ Lehket álo ustitlaččat, vuollegaččat ja guhkesmielalaččat. # ustitlaččat

SELECT:Wr1801c (A Pl Nom) IF (-2 (A Pl Nom))(-1 JA/DAHJE OR COMMA)(1 JA/DAHJE OR COMMA)(2 (A Pl Nom));
	#$ Lehket álo ustitlaččat, vuollegaččat ja guhkesmielalaččat. # vuollegaččat

SELECT:Wr1802c (A Pl Nom) IF (-2 (Pl Nom))(NEGATE -2 Adv)(-1 JA/DAHJE)(1C NP-BOUNDARY);
	#$ Lehket álo ustitlaččat, vuollegaččat ja guhkesmielalaččat. # guhkesmielalaččat
	#$ De bohte ollu tuollárat ja suttolaččat ja čohkkájedje beavddis.  # suttolaččat

SELECT:Wr1803 (Pl Nom) IF (-2 (Pl Nom))(NEGATE -2 Adv OR Inf)(-1 JA/DAHJE)((1 JA/DAHJE OR CLB LINK 1 NP-BOUNDARY) OR (1 CLB)) ;

SELECT:PlNomCCPlNom (Pl Nom) IF (1 JA/DAHJE LINK *1C (Pl Nom) BARRIER NOT-NPMOD-ACC)(NEGATE 2 Interr);
	#$ Dása gullet Sámi sierrabibliotehka, oahpahushálddašeami doaimmat ja sierra sámi mánáidgárdedoarjagat.


#!! ## And now some rules for adverbs that modify adjectives

SELECT:AdvModA2 Adv IF (0 A LINK NEGATE 0 Inf)(1C A LINK NEGATE *1 N BARRIER NOT-NPMOD);
	## ...jus ii leat čielgasit dárbbašmeahttun dahje geavatlaččat veadjemeahttun.

SELECT:AdvModA3 Adv IF (0 A LINK -1 CC LINK -1C Adv); # not in use?

SELECT:AdvModA4 Adv IF (0 A LINK 1 CC LINK 1 Adv);
	## ...dan muddui go dat lea geavatlaččat ja ruđalaččat vejolaš. # Funker ikke med 1C Ad
	## Maŋŋá soađi lea sámi álbmot rahčan ollu vai dat adnojuvvošii seammá dássásažžan, sihke duođalaččat ja rievttálaččat.
	#$ Sámedikki eará orgánat mearridit ieža galget go čoahkkimat leat rabas vai gitta.

SELECT:InfMiella Inf IF (-1 MIELLA LINK *-1 VFIN)(NEGATE 0 COPULAS) ;
	#$ Ferte dasto sihkkarastojuvvot dárbbašlaš gelbbolašvuohta čuovvolit dan sámepolitihka maid stáhta, guovlulaš ja báikkálaš hálddahusdásit hábmejit.
	#% Sivvan dasa lea go ođđa stivra ii nagot rehketdoalus lohkat gosa doarjagat leat mannan.

SELECT:InfAcc Inf IF (-1 Acc LINK NOT 0 TIME OR Rel LINK *-1 DADJAT BARRIER SV-BOUNDARY OR AUX LINK NOT 0 AUX OR Adv OR N) ;
#MAP:<OBJInf4 (@-F<OBJ) TARGET Inf (-1 Acc LINK NOT 0 TIME OR Rel LINK *-1 TRANS-V BARRIER SV-BOUNDARY OR AUX LINK NOT 0 AUX OR Adv OR N OR DADJAT LINK NOT 0C Ger OR Imprt)(NEGATE 0 Pl3 LINK *-1 TRANS-V LINK 0 (Pl Nom)) ; 
#	#$ Bearalvági gielddadoavttir lohká dan leat mielaheapme.
#


#This rule comes ones more
SELECT:SECr2996b PL3-V IF (NEGATE *-1 VFIN BARRIER CS OR CC)((*1 (Pl Nom) BARRIER NOT-NPMODADV LINK NOT 0 Adv OR Inf) OR (1 ("leat" PrfPrc) LINK *1 (Pl Nom) BARRIER NOT-NPMODADV LINK NOT 0 Adv OR Inf)) (NEGATE 0 ("leat" Imprt Pl2))(NEGATE 0 Inf LINK -1 MOVT)(NEGATE 0 Imprt LINK -1 CC OR BOS OR COMMA OR (":") OR ("de") OR PUNCT)(NEGATE 0 Sg3 LINK -1 Nom + Sg);
	## Ja dis leat velá buot oaivvi vuoktačalmmit lohkkojuvvon.
	#$ Son lea čállán lohkameahttun čállosiid sámi historjjás ja sus leat leamaš ollu luohttámušdoaimmat.
	#% Mii doarjut Sámelávdegotti evttohusa oainnu ahte sámegiela berre sáhttit válljet fágan dan sadjái go duiskkagiela gymnásaráhkkaneaddji linnjás nuoraidskuvlla ovccát jagis.

REMOVE:InfCS Inf IF (0 VFIN LINK NOT 1 REAL-V)((-1 CS) OR (-1 Rel) OR (-1 COMMA LINK *-1 Rel BARRIER S-BOUNDARY OR Inf LINK NEGATE *-1 AUX BARRIER REAL-V) OR (*-1 Acc BARRIER NOT-ADV-PCLE LINK *-1 (TV Pl3) BARRIER NOT-NPMOD LINK *-1 Rel BARRIER NOT-ADV-PCLE LINK -1 (N Nom Pl)))(NEGATE -1 go OR ("ahte") LINK NOT -1 BOC OR COMMA)(NEGATE -1 (Neg Sup)) ;
	#$ Go bassat lihtiid dahje málestit siiddas, de sáhttit seammás guldalit radio.
	#$ Organisašuvnna bláđđi man gohčodit Fokusin muitala ahte gonagas Olav su áiggistis lei bivdán beassat leat servodaga suodjaleaddji.
	#$ Áirasat geat vuostálastet eavttuhusa jienastit nu ahte deaddilit boalu "vuostá" ja čuožžilit.
	#% Son dáhttu geiget jahkásaš sámi musihkkabálkkašumi maid bokte áigot gudnejahttit, čalmmustahttit ja ovddidit sámi kultuvrra ja musihka
	#% Oahpaheaddji šattai geavahit buot čehppodagas, amas massit vátna oahppiidis.
	#% Ovddidit sámi terminologiija ja dahkat dan olahahttin.

SELECT:InfCSPrt Prt IF (0 Prs LINK NOT 1 REAL-V)(-1 CS LINK -1 Prt) ;
	#$ Buorre lei go bohtet.


SELECT:NomAct Nom IF (0 PrfPrc LINK 1 COMMA OR JA/DAHJE LINK 1 Nom LINK 1 COMMA OR JA/DAHJE) ;
SELECT:Nom Nom IF (0 Gen LINK 1 ("(") LINK 1 Nom)(NEGATE *-1 TRANS-V) ;
	#$ Oahpus lea bealli lágiduvvon oassi (duddjon, logaldallamat, bagadallamat, bargobájit, seminárat jna.) ja bealli iešheanalaš bargan (duddjon, lohkan, bargobihtáid coavdin, reflekterema cállin jna.).


SELECT:NomAg (NomAg Nom) IF (NEGATE *-1 REAL-V)(1 (":") LINK 1 Prop) ;
	#$ Čoahkkingirjji čállit: Ellen Aina Eira ja Ellen Mienna

SELECT:Nom Prop + Nom IF (1 EOS OR CC)(*-1 (":") BARRIER WORD - Prop LINK -1 (NomAg Nom) LINK NEGATE *-1 REAL-V) ;
SELECT:Nom Prop + Nom IF (1 EOS OR CC)(*-1 CC BARRIER WORD - Prop LINK -1C Nom) ;

#SELECT:InfMiella Inf IF (-1 MIELLA LINK *-1 BOS BARRIER WORD) ;
	#$ Sámegieloahput mat addet gelbbolašvuođa lohkagoahtit sámegiela dahje eará oahpuid bachelordásis Sámi allaskuvllas.

## SELECT PrfPrc, instead of VFIN
SELECT:PrfPrcSemyear PrfPrc (0 ("riegádit")) ;  #constraints?
	#$ Riegádan 1958 ja bajásšaddan Kárášjogas.

SECTION

SELECT:duššan PrfPrc IF (0 ("duššat") LINK 1 HUMAN) ;
REMOVE:oza N IF (0 ("ohcat") LINK 0 (vblex)) ; #foreløpig regel for apertium, fordi apert ikke tolker tab eller avsnittslutt som setningsdelimiter når utenfor html. 

#SELECT:vfin VFIN IF (NEGATE *-1 VFIN)(NEGATE *1 VFIN BARRIER S-BOUNDARY)(NEGATE 0 Attr LINK 1 N)(NEGATE -1 Nom LINK *-1 ("leat" PrfPrc))(NEGATE -1 MIELLA + Nom OR MIELLA + Acc)(NEGATE 0 TRANS-V LINK -1 Acc LINK -1 MIELLA + Nom OR MIELLA + Acc)(NEGATE -1 Acc LINK -1 BOS LINK 3 EOS OR ("vai"))(NEGATE 0 Inf - AUX - Imprt LINK -1 BOS) (NEGATE 0 Inf - ("leat") LINK NOT -1 Nom + Pl LINK 2 EOS - ("?")) (NEGATE 0 Inf - ("leat") LINK NOT -1 Nom + Pl LINK NEGATE *2 WORD) ;

	#$ Dál lean cealkán dan dudnuide.
	#$ Oahpa goarrut gávtti ja gođđit ránu.
	#$ Bohccot livvadit. 
	#% Njuolggadusat meahccegeavaheami rievdamis – rabas gulaskuddan.
	#% Leamaš váttis ipmirdit dan.
	#% Áigemearri váidalusa buktit.
	#% Duššan skohtervuoddji gullevaš boazodollui.

SELECT:HeadlinerInf Inf (NEGATE *-1 VFIN)(NOT 0 ("leat"))(NEGATE *-1 (Nom Pl1))(NEGATE 0 Pl3 LINK *-1 (Nom Pl))((1 EOS - ("?")) OR (NEGATE *1 WORD)) ;
	#$ Áigemearri váidalusa buktit. 
	#$ Vuoigatvuohta journála geahččat. 
	#% Bohccot livvadit. 

SELECT:Pl1coord (vblex p1 pl) IF (0 Pl1 OR Inf LINK *-1 CC OR ("de") BARRIER NOT-ADV LINK *-1C V + Pl1  BARRIER VFIN OR Inf) ;
	#$ Go bassat lihtiid dahje málestit siiddas, de sáhttit seammás guldalit radio.
	#$ Sámi servodat lea vuos ain huksemin servodagas, ja mii leat vuoruhan ja ain fertet vuoruhit kultuvrra ja giela vuođđoosiid.

SELECT:Imprtmat Imprt IF (1 ("mat")) ;
	#$ Dán mii gávnnaimet, dutkka mat leago dat du bártni gákti.


REMOVE:ImprtQst Imprt IF (*1 Qst BARRIER REAL-V OR COMMA OR (":") OR (""")) ;
	#$ Girdi mielde go don manat Helssegii?
		

#!! ## Proper nouns

#!! # VERBS

#!! ## Disambiguating verbs - part 1 

#!! First ConNeg forms, they are dependent upon Neg verbs. Then Imperative 
#!! (with their special syntax), infinitive, and other infinite forms. 
#!! Person comes later (in part 2)




#!! ## ConNeg forms

#!! Number following the rule headers below refer to numbers of hit in a 13 053 859 word corpus.

#!! * **ConNegImp** selects ConNeg Imprt if Neg Imprt to the left. - 4265
SELECT:ConNegImp (Imprt ConNeg) IF (*-1 (Neg Imprt) BARRIER ConNeg OR SV-BOUNDARY);
 	#$ Ale ge jorggit selggiid sutnje gii bivdá dus loanas.

SELECT:NegImp Neg + Imprt IF (1 ConNeg + Imprt) ;


#!! * **PrfPrcConNeg**  to ConNeg Aux after PrfPrc
SELECT:PrfPrcConNeg ConNeg + AUX IF (*-1 PrfPrc BARRIER REAL-V OR SV-BOUNDARY LINK *-1 REAL-NEG BARRIER NOT-ADV-PCLE)(NEGATE *1 REAL-V BARRIER NOT-ADV-PCLE); 
	#$ Ii dal láhppán gal leat. 

# Problem 1
#!! * **ConNegIfNeg** selects Ind ConNeg if Neg Ind to the left. This is the main (and common) ConNeg rule. - 660327
SELECT:ConNegIfNeg ConNeg IF (*-1 REAL-NEG BARRIER ConNeg OR PrfPrc OR SV-BOUNDARY)(NEGATE 0 N OR PrfPrc LINK *1 ConNeg BARRIER NOT-ADV-PCLE)(NEGATE 0 Loc LINK -1 NEGFOC LINK -1 Loc)(NEGATE 0 Inf LINK *-1 REAL-NEG LINK -1 ConNeg LINK -1 REAL-NEG)(NOT 0 Adv) ;
 	#$ Eai dat dohkke šat maninge.


#!! * **ConNegPrt** selects Prt if Prt to the left
SELECT:ConNegPrt (ConNeg Prt) IF (0 (ConNeg Prs) LINK -1 REAL-NEG LINK *-1 S-BOUNDARY LINK *-1C Prt) ;
	#$ Marit lei áidna sis gii ii hállan sámegiela.	

#!! * **ConNegCondIfNeg** selects Cond ConNeg if Neg Cond to the left. Less used, obviously. - 0 - homonymi?
SELECT:ConNegCondIfNeg (Cond ConNeg) IF (*-1 REAL-NEG BARRIER ConNeg OR SV-BOUNDARY);
 	#$ Ii go son ollu eanet gárvvohivčče din?

#!! * **ConNegPrfPrc** selects ConNeg for leat when topicalised PrfPrc between Neg and leat - 713
SELECT:ConNegPrfPrc ConNeg IF (*-1 PrfPrc BARRIER NOT-ADV-PCLE LINK *-1 Neg BARRIER SV-BOUNDARY)(0 ("leat"))(NEGATE *1 PrfPrc BARRIER NOT-ADV-PCLE);
 	#$ Ii dál láhppán gal leat.
	# Need more advanced rule to select ConNeg when there is more
	# material between Neg and ConNeg.

#!! * **ConNegImpCC** catches the second ConNeg in cases like don't smile or laugh - 0
SELECT:ConNegImpCC (Imprt ConNeg) IF (*-1 ("dahje") + CC BARRIER NOT-ADV LINK *-1C (Imprt ConNeg) BARRIER REAL-V);

#!! * **ConNegIndCC** catches the second ConNeg in cases like doesn't smile or laugh - 369
SELECT:ConNegIndCC Ind + ConNeg IF (*-1 ("dahje") + CC BARRIER NOT-ADV LINK *-1C Ind + ConNeg BARRIER NOT-ADV);
 	#$ Allet nappo moraš ja cealkke...

#!! * **NotConNegIfNotNeg** removes ConNeg if no Neg to the left. Consider unifying with NotConNegNotNeg. - 1094269
REMOVE:NotConNegIfNotNeg ConNeg IF (NEGATE *-1 Neg BARRIER CLB OR Ind)(NEGATE *-1 Nom LINK -1 Neg)(NEGATE *-1 (@CVP) OR COMMA BARRIER NOT-A-ADV LINK *-1 ConNeg BARRIER VFIN)(NEGATE -1 ("ja" CC) OR ("dahje" CC) LINK **-1 ConNeg LINK -1 Neg) ; 
 	#$ Juohke muorra mii ii šaddat buriid šattuid, čullojuvvo ja bálkestuvvo dollii.
 	#$ Gávpot mii lea vári alde, ii sáhte leat čihkosis.
 	#% Bearehan dal son ii geige vel gieđa ja váldde maiddái eallima muoras ja bora ja beasa nu eallit agálaččat.
 	#% Ii dal láhppán gal leat. 
	## Jesus celkkii: "Oanehis áiggi lean velá din luhtte."
	#% Allet atte beatnagiidda dan mii lea bassi, alletge bálkes bearraliiddádet spiinniid ovdii, vai dat eai duolmma daid, fallet din ja gaikkot din.



#!! * **NotConNegNotNeg** removes remaining ConNegs whenever no Neg to the left. - 5862
REMOVE:NotConNegNotNeg ConNeg IF (NEGATE *-1 Neg BARRIER SV-BOUNDARY OR go)
(NEGATE -1 Rel LINK -1 Neg)(NEGATE -1 Adv LINK -1 Neg)(NEGATE -2 Rel LINK -1 Neg)(NEGATE *-1 (@CVP) BARRIER NOT-A-ADV LINK *-1 ConNeg BARRIER VFIN)(NEGATE -1 ("ja" CC) OR ("dahje" CC) LINK **-1 ConNeg LINK -1 Neg) ;
	#% Buorástahttit, máidnut, buriin mielain bargguid doaimmahit vai ii čuorvvo heajos diliid ja ii de fidne guoli.
# Look at this and the previous one: they overlap appr 95%, but both catch some cases the other rule does not. 






#!! ## Imperative

#!! See also *Imprt or Ind* some sections down.

#!! * **PassLNotImprt** removes Imprt when passive (sentence-initial, hence important)
REMOVE:PassLNotImprt Imprt IF (0 Der/PassL)(*1 Rel or CS);
	#$ Ásahuvvo bargojoavku mas leat sámi mediaid ovddasteaddjit.
	# Perhaps without the Rel/CS condition as well.

#!! * **ImprtLeat** says BOS Leat A is Imprt - 575
SELECT:ImprtLeat Imprt IF (-1 BOS OR PUNCT OR REAL-CLB)(0 ("leat"))(1 A);
 	#$ Leage buorre!
	
#!! * **ImprtDál** 
SELECT:ImprtDál Imprt IF (-1 BOS OR PUNCT OR REAL-CLB)(1 ("dál"));
 	#$ Gullet dál, maid veardádus gilvi birra mearkkaša...
	
#!! * **SelImprtExcl** selects initial Imprt when excl mark 
SELECT:SelImprtExcl Imprt IF (-1 BOS OR PUNCT OR REAL-CLB)(*1 ("!"));
SELECT:SelImprtExclPl (Imprt Pl2) IF (-2 Nom + Pl)(-1 COMMA)(*1 ("!"));
	#$ Mánát, váldet alcceseattet láibevajahasaid!
	
SELECT:SelImprtExclSg (Imprt Sg2) IF (-2 Nom + Sg)(-1 COMMA)(*1 ("!"));

#!! * **ImprtComma** 
SELECT:ImprtComma Imprt IF (-1 COMMA LINK -1 Imprt LINK *-1 BOS BARRIER NOT-ADV)(NEGATE 1 COMMA LINK 1 VFIN LINK NOT 0 Imprt LINK NOT 1 Nom) ;
	#% Huikkádallet, šuhkket, boagustit, snuđđot.

#!! * **ImprtNotVGen**
REMOVE:ImprtNotVGen VGen IF (*-1 BOS OR PUNCT OR REAL-CLB BARRIER WORD)(0 Imprt)(NEGATE *1C VFIN BARRIER NOT-ADV-PCLE);
REMOVE:ImprtNotVGenOza VGen IF (0 ("ohcat") LINK 1 Ill) (NOT -1 ("ohcat"));

#!! * **NotImprtInd** 
REMOVE:NotImprtInd Imprt IF (*-1/* (neg) OR Neg  BARRIER CLB OR Ind);

#!! * **NotImprtConNeg** 
REMOVE:NotImprtConNeg Imprt IF (*-1 ConNeg BARRIER COMMA OR SV-BOUNDARY);
 	#$ Go bohciida soahpameahttunvuohta das ahte galget go journála... 
 	#$ Mánát eai gillen dálu čorget.
 	#$ Na muhto don it leat dasa nama čállán!

#!! * **NotImprtA** 
REMOVE:NotImprtA Imprt IF (-1 A LINK NOT 0 Ess);

#!! * **NotImprtN** 
REMOVE:NotImprtN Imprt IF (-1 N OR Po);
 	#$ Čuorvvas gullo Ramas. 
 	#$ Boarrásis leai dat vel eanaš ealáhussan ge.
 # This will work fine when headings no longer interfere.


#!! * **NotImprtVFIN** 
REMOVE:NotImprtVFIN Imprt IF (*1C VFIN BARRIER NOT-ADV-PCLE LINK NEGATE 1 go);
 	#$ Lágas ii duššaduvvo oktage bustávaš dahje čuoggáš, ovdal go...
 	#% Iskka massá go viessu ollu lieggasa.


#!! * **NotImprtSlash** 
REMOVE:NotImprtSlash Imprt IF (*-1 ("\(") BARRIER ("\)"))(0 N);
 	#$ Láigolihtus eai čuovo makkárge sierravuoigatvuođat gáddái (buo. riehkit, stárat).


# Moving candidates down:
#!! * **NotImprtGo** 
REMOVE:NotImprtGo Imprt IF (1 ("go" Pcle));


#!! * bearrat TV or berret IV - berret is aux
SELECT:berret ("berret") (0 ("bearrat") LINK *1 Inf BARRIER REAL-V OR S-BOUNDARY) ;
	#$ Válljedettiin journálaovddasvástideaddji berre árvvoštallat su fágagealbbu.


#!! ## Infinitive

REMOVE:Inf Inf (0 VFIN LINK -1 COMMA LINK -1 VFIN)((1 COMMA LINK 1 VFIN) OR (1 EOS)) ;
	#$ Huikkádallet, šuhkket, boagustit, snuđđot.

REMOVE:PlNom Nom + Pl (0 Inf) ((-1 ("leat") + Sg3) OR (-1 Adv LINK -1 ("leat") + Sg3))  ;
	#$ lei beare vázzit


#!! * **r2974** was moved up to select PL3-V after N Pl, might be relaxed to REMOVE Inf
SELECT:Pl3V PL3-V IF (*-1C (N Pl Nom) OR (A Pl Nom) OR (Indef Pl Nom) BARRIER Pl3 OR Du3 OR Rel LINK *-1 BOC BARRIER Pl3 or Du3) ;
	## Divššohasaid birra geat leat uminddegin dahkkon.
	## Eatnagat bohtet nuortan ja oarjin.
	## Geafit gal leat álo din luhtte.
SELECT:Pl3VNumN PL3-V IF (-1 N + Gen + Sg LINK -1 Num + Nom LINK *-1 BOC BARRIER SV-BOUNDARY OR REAL-V) ;
	#$ Go mu isit ja mu guokte bártni gárvodit, mun ráhkadan niestti.

#!! * **headofparts** 
SELECT:headofparts PL3-V IF (-1 Loc + Pl LINK *-1 HEADOFPARTS + Nom BARRIER NOT-NPMOD LINK *-1 BOC BARRIER Pl3 or Du3) ;
	#$ Eanaš oassi olbmuin cahkkehit dola boastut.


#!! * **r2976** was moved up to select PL3-V after N Pl, might be relaxed to REMOVE Inf
SELECT:r2976 PL3-V IF ((*1C NP-HEAD-PL-NOM BARRIER PPRON-NOT-SII OR S-BOUNDARY LINK 0 HAB-ACTOR) OR (*1C NUM LINK *1 (N Sg Gen) + HAB-ACTOR BARRIER NOT-A))(NEGATE *-1 VFIN BARRIER CS)(NEGATE 0 Inf LINK 1 COMMA LINK 1 Inf)(NEGATE 2 VFIN + Pl3) ;
	#$ Ohppiidráđis lea stuora fápmu skuvllas dan dihte go leat nu máŋggas geat hupmet ovttas áššiid birra.
	#$ Dása gullet Sámi sierrabibliotehka, oahpahushálddašeami doaimmat ja sierra sámi mánáidgárdedoarjagat.	
	#$ Dál leat sus 137 gáicca.
	#% Diet vuoigatvuođat leat ráddjejuvvon guoskat dan vuoigatvuhtii ahte oamastit, atnit, ovddidit ja mearridit daid eatnamiid ja riggodagaid mat sis leat odne árbevirolaš čanastumi dihte.

#!! * **r1809** Not Pl1 (but Inf) if VFIN to the left, This is the basic Inf rule.
REMOVE:r1809 Pl1 IF (*-1 VFIN BARRIER NOT-ADV-PCLE-ILL-LOC-COM OR S-BOUNDARY LINK NOT 0 N)(0 Inf);
#REMOVE:r1809 Pl1 IF (-1 VFIN LINK NOT 0 N)(0 Inf); # old, careful version
 	#$ Bargu lei kánske árvvoštallat dan.

#!! * **r1812** 
SELECT:r1812 Inf IF (*-1 NP-HEAD-SG-NOM BARRIER NOT-ADV-PCLE LINK *-1 CC BARRIER NOT-NPMODADV LINK *-1 Inf BARRIER NOT-ADV);
 	#$ Dearvvašvuođaveahkkedárbu galgá árvvoštallojuvvot ja diehtu addojuvvot.
 # This rule has to come before the ones below.
 

#!! * **InfCompCs** 
SELECT:InfCompCs (Inf @COMP-CS<) IF  (NOT 1 Inf);
 	#$ Anán buorebun odne vuolgit go ihttážii vuordit. # For tøff regel
 	#$ Anán buorebun ahte mii vuolgit go ihttážii fertet vuordit. # For tøff regel
# The above rule leans on the following one:
# MAP (@COMP-CS<) TARGET Inf IF (*-1 ("go" CS) BARRIER NOT-ADV LINK -1 Inf)(NEGATE *1 VFIN BARRIER NOT-ADV-PCLE);

#!! * **r1811** 
SELECT:r1811 Inf IF (*-1 ("ii" Neg Sup) BARRIER REAL-V);
 	#$ ...amaset sin čalmmit oaidnit,... 

# ! * __r1814__ 
#SELECT:r1814 Inf IF (-1 BOS)(*1 ("ii" V IV Neg Sg3) + Ind BARRIER V);
 	#$ Viežžat viervván rivgo čiektageregin ii leat ásti, iige sáváháhttin.
     
#!! * **EssInf**
SELECT:EssInf Inf IF (*-1 LEAT-FIN-NOT-IMPRT BARRIER NOT-ADV LINK -1 Ess);
	#$ Sin bargun lei váldit fáŋgan Gonagasa.

#!! Rules that prevent later selection of Inf for a finite verb in the frame 			
SELECT:InfCoord Inf IF (0 VFIN LINK 1 COMMA OR CC LINK 1 Inf LINK 1 COMMA OR CC LINK 1 Inf) ;
	#$ Diet vuoigatvuođat leat ráddjejuvvon guoskat dan vuoigatvuhtii ahte oamastit, atnit, ovddidit ja mearridit daid eatnamiid ja riggodagaid mat sis leat odne árbevirolaš čanastumi dihte.
			  
#!! *INF-V...CC...*
 
#!! * **r1816** 
REMOVE:r1816 Inf IF (*-1 NP-HEAD-PL-NOM BARRIER NOT-NPMODADV LINK -1 CC)(NEGATE *-1 A BARRIER NOT-ADV-PCLE)(NEGATE *-1 INF-V OR Inf BARRIER REAL-V);

#! * __r1817__ 
#REMOVE:r1817 Inf IF (*-1 (Num Nom) BARRIER NOT-N-A LINK -1 CC LINK NEGATE *-1 Inf OR INF-V BARRIER VFIN);
 	#$ ...ja ollugat biehttalit iežaset sámevuođa.
 	#$ ...ja máŋga Davviálbmogiid fakultehta studeantta leat oasi oahpus gazzan Romssa universitehtas.

#!! * **r1818** 
REMOVE:r1818 Inf IF (-1 CC LINK NEGATE *-1 Inf BARRIER S-BOUNDARY) ;
	#$ Interneahtta leat mediat mat leat guovddážis nuoraidkultuvrras , ja leat danin stuorra hástalussan barggus sihkkarastit.

REMOVE:Inf Inf IF (0 VFIN LINK *-1 BOS BARRIER REAL-V OR MIELLA + Nom OR S-BOUNDARY - PUNCT)(NEGATE *1 COPULAS BARRIER REAL-V)(NOT 1 ("vai")) ;
	#$ Norggas gávdnat boazodoalu Finnmárkkus davvin gitta Hedemárkui lullin.
	#$ – Jáddat mátketelefovnnaid ja váldde nuoraid mielde bivdui.

#!! * **r1819** 
SELECT:r1819 N IF (-3 (A Attr))(-2 CC)(-1 PrfPrc)(0 N);
 	#$ Áirasat geat vuostálastet eavttuhusa jienastit nu ahte deaddilit boalu "vuostá" ja čuožžilit.
 	#$ Mearrasápmelaččat ledje áŋgiris ja oahppan bivdit.


	# Going for Pl1 instead of Inf 
	# - - - - - - - - - - - - - - 

#!! * **r1820** 
REMOVE:r1820 Inf IF (-1C Adv LINK *-1 BOS BARRIER NOT-NPMOD);
 	#$ Vulobealde guorahallat fas mo sáhtášii deattuhit dákkár jávohisvuođa.

#!! * **r1821** 
REMOVE:r1821 Pl1 IF (-1 BOS)(0 Inf)(NEGATE 0 Qst OR Imprt);


	# Now we select Inf 
	# - - - - - - - - -

 # The first rule seems much better without the NOT 0 Adv.

SELECT:XYr1822 Inf IF (*-1 INF-V + V BARRIER SV-BOUNDARY OR Neg OR COMMA)(NOT 0 Adv);
	## Skuvla galggai oahpahit sámemánáide dárogiela.
	## Prošeavtta áigut čađahit.
	#% Dás galget nuorat, čohkut dahje veallut duorggaid alde buolli árrandola ovddas vuostáiváldit oahpahusa sis.
	#% Badjel bealli ohppiin luohkás geat čakčat álge nuoraidskuvlii, leat válljen lullisámegiela.
	


#!! * **r1823** 
SELECT:r1823 Inf IF (*-1 INF-V + V BARRIER NP-HEAD-PL-NOM OR SV-BOUNDARY LINK 0 VFIN OR PrfPrc OR Inf LINK NOT 0 N)(NOT 0 ("leat"));
 	#$ Son oažžu vuokkain ja luovosfirpmiin, namalassii golgadagain, bivdit miehtá deanu.

#!! * **r1824** 
SELECT:r1824 Inf IF (-2 Inf)(-1 COMMA)(1 COMMA OR CC);
 	#$ Luondduresursaealáhusat sáhttet álggahuvvot, bisuhuvvot ja ovddiduvvot.

	# This rule looks backwards across a relative clause:
#!! * **r1825** 
SELECT:r1825 Inf IF (*-1 VFIN BARRIER S-BOUNDARY LINK *-1 (Pron Rel) BARRIER S-BOUNDARY LINK *-1 INF-V BARRIER Inf OR SV-BOUNDARY);
 	#$ ...galgá sihke divššohas ja váhnemat dahje earát geain lea váhnenovddasvástádus oažžut dieđu.

#!! * **r1827** 
SELECT:r1827 Inf IF (*-1 (A Nom) BARRIER NOT-ADV OR COMMA LINK *-1 COPULAS BARRIER NOT-ADV-PCLE);

#!! * **r1828** 
SELECT:r1828 Inf IF (-1 ADVLCASE LINK *-1 (A Nom) BARRIER NOT-NPMOD LINK *-1 COPULAS BARRIER NOT-ADV-PCLE);
 	#$ Dál lea váttis dadjat juste beaivvi goas váttisvuođat čoavdašuvvet.
 	#$ Danne go mii orrut tunealla lahka, de lea álki isidii vuodjit tunealla go manná girdišilljui. 




#!! ## Verbgenitive
#     ------------
	
# Background: 	
# VGen is a bad lookalike for various finite forms. It is rare, and occurs in
# special contexts.

# Philosophy:
# 0. We remove some VGens that otherwise would have turned up'
# 1. We make rules selecting VGen where we feel safe to say we have found it.
# 2. We remove all remaining VGen readings.

# 0. These are not VGen

#!! * **VGen** is typo
REMOVE:TypoNotVGen VGen IF (0 ("mielddistit") OR ("gohčodit") OR ("ráhkkanit") OR ("soaitit"));


# 1. Looking for VGen.

#!! * **VGen** selects VGen after VGEN-V-TRIGGER-verb
SELECT:VGen1 VGen IF (*0 VGEN-V-TRIGGER + MAIN-V BARRIER Neg OR SV-BOUNDARY OR COMMA)(0 VGEN-V LINK NOT 0 Der/Pass OR Loc OR Inf)(NEGATE 1 CC LINK 1 VGEN-V-TRIGGER) ;	
	#$ Ii lean ávki geahččalit gávdnat čoavdaga oađi dahje oza.
	#$ Suga láviime fitnat gitta Biessavvonis.
	#% Mánát geat orro nu guhkkin eret ahte eai sáhttán beaivválaččat vácci boahtit skuvlii, orro internáhtas ja boradedje doppe, ođđe oađđensálain ja barge bihtáid klássalanjas maŋŋelgaskabeaivve.


#!! * **Gen2**	selects VGen after after gaskan and lahka
SELECT:VGen2 VGen IF (-1 ("gaskan") OR ("lahka"))((NEGATE 0 Der/Pass OR Loc OR Inf OR (N Gen) - ("bora")) OR ((0 ("borrat") OR ("jápmit"))));
	## Gaskan bora viehkalin olggos.

#!! * **VGen3** selects VGen after copulas
SELECT:VGen3 VGen IF (*-1 COPULAS BARRIER NOT-ADV-PCLE)(0 VGEN-V LINK NOT 0 Der/Pass OR Loc OR Inf OR (N Nom));  
	## Muhtumat ledje vácci, ja muhtumat ledje čuoigga. 

#!! * **VGen4**
SELECT:VGen4 VGen IF (-2 COPULAS)(-1 Indef OR Adv)(0 VGEN-V LINK NOT 0 Der/Pass OR Loc OR Inf);  	
	## Dát ađa lea uhccán vuošša. 
	
#!! * **VGenCoor**
SELECT:VGenCoor VGen IF (-1 CC LINK -1C VGen) ;
	#$ Biilaráiddut jurre ijatbeaivái Duortnosjohkaráigge boares geainnu mielde davás: stuorra dievvalástejuvvon guorbmebiillat juorbbu ja ráđa ja smávva meahccebiillat daid maŋis.

# 2. Done that, now we remove whatever VGen was left.

#!! * **KillAllVGen** removes all VGen (r1842)
REMOVE:KillAllVGen VGen IF (0 (VGen));


#!! ## Supinum vs. potential -- no example found in large corpus

#!! ## Perfect Participle

REMOVE:inCoor PrfPrc IF (-1 CCCOMMA OR JA/DAHJE LINK -1 N + Nom)(0 N + Nom LINK 1 CCCOMMA OR JA/DAHJE LINK 1 N + Nom);
	#$ Sámi mánáidgárdeoahpaheaddjeoahppu, MOA 2017 joavkku, lea odne čájehan Duodji, Juoigan ja Hutkáivuohta (DJH) máhttoviidodaga joavkoeksámena.


REMOVE PrfPrc IF (0 N LINK 1 ("/") LINK 1 N);
	#$ Dát ruhta galgá juhkkot njealji sadjái, namalassii čuoigan/luođovaláštallamii ja golmma spábbačiekčanjovkui.


#!! * **r1844** removes PrfPrc if 0 is the second N in an N and ... N construction
REMOVE:Yr1844 PrfPrc IF (*-1 CC BARRIER NOT-NPMOD LINK -1 N LINK NEGATE *-1 REAL-COPULAS BARRIER NOT-NPMODADV)(0 N)(NEGATE 0 ("leapma" Der/Dimin))(NEGATE -1 CC LINK *-1 PrfPrc BARRIER REAL-V);
 	## Journála sirdin ja luoikan.
	## ...de lassánii maid sisafárren ja koloniseren dáidda guovlluide. 

#!! * **r1844** removes PrfPrc if 0 is the second N in an N and Gen ... N construction (this is marginal)
REMOVE:Yr1845 PrfPrc IF (*-1 Gen BARRIER NOT-NPMOD LINK *-1 CC BARRIER NOT-NPMOD LINK -1 N LINK NEGATE *-1 REAL-COPULAS BARRIER NOT-NPMODADV)(0 N)(NEGATE 0 ("leapma" Der/Dimin));
	## Iige njuovvan ja bierggu rádjan leat mihkkege dearvvašvuođaváttisvuođaid.
 # The reason for the specific mention of Gen in this rule is that at this stage, 
 # the nominals in question are both Acc and Gen. Since Acc is NOT-NPMOD, it acts as a 
 # barrier for the previous rule. 

#!! * __PrfPrc_Ess__ removes N Ess if 0 PrfPrc
REMOVE:YPrfPrc_Ess (N Ess) IF (*-1 COPULAS + ConNeg BARRIER NOT-ADV-PCLE LINK *-1 Neg BARRIER NOT-ADV-PCLE)(0 PrfPrc)(NEGATE *1 ConNeg OR PrfPrc BARRIER S-BOUNDARY);
	## Manne son ii leat ohcan dan ođđa virggi?
	

#!! * **r1852** selects PrfPrc if copula to the left
SELECT:Yr1852 PrfPrc IF ((*-1 REAL-COPULAS OR ("galgat") BARRIER NOT-ADV-PCLE LINK 0 VFIN LINK NOT 0 PrfPrc) OR (-1 LEFT)) (NEGATE 0 Ess LINK 1 (A Nom) OR PrfPrc)(NEGATE 0 ESS-ADVL-N)(NEGATE 0 Actio LINK 1 PrfPrc LINK 0 MAIN-V LINK NOT 0 VFIN)(NEGATE 1 ("veara"))(NEGATE 1 CC LINK 1 Actio LINK NOT 0 PrfPrc)(NEGATE 0 Actio OR N LINK *1 ("leat" PrfPrc) BARRIER COPULAS) ;
SELECT:Yr1852 PrfPrc IF (-1 Adv LINK -1 REAL-COPULAS OR ("galgat") LINK 0 VFIN LINK NOT 0 PrfPrc)  (NEGATE 0 Ess LINK 1 (A Nom) OR PrfPrc)(NEGATE 0 ESS-ADVL-N)(NEGATE 0 Actio LINK 1 PrfPrc LINK 0 MAIN-V LINK NOT 0 VFIN)(NEGATE 1 ("veara"))(NEGATE 1 CC LINK 1 Actio LINK NOT 0 PrfPrc)(NEGATE 0 Actio OR N LINK *1 ("leat" PrfPrc) BARRIER COPULAS);
	## Norgga láhkaaddit leat áigá jo suokkardan vuđolaččat kvenaid eamiálbmotstáhtusa gažaldaga. 

SELECT:sonSG3V PrfPrc IF (-1 Pers + Sg3 + Nom) (1 COPULAS + Sg3) ; 
	#$ Eatnistis son árben lea dan luonddu čáppa moji.

	
#!! * **r1853** selects PrfPrc if Rel to the left which again is linked to copula
SELECT:Yr1853 PrfPrc IF (*-1C Rel LINK -1 Pron OR N LINK *-1 REAL-COPULAS BARRIER NOT-ADV-PCLE)(NEGATE 0 Ess LINK 1 (A Nom) OR PrfPrc)(NEGATE 0 Actio LINK 1 PrfPrc LINK 0 MAIN-V LINK NOT 0 VFIN)(NEGATE 1 ("veara"))(NEGATE 1 CC LINK 1 Actio LINK NOT 0 PrfPrc)(NEGATE 0 Actio OR N LINK *1 ("leat" PrfPrc) BARRIER COPULAS);
	## Muhtin biirres leat gávcci 12 áirasis leamaš nissonolbmot, ja eará biirres leat buohkat geat leat válljejuvvon leamaš dievddut . # The relative sentence version. 
	 	

##! * __PrfPrcModA__ selects PrfPrc as modifier to an adjective
SELECT:PrfPrcModA PrfPrc IF (1 A LINK NOT 0 N)(NOT 2 N);

SELECT:PrfPrcModN PrfPrc IF (-1 VFIN)(*1 N BARRIER WORD - Attr) ;
	#$ Sámi servodat dárbbaša oahppan sámegielat journalisttaid.

#!! ### Topicalized version
#   - - - - - - - - - -

#!! the following chapter should be possible to unify.

#!! * **r1855** selects PrfPrc if Nom to the left linked to copula
SELECT:Yr1855 PrfPrc IF (*-1 Nom BARRIER SV-BOUNDARY LINK *-1 REAL-COPULAS BARRIER NOT-ADV-PCLE LINK NEGATE *-1 Nom BARRIER S-BOUNDARY)(NEGATE 0 Ess LINK 1 (A Nom) OR PrfPrc)(NEGATE 0 Actio LINK 1 PrfPrc LINK 0 MAIN-V LINK NOT 0 VFIN)(NEGATE 1 ("veara"))(NEGATE 1 CC LINK 1 Actio LINK NOT 0 PrfPrc)(NEGATE 0 Actio OR N LINK *1 ("leat" PrfPrc) BARRIER COPULAS)(NEGATE 0 AUX);
	## Muhtin biirres leat gávcci áirasa leamaš nissonolbmot, ja eará biirres leat buohkat geat leat válljejuvvon leamaš dievddut.

#!! * **r1857** selects PrfPrc if Acc to the left linked to copula
SELECT:Yr1857 PrfPrc IF (*-1 Acc BARRIER NOT-ADV LINK *-1 REAL-COPULAS BARRIER NOT-NPMODADV)(NEGATE 0 Actio LINK -1 PrsPrc)(NEGATE 1 COMMA LINK *1 Actio BARRIER NOT-NPMOD)(NEGATE 0 Actio LINK *1 PrfPrc BARRIER NOT-ADV-PCLE);
	## Leago barggu ohcan? 

#!! * **r1858** selects PrfPrc if NP head to the left linked to copula
SELECT:Yr1858 PrfPrc IF (-1 NP-HEAD-NOM LINK *-1 REAL-COPULAS BARRIER NOT-NP)(NEGATE 0 Actio LINK -1 PrsPrc)(NEGATE 0 Actio LINK 1 CC LINK 1 Actio LINK *1 N BARRIER NOT-NP)(NEGATE 1 ("veara"))(NEGATE 0 N LINK -1 A + Attr) ;
	## Leatgo sii njuovadan juo?
	#% Sihkkelastin lea mu mielas hui friddja valáštallan.

#!! * **r1857** selects PrfPrc if copula to the left
SELECT:Yr1859 PrfPrc IF (*-1 REAL-COPULAS BARRIER SV-BOUNDARY OR CC OR CS LINK NOT 0 PrfPrc LINK 0 VFIN)(NEGATE 0 Actio LINK *1 PrfPrc BARRIER NOT-ADV-PCLE LINK NOT 0 PrfPrc)(NEGATE 0 Ess LINK 1 (A Nom) OR PrfPrc)(NEGATE 0 ESS-ADVL-N)(NEGATE 1 ("dihte") OR ("dihtii"))(NEGATE 1 COMMA OR CC LINK *1 Actio BARRIER NOT-NPMOD)(NEGATE 1 ("veara"))(NEGATE 1 ("veara"))(NEGATE 0 N LINK -1 A + Attr) ;
	## Biret-Elle lea easka skuvllas geargan.
	#% Dat lea min guhkesáigge áŋgiruššan sámegielaiguin. 
	#% dat lea min guhkesáigge áŋgiruššan sámegielaiguin.

#!! * **r1861** selects PrfPrc if VFIN to the left
SELECT:Yr1861 PrfPrc IF (*-1 VFIN BARRIER S-BOUNDARY OR (Refl Acc))(NEGATE 0 N OR Actio OR Ess)(NEGATE *-1 CC OR COMMA BARRIER VFIN);
	## Sámediggi sáhttá mearridit meannudit ášši giddejuvvon uvssaid duohken.
	#% Ingalill Olsen lohká gal iežas garrasit behtton ja healkkehan gielddarievtti duomu geažil .
 
    
 # PrfPrc if there is not a Sg1 to the left, but if there is a CC to
 # the right of a possible Sg1, then we forget about the Sg1.

#!! * **r3576** selects PrfPrc if Acc to the left linked to activity verb
SELECT:Yr3576 PrfPrc IF (*-1 Acc BARRIER NOT-ADV LINK -1 Acc LINK *-1 VERBAL-ACTIVITY-V BARRIER NOT-NPMOD) ;  
	## Son logai oappás dan bargan.	

#!! * **r1863** is the mannan vahkku rule
SELECT:Yr1863 PrfPrc IF (0 ("mannat"))(1 TIME);
	## Ii leat boahtán mannan vahkus.
	## Mannan čuohtejagi gaskkamuttos lei stuorra gilvu.

REMOVE:PrfPrcifN PrfPrc IF (0 N LINK -1 Dem + Attr)(NEGATE *1 N BARRIER NOT-NPMOD) ;
	## Dákkár áŋgiruššan lea máŋgga dáfus buorre.

#!! ## Actio


#!! ## Present participle
#!! *orrut vs. orrot) 
#!! ## Rules for "addit" (which is an adjective, but more often a verb)
#!! ## Actio Loc = N Loc

LIST JAHKKIT = "jáhkkit" "sávvat" "vuordit" "beassat" ;


#!! * **ActioLocleat** is an IFF rule, we also need rule for 'leat', like in lea go biergu oastimis
SELECT:ActioLocleat (Actio Loc) IF  (0 ("oažžut") OR ("fidnet") LINK *-1 ("leat") BARRIER REAL-V OR S-BOUNDARY) ; 
	#$ Odne leat dákkár herskot hárve oažžumis midjiide gávpotsápmelaččaide.

#!! * **ActioLoc** is an IFF rule, we also need rule for 'leat', like in lea go biergu oastimis
IFF:ActioLoc (Actio Loc) - JAHKKIT IF  (0 (N Loc) LINK *-1 ("ceavzit") OR ("heaitit") OR ("geargat") OR ("gieldit") OR ("váibat") OR ("dolkat") OR ("ádjánit") OR ("beassat") OR ("vajálduhttit") OR ("hehttet") OR ("biehttalit") OR ("ballat") BARRIER REAL-V OR S-BOUNDARY) ; 
	#Lene: removed "leat"
	#% Dáruiduhttin lea hearkkes ášši, danne leat nu olu dovddut dán digaštallamis.




#!! ## Actio Nom = Ess

SELECT:ess Ess (0 (Der/NomAct Nom) LINK *-1 OPRED-V BARRIER NOT-A-ADV) ;
	#$ Máttagánda Erik Loftsgård (17) vuittii sotnabeaivi vuosttas Finnmarksløpet-junior-gilvvu ja gárttai seammás historjjálaš vuoitin.
	
SELECT:vierruEss Ess (0 ("vierru") LINK -1 COPULAS)(*1 Inf BARRIER SV-BOUNDARY) ;
	#$ Juovllaid sudnos lei vierrun ruovttus ávvudit, ovttas váhnemiiguin ja unnaoappážiin.

#!! ## Imprt or Ind



#!! * **removeAllImp**
#REMOVE:removeAllImp Imprt ;

#!! ## Nouns or verbs

#!! The rules are no documented yet

#!! * **VFINAttr**
REMOVE:VFINAttr VFIN IF (-1C Attr LINK NOT 0 ("eanet"));
  	#$ Eambbo doaimmat ruovttuin váikkuhit maiddái veahkkebálvalusossodaga.
  	#$ Máhtte-vilbealle duddjui ođđa fatnasa.

#!! * **NPlbuorit**  
SELECT:NPlbuorit (N Pl) IF (-1 ("buorre" Pl Nom))(0 V + Ind);  
	#$ Das leat buorit lanjat čáppa guovllus.

#!! * **ActioEssNum**	
REMOVE:ActioEssNum (Actio Ess) IF  (-1 (Num Gen) OR (Num Nom))(0 (N Gen));	
	#$ Odne leat golbma vihaheame.

#!! * **ActEssIfSensationv**	
SELECT:ActEssIfSensationv (Actio Ess)  IF (-1 Acc LINK *-1 PERCEPTION-V OR DADJAT BARRIER SV-BOUNDARY);
	#$ Go gulan olbmuid sámásteame, de lea dego doložii máhccat, go mánnán gullen áhči, eagi ja Ristiinna-imi humadeame.

#!! * **NoActorIfSg3**
REMOVE:NoActorIfSg3 Actor IF (0 VFIN + Sg3)(NEGATE 0 Gen LINK 1 Po OR N) ;
	#$ Heasta čuožžu golmma juolggi nalde.
	#$ Dat orru buorre fanas.
	#% Muhto sárgu mielas dárbbaša sámi servvodat eambbo ironiija.
	
#!! * **GenIfPo**
SELECT:GenIfPo Gen (1C Po OR ("gaskasaš")) ;	
	#$ Soames muitalii oktii ahte son muitalii lulli dážaide ahte sus gal lea lávvu mas lea golbma gearddi , parabola ja vuojadanálddis vel árrana bálddas.

SELECT:r1082 Gen IF (0 COUNTABLE - Ess OR ("nr"))(1 ABBR OR ACR OR Num LINK 1C Po OR ("gaskasaš")) ;
	#$ Luonddumáŋggabealatvuođalága paragráfa mielde ii leat lohpi lottibeasi bilidit man dat lea juo álgán ráhkadit.


#!! * **semináraNOM**
SELECT:semináraNOM Nom (0 Sem/Event LINK 0 Gen)(*1 Gen BARRIER NOT-A LINK 1 ("birra")) ;	
	#$ Seminára vieruid birra čohkkii birrasit 100 olbmo.


SECTION

#!! ## Demonstrative pronouns, agreement in DP - should it be moved to after verbmappings? 
#!! The rules are no documented yet

#!! * **DemAttr**
SELECT:DemAttr $$CASE + (Dem Sg) IF (0 DAT-PRON)(*1 $$CASE + (N Sg) BARRIER NOT-NPMOD - Pcle OR TIME-N OR Num LINK NOT 0 VFIN OR ("leat") OR (Actio Ess) OR Po)(NEGATE 0 Acc LINK *-1 STRICT-TRANS-V BARRIER Acc LINK NOT -1 Acc LINK *2 Dem LINK NEGATE *1 Acc BARRIER NOT-NPMOD)  ;
	#$ Dalle galgá beare uhccán ovddastuvvon sohkabeali evttohasaid loktet badjeliidda dan listtas.
	#$ Jáhkán leat giehtalaga vel dán ge áššis.
	#% Mun muitalin dan vielljasan. # Should not hit this one.

SELECT:nubbiAttr $$CASE + ("nubbi") + Attr IF (*1 $$CASE + (N Sg) BARRIER NOT-NPMOD OR TIME-N OR Num LINK NOT 0 VFIN OR ("leat") OR (Actio Ess) OR Po)(NEGATE 0 Acc LINK *-1 STRICT-TRANS-V BARRIER Acc LINK NOT -1 Acc LINK *2 ("nubbi") LINK NEGATE *1 Acc BARRIER NOT-NPMOD)  ;

SELECT:GenTime Gen (0 ("nubbi") OR Dem LINK 1 Sem/Time OR ("jagáš") LINK 1 N)  ;
	#$ Rolf Morten Anti Amundsen, dahje Rolffa nugo eatnašat dovdet su, lea musihkar gii vuittii dán jagáš Sami Music Awards komponistabálkkašumi.


#!! * **IndefAgree** guhtege goappašat iešguhtege guhte

SELECT:IndefAgree $$CASE IF (0 ("goappašat") OR ("iešguhte") OR ("guhte") OR ("guktot"))(*1 $$CASE + N BARRIER NOT-NPMOD OR TIME-N OR Num LINK NOT 0 VFIN OR ("leat") OR (Actio Ess) OR Po) ;

REMOVE:guovttos ("guovttos") (0 ("guktot") LINK NOT -1 (N Gen)) ;

SELECT:demPlLocAttr (Dem Pl Loc Attr) IF (0 (Dem Pl Com Attr)) ((*1 (Pl Loc) BARRIER NOT-NPMOD) OR (1 PrfPrc LINK 1 (Pl Loc)))  ;  #new analysis
REMOVE (Dem Pl Loc Attr) ;

#!! * **DemCASEPl**
SELECT:DemCASEPl $$CASE + (Dem Pl) IF (0 DAT-PRON)(*1 $$CASE + (N Pl) BARRIER NOT-NPMOD) ; 

#!! * **DemCASESg**
SELECT:DemCASESg $$CASE + (Dem Sg) IF (0 DAT-PRON)(1 Gen - Num LINK 1 $$CASE + (N Sg)) ; 

#!! * **DemAttrNum**
SELECT:DemAttrNum $$CASE + (Dem Sg) IF (0 DAT-PRON)(1 $$CASE + Num LINK *1 (N Sg) BARRIER NOT-NPMOD OR TIME-N OR Num LINK NOT 0 VFIN OR ("leat") OR (Actio Ess))(NEGATE 0 Acc LINK *-1 STRICT-TRANS-V BARRIER Acc LINK *1 Dem LINK NEGATE *1 Acc BARRIER NOT-NPMOD)(NEGATE 0 Acc LINK 1 Num LINK 1 Sem/Time)  ; 
	#$ Dovddat go dan vihtta bártni geat leat doppe.
	#% Märtha Louise oaččui dan 16 jagi áigi.

#!! * **DemAcc**
REMOVE:DemAcc (Dem Acc) IF (0 DAT-PRON)(1C ADVLCASE)(NEGATE *-1 TRANS-V BARRIER REAL-V OR COMMA);
#SELECT:DemAcc (Dem Acc) IF (0 DAT-PRON)(1C ADVLCASE - Ill);
	#$ De našunálastáhtat válde daid háldoseaset.
	#Comment: Surely the original REMOVE is a mistake? or concider moving to after verbmappings!

#!! * **DemAttr**
REMOVE:DemAttr (Dem Attr Sg) IF (0 DAT-PRON)(NEGATE *1 (Sg Loc) OR (Sg Ill) BARRIER NOT-NPMOD);
REMOVE:DemAttr (Dem Attr Pl) IF (0 DAT-PRON)(NEGATE *1 (Pl Loc) OR (Pl Ill) OR (Pl Com) BARRIER NOT-NPMOD);

REMOVE:TESTAttr Attr IF (NEGATE 1 A OR N OR Num OR Pr OR CC OR COMMA OR PrfPrc OR Attr)(NEGATE 1 Actio LINK 1 ("veara") OR Po) ;
	#% Dattetge ii leat muitaluvvon makkárge namahan veara geavahanriiddu birra dálolaččaid ja boazodoalu gaskkas.
	#% Dat ledje muhtin eará dovddus olbmot.

	
#!! # VERB MAPPINGS
LIST TEXT-@>N = ("mearridit") ;


#!! ## Verbs as predicatives (@SPRED>) and (@<OPRED) 

#!! ### The tags (@SPRED>) and (@<OPRED) target  PrfPrc
#!! The rules are no documented yet

#!! * **spredPrfPrc** Buressivdniduvvon lehkos (topicalised PrfPrc) -- was r494
MAP:spredPrfPrc (@SPRED>) TARGET PrfPrc (-1 BOC)(0 Der/Pass)(1 ("leat") LINK NOT 1 V)(*2 NP-HEAD-NOM BARRIER NOT-NP);
	#$ Buressivdniduvvon lehkos son guhte boahtá Hearránammii.

#!! * **opredPrfPrc**	
MAP:opredPrfPrc (@<OPRED) TARGET PrfPrc (*-1 (Refl Acc) BARRIER NOT-ADV) ((*-2 STRICT-TRANS-V BARRIER NOT-NPMODADV) OR (*-2 Nom BARRIER NOT-ADV LINK -1 STRICT-TRANS-V))(0 Der/Pass LINK NOT 1 (Pl Loc))(NEGATE 0 TEXT-@>N  LINK 1 TEXT OR ABSTR-TEXT) ;
	#$ Seammás orru Alimusriekti Skjerstad-áššis dovdan iežas čadnojuvvon dupmui.
	#$ Nu dovdá Bierna iežas geatnegahtton seailluhit sámi kultuvrra.

#!! * **opredPrfPrc**
MAP:opredPrfPrc (@<OPRED) TARGET PrfPrc (*-1 COMMA BARRIER NP OR S-BOUNDARY LINK -1 Ess)(1 EOC OR CC) ;	
	#$ Dat boahtá dohko ja gávdná dan guorusin, suohpalastojuvvon ja čiŋahuvvon.


#!! ## Passive verbs often have 

LIST PASS = Der/PassS Der/PassL "botnjasit" ;

MAP:-FMAINVPrfPrc (@-FMAINV) TARGET PrfPrc + PASS (*-1 REAL-COPULAS + VFIN + Sg3 OR REAL-COPULAS + ConNeg BARRIER NOT-ADV-PCLE LINK NEGATE -1 Nom OR HAB-ACTOR + Loc)(NEGATE *-1C PrfPrc BARRIER REAL-V OR N) (*1 (N Sg Nom) BARRIER NOT-A) ; 
#% Dát mearkkaša ahte dáid suohkaniid ássiin lea viiddiduvvon vuoigatvuohta.

MAP:-FMAINVPrfPrcbisanit (@-FMAINV) TARGET PrfPrc + ("bisánit") (1 Sem/Route) ; 


#!! ## Verbs as prenominal participles (@>N):

#!! * Some verbs will not be @>N if not Pass
LIST NOT-@>N = "bargat" "báhcit" "bálkestit" ("beahttit" Der/PassS) "beroštit" "bidjat" "boahtit" "buollát" "buohcat" "čadnot" "čuolastit" "duođaštit" "eallit"  "fitnat" "galbet" "gearggus" "hálbut" "hárjehallat" "juhkan" "johtit" "leat" "lebbet" "logaldallat" "náđđut" "oaidnit" "oažžut" "orrot" "orrut"  "ihtit" "rahčat" "ráigat" "suhtadit" "unnut" "vuodjit" "váldot" "vuodjat" Der/InchL ;

#!! * **NPrfPrc1** with 1C N Nom
MAP:>NPrfPrc1 (@>N) TARGET PrfPrc - NOT-@>N (NEGATE *-1 Neg BARRIER REAL-V)(NEGATE -1 COPULAS LINK *-1 Ill BARRIER NOT-A)(NEGATE 0 AUX)(NEGATE 0 TRANS-V LINK *1 Acc BARRIER NOT-NPMOD)(NEGATE 0 MOVEMENT-V LINK *1 Ill OR DOHKO OR ("lusa") BARRIER NOT-NPMOD)(NEGATE 0 PrfPrc LINK NOT 0 Der/Pass LINK 1 Attr)(*1 (N Nom) - Prop BARRIER NOT-A OR COMPAR OR PUNCT) ; # LINK NEGATE 0 TITLE LINK 1 Sem/Fem OR Sem/Mal OR Sem/Sur) ;
	#$ Son lea oahppan olmmoš.
	#$ .. maid almmolaš dohkkehuvvon dearvvašvuođabargit addet, ..
	#$ Mearrasápmelaččat ledje áŋgiris ja oahppan bivdit.
	#$ Sus leat hervejuvvon gálssohat main heaŋgájit čáppa diehpit. 
  	#$ Sieiddi bálvalemiin sturron eallu gohčoduvvui bálvvoseallun, mii sáhtii jávkat, go oamasteaddji jámii dahje go sieidi ii bálvaluvvon.
	#$ Ruonáeana lea čielgasit ráddjejuvvon geográfalaš guovlu. 
	#% Dieđihuvvon láhppon – gávdnon dearvan.

MAP:>NPrfPrc (@>N) TARGET PrfPrc - NOT-@>N (-1 VFIN OR PrfPrc)(0 ("oahppat") OR ("duššat"))(*1 PROFESSION OR HUMAN BARRIER WORD - Attr) ;
	#$ Sámi servodat dárbbaša oahppan sámegielat journalisttaid.


#!! * **NPrfPrc2** with -1C Dem or Num or Attr or Indef
MAP:>NPrfPrc2 (@>N) TARGET PrfPrc - NOT-@>N (*-1C Dem OR Num OR Attr OR Indef BARRIER NOT-A LINK NOT 0 Ill)(NEGATE 0 AUX)(*1 N BARRIER NOT-A);
	#$ Galgá leat okta čohkkejuvvon divššohasjournála. 

#!! * **NPrfPrc3** with PrfPrc or ConNeg to the left, the N can be different cases
MAP:>NPrfPrc3 (@>N) TARGET PrfPrc - NOT-@>N (-1 PrfPrc LINK *-1 ("leat") OR Neg BARRIER REAL-V OR S-BOUNDARY)(NEGATE 0 AUX)(1 N - Ess);
	#$ ...dan mii lei dáhpáhuvvan bosttahuvvon olbmuide. 
	#$ Ránnjá Ole Vuolab imašta manne eai leat čorgen eaige čázi ge gidden buollán viesus.
	#$ Ii lean boahtán mannan vahkus.

#!! * **NPrfPrc4** mannat in front of TIME
MAP:>NPrfPrc4 (@>N) TARGET PrfPrc (0 ("mannat") OR ("vássit"))(1 TIME);
	#$ Dalle šattašii liikka buoret dálki go mannan sotnabeaivve. 
	#$ Mu oambealle lei dáppe mannan čavčča maid.
	
#!! * **NPrfPrc5** for LEX-PASS
MAP:>NPrfPrc2 (@>N) TARGET PrfPrc - NOT-@>N (0 LEX-PASS LINK 1 N LINK NOT 0 PLACE);
	#$ Ohcame láhppon dievddu Nordlánddas. 


#!! * **NPrfPrcPr** after Pr 
MAP:>NPrfPrcPr (@>N) TARGET PrfPrc - NOT-@>N (*-1 Pr BARRIER NOT-NP LINK -1 NP-BOUNDARY LINK NOT 0 ("leat"))(NEGATE 0 AUX)(1 (N Gen));
	#$ 1978 láhka balansere gaskal sámi vuoigatvuođaid boarásmuvvan oainnu,...  

#!! * **NPrfPrcPo** before Po
MAP:>NPrfPrcPo (@>N) TARGET PrfPrc - NOT-@>N (*1 (N Gen) BARRIER NOT-A LINK 1 Po)(NEGATE 0 AUX)(NEGATE 0 MOVEMENT-V LINK *1 Ill OR DOHKO OR ("lusa") BARRIER REAL-V)(NEGATE *-1 ("leat") BARRIER NOT-ADV-PCLE) ;
	#$ Áššiin meahcceeatnamiid nuppástuvvan geavaheami birra galget stáhtalaš, fylkkagielddalaš ja gielddalaš eiseválddit árvvoštallat, mo nuppástupmi váikkuha sámi kultuvrii, boazodollui, meahcásteapmái, ealáhusdoaimmaheapmái ja servodateallimii. 
	#% Dan čájehit guorahallamat maid Telemarksforskning lea čađahan Sámedikki ovddas. 

#!! * **NPrfPrcGen** after Gen
MAP:>NPrfPrcGen (@>N) TARGET PrfPrc - NOT-@>N (-1 Gen LINK *-1 REAL-V - COPULAS BARRIER S-BOUNDARY)(NEGATE 0 AUX)(1 N - ADVLCASE)(NEGATE 0 TRANS-V LINK 1 Acc) ;
	#$ Juos ášši gusto restii Finnmárkku meahcceeatnamiid nuppástuvvan geavaheamis, de ii searvit Sámedikki válljen maŋimuš stivrralahttu gii ii ovddas boazodoalu.  


#!! * **NPrfPrc** between aux and prfprc
MAP:>Nbetween (@>N) TARGET PrfPrc - NOT-@>N (-1 VFIN + ("leat"))(1 N LINK *1 PrfPrc BARRIER NOT-ADV-PCLE) ;
	#$ Son lea mannan áigodaga leamaš Sámediggeráđis ja sus lei ovddasvástádus kultuvrra ja dearvvašvuođa ovddas.


#!! * **NPrfPrc6** the verb can be to the right
MAP:>NPrfPrc6 (@>N) TARGET PrfPrc - NOT-@>N (*0 VFIN BARRIER S-BOUNDARY OR (Refl Acc))(NEGATE 0 AUX)(*1 N BARRIER NOT-A LINK NOT 0 TIME OR Prop LINK NEGATE 1 TIME - Sem/Date)(NEGATE *-1 COPULAS OR Neg BARRIER Nom OR S-BOUNDARY OR REAL-V)(NEGATE 0 TRANS-V LINK *1 Acc BARRIER NOT-NPMODADV LINK *-1 PrfPrc LINK *-1 COPULAS OR Neg BARRIER S-BOUNDARY OR REAL-V)(NEGATE 0 TRANS-V LINK 1 Ess) # LINK *1 Acc BARRIER NOT-NPMODADV LINK *-1 PrfPrc LINK *-1 COPULAS OR Neg BARRIER S-BOUNDARY OR REAL-V)
(NOT 1 PUNCT)(NEGATE 0 TRANS-V LINK *1 Acc BARRIER NOT-A)(NEGATE 0 PrfPrc LINK NOT 0 Der/Pass LINK 1 Attr)(NEGATE -1 CC LINK *-1 PrfPrc BARRIER SV-BOUNDARY LINK -1 COPULAS)(NEGATE -1 CC LINK *-1 PrfPrc LINK -1 Adv LINK -1 (Refl Acc) LINK *-1 MUITALIT) ;
	#$ Ovdamearkan oaivvilduvvon fálaldahkii lea sámi kulturdieđu lohkanbaji.
	#$ Sus lea ovddasvástádus gárrenávnnasfuolaheami spesialiserejuvvon osiin.
	#$ Son deaivvai náitalan nissona.
	#$ Seammago ovdalis namahuvvon riektehistorjjálaš ovdanbuktimis leat dán barggus maid muhtun buhtes historjjálaš dilit, ja almmuhuvvo danne joavkku čielggadeami mielddusin (2. mielddus). 
	#$ Divššohasas, gii dárbbaša guhkit áiggi ja oktiiheivehuvvon dearvvašvuođabálvalusaid, ..    
	#% Vuosttas čuokkis ii gusto daid opmodagaid luobaheapmái mat leat čuldojuvvon almmolaš juohkogávppi bokte ja mat leat biddjojuvvon huksenguovlun plánain plána- ja huksenlága mielde, dahje čuldojuvvon duktasajiide maidda lea huksejuvvon.
	#% Ingalill Olsen lohká gal iežas garrasit behtton ja healkkehan gielddarievtti duomu geažil .
	#% Dál lohká iežas garrasit behtton fylkamánnái, go oaivvilda beare unnán bohccuid ovddas ožžon buhtadusa .

#!! * **NPrfPrc7** Der/Pass, no TIME to the right
#MAP:>NPrfPrc7 (@>N) TARGET PrfPrc - NOT-@>N IF (*-1 S-BOUNDARY BARRIER NOT-NPMODADV)(0 Der/Pass)(*1 N BARRIER NOT-A LINK NOT 0 TIME);

#!! * **NPrfPrcdouble** the verb can be to the right
MAP:>NPrfPrcdouble (@>N) TARGET PrfPrc - NOT-@>N (*-1 VFIN BARRIER S-BOUNDARY OR (Refl Acc))(NEGATE 0 AUX)(1 @>N) (NEGATE *-1 COPULAS OR Neg BARRIER Nom OR S-BOUNDARY OR REAL-V) ;
	#$ Kárášjogas ohcagohtet otne njeljiin beatnagiin navdon jápmán olbmo.


#!! * **NPrfPrcCoor** coordination
MAP:>NPrfPrcCoor (@>N) TARGET PrfPrc - NOT-@>N (1 JA/DAHJE LINK 1 PrfPrc - NOT-@>N LINK *1 N BARRIER NOT-NPMOD)(NEGATE -1 Adv LINK -1 (Refl Acc) LINK *-1 MUITALIT) (NEGATE *-1 ("leat") BARRIER REAL-V OR S-BOUNDARY) ;
	#$ Ja de lei álgit fas álggus, bráhkáin ja eará gaskaboddosaš lanjain dahje báhcán ja mieskan ja bombejuvvon skuvlavisttiin.



#!! ### (@+FAUXV) and (@+FMAINV) target Neg, orrut
#!! * **+FAUXVNeg**
MAP:+FAUXVNeg (@+FAUXV) TARGET Neg (NEGATE 0 A LINK 1 CC LINK 1 A) ;
	#$ In.

#!! * **+FMAINVorrut** finite orrut
MAP:+FMAINVorrut (@+FMAINV) TARGET ("orrut") + VFIN (NEGATE *1 PrfPrc OR (Actio Ess) BARRIER SV-BOUNDARY)(NEGATE 0 PrfPrc LINK *-1 COPULAS BARRIER SV-BOUNDARY) ;
	#$ Sápmelaččat guđet orrot Norgga bealde ...
	#$ Dat orru buorre fanas.
	#$ Mun orun stuorra vilges viesus.
	#$ Oro jaska!

#!! * **FAUXVorrut** finite orrut
MAP:+FAUXVorrut (@+FAUXV) TARGET ("orrut") + VFIN-NOT-IMPRT (*1 PrfPrc OR (Actio Ess) BARRIER REAL-V OR S-BOUNDARY) ;
	#$ Nieiddat orro váibagoahtán.
	#$ Mánás orru leamen borranmiella.

#!! * **FAUXVorrut** infinite orrut
MAP:-FAUXVorrut (@-FAUXV) TARGET ("orrut") - VFIN (*1 PrfPrc OR (Actio Ess) BARRIER NOT-ADV-PCLE) ;
	#$ Mánás ii oro leamen borranmiella.


MAP:<ADVLInf (@<ADVL) TARGET Inf - ("leat") (*-1 ("gearggus") BARRIER NOT-ADV-PCLE) ; 
	#$ Sii leat gergosat nammadit lahtuid stivrii.

MAP:<ADVLInf (@<ADVL) TARGET Inf - ("leat") (*-1 ("váldit") OR ("váldot") BARRIER SV-BOUNDARY - ("de") OR COMMA) ; 
	#$ Skutervuoddji váldui de mielde dearvvašvuođaguovddážii varraiskosiid váldit.

#!! ### (@A<)  target Inf 	
#!! * **AInf** Inf 	
MAP:A<Inf (@A<) TARGET Inf ((*-1 (A Nom) BARRIER NOT-ADV LINK *-1 COPULAS BARRIER SV-BOUNDARY LINK *-1 NP-HEAD-NOM BARRIER S-BOUNDARY) OR (*-1 (A Nom) BARRIER NOT-ADV LINK *-1 COPULAS BARRIER SV-BOUNDARY LINK 1 NP-HEAD-NOM OR Prop) OR (*-1 ("vejolaš") OR ("lossat") OR ("álki") OR ("buorre") BARRIER NOT-ADV LINK *-1 COPULAS BARRIER VFIN OR FMAINV LINK *-1 HAB-ACTOR BARRIER SV-BOUNDARY OR N)) ;	
	#$ Dološ olbmot ledje hui čeahpit dulkot nieguid.

#!! * **r368**	
MAP:r368 (@A<) TARGET Inf (-1 NP-HEAD-ACC LINK -1 (A Nom) LINK *-1 ("leat") BARRIER NOT-ADV-PCLE LINK *-1 NP-HEAD-NOM BARRIER VFIN-NOT-AUX OR S-BOUNDARY);	

#MAP:dehalas (@A<) TARGET Inf (-1 DEHALAS + Nom OR ("mielas") + Nom) ;
MAP:dehalas (@A<) TARGET Inf + TRANS-V (-1 Acc LINK -1 DEHALAS + Nom) ;
MAP:dehalas (@A<) TARGET Inf (-1 A + Nom LINK *-1 BOS BARRIER WORD) ;
MAP:dehalasCoord (@A<) TARGET Inf (-1 CC LINK -1 (@A<)) ;
	#$ Dehálaš bargat dásseárvoáššiiguin.
	#$ Váttis dievdoolbmuid oažžut bargat dásseárvoáššiiguin.
	#$ Áŋgirat juoiggadit ja čuojahit rumbbuid.


#!! ### (@<SUBJ)  target Inf

#!!	* **SUBJInf** infinitive 
#	'lea suohtas oahppat'
MAP:<SUBJInf1 (@<SUBJ) TARGET Inf (NEGATE 0 AUX LINK 1 Inf)(*-1 A BARRIER WORD - Ill - Adv LINK *-1 COPULAS BARRIER NOT-ADV LINK NEGATE *-1 Nom BARRIER S-BOUNDARY)(NEGATE -1 ("mielas") LINK -1 COPULAS);
	#$ Mu mielas lea suohtas oahppat sámegiela.
	#$ Ja ii leatge buorre sutnje orrut suhkkes vuvddiid siste, gos lea liegga ilbmi.
	#$ Danne go mii orrut tunealla lahka, de lea álki isidii vuodjit tunealla go manná girdišilljui. 
MAP:<SUBJInf1b (@<SUBJ) TARGET Inf (NEGATE 0 AUX LINK 1 Inf)(*-1 A BARRIER WORD - Ill - Adv LINK -1 Adv LINK -1 COPULAS LINK NEGATE *-1 Nom BARRIER S-BOUNDARY);
	#$ Dárbbašlaš lea dikšut jávrriid ovdal go lea ekonomalaččat veara bivdit.

MAP:<SUBJInf1 (@<SUBJ) TARGET Inf (NEGATE 0 AUX LINK 1 Inf)(-1 Acc - MIELLA LINK *-1 (A Nom) BARRIER WORD - Ill - Adv LINK *-1 COPULAS BARRIER NOT-ADV LINK NEGATE *-1 Nom BARRIER S-BOUNDARY);
	#$ Raporttas galgá gozihanlávdegoddi čilget gozihandoaimmas, árvvoštallat stivrra jahkemuitalusa ja dilálašvuođaid maid dat siskkilda, ja árvvoštallat mo dát láhka doaibmá ja leago sávahahtti dan nuppástuhttit. 


#	álki lea vánddardit
#!! * **<SUBJInf2**
MAP:<SUBJInf2 (@<SUBJ) TARGET Inf (NEGATE 0 AUX LINK 1 Inf)((*-1 ("leat") BARRIER NOT-ADV-PCLE LINK -1 A LINK *-1 BOC BARRIER NOT-ADV) OR (-1 Adv LINK -1 ("leat") LINK -1 A LINK *-1 BOC BARRIER NOT-ADV));	
	#$ Vearrámus lea go nu váttis lea oažžut eret dan gova, lohká nisu, gii ii hálit namas aviisii. 
	#$ Man álki livččii de vánddardit. # her  'de' brukt som Pcle 

#!! * **r354**
MAP:r354 (@<SUBJ) TARGET Inf (NEGATE 0 AUX LINK 1 Inf)(-1 ADVLCASE LINK *-1 A BARRIER NOT-ADV LINK *-1 COPULAS BARRIER NOT-ADV LINK NEGATE *-1 Nom BARRIER S-BOUNDARY);

#	govt lei vuodjit
#!! * **<SUBJInf3**
MAP:<SUBJInf3 (@<SUBJ) TARGET Inf (-1 COPULAS LINK -1 Adv LINK -1 BOS)(NEGATE *1 REAL-V) ;	
	#$ Govt lei vuodjit Olmmáivákkis deike?

#	lea suohtas beassat	
#!! * **<SUBJInf4**
MAP:<SUBJInf4 (@<SUBJ) TARGET Inf (NEGATE -1 AUX)(*-1 A OR Sem/Perc-emo BARRIER NOT-ADV LINK NOT 0 MIELLA + Nom LINK *-1 COPULAS BARRIER NOT-NPMODADV LINK NEGATE *-1 Nom BARRIER S-BOUNDARY)(NEGATE -1 ("mielas") LINK -1 COPULAS) ;	
	#$ Mu mielas lea suohtas beassat johtit riikkas riikii ja báikkis báikái. 
	#$ Lea nana vásáhus vuodjit geainnu, erenoamážit go lea fiinna dálki.
	#% Mus lea miella máhccat ruoktot Deanu gildii.

#	mii ávkkiid lea min doppe johtit
#!! * **<SUBJInf5**
MAP:<SUBJInf5 (@<SUBJ) TARGET Inf (*-1 ("leat") BARRIER AUX LINK -1 (N Pl Acc) LINK -1 ("mii") OR ("gii") LINK 0 (Interr Sg Nom) LINK *-1 BOC BARRIER WORD);	
	#$ Mii ávkkiid lea min doppe johtit?

#	bargu lei váldit
#!! * **<SUBJInf6**
MAP:<SUBJInf6 (@<SUBJ) TARGET Inf ((*-1 COPULAS BARRIER NOT-ADV-PCLE LINK -1 Ess LINK *-1 BOS BARRIER V) OR (-1 Ess - Actio LINK *-1 COPULAS BARRIER NOT-ADV-PCLE LINK *-1 BOC OR COMMA BARRIER NOT-ADV))(NEGATE -1 (N Ess) LINK *-1 ("šaddat") BARRIER NOT-A) ; # (NEGATE *1 REAL-V) ;
	#$ Sin bargun lei váldit fáŋgan Gonagasa.
	#% Sutnje dát lei dušše váivves geatnegasvuohta, man šattai eadnin bargat nieiddas buorrin.

#	vuolgit lea suohtas
#!! * **SUBJ>Inf**
MAP:SUBJ>Inf (@SUBJ>) TARGET Inf (1 COPULAS + VFIN)(*2 A BARRIER NOT-ADV);
	#$ Vuolgit lea suohtas.

#!! ### (@<SPRED)  target Inf

#!!	* **SPREDInf** infinitive, oaidnit dego
MAP:<SPREDoaidnit (@<SPRED) TARGET Inf (0 ("oaidnit") LINK *-1 ("leat") BARRIER NOT-ADV-PCLE LINK *-1 (N Nom) BARRIER WORD - ADVLCASE - Adv)(*1 ("dego") OR go BARRIER SV-BOUNDARY)  ; 
	#$ Njunni lei oaidnit dego unna buđehaš.

#!!	* **SUBJInf** infinitive 
#	ulbmil lea oahppat
MAP:<SUBJInf (@<SUBJ) TARGET Inf (*-1 ("leat") BARRIER NOT-ADV-PCLE LINK *-1 (N Nom) BARRIER WORD - ADVLCASE - Adv)  ;
	#$ Gurssa ulbmil lea oahppat goarrut Ivgu gávtti.
	#$ Dan ulbmil das lea nannet oktavuođa ja mearridit vejolaš ovttasbargosurggiid mat leat ealáhusa oasálaččaide ávkin.

MAP:<SUBJInfcoor1 (@<SUBJ) TARGET Inf (0 ("oažžut") LINK -1 JA/DAHJE OR COMMA LINK *-1 Inf LINK *-1 ("oažžut" @<SUBJ))(*1 Inf)  ;
	#$ Dávjá váhnemat eai dušše gallestallan mánáid, muhto oahpaheaddji, ee. lei earánin oažžut su dulkot eiseválddiid breavaid ja oažžut veahki vástidit dáid.
MAP:<SUBJInfcoor2 (@<SUBJ) TARGET Inf (-1 JA/DAHJE OR COMMA LINK *-1 (Inf @<SUBJ) BARRIER SV-BOUNDARY)  ;
	#$ Ulbmil čoahkkimiiguin lea oažžut buoret oktavuođa EU:i, ovddidit sámepolitihka luondduvalljodatgeavaheami, davviguovlluid ovdánahttinplánaid, Davviriikkalaš sámekonvenšuvnna ja ruoššabeale sámiid dilálašvuođa ja riektadili ektui.

#!!	* **NInf** Inf 		
MAP:N<Infmuitalit (@N<) TARGET Inf (-1 (N Loc) LINK *-1 ("muitalit") BARRIER FMAINV);	
	#$ Dás son muitala iežas vásáhusaid birra Cizáš mánáidgárddis Oslos ja muđuid rahčamušain oažžut mánáguovtto guovttegielalažžan.


#!! ### (@<ADVL)  target Inf, Actio Ess

#!!	* **ADVLActioEss** Actio Ess 
#	fitnat doppe oahppamen
MAP:<ADVLActioEss (@<ADVL) TARGET (Actio Ess)(*-1 ("fitnat") OR ("mannat") BARRIER S-BOUNDARY OR MAIN-V OR Nom); 
	#$ Skuvlavázzit leat fitnan Sihčajávrris oahppamen mot sáhttá birget luonddus.


MAP:<OBJessInf (@<OBJ) TARGET Inf (-1 DEHALAS + Ess LINK *-1 ("atnit") BARRIER NOT-ADV-PCLE) ; 
	#$ Sámediggi bealistis anii dehálažžan oažžut čađahuvvot eará láhkarievdadusaid.

#!! * **+FAUXVboahtit** boahtit as AUX      
MAP:+FAUXVboahtit (@+FAUXV) TARGET ("boahtit") + VFIN-NOT-IMPRT (*1 Inf BARRIER NOT-ADV-PCLE)(NEGATE 0 ("boahtit") LINK *1 Nom OR Ess BARRIER NOT-ADV-PCLE) ;
	#$ Na geasset go bođii diehtit ahte ii beasa skuvlii?

#!! * **+FAUXVboahtit**  boahtit coming before the mainverb
MAP:+FAUXVboahtit (@+FAUXV) TARGET VFIN-NOT-IMPRT + ("boahtit") (NOT 0 Imprt LINK *1 AUX + Inf OR PERCEPTION-V + Inf OR MOVEMENT-V + Inf BARRIER NOT-ADV) ;
	#$ Nuppi bealis lea vejolaš bidjat ráji oarjelii dainna lágiin ahte caggá Sirgása bohccuid beassamis Gjerdalenii, mii boahtá šaddat Hápmirii mávssolaš guohtunguovlun boahtteáiggis.
	#$ Jus dulvvi niegadat, de boađát gullat unohis šlárvaságaid.

#!! * **-FAUXVboahtit** boahtit as AUX      
MAP:-FAUXVboahtit (@-FAUXV) TARGET ("boahtit") + ConNeg (*1 Inf BARRIER NOT-ADV-PCLE)(NEGATE 0 ("boahtit") LINK *1 Nom OR Ess BARRIER NOT-ADV-PCLE) ;
	#$ Dál eat boađe nu vánddardit guovllus


#!!	* **ADVLInf** Inf 
#	vulggii baikit (add Der/Pass to the rule?)
MAP:<ADVLInf (@<ADVL) TARGET Inf - ("leat") (*-1 MOVEMENT-V - AUX OR ("atnit") OR ("geavahit") OR ("jorgalit") OR ("bidjat") OR ("movttiidahttit") OR ("guođđit") OR ("báhcit") OR ("bisánit") OR ("čohkánit") OR ("gieldit") + Der/Pass OR ("čohkkedit") BARRIER S-BOUNDARY OR COMMA OR REAL-V OR Ess LINK NOT 0 OLLU OR AUX OR SUBJ OR OBJ OR FAUXV)(NEGATE -1 CC)(NEGATE 0 AUX OR PERCEPTION-V OR ("diehtit") LINK *-1 ("boahtit") BARRIER NOT-ADV-PCLE)(NEGATE 0 Adv); 
	#$ Stálus bođii baikahoahppu, ja son guđii seahka ja vulggii baikit. 
	#$ Boađe mu lusa geahččat ođđa govaid! 
	#$ Boađe sisa gáfe juhkat. 
	#$ Viečča vieljat boradit!
	#$ Sii bisánedje vuordit.

MAP:<ADVLInf (@<ADVL) TARGET Inf + TRANS-V (-1 Acc LINK -1 ("váldit") LINK *-1 BOS BARRIER WORD)(NEGATE 0 Adv)(*1 Acc BARRIER NOT-NPMOD); 
MAP:<ADVLInf (@<ADVL) TARGET Inf + TRANS-V (-1 Ess LINK -1 Acc LINK -1 ("váldit") LINK *-1 BOS BARRIER WORD)(NEGATE 0 Adv)(*1 Acc BARRIER NOT-NPMOD); 
	#$ Váldán advokáhta čielggadit iežas vuoigatvuođaid  ❡ .
	#$ Váldán advokáhta veahkkin oažžut vástádusa gielddas  ❡ .
	#% Dáhtut go min gaikut dan eret?

#!!	* **ADVLInf** Inf 
#	vulggii baikit
MAP:-f<ADVLInf (@-F<ADVL) TARGET Inf - ("leat") (*-1 MOVEMENT-V - AUX OR ("atnit") OR ("geavahit") OR ("bidjat") OR ("movttiidahttit") OR ("guođđit") BARRIER S-BOUNDARY OR COMMA OR REAL-V OR Ess LINK 0 SUBJ OR OBJ)(NEGATE -1 CC)(NEGATE 0 AUX OR PERCEPTION-V OR ("diehtit") LINK *-1 ("boahtit") BARRIER NOT-ADV-PCLE)(NEGATE -1 ("boahtit")); 
	#$ Lea erenoamáš dehálaš movttiidahttit sámegielat doaktáriid bargat gielddain.


#!!	* **ADVLInf** Inf
MAP:<ADVLInf (@<ADVL) TARGET Inf (-1 ADVLCASE LINK -1 REAL-V - AUX - @>N)(NEGATE 0 ("leat")) ;
	#$ Veahkehit miljovdnabeliin bosádit heakka ásaheapmái.
	#% – Ii sáhte guođohemiin eastadit goaskinvahágiid. 
	#% Finnmárkkuopmodat sáhttá dárkileappot albmaduvvon guovlluide mearridit gáržžádusaid.


#!!	* **ADVLInf** Inf in questions
MAP:<ADVLInf (@<ADVL) TARGET Inf (-1 Nom LINK *-1 MOVEMENT-V BARRIER NOT-PCLE LINK -1 BOC)(NEGATE 0 ("leat")) ;
	#$ Manaigo Biret-Elle viežžat boastta? 

#!!	* **r360x**
MAP:r360x (@<ADVL) TARGET Inf IF (-1 ADVLCASE LINK NOT 0 NP-HEAD-NOM LINK *-1 NOT-AUX-V BARRIER NOT-NPMOD LINK NOT 0 TRANS-V OR @>N)(NEGATE 0 ("leat"))(*-2 VFIN BARRIER S-BOUNDARY);

MAP:veahkkiF<ADVL (@-F<ADVL) TARGET Inf (-1 ("veahkki" Acc) LINK -1 (TV Inf @<SUBJ)) ;


MAP:veahkki<ADVL (@<ADVL) TARGET Inf IF (-1 (".*veahkki"r) + Acc OR Sem/Money + Acc LINK *-1 NOT-AUX-V + TRANS-V BARRIER NOT-NPMOD)(NEGATE 0 ("leat")) ;
MAP:veahkki<ADVL (@<ADVL) TARGET Inf IF (-1 Loc LINK -1 (".*veahkki"r) + Acc OR Sem/Money + Acc LINK *-1 NOT-AUX-V + TRANS-V BARRIER NOT-NPMOD)(NEGATE 0 ("leat")) ;
MAP:veahkki<ADVL (@<ADVL) TARGET Inf IF (-1 ("veahkki") OR Sem/Money OR (".*gelbbolašvuohta"r))(NEGATE 0 ("leat"))(NEGATE *-1 AUX BARRIER SV-BOUNDARY) ;
	#$ Dat lei earánin oažžut dulkot eiseválddiid breavaid ja oažžut veahki vástidit dáid.
	#$ Sámegieloahput mat addet sámegielgelbbolašvuođa lohkagoahtit sámegiela dahje eará oahpuid bachelordásis.
	#$ Sii sáhttet ohcat doarjaga Gáivuona suohkanis searvat gursii.

#	lea dálki vuolgit
#!!	* **<ADVLInf**
MAP:<ADVLInf (@<ADVL) TARGET Inf (-1 (N Nom) LINK NOT 0 GEN-CONCEPT OR MIELLA OR (A Nom) OR Inf OR (Der/NomAct) OR REAL-V OR Px + Nom LINK *-1 ("leat") BARRIER SV-BOUNDARY)(NEGATE 0 ("leat")) ;
	#$ Odne lea goalki, nu ahte lea buorre dálki vuolgit fávlái.
	#$ Dán golbmasis ii leat ruhta oastit bileahta.  
	#$ Mus lea biergu vuoššat. 
	#$ Mus livčče guolit viežžat.  # Nickel s. 471
	#$ Son ii áiggo gal dadjat ledje go Álttás dohkálaš ákkat hilgut doarjagiid.

#!!	* **InfdAdvl**
MAP:InfdAdvl (@<ADVL) TARGET Inf (-1 (N Nom) LINK NOT 0 GEN-CONCEPT OR MIELLA OR (A Nom) OR Inf OR (Der/NomAct) OR REAL-V OR Px + Nom LINK *-1 CC BARRIER NOT-NPMODADV LINK -1 (N Nom) LINK *-1 COPULAS BARRIER NOT-NPMOD)(NEGATE 0 ("leat")) ;

#!!	* **InfADVL**	
MAP:InfADVL (@<ADVL) TARGET Inf (-1 (N Nom) LINK NOT 0 GEN-CONCEPT OR MIELLA OR (A Nom) OR Inf OR (Der/NomAct) OR REAL-V OR Px + Nom LINK *-1 ("leat") BARRIER NOT-NPMOD)(0 TRANS-V LINK NEGATE *1 Acc BARRIER NOT-NPMOD)(NEGATE 0 ("leat")) ;

#!!	* **ADVLInf** Inf 		
MAP:F<ADVLInfReksj1 (@-F<ADVL) TARGET Inf ((*-1 REKS-V BARRIER NOT-ADV LINK NOT 0 PrfPrc LINK -1 COPULAS) OR  (0 TRANS-V LINK -1 Acc LINK *-1 REKS-V BARRIER NOT-ADV LINK NOT 0 PrfPrc LINK -1 COPULAS)) ; 


#!! ### @-F<OBJ  target Inf 

#!!	* **FOBJInf** Inf 
MAP:-F<OBJInf (@-F<OBJ) TARGET Inf ((-1 TRANS-V + Inf LINK NOT 0 AUX LINK -1 ("leat")) OR (-1 TRANS-V + Inf LINK NOT 0 AUX LINK -1 MIELLA LINK *-1 ("leat") BARRIER NOT-ADV-PCLE) OR (-1 Acc LINK -1 TRANS-V + Inf LINK NOT 0 AUX LINK -1 ("leat") LINK -1 MIELLA OR GEN-CONCEPT) OR (-1 TRANS-V + Inf LINK NOT 0 AUX LINK -1 Ess LINK *-1 OPRED-V BARRIER NOT-ADV-PCLE)) ;
	#$ Gurssa ulbmil lea oahppat goarrut Ivgu gávtti.
	#$ Ulbmil lea oažžut nuppi boagustit.

MAP:-F<OBJInf (@-F<OBJ) TARGET Inf ((-1 TRANS-V + Inf LINK 0 SUBJ OR SPRED) OR (-1 Acc LINK -1 TRANS-V + Inf LINK 0 SUBJ OR SPRED)) ;

	#$ Giellatekno bargá dahkat dákkár prográmmaid olámuddui sámegielaide, ja maiddái dahkat álkibun eará vehádatgielaide ráhkadit dákkár prográmmaid.

MAP:-F<OBJInf4 (@-F<OBJ) TARGET Inf (*-1 TRANS-V + Inf BARRIER NOT-ADV-PCLE LINK -1 Acc LINK NOT 0 TIME OR Rel LINK *-1 TRANS-V BARRIER SV-BOUNDARY OR AUX LINK NOT 0 AUX OR Adv OR N LINK NOT 0C Ger OR Imprt)(NEGATE 0 Pl3 LINK *-1 TRANS-V LINK 0 (Pl Nom)) ; 



#!! ### (@N<)  target Inf, Actio Ess

#!!	* **NInf** Inf 		
MAP:N<Inf1 (@N<) TARGET Inf (-1 MIELLA + Nom OR MIELLA + Ess LINK NOT 0 PrfPrc LINK *-1 ("leat") OR ("čađahit") OR ("ollašuvvat") BARRIER VFIN OR FMAINV LINK *-1 NP-HEAD-NOM OR PRESENT-MARKER OR PAST-MARKER OR Loc OR S-BOUNDARY OR BOS BARRIER VFIN-NOT-AUX OR S-BOUNDARY)(NEGATE 0 TRANS-V LINK -1 NP-HEAD-NOM LINK NOT 0 GEN-CONCEPT LINK *-1 COPULAS BARRIER NOT-NPMOD LINK -1 Loc LINK 0 HUMAN OR Pers LINK *-1 BOS BARRIER NOT-NPMOD);	
	#$ Dál lea Bireha vuorru geahččat sávzzaid.
	#$ Sis lea nana árbevierru veahkehit ieš iežas persovnnalaš fierpmádagaid vehkiin.
	#$ Lea geatnegasvuohta ovddidit buot mánáid beroštumiid servodagas.

MAP:HLN<Infheadliner (@N<) TARGET Inf (-1 MIELLA) (*1 HEADLINEMARK) (NEGATE 0 TRANS-V LINK -1 NP-HEAD-NOM LINK NOT 0 GEN-CONCEPT LINK *-1 COPULAS BARRIER NOT-NPMOD LINK -1 Loc LINK 0 HUMAN OR Pers LINK *-1 BOS BARRIER NOT-NPMOD);	
	#$ Stuora gudni oažžut dán bálkkašumi ❡ .


#!!	* **N<Inf2**	
MAP:N<Inf2 (@N<) TARGET Inf (-1 NP-HEAD-ACC LINK -1 MIELLA + Nom LINK NOT 0 REAL-V LINK *-1 ("leat") BARRIER REAL-V LINK *-1 NP-HEAD-NOM OR PRESENT-MARKER OR PAST-MARKER OR Loc BARRIER VFIN-NOT-AUX OR S-BOUNDARY);	
	#$ Dus ii leat lohpi dan dahkat!	

#!!	* **N<Inf2Gen**	
MAP:N<Inf2Gen (@N<) TARGET Inf (-1 NP-HEAD-ACC LINK -1 Gen LINK -1 MIELLA + Nom LINK NOT 0 REAL-V LINK *-1 ("leat") BARRIER REAL-V LINK *-1 NP-HEAD-NOM OR PRESENT-MARKER OR PAST-MARKER OR Loc BARRIER VFIN-NOT-AUX OR S-BOUNDARY);	
	#$ Divššohasas lea vuoigatvuohta iežas journála geahččat oktan mildosiiguin.

#!!	* **N<Inf3**
MAP:N<Inf3 (@N<) TARGET Inf (-1 MIELLA + Acc LINK *-1 TRANS-V BARRIER Acc OR REAL-V OR SV-BOUNDARY LINK NOT 0 AUX) ; 	
	#$ Suohkanat addet váhnemiidda vejolašvuođa bidjat mánáideaset dakkár skuvllaide.
	#$ Máŋgga sámi guovllus eiseválddit eai váldán ovddasvástádusa lágidit bálvalusaid.	

#!!	* **N<Inf4**
MAP:N<Inf4 (@N<) TARGET Inf (-1 MIELLA + Nom LINK -1 BOS) ; 	
	#$ Riekti oassálastit luondduriggodagaid hálddašeamis sáhttá čađahuvvot dakko bokte ahte Sámediggi nammada áirasiid oktasaš stivrenorgánaide, nu movt Finnmárkoopmodahkii ja guovllu boraspirelávdegottiide.	

#!! * **N<Infcoor**
MAP:N<Infcoor (@N<) TARGET Inf (-1 JA/DAHJE LINK *-1 (@N<) BARRIER REAL-V) ; 
	#$ Dus lea vuoigatvuohta oažžut oahpahusa mearriduvvon oahppoplána mielde, muhto dus lea seammás geatnegasvuohta oassálastit aktiivvalaččat ulbmiliid juksamii ja oassálastit iežat oahppanbarggu plánemii ja árvvoštallamii.


#!!	* **N<Inf2**	
MAP:N<Inf (@<ADVL) TARGET Inf (-1 NP-HEAD-ACC + MIELLA LINK NEGATE -1 AUX) ;	 # for å få supinum i smj, er ikke helt sikker..
	#$ Sámediggi áigu oččodit sámi mánáide individuála vuoigatvuođa oažžut sámi mánáidgárdefálaldaga.
	#% Dat gohčoduvvo vahátjávkadeapmin (skadefelling), ja dasa galgá sierralobi ohcat.


#!! ### (@<ADVL)  target Inf, Actio Ess

LIST NOT-ADVL-V = "jáhkkit" "hárjánit" "nákcet" ;

#!!	* **ADVLInf** Inf 		
MAP:<ADVLInfReksj1 (@<ADVL) TARGET Inf ((*-1 REKS-V - NOT-ADVL-V BARRIER NOT-ADV OR Subqst LINK NOT 0 FAUXV) OR (-1 REAL-V + IV - AUX - NOT-ADVL-V LINK NOT 0 FAUXV) OR (0 TRANS-V LINK -1 Acc LINK -1 REKS-V - NOT-ADVL-V LINK NOT 0 FAUXV)) ; 
	#$ Go beassážiid leat čáppa dálkkit ja beaivvádat, de olbmot liikojit čuoigat meahcis dahje vuodjit mohtorgielkkáin. 
	#$ Go áŋgiruššá oažžut sámi mánáidgárdefálaldaga maiddái dáža mánáide, de nannešii dat maiddái sámi mánáid iešdovddu.

#!!	* **<ADVLInfReksj2**
MAP:<ADVLInfReksj2 (@<ADVL) TARGET Inf (-1 CC LINK *-1 Inf BARRIER REAL-V LINK *-1 REKS-V - ("jáhkkit") BARRIER NOT-ADV LINK *-1 Nom BARRIER S-BOUNDARY);
	#$ Go beassážiid leat čáppa dálkkit ja beaivvádat, de olbmot liikojit čuoigat meahcis dahje vuodjit mohtorgielkkáin. 

#!!	* **<ADVLInfReksj3**
MAP:<ADVLInfReksj3 (@<ADVL) TARGET Inf (-1 Acc LINK NOT 0 HUMAN LINK *-1 REKS-V - ("jáhkkit") BARRIER NOT-NPMOD) ; 
	#$ Son lea dakkár mii ii beroš lága čuovvut!

#!! * **ADVLActioEss** Inf 		
MAP:<ADVLActioEss (@<ADVL) TARGET (Actio Ess)(*-1 ("oidnot") OR ("gullot") BARRIER NOT-ADV-PCLE) ;
	#$ Olbmáguovttos jurddašišgođiiga: - Movt dat galgá leat go ii oidno goassege máksimen ja vuovdi cealká ahte lea jo máksojuvvon.

#MAP:TEST (@<OBJ) TARGET Inf IF (*-1 VFIN LINK 0 FMAINV) ; 

#!! ### (@<OBJ)  target Inf, Actio Ess, PrfPrc 

#!!	* **ADVLInf** bargat 		
MAP:<ADVLInf1 (@<ADVL) TARGET Inf (*-1 ("bargat") BARRIER SV-BOUNDARY OR AUX OR Acc OR CC OR COMMA LINK NOT 0 AUX OR Adv OR Acc LINK NOT 0C Ger)(NEGATE 0 Pl3 LINK *-1 TRANS-V LINK 0 (Pl Nom))(NEGATE -1 CC LINK -1 Inf LINK -1 AUX)(NEGATE 0 Inf LINK *-1 AUX BARRIER REAL-V) ; 


#!!	* **OBJInf** Inf 		
MAP:<OBJInf1 (@<OBJ) TARGET Inf (*-1 TRANS-V BARRIER NOT-ADV-PCLE OR COMMA LINK NOT 0 AUX OR Adv OR Acc LINK NOT 0C Ger)(NEGATE 0 Pl3 LINK *-1 TRANS-V LINK 0 (Pl Nom))(NEGATE -1 CC LINK -1 Inf LINK -1 AUX)(NEGATE 0 Inf LINK *-1 AUX BARRIER REAL-V) ; 
	#$ Mun hálidan oahppat goarrut gávtti.
	#$ Oahpa goarrut gávtti.
	#$ Go ollet Sáhpánvári ala, de muitte čuovvolit bálgá mii manná olgeš guvlui.
	#$ Anán buorebun odne vuolgit go ihttážii vuordit. 
	#$ Duosttan go dahkat dan?
	#$ Olbmot dattetge dinejedje veaháš go vuvde dujiid, eaige dárbbašan oastit ávdnasiid duddjomii.
	#$ Oahpa goarrut gávtti ja gođđit ránu.
	#% Áigugo fas skuvlii álgit?

MAP:<OBJInf1 (@<OBJ) TARGET Inf (-1 Nom LINK *-1 TRANS-V BARRIER NOT-NPMODADV OR @CVP LINK NOT 0 AUX OR Adv OR Acc LINK NOT 0C Ger) ; 
	#$ Raporttas bokte dáhttu bargojoavku čalmmustahttit Sámedikki.
	#$ Dan maŋŋá duste maiddái eará dearvvašvuođabargit hállat dan birra.

#!!	* **OBJInf** Inf 		
MAP:<OBJInfforanVFIN (@OBJ>) TARGET Inf (1 TRANS-V + VFIN)(-1 BOC) ; 
	#$ Galgabehtet muitit, ahte báhčit oažžu dáppe meahcis...


#!!	* **<OBJInf2**
MAP:<OBJInf2 (@<OBJ) TARGET Inf + IV (*-1 HAB-ACTOR + Acc BARRIER REAL-V OR S-BOUNDARY LINK *-1 TRANS-V BARRIER NOT-NPMODADV LINK NOT 0 (A Attr) OR MOD-ASP OR Neg OR COPULAS LINK *-1 Nom)(NEGATE *-1 CC BARRIER VFIN LINK *-1 Inf BARRIER VFIN LINK *-1 AUX BARRIER NOT-ADV-PCLE LINK 0 VFIN)(NEGATE 0 Inf LINK *-1 MOD-ASP OR Neg OR COPULAS BARRIER REAL-V);
	#$ Mánnávuođas son vásihii dulvademiid ja eará luonddubillistemiid mat leat ožžon su čállit divttaid. 
	#$ Mun háliidan mánnán boahtit ruoktot! 

	# The last constraint is for elliptic phrases.

#!!	* **<OBJInf3**
MAP:<OBJInf3 (@<OBJ) TARGET Inf (*-1 HAB-ACTOR + Acc BARRIER NOT-A-ADV LINK *-1 TRANS-V BARRIER NOT-NPMOD OR @CVP LINK NOT 0 AUX OR (A Attr) LINK 0 VFIN OR Inf)(NEGATE *-1 CC BARRIER VFIN LINK *-1 Inf BARRIER VFIN LINK *-1 AUX BARRIER NOT-ADV-PCLE LINK 0 VFIN);
	#$ Dáhto mánát boahtit ruoktot! 

#!!	* **1** maps <OBJ to Inf if -1 TV etc.
MAP:1 (@<OBJ) TARGET Inf (*-1 TRANS-V BARRIER SV-BOUNDARY OR AUX OR Acc OR COMMA LINK NOT 0 AUX LINK 0 PrfPrc OR ConNeg LINK -1 Neg OR COPULAS) ; 

#!!	* **<OBJInf4**
MAP:<OBJInf4 (@<OBJ) TARGET Inf (-1 Acc LINK NOT 0 TIME OR Rel LINK *-1 TRANS-V BARRIER SV-BOUNDARY OR AUX LINK NOT 0 AUX OR Adv OR N LINK NOT 0C Ger OR Imprt)(NEGATE 0 Pl3 LINK *-1 TRANS-V - DADJAT LINK 0 (Pl Nom)) ; 
	#$ Eadni oahpaha nieidasis beaskka goarrut.


MAP:<OBJInfAux (@<OBJ) TARGET Inf (-1 Acc LINK NOT 0 TIME OR Rel LINK *-1 TRANS-V BARRIER SV-BOUNDARY OR AUX LINK NOT 0 AUX - ("sihtat") OR Adv OR N LINK NOT 0C Ger OR Imprt)(NEGATE 0 Pl3 LINK *-1 TRANS-V LINK 0 (Pl Nom)) ; 
	#$ Soai siđaiga dávjá Liná lohkat jitnosit go sis lei lohkanhárjehallan.

# Coordination
#!!	* **<OBJInfCoord1**
MAP:<OBJInfCoord1 (@<OBJ) TARGET Inf (*-1 CC BARRIER NOT-ADV LINK *-1 Inf BARRIER REAL-V LINK *-1 NOT-AUX-V BARRIER NOT-ADV LINK NOT 0 Imprt OR AUX) ; 
# LINK *-1 Nom BARRIER S-BOUNDARY);
	#$ Son boahtá stálljii ja veahkeha čorget ja biebmat heasttaid.
	#$ Mun hálidan oahppat goarrut gávtti ja gođđit ránu.

#MAP:<OBJInfCoord3 (@<OBJ) TARGET Inf (*-1 CC BARRIER NOT-ADV LINK *-1 (Inf @<OBJ) BARRIER REAL-V)(NEGATE 0 VFIN + AUX LINK *1 Inf BARRIER SV-BOUNDARY)  ; 
MAP:<OBJInfCoord3 (@<OBJ) TARGET Inf (*-1 CC OR COMMA BARRIER SV-BOUNDARY OR REAL-V LINK *-1 (Inf @<OBJ) BARRIER SV-BOUNDARY OR REAL-V)(NEGATE 0 VFIN + AUX LINK *1 Inf BARRIER SV-BOUNDARY)  ; 
	#$ Giellatekno bargá dahkat dákkár prográmmaid olámuddui sámegielaide, ja maiddái dahkat álkibun eará vehádatgielaide ráhkadit dákkár prográmmaid.
	
	#$ Go skuvla nogai sullii geassemánu 1. beaivve, de barge internáhttaoahpaheaddjit ijat beaivái láhkket ohppiid bevddiid, oljet láhttiid ja rádjat buot skuvlastohpogálvvuid ja dávviriid keallirlatnjii.


#!!	* **<OBJInfCoord2**
MAP:<OBJInfCoord2 (@<OBJ) TARGET Inf (-1 CC LINK *-1 Inf BARRIER REAL-V LINK *-1 NOT-AUX-V BARRIER NOT-ADV LINK 0 Imprt OR AUX LINK *-1 BOC BARRIER WORD);
	#$ Oahpa goarrut gávtti ja gođđit ránu.	  

#!! * **OBJActioEss** Inf 		
MAP:<OBJActioEss1 (@<OBJ) TARGET (Actio Ess) (*-1 Acc BARRIER NOT-ADV-PCLE LINK NOT 0 TIME LINK *-1 TRANS-V + PERCEPTION-V OR DADJAT BARRIER SV-BOUNDARY OR AUX LINK NOT 0 AUX OR Adv OR N LINK NOT 0C Ger OR Imprt);
	#$ De oinnii olbmá čohkkámin tuolloviesus.

#!!	* **<OBJActioEss2**
MAP:<OBJActioEss2 (@<OBJ) TARGET (Actio Ess)(*-1 HAB-ACTOR + Acc OR Sem/Hum + Acc BARRIER NOT-ADV-PCLE LINK *-1 TRANS-V + PERCEPTION-V OR DADJAT BARRIER SV-BOUNDARY)(NEGATE 0 AUX LINK 1 Inf);
	#$ Oađidettiin gullen olbmo boahtimin.

#!! * **OBJPrfPrc** PrfPrc 			
#MAP:<OBJPrfPrc1 (@<OBJ) TARGET PrfPrc IF ((*-1 Acc BARRIER NOT-ADV LINK *-1 VERBAL-ACTIVITY-V OR PERCEPTION-V BARRIER NOT-NPMODADV) OR (*-1 Acc BARRIER NOT-ADV LINK -1 Nom LINK *-1 VERBAL-ACTIVITY-V OR PERCEPTION-V BARRIER NOT-NPMODADV));	
MAP:<OBJPrfPrc1 (@<OBJ) TARGET PrfPrc IF ((*-1 Acc BARRIER NOT-ADV LINK *-1 VERBAL-ACTIVITY-V OR PERCEPTION-V OR DADJAT BARRIER SV-BOUNDARY) OR (*-1 Acc BARRIER NOT-ADV LINK -1 Nom LINK *-1 VERBAL-ACTIVITY-V OR PERCEPTION-V OR DADJAT BARRIER NOT-NPMODADV));	
	#$ Sii lohket fatnasa gopmánan.
	#$ Olbmot muitaledje juo du vuolgán.   

MAP:<OBJPrfPrcLoc (@<OBJ) TARGET PrfPrc IF (-1 Loc + Sem/Org LINK *-1 DADJAT BARRIER SV-BOUNDARY);		
	#$ NSR Lulli-Sámi sámediggeáirras, Jarle Jonassen, lohká sin iežaset bellodagas diehtán ahte Sámeálbmotfoandda ruđat ledje sihkkaraston stáhtaobligašuvdnan ja dan bokte leat ožžon reanttuid Sámediggái.

MAP:<OBJPrfPrcCoor (@<OBJ) TARGET PrfPrc IF (-1 CC LINK -1 (PrfPrc @<OBJ) );	


#!!	* **<OBJPrfPrc2**	
MAP:<OBJPrfPrc2 (@<OBJ) TARGET PrfPrc IF (*-1 Acc BARRIER NOT-ADV LINK -1 Acc LINK *-1 VERBAL-ACTIVITY-V BARRIER NOT-NPMODADV)(0 TRANS-V);
	#$ Son logai oappás dan bargan.




MAP:<OBJ (@<OBJ) TARGET Inf IF (*-1 CC BARRIER NOT-ADV LINK *-1 (V @<OBJ) BARRIER REAL-V OR SV-BOUNDARY) ;	
	#$ Tretnes mielas lea áibbas endorii go Bargiidbellodaga parlamenttalaš jođiheaddji ja nubbinjođiheaddji Helga Pedersen čuoččuhii NRK Sámi Radios Bargiidbellodaga bargan eanemusat sámeáššiiguin ja nu mahkáš leat njunnošis sámeáššiin.



#!! ### (@+FMAINV) and (@+FAUXV) and (@-FAUXV)

#!! * **+FMAINVaux** AUX-OR-MAIN verbs     
MAP:+FMAINVaux (@+FMAINV) TARGET VFIN IF (0 AUX-OR-MAIN + TV LINK NOT 0 ("áigut") LINK *1 HAB-ACTOR + Acc BARRIER NP-BOUNDARY LINK 1 Inf + IV) ;
	#$ Mun háliidan mánnán boahtit ruoktot!
	#$ Ruoŧa hállehus háliida min veahkehit. (ii galgga deaivat dan)

#!! * **+FAUXVcop** AUX  COPULAS     
MAP:+FAUXVcop (@+FAUXV) TARGET COPULAS + Cond (*1 Inf BARRIER SV-BOUNDARY OR AUX OR MIELLA OR DEHALAS LINK NOT 0 ADVL OR OBJ) (NEGATE 0 ("šaddat") LINK *1 Nom OR Ess BARRIER NOT-ADV-PCLE LINK NOT 1 Inf) ;



#!! * **+FMAINVcop** COPULAS verbs     
MAP:+FMAINVcop (@+FMAINV) TARGET VFIN + COPULAS (*1 Nom OR Ess BARRIER NOT-ADV-PCLE LINK NOT 0 (Actio Ess) LINK 1 Inf)(NEGATE 0 ("šaddat") LINK *1 (N Ess) BARRIER NOT-A LINK 1 Inf) ;
MAP:+FMAINVcop (@-FMAINV) TARGET PrfPrc + COPULAS (*1 Nom OR Ess BARRIER NOT-ADV-PCLE LINK NOT 0 (Actio Ess) LINK 1 Inf);
	#$ Lei váttis vuodjit odne.
	#$ Leamaš váttis ipmirdit dan.
	#% Sutnje dát lei dušše váivves geatnegasvuohta, man šattai eadnin bargat nieiddas buorrin.

#!! * **+FAUXVaux** AUX  verbs     
MAP:+FAUXVaux (@+FAUXV) TARGET AUX + VFIN-NOT-IMPRT (NEGATE 0 ("leat"))(*1 Inf BARRIER SV-BOUNDARY OR AUX LINK NOT 0 ADVL OR OBJ) (NEGATE 0 ("šaddat") LINK *1 Nom OR Ess BARRIER NOT-ADV-PCLE LINK NOT 1 Inf) ;
	#$ Máhtte áigu heaitit vuola juhkamis.
	#$ Dalle galget miesit leat merkejuvvon eaiggáda merkii.  
	#$ Don šattat vel joksat Sirpmás gohcciid.  
	#$ Mu mielas lea suohtas beassat johtit riikkas riikii ja báikkis báikái.
	#$ Sutnje dát lei dušše váivves geatnegasvuohta, man šattai eadnin bargat nieiddas buorrin.


#!! * **-FAUXVaux** AUX verbs     
MAP:-FAUXVaux (@-FAUXV) TARGET AUX + Inf (NOT -1 HAB-ACTOR + Loc)(*-1 AUX BARRIER REAL-V OR S-BOUNDARY)(*1 Inf BARRIER REAL-V OR S-BOUNDARY OR (A Nom) OR MIELLA OR (N Ess) OR (A Ess)) ;
MAP:-FAUXVaux (@-FAUXV) TARGET REAL-COPULAS + Inf (NOT -1 HAB-ACTOR + Loc)(*-1 AUX BARRIER REAL-V OR S-BOUNDARY)(*1 PrfPrc - @>N BARRIER REAL-V OR S-BOUNDARY OR (A Nom) OR MIELLA) ;
	#$ Don fertet máhttit čilget niegu veardádusaid, ovdalgo sáhtát dan dieđu ipmirdit.
	#$ Dalle galget miesit leat merkejuvvon eaiggáda merkii.  
	#$ Čuollun sáhttá leat dušše okta fierbmi gurgaluvvon.
	#% Min ulbmil lea ahte galgá leat vejolaš eallit mielkebuvttadandietnasiin fylkkas, deattasta Tretnes.

#!! * **+FMAINVcopInfconstr** leat before Inf     
MAP:+FMAINVcopInfconstr (@+FMAINV) TARGET ("leat") + VFIN (NEGATE *-1 PrfPrc OR (Actio Ess) BARRIER SV-BOUNDARY OR COMMA)(*1 Inf BARRIER REAL-V) ;	
	#$ Mis lea cahkkehit dola.  
	#$ Govt lei vuodjit...
	#$ Gean vuorru lea dál bálkestit birccu?  


#  __+FMAINVcopInfconstr2__  leat before Inf     
#MAP:+FMAINVcopInfconstr (@+FMAINV) TARGET ("leat") + VFIN (-1 HAB-ACTOR + Loc)(1 Inf LINK *1 Acc BARRIER NOT-NPMOD); # perhaps no need for this?

#!! * **+FMAINVCop**  copulas even if PrfPrc coming after
MAP:+FMAINVCop (@+FMAINV) TARGET VFIN-NOT-IMPRT + COPULAS (*1 Nom BARRIER PrfPrc OR (Actio Ess) LINK *1 COMMA OR CS BARRIER VFIN LINK *1 COPULAS + PrfPrc BARRIER VFIN OR Nom OR S-BOUNDARY OR LEFT) ;	
	#$ Eará guovlluin gos leat vel unnit sámit go Finnmárkkus, leamaš dákkár ákkain vel stuorát deaddu almmolaš ságastallamis.


#!! * **+FAUXVCop**  copulas coming before the mainverb
MAP:+FAUXVCop (@+FAUXV) TARGET VFIN-NOT-IMPRT + COPULAS (*1 PrfPrc OR (Actio Ess) BARRIER VFIN OR S-BOUNDARY OR (PUNCT LEFT) LINK NOT 0 (@>N) LINK NOT -1 (Num Nom)) ;	
	#$ Son lea leamaš oahpaheaddji maŋimuš 15-20 jagi, sihke mánáidskuvllas ja joatkkaskuvllas.
	#% Mun, Johanas, lean dat guhte lean gullan ja oaidnán buot dán .
	#% Eará guovlluin gos leat vel unnit sámit go Finnmárkkus, leamaš dákkár ákkain vel stuorát deaddu almmolaš ságastallamis.


#!! * **+FAUXVCop**  copulas coming before the mainverb, relative clause inbetween
MAP:+FAUXVCop (@+FAUXV) TARGET VFIN-NOT-IMPRT + COPULAS (*1 Rel LINK 1 ("leat") + VFIN LINK *1 PrfPrc LINK *1 PrfPrc BARRIER VFIN OR S-BOUNDARY OR (PUNCT LEFT) OR CC LINK NOT 0 (@>N) LINK NOT -1 (Num Nom));	
	#$ Muhtin biirres leat gávcci 12 áirasis leamaš nissonolbmot, ja eará biirres leat buohkat geat leat válljejuvvon leamaš dievddut.
	#% Guoskevaš eiseválddiin lea maid váidinvuoigatvuohta ođđa dieđuid vuođul mat leat boahtán maŋŋá vuosttaldanáigemeari.

	
#!! * **+FMAINVcopMannan**  leat before mannan TIME     
MAP:+FMAINVcopMannan (@+FMAINV) TARGET ("leat") + VFIN (*1 ("mannat" PrfPrc) BARRIER SV-BOUNDARY OR REAL-V LINK 1 TIME);
	#$ Mu oambealli lei dáppe mannan čavčča maid.

#!! * **+FMAINVHabconstr**  in habitive constructions    
MAP:+FMAINVHabconstr (@+FMAINV) TARGET ("leat" Pl3) OR ("leat" Sg3) (-1 HAB-ACTOR + Loc);
MAP:+FMAINVHabconstr (@-FMAINV) TARGET ("leat" PrfPrc) (-1 HAB-ACTOR + Loc);
	#$ Áhkus leat sávzzat.
	#$ Áhkus leamaš sávzzat.
	#$ Sus leat hervejuvvon gálssohat main heaŋgájit diehpit.
	#$ Mearrasámiinge ledje ovdal moadde dábmojuvvon bohcco maid guođohedje, muhto dat jávke loahpas siseatnama sámiid bohccuid sisa. 
	#$ Eŋgelis lei gollemihttosoabbi gávpoga ja dan poarttaid ja muvrraid mihtideami várás .

#!! * **+FMAINVCoopCoord** coordination  
MAP:+FMAINVCoopCoord (@+FMAINV) TARGET ("leat" Du3) OR ("leat" Pl3) (*1 (N Nom) OR (A Nom) OR (Num Nom) BARRIER NOT-NPMODADV LINK *-1 VFIN LINK -1 NP-HEAD-NOM OR HAB-ACTOR + Loc OR TIME OR TIME-MARKER OR DOPPE)(NEGATE *1 N OR A LINK *1 (Actio Ess) OR PrfPrc BARRIER VFIN OR SV-BOUNDARY);
	#$ Nemi ja Pondus leaba sárgojuvvon hámit.

#!! * **+FAUXVleat**  
MAP:+FAUXVleat (@+FAUXV) TARGET ("leat") + VFIN-NOT-IMPRT ((*1 PrfPrc - @>N OR (Actio Ess) BARRIER SV-BOUNDARY OR REAL-V OR (PUNCT LEFT) LINK NOT -1 Num LINK NEGATE 2 (N Nom) LINK *-1 VFIN LINK -1 Nom OR HAB-ACTOR + Loc) OR (1 ("nu") LINK 1 MOVT))(NEGATE *1 ("mannat" PrfPrc) BARRIER SV-BOUNDARY OR REAL-V LINK 1 TIME);
	#$ Sápmelaččat ledje orron dáin guovlluin 1600-logu birrasiin.
	#$ Mun ledjen borramin.	
	#% Ja soames báikkiin lea smávva sieđga, muhto jos eai leat goikesieđggat, de lea headju buollit, muhto go leat goikan sieđggat seahká, de lea buorre buollit.

#!! * **+FMAINVAux1**  
MAP:+FMAINVAux1 (@+FMAINV) TARGET VFIN + AUX-OR-MAIN (NEGATE *1 Inf BARRIER S-BOUNDARY);
	#$ Maid Máret áiggui goarrumis?

#!! * **-FMAINVAux2**  
MAP:-FMAINVAux2 (@-FMAINV) TARGET ConNeg + ("leat") (*1 (N Nom) BARRIER NOT-NPMOD LINK NOT 0 PrfPrc LINK *-1 REAL-NEG LINK -1 Nom OR HAB-ACTOR + Loc);
	#$ Muhto dus ii leat riekti dearpat su beare dan dihte.
		
#!! * **+FAUXVCop**  copulas coming after the mainverb
MAP:+FAUXVCop (@+FAUXV) TARGET VFIN-NOT-IMPRT + COPULAS (*-1 PrfPrc OR (Actio Ess) BARRIER NOT-ADV-PCLE LINK NOT 0 SYN-V OR (@>N) OR (N Nom))
 ;
	#$ Ja boahtán ledje su gudnejahttit.
 	#% Lonohallan lea čatnon Barents regiovdnii, dát mearkkaša ahte lonohallan lea Ruššii.		
  
    
#!! * **+FMAINVCop**  copulas 	
MAP:+FMAINVCop (@+FMAINV) TARGET VFIN + COPULAS (NEGATE *-1 Inf OR PrfPrc OR (Actio Ess) BARRIER VFIN OR S-BOUNDARY)(NEGATE *1 Inf OR PrfPrc - @>N OR (Actio Ess) BARRIER VFIN OR S-BOUNDARY OR (PUNCT LEFT));	
	#$ Mun lean Romssas. 

    
#!! * **+FMAINV**  to the remaining finite verbs which are not AUX    
MAP:+FMAINV (@+FMAINV) TARGET VFIN-NOT-AUX (NOT 0 V + OBJ OR V + SUBJ OR V + ADVL OR (V @>N) OR (V @N<))(NEGATE *0 VFIN-NOT-AUX OR @+FMAINV BARRIER S-BOUNDARY OR COMMA OR LEFT OR RIGHT) (NEGATE *-1 (V @>N) BARRIER NOT-A OR LEFT) 
(NEGATE *-1 REAL-V - @>N BARRIER S-BOUNDARY OR COMMA OR LEFT)
(NEGATE *1 VFIN OR PrfPrc - @>N BARRIER S-BOUNDARY OR COMMA OR RIGHT)(NEGATE 0 Inf LINK -1 CC LINK *-1 Inf BARRIER REAL-V LINK -1 AUX) ;
	#$ Boađe boahtte vahku.
	#$ Son lea oappáin náitalan.
	#$ Mánná čohkána láhttái.
	#$ Son gii oažžu lobi, galgá vuostáiváldit goartta dahje sullasačča, mii čájeha ahte sus lea lohpi.
	#$ Ovdal go čielggadan dan lagabui,...
	#$ Gal mun birgen, Gállá dajai, go manadettiin deaddilii uvssa gitta.
	#$ Norggas gávdnat boazodoalu Finnmárkkus davvin gitta Hedemárkui lullin.
	#% Mun lean ráhkkásisttán ožžon cummá.

MAP:+FMAINV (@+FMAINV) TARGET VFIN-NOT-AUX (NOT 0 V + OBJ OR V + SUBJ OR V + ADVL OR (V @>N) OR (V @N<))(0 Pl3 LINK -1 Nom + Pl OR Nom + Pl3) ;
	#$ Eai dat hála dáppe sámegiela dat hállet dušše dárogiela (LIA)
 	#$ dat bohte dat bohte dat bohte dalle Ruŧŧii dat bohte soađi áigge . (LIA)

MAP:+FMAINVinfv (@+FMAINV) TARGET VFIN-NOT-AUX + INF-V (1 Inf) ;
	#$ Liná liikui sárgut ja tennjet.

MAP:+FMAINVC (@+FMAINV) TARGET VFIN-NOT-AUX IF (NOT 0 V + OBJ OR V + SUBJ OR V + ADVL OR (V @>N) OR (V @N<)) (0C VFIN) ;

MAP:+FMAINVObjV_a (@+FMAINV) TARGET VFIN-NOT-AUX + TRANS-V (NOT 0 V + OBJ OR V + SUBJ OR V + ADVL OR (V @>N) OR (V @N<) OR Ill)(1 V + OBJ);
MAP:+FMAINVObjV_b (@+FMAINV) TARGET VFIN-NOT-AUX + TRANS-V (NOT 0 V + OBJ OR V + SUBJ OR V + ADVL OR (V @>N) OR (V @N<) OR Ill)(1 Acc LINK 1 TRANS-V + OBJ) ;
	#$ Dii oahppabehtet lohkat ja čállit sámegillii.

#!! * **+FMAINV**  to finite verb after mainverb
MAP:+FMAINVafterInf (@+FMAINV) TARGET VFIN-NOT-AUX + INF-V (NOT 0 V + OBJ OR V + SUBJ OR V + ADVL OR (V @>N) OR (V @N<)) (-1 (V @OBJ>) LINK -1 BOC) ;
	#$ Galgabehtet muitit, ahte báhčit oažžu dáppe meahcis...

# Mapping both verbs in a rel bisetning even if no comma
MAP:+FMAINVRel1 (@+FMAINV) TARGET VFIN-NOT-AUX (NOT 0 V + OBJ OR V + SUBJ OR V + ADVL OR (V @>N) OR (V @N<) OR Adv) (*-1 Rel BARRIER SV-BOUNDARY LINK NEGATE *-1 VFIN BARRIER S-BOUNDARY) ;
MAP:+FMAINVRel2 (@+FMAINV) TARGET VFIN-NOT-AUX (NOT 0 V + OBJ OR V + SUBJ OR V + ADVL OR (V @>N) OR (V @N<) OR Adv OR N) (*-1 FMAINV BARRIER REAL-V LINK *-1 Rel BARRIER SV-BOUNDARY LINK NEGATE *-1 VFIN) ;
	#$ Áldu mii dálvvi miehtá njamaha miesi gohčoduvvo čearpmateadni.

MAP:+FMAINV (@+FMAINV) TARGET VFIN-NOT-AUX (NOT 0 SYN-V) (*-1 (N Nom) OR (Pron Nom) BARRIER NOT-ADV-PCLE LINK NOT 0 Inf LINK NEGATE *-1 REAL-V - @>N BARRIER S-BOUNDARY OR COMMA)(NEGATE *1 VFIN OR PrfPrc - @>N BARRIER S-BOUNDARY OR COMMA) ;

MAP:-FMAINV (@-FMAINV) TARGET PrfPrc (NOT 0 SYN-V) (*-1 FAUXV BARRIER REAL-V OR S-BOUNDARY)(NEGATE 0 AUX LINK *1 Inf BARRIER REAL-V OR S-BOUNDARY) ;
	#$ Čuollun sáhttá leat dušše okta fierbmi gurgaluvvon.

MAP:-FMAINV (@-FMAINV) TARGET PrfPrc (NOT 0 SYN-V) (*-1 MOVT BARRIER REAL-V OR S-BOUNDARY LINK -1 ("nu") LINK -1 FAUXV)(NEGATE 0 AUX LINK *1 Inf BARRIER REAL-V OR S-BOUNDARY) ;


MAP:+FMAINV (@+FMAINV) TARGET VFIN-NOT-AUX + Imprt (NOT 0 SYN-V) (*-1 BOC BARRIER NOT-ADV)(NEGATE 1 Inf LINK NOT -1 INF-V)(NEGATE 0 Attr LINK *1 (N Nom) BARRIER NOT-A) ;

MAP:+FMAINV (@+FMAINV) TARGET AUX + VFIN (NEGATE *1 Inf OR PrfPrc - @>N OR (Actio Ess)) ;
	#$ Mearrasápmelaččat ledje áŋgiris ja oahppan bivdit.
	


MAP:+FMAINV (@+FMAINV) TARGET VFIN-NOT-AUX + Pl3 (NOT 0 SYN-V)(*1 NP-HEAD-PL-NOM BARRIER NOT-NPMODADV) ;

MAP:+FMAINVQst (@+FMAINV) TARGET VFIN-NOT-AUX (NOT 0 SYN-V)(*-1 BOS BARRIER WORD)(1 go + Qst) ;


MAP:+FMAINV (@+FMAINV) TARGET VFIN-NOT-AUX + Sg3 (NOT 0 SYN-V)(NEGATE *-1 REAL-V - @>N BARRIER S-BOUNDARY OR COMMA)(*1 NP-HEAD-SG-NOM BARRIER NOT-NPMOD-ACC-ADV LINK NOT 1 VFIN + Sg3) ;
	#$ Loahpas jearai Johttisámelisttu Anders Somby jr. sudnos njuolga háliida go čearru gulahallat, juo dahje ii.
	#% Ádjá viessu lea guokte miilla bajábeal márkana.


MAP:+FMAINV (@+FMAINV) TARGET VFIN-NOT-AUX + 1-2-V OR VFIN-NOT-AUX + Sg3 (NOT 0 SYN-V)(*-1 BOC BARRIER REAL-V)(NEGATE 0 N LINK -1 Dem)(NEGATE 0 N LINK *-1 COMMA OR @CNP BARRIER NOT-A-ADV LINK -1 N)(NEGATE 0 Pl LINK *1 VFIN + Pl3 BARRIER REAL-V OR S-BOUNDARY OR COMMA)(NEGATE *1 VFIN OR PrfPrc - @>N BARRIER S-BOUNDARY OR COMMA)(NEGATE 0 Inf LINK -1 CC LINK *-1 Inf BARRIER REAL-V LINK -1 AUX)  ;

MAP:-FMAINVfragment (@-FMAINV) TARGET PrfPrc (NOT 0 SYN-V) (-1 Adv LINK -1 BOS) (1 Ess OR ADVLCASE LINK NEGATE 1 WORD) ;
	#$ Guhká bargan journalistan.


#!! ### (@-FMAINV) and (@-FAUXV)

#!! * **-FAUXVConNegCop**  to ConNeg COPULAS   
MAP:-FAUXVConNegCop (@-FAUXV) TARGET ConNeg + COPULAS (*-1 REAL-NEG BARRIER REAL-V)(*1 PrfPrc OR (Actio Ess) BARRIER REAL-V OR S-BOUNDARY);
	#$ Muhto juos Kristus ii leat bajásčuožžilan, ..

#!! * **-FAUXVConNegAux**  to ConNeg AUX-OR-MAIN   
MAP:-FAUXVConNegAux (@-FAUXV) TARGET ConNeg + AUX-OR-MAIN (*-1 REAL-NEG BARRIER REAL-V)(*1 Inf OR PrfPrc BARRIER REAL-V OR S-BOUNDARY LINK NOT 0 OBJ);
	#$ Eai guovttis ge nagot loktet dan geađggi.
	#$ In sáhte olles gáhku borrat. 
	#$ Daid ii galgga suovvat bargat joavdelasaid.
	#$ Ii daid galgga joavdelasaid bargat suovvat.
	#% – Ii son fal siđa munno boahtit , dajai Mattias

#!! * **-FAUXVConNegAux**  to ConNeg AUX   
MAP:-FAUXVConNegAux (@-FAUXV) TARGET ConNeg + AUX - ("leat") (*-1 REAL-NEG BARRIER S-BOUNDARY OR ConNeg)((*1 Inf BARRIER REAL-V OR S-BOUNDARY OR Attr LINK NOT 0 OBJ) OR (0 TRANS-V LINK *-1 Acc BARRIER NOT-NPMOD LINK *1 Inf BARRIER REAL-V OR S-BOUNDARY OR Attr LINK NOT 0 OBJ));
	#$ Ii sáhte suohtastallat.
	#$ Ii sáhte dieđusge dušše suohtastallat.
	#$ Eat mii sáhte vuolgit.
	#$ In sáhte gáhku borrat.
	#% It don leat mus goahtemuorran orron.
	#% – Ii son fal siđa munno boahtit , dajai Mattias
	


MAP:-FAUXVConNegAuxNeg (@-FAUXV) TARGET ConNeg + AUX - ("leat") (*-1 REAL-NEG BARRIER S-BOUNDARY OR ConNeg)(1 Ind + Neg LINK *1 Inf BARRIER REAL-V OR S-BOUNDARY);	
	#$ Lean diŋgaohcci ja in háliit in minuhta ge duššái golahit.

#!! * **-FMAINVConNeg**  to ConNeg   
MAP:-FMAINVConNeg (@-FMAINV) TARGET ConNeg (*-1 ConNeg BARRIER REAL-V OR S-BOUNDARY)(NEGATE *1 Inf OR PrfPrc OR (Actio Ess) BARRIER REAL-V OR S-BOUNDARY)(NEGATE **1 Adv BARRIER REAL-V OR S-BOUNDARY LINK 1 Inf)(NEGATE 0 Inf LINK -1 REAL-NEG LINK -1 ConNeg LINK -1 REAL-NEG) ;
	#$ Ii son liikon dohko.
	#$ Sieiddi bálvalemiin sturron eallu gohčoduvvui bálvvoseallun, mii sáhtii jávkat, go oamasteaddji jámii dahje go sieidi ii bálvaluvvon.
	#$ Iigo dutnje šat boađe áviisa?
	#% Ja go álget johttágoahtit, de eai oaččo ealuid čoahkkái, ja go ožžot, de lea nu arvi ja muohtti, ahte leat visot nu njuoskasat.

MAP:-FMAINVConNegCoor (@-FAUXV) TARGET (Prs ConNeg) + AUX (-1 ("ja" CC) OR ("dahje" CC) LINK **-1 ConNeg LINK -1 Neg)(*1 Inf BARRIER REAL-V OR S-BOUNDARY)(NEGATE 0 VFIN);
	#$ Bearehan dal son ii geige vel gieđa ja váldde maiddái eallima muoras ja bora ja beasa nu eallit agálaččat.
	#% Olmmoš gean lusa stállu lea vuolgán, ii oaččo šat ráfi ja ferte vuolgit olggos fággádallat suinna.


MAP:-FMAINVConNegCoor (@-FMAINV) TARGET (Prs ConNeg) (-1 ("ja" CC) OR ("dahje" CC) LINK **-1 ConNeg BARRIER VFIN LINK *-1 Neg)(NEGATE 0 VFIN);
	#$ Bearehan dal son ii geige vel gieđa ja váldde maiddái eallima muoras ja bora ja beasa nu eallit agálaččat.
	#% Olmmoš gean lusa stállu lea vuolgán, ii oaččo šat ráfi ja ferte vuolgit olggos fággádallat suinna.
	#% Ánne ii lean guhkes áigái leamaš ná buohcci, lei nu illáveaje ja šliettas, ii veadján maidege.


#!! * **-FMAINVConNeg**  to ConNeg
#MAP:-FMAINVConNeg (@-FMAINV) TARGET ConNeg (*-1 REAL-COPULAS + VFIN OR ("orrut") + VFIN OR Neg BARRIER SV-BOUNDARY)(NEGATE *-1C PrfPrc BARRIER REAL-V OR N); 
MAP:-FMAINVConNeg (@-FMAINV) TARGET ConNeg (*-1 REAL-NEG BARRIER SV-BOUNDARY OR ConNeg)(NEGATE 0 AUX LINK *-1 PrfPrc BARRIER REAL-V)(NEGATE 0 AUX LINK 1 Inf)(NEGATE 0 Inf LINK -1 REAL-NEG LINK -1 ConNeg LINK -1 REAL-NEG) ; 
	#$ Ii okta ge geahččalan eará sivaid ohcat.
	#% It don leat mus goahtemuorran orron.
	#% De ledje baicca eatnašat dan dilis ahte eai máhttán stávetge.

#!! * **-FMAINVConNeg**  to ConNeg Aux after PrfPrc
MAP:-FAUXVConNeg (@-FAUXV) TARGET ConNeg + AUX (*-1 PrfPrc BARRIER REAL-V OR SV-BOUNDARY LINK *-1 REAL-NEG BARRIER NOT-ADV-PCLE)(NEGATE *1 REAL-V BARRIER NOT-ADV-PCLE)(NEGATE 1 Nom); 
	#$ Ii dal láhppán gal leat. 
	#% Ii ge ságastallan leat vejolaš jus eai leat journalisttat geat máhttet sámegiela bures.

#!! * **-FMAINVConNegCop**  to ConNeg COPULAS   
MAP:-FMAINVConNegCop (@-FMAINV) TARGET ConNeg + COPULAS (*-1 REAL-NEG BARRIER REAL-V)(NEGATE *1 PrfPrc OR Inf OR (Actio Ess) BARRIER REAL-V OR S-BOUNDARY OR Nom);
	#$ Ii lean šat nu álki vuovdit.


#MAP:ConNegCop (@-FMAINV) TARGET ConNeg (*-1 REAL-NEG BARRIER S-BOUNDARY OR ConNeg)(0 ("leat"))(1 (A Nom));
	#$ Ii ge almmolaš sámi ságastallan leat vejolaš jus eai leat journalisttat geat máhttet sámegiela bures.


#!! * **-FAUXVPrfPrcAux**  to PrfPrc AUX  before Inf or Actio Ess
MAP:-FAUXVPrfPrcAux (@-FAUXV) TARGET PrfPrc + AUX (*1 Inf OR (Actio Ess) BARRIER SV-BOUNDARY OR (N Ess) OR (A Ess))(*-1 ("leat") BARRIER NOT-ADV-PCLE) (NEGATE 0 AUX-OR-MAIN LINK *1 Acc BARRIER NOT-ADV-PCLE LINK 1 Inf + OBJ);	
	#$ In leat sáhttán gáhku borrat dan rájes.
	#% Jus lean váiban dahje ferten lihkkat árrat, de manan nohkkat oalle árrat.

#!! * **-FMAINVPrfPrc**  to PrfPrc 
MAP:-FMAINVPrfPrc (@-FMAINV) TARGET PrfPrc (*-1 REAL-COPULAS + VFIN OR ("orrut") + VFIN OR Neg BARRIER S-BOUNDARY)
#(NEGATE *-1C PrfPrc BARRIER REAL-V OR N)
(NEGATE -1 PrfPrc); 
	#$ Muhto boanddat leat goikadan ollu jekkiid ja gilván daidda rásiid.
	#$ Mu mielas lea suohtas oahppat sámegiela.
	#$ Ja Biret-Elle lea easka skuvllas geargan.
	#$ Máŋgasat leat maid vásihan, ahte sii leat bártidan.
	#$ In leat leamaš mielde gáhku láibume ovdal.  
	#$ Moanaid guovlluin gos lavdnji lea leamaš adnon boaldámuššan lea buot lavdnji dál čuhppon.
	#$ Oahppit leat lohkan ollu girjjiid.
	#$ Lean mun gal oaidnán čakčaseavdnjadiin dola rievdamiin čázi alde Deanus.

#!! * **-FMAINVPrfPrcEss**  to PrfPrc before Ess
MAP:-FMAINVPrfPrcEss (@-FMAINV) TARGET PrfPrc IF (*-1 REAL-COPULAS OR ("orrut") BARRIER PrfPrc OR (Actio Ess))(NEGATE 0 AUX LINK *1 Inf BARRIER S-BOUNDARY)(1 Ess);
	#$ Moanaid guovlluin gos lavdnji lea leamaš adnon boaldámuššan lea buot lavdnji dál čuhppon.

#!! * **-FMAINVPrfPrcleat**  to PrfPrc leat
MAP:-FMAINVPrfPrcleat (@-FMAINV) TARGET ("leat" PrfPrc) ; 
	#$ Ránus lea álo leamaš buorre haddi.

#!! * **-FMAINVPrfPrcafterAuxAux**  to PrfPrc after two Auxs
MAP:-FMAINVPrfPrcafterAuxAux (@-FMAINV) TARGET PrfPrc (*-1 REAL-COPULAS OR ("orrut") BARRIER SV-BOUNDARY LINK *-1 Neg BARRIER NOT-ADV); 
	#$ Olbmot eai orron čadnon dušše iežaset mearragáddái bivdit. 
	
#!! * **-FMAINVPrfPrccoord**  to PrfPrc coordination	
MAP:-FMAINVPrfPrccoord (@-FMAINV) TARGET PrfPrc (*-1 CC BARRIER REAL-V OR S-BOUNDARY LINK *-1 PrfPrc - @>N BARRIER REAL-V OR S-BOUNDARY LINK *-1 REAL-COPULAS OR ("orrut") BARRIER NOT-ADV-PCLE)(NEGATE 0 VFIN + AUX LINK *1 Inf BARRIER NOT-ADV-PCLE);
	#$ ... dákkár orgánii leat boahtán dahje ovddiduvvon.
	#$ Sápmelaččat ledje dohkkehuvvon Norgga álgoálbmogin ovdalgo konvenšuvnna ledje šiehtadan gárvásii ja dasa vuolláičállán.
	#%  Jus lean váiban dahje ferten lihkkat árrat, de manan nohkkat oalle árrat.

#!! * **-FMAINVPrfPrccoord**  to PrfPrc coordination	
MAP:-FMAINVPrfPrccoord (@-FMAINV) TARGET PrfPrc (-1 CC LINK -1 @-FMAINV) ;
	#$ Son lei vuolggahan mu dán máilbmái dáhpedorpmis geainna nu, riegádahttán ja guoddán mu heakkastis vahágis, biebman ja bivttastan dalle go muittii.

#!! * **-FMAINVPrfbeforeAux**  to PrfPrc before the Aux	
MAP:-FMAINVPrfbeforeAux (@-FMAINV) TARGET PrfPrc (*1 REAL-COPULAS + FAUXV BARRIER NOT-ADV-PCLE LINK NEGATE *1 PrfPrc OR (Actio Ess) BARRIER NOT-ADV-PCLE)(NEGATE -1 BOC LINK 1 Der/Pass LINK 1 ("leat") LINK *2 NP-HEAD-NOM BARRIER NOT-NP);     
	#$ Ja boahtán ledje su gudnejahttit.

#!! * **-FMAINVPrfafterMan**  to PrfPrc before the Aux	
MAP:-FMAINVPrfafterMan (@-FMAINV) TARGET PrfPrc (-1 ("man"))(*1 ("leat") BARRIER REAL-V);     
	#$ Dattetge duvdet min sámepolitihkkarat iežaset buorren ovdamearkan álgoálbmotservvodagas , dasa ahte man lihkostuvvan min Sámi ovttasbargu stáhta eiseválddiiguin lea leamaš.

MAP:-FMAINV (@-FMAINV) TARGET ("beaivádit" PrfPrc)(1 Sem/Date) ;

#MAP:<OBJPrfPrc3 (@<OBJ) TARGET PrfPrc IF (*-1 TRANS-V - AUX BARRIER SV-BOUNDARY)(NEGATE 0 Ess LINK *-1 OPRED-V);
	#$ Ii lean gal mihkkege heahtediliid, muhto lei čielggas ahte muhtumat dovde dorvvohisvuođa ja dovde okto báhcán.
	#% go oaidná biebman boahtá divrras šaddat.

#!! * **-FMAINVInf**  to Inf
MAP:-FMAINVInf (@-FMAINV) TARGET Inf (NOT 0 FAUXV)(NEGATE 0 A LINK *-1 COPULAS BARRIER REAL-V OR (N Nom) OR ADVLCASE OR MIELLA OR (A Nom))(NEGATE 0 (Pl Nom) LINK *1 VFIN + Pl3 OR VFIN + Du3 BARRIER NOT-NPMODADV OR COMMA)(NEGATE 0 N LINK -1 Gen LINK -1 CC LINK -1 N)(NEGATE -1 COMMA LINK -1 V + Pl3)(NEGATE 0 Pl3 LINK -1 Nom + Pl OR Nom + Pl3) ;
	#% Eai dat hála dáppe sámegiela dat hállet dušše dárogiela (LIA)

#(NEGATE 0 A LINK *-1 COPULAS + Sg3 BARRIER REAL-V OR (N Nom) OR ADVLCASE OR MIELLA OR (A Nom) LINK *-1 MIELLA OR GEN-CONCEPT BARRIER REAL-V) ;


MAP:-FMAINVInf (@-FMAINV) TARGET Inf (NOT 0 FAUXV)(*-1 COPULAS + Sg3 BARRIER REAL-V OR (N Nom) OR ADVLCASE OR MIELLA OR (A Nom) LINK *-1 MIELLA OR GEN-CONCEPT BARRIER REAL-V) ;
	#$ Eará guovddáš ulbmil prošeavttain lea fuomášahttit dan máhttodárbbu maid riikkaidgaskasaš ja sisriikkalaš birasulbmilat bohciidahttet.


#!! * **-FMAUXVActioEss**  to Actio Ess
MAP:-FMAUXVActioEss (@-FAUXV) TARGET (V Actio Ess) + AUX (*1 Inf BARRIER REAL-V);
	#$ Mun ledjen áigume čállit reivve.

#!! * **-FMAINVActioEss**  to Actio Ess
MAP:-FMAINVActioEss (@-FMAINV) TARGET (V Actio Ess)(NEGATE 0 AUX LINK 1 Inf)(NEGATE -1 OPRED-V);
	#$ Biret lea čállime.

#!! * **-FMAINVSup**  to Sup
MAP:-FMAINVSup (@-FMAINV) TARGET Sup - Neg ;
	#$ Áddjá bázii mállása vuoššažit. 

#!! * **+FAUXV** to  Aux
MAP:+FAUXV (@+FAUXV) TARGET VFIN-NOT-IMPRT + AUX (NEGATE 0 AUX-OR-MAIN LINK *1 Acc BARRIER NOT-ADV-PCLE LINK 1 Inf + OBJ)(NEGATE 0 AUX-OR-MAIN LINK *1 Acc BARRIER NOT-ADV-PCLE LINK *1 VFIN BARRIER REAL-V)(NEGATE 0 A LINK 1 CC LINK 1 A) ;

MAP:+FMAINV (@+FMAINV) TARGET VFIN-NOT-IMPRT + AUX-OR-MAIN ;
	#$ Soai siđaiga dávjá Liná lohkat jitnosit go sis lei lohkanhárjehallan.
	#$ Lásse dat lei sihtan mu váldit dan.

#!! * **NPrsPrc1** with 1C N Nom
MAP:NPrsPrc1 (@>N) TARGET PrsPrc 
#((-1 (N Loc) OR BUOT OR Attr OR (N Gen)) OR (-1 CC LINK -1 Attr))
((*-1 VFIN BARRIER COMMA OR S-BOUNDARY) OR (1 N LINK *1 VFIN BARRIER S-BOUNDARY OR COMMA))
(NEGATE 0 N - Der/NomAg)(NEGATE 0 ("orrut") LINK *-1 Ill)
(1 N)(NEGATE 0 VFIN LINK *1 Inf BARRIER REAL-V)(NEGATE 1 (N Gen) LINK 1 N) ;
	#% – Biilavuddjiide geat eai leat oahppásat guovllus orru mohkki mii lea ovdalaš šalddi amas ja váttis vuodjit .

MAP:NPrsPrc2 (@>N) TARGET PrsPrc ((0C PrsPrc) OR (0 ("johtit") LINK 1 N));
#MAP:NPrsPrc2 (@>N) TARGET PrsPrc (NOT 0 NomAg OR VFIN LINK 1C N) ;
	#% Viežžat studeanttakoartta (dan oažžu maŋŋel go leat máksán studeantadivvaga).


#!! * **ActioNom** with 1C N Nom
MAP:ActioNom (@>N) TARGET (Actio Nom)(-1 HAB-ACTOR + Gen)(*1C (N Nom) BARRIER NOT-A) ;
	#$ Stáhta nammadan lahttu ja sadjásašlahttu galgaba deavdit gáibádusaid.
	#$ Dán gieđahallamii ii searvit Finnmárkku fylkkadikki válljen maŋimuš stivrralahttu.

#!! * **<ADVLVAbess** VAbess ADVL
MAP:<ADVLVAbess (@<ADVL) TARGET VAbess - Attr (*-1 VFIN BARRIER S-BOUNDARY)(NOT 0 SYN-V) ;
	#$ Mus báhce moadde girjji logakeahttá.
MAP:ADVL>VAbess (@ADVL>) TARGET VAbess - Attr (*1 VFIN BARRIER S-BOUNDARY) ;

#!! * **<ADVLVGen** VGen ADVL
MAP:<ADVLVGen (@<ADVL) TARGET VGen (*-1 VFIN OR Inf BARRIER S-BOUNDARY) ;
	#$ Máhtte manai čuoigga skuvlii.

#!! * **ADVL>VGen** VGen ADVL
MAP:ADVL>VGen (@ADVL>) TARGET VGen (*1 VFIN OR Inf BARRIER S-BOUNDARY) ;
	#$ Suga láviime fitnat gitta Biessavvonis.
	
MAP:ADVL>VGen (@ADVL>) TARGET VGen (*1 VFIN BARRIER S-BOUNDARY) ;

#!! * **<ADVLGer** Gerundium ADVL
MAP:<ADVLGer (@<ADVL) TARGET Ger (NEGATE *1 VFIN BARRIER S-BOUNDARY OR COMMA) ;
	#$ Biigá lei bassaladdame báhpa boađedettiin.
	
#!! * **ADVLGer>**
MAP:ADVLGer> (@ADVL>) TARGET Ger (*1 VFIN BARRIER S-BOUNDARY);
	#$ Konvenšuvdnaevttohusa ráhkadettiin delegašuvnnat leat váldán vuhtii Ruoŧa ođđa boazodoallolága.
	#$ Báhpa boradettiin biigá lei bassaladdame. 

#!! * **-FMAINVLoc** Actio Loc
MAP:-FMAINVLoc (@<ADVL) TARGET (Actio Loc) ; 

#!! * **>AActioGen** Actio Gen
MAP:>AActioGen_a (@>A) TARGET (Actio Gen) (1 ("veara"));
MAP:>AActioGen_b (@>A) TARGET (Actio Gen) (1 CC LINK 1 Gen LINK 1 ("veara")) ;
	#$ Dasgo bargi lea bálkkás veara.

#!! * **PrfPrcEllipsis** being verbal head when finite verb is missing
MAP:PrfPrcEllipsis @-FMAINV TARGET PrfPrc (NEGATE *-1 VFIN)(1 A OR N);


#!! ### And then we remove the verbs which didn't get any syntactic tag, in favour of verbs with syntactic tags.

MAP:fragmverb (@-FMAINV) TARGET PrfPrc (0C PrfPrc)  ;
MAP:fragmverb (@-FMAINV) TARGET PrfPrc (-1 BOS)(1 EOS)  ;
	#$ Ođastuvvon: 07.03.2017 - 16:29


#!! * **realverbX**
MAP:realverbX (@X) TARGET REAL-V  ;
	## Oađidettiin gullen olbmo boahtimin.


#!! * **NomActLocX**
MAP:NomActLocX (@X) TARGET (Der/NomAct Loc)(0 @<ADVL)  ;

#!! * **NomActX** removes other readings when PrfPrc Or Actio Ess
MAP:NomActX (@X) TARGET (Der/NomAct) OR Actio (0 (V @<OBJ) OR FMAINV OR (V @>N))  ;

#!! * **IfonlyVerb** selects the FMAINV reading in the cohort
SELECT:IfonlyVerb FMAINV IF (NEGATE *0 FMAINV BARRIER S-BOUNDARY)(NOT 0 FAUXV OR (Inf @A<) OR (Inf @N<))(NEGATE *-1 ("go") LINK *-1 VFIN) ;
	#$ Juohke loddi gielainis lávlu.
	#$ Das maŋŋá dajan juoidá álbmotrievtti mearkkašumi birra, ja geavahan das veahkkin ođđaseamos riektegáldu.
	#% Lea 43 olbmo unnit go diibmá dán muddui.
# The last NOT clause keeps the correct Ill in the 43 sentence.
# The intuition is that the frame "Lea ... go ... (lei)" is an ellipsis frame.

# Problem 2
#!! * **IfonlyConNeg** ConNeg if it is @-FMAINV or @-FAUXV
SELECT:IfonlyConNeg ConNeg IF (0 (ConNeg @-FMAINV) OR (ConNeg @-FAUXV))(NEGATE 1 ("leat") + REAL-V) ;
	#% Iige almmolaš sámi ságastallan leat vejolaš. 
	#% De ledje baicca eatnašat dan dilis ahte eai máhttán eai stávetge.
	#% Ja go álget johttágoahtit, de eai oaččo ealuid čoahkkái, ja go ožžot, de lea nu arvi ja muohtti, ahte leat visot nu njuoskasat.


#!! ### **killifVinCohort** This rule removes all other readings, if there is a mapped V reading in the same cohort. Every case which this goes wrong, should be fixed in mapping rules or previous disrules.
SELECT:killifVinCohort SYN-V  ;

#!! * **X**
REMOVE:X (@X) ;

#SELECT:Sg2 (V Prt Sg2) IF (0 Prs + Pl3  LINK *-1 BOS BARRIER WORD)(NEGATE 1 go LINK 1 HAB-ACTOR + Loc) ;

SELECT:Adv Adv (0 A OR Loc LINK 1 @+FMAINV OR @+FAUXV)(-1 N OR Pers) ;
	#$ Tommi ja Annika dárkilit geahččaba Pippi oaidnin dihte mo diŋggaohccit barget.
	#$ Son lei oainnat plánen irggis lusa vuolgit ovdal ođđabeairuohta, muhto dálki ii orron gal dasa riekta miehtame.

	
SELECT:Adv Adv (0 A LINK 0 GRADE-ADV LINK 1 @+FMAINV - ("leat") OR @+FAUXV)(NOT 0 Pron)(-1 N OR @CVP OR Po) ;
	#$ Biret ja Máret leigga ruovttus juovlabasiid, go issorasat dálkkáskii.


SELECT:IV (V IV) (0 ("lávet") + FAUXV OR ("áigut") + FAUXV) ; 
SELECT:TV (V TV) (0 ("lávet") - FAUXV OR ("áigut") - FAUXV) ; 

#!! # NOUNS
# Disambiguating nouns 
# Nouns vs. adjectives




REMOVE A + $$CASE IF (NOT -1 ("leat"))(0 N + $$CASE) (1 V) ;
	#$ Dutkansearvvi áigečállaga doaimmaheamis vástida doaimmahusgoddi.


	
# Disambiguating proper nouns 

#!! # CASE DISAMBIGUATION

#!! ## Num as subject, tricky cases - the rule should be here because of the verbdisambiguation
SELECT:NumNomSubj (Num Sg Nom)(0 NUM LINK -1 V + Pl3  LINK NOT 0 Imprt LINK NEGATE *-1 (N Pl Nom) OR (Pers Pl3) OR (Coll Nom) OR Sem/Group + Nom OR NUM + Nom BARRIER SV-BOUNDARY)(NEGATE 1 Gen LINK 1 Po) ; 
SELECT:NumNomCoor (Num Sg Nom)(0 Num LINK *-1 JA/DAHJE BARRIER NOT-ADV LINK -1C (Num Nom))(NEGATE 1 Gen LINK 1 Po) ;
	#$ Juos evttohusa meahcceeatnamiid nuppástuvvan geavaheami birra, mii juogo gusto dušše Kárášjoga, Guovdageainnu, Unjárgga, Porsáŋggu ja Deanu gildii dahje gusto dušše restii Finnmárkkus, dorjot golbma ja dušše golbma stivrralahtu, de sáhttet golbma stivrralahtu ovtta oli gáibidit ášši gieđahallat oktii vel.

SELECT:NumNomSubj (Num Sg Nom)(0 NUM LINK -1 Loc LINK -1 V + Pl3  LINK NOT 0 Imprt LINK NEGATE *-1 (N Pl Nom) OR (Pers Pl3) OR (Coll Nom) OR Sem/Group + Nom OR NUM + Nom BARRIER SV-BOUNDARY)(NEGATE 1 Gen LINK 1 Po) ; 
	#$ Dál leat sus 137 gáicca.

SELECT:NumNomCop (Num Nom) IF ((*-1 COPULAS + FMAINV OR Der/Pass + FMAINV BARRIER NOT-ADV-PCLE LINK *-1 NP-HEAD-NOM BARRIER NOT-ADV-PCLE) OR (*-1 NP-HEAD-NOM BARRIER NOT-ADV-PCLE LINK *-1 COPULAS + FMAINV BARRIER NOT-A LINK -1 (CC @CVP) OR ("de") OR BOS))(1 (Sg Gen))(NEGATE 2 Po OR (A Nom) OR Pr) ;
	#$ Ja go geassit eret dábálaš goluid, de lea buhtes sisaboahtu sullii 100 000 ruvnnu, muitala Eriksen.

SELECT:NumNomCop2 (Num Nom) IF (*-1 COPULAS + FMAINV BARRIER SV-BOUNDARY)(1 ("jahki") + Gen)  ;
	#$ Norgga álbmogis leat jo miljovdna ássi badjel 60 jagi.

SELECT:NumNomYear (Num Nom) IF ((0 Sem/Year LINK -1 Sem/Sur LINK 2 (":") OR (";") OR (PUNCT RIGHT)) OR (1 MANNU)) ;
SELECT:NumNomYear (Num Nom) IF (0 Sem/Year LINK -1 Sem/Sur LINK -1 (PUNCT LEFT))(1 COMMA OR (";") OR (PUNCT RIGHT)) ;
SELECT:NumNomPage (Num Nom) IF (-1 (":") LINK -1 Sem/Year LINK -1 Sem/Sur) ;
SELECT:Sur (Sem/Sur Nom) IF (-1 (":") OR (PUNCT LEFT) OR (";") LINK 2 Sem/Year LINK 1 (":") OR (";") OR (PUNCT RIGHT) OR COMMA) ;
	#$ Nu lea ášši (Magga 2001; Johansen 1992).
	#$ Oaivevearbbat ja koloratiivavearbbat gávdnojit maid davvisámegiela sátnegirjjiin (Sammallahti 1989, 1993; Vest 1995).


#!! * **DiminNomPxSg1**	
SELECT:DiminNomPxSg1 Nom IF (0 (Der/Dimin PxSg1) OR (".*š"r) + PxSg1 LINK -1 COMMA OR BOC LINK 2 COMMA) ;
	#$ Jurddaš, ustibažžan, don leat nu nuorra ja liekkus.


#!! ## ACCUSATIVE-GENITIVE DISAMBIGUATION


#!! ## Secure rules for choosing Acc

# pron (select/remove Gen)
 

#!! * **PGenN** selects Gen when (Pron Pers) to the left and N to the right *mu sámevuođa iđuid*
SELECT:PGenN Gen IF (-1C (Pron Pers) + Gen LINK NOT -1 CC)(1 (N Acc) OR (N Com) OR (N Ill) OR (N Nom))(NEGATE 0 Num OR (Pron Rel))(NEGATE 1 V);
	#$ Dárogiella ja norgalaš cukcasat eai livčče sáhttán seammalágan fátmmasteaddji vugiin guoddit mu sámevuođa iđuid.
	#$ Hans A. Oppstad lei sátnejođiheaddji mu Skuvvanári álgojagiid.
	#$ Vearrámusat leat min iežamet olbmot.
	#% Soai lohkaba su girjji eaŋgalasgillii.

#!! * **CoGen1** (quite strict) selects the first of coordinated genitives *riikkaid, čearuid ja boazoorohagaid ovttasbarggu*
SELECT:CoGen1 Gen IF (0 Gen LINK 1 COMMA LINK 1C Gen LINK 1 JA/DAHJE LINK 1 Gen LINK 1 (N Acc));#(NEGATE -1 Gen)(NEGATE -1 ("sihke"))(NEGATE -1 JA/DAHJE LINK -1 Nom)(NEGATE 0 Num);
	#$ Konvenšuvdna galgá ovddidit ja ovdánahttit riikkaid, čearuid ja boazoorohagaid ovttasbarggu, nu maiddái ovttaskas boazodolliid ovttasbarggu.
	#$ Moai bijaime girjái elliid, guliid ja jávrriid dovdomearkkaid jna.
	#% De ledjen mun jo muhtun áigge muitalan gaskadási ohppiide sihke máidnasiid, cukcasiid ja gobmemuitalusaid sámi sisdoaluin.
	#% Mii juohkit guovllu viđa guovllulaš oassái: Romsa ja davágeaži Norrbotten, Davit Nordlánda ja gaska ja lulimus Norrbotten, Helgelánda ja Västerbotten, Davvi-Trøndelága ja davágeaži Jämtlánda. # Negate is for Västerbotten.
	#% Maŋŋil váccii joatkkaskuvlla Álttás ja logai psykologiija, davviriikalašfága ja pedagogihka váldofága Romssa Universitehtas.

SELECT:CoGen2 Gen IF (-1 COMMA LINK -1C Gen)(0 Gen)(1 CC LINK 1 Gen LINK 1C Acc);

#Comment: Make a rule that selects Gen for sentences like: Goalmmát oasis lea bargosajiid ja bargoeallima eavttuid birra, njealját oasis lea fidnooahpuid, giehtaduoji ja boaittobeali báikkiid ealáhusaid birra. => boaittobeali should not be Acc

#!! ###  Semantihkka: Choosing accusative or genitive semantically

	# By frequence :
	# Xx Best rules!
	# X Good rules  
	# x Found, but not necassarily good


#!! * **vuoiAcc** selects accusative if *vuoi* or *vuoi surgat* to the left 
SELECT:vuoiAcc Acc IF ((*-1 ("vuoi") BARRIER NOT-NPMODADV) OR (*-1 ("surgat") BARRIER NOT-NPMODADV LINK -1 ("vuoi"))) ;
	#$ Vuoi mu, maid mun dál galggan?
	#$ Vuoi dan olbmo gean bokte fillehusat bohtet.
	#$ Vuoi surgat dan gándda!

#!! * **lihkkuAcc** selects accusative 
SELECT:lihkkuAcc Acc IF (0 ("lihkku") LINK 1 ("beaivi") LINK 0 Ill OR Com) ;
	#$ lihku beivviin

	
LIST SAPMI-N-HEAD =  "álbmot" "álbmotbeaivi" "álbmotdálkkodeapmi" "álbmotjoavku" "árbevierru" "ássanguovlu" "ássi" "áviisa" "beales" "beroštupmi" "boaris" ("boazodoallu" Nom) ("boazodoallu" Loc) "byråkráhtagiella" "diehtobáŋku" ("dearvvašvuohta" Cmp/SplitR) "dievdu" "doaibma" "eamiálbmotdutkan" ("dutkan" Sg) "ealáhus" "fágabiras" "fierpmádat" "giella" "gilli" "giliássi" ("guovlu" Pl) "kulturovdáneapmi" "kulturviessu" "plána" "doaibmaplána" "geavaheaddji" "gelbbolašvuođadoaibma" "giellaláhka" "girjjálašvuohta" "guolásteapmi" "guovddášguovlu" "hálddašanguovlu" "iešdovdu" ("nuoraidsearvi" Pl) "oahppoplána" "oktasaš" "perspektiiva" "mánná" "nisu" "álbmot" "bargi" "geavaheaddji" "jienastuslohku" "kulturmuitu" "kulturmuitohálddašeapmi" "kulturmuitosuodjalus" "kultuvra" "náhkkegálvu" "nuorra" "oktavuohta" "resursageavaheapmi" "riggodat" "riikkajoavku" "searvevuohta" "searvi" "servodat" ("siida" Pl) "siseana" "siseatnansiida" "sisafárren" "sosiáladutkan" "strategiijaplána" "valáštallan" "vierru" "vuohki" "vuoigatvuohta" "vuoigatvuođabargu" (PUNCT LEFT) ;

LIST SAPMI-PROP-HEAD = "allaskuvla" "bálvalusguovddáš" "cup" "dihtorlávdegoddi" "dearvvašvuođadutkanguovddáš" "doavttersearvi" "ealáhusfoanda" "eanahálddahus" "giellaláhka" "giellalávdegoddi" "giellaráđđi" "guovddáš" ("joatkka" Cmp/SgNom) "kulturmuitoráđđi" "l" "L-97" "leatna" "meašttirgilvu" "ossodat" "ovdanahttinfoanda" "parlamentáralaš" ("riddu" Cmp/SgNom) "sátnevuorká" "sierrabibliotehka" ("siida" Sg)"teáhter" "valáštallanlihttu" "válgalávdegoddi" "válgalihttu" "vuoigatvuođalávdegoddi" "vuoigatvuođalávdegoddi" ;


# Gen MODIFIERS; most are kalled SEM+

# HUMAN

#!! * **SEMnotPossessor** Removes Gen if you are not a possible possessor (a human) # HAB-ACTOR
REMOVE:SEMnotPossessor Gen IF (0 (N Acc) or (Rel Gen)  LINK NOT 0 HUMAN-INDIVIDUAL OR HUM-FUNCTION OR HUM-ATTR OR HUM-VERB  OR HAB-ACTOR OR ABSTR-TEXT  OR ANIMAL OR TITLE OR NATION OR ("dállu") OR ("báiki") OR ORGANIZATION OR Prop OR Sem/Hum) 
(NEGATE -1 Num OR Pr)(*1 ("bearaš"))(NEGATE 0 ANIMAL LINK *1 FAMILY-ALSO-ABSTRACT BARRIER NOT-NPMOD)(NEGATE 0 ("viidni") OR ("čáhci") OR ("illu") LINK -1 ("eallin"))(NEGATE 0 TIME LINK NOT -1 ("árvu") + Loc OR ("árvvus")) ;
	#$ Biret addá skeaŋkka Lemehii. 
	#$ Moai oste bileahta Harry Potter-filbmii.
	#% Abrama áhká namma lei Sarai, ja Nahora eamit lei Milka, Hanana nieida.
	#% Loahpas jearai listtu Anders Somby jr. sudnos njuolga háliida go čearru gulahallat, juo dahje ii.
	#% Hearrá guhte lea profehtaid vuoiŋŋa Ipmil, lea vuolggahan eŋgelis čájehit bálvaleddjiidasas dan mii fargga dáhpáhuvvá.


#!! * **SEMnotHUM** removes Gen. This is when an NP is thought to be the OBJ, because it's not in the human sets and to the right is NON-FAMILY *njálgáid mánáide*. 
REMOVE:SEMnotHUM Gen IF (0 (N Acc) LINK NOT 0 Prop + Sem/Plc OR HUMAN-INDIVIDUAL OR HUMAN-GROUP OR Prop OR PROFESSION OR OFFICE OR HUM-FUNCTION OR HUM-ATTR OR HUM-VERB OR ABSTR-TEXT OR PLACE OR NATION OR EDUCATION OR ORGANIZATION OR INSTITUTION OR INDUSTRY OR TIME OR Sem/Domain OR Loc)(NEGATE 0 (NomAg Gen))(NEGATE -1 Num OR Ord OR Pr)(NEGATE -1 @CNP LINK -1 Gen)(*1 NON-FAMILY OR Num BARRIER NOT-A)(NEGATE 1 COMMA)(NEGATE *1 TRANS-V + FMAINV BARRIER S-BOUNDARY);
	#$ Mun roggen ozastan njálgáid mánáide.
	#$ Mun lean boahtán bidjat soahpameahttunvuođa bártni ja áhčis gaskii.
	#$ Lei hui vuogas boahtit Kárášjohkii nuppiid badjemánáid lusa, dáppe in dárbbašan doarrut ja cielauhuvvot ja doaruhuvvot go ledjen badjeolbmuid mánná.
	#% Čáhppesmurjjiid olbmot gazze milkkiin ovdal.
	#% Mo almitdási riekti meannudii vaikkoba dakkár vuolit dási olbmuid vihtančilgehusaiguin?
 
#!! * **SEMXr2066** Removes Gen if there is a human or org to the right, exeption for *čállingiela áhčči* and so on
REMOVE:SEMXr2066 Gen IF (0 Der/NomAct)(*1 HUMAN-INDIVIDUAL OR PROFESSION OR OFFICE OR HUM-FUNCTION OR HUM-ATTR OR HUM-VERB OR ("bearaš") OR NATION BARRIER NOT-A LINK NOT 0 ("eadni") OR ("áhčči") OR ("čállit"));
	#$ Vuosttaš sámi oahppan dearvvašvuođabargit bohte Kárášjohkii 1970-logu álggus.
	#$ Dearvvašvuođabargiidlága § 10, mii gieđahallá dieđuid addima pasienttaide, ja pasientavuoigatvuođalága § 1-3, mii gieđahallá vuoigatvuođa informerejuvvon miehtamii, leat ovdamearkkat dakkár vuoigatvuođain.
 
#SELECT:SEMr2067 Gen IF (0 ("áhčči") OR ("ipmil"))(1 ("Vuoigŋa") OR ("vuoigŋa"));
	#$ Ehpet dii leat ieža sárdnumin, muhto Áhčádet Vuoigŋa sárdnu din bokte.

SELECT:SEMalmminávli Gen IF (0 ("albmi"))(1 ("návli"));
	#$ Guhkes ja losses vuojeheame maŋŋá sii deive Davvinástti dahjege Almmi Návlli lusa.

SELECT:SEMpálla Gen IF (0 ("pálla"))(1 ("sadji"));
	#$ Lohke dat golmmas geat pálla saji ožžo.

#REMOVE:SEMr2068 Gen IF (0 ("vuoigŋa"))(1 ("ipmil"));  
	#$ Muhto jos mun áján olggos bahá vuoiŋŋaid Ipmila Vuoiŋŋa fámuin.

#!! * **SEMgenEss** Removes Acc if there is Gen + Ess, like *dálu eamidin*
REMOVE:SEMgenEss Acc IF (0 PLACE LINK 0 Gen)(1 HUMAN LINK 0 Ess);
	#$ Vánit eahpiduvvo ahte stáhta geatnegasvuođat sámiid ektui 27. artihkkala mielde dagahivččii stáhta dihto guovlluid eaiggádin geatnegassan doaimmahit eaiggáduššanvuoigatvuođa nu ahte deavddášii 27. artihkkala geatnegasvuođa.	
	#$ Máret beasai dálu eamidin.


#REMOVE:SEMTr Gen IF (0 (N Acc) LINK NOT 0 TIME-N OR Sem/Route LINK *1 TIME-N + Gen BARRIER NOT-NPMOD OR CC)(*-1 STRICT-TV-V BARRIER SV-BOUNDARY OR Num) ; #not a good rule
	#$ Vuojadansearvi váldá din fitnodagain oktavuođa vahkku 20:s, gullat mo dii sáhttibehtet veahkehit.

#REMOVE:SEMr2070 Gen IF (0 SINGLE-SOUND)(1 ROUTE);
	#$ Ii son bealkke ii ge čuorvvo, ii ge oktage gula su jiena gáhtaid alde.
 
#!! * **SEMXxr2071** Removes Gen: Nobody can possess a Proper name? Except from (Pron Pers) and Sem/Fem OR Sem/Mal
REMOVE:SEMXxr2071 Gen IF (NEGATE -1 Pr)(NEGATE *-1 Num BARRIER NOT-A)(0 (N Acc) LINK NOT 0 NATION OR Sem/Plc OR TIME OR Sem/Fem OR Sem/Mal OR ORGANIZATION OR ACR)(1 Prop LINK NOT 0 NATION OR ("Vuoigŋa")) (NEGATE 1 Prop + Sem/Org)(NEGATE 1 Prop + Attr LINK 1 Prop + Sem/Org)(NEGATE 0 N-PX LINK 1 Sem/Fem OR Sem/Mal OR Sem/Sur LINK *1 Po BARRIER NOT-NPMOD)(NEGATE -1 CC LINK -1 Prop + Sem/Plc LINK 3 Gen LINK 1C Nom);
	#$ Moai boraime njálgga gáhkuid Hánssa ođđa láibbohagas.#Comment: except it doesn't hit this at all..
	#$ ...go čielggadii oamastanvuoigatvuođa Finnmárkku eatnamiid hárrái.
	#% Dalle ledje njeallje bistevaš skuvlla Finnmárkkus. 
	#% Romssa ja davágeaži Norrbottena boazoorohagat ja čearut riikaráji guoras ja konvenšuvdnaguovlluin. 
	#% Romsa ja davágeaži Norrbotten, Davit Nordlánda ja gaska ja lulimus Norrbotten, Helgelánda ja Västerbotten, Davvi-Trøndelága ja davágeaži Jämtlánda. #Comment: davágeaži can "possess" (or mofify if you will) Jämtlánda.
	#% Loahpas jearai listtu Anders Somby jr. sudnos njuolga háliida go čearru gulahallat, juo dahje ii.

#!! * **SEMXxPropOrg** Removes Gen: Who can possess Prop Sem/Org?    
REMOVE:SEMXxrPropOrg Gen IF (NEGATE -1 Num)(0 (N Acc) LINK NOT 0 NATION OR Sem/Plc OR TIME OR ACR)(1 Prop + Sem/Org LINK NOT 1 Prop + Sem/Org);
	#$ Álggos coggalii Anti vuosttaš spáppa Sørøy Glimt mollii 22 minuvtta maŋŋel go čiekčamat ledje álgán Niitoguolban stadionas.

#!! * **SEMlohkat**     
REMOVE:SEMlohka Gen IF (NEGATE -1 Num)(0 Sem/Edu + Acc LINK 1 (".*dássi"r))(*-1 ("lohkat") BARRIER SV-BOUNDARY);
	#$ Sámegieloahput mat addet sámegielgelbbolašvuođa lohkagoahtit sámegiela dahje eará oahpuid bachelordásis Sámi allaskuvllas.
 
#!! * **SEMNation** Removes Gen: Who can possess Sápmi?    
#REMOVE:SEMNation Gen IF (0 (N Acc) + Prop)(1 NATION + Prop);
	#$ Bovden Eurohparáđi Sápmái. 
	#% Oslo Sámi Searvi
	#% Norgga Sápmi
    
#!! * **SEMdep** Select Gen if main-organization in front of department   
SELECT:SEMdep Gen IF (0 MAIN-ORGANIZATION OR EDUCATION)(1 DEPARTMENT) ;	
#!! * **SEMorghum** select gen if organization or education in front of human or text  
#SELECT:SEMorghum Gen IF (0 ORGANIZATION OR EDUCATION - ("oahpahus") - ("oahppu") LINK NOT 0 Attr)(1 HUMAN OR TEXT LINK NOT 0 Ess OR Sem/Sur)(NEGATE 1 ("norgalaš"))(NOT 0 Nom)(NEGATE 0 ("oahppu") LINK -1 ("gazzat")) ;	
	#$ gaskadási ohppiid
	#% Alaskai gazzat oahpu máttuid bálgáid maŋis.
	#% ja leat njunnošis fállamin oahppofálaldagaid sámi servodagaid dárbbuid ja ávkki vuođul.

LIST WEBPAGE = (".*\.no"r Prop) (".*\.fi"r Prop) ;

SELECT:SEMwebpage Gen IF (0 WEBPAGE)(1 TEXT) ;	
	#$ eandalit dán oahpa.no neahttasiiddus


SELECT:SEMedu Acc IF (-1 ORGANIZATION OR EDUCATION - ("oahpahus") OR Sem/Edu LINK NOT 0 Attr)(0 Sem/Edu + Acc OR ("master") + Acc LINK NOT 1 N OR @CNP) ;	
	#$ NOKUT lea dohkkehan Sámi allaskuvlla viđa jagi oahpaheaddjeoahpuid masteriid vissis eavttuiguin.
    
    
#!! * **SEMXr2073** Remove Gen: Accusative in front of a human group *loktema sámiid buorrin*
REMOVE:SEMXr2073 Gen IF (0 (N Acc) LINK 0 Sem/Act)(NEGATE -1 Num OR Ord)(1 HUMAN-GROUP);
	#$ Ráđđehus deattuha sámi kulturáddejumi gelbbolašvuođa loktema sámiid buorrin.
	
#!! * **SEMr2074** Selects Gen in front of HUMAN-GROUP
SELECT:SEMr2074 Gen IF (0 Sem/Plc OR Sem/Org OR ACR)(1 HUMAN-GROUP) ;	# or (".*joavku"r) ?
	#$

#!! * **SEMGenOrg** Selects Gen in front of Sem/Act
SELECT:SEMGenOrg Gen IF (0 Sem/Org LINK NOT 0 Nom)(1 Sem/Act OR Der/NomAct) ;
SELECT:SEMGenOrg Gen IF (0 Sem/Org LINK NOT 0 Nom)(1 CC LINK 1 Sem/Org LINK 1 Sem/Act OR Der/NomAct) ;
	#$ Sámediggeráđđi ádde ahte dáhpáhus lea maŋidan departemeantta ja ráđđehusa barggu


#!!  * **SEMactor** Select Gen in front of ABSTRACT and RIEKTEDILLI *unnitlogu oaidninčiegas*
SELECT:SEMactor Gen IF (0 HAB-ACTOR + Acc OR (Pron Acc) LINK NOT 0 Loc)(*1 ABSTRACT - ("ovdamearka") OR RIEKTEDILLI BARRIER NOT-NPMOD)(NEGATE *-1 TRANS-V + FMAINV OR Inf BARRIER SV-BOUNDARY OR Acc)(NEGATE 1 ("ja"))(NEGATE 0 (Pron Dem) LINK 1 Acc);
	#$ Romeriid vealahanáššis lea dárbu geahččat sihke ovttaskas olbmo, struktuvrralaš ja unnitlogu oaidninčiegas.

#!! * **SEMXr2076** Selects Gen if you are HUMAN or Pron with an ABSTRACT to your right *iežaset vuoigatvuođa*
SELECT:SEMXr2076 Gen IF (*-1 TRANS-V BARRIER SV-BOUNDARY OR Acc LINK 0 FMAINV OR Inf )(0 Acc + HUMAN OR Acc + Pers OR Acc + Refl)(*1 ABSTRACT OR RIEKTEDILLI BARRIER REAL-V OR Gen OR (":") LINK 0 Acc); # LINK NEGATE 0 VERBAL-ACTIVITY-V OR DADJAT OR HUMAN-AGENT-V OR NOT-HUMAN-OBJECT-V
	#$ Mátta-Norgga almennehiin dihtto ahte giliolbmot gitta 1700-lohkui dahke iežaset vuoigatvuođa almennehiidda erenoamážit gustojeaddjin Gonagasa eiseválddiide.

#!! * **VocNom** 

SELECT:VocNom (N Nom) IF (-1 COMMA LINK -1 Imprt LINK -1 BOS)(1 COMMA OR EOS) ;
	#$ Gula, Pippi, oažžugo čoaggit visot maid dievás gávdná?


#!! * **SEMyouareNom** Removes Gen and Acc when 0 FAMILY or PROFESSION because you are Nom. Not if -1 Num and VFIN is LEAT or IV *Oahpai go Sire sámegiela*
REMOVE:SEMyouareNom Gen OR Acc IF (*-1 VFIN BARRIER SV-BOUNDARY)(0 (N Nom) + FAMILY OR NON-FAMILY OR PROFESSION1)((*1 EOS BARRIER WORD - Acc OR TIME-ADV OR (VGen)) OR (*1 Acc BARRIER NOT-NPMOD LINK NOT 0 TEXT LINK 1 Loc OR Ill LINK 1 EOS)) (NEGATE *-1 NUM - NUMUNIT BARRIER NOT-Attr LINK *-1 LEAT-FIN-NOT-IMPRT OR IV OR TV BARRIER S-BOUNDARY)(NEGATE *-1 TRANS-V + FMAINV BARRIER NOT-NPMOD LINK NEGATE *2 Acc BARRIER SV-BOUNDARY)(NEGATE -1 SAPMI)(NEGATE *-1 TRANS-V + FMAINV BARRIER S-BOUNDARY LINK -1 Nom OR ("vai")) ; # (NEGATE *-1 NUM - NUMUNIT BARRIER NOT-Attr LINK 
	#$ Oahpai go Sire sámegiela?
	#$ Oahpaigo Sire sámegiela universitehtas?
	#$ Do boahtá Lemet Niillas, min ođđa oahpaheaddji.
	#% Jávregáttis leat guokte olbmá.
	#% Doppe Ambram čuorvvi Hearrá nama.
	#% Giliviessu geassá 200 olbmo. 
	#% Dovdágo Biret Turi Lene Antonsen?
	#% Go Bargiidbellodat rivvii NSR:s fámu Sámedikkis 2016:s, de moittii NSR Bargiidbellodaga rihkkume dehálaš vieru go válljejedje čoahkkinjođiheaddji iežaset searvvis.
	#Comment: If you get problems with Acc, concider making a separate rule for it. 

#!! * **SEMyouareGen** Removes Nom if movement verb to the left and illative to the right, because you are the modifier of Ill *mannat Madame Tussaud kabinehttii*
REMOVE:SEMyouareGen Nom IF (*-1 MOVEMENT-V + FMAINV BARRIER SV-BOUNDARY)(0 Nom OR Gen LINK NOT 0 Acc)(1 Ill)(NEGATE 0 Prop LINK 1 Prop + Ill)(NEGATE 1 ("ealáhat") + Ill);
	#$ Odne áigo mannat Madame Tussaud kabinehttii.
	#$ Háliidetne geahččat dien nieidda, gii easká lea boahtán ránnjá vissui, eadni, dajai Tommi.
	#$ Árračavčča bođii skuvlladirektevra Gjermundsen guossái.
	#% 1980:s manai Anny Hámmárfestii vázzit buohccedivššárskuvlla.

#!! * **SEMnotNom** Removes Nom if a Nom to the right followed by a transitive verb. 0 is animate and to the right is Ill. You are the modifier of Ill
REMOVE:SEMnotNom Nom IF (*-1 TRANS-V + FMAINV BARRIER SV-BOUNDARY LINK *-1 Pron + Nom OR N + Nom BARRIER S-BOUNDARY)(0 Acc OR Gen)(NEGATE *-1 MOVEMENT-V BARRIER SV-BOUNDARY)(1C Ill OR Acc)(NEGATE 0 Num) ;
	#$ Máttimus sápmelaččat eahpidedje dáža dutkiid eanet go davimus sápmelaččat.
	#$ 1961 manai Fokstad ealáhahkii.
	#$ Doppe Abram čuorvvui Hearrá nama.
	#% Finnmárkkuláhka ii nuppástuhte geavahan- ja oamastanvuoigatvuođaid Finnmárkku eatnamiidda (earret opmodagaid sirdima Statskog SF as Finnmárkkuopmodahkii).


#REMOVE:XXX Acc OR Nom (*-1 MOVEMENT-V BARRIER SV-BOUNDARY)(0 (N Gen))(*1 Ill BARRIER NOT-Attr)(NEGATE 0 ABSTR-ROUTE + Acc OR ("ádjit"))(NEGATE 0 Nom LINK *-1 MOVEMENT-V + (+FMAINV) BARRIER SV-BOUNDARY) ;
	#% 1980:s manai Anny Hámmarfestii vázzit buohccedivššárskuvlla. 
	#% Muhtumat válljejedje vázzit beanagullama skuvlii nuppi guvlui.
	
#SELECT:SEMbody Gen (*-1 TRANS-V + FMAINV BARRIER SV-BOUNDARY)(0 Prop OR Pron)(*1 BODY + Acc BARRIER NOT-Attr)(NEGATE 0 (Pron Dem) OR (Pron Refl))(NEGATE 1 Gen LINK 1 Po) ;
	
REMOVE:SEMLang N + Gen IF (0 Nom OR Acc LINK NOT 0 NATION OR NATIONAL-IDENTITY OR Sem/Plc OR Sem/Hum OR HUMAN OR Sem/Time OR Sem/Org OR Sem/Lang)(1 Sem/Lang - Gen LINK NOT 0 ("suopman") OR ("giella") OR ("váldogiella") )(NEGATE -1 Pr OR Num) ;
	#$ Jorgalanprográmma davvisámegielas dárogillii dahká álkibun geavahit sámegiela.
	#% Seminára váldogiella lea davvisámegiella. 
	
	
# Accusative in front of a NATION or "sápmi":

#!! * **SEMXxr2081** Removes Gen if NATION or POLITICAL-PLACE are to your right *dilálašvuođaid sámi*
REMOVE:SEMXxr2081 Gen IF (0 Acc LINK NOT 0 NATION OR GENERAL-PLACE OR ORGANIZATION OR EDUCATION OR ANIMATE OR ("evangelium") OR Prop + Sem/Plc OR ACR)(0 N LINK NEGATE *-1 Pr OR Num BARRIER NOT-NPMOD)(0 N LINK NEGATE -1 Attr LINK -1 Pr OR Num) (*-1 TRANS-V BARRIER SV-BOUNDARY)(1 NATION OR POLITICAL-PLACE LINK NOT 0 ("sápmi" Pl));
	#$ Ráđđehus láhčá dilálašvuođaid sámi kultuvrra máŋggadáfotvuođa seailluheapmái.
	#% Politihkalaš bellodagat ja listtut ráhkkanit čavčča gieldda- ja fylkkadiggeválggaide.
	
    
# "Sámegiela hálddašanguovlu/hálddahusguovllu"
#!! * **SEMr2082** Selects Gen if you are LANGUAGE, giellanjuolggadus or giellaláhka in Acc-case and to your right is SAPMI-N-HEAD *sámegiela hálddašanguovlun*
SELECT:SEMr2082 Gen IF (0 LANGUAGE + Acc OR ("giellanjuolggadus") + Acc OR ("giellaláhka") + Acc)(1 SAPMI-N-HEAD);
	#$ Sámiláhka meroštallá sámegiela hálddašanguovlun Kárášjoga, Guovdageainnu, Unjárgga, Deanu, Porsáŋggu ja Gáivuona gielddaid. 
    
#SELECT:SEMr2083 Gen IF (0 ("sámiláhka"))(1 ("giellanjuolggadus"));    
   
#!! * **SEMr2084** Selects Gen for *hálddašanguovllu suohkanat/gielddat*
SELECT:SEMr2084 Gen IF (0 ("hálddašanguovlu") OR ("hálddahusguovlu") LINK 0 Acc)(1 ("gielda") OR ("suohkan") OR ("fylka"));   
	#$ Hálddašanguovllu suohkanat leat ovttasráđiid mearridan nu.
    
# Gen + MEDIA
#SELECT:SEMr2086 Gen IF (0 NATION LINK 0 Acc)(1 MEDIA);
	#$ Mediat Ruoŧa rádio, Oahpahusrádio ja Ruoŧa Televišuvdna, ožžot jagis 2002 ođđa sáddenlobiid.    

#!! * **SEMguovttis** selects genitive in front of guovttos and guovttis
SELECT:SEMguovttis (N Gen)(0 HUMAN LINK 1 GUOVTTIS) ;
	#$ Muhto loahpas nagodeigga nissoniid guovttos oažžut botnjiideaskka mielde.
    
#!! * **SEMXr2087** selects Gen if you are a Prop/Plc followed by "gielda" or "suohkan"
SELECT:SEMXr2087 Gen IF (0 Prop + Sem/Plc + Acc)(*1 ("suohkan") OR ("gielda") BARRIER NOT-NPMOD LINK 0 Sg); 
	#$

#!! * **SEMXr2087** Selects Gen if you have "eana" or "guovu" immediately to your right *Gomorra eatnamii*
REMOVE:SEMXr2087 Acc IF (0 Prop + Gen + Sem/Plc)(1 ("eana") OR ("guovlu")); 
#SELECT:SEMr2087 Sem/Plc + Gen IF (0 Prop + Acc)(1 CC LINK 1 Gen LINK 1 ("eana")); 
	#$ Sodoma ja Gomorra eatnamii geavvá geahppaseabbot duopmobeaivvi go dan gávpogii.

#!! * **SEMhumgroup** , tja
SELECT:SEMhumgroup HUMAN-GROUP + Gen IF (-1 Prop)(1 (".*bálkkašupmi"r)) ;
	#$ Davviriikkaid ráđi girjjálašvuođabálkkašupmi. 
    
# "guovddášlihttu"
#SELECT:SEMr2088 Gen IF (0 ("gielda" Pl))(1 ("guovddášlihttu"));
	#$

LIST SIERRA-GUOVLU = (".*guovlu"r) "suomabealli" "norggabealli" "riddu" "ruoŧabealli" "ruoššabealli" ;


#!! * __SEMplcGen_a__ Selects Gen if you are GEOGRAPHICAL-PLACE or Prop + Sem/Plc in front of PLACE-ADV *Finnmárkku máttabealde*
SELECT:SEMplcGen_a Gen IF (0 Prop + Sem/Plc OR GEOGRAPHICAL-PLACE LINK 0 Acc)(1 PLACE-ADV);
#!! * __SEMplcGen_b__ Selects Gen if you are GEOGRAPHICAL-PLACE or Prop + Sem/Plc after a PLACE-ADV
SELECT:SEMplcGen_b Gen IF (0 Prop + Sem/Plc OR GEOGRAPHICAL-PLACE LINK 0 Acc)(-1 PLACE-ADV);
	#$ vealgelogahallama mearkkašumi fylkkain Finnmárkku máttabealde
	#$ Nuppe dáfus ledje mihá eanebut geat bivde lagabus gátti. 
	#Comment: I've added "lagabus" to the set PLACE-ADV, but please observe that this adv only can be positioned IN FRONT of the Sem/Plc-word (?bivde gátti lagabus). If a situations should emerge, concider dividing the PLACE-ADV to two (or more) sets for adv based on weather they can be placed in front or following the Sem/Plc-word, or both. 

	
#!! * **SEMplcGen2** Removes Gen in front of a GENERAL-PLACE or POLITICAL-PLACE, if you are a noun *bidjen hildu sadjásis* 
REMOVE:SEMplcGen2 Gen IF (0 N LINK NOT 0 ABSTR-PLACE OR ACTIVITY OR HUMAN OR TOPIC OR PROCESS OR INDUSTRY OR INSTITUTION OR ORGANIZATION OR TIME OR Prop OR SIERRA-GUOVLU OR SAPMI OR LANGUAGE OR ("bealli") LINK NEGATE *-1 Pr BARRIER NOT-NPMOD)(1 GENERAL-PLACE + Loc OR POLITICAL-PLACE + Loc LINK NOT 0 ("biras" Pl Loc) OR ("sápmi"))(NEGATE *-1 Num + Nom BARRIER NOT-A) ;
	#$ Mun oidnen dan golbmasa gávpešiljus.
	#$ Mun bidjen hildu sadjásis.
	#% Sii leat ságastallan daid sápmelaččaiguin geat dárbbašit dálvejagi ja geassejagi guohtuneatnamiid nuppi bealde riikaráji.	
	#% 12 ođđa njoammuma Romssas.

#!! * **SEMplcGen3** Removes Gen in front of GENERAL-PLACE or POLITICAL-PLACE, if you are ABSTR-TEXT or TEXT *cealkámušaid guovlluid dearvvašvuođafitnodagaid jahkedieđáhusain*
REMOVE:SEMplcGen3 Gen IF (0 ABSTR-TEXT OR TEXT LINK NEGATE *-1 Pr BARRIER NOT-NPMOD)(1 GENERAL-PLACE OR POLITICAL-PLACE LINK NOT 0 ("biras" Pl Loc) OR ("sápmi"));
	#$ Sámediggi sáhttá maid buktit cealkámušaid guovlluid dearvvašvuođafitnodagaid jahkedieđáhusain ja plánaárvalusain.

#!! * **SEMXr2079** Removes Gen if you are Acc in front of MANNU *guđii virggi skábmanánu 1. b.*
REMOVE:SEMXr2079 Gen IF (0 Acc LINK NOT 0 ("jahki") OR Sem/Time OR LAHKA OR ("Sámediggi") OR Sem/Year)(NEGATE *-1 Num OR Pr BARRIER NOT-NPMOD)(1 MANNU);
	#$ Carsten Smith guđii Sámi vuoigatvuođalávdegotti ovdaolbmo virggi skábmamánu 1. b. 1985.	

# Genitive in front of ''virgi''
#REMOVE:SEMr2080 Acc IF (0 OFFICE OR PROFESSION)(1 ("virgi"));
	#$ Carsten Smith guđii Sámi vuoigatvuođalávdegotti ovdaolbmo virggi skábmamánu 1. b. 1985. #Ritva comment: dása gal ferte gávdnot eará čoavddus (gullá oktii bajit njuolggadusain).



#!! * **SEMxhab** Selects Acc if COPULAS to the left of HAB-ACTOR *lea min*
SELECT:SEMxhab Acc IF (0 HAB-ACTOR)(*-1 COPULAS BARRIER NOT-NPMOD LINK -1 (Pl Acc) LINK -1 ("mii" Sg Nom)) ; 
	#$ Mii ávkkiid lea min doppe johtit?

#!! * **SEMxboaris** Selects Gen if you are *boaris* in front of SAPMI-N-HEAD or SAPMI-PROP-HEAD *sii dolvo áhku boarrásiid siidii*
SELECT:SEMxboaris Gen IF (0 ("boaris" Pl Acc) LINK 1 SAPMI-N-HEAD OR SAPMI-PROP-HEAD) ;
	#$ Sii dolvo ieža áhku boarrásiid siidii. #Comment: modified, but still not sure we need this rule at all.. 

#!! * **EMeallimamuorra** Selects Gen eallima muorra
SELECT:SEMeallimamuorra Gen IF (0 ("eallin" Sg) LINK 1 ("muorra")) ;


#!! * **ACRGen** Selects genitive: NRK Sápmi
SELECT:ACRGen (ACR Gen)(NOT 0 Attr)(1 Prop + Sem/Obj OR Prop + Sem/Plc OR Prop + Sem/Org) ;
	#$ NSR Lulli-Sámi sámediggeáirras, NRK Sápmi

#!! * **ACRAttr** Selects genitive: IL Nordlys
SELECT:ACRAttr (ACR Attr)(1 Prop + Sem/Obj OR Prop + Sem/Plc OR Prop + Sem/Org) ;
	#$ IL Nordlys


#!! * **AccSemFeat** Selects genitive: IL Nordlys
SELECT:AccSemFeat Acc IF (0 Sem/Feat)(*-1 ("vásihit") BARRIER SV-BOUNDARY);
	#$ Váhnemat vásihit unohisvuođaid dearvvašvuođasuorggi deaivvadeamis.


# "heagga"
#SELECT:SEMr2090 Gen IF (0 (Acc))(1 ("heagga") LINK 0 Px);
	#$ Sii ealihit sin heakkaset vuodjimiin ja geasehemiin.

#REMOVE Acc IF (-1 HUPMAT)(0 HUMAN) ; 
	#$ Mun human Tronda beatnaga birra.

# buo.
#SELECT:SEMr2091 Acc IF (*-1 ("buo") BARRIER NOT-NPMOD)(0 (N Gen));
	#$ , buo. sierra sámestáhta.

# ovttasráđiid
#REMOVE:SEMr2092 Gen IF (1 ("ovttasráđđi" Pl Gen));
	#$

SELECT:SEMváldit Acc IF (0 ("boddu") + Gen LINK *-1 ("váldit") OR ("doallat") BARRIER NOT-NPMOD);
	#$ NRK lea váldán biilla giđđajohtima sáddemis.


#!! * **SEMXxr2093** Selects accusative: if váldit to the left and mielde to the right: *váldit mielde*
SELECT:SEMXxr2093 Acc IF (*-1 ("váldit") BARRIER NOT-NPMOD)(0 Gen)(1 ("mielde") OR ("fárrui"));
	#$ Sámediggi lea bivdán ráđđehusa váldit Sámedikki mielde árvoháhkanprográmmabargui.
	#$ Ii lean dábálaš váldit mánáid mielde rohkosvissui.

# "barggahit"
#SELECT:SEMr2094 Acc IF (*-1 ("barggahit") BARRIER NOT-ADV-PCLE)(0 HUMAN)(NEGATE 0 Gen LINK 1 HUMAN); 
	#$    

#!! * **SEMXr2096** Removes genitive: because Accusative in front of an organization
REMOVE:SEMXr2096 Gen IF (NEGATE -1 Num)(0 Acc LINK 0 TEXT OR ABSTR-TEXT OR ABSTRACT)(1 ORGANIZATION - ("poasta"));  
    # We could do it the other way: IF (O Acc LINK NOT HUMAN OR POLITICAL-PLACE OR GENERAL-PLACE).
    # Or we could concider to widen the other part: (NEGATE 1 HUMAN-INDIVIDUAL OR HUM-FUNCTION OR HUM-ATTR OR HUM-VERB OR ANIMALS OR ABSTRACT)
	#$ Dat lea goappaš riikkaid eiseválddiid hálddus addit lobi boazoguohtunlávdegotti ja dárkkistangotti miellahttui.
	#% Ruđat ledje biddjojuvvon Giellaprošeavtta postii Sámedikki bušeahtas, ja Sámediggi ávžžuhii giellabirrasiid ohcamiid sáddet Sámediggái.
  
#REMOVE:SEMr2097 Gen IF (0 N LINK NOT 0 NATION OR TIME)(NEGATE -1 Num)(1 ("sámediggi") OR ("Sámediggi"));  
	#$ Sámedikkit nammadit lahtuid sámedikkiid gaskasaš ovttasbargoorgánii.  

#Xx
#!! * **SEMGenORG** selects Gen (modifier): in front of an organization *Stáhta Oahpahuskantuvra*
SELECT:SEMGenORG Gen IF (0 NATION LINK 0 Acc)(*1 ORGANIZATION OR INSTITUTION OR EDUCATION OR INDUSTRY BARRIER NOT-NPMOD OR CC LINK NOT 0 ("skuvla"))(NEGATE *-1 STRICT-TRANS-V BARRIER SV-BOUNDARY OR Acc)(NEGATE -1 Gen LINK -1 CC LINK -1 Acc LINK *-1 STRICT-TRANS-V BARRIER SV-BOUNDARY OR Acc) ; 
	#$ Artihkal maid dás almmuhat, lea logaldallan maid son doalai internáhttakonfereanssas maid Stáhta Oahpahuskantuvra Finnmárkkus lágidii Guovdageainnus 1999:s.
	#$ 80-logu beallemuttus ásahuvvojedje sámi organisašuvnnat.
	#$ Norgga ja Ruoŧa ráđđehusaide kommišuvdna lea jagi 1997 cuoŋománu rájis bargan doaimmaiguin mat dasa ledje biddjon ja ela odne, čoahkkimis Helssegis, loahpahan barggu ovttahienalaš evttohusain.
	#% 1970-1980 jagiid ásahedje maiddái sámi kulturguovddážiid dáid skuvllaid olis, ja maid stáhta ja fylkkagielda ruhtadedje.
	#% Deanu vuoigatvuođaid oamasteaddjit stevdnejit Suoma stáhta diggái.
	#%  Deanu vuoigatvuođaid oamasteaddjit stevdnejit Suoma ja Norgga stáhtaid diggái.


#!! * **SEMGenORG** selects Gen (modifier): in front of an organization *Stáhta Oahpahuskantuvra*
SELECT:SEMGenOrgyear Gen IF (0 ORGANIZATION OR INSTITUTION OR EDUCATION OR INDUSTRY LINK 1 Sem/Year LINK 1 Sem/Prod-cogn OR Sem/Txt OR Sem/Money OR ("rehketdoallu")) ; 
	#$ Sámedikki 2017 bušeahta
SELECT:SEMsemyear Gen IF (0 Sem/Year LINK *-1 ORGANIZATION OR INSTITUTION OR EDUCATION OR INDUSTRY BARRIER NOT-ADV-PCLE - V)(1 Sem/Prod-cogn OR Sem/Txt OR Sem/Money OR ("rehketdoallu")) ; 
	#$ Sámediggeráđđi lea 2013 bušeahtas evttohan dan.
SELECT:SEMGenOrgtime Gen IF (0 (Sem/Measr_Time) LINK 1 Sem/Prod-cogn OR Sem/Txt OR Sem/Money OR ("rehketdoallu")) ; 


#REMOVE:SEMr2099 Gen IF (0 N LINK NOT 0 TIME OR ANIMATE OR NATIONAL-IDENTITY OR POLITICAL-PLACE)(NEGATE -1 Num)(1 ("servodat"));
	#$ Lea dan vuođul geatnegasvuohta ovddidit buot mánáid beroštumiid servodagas.  
    # Should consider if we could put in (1 ORGANIZATION). Then we have to add at least Prop as 0. 

# x    
#!! * **SEMgen1** removes Acc if buot, gait or buohkat in front of a genitive, followed by a plural noun *buot Norlándda ohppiid*
REMOVE:SEMgen1 Acc IF (-1 BUOT OR ("buohkat"))(0 (Sg Gen) LINK *1 (N Pl) BARRIER REAL-V);  
	#$ Son lea almmuhan eanemus girjjiid buot sámi čálliin. 
	#$ Kantuvra meannuda buot Nordlándda, Romssa ja Finnmárkku ohppiid ja studeanttaid loatna- ja stipeandaohcamiid, ja buot velggolaččaid máksingeahpádusohcamiid, geat orrot seamma guovllus.
 
    
# "bargat" + object: X
#!! * **SEMgen2** removes Acc if bargat or dihte are FMAINV or Inf and are found somewhere to the left of a Gen, which is followed by a noun *bargame boazodoallolága ođastemiin*
REMOVE:SEMgen2 Acc IF (*-1 ("bargat") BARRIER SV-BOUNDARY OR ("dihte") LINK 0 FMAINV OR Inf)(NEGATE -1 Pr)(0 Gen LINK NOT 0 TEXT OR ("bargu") OR TIME OR Refl LINK *1 N BARRIER VFIN)(NOT -1 Num + Acc) ; 
	#$ Leat maid bargame dálá boazodoallolága ođastemiin.
	#$ Dán leat mii bargan ovttas guoskevaš čearuid, boazoorohagaid, ja eiseválddiid áirasiiguin.
	#$ Almmatge lea jáhkkimis ahte boahtteáiggis bargojuvvo dáiguin jurdagiiguin, dannego kommišuvnna oaivila mielde eai boađe makkárge eará čovdosat nu bures buorrin buohkaide.
	#$ De son barggai guokte jagi buohccidivššárin Kolvereidas, Trøndelágas.

#!! * **SEMXr2103** Selects accusative: OASSI is usually accusative *hálddaša stuora oasi*
SELECT:SEMXr2103 Acc IF (*-1 TRANS-V BARRIER SV-BOUNDARY)(NEGATE -1 Num)(0 OASSI LINK 0 Gen)(1 Gen LINK NOT 0 Der/NomAg);
	#$ Dát foanda hálddaša stuora oasi Boazodoallošiehtadusa ruđain.
	#$ Son vuolggahii muhtun bálvaleaddji lusa vai son oččošii oasis viidnegárddi šattuin. 
	#Comments: Try to make a set of nouns which cannot be Gen in a AccGen string.
	# Possible Gen-construction: Nuppi oasi geavaheaddji ......

# Writing a TEXT:

#!! * **SEMXxr2104** Selects accusative: if WRITING-ACTIVITY-V to the left and you are a TEXT *čállá vaidaga*
SELECT:SEMtext Acc IF (*-1 WRITING-ACTIVITY-V BARRIER SV-BOUNDARY)(NEGATE -1 Num)(0 TEXT OR ABSTR-TEXT LINK 0 Acc)(NEGATE 1 CONCEPT OR TEXT OR ABSTR-TEXT OR LANGUAGE OR Po);
	#$ Sámi geavaheaddjit galggašedje sáhttit váidalit njálmmálaččat, man vuođul fylkkadoavttir de čállá váidaga, dárbbu mielde dulkka vehkiin.

#!! * **SEMXxacc**Removes accusative: if WRITING-ACTIVITY-V to the left and a noun to the right *čállit Norgga vásáhusaid*
REMOVE:SEMXxacc Acc OR Nom IF (*-1 WRITING-ACTIVITY-V + REAL-V BARRIER SV-BOUNDARY)(NEGATE -1 Num)(NEGATE 0 TEXT OR ABSTR-TEXT OR LANGUAGE OR ("namma"))(*1 N LINK 0 Gen OR Acc)(NEGATE 0 A LINK 1 N);
	#$ Dán oasis mii čállit Norgga ja Ruoŧa vásáhusaid ja juohkit daid osiide.
	#% Dá lea son Deanu girjerádjosis nuoraide muitaleamen mo čállit buori giehtačállosa.
	#% Sii bovdejedje 400 neavttára mielde čállit nama seksuálaillasteami vuostá.

REMOVE:SEMXxacc Gen IF (*-1 WRITING-ACTIVITY-V BARRIER SV-BOUNDARY)(0 TEXT OR ABSTR-TEXT OR ("namma"))(NEGATE 1 TEXT OR ABSTR-TEXT OR ("namma"));
	#$ Sii bovdejedje sin čállit nama seksuálaillasteami vuostá.

    
#!! * **SEMXxOrgRep** Selects genitive: An organization´s representative *Sámiráđi ovdaolmmoš*
SELECT:SEMXxOrgRep Gen IF (0 Acc LINK 0 ORGANIZATION OR ("Filbmainstituhtta") OR INSTITUTION OR Sem/Org)(1 PROFESSION OR HUM-FUNCTION OR HUM-ATTR OR HUM-VERB OR OFFICE LINK NOT 0 Pl)(NEGATE -1 OPRED-V LINK 2 Ess);
  	#$ Dán gáibideigga foandda hoavda Per Edvard Klemetsen ja Norgga Sámiráđi ovdaolmmoš Aslak Nils Sara gielddaministaris Arne Rettedalas geainna soai maŋŋebárgga deaivvadeigga Oslos.
  	#$ Jus áššebáhpirat leat váilevaččat, de galgá lávdegotti jođiheaddji addit sutnje, gii ovddida ášši, áigemeari divvut váilevašvuođaid. 
  	#% Bargiidbellodaga Jørn Are Gaski ii dáhtošii oaidnit Nordkalottfolket dievasčoahkkinjođiheaddjin.

# ášši
#! * __SEMr2106__ Gen if -1 NATIONAL-IDENTITY 
#SELECT:SEMr2106 Gen IF (-1 NATIONAL-IDENTITY)(0 ("ášši"));
	#$

# fáktemuš
#!! * **SEMxr2107** Acc if *-1 fáktemuš
SELECT:SEMxr2107 Acc IF (*-1 ("fáktemuš") BARRIER NOT-NPMOD)(0 (N Gen))(NEGATE 1 Acc OR Po);
	#$ Dain lea fáktemuš juohke balvva.
	#Comment:Subjeaktapredikatiiva objeakta @SPRED<OBJ. Gávdnošiigo dáidde oktasaš njuolggadus?
 
# egentlig laga for njuolggadus, men tester med et sett
REMOVE:SEMlahka-n Gen IF (0 LAHKA-N + Acc LINK 1 Gen LINK NOT 0 TEXT OR ABSTR-TEXT OR SATNI OR KLASS OR Der/NomAct OR Sem/Act)(*-1 TRANS-V BARRIER SV-BOUNDARY) ;
	#$ Finnmárkkuopmodat sáhttá addit dárkilet njuolggadusaid luondduvalljodagaid ávkkástallamii iežas eatnamiin, earret eará ferte olbmos lohpi dasa.

# SÁPMI 


#!! * **SEMXxr2108** Selects genitive if you are SAPMI with an Acc/Gen immediately to your left and a noun immediately to your right *girji sámi áššiid (birra)*
SELECT:SEMXxr2108 Gen IF (-1 Acc OR Gen)(0 SAPMI + Sg)(1 N); 
	#$ Čálán girjji sámi áššiid birra. 
	
#!! * **SEMsapmiModifier** Selects genitive (modifier): Sámi, suoma or ruoŧa  as modifier of noun *sámi oahpahus*
SELECT:SEMsapmiModifier Gen IF (0 SAPMI OR SUOPMA)(*1 N BARRIER NOT-A); # Comment: Replaces SEMXxr2112. The difference is that it doesn't have the same restrictions on verbs. # OR NATIONAL-IDENTITY-NOT-SAPMI
SELECT:SEMsapmiModifier Gen IF (0 SAPMI OR SUOPMA LINK NEGATE *-1 COPULAS)(1 PrfPrc LINK 1 N); # Comment: Replaces SEMXxr2112. The difference is that it doesn't have the same restrictions on verbs. # OR NATIONAL-IDENTITY-NOT-SAPMI
	#$ Ja muđui dán čoahkkimis badjesápmelaččat dorjo eanet sámi hábmejuvvon oahpahusa.
	#$ Dat movt sámi oahpahus lea doaibman.
#! * Select genitive (modifier): Sámi, suoma or ruoŧa  as modifier of noun, but with restrictions on which verb can appear to the left of you. 
	#$ Dalle čujuhuvvui ahte lea dárbu nannet doaimmaid mat ovddidit sámi dutkama, fágaidgaskasaš ovttasbarggu ja sámi áššiid oahpahusa.
	#$ Ráđđehus deattuha sámegiela ja sámi kulturáddejumi gelbbolašvuođa loktema.	 
	#$ Ráđđehus deattuha sámegiela ja sámi kulturáddejumi gelbbolašvuođa loktema, ja sámi bargiid bálkáheami.	 	
	#$ Soai áiguba ovddastit sámi álbmoga dáhtu go Tromsa ohcá Olympia gilvvuid lágidit 2014:s.	
	#$ Mii áigut oahpahit sámi álbmogii buot.	
	#% Seminarium heaittihuvvui, ja miššonearaid gevrejedje oahpahit sámiid dárogillii.  
	#% Ja muđui dán čoahkkimis badjesápmelaččat dorjo eanet sámi hábmejuvvon oahpahusa.

#!! * **SEMsamegiellaCoord** Selects genitive 
SELECT:SEMsamegiellaCoord Gen IF (0 ("sámegiella") LINK 1 CC LINK 1 ("sápmi") + Gen LINK 1 ("girjjálašvuohta") + Gen LINK 1 Acc) ;

#!! * **SEMAcc** Selects accusative #to be generalised
SELECT:SEMsamegiellaCoord Acc IF (0 N + Gen LINK NOT 1 Acc)(-1 ("girjjálašvuohta") + Gen LINK -1 ("sápmi") + Gen LINK -1 CC LINK -1 ("sámegiella") + Gen LINK *-1 STRICT-TRANS-V BARRIER SV-BOUNDARY) ;
	#$ Gielladieđagoahti čađaha sámegiela ja sámi girjjálašvuođa ovdáneami dutkamis ja oahpahusas.


#!! * **SEMálbmot** Selects genitive #to be generalised
SELECT:SEMálbmot Gen IF (0 Sem/Group LINK *1 Sem/Edu OR Sem/Hum BARRIER NOT-A) ;
	#$ Stáhtus eamiálbmot alit oahppo- ja dutkanásahussan eaktuda, ahte allaskuvla hábme ja ovddida oahppofálaldagaid.


#SELECT:SEMXxr2112 Gen IF (NEGATE *-1 ("máhttit") OR ("oahppat") OR ("oahpahit") OR ("juohkit") OR ("čállit") OR ("gallestallat") OR ("galledit") OR VERBAL-ACTIVITY-V BARRIER SV-BOUNDARY LINK 0 FMAINV OR Inf)(0 SAPMI OR SUOPMA)(*1 N BARRIER NOT-A); 
	#$ Dat movt sámi oahpahus lea doaibman.
	#% Mii áigut oahpahit sámi álbmogii buot. # Does not hit because of restrictions. Without the rule, this sentence gets an incorrect reading.

#! * Select genitive (modifier): Sámi, suoma or ruoŧa  as modifier of noun, but with restrictions on which verb can appear to the left of you.
#SELECT:SEMXr2113 Gen IF (NEGATE *-1 ("máhttit") OR ("oahppat") OR ("oahpahit") OR ("juohkit") OR ("čállit") OR ("gallestallat") OR ("galledit") OR VERBAL-ACTIVITY-V BARRIER SV-BOUNDARY LINK 0 FMAINV OR Inf)(0 SAPMI OR SUOPMA)(1 (@CNP) LINK 1 SAPMI OR SUOPMA OR NATION LINK 0 Gen); 
	#$ 

#!! * **SEMsapmiModifier2** Select genitive (modifier): Sámi, suoma or ruoŧa on both sides of CNP as modifier of noun *Suoma ja Ruošša soahti*
SELECT:SEMsapmiModifier2 Gen IF (0 SAPMI OR SUOPMA)(1 (@CNP) LINK 1 SAPMI OR SUOPMA OR NATION LINK 0 Gen); # Comment: Replaces SEMXxr2113. The difference is that it doesn't have the same restrictions on verbs. 
	#$ 1700-logu álggus leai Suoma ja Ruošša gaskasaš soahti giksin.
	#$ Das leat mielde olbmot geain lea sihke suoma ja sámi gullevašvuohta.
	#$ Beasaimet váldit buoremus beliid Suoma ja Norgga mánáidgárdevieruin veahkkin hábmet pedagogihka.

#!! * **SEMdazaModifier** Selects genitive (modifier): dáža, indiána, maya-indiána or romer  as modifier of noun *dáža oahpahus*
SELECT:SEMdazaModifier Gen IF (0 ("dáža") OR ("dáčča") OR ("indiána") OR ("maya-#indiána") OR ("romer"))(*1 N BARRIER NOT-A)(NEGATE 1 (N Ess));
	#$ Mu oainnu mielde lei dáža servodat ollu verrivuođaid dahkan sámiid vuostái.
	#$ Ii leat álo leamaš nu álki heivehit dábálaš dáža oahpahusfáttá klássaide main leat sámegielat mánát.
	#% Vearrevuohta maid okkupašuvdnafápmustivrejeaddjin lei čájehan, almmostahtii dážaid hearrán unnitlogu ektui.

#SELECT:SEMr2114 Gen IF (0 SAPMI OR SUOPMA)(1 (@CNP))(2 ("eará") OR ("iežá"))(3 N);
	#$ Ulbmil lea čuovvulit sámilága áigumušaid ja vuoigatvuođaid sámegiela viidásut geavaheamis.
	#$ 
	
# LAHKA


#!! * **SEMXr2115** Selects genitive (modifier) in front of a lahka-noun *spábbačiekčanlága vuoigatvuohta*
SELECT:SEMXr2115 Gen IF (0 LAHKA)(*1 LAHKA-GEN-N OR LAHKA-N OR ("áigumuš") BARRIER NOT-Attr LINK 0 Acc);	
	#$ Dálá Ruoŧabeali boazodoalloláhka 1971 rájes, čuovvu ovddit lága hámi dáfus.
	
#!! * **SEMXr2116** Selects genitive (modifier) if you are LAHKA OR ORGANIZATION followed by mannu, day and numerals..
SELECT:SEMXr2116 Gen IF (0 LAHKA OR ORGANIZATION)(1 MANNU)(2 Ord OR CARDINALS)(3 ("beaivi") OR ("b"))(4 Num)(*5 N BARRIER NOT-NPMOD LINK NEGATE *1 FMAINV OR Inf BARRIER S-BOUNDARY);	
	#$ Dás čujuha lávdegoddi muđui Sámedikki miessemánu 30. beaivvi 2000 doalloplána čuoggái 13.1.
	

#!! * **SEMvaldi** Selects removes NomAg váldi, till we find examples of actual use of it 
REMOVE:LexSel ("váldi" NomAg) ; #(-1 ("politihkalaš")) ;
	#$ Sámediggedieđáhus sámegiela birra čilge ja ságaškuššá makkár rolla ja politihkalaš váldi Sámedikkis galgá leat giellagažaldagain.
	
# KLASS


#!! * **SEMtext** (modifier) selects genitive (modifier) if you are a TEXT in front of KLASS *doalloplána čuoggái*
SELECT:SEMtext Gen IF (0 TEXT OR ABSTR-TEXT LINK 0 Acc)(1 KLASS);
	#$ Dás čujuha lávdegoddi muđui Sámedikki miessemánu 30. beaivvi 2000 doalloplána čuoggái 13.1.
	
	
# GIELLA 


#!! *  **SEMgiella1** (modifier) selects Gen if you are a LANGUAGE in front of LESSON or SATNI *sámegiela oahpahusa*
SELECT:SEMgiella1 Gen IF (0 LANGUAGE LINK 0 Acc)(*1 LESSON OR SATNI OR ("dilli") OR ("prošeakta") OR ("geavaheapmi") OR ("doaibmaplána") OR ("ovdáneapmi") OR ("sajádat") OR ("korrektur-#prográmma") OR ("dutkan#dilli") OR ("vuohki") BARRIER NOT-NPMOD LINK NOT 0 Nom OR Acc LINK NOT 1 ("ektui") OR ("olis") OR ("dihte"))(NEGATE *-1 ("oahppat") BARRIER SV-BOUNDARY) ;
	#$ Muhtun mánát geat eai lean hárjánan sámegiela hállat, čehppo dađistaga sámegiela geavaheamis.
	#$ Mus ii leat dát áviisačálus, muhto mun ádden du vástádusas ahte muhtun lea čállán sámegiela oahpahusa birra Kárášjoga nuoraidskuvllas.
	#$ Ulbmil lea čuovvulit sámilága áigumušaid ja vuoigatvuođaid sámegiela viidásut geavaheamis.

#SELECT:r2119 Gen IF (*-1 TRANS-V BARRIER SV-BOUNDARY)(0 LANGUAGE LINK 0 Acc)(*1 LESSON OR ("dilli") OR ("prošeakta") OR ("geavaheapmi") OR ("doaibmaplána") OR ("ovdáneapmi") OR ("sajádat") OR ("korrektur-#prográmma") BARRIER NOT-NPMOD); 
	#$

#!! * **SEMsamegiella** selects Gen for LANGUAGE if *1 is LESSON 
SELECT:SEMsamegiella Gen IF (0 LANGUAGE + Gen)(*1 LESSON OR ("analysa") BARRIER NOT-Attr)(NEGATE *1 LESSON LINK 1 ("ja"))(NEGATE *-1 ("oahppat") BARRIER SV-BOUNDARY);
	#$ Gávpogis lei sámegiela oahpahus dušše ovtta skuvllas.
	#% Ráđđehus deattuha sámegiela ja sámi kulturáddejumi.

#SELECT:SEMr2120 Gen IF (0 LANGUAGE LINK 0 Acc)(1 SATNI);
	#$

#SELECT:SEMr2121 Gen IF (0 LANGUAGE LINK 0 Acc)(1 ("nubbi") OR ("vuosttaš") OR Ord OR ("a") OR ("b") OR ("c"))(2 ("giella") LINK NOT 0 Ess);
	#$

#!! * **SEMlang** removes Gen if LANGUAGE is to the right, but not if you are ACTOR-ROLE and so on *oahpponeavvuid sámegillii* 
REMOVE:SEMlang Gen IF (0 N LINK NOT 0 ACTOR-ROLE OR ABSTR-PLACE OR TIME OR TEXT OR ABSTR-TEXT OR SETTLEMENT-PLACE OR EMOTION OR ACTIVITY OR Sem/Lang)(1 LANGUAGE  -  ("suopman"))(NEGATE -1 Ger); 	
	#$ Son lea maid leamaš mielde ráhkadeame oahpponeavvuid sámegillii ja lea jorgalan oahppogirjjiid sámegillii.
	#% Mun ohppen sin áddestalladettiin máttanorgga suopmana hállat.

#!! * **SEMlang2** Gen if you are LANGUAGE with 1 N: You are only a modifier in a sentence with a TV-verb, if there is an Acc or Com between you, or if the Obj is topicalized *ráhkadii sámegiela Áppesa*
SELECT:SEMlang2 Gen IF (0 LANGUAGE + Gen)(*1 N BARRIER NOT-A OR ("vissis"))(NEGATE *1 TIME-N BARRIER NOT-Attr)(NEGATE *-1 TRANS-V CBARRIER Acc OR Com LINK NEGATE *-1 Acc BARRIER S-BOUNDARY)(NEGATE 1 COMMA LINK 1 Acc)(NEGATE -1 STRICT-TRANS-V LINK NOT 2 Acc)(NEGATE 0 Acc LINK 1 Ess) ;
	#$ Báhpaeamit, Margrethe Wiig, gii lei gasku giellamoivvi, ráhkadii iežas sárgumiiguin sámegiela Áppesa, mii lei stuorra ávkin sámegiela oahpahusas.
	#$ Doaibmaplána lea bidjan buori vuođu ja leamaš mielde rievdadeame miellaguottuid sámegiela geavaheami ja oainnusin dahkama ektui.
	#% Moai hálidetne oahppat sámegiela universitehtas.
	#% Sosiolingvistihkas leat guokte vuogi oaidnit gielaid servodagas.
	#% Dát joavku galggašii beassat geavahit iežas giela dearvvašvuođabargiiguin.
	#Comment1: This rule was made so that SEMsapmiModifier would not be destoryed. It suffered from adding LANGUAGE to it (because then we needed the NEGATE).
	#Comment2: You are only a modifier in a sentence with a TV-verb, if there is an Acc or Com between you, or if the Obj is topicalized (as in: don't negate if you can't find an Acc between you but instead there is an Acc to the left of the TV-verb).


#!! * **SEMgiella2** Gen if you are Pron followed by giella *iežas giella*
SELECT:SEMgiella2 Gen IF (0 Refl OR Pers)(1 ("giella"));
	#$ Dát joavku galggašii beassat geavahit iežas giela dearvvašvuođabargiiguin. 
	#$ Sii hállet iežaset giela.      


#!! * **vdicNom** Selects Nom
REMOVE:vdicNom Acc IF  (*-1 <vdic> BARRIER Nom)(0 Nom) ;
	#$ Sii leat áŋgiruššan fágalaš sisdoaluiguin, dadjá Sámi Allaskuvlla rektor Gunvor Guttorm.




# SÁMEDIGGI / STÁHTA


#REMOVE:SEMr2124 Gen IF (0 (N Acc) LINK NOT 0 ("Norga") OR NATION-NOT-NORGA OR HUMAN)(1 ("Sámediggi") OR ("stáhta"));
	#$ Departemeanttat buktet jahkásaš mearkkašumiideaset Sámedikki doaimmaid hárrái.

SELECT:SEMrolla Gen IF (0 ("Sámediggi"))(1 ("rolla"));
	#$

#SELECT:SEMr2127 Gen IF (0 ("stáhta") LINK 0 Acc)(1 ("sápmi" Gen) OR ("Sápmi" Gen))(2 ("joatkkaskuvla"));	
	#$

SELECT:SEMstahta Gen IF (-1 FYLKA LINK 0 Gen)(0 ("stáhta") LINK 0 Acc)(1 ("oahpahuskantuvra"));	
	#$

SELECT:SEMr2130 Gen IF (0 ("Sámediggi" Acc))(*1 SAMEDIGGI-GEN OR TEXT OR ABSTR-TEXT BARRIER NOT-NPMOD LINK NOT 0 Ess);
	#$ Sámediggi lea geigen jahkedieđáhusas departementii Sámedikki doaimmas 2005.

#!! * **SEMstahta1** Gen if 0 stáhta 1 org etc.
SELECT:SEMstahta1 Gen IF (0 ("stáhta") OR ORGANIZATION LINK 0 Acc)(*1 STAHTA-GEN OR ORGANIZATION OR TEXT OR ABSTR-TEXT OR INSTITUTION OR RIEKTEDILLI BARRIER NOT-NPMOD OR COMMA OR CC OR (":") OR Dem LINK NOT 0 Ess OR ("Norga"))(NEGATE *-1 STRICT-TRANS-V BARRIER SV-BOUNDARY OR Acc) ; 
	# TEXT and ABSTR-TEXT are perhaps too big.
	#$ Heiveheami ovddidit ovttas Sámedikkiin ja Davvi-Norgga spesiálapedagogalaš birrasiiguin, stáhta spesiálapedagogalaš doarjjavuogádagain ja Læringssenteret:iin.
	#% Deanu vuoigatvuođaid oamasteaddjit stevdnejit Suoma stáhta diggái.

	      
# FYLKA

#!! * **SEMfylka1** Gen if you are FYLKA followed by fylka *Romssa fylkkasuohkan*
SELECT:SEMfylka1 Gen IF (0 FYLKA + Acc)(1 ("fylka") OR ("fylkkagielda") OR ("fylkkasuohkan") OR ("ámta"));
	#$ Romssa fylkkasuohkan lea dahkan Ráissá joatkkaskuvlla sáme- ja suomagieloahpahusa ovdavázzinskuvlan.
	#Comment: Isn't this actually taken care of by GenGYLKA3?

#!! * **SEMfylka2** Gen if you are FYLKA, then "ja" to the right followed by FYLKA *Finnmárkku ja Romssa fylkkagielddaide*
SELECT:SEMfylka2 Gen IF (0 FYLKA + Acc)(1 CC)(2 FYLKA + Gen);
	#$ Giellanjuolggadusat leat váikkuhan vaikkoba Finnmárkku ja Romssa fylkkagielddaide nu, ahte leat váldigoahtán ovdan dákkár áššiid.
   
#!! * **SEMfylka3** Gen if FYLKA and some place or org to the right *Finnmárkku ássiide*
SELECT:SEMfylka3 Gen IF (0 FYLKA + Acc)(*1 GEOGRAPHICAL-PLACE OR GEN-POLITICAL-PLACE OR HUMAN OR DILLI OR INDUSTRY OR RIEKTEDILLI OR ORGANIZATION BARRIER NOT-NPMODADVII LINK NOT 0 ("stuorradiggi")); # NPNHAI
	#$ Prinsihppa ahte láhka ii lihkahala sajáiduvvan vuoigatvuođaid gusto maiddái dážaide, kvenaide ja eará Finnmárkku ássiide.
	#$ Álbmotrievttálaš ja historjjálaš ođđa riektegáldoosiid ferte árvvoštallat stáhta dálá geavada ja riikka lágaid ektui, erenoamážit Finnmárkku eatnamiid vuoigatvuođaid deasttas.


	# OTHER 
	
#!! ### Other genitive rules

#!! * **topGEN** Selects Gen if sentence intitial. To the right a Prf Prc that modifies nominative *Stáhta nammadan láhtu*
SELECT:topGEN Gen IF (-1 BOS)((0 Gen LINK 1 (@>N) + PrfPrc LINK 1 Nom) OR (0 Gen LINK 1 Gen LINK 1 (@>N) + PrfPrc LINK 1 Nom))(NEGATE 0 ABBR) ; 
	#$ Stáhta nammadan láhttu ja sadjásašlahttu galgaba deavdit gáibádusaid, maid Lahtut ja sadjásašlahtut nammaduvvojit gitta njealji jagi rádjai háválassii.
	#$ Skuvllaviesu báhcán fiellut ja ávdnasat dolvouvvojedje Goalsevuohpái ja adnoje doppe skuvlii.
	#$ Plána celkojuvvon mihttomearri, juobe vuosttaš veršuvnnas, lei bajásgeassit ođđa sámebuolvvaid.
	#$ Su iežas ráhkadan govus 1 čájeha jurddadási
	#% Ollesdálveskuvlla ožžon easka go jagi maŋŋeil mannen Kárášjohkii vázzit Sámi álbmotallaskuvlla.

#!! * **NomQst** Selects Nom in a Qst-sentence. To the left is Nom and leat with a Qst-particle *Leat go álbmotmeahcit veahkaváldi*
SELECT:NomQst Nom ((*-1 Nom BARRIER NOT-NPMODADV LINK *-1 (Qst "leat") + FMAINV BARRIER NOT-NPMODADV LINK -1 BOS) OR (*-1 Nom BARRIER NOT-NPMODADV LINK *-1 Qst BARRIER NOT-NPMODADV LINK -1 ("leat") + FMAINV LINK -1 BOS))(NEGATE *1 Nom BARRIER S-BOUNDARY) ; 
	#$ Leat go álbmotmeahcit veahkaváldi báikkálaš olbmuid vuostá?

#!! ***Genlassin** Selects Gen if first one to the right is lassin *bargostipeanddaid lassin*
SELECT:Genlassin Gen (1 ("lassi" Ess)) ;
	#$ Bargostipeanddaid lassin leat juolludan vel mátke-, lohkan- ja ávnnasstipeanddaid, sadjásaš stipeandda ja álggahanstipeanddaid oktiibuot 580 000 ruvnnu ovddas.

#!! ***lassinIll** Selects Ill if first one to the left is lassin *lassin Sarai*
SELECT:lassinIll Ill (-1 ("lassi" Ess) LINK NOT -1 Gen OR Ill) ;
	#$ Lassin Sarai ožžot golmma jagi bargostipeandda girječálli Sara Margrethe Oskal, govvadáiddár Matti Aikio, hábmejeaddji Anne Berit Anti ja girječálli Sigbjørn Skåden.
		
	
#!! ***GenAhkásaš** Selects Gen 
SELECT:GenAhkásaš Gen (0 Pron LINK 1 ("ahkásaš")) ;
	#$ Son vuohttá ahte Sámediggi ii nagot geasuhit su ahkásaččaid.
		
		
#!! ### Gen and preposition/postposition



#!! * **GenAPP** Selects genitive when a preposition to the left, or when a postposition to the right *rastá riikarájiid*
SELECT:GenAPP Gen IF ((*-1C Pr BARRIER NOT-NPMOD) OR (*1C Po BARRIER WORD))(NEGATE 0 NUM + (Sg Acc) LINK -1 Pr LINK 2 Gen)(NOT 0 Ess) ;
	#$ Beana goarjádii beavddi vuollái.
	#$ Seammás sáhttet riikat nannet luohttámuša ođđa konvenšuvdnii ja nu hukset buori ovttasbarggu rastá riikarájiid.
	#$ Gánda: "Birra buot oppa máilmmi."
	#% Vuosttasnaššuvnnaid čoagganeapmi ovddasta badjel 300 indiánaservodaga miehtá Kanada ja gáibida dál iešstivrejumi Davvi-Amerihká buot eamiálbmogiidda.


#!! * **NomIfPo** removes Nom if sentence initial, because it modifies Gen
REMOVE:NomIfPo Nom IF (-1 BOS)(1 Gen LINK 1 Po)(3 COPULAS + FMAINV LINK NEGATE *1 A BARRIER NOT-ADV-PCLE);
	#$ Pippi viesu bálddas lei nubbi gárdin ja nubbi viessu.
	#% Olmmái háma duohken hehtte suoládeami. 

	
#!! * **GenPoCoordPunct** Selects genitive for coordinated postpositions: with PUNKT to the left 
SELECT:GenPoCoordPunct Gen IF (1 PUNCT-RIGHT LINK *1 Gen BARRIER NOT-NPMOD LINK *1C Po OR ("gaskasaš") BARRIER WORD) ;
	#$
	
#!! * **GenPoCoord** Selects genitive for coordinated postpositions *ráŋggáštusa ja buhtadusa hárrái*
SELECT:GenPoCoord Gen IF ((1 JA/DAHJE OR ("dahjege") LINK *1C Gen BARRIER NOT-NPMOD LINK *1C Po OR ("gaskasaš") BARRIER NP-BOUNDARY OR Nom) 
OR (1 JA/DAHJE OR ("dahjege") LINK 1 Gen LINK *1C Gen BARRIER NOT-NPMOD LINK *1C Po BARRIER NP-BOUNDARY OR Nom)
)
(NEGATE -1 Gen LINK 2 JA/DAHJE LINK 1 Gen LINK 1 Acc)(NEGATE -1 FMAINV + STRICT-TRANS-V)
(NEGATE 0 Cmp/SplitR) ; 
	#$ Ovdamearkan sáhttá namuhit ahte dábálaš mearrádusat ráŋggáštusa ja buhtadusaid hárrái bohtet prinsihpalaččat go dahkkojuvvojit dagut mat eai leat gullevaččat boazodollui.
	#$ Dárkkisteami maŋŋil gárte dan guovtti riikka boazodoallolágat measta áibbas ovttaláganat viidodaga ja sisdoalu dáfus.
	#$ Guhkes ja losses vuojeheame maŋŋá sii deive Davvinástti dahjege Almmi Návlli lusa.
	#% Lávdegoddi lea maiddái atnán áiggi ságastallat davviruoŧa čearuid ja Romssa boazoorohagaid gaskaneas váttisvuođaid birra.
	#% Olmmoš galgá čuovvut iežas guovllu gáktemálle, muhto sáhttá válljet liinni ja ivnni miela mielde.
	#% Mun attán heaggan, áhkkán ja ruhtan sávzzaid ovddas.


SELECT:GenPoCoord Gen IF ((1 COMMA LINK 1 Gen LINK 1 JA/DAHJE OR ("dahjege") LINK *1C Gen BARRIER NOT-NPMOD LINK *1C Po OR ("gaskasaš") BARRIER NP-BOUNDARY OR Nom) 
OR (1 COMMA LINK 1 Gen LINK 1 JA/DAHJE OR ("dahjege") LINK 1 Gen LINK *1C Gen BARRIER NOT-NPMOD LINK *1C Po BARRIER NP-BOUNDARY OR Nom)
)
(NEGATE -1 Gen LINK 2 JA/DAHJE LINK 1 Gen LINK 1 Acc)(NEGATE -1 FMAINV + STRICT-TRANS-V)
(NEGATE 0 Cmp/SplitR) ; 
	#$ Dáruiduhttináiggis Anders Larsen geahččalii kultuvrra, politihka ja dáidaga vehkiin sámiálbmoga árvvu loktet.	

#SELECT:GenPoCoord2 Gen 
	#%Vuosttaš láđđasis njealját lađđasii gustojit seamma láhkai áššiide meahcceeatnamiid dahje vuoigatvuođaid meahcceeatnamiin luobaheami ja láigoheami birra.
	#$ Goalmmát oasis lea bargosajiid ja bargoeallima eavttuid birra, njealját oasis lea fidnooahpuid, giehtaduoji ja boaittobeali báikkiid ealáhusaid birra.

#!! * **GenGenPo** (modifies pp-phrase) selects Gen in front of postposition-phrase *álgojagiid soađi maŋŋá*
SELECT:GenGenPo Gen IF (1C Gen - Pron)(2C Po)(NEGATE -1 (Pron Refl))(NEGATE 0 Acc LINK *-1 TRANS-V BARRIER Acc OR SV-BOUNDARY)(NEGATE 0 Acc LINK *-1 (@CNP) BARRIER NOT-NPMOD LINK -1 Acc)(NEGATE 0 Adv OR PrfPrc OR VFIN OR Nom)(NEGATE 2 Px)(NEGATE 0 Acc LINK 1 (Pron Rel))(NEGATE 0 (Pron Dem) OR (A Ord) OR TIME-N)(NEGATE 0 BODY + Acc LINK *-1 Prop + Gen OR Pron BARRIER NOT-Attr LINK *-1 TRANS-V + FMAINV BARRIER SV-BOUNDARY); 
	#$ Internáhtta ii sáhte leat ruovttu sajis, muhto álgojagiid soađi maŋŋá lei olbmuin gárži ruovttuin ja ovttahat biebmu.
	#$ Dat ii leat hábmejuvvon sámi boazodoalu beroštumiid mielde.
	#$ Muhto leimmet gal bivnnuhat gili nieiddaid gaskkas.
	#% Sii galget hábmet iežaset oaiviliid historjjá birra.
	#% Ledjen optimista ja dollen logaldallamiid ášši birra.

#!! * **GenORG** (modifies Loc) selects Gen if you are MAIN-ORGANIZATION and to your right is Loc *dearvvašvuođafitnodagaid jahkedieđáhusain*
SELECT:GenORG Gen IF (0 MAIN-ORGANIZATION + Gen)(1 SAMEDIGGI-GEN + Loc) ;
	#$ Sámediggi sáhttá maid buktit cealkámušaid guovlluid dearvvašvuođafitnodagaid jahkedieđáhusain ja plánaárvalusain.
	#$ Evttohus bođii suohkana politihkkariin.
	#$ Ledje stuora hástalusat oahpahusráđi barggus ja lei somá bargagoahtit Edela buori bargovuođu ala.

#!! * **GenPropSem/Semcon** 
SELECT:GenPropvuogádat Gen IF (0 Prop + Gen)(1 Sem/Semcon LINK 1 Po) ;
		
#!! * **SEMnom** (modifies Nom) removes Acc if sentence boundary or adv to the left. To the right is Nom followed by a transitive verb and Acc *stálu beana njoallu háviid*
REMOVE:SEMnom Acc IF (-1 Adv OR BOC OR S-BOUNDARY)(1C (N Nom))(2 TRANS-V + FMAINV)(NEGATE 0 (Pron Dem))(*3 Acc BARRIER COMMA OR S-BOUNDARY); 
	#$ Jus stálu beana njoallu stálu háviid, de hávit savvojit.
	#$ Mánáidgirjjiin muitaluvvo dávjá govt olbmuid mánát deivet nuppi máilmmi ássiid.
	#$ Boazodoallu gal lea dorvvastan suddjemiidda go dat oaidná movt eatnamat ja meahcci gáržžiduvvo go ruđa fápmu vuoitá.


#!! * **SEMDomain** 
SELECT:SEMDomain Gen IF (-1 NATION)(0 Sem/Domain LINK 1 Sem/Plc - Prop OR Sem/Group) ;
	#$ Buresboahtin Giellateknoi – sámi giellateknologiija guovddážii. 

#!! * **deaivatGenlusa** selects genitive when used like deaivat Gen lusa/lahkosii even if the verb deaivat belongs to the strict TV set.
SELECT:deaivatGenlusa (N Gen)(-1 ("deaivat") + VFIN)(*1 ("lusa" Po) OR ("lahkosii" Po) BARRIER REAL-V); 
	#$ Guhkes ja losses vuojeheame maŋŋá sii deive Davvinástti dahjege Almmi Návlli lusa.

	
#!! ### Genitive in place adverbials ROUTE

#!! * **GenPlc** Selects genitive if you are ROUTE, and there is a MOVEMENT-V to your left or right *boahtiba dán geainnu*
SELECT:GenRoute Gen IF (*0 MOVEMENT-V BARRIER SV-BOUNDARY OR COMMA OR NP-HEAD-ACC - (Pron Dem))(0 ROUTE)(NOT 0 ("mátki"))(NEGATE -1 OKTA)(NEGATE *-1 JOHTIT + IV BARRIER SV-BOUNDARY LINK *1 ROUTE BARRIER NOT-NPMOD) ;
	#$ Soai boahtiba dán geainnu.
	#$ Fertiimet vázzit bálgáid geasset ja Deanu mielde dálvet.
	#$ Mii vujiimet duon luotta. 
# the NOT "mátki" is there since the mátki form was Err/Orth Gen/Acc, and gave Gen to sentences like "Mátki lei buorre"
	 
#!! * Selects accusative if you are ROUTE, and the verb čuovvut to the left. 
SELECT:AccRoute ROUTE + Acc IF (*-1 ("čuovvut") BARRIER SV-BOUNDARY OR (N Acc) LINK 0 FMAINV OR Inf) ;
	#$ Son rávve olbmuid čuovvut skohtermáđii dárkilit go sáhttá leat suddi goappat beale luotta.

#!! * **ruovttoluottaAdv**
SELECT:ruovttoluottaAdv ("ruovttoluotta" Adv) ; #((0 Acc LINK -1 TRANS-V LINK 2 Acc) OR (NEGATE *0 TRANS-V)) ;
	#$ Eiseválddit gesse ruovttoluotta dán gáibádusa.

#!! ### Adjectives take object
SELECT:oazzulat (N Acc) (-1 ("oažžul")) ;
	#$ Mánát leat oažžulat guliid.

#!! ### Temporal adverbials: Choosing accusative or genitive TIME

#!! * **GenMannuOrdRight** selects Gen if you are mannu and to your right is A Ord *miessemánu 10.*
SELECT:GenMannuOrdRight Gen IF (0 MANNU LINK 1 Ord)(NEGATE 0 (Sg Gen) LINK -1 Num) ;  
	#$ Mu riegádanbeaivi lea čakčamánu 3. b.
	#$ Juo cuoŋománu 10. beaivvi ija vuostá mátkkoštii Ruvdnaprinseassa Märtha badjel ráji Ruŧŧii oktan Ruvdnaprinsabára golmmain mánáin.

#!! * **GenMannuOrdLeft** selects Gen if you are mannu, to your left is Ord and to your right is a numeral
SELECT:GenMannuOrdLeft Gen IF (-1 Ord)((0 MANNU LINK 1 Num) OR (0 ("beaivi") OR ("vahkku"))) ;
	#$ 23. cuoŋománu 1728 dovddus geaskkus lea ee. celkon ahte mearraguollebivdu lea luđolaš earret
	#$ Evaluerenraporta addojuvvui GOD:ii geassemánu 20. beaivvi 2000.
	#$ Geassemánu 8. beaivvi 2001 láhkaásahus nr 676 oktagaslaš plánaid birra dearvvašvuođalágaid vuođul lea addojuvvon láhkamearrádusaid láhkavuođuin.


SELECT:Du3 Nom IF (0 Sg LINK *-1 CRD BARRIER NOT-NPMOD LINK -1 Nom + Sg LINK *-1 VFIN + Du3 BARRIER NOT-NPMOD)(NEGATE 0 VFIN)(NEGATE 1 Nom); 
SELECT:Du3 Nom IF (0 Sg LINK 1 CRD LINK *1 Nom + Sg BARRIER NOT-NPMOD)( *-1 VFIN + Du3  BARRIER NOT-NPMOD)(NEGATE 0 VFIN); 
	#$ Norggas fuolaheigga Thomas von Westen ja Knud Leem 1700-jagiin ahte muhtun sámit ožžo oahpaheaddjeoahpa.

#!! * **JahkeNumNom** selects Nom if you are Num, to your left is beaivi, then ord/Num and then mannu *borgemánu 1. b. 1891*
SELECT:NumNomMannu Nom IF (-3 MANNU)(-2 Ord OR Num)(-1 BEAIVI)(0 Num);
	#$ Sámedikki bargiidčoahkkimis Guovdageainnus juovlamánu 7. beaivvi 2001 mearridii...
	#$ Maiddái borgemánu 1. b. 1891 čuovvu njuolggadusain.
	#$ Juovlamánu 1. beaivi 1972 lei buorre beaivi.
	#$ Carsten Smith guđii Sámi vuoigatvuođalávdegotti ovdaolbmo virggi skábmamánu 1. beaivvi 1985.

#!! * **GenBoahtte** selects Gen if you are time, to your left is boahtte, boahtit, čuovvovaš or ovddit
SELECT:GenBoahtte Gen IF (-1 ("boahtte") OR ("boahtit" PrsPrc) OR ("čuovvovaš" Attr) OR ("ovddit" Attr))(0 TIME)(NEGATE 0 Px) ;
	#$ Boađe boahtte vahku.
	#Comment: Assuming that the time-expression following those words will be in an idiomatic construction that implies a point of time rather than a period


#!! * **TIMEobs** selects Gen if you are time, and to your right is an intransitive real-verb. No adverbials allowed to the right *vuolggán bearjadaga*
SELECT:TIMEobs TIME + Gen IF (*-1 REAL-V BARRIER NOT-ADV-PCLE LINK NOT 0 TRANS-V OR ("orrut"))(NOT 1 ADVL)(NEGATE 0 VAHKKU-DUR + Acc)(NEGATE 0 Nom LINK *-1 COPULAS BARRIER NOT-ADV-PCLE)(NEGATE 0 Num) ;
	#$ Mun vuolggán bearjadaga ja boađán duorastaga.
	#$ Mánát leat olgun čierastallame juovlabasiid, ja sáhttet čierastit njuolgga stálu sehkkii.
	#$ Gursa bistá miessemánu beallemuddui.
	#$ Mun boađán bearjadaga.
	#% Mii leat sullii diimmu fávllis ovdalgo suhkat gáddái.
	#% Arkiivaávdnasat galget sáddejuvvot go leat sullii 25-30 jagi boarrásat.

#!! * **GenGuhte** selects Gen if you are vahkku with guhte to your left *guđe beaivvi*
SELECT:GenGuhte Gen IF (-1 ("guhte" Gen))(0 VAHKKU LINK 0 Acc);	
	#$ Dieđátgo guđe beaivvi mis lea čoahkkin?

#!! * **GenMan** selects Gen : man adj
SELECT:GenMen Gen IF (0 ("mii") LINK 1 (A Attr));	
	#$ Mii háliidit maiddái diehtit man guhkes áiggi don geavahat jorgalit dan guokte teavstta.


LIST AVZZUHIT = "dáhttut" "ávžžuhit" "veahkehit" ;

REMOVE:avzzuhit Acc IF (-1 AVZZUHIT)(0 (N Gen)) ((*1 HAB-ACTOR BARRIER NOT-NPMOD LINK 1 Inf) OR (*1 ("giellabiras") BARRIER NOT-NPMOD LINK 1 Acc LINK 1 Inf)) ; 
	#$ Sáhttá maiddá veahkehit guovllu eará boazoeaiggádiid buoridit dietnasa.

REMOVE:avzzuhit Gen IF (-1 AVZZUHIT)(0 (N Gen) + HAB-ACTOR) ((1 Inf) OR (1 Acc LINK 1 Inf)) ; 
	#$ Sámediggi ávžžuhii giellabirrasiid ohcamiid sáddet Sámediggái.


REMOVE:guovllu Acc IF (-1 TRANS-V)(0 (N Gen)) (*1 (N Acc) BARRIER NOT-NPMOD LINK 1 Inf) ; 
	#$ Sáhttá maiddá veahkehit guovllu eará boazoeaiggádiid buoridit dietnasa.

#!! * __Nom_b_1__ selects Nom if you are b/beaivi with a numeral/Ord to your left and a mannu to the left of that. To your right a finite verb čuovvut
SELECT:Nom_b_1 Nom IF (-2 Gen LINK 0 MANNU)(-1 Num OR Ord)(0 ("b"))(*1 VFIN + Sg3 BARRIER REAL-WORD-NOT-ABBR LINK 0 ("čuovvut"));
	## Maiddái borgemánu 1. b. 1891 čuovvu njuolggadusain.
	#Comment: Not in use?  b. gets now only Adv analysis

#!! * __Nom_b_2__ selects Nom if your are b with a numeral/Ord to your left and a mannu to the left of that. To your right copulas followd by beaivi in nom-case *juovlamánu 1. b. 1972 lei buorre beaivi*
SELECT:Nom_b_2 Nom IF (-2 Gen LINK 0 MANNU)(-1 Num OR Ord)(0 ("b"))(*1 COPULAS + Sg3 BARRIER REAL-WORD-NOT-ABBR LINK *1 BEAIVI-REF + Nom);
	## Juovlamánu 1. b. 1972 lei buorre beaivi.
	#Comment: Not in use? b. gets now only Adv analysis

#!! * __Nom_b_3__ selects Nom if you are b/beaivi with Num/Ord to your left, with mannu to the left of that, with copulas even futer to the left and beaivi to the left of copulas
SELECT:Nom_b_3 Nom IF (-4 BEAIVI-REF + Nom)(-3 COPULAS + Sg3)(-2 Gen LINK 0 MANNU)(-1 Num OR Ord)(0 ("b"));
	## Mu riegádanbeaivi lea čakčamánu 3. b.
	#Comment: Not in use? b. gets now only Adv analysis

#!! * **aigiAcc** Gen if 0 TIME 1 áigi
SELECT:aigiAcc TIME + Acc IF (NEGATE -1 Num)(1 ("áigi" Adv)) ;
	#$ Son bođii jagi áigi.
SELECT:aigiAdv Adv IF (0 ("áigi" Adv) LINK -1 TIME) ;

#Time-accusative:
#!! * **accgenbeaivi** ávvudit riegádanbeaivvi
SELECT:accgenbeaivi Acc (0 GEN-BEAIVI LINK *-1 ("ávvudit") BARRIER SV-BOUNDARY) ;
	#$ Mii ávvudit Sámi allaskuvlla vuosttaš professora ja Sámedikki vuosttaš presideantta Ole Henrik Magga 70. riegádanbeaivvi seminárain.


#!! * **GenBeaivi2** selects Gen if you are beaivi with the end of the sentence or comma to your right. Restrictions to the left *riegádanbeaivvi,*
SELECT:GenBeaivi2 Gen IF (NEGATE *-1 STRICT-TRANS-V OR DURATION-V BARRIER SV-BOUNDARY OR DURATION-A LINK NOT 0 ("leat"))(0 BEAIVI-REF)(1 EOS OR COMMA LINK NEGATE *1 REAL-V);	
	#$ Mii leimmet doppe mu riegádanbeaivvi, čakčamánu 3. b.
	#$ Mun vuolggán ihttin, bearjadaga.
	#$ Mun láven basadanlanja bassat juohke bearjadaga.
	#% Festiválain ja ávvuseminárain ávvudit guovddáža ávvubeaivvi.

#!! * **GenBeaivi3** selects Gen if you are beaivi with the beginning of the sentence to your right *Bearjadaga mii vuolgit*
SELECT:GenBeaivi3 Gen IF (*-1 BOC OR NP-BOUNDARY BARRIER NOT-ADV-PCLE)(0 BEAIVI-REF LINK NOT 0 ("beaivi") LINK NOT 1 COPULAS + FMAINV) ;
	#$ Bearjadaga mii vuolgit oaggut.
	#$ Mánnodagaid gehčče vel Sámi mánáid-TV ovttas mánáiguin.
	#$ Giellatekno álggahuvvui 1.8.2001 prošeaktan ráhkadit sámegiela grammatihkalaš analysa.

#!! * **GenBeaivi4** selects Gen if you are beaivi with a NP-boundary to your right
SELECT:GenBeaivi4 Gen IF (-1 NP-BOUNDARY)(0 BEAIVI-REF - ("beaivi") LINK NOT 1 COPULAS + FMAINV); #Not in use?

SELECT:GenTimeAfterBeaivi Gen IF (0 Sem/Time LINK -1 BEAIVI-REF + Gen) ;
	#$ Kárášjoga gielda dieđihii lávvardaga gaskabeaivvi ahte leat ožžon vástádusa iskosis.


#!! * **GenDate** selects Gen if you are Sem/Date
SELECT:GenDate Gen IF (0 Sem/Date LINK NOT 1 COPULAS + FMAINV); 


#!! * **GenJuohke** selects Gen if juohke or seamma to the left *juohke dálvvi*
SELECT:GenJuohke Gen IF (-1 ("juohke") OR ("seammá"))(0 TIME); 
	#$ Mun ledjen doppe juohke dálvvi.
	#$ Son lei doppe seamma dálvvi go don.
	#$ Juohke giđa sii lávejedje bassat dáhki ja seinniid.

#!! * **GenJahkiNum** selects Gen if you are jahki num with a numeral to your right *Skuvlajagi 1998-99*
SELECT:GenJahkiNum Gen IF (0 JAHKI-NUM)(1 Num); 
	#$ Skuvlajagi 1998-99 rivde doarjjajuohkima eavttut. 
	#$ Soaŋu-girji, mas leat noveallat, bođii jagi 1971.
	#$ Son vuittii Davviriikkaid Ráđi girjjálašvuođabálkkašumi jagi 1991 dán girjji ovddas. 

#!! * **AigiModifier** (modifier) selects Gen if aigi to the right *konferánssa áiggi*
SELECT:AigiModifier Gen IF (NEGATE 0 REAL-V)(1 ("áigi"))(NEGATE 1 (Sg Loc) LINK 1 ("áigi" Sg Ill));
	#$ Čoahkkimis lea lávdegoddi árvalan konfereanssa áiggi, báikki ja fáttáid.
	#$ Soađi áigge duiskalaččat bolde eanaš viesuid Finnmárkkus ja Davvi-Romssas.
	#$ Organisašuvnna bláđđi man gohčodit Fokusin muitala ahte gonagas Olav su áiggistis lei bivdán beassat leat servodaga suodjaleaddji.
	#$ Son riegádii gonagasa Herodesa áiggi.
	#$ Dán áiggi sii johtet biillaiguin, jos headjavissui lea guhkit mátki.

#!! * **GenHávvi** selects Gen for hávvi if Acc somewhere to the right 
SELECT:GenHávvi Gen IF (0 ("hávvi"))(*1C Acc BARRIER S-BOUNDARY);
	#$ Váilevaš vejolašvuohta doaimmahit kultuvrra ávnnaslaš beliid sáhttá muhtun háviid mearkkašit álbmotriekterihkkuma.


#!! * **GenHávvi2** selects Gen for hávvi if a transitive verb cannot be found somewhere in the sentence
SELECT:GenHávvi2 Gen IF (0 ("hávvi"))(NEGATE *0 TRANS-V BARRIER SV-BOUNDARY);
	#$ Fágalaš sudjadeapmi daid háviid go doavttir lea dahkan biehttaleami.
	#$ Eará háviid ii leat hoahpu journálačállimiin.

#!! * **GenGeardi** selects Gen if the beginning of the sentence to the left *Eará háviid*
SELECT:GenGeardi Gen IF (*-1 BOC BARRIER NOT-NPMODADV)(0 ("áigi") OR ("bodda") OR ("geardi") OR ("hávvi"));
	#$ Eará gerddiid ii dáhpáhuvvan nu go álggus leimme jurddašan.
	#$ Ovddeš áiggiid čoakkalmasviesut ledje nuoraid áidna deaivvadansadji.

#!! * **GenRbeaivi** (modifier) selects Gen if riegádanbeaivi to your right 
SELECT:GenRbeaivi HAB-ACTOR + Gen IF (1 GEN-BEAIVI);
	#$ Golbma čáppa, doaimmalaš nieidda livžo áhčiset riegádanbeaivve.
	#$ Mii leimmet doppe bearjadaga, mu riegádanbeaivvi.

#!! * **GenGeardi2** selects Gen for geardi if Num Gen or Ord to the left 
SELECT:GenGeardi2 Gen IF (-1 (Num Gen) OR Ord)(0 ("geardi"));
	## Galggat golmma geardde beaivvis borrat ovtta tableahta.

#!! * **GenTimePl** selects Gen for TIME-N + Pl if an attribute to the left *lagamus beivviid*
SELECT:GenTimePl TIME + (Pl Gen) IF (-1 Attr);
	#$ NVE dieđiha maid, ahte dáid lagamus beivviid eat dárbbaš Finnmárkkus ballat dulvvis.
	#$ Duodji lei deaŧalaš sivva manne olbmot Gáivuonas birgejedje nu bures váttes jagiid vuosttaš ja nuppi máilbmesoađi gaskkas.

#Time-accusative:
#!! * **GenDURadj1** selects Gen if a duration adverbial to the left 
SELECT:GenDURadj1 Gen IF (NEGATE -1 DURATION-A)(0 ("čakčageassi") OR ("giđđadálvi") OR ("čakčadálvi") OR ("giđđageassi") LINK 0 Acc);
	#$ Unjárgga guvlui, muitaluvvo ahte olbmot lávejedje čakčageasi, go lájuiguin gerge, mannat Oardajávrái ja Oardavuonjoga bajágeahčái Báhcavuona suohkanii bivdit dálvuoli.
	#Comment: Duomma says that those compounds typically are in Genitive and not accusative case if they are a time adverbial.

#!! * **GenDURadj2** removes Gen for TIME-N, if duration adjective to the left *olles dálvvi*
REMOVE:GenDURadj2 TIME + Gen IF (*-1 DURATION-A BARRIER NOT-NPMOD OR Num)(NEGATE 1 N + Nom) ;
	#$ Jus oahppi dahje ohppiidjoavku jávká lobihemiid osiid diimmus dahje olles diimmu.
	#$ Bohccot šaddet gárddis jorrat guhkes beaivvi 20-30 buolašgrádas.
	#$ Doai leahppi áŋgirit lohkan sámegiela olles dálvvi.


#!! * **GenDURNumPl** duháhiid jagiid
REMOVE:GenDURNumPl Gen IF (*0 PLACE-V OR DURATION-V BARRIER SV-BOUNDARY)(0 (Num Pl) OR ("moanat" Pl) LINK *1 VAHKKU-DUR + (Pl Gen) BARRIER NOT-A LINK NOT 1 Ill OR TIME OR Po OR PREGEN LINK NEGATE 0 Nom LINK 1 EOS)(NEGATE *-1 Dem OR TIME-A OR Ord OR Indef OR PREGEN OR Pr BARRIER NOT-NPMOD)(NOT 1 ("biras" Pl Loc));
	#$ Sápmelaččaid historjá álbmogin lea duháhiid jagiid boaris, muhto dálá ássanguovlluin sápmelaččat eai leat ássan nu guhká.
	#$ Son lei moanaid jagiid lullisámi skuvllajođiheaddjin.

#!! * **GenDUR1** removes Gen for VAHKKU-DUR if duration verb or place verb somewhere in the sentence. Restrictions. *ádjánii beaivvi*
REMOVE:GenDUR1 Gen IF (*0 PLACE-V OR DURATION-V BARRIER SV-BOUNDARY)(0 VAHKKU-DUR + Acc - Pl LINK NOT 1 Ill OR TIME OR Po OR PREGEN LINK NEGATE 0 Nom LINK 1 EOS)((NEGATE *-1 Dem OR TIME-A OR Ord OR Indef OR PREGEN OR Pr OR Num BARRIER NOT-NPMOD) OR (-1 ("moanat")))(NOT 1 ("biras" Pl Loc) OR STRUCTURE) ;
	#$ Mii leat sullii diimmu fávllis ovdalgo suhkat gáddái.
	#$ Skiippaguras Kárášjohkii ádjánii bures beaivvi, muhto vuloseano ii ádjánan go beaivvi go dábálaš olu čáhci lei (Fixdal 1938: 64).
	#$ Mii oruimet doppe jagi.
	#$ Beaivvi mun čohkkájin doppe.
	#% Sápmelaččat ledje orron dáin guovlluin 1600-logu birrasiin.
	#% 1700-logu álggus leai Suoma ja Ruošša gaskasaš soahti giksin.

#!! * **GenDURNum** vázzen guokte maŋimuš jagi doppe
REMOVE:NomDURNum Nom IF (*0 PLACE-V OR DURATION-V BARRIER SV-BOUNDARY)(0 (Sg Num) LINK *1 VAHKKU-DUR + (Sg Gen) BARRIER NOT-A LINK NOT 1 Ill OR TIME OR Po OR PREGEN LINK NEGATE 0 Nom LINK 1 EOS)(NEGATE *-1 Dem OR TIME-A OR Ord OR Indef OR PREGEN OR Pr BARRIER NOT-NPMOD)(NOT 1 ("biras" Pl Loc));
	#$ Go jagi maŋŋil álggahuvvui Kárášjoga gymnása, de fárrejin ruoktot ja vázzen guokte maŋimus jagi dáppe. 
	#$ Niillas lea máŋga jagi bargan sámi áššiiguin, ja Álttá-Guovdageainnu riiddus son lei okta nealgudeddjiin Stuoradikki olggobealde. 
	#$ 1982:s son roasmmohuvai bávkilahttima oktavuođas Álttádulvadeami vuostá, váldojuvvui gitta, muhto báhtarii Kanadai, ja orui doppe álgoálbmogiid luhtte moadde jagi. 

#!! * **GenDUR2** removes Gen for VAHKKU-DUR if the duration verb or place verb to the left is perfectum participle or infinitive with an auxiliary to the left 
REMOVE:GenDUR2 Gen IF (*-1 PLACE-V OR DURATION-V BARRIER SV-BOUNDARY LINK 0 PrfPrc OR Inf LINK *-1 AUX OR Neg BARRIER SV-BOUNDARY)(0 VAHKKU-DUR + Acc LINK NEGATE *-1 Dem OR TIME-A OR Ord OR Num BARRIER NOT-NPMOD LINK NEGATE *1 VAHKKU-DUR + Acc LINK 1 Po)(NOT 1 ("biras" Pl Loc));

#!! * **NoTimeAcc** removes Acc for time if POINT-IN-TIME-SPEC or Ord to the left *vuosttas beaivvi*
REMOVE:NoTimeAcc TIME + Acc IF (-1 POINT-IN-TIME-SPEC OR Ord)(NEGATE -1 Ord LINK *-1 STRICT-TRANS-V);
	#$ Eará beaivvi soaitá čuožžut ahte galgat láibut bihpporgáhkuid jna.
	#$ Vuosttas čoahkkinbeaivvi deattuhuvvojedje muhtun ráji dat čuolmmat ja hástalusat Skandinavias leat ovddabealde boranávddiid hálddašeami oktavuođas.
	#$ Čalmmehis stálu maid stohke dološ áiggi.
	#$ Vuos galgá liinna seaktit, de suohpput ja nuppi iđida fas geassit dan.
	#% Ovddemus galge nannet dárogieloahpahusa juogidettiin vuosttaš ja nuppi jagi goabbat sadjái.

#!! * **NoTimeAccII** removes Acc for time if POINT-IN-TIME verb to the left 
REMOVE:NoTimeAccII TIME + Acc IF (*-1 POINT-IN-TIME-V BARRIER SV-BOUNDARY);
	#$ Bivdu, eandalii beatnagiin bivdin, sáhttá hehttet boazobarggu čohkken- ja johtináiggi.

#!! * **NoTimeAccIII** removes Acc for time if POINT-IN-TIME verb to the left is infinitive or perfectum participle with an auxilliary or negation to the left 
REMOVE:NoTimeAccIII TIME + Acc IF (*-1 POINT-IN-TIME-V  BARRIER SV-BOUNDARY LINK 0 PrfPrc OR Inf LINK *-1 AUX OR Neg BARRIER SV-BOUNDARY);
	#$
    # Comment: Can still be Acc @ADVL if standing after a DURATION-A
    # Comment: That is why these rules have to stand after the DURATION-A rule

#!! * **AccBeaivi** removes Acc for relative pronouns if followed by general beaivi *guđe beaivvi*
REMOVE:AccBeaivi Acc IF (0 (Pron Rel) LINK 1 GEN-BEAIVI) ;
	#$ Dieđátgo guđe beaivvi mis lea čoahkkin?

#!! * **timeADVL** selects Gen for time: when perfectum participle or infinitive to the left are time adverbial verbs or not time object verbs, to the left of this there shall be an auxiliary *lean čoavdán cealkagiid maŋimuš áiggi*
SELECT:timeADVL1 TIME + Gen IF (*-1 TIME-ADVL-V OR N-TIMEOBJECT-V BARRIER SV-BOUNDARY LINK 0 (PrfPrc) OR (Inf) LINK *-1 AUX BARRIER SV-BOUNDARY)(NEGATE -1 Attr LINK -1 Num + Acc)(NEGATE -1 Num + Acc)(NOT 0 ("sesoŋŋa")) ;
	#$ Skuvllas mun lean čoavdán ollu cealkagiid maŋimuš áiggi.
	#$ Sápmelaččat ledje orron dáin guovlluin 1600-logu birrasiin.


SELECT:timePP Gen IF (0 TIME OR ("lohku") LINK 1 ("loahppa") OR ("álgu") LINK 0 Loc OR Ill) ;


SELECT:timeADVL3 TIME + Gen IF (*-1 TIME-ADVL-V OR N-TIMEOBJECT-V BARRIER SV-BOUNDARY)(NEGATE 0 Acc LINK *-1 CC BARRIER NOT-NPMOD LINK -1 Acc OR Attr LINK *-1 STRICT-TRANS-V)(NEGATE -1 Attr LINK -1 Num + Acc)(NEGATE -1 Num + Acc)(NOT 0 ("sesoŋŋa"))  ;
	#$ Jugaheadnu sutnje viinni maiddái dán ija, ja mana don gis ja veallá suinna.
	#% Girjjis leat boares govat ja ođđa lyrihkka mat čatnet oktii doloža ja dálá áiggi.
	#% Ovddemus galge nannet dárogieloahpahusa juogidettiin vuosttaš ja nuppi jagi goabbat sadjái.
	#% De fárrejin ruoktot ja vázzen guokte maŋimus jagi dáppe.

SELECT:timeADVL4 TIME + Gen IF (**-1 Acc BARRIER SV-BOUNDARY LINK -1 TRANS-V + FMAINV)(NEGATE 0 Acc LINK *-1 CC BARRIER NOT-NPMOD LINK -1 Acc OR Attr LINK *-1 STRICT-TRANS-V)(NEGATE 0 Pl LINK -1 Num + Pl)(NEGATE -1 Attr LINK -1 Num + Acc)(NEGATE -1 Num + Acc OR ("buorre") + Acc) ;
	#$ Ja don ii dahkan maidege dan jagi, muhto bođii fas nuppi jagi, ja de ledje sus guoimmit mielde.
	#% Sivdnidit biebmobázahusaid, ja sávvat buori boahtteáiggi.

SELECT:timeADVL2 TIME-N + Gen IF (*1 FMAINV - ("golahit") BARRIER SV-BOUNDARY)(NEGATE 0 Nom LINK *1 FMAINV + COPULAS OR FMAINV + DURATION-V BARRIER SV-BOUNDARY)(NEGATE 1 MANNEL)(NEGATE 0 Prop)(NEGATE -1 Attr LINK -1 Num + Acc)(NEGATE -1 Num + Acc)(NOT 0 ("sesoŋŋa"))  ;	
	#$ Dán geasi áiggun bargat.
	#% Eahket gollá jođánit.
	#% In háliit in minuhta ge duššái golahit.

SELECT:timeADVLacc TIME + Acc IF (NEGATE *-1 Num BARRIER NOT-A)(1 MANNEL)  ;	
	#$ Go jagi maŋŋil álggahuvvui Kárášjoga gymnása, de fárrejin ruoktot ja vázzen guokte maŋimus jagi dáppe.
	
SELECT:GenIndef Indef + Gen IF (1C TIME + Gen) ;	

SELECT:GenMuitalit Gen (0 (Pers Acc) OR (Refl Acc) OR Prop + Acc LINK -1 ("muitalit") LINK 2 Loc) ;
SELECT:GenMuitalit Gen (0 (Pers Acc) OR (Refl Acc) OR Prop + Acc LINK -1 ("muitalit") LINK *2 ("birra") BARRIER SV-BOUNDARY) ;

REMOVE:AccINF-V Acc (0 Gen LINK -1 INF-V)(1 N + ADVLCASE LINK 1 (IV Inf))(NEGATE 0 Pers + DU-NR LINK 1 Com) ;


	# Accusative sentence-finally
	# ---------------------------

	# Removed this one because it made trouble. The example function although. 
	#REMOVE Gen IF (NEGATE *-1 Pr OR Num OR CC BARRIER NPNH)(0 N OR Pron)(NEGATE 0 TIME)
 #	(*1 EOC2 BARRIER NOT-ADV-PCLE) ;
	## Ferte čájehit várrugasvuođa go lea sáhka sihkkumis dakkár dieđuid.

#SELECT:TESTr2021 Acc IF (*-1 NP-HEAD-ACC BARRIER VFIN LINK *-1 TRANS-V BARRIER NOT-NPMOD OR @CVP)(*-1 NP-HEAD-NOM BARRIER NOT-ADV-PCLE)(0 Gen LINK NOT 0 Loc)(1 EOS);	
	## Unna mánážat dulbmet eatni askki, stuora mánát fas váimmu.


 # CC in the preceding rule because of coordination after Pr. The following rule compensates for it.

#!! * **theAccusative**_ selects Acc if you are a N or Pron with CC to your right, followed by Acc and a CLB or VFIN *gápmagiid ja vuoddagiid, sii geavahedje*
SELECT:theAccusative Acc IF (NEGATE *-1 Pr OR Num BARRIER NOT-NPMOD)(0 NOTIME OR Pron)(NEGATE 0 Nom OR Loc OR Adv OR NOT-REAL-V OR Prop + Sem/Plc) (*1 JA/DAHJE BARRIER NOT-ADV - ABBR LINK *1 Acc BARRIER NOT-NPMOD LINK *1 CLB OR VFIN OR Attr - ("gaskaneas") OR @CVP BARRIER NOT-ADV-PCLE LINK NEGATE *1 Gen BARRIER NOT-NPMOD)
(NEGATE -1 COMMA LINK -1 Gen)(NEGATE -1 Gen LINK -1 COMMA LINK -1 Gen)(*0 TRANS-V);
	#$ Muhtun sámi biktasiid, nugo gápmagiid ja vuoddagiid, sii geavahedje guhkit, gitta 1960-logu rádjai.
	#$ Ii oktage goit hálit billistit iežas birgejumi vuođu, sihke eatnamiid ja elliid. 
	#$ Gielddaid, fuođđo- ja guolástaneiseválddiid jna. ja guoskevaš geavaheddjiid servviid galgá gullat ovddalgihtii.
	#% Suoma Riikkabeaivvi eanetlohku dohkkehii Norgga ja Suoma soahpamuša.
	#% Dáruiduhttin lea Norgga historjá, ii ge dušše sápmelačča dahje kveana.

	
#SELECT:nugo_Acc Acc IF (0 Gen - Num LINK *-1 ("nugo") BARRIER NOT-A-ADV OR CRD LINK *-1 Acc BARRIER SV-BOUNDARY OR Nom OR N - ADVLCASE LINK NEGATE *-1 VERBAL-ACTIVITY)(NEGATE 1 N OR VFIN) ;
	#$ Sámit soitet erenoamážit amašit norpmaid nugo guhkes váccahagaid ja oktasašvuođa váiluma. 


SELECT:Colon_Acc Acc IF (0 Gen - Num LINK *-1 (":") BARRIER NOT-A-ADV OR CRD LINK *-1 Acc BARRIER SV-BOUNDARY OR Nom OR N - ADVLCASE LINK *-1 TRANS-V BARRIER SV-BOUNDARY LINK NOT 0 VERBAL-ACTIVITY)(NEGATE 1 N OR VFIN) ;
	#$ Sii bukte skeaŋkkaid mánnái: golliid, suovvasiid ja myrra.
	#% 2: Duommá ja Máret leaba gárdimis.
    
#SELECT:TESTr2024 Acc IF (NEGATE *-1 Pr OR Num BARRIER NP-BOUNDARY)(0 NOTIME OR Pron)(NEGATE 0 Nom OR Loc OR Adv OR NOT-REAL-V OR Prop + Plc)(1 CC LINK 1 Gen LINK 1 Acc LINK *1 CLB OR VFIN BARRIER NOT-ADV-PCLE LINK NEGATE *1 Gen BARRIER NOT-NPMOD)(NEGATE -1 COMMA LINK -1 Gen)(NEGATE -1 Gen LINK -1 COMMA LINK -1 Gen);      
 
#SELECT Acc IF (NEGATE *-1 Pr OR Num BARRIER NOT-NPMOD)(0 NOTIME OR Pron)(NEGATE 0 Nom OR Loc OR Adv OR NOT-REAL-V)(*1C CLB OR VFIN BARRIER NOT-ADV-PCLE LINK NEGATE *1 Gen BARRIER NOT-NPMOD)(NEGATE 2 ("ii") LINK 1 Gen LINK 1 N);
	## Jus divššohas roasmmohuvvá dahje oažžu duođalaš váttisvuođaid,...
	## Liggen dihtii viesuideaset, ferte ollusat muoraid boaldit.

#!! * **NotGenitive** selects Acc if you are a N or Pron with punctuation marks to your right, followed by a noun-phrase boundary
REMOVE:NotGenitive Gen IF (NEGATE *-1 Pr OR Num BARRIER NOT-NPMOD)(0 NOTIME OR Pron)(*1 PUNCT BARRIER NOT-ADV-PCLE LINK 1 NP-BOUNDARY) ;
	#$ Jos sámegiela universitehta eksámen (vuođđofága dahje gaskafága) ii adnojuvvo bálkábajidanfágan, de mii evttohat ahet sáthta juolluda stipeanddaid oahpaheddjiide geat váldet dán eksámena.
	#$ Huitfeldt - fidnii munnje olles govvaalbuma maid su eadni lei govvidan Finnmárkku-mátkkis iežas vánhemiiguin 1901:s.

REMOVE:TESTr2026 Gen - Dem IF (*-2 TRANS-V BARRIER S-BOUNDARY OR Pr OR Acc)(NEGATE *-1 Pr OR CC OR (Num Nom) BARRIER NOT-NPMOD)(-1 Gen OR Attr)(0 NOTIME OR Pron)(NEGATE 0 ALU)((*1 CLB OR LEFT BARRIER NOT-ADV-PCLE) OR (*1 TIME BARRIER NOT-NPMOD OR Num OR CRD) OR (1 Num LINK 1 TIME) OR (1 Adv LINK 1 COMMA OR EOS)) (NEGATE 1 COMMA LINK 1 Gen LINK *1 Po OR (N Ill) OR (N Loc) BARRIER V)(NEGATE -1 Gen LINK -1 CC LINK -1 Gen);
	#$ ...ahte vuhtiiváldit divššohasa dárbbuid, beroštumiid ja riektesihkarvuođa dearvvašvuođabálvalusa ektui. 
	#$ Sámi servodat dárbbaša dárbbašlaš veahki hukset min iežamet oahpahusvuogádaga guhkit áiggi boahtteáiggis. 
	#$ Dievasčoahkkin galgá dán háve meannudit ášši mii šaddá láidestit min doaimmaid máŋga jagi ovddasguvlui.
	#% Danne lea ráđđehussii deaŧalaš sápmelaččaid oassálastin dálkkádatáššiid bargui ja lea maid deaŧalaš ahte sámit ásahusaideaset bokte sáhttet leat mielde ásaheamen máhttovuođu luonddu, birrasa ja dálkkádatrievdamiid oktavuođas. # oktavuođas is N Loc
	#% Juos mearriduvvo meahcceeatnamiid nuppástuvvan geavaheapmi golmma ja dušše golmma stivrralahtu doarjagiin ja olles uhcitlohku vuođđudallá oainnus sámi kultuvrra, boazodoalu, meahcásteami, ealáhusdoaimmaheami ja servodateallima destii mii lea árvvoštallojuvvon Sámedikki válddahusaid vuođul, de sáhttá uhcitlohku ovtta oli stivrračoahkkima áigge gáibidit ášši Sámedikki gieđahallamii. # destii is N Ill
	#% Mun lean lohkan ollu girjjiid dán dálvvi.


REMOVE:TESTr2027 Gen IF (*-1 TRANS-V)(0 ("dat" Acc))(1 CC OR CS);		
	## Šloahtta dieđiha gait suohkaniidda gos kruvdnoprinsabárra manná guossis dan ahte sii geat sihtet govvet mannet dohko gos preassagovvejeaddjit leat. 
 	# Difficult to make a border... 
 	
#!! ###  Reflexive pronouns: acc or gen

REMOVE:genFinally Gen (0 Acc LINK NOT 0 Nom LINK NEGATE *-1 Pr OR Num BARRIER NOT-A)(NEGATE *-1 REAL-V)(NEGATE -1 Nom)(*1 EOC OR CS BARRIER WORD) ;
	#$ Naba daid stuora dorskiid, gosa daid bidjet?
	#$ Muhtun sámi biktasiid, nugo gápmagiid ja vuoddagiid, sii geavahedje guhkit, gitta 1960-logu rádjai.
	#% Boazodoallu čuohtejagi perspektiivvas.
	#% Badjel 800 ođđa njoammuma.

# Genitive between "nugo" and N
	
#!! * **NUGOr2159** selects Gen between nugo and N *nugo suorri dulkaoahpu*
SELECT:NUGOr2159 Gen IF (-1 ("nugo"))(1 N LINK NOT 0 PLACE);
	#$ Ráđđehus áigu ruhtadit dehálaš oktasašdoaimmaid, nugo suorggi dulkaoahpu ja diehtojuohkinbálvalusa.	
	#$ Muhto sierraortnegiid, nugo lasi klássaid juogadandiimmuid, eai ožžon jos skuvllat eai atnán iežaset giellaseahkalasskuvlan ja čáhkadan sámi kulturoahpu. 
	
# Genitive in comparison
	
SELECT:COMPAr2169 Gen IF (NEGATE *-1 TRANS-V BARRIER NOT-ADV-PCLE)(NEGATE 0 V)(1 (A Der/Comp Ess))(NEGATE 1 V OR Adv)(NEGATE 2 go);
	#$ Mun jáhkán du iežan falibun.

	
#!! * **AccIEScoord** selects (Pron Refl Acc) Acc in front of "ja" to the left. To the right Loc or Ill *elliideaset ja iežaset ealáhussii*
#SELECT:AccIEScoord (Pron Refl Acc)(-1 ("ja") LINK -1 Acc)(1 (N Loc) OR Ill)(NEGATE -1 ("ja") LINK -1 Acc LINK -1 Gen) ;
	#$ Sii ožžot geavahit eatnama ja gáttiid ja elliideaset ja iežaset ealáhussii.
	#Comment: Is this perhaps too narrow? Might wanna make one for objects in general. Seems like a good idea! Anyhow, for now: this rule only works when Acc already has been chosen (remove C to see this).

#!! * **GenIES** (modifier) selects (Pron Refl Gen) if NON-FAMILY OR ("bellodat") OR SAMEDIGGI-GEN to the right *iežaset mánáide*
SELECT:GenIES (Pron Refl Gen) IF (*1 NON-FAMILY OR ("bellodat") OR SAMEDIGGI-GEN BARRIER NOT-Attr LINK NOT 0 Ess)(NEGATE *-1 OPRED-V BARRIER NOT-ADV-PCLE) ;
	#$ Riektavuođa dihtii berre namahuvvot ahte cealkámušas maiddái addui vejolašvuohta ásahit sierra linnjáid váhnemiidda geat dáhttot iežaset mánáide eanet sámegiela oahpahusa geavaheami.
	#$ Dan mun muitalan dušše dutnje, iežan buoremus olmmái.
	#% John Issát oaivvilda ahte sámegiella lea issoras dehálaš oassi sámis, ja dan seamma lea ahte olmmoš dovdá iežas sápmelažžan, álohii, ii ge dušše dalle go álcces lea ávkin. 


#!! * **AccIES** SELECTS accusative object (Pron Refl Acc)  
SELECT:iesAcc (Pron Refl Acc)(*1 Inf BARRIER WORD - Ess) ;
	#$ Ráhkkanit galggat iežat vuolgit, mun doalvvun du siidii.

#!! * **AccIES** (modifier) removes accusative object (Pron Refl Acc) if Ill or Loc to the right, but not if a transitive verb is found to the left 
REMOVE:AccIES (Pron Refl Acc) IF (NEGATE *-1 TRANS-V BARRIER Acc OR S-BOUNDARY)(NOT -1 CC)(1 Ill OR (N Loc) OR Inf);
	#$ Sáhttágo Finnmárkkuopmodat bidjat dárkilet eavttuid bivdui, guolásteapmái ja lubmemii iežas eatnamiin?
	#$ Sii ožžot geavahit eatnama ja gáttiid ja elliideaset ja iežaset ealáhussii.
	

#!! * **GenIESinf** removes (Pron Refl Gen) if a transitive verb to the left and an Inf to the right 
REMOVE:GenIESinf (Pron Refl Gen) IF ((*-1 MUITALIT - ("juoigat") BARRIER Acc OR S-BOUNDARY) OR (-1 Adv LINK *-1 MUITALIT BARRIER Acc OR S-BOUNDARY) OR (-1 Nom LINK *-1 MUITALIT BARRIER Acc OR S-BOUNDARY))((*1 Inf OR Ess OR PrfPrc OR Nom BARRIER NOT-NPMOD) OR (1 Adv OR A LINK 1 Inf OR Ess OR PrfPrc)) ;
	#$ Sii lohket iežaset leat oalle duhtavaččat dán jagáš básárdoaluin.
	#$ IL Nordlysa beaivválaš jođiheaddji, Nils Peder Eriksen, lohká iežaset leat oalle duhtavaččat dán jagáš básárdoaluin.
	#$ Máze hárjeheaddji, Aslak Anders Tornensis, logai iežas duhtavažžan go juohkit šadde čuoggáid Hámmárfeastta 2:in.
	#$ Olli lohká diehttelasaid iežas duhtavažžan go olles bellodat lea su duogábealde, nu ahte son dovdá stuorra doarjaga čavget sámediggeválgii.
	#$ Vaikko leage šaddan addit gieđa ja čalmmi sámi vuoigatvuođaid doarrumis, de liikká ii loga Somby iežas olu vaháguvvan.
	#% Marit Kristine juoigá iežas ipmiba Nils Ante Triumf. 
	
LIST SOME = "Facebook" "Instagram" "TikTok" ;

REMOVE:muitalitTxt Acc IF (*-1 MUITALIT BARRIER SV-BOUNDARY)(0 Refl + Gen OR Pers + Gen)(1 Sem/Txt OR SOME LINK 0 Loc OR Ill) ;
	#$ Nu čállá Hætta Isaksen iežas Instagramii.


REMOVE:GenIESEss Refl + Gen IF (*-1 OPRED-V OR MUITALIT BARRIER REAL-V OR Acc)(*1 Ess OR Ill OR (A Nom) OR ("illu" Loc) BARRIER NOT-ADV) ;

REMOVE:GenIESEss Refl + Gen IF (1 ("jamás")) ;
	
REMOVE:AccIESPo (Pron Refl Acc) IF (NEGATE *-1 TRANS-V + FMAINV BARRIER Acc OR S-BOUNDARY)(*1 (N Gen) BARRIER NOT-Attr LINK 1 Po)(NEGATE 0 TIME-N) ;
	#$ Dieđálin eanet Oslo go iežan gili birra.
	#$ Mii leat fitnan su luhtte ja bivdán su muitalit iežas skuvlavázzima birra.

#!! * **NomIfProp** Removes Acc and Gen when you Prop because you are Nom. To the left is a sg3-verb. Should not hit Prop that are Sem/Plc.
REMOVE:NomIfProp Acc OR Gen IF ((*-1 SG3-V BARRIER SV-BOUNDARY OR COMMA) OR (*-1 FMAINV - ConNeg BARRIER WORD - Attr LINK -1 SG3-V))
(NEGATE *-1 Acc OR (A Attr) BARRIER SG3-V LINK *-1 Nom BARRIER SG3-V LINK *-1 SG3-V)
(0 (N Nom) + Prop OR (N Acc) + Prop)(NEGATE 0 Prop + Sem/Plc)(NEGATE -1 Acc LINK *1 Ill BARRIER NOT-Attr)(NEGATE 0 Gen LINK 1 N - Prop - ABBR LINK *-2 SG3-V LINK -1 Nom)(NEGATE -1 ACC-INF-V LINK 2 Inf) ;
	#$ Logi minuvtta maŋŋel nagodii Sørøy Glimt čiekči Stig Martinsen dulbet bohtosa ovttain moalain.
	#$ de liikká ii loga Somby iežas olu vaháguvvan.
	#% Issát ásai Beer Lahai Roi birrasiin. 
	#% Doppe Abram čuorvvui Hearrá nama. 
	#% Suovai Liná vuoiŋŋastit go váibbai.


#!! * **NomIfProp2** Removes Acc and Nom when you are Prop Sem/Plc because you are Gen. To the left is a sg3-verb. To the right is a noun.
REMOVE:NomIfProp2 Nom OR Acc (*-1 SG3-V BARRIER SV-BOUNDARY)(0 Sem/Plc LINK NEGATE 0 Sem/Fem OR Sem/Mal OR Sem/Sur)(0 Nom + Prop + Sem/Plc OR Acc + Prop + Sem/Plc)(*1 N BARRIER NOT-Attr) ;
	#$ Issát ásai Røros birrasiin.
	#$ Sidjiide lei maid liegga Ruija geasuheaddji guovlu.
	#$ Vuosttasnaššuvnnaid čoagganeapmi ovddasta badjel 300 indiánaservodaga miehtá Kanada ja gáibida dál iešstivrejumi Davvi-Amerihká buot eamiálbmogiidda.

#!! * **NomSentFin** Selects Nom if you are Acc or Gen and EOS is to yoru right. Copulas is found to the left 
SELECT:NomSentFin1 Nom (*-1 FMAINV + COPULAS BARRIER SV-BOUNDARY OR Nom)(0 Acc OR Gen LINK 1 EOC)(NEGATE *-1 Num BARRIER NOT-A) ;
SELECT:NomSentFin2 Nom (-1 VFIN + COPULAS)(0 Acc OR Gen LINK 1 FMAINV + COPULAS LINK 1 EOC) ;
	#$ Organisašuvnna bláđđi man gohčodit Fokusin muitala ahte gonagas Olav su áiggistis lei bivdán beassat leat servodaga suodjaleaddji.
	#$ Mun lean geahččalan eiseválddiid ektui dan jagi, muhto lea váttis ášši ja šaddá jođánit vuollánit, dadjá son.
	#$ Alf Edvard muitala ahte koronapandemiija lea dagahan nu ahte festivála, mii dábálaččaid lágiduvvo tealttás, šadda dán jagi olgofestivála.

#!! * __jr_sr__ Selects (ABBR Nom) if you are *jr* or *sr* and first one to your left is (Sem/Sur Nom)
SELECT:jr_sr (ABBR Nom)(0 ("jr") OR ("sr") LINK -1 (Sem/Sur Nom)) ;



#!! ### Accusative object 


#!! * **AccActioEss** Selects accusative: when a Strict transitive verb actio ess to the left, but not if there is an other Acc to the right followed by EOS
SELECT:AccActioEss Acc IF (*-1 STRICT-TRANS-V + (Actio Ess) BARRIER SV-BOUNDARY OR Acc)(NEGATE 1 Acc OR Gen LINK 1 EOS OR Rel)(NEGATE 1 Acc + Der/NomAct OR Acc + Sem/Act)  ;
	#$ Lea maid deaŧalaš ahte sámit ásahusaideaset bokte sáhttet leat mielde ásaheamen máhttovuođu luonddu, birrasa ja dálkkádatrievdamiid oktavuođas.
	#% Sáhttá leat mielde nannemin boazodoalu ceavzima rašes guovllus
	#% Ruoŧa SVT lea dál ovddideamen ođđa sámi ráhkisvuođa tv-ráiddu mas olbmot besset oahpásmuvvat boazovázziiguin.
	#Comment: "Steals" some sentences from AccTV1 and 2, but the alternative is to make those rules even longer than they already are.. 

#!! * **AccEss** removes Acc when you are SAPMI-N-HEAD with an Ess to your right, but not if there is a transitive mainverb to the left *dutkama duogážin*
REMOVE:AccEss Acc IF (0 SAPMI-N-HEAD LINK 1C (N Ess) LINK NEGATE *-1 TRANS-V + FMAINV - ("bargat") - ("ollit") BARRIER SV-BOUNDARY)(NEGATE 0 SAPMI-N-HEAD LINK 1 (N Ess) LINK 1 Loc)(NEGATE 1 (N Ess) LINK 1 FMAINV); 
	#$ Son láve bargat áviissa váldodoaimmaheaddjin.
	#$ 2. máilmmesoađi maŋŋá fuobmágohte dutkit sosiáladarvinisttalaš osiid ja nálalaš mearkkaid mat ledje ovddeš politihka ja dutkama duogážin.
	#% Dán váilevašvuođa vigge buoridit nuvttá sámegielkurssa fállama bokte ja bálkálasáhusaiguin sidjiide geat máhtte geavahit giela veahkkin oahpahusas.

# Main rules are: AccTV1, r2271, AccTV2, AccTV3, topOBJ1, topOBJ2, AccInf

#!! ***topOBJPers** Removes Gen if you are Acc, and to you right is a Pron followed by a transitive verb. You have to be sentence initial
REMOVE:topOBJPers Gen (0 Acc LINK 1 Pron LINK 1 TRANS-V)(-1 BOC) ;
	#$ Gáhkuid mii goikadit geađggi alde.
	
#!! ***AccVAbess** Selects Gen if to the right is abessive 
SELECT:AccVAbess Acc (0 (N Gen) LINK 1 (TV VAbess))(NEGATE -1 Num) ;
	#$ 	

#!! * **topOBJ1** Selects accusative: when a Strict transitive verb to the right (topicalized object) *beaskka geavahedje*
SELECT:topOBJ1 Acc IF 
(*1 STRICT-TRANS-V BARRIER SV-BOUNDARY OR COMMA OR XGO OR Acc LINK NOT 0 FAUXV OR VAbess LINK NEGATE *1 Acc - Rel OR ("ahte") OR Inf OR VAbess OR VFIN - Neg BARRIER S-BOUNDARY OR COMMA)
(NEGATE 0 (Sg Nom) - Px LINK *-1 V + Sg3  BARRIER SV-BOUNDARY LINK NEGATE *-1C Nom BARRIER SV-BOUNDARY)
(NEGATE 0 (Sg Nom) - Px LINK *1 V + Sg3  BARRIER NOT-ADV-PCLE)
(NEGATE 0 (Sg Nom) - Px LINK -1 CC LINK -1 Nom LINK *3 V + Du3  OR V + Pl3  BARRIER NOT-ADV-PCLE)
(NEGATE 0 (Pron Gen) LINK 1 Attr)
(NEGATE -1 NUM)(NEGATE -1 PrfPrc LINK NEGATE *-1 REAL-COPULAS BARRIER SV-BOUNDARY)
(NEGATE 0 PrfPrc OR Adv OR VFIN OR Loc OR NUM LINK NOT 0 Px)
(NEGATE 0 Gen LINK 1 Nom)
(NEGATE 0 Gen LINK 1 (@>N) + PrfPrc LINK 1 Nom)
(NEGATE 0 Nom LINK *-1 @CNP BARRIER WORD - Attr LINK -1 Nom LINK *-1 VFIN + Du3  BARRIER WORD - Attr) ;
	# Comment: topOBJ1 looks only for the strict-TV: it doesn't care where FAUX is, it doesn't need a finite mainverb, and these things seem to be an advantage. TopOBJ2 is actually more strict in that manner.
	#$ Áigubeahtti go iežade dálu vuovdit?
	#$ Beaskka geavahedje gávtti badjel.
	#$ Vuovssá goddá boanda. 
	#$ Son mu nama ipmirdii boastut.
	#$ Dat gal lea čalmmehis ášši dan čuoččuhit.
	#$ Duosttažan go dan dahkat?
	#$ In mun gal mánnásan diekkár oahpaheaddji sávaše.
	#$ Láđđi fertejetne oastit, nu ahte láđđegákti lei divraseabbo.
	#% Áššiin meahcceeatnamiid nuppástuvvan geavaheami birra galget stáhtalaš, fylkkagielddalaš ja gielddalaš eiseváldit  árvvoštallat, mo nuppástupmi váikkuha sámi kultuvrii, sboazodollui, meahccásteapmái, ealáhusdoaimmaheapmái ja servodateallimii.
	#% Bengt jorgalii ja oinnii nieiddaža iežas ovddabealde gean ii dovdan ii gostege.
	#% Dan galgaba Anne ja Elin muitalit Sápmái.
	#% Dan galgaba Anne Henriette Hætta Reinås ja Marita Kristin Eilertsen Tøsse muitalit Sápmái.

#!! * **topOBJ2** Selects Acc when a transitive finite mainverb to the right (less strict) *dan juohkehaš fuobmá*
SELECT:topOBJ2 Acc IF 
(*1 TRANS-V + FMAINV BARRIER SV-BOUNDARY OR COMMA OR @CNP OR Po OR XGO OR Acc OR N LINK NOT 0 FAUXV OR VAbess OR Ger LINK NEGATE *1C Acc OR ("ahte") OR VAbess OR VFIN BARRIER S-BOUNDARY OR COMMA)
(*1 TRANS-V + FMAINV BARRIER SV-BOUNDARY OR COMMA OR @CNP OR Po OR XGO OR Acc LINK NOT 0 FAUXV OR VAbess OR Ger LINK NEGATE *1 (N Acc) BARRIER S-BOUNDARY OR COMMA LINK NOT 0 Nom LINK 1 EOC OR Dem) 
(NEGATE 0 (Sg Nom) LINK NOT 0 Px LINK *-1 V + Sg3  BARRIER SV-BOUNDARY)
(NEGATE -1 PrfPrc LINK NEGATE *-1 REAL-COPULAS BARRIER SV-BOUNDARY)
(NEGATE -1 NUM)
(NEGATE 0 PrfPrc OR Adv OR VFIN OR Loc OR NUM LINK NOT 0 Px)
(NEGATE *-1 (N Acc) OR (Pers Acc) OR (Rel Acc) BARRIER SV-BOUNDARY)
(NEGATE 0 Nom - Px)
(NEGATE 0 Gen LINK 1 (@>N) + PrfPrc LINK 1 Nom)
(NEGATE 0 (Pron Refl) LINK 1 (N Ess))
(NEGATE 0 (Pron Pers) - ("dat" Pron Pers) LINK 1 (N Nom) + FAMILY-ONLY-HUMAN)
(NEGATE *-1 ("leat") + FMAINV BARRIER SV-BOUNDARY OR COMMA)
(NEGATE 0 (Dem Gen) LINK 1C (N Gen))
(NEGATE *1 OPRED-V + FMAINV BARRIER SV-BOUNDARY LINK 1 Ess LINK 1 Inf)(NEGATE -1 go + CS LINK NOT -1 BOS)(NEGATE 1 Nom LINK 1 VFIN LINK 1 Gen LINK 1 Po) ;
#(NEGATE *1 (N Nom) BARRIER NOT-Attr)
	#$ Dan gal juohkehaš fuobmá dakkaviđe.
	#$ Gáhkuid mii goikadit geađggi nalde goardagis.
	#$ Gos daid lehpet goldán? 
	#$ Sarridiid mii gal borašeimmet.
	#$ Daid heasttaid son lohká heivet buoremusat deike galbma guovlluide.
	#$ Nillá-mága gasttihišgođii.
	#% Maiddái su eadnái ja villjii son attii divrras skeaŋkkaid.
	#% Ja álbmotčoahkkimat dollojit gos báikki olbmot besset buktit oaiviliid.
	#% Ehpet dii leat ieža sárdnumin, muhto Áhčádet Vuoigŋa sárdnu din bokte.

SELECT:topOBJ2CC Acc IF 
(*1 (N Ill) BARRIER NOT-NPMOD LINK 1 CC LINK *1 (N Ill) BARRIER NOT-NPMOD LINK 1 TRANS-V + FMAINV LINK NOT 0 FAUXV OR VAbess OR Ger LINK NEGATE *1C Acc OR ("ahte") OR VAbess OR VFIN BARRIER S-BOUNDARY OR COMMA) ;
	#$ Mávssu lahtuide ja sadjásašlahtuide máksá dat orgána, mii lea nammadan guoskevačča.

#SELECT:TEST Nom (0 ("dušši") LINK -1 go LINK -1 Comp) ;

#!! * **topOBJ3** Selects Acc. It is not depending on a transitive verb like topOBJ1 and 2, but selects Acc when Aux to the left, but only if there is no chanse of it beeing a Nom 
SELECT:topOBJ3 Acc IF (*-1 BOC BARRIER NOT-NPMOD)(0 Acc OR Gen LINK NEGATE 0 Nom)(*1 FAUXV BARRIER NOT-ADV-PCLE)(NEGATE 0 TIME-N OR TIME LINK NEGATE -1 ("ollis"))(NEGATE *-1 Num BARRIER NOT-A)(*1 TRANS-V BARRIER SV-BOUNDARY) ;
	#$ Dán filmma leat Kárášjoga nuorat oaidnán, eaige šat vuolgge sirkusii.
	#$ Min ii leat veara dohko vuolgit.
	#$ Mii doarjut Sámelávdegotti evttohusa oainnu ahte sámegiela berre sáhttit válljet fágan dan sadjái go duiskkagiela gymnásaráhkkaneaddji linnjás nuoraidskuvlla ovccát jagis.
	#$ Prošeavtta leaba Beate Mortensen Nesheim ja Svanhild Andersen čállán.
	#$ Dáid ii galgga suovvat bargat joavdelasaid.
	#$ Sajáiduvvan fágadoahpagiid sáhttá dieđusge geavahit. 



SELECT:AccTVspec Acc IF 
(*-1C STRICT-TRANS-V BARRIER NOT-NPMODADV - (N Ess) - (N Loc) - Pcle - (A Ess) LINK NEGATE *-1C Acc BARRIER S-BOUNDARY OR CC)
(0 NP-HEAD-ACC OR Num OR EAMBBO LINK *1 Sem/Time BARRIER WORD - Num)
(NEGATE *-1 NUM BARRIER NOT-Attr LINK NOT 0 NUMUNIT)
(NEGATE 0 (Pron Indef) OR Adv)
(NEGATE 1 (N Nom) OR Ger)
(NEGATE 0 Dem LINK -1 OLLU LINK -1 STRICT-TRANS-V)
(NEGATE 0 Sem/Fem OR Sem/Mal + Nom OR Sem/Sur + Nom LINK *-1 VERBAL-ACTIVITY-V OR DADJAT OR HUMAN-AGENT-V BARRIER SV-BOUNDARY LINK *2 ("ahte") OR EOC)
(NEGATE -1 <vdic>)(NEGATE *-1 ("váldit") BARRIER NOT-NPMOD LINK 1 Gen LINK 1 Ill + BODY) ;
	#$ ja mii galggai earenoamážit bargat guovlluid giellabirrasiid ektui, ja geavahit jagi doaibmabijuid identifiseremii, mat Norlándda Fylkkamánnis leat ožžon doarjaga máŋga jagi.
	
#!! * **AccTV1** Selects accusative: when a Strict transitive verb to the left (barrier exludes everything but: adv, N Ess , N Loc and Pcle). No Acc allowed to the left of the verb. No Acc allowed to the right of you, except pronouns and education (sentenceboundary and N Ess as barriers). Only numunit numerals are allowed to the left. You are not Acc if you are: time, ruote or Pron Indef. Neither if you are Pron Refl with Gen to your right followed by N Ess. Neither if you are Pron Refl with Gen to your right followed by Po. N Nom and Ger not allowed immediatly to your right. You are not Acc if you are a Nom cased Prop and the verb is some kind of verbalactivityverb and ahte or sentenceboundary is to the right. Vdic not allowed immediately to your left. If váldit is the verb, you are likely to be a Gen if Ill-body noun is found to the right. *oste mielkki gávppis*
SELECT:AccTV1 Acc IF 
(*-1C STRICT-TRANS-V BARRIER NOT-NPMODADV - (N Ess) - (N Loc) - Pcle - (A Ess) LINK NEGATE *-1C Acc BARRIER S-BOUNDARY OR CC)
(0 NP-HEAD-ACC OR Num OR EAMBBO LINK NEGATE *1 Acc - Pron BARRIER S-BOUNDARY OR (N Ess) OR ("sihke") LINK NOT 1 Ger)
(NEGATE *-1 NUM BARRIER NOT-Attr LINK NOT 0 NUMUNIT)
(NEGATE 0 (Pron Indef) OR Adv)(NEGATE 0 (Pron Refl) LINK 1 (N Ess))(NEGATE 0 (Pron Refl) LINK 1 Gen LINK 1 Po)
(NEGATE 1 (N Nom) OR Ger)
(NEGATE 0 Dem LINK -1 OLLU LINK -1 STRICT-TRANS-V)
(NEGATE 0 Refl + Gen LINK 1 N)
(NEGATE 0 Sem/Fem OR Sem/Mal + Nom OR Sem/Sur + Nom LINK *-1 VERBAL-ACTIVITY-V OR DADJAT OR HUMAN-AGENT-V BARRIER SV-BOUNDARY LINK *2 ("ahte") OR EOC)
(NEGATE -1 <vdic>)(NEGATE *-1 ("váldit") BARRIER NOT-NPMOD LINK 1 Gen LINK 1 Ill + BODY) ;#(NEGATE 0 Prop + Gen OR Pron + Gen LINK *1 BODY + Acc BARRIER S-BOUNDARY)  
;
	#$ Moai oste mielkki gávppis.
	#$ Šiehtadus sisttisdoallá dárkilis evttohusa movt guohtuneatnamiid sáhttá atnit ávkin goabbat bealde riikaráji.
	#$ Soai oastiba bileahta su filbmii.
	#$ Mii fertiimet doalvut su buohccivissui.
	#$ Sin bargun lei váldit fáŋgan gonagasa.
	#$ Lean mun gal oaidnán dola rievdamin čázi alde Deanus, muhto dat leamaš vissa gobmi.
	#% Dieđálin eanet Oslo go iežan gili birra. 
	#% Raporttas galgá gozihanlávdegoddi čilget gozihandoaimmas, árvvoštallat stivrra jahkemuitalusa ja dilálašvuođaid maid dat siskkilda, ja árvvoštallat mo dát láhka doaibmá ja leago sávahahtti dan nuppástuhttit. #Hits láhka
	#% Dát dieđusge mearkkaša ollu dan olbmui gii lea vuoitán.


SELECT:AccTV Acc IF 
(*-1C STRICT-TRANS-V BARRIER NOT-NPMODADV - (N Ess) - (N Loc) - Pcle - (A Ess) LINK NEGATE *-1C Acc BARRIER S-BOUNDARY OR CC)
(0 NP-HEAD-ACC OR Num OR EAMBBO LINK 1 Gen LINK 1 N + ADVLCASE)
(NEGATE *-1 NUM BARRIER NOT-Attr LINK NOT 0 NUMUNIT)
(NEGATE 0 (Pron Indef) OR Adv)
(NEGATE 0 Dem LINK -1 OLLU LINK -1 STRICT-TRANS-V)
(NEGATE 0 Refl + Gen LINK 1 N)
(NEGATE 0 Sem/Fem OR Sem/Mal + Nom OR Sem/Sur + Nom LINK *-1 VERBAL-ACTIVITY-V OR DADJAT OR HUMAN-AGENT-V BARRIER SV-BOUNDARY LINK *2 ("ahte") OR EOC)
(NEGATE -1 <vdic>)(NEGATE *-1 ("váldit") BARRIER NOT-NPMOD LINK 1 Gen LINK 1 Ill + BODY) ;
	#% Sámenissonat bivdet veahki Norgga Gonagasas.


SELECT:AccAuxStrictTrans Acc IF (0 Gen OR Nom)(1 AUX + PrfPrc LINK 1C STRICT-TRANS-V)(*-1 COPULAS + VFIN BARRIER WORD - A)  ;
	#$ sii leat ollu fleksibilitehta šaddan čájehit


SELECT:AccRelbetween Acc + Interr (*-1 JEARRAT BARRIER NOT-ADV-PCLE LINK -1 Nom LINK NEGATE *-1 VFIN)(*1 TRANS-V BARRIER NOT-ADV-PCLE) ;
	#$ Ja Ipmil diehtá maid Vuoigŋa hálida dasgo Vuoigŋa rohkadallá bassi olbmuid ovddas Ipmila dáhtu mielde.
SELECT:AccTV Acc IF 
(*-1C STRICT-TRANS-V BARRIER NOT-NPMODADV - (N Ess) - (N Loc) - Pcle - (A Ess) LINK NEGATE *-1C Acc BARRIER S-BOUNDARY OR CC)
(1 A + Attr LINK NOT 1 Acc) ;
	#$ Sámediggeráđđi háliida nannet Sámedikki rolla boahttevaš boazodoalu politihkkahábmemis.

SELECT:TopAccCoorTEST Acc IF (0 Gen OR Nom)(1 @CNP LINK 1C Acc)  ;
	#$ - Sámediggi galgá aivve fal giela ja kultuvrra ovddidit.



#!! * **gosnevrriid** selects Acc in the special cases where there is an Acc Pl in the beginning of the question which is not the object of the verb: Gos nevrriid...
SELECT:gosnevrriid Acc (*-1 TRANS-V BARRIER Acc - Dem OR S-BOUNDARY LINK *-1 (Acc Pl) LINK -1 MO)(0 Gen LINK *1 ("?")) ;
	#$ Gos nevrriid Fávdna lei fidnen dan fiinna dávgebissu?


SELECT:AccSTV Acc IF (*-1 STRICT-TRANS-V BARRIER SV-BOUNDARY - (N Ess) - (N Loc) - (Pcle) - (A Ess) LINK NEGATE *-1 Acc BARRIER S-BOUNDARY)(1 Acc LINK 1 (Ger TV)) ;
	#$ Stivrra lahttu, gii eaktodáhtus dahje sahtedohko lea dagahan Finnmárkkuopmodahkii manahusa luohttámušdoaimma čađahettiin, lea geatnegas buhttet manahusa.


#!! * **PronNP** (removes Acc): selects Gen for Pron Pers if Acc or Ill to the right, given that there is a secure object or that no transitive verb is found *bija ruđa mu kontoi*
REMOVE:PronNP Acc IF (0 Pers + Gen)(NEGATE 0* TRANS-V + FMAINV BARRIER Acc OR S-BOUNDARY)(*1 Acc OR Ill BARRIER NOT-Attr LINK NOT 1 Inf) ;
	#$ Oinniide go Mihkkala, munno nuoramus bártni?
	#$ Bija fal ruđa njuolgga mu kontui.
	#$ Min márkanii lea ráhkaduvvon ođđa skuvla.
	#$ Mun oainnán fierpmi, su merkka, ja dieđán ahte Spider-Man lea leamaš das.
	#$ Mun lean náitalan, ja mu eamit bargá girdišiljus.
	#$ Áhkku láve boahtit min siidii čakčat.

#!! * **dahkatGen** selects Gen when dahkat or bargat takes only adverb
SELECT:dahkatGen Gen IF (-1 ("dahkat") OR ("bargat"))(0 Acc + HUMAN OR Pron + Acc)(1 N + Loc LINK 1 Adv) ;
	#$ Mun dahken iežan mielas bures #in apertium smenob we don't go for mielas Po

SELECT:dihteAcc Acc IF (0 Gen LINK 1 Cmp LINK 1 ("dihtti")) ;
SELECT:dihteAcc Acc IF (0 Gen LINK 1 ("dihtti") + Allegro + Gen) ;

#!! * **r2206** selects Gen when a finite verb to the left and Nom or Acc to the right *lohkaba su girjji*
SELECT:r2206 Gen IF (*-1 VFIN BARRIER N OR (Pers Pron) OR CLB)(0 N OR Pron LINK NOT 0 ("buorre") OR VFIN)
(*1 (N Nom) OR (N Acc) BARRIER NOT-NPMOD OR (":") OR PrfPrc OR CC OR PUNCT OR ("ollis") OR Pron LINK NOT 0 Prop OR Loc OR PrfPrc OR TIME)(NEGATE 0 Num OR Dem OR VGen OR PrfPrc OR PrsPrc OR Indef OR Interr OR (Pron Rel) OR Adv OR TIME-N OR Loc OR (Actio Ess) OR Nom)(NEGATE *-1 TRANS-V + FMAINV BARRIER NOT-NPMOD OR @CVP LINK *1 (Pron Refl Acc) BARRIER NOT-NPMOD LINK 1 Ess)
(NEGATE 1 CLB OR Arab)
(NEGATE *1 Po OR V-TRANS BARRIER SV-BOUNDARY)(NEGATE *-1 PrfPrc + FMAINV BARRIER SV-BOUNDARY LINK *1 ANIMATE + Acc BARRIER NOT-NPMOD LINK 1 Gen)(NEGATE *-1 Num + Acc BARRIER NOT-Attr) ;
	#$ Soai lohkaba su girjji eaŋgalasgillii.
	#$ De son ohcá stálu silbbaid.
	#$ Beana lea olbmo buoremus ustit.
	#$ Buvtte deike su pistovlla.
	#$ Dat mielddisbuktá ahte mánát ja ollesolbmot fertejit guođđit mánáidgárddi njeallje seainni ja šilju ja vuolgit mátkái.
	#% Alimusriekti gávnnai dettetge doarjaga Čáhppesvuovdeášši bohtosii riikkaidgaskasaš álgoálbmot- ja unnitlohkorievttis. 
	#% Su dieđuid mielde gehčče árbevirolaš konsearttaid 350 olbmo oktanaga.


SELECT:AccRefl Acc (0 Refl) ((-1 Nom LINK -1 OPRED-V) OR (-1 Nom LINK -1 OPRED-V))(1 Ess OR PrfPrc + Der/Pass) ;
	#$ Nu dovdá Bierna iežas geatnegahtton seailluhit sámi kultuvrra.

#!! * **r2271** Removes genitive when a transitive verb to the left and you (not if you are a pronoun) are followed by Ill/Loc/Com/Adv: *doalvvui stálu meahccái*
REMOVE:r2271 Gen IF (*-1 TRANS-V - ("bargat") BARRIER SV-BOUNDARY OR Acc OR COMMA LINK NEGATE *-1C Acc BARRIER SV-BOUNDARY)(0 (N Acc) LINK NOT 0 (Pron))
(1 Ill OR Loc OR Com OR Adv LINK NEGATE *1 Acc BARRIER TRANS-V)(NEGATE 0 Refl)
(NEGATE -1 @CNP LINK -1 Attr OR Gen)
(NEGATE *-1 Pr OR NUM BARRIER NP-BOUNDARY)
(NEGATE *-1 ("mii") BARRIER Acc OR S-BOUNDARY OR Po)
(NEGATE *-1 CC BARRIER NOT-NP LINK *-1 GASKAL BARRIER S-BOUNDARY)
(NEGATE *-1 HEADOFPARTS LINK 1 Gen LINK 1 Loc)
(NEGATE *2 REAL-V BARRIER S-BOUNDARY);
	#$ De son doalvvui stálu meahccái.
	#$ Biera oahpaha matematihka munnje.
	#$ Duopmár bosádii vuoru johtui. 
	#$ Máŋga gávpeolbmo lonuhedje fáhcaid gálvvuiguin.
	#% Ja dál mun goittotge lean riegádahttán bártni su boaresvuođa beivviin.
	#% Leat maid bargame dálá boazodoallolága ođastemiin.
	#% Sámi mánáidgárdeoahpaheaddjeoahppu ráhkkanahttá bargui otná ja boahtteáiggi mánáidgárdái.

#!! * **AccTV2** Selects accusative: when a transitive verb to the left. No Acc allowed to the left in the sentence (sentenceboundary as a barrier). No Acc allowed to the right (barriers are CC, comma and sentenceboundary). Note that Gen to the right followed by a noun is allowed. You shall not be: route, time, Pron Dem. You are not Acc if you are: Gen-cased Pron or Animate with Ill immediately to your right. No Acc, Com, N Nom or Gerundium allowed immediately to your right. No Gen followed by Po allowed immediately to your right. A SG3-verb is only allowed to your left (barriers excluding everything except NP-heads and adverbs, PrfPrc is also a barrier) if there is a Nom left to the SG3-verb. No vdic allowed immediately to your left. You are not Acc if: you are a Nom-cased Prop, followed by ahte or EOS and the verb found to the left (SV-boundary) is some kind of verbalactivityverb or a humanagentverb.

SELECT:AccTV2 Acc IF (*-1C TRANS-V - Imprt BARRIER NOT-NPMODADV OR @CVP OR (":") LINK NEGATE *-1 Acc - Num BARRIER S-BOUNDARY OR (N Ess) OR NEGFOC)
(0 NP-HEAD-ACC LINK NEGATE *1 Acc BARRIER CC OR COMMA OR SV-BOUNDARY LINK NOT 0 (Pron Gen) LINK *1 N BARRIER NOT-NPMOD OR COMMA)
(0 NP-HEAD-ACC LINK NEGATE *1 N BARRIER NOT-NPMOD LINK 0 Ill OR Loc LINK 1 Acc)
(NEGATE *-1 NUM BARRIER NOT-Attr LINK NOT 0 NUMUNIT)
(NEGATE 0 (Pron Dem) LINK *1 N BARRIER NOT-Attr)
(NEGATE 0 ANIMATE + Gen OR Pron + Sg + Gen LINK 1 Ill)
(NEGATE *1 Acc BARRIER NOT-NPMOD)
(NEGATE 1 Acc OR Com OR (N Nom) OR Ger)
(NEGATE 1 Gen LINK 1 Po)(NEGATE *-1C SG3-V BARRIER NOT-NPMODADV OR PrfPrc LINK NEGATE *-1 (N Nom) OR (Pron Nom) BARRIER S-BOUNDARY)
(NEGATE -1 <vdic>)
(NEGATE -1 COMMA LINK -1C Gen)
(NEGATE 0 Sem/Fem OR Sem/Mal + Nom OR Sem/Sur + Nom OR Sem/Plc + Nom OR Sem/Org + Nom LINK *-1 VERBAL-ACTIVITY-V OR DADJAT OR HUMAN-AGENT-V BARRIER SV-BOUNDARY LINK *2 ("ahte") OR EOC) ; #(NEGATE *-1 ILL-V + FMAINV BARRIER SV-BOUNDARY LINK *1 (N Gen) + SAPMI OR SUOPMA OR ("dáža") OR ("dáčča") LINK *1 Ill BARRIER NOT-A - ("ja") + @CNP) 
;
# OR COMMA
	#$ De son ohcá stálu silbbaid.	
	#$ Sámegillii galbet universitehta lea šaddan realitehta.
	#$ In máhte suomagiela, eaige nieiddat máhte nu bures eŋgelasgiela.
	#$ De son ohcá stálu silbbaid ja politiija golliid. 
	#$ Fidniimet gillái náitalan nissona veahkkin, ja guokte vuosttamuš jagi vuoššaimet liema ja biđđosa.
	#$ Iežas ealu son doalai sierra iige luoitán daid mastat Labana sávzzaiguin ja gáiccaiguin.
	#% Ulbmil biirejoavkkuin lea ráhkkanahttit čikčiid boahtteáigái spábbačiekčanšiljus ja čohkket Finnmárkku biirre buoremus čikčiid.
	#% Dieđusge ledje sámisogalaččat Muosáin, ja velá dat olbmotge gulle dáža, sámi ja kveana seaguhussii.
	#% Mánáidgárddi ulbmilin lea nannet mánáid sámi iešdovddu ovddidettiin sámegiela geavahusa. #Make or fix a SEM-rule for this.
	
#!! * **AccTV3** Selects accusative: when transitive verb to the left, if it doesn't find a barrier: comma, Num, real-v, Ess, s-boundary. Acc not allowed to the left of the verb. Not Acc if animate or Gen in front of Ill. Numerals the only Acc allowed to the right. Not Num, time route or adv. Not Com or Ger immediately to the right. Neither Po. Not Acc if sg3-verb to the left without a Nom to its left. Not Pron Dem followed by N, neither Pron Rel followed by time. No vdic immediately to your left. No Nom-cased Prop with some sort of verbal activity to its left is allowed..  

SELECT:AccTV3 Acc IF 
(*-1 TRANS-V BARRIER COMMA OR Num OR REAL-V OR Ess OR S-BOUNDARY OR (N Acc) LINK NEGATE *-1 Acc BARRIER S-BOUNDARY)
(NEGATE *-1C Acc BARRIER SV-BOUNDARY)(NEGATE 0 ANIMATE + Gen OR Pron + Gen LINK *1 Ill BARRIER NOT-NPMOD) 
(NEGATE *1 Acc - Num BARRIER S-BOUNDARY OR COMMA OR @CVP)
(NEGATE 0 Num OR Adv)(NEGATE *1 Po BARRIER NP-BOUNDARY)
(NEGATE 1 Com OR Ger)
(NEGATE *-1C SG3-V BARRIER NOT-NPMODADV OR PrfPrc LINK NEGATE *-1 (Sg Nom) BARRIER S-BOUNDARY)
(NEGATE 0 (Pron Dem) LINK 1 N)(NEGATE 0 (Pron Rel) LINK 1 TIME-N)
(NEGATE -1 <vdic>)(NEGATE 0 Sem/Fem OR Sem/Mal + Nom OR Sem/Sur + Nom LINK *-1 VERBAL-ACTIVITY-V OR DADJAT OR HUMAN-AGENT-V BARRIER SV-BOUNDARY LINK *2 ("ahte") OR EOC)
(NEGATE 0 ("jr") OR ("sr") LINK -1 Sem/Sur)
(NEGATE *-1 VAbess BARRIER NOT-NPMOD LINK -1 Loc) 
(NEGATE 1 ADVLCASE LINK -2 @CNP LINK -1 Attr OR Gen) 
(NEGATE *-1 ("birra") BARRIER NOT-ADV LINK *-1 VERBAL-ACTIVITY BARRIER SV-BOUNDARY) 
; 
	#$ Biera oahpaha munnje matematihka.
	#$ Oastte min ja min eatnamiid láibbiin, de mii ja min eatnamat šaddet farao opmodahkan.
	#$ Son áiggui sáddet iežas bártni mu viežžat, son muitalii. #Comment: correct, but not't sure it's a good idea.. mu is obj of viežžat.
	#$ Nieiddat liikojedje guldalit sávaldatkonseartta ja mánáidbottu.
	#% Lávvui gullá maid uvssot.
	#% Dieđátgo guđe beaivvi mis lea čoahkkin?
	#% Mii fertet maiddái geahčadit makkár lassi guoimmuhemiid galgat fállat vai bohtet vel eanet olbmot boahtte jagi, loahpaha duhtavaš beaivválaš jođiheaddji Eriksen. #Comment: search for the vdic-rule.
	#% Seammás go skuvlla, sámi oahpahusráđi ja Sámi boazodoallohálddáhusa ovddasteaddjit háliidedje ahte sámegielat oahppit ožžot oahpahusa sámegillii, de dajai Eanandoallooahpahusráđi jođiheaddji Ola Nystø ahte LOR bealis leat álo geahččan boazodoalloskuvlla fágaskuvlan boazodoalus.
	#% Gaskavahku bođii Ávvira journalista munnje Sámedikki dievasčoahkkinbeaivvi maŋŋel ja jearai mu giellageavaheami birra dáppe Sámedikki sárdnestuolus.


#!! * **OLDr2466** Selects accusative: when transitive verb to the left, but not if the TV is FAUX OR LOC-V
SELECT:OLDr2466 Acc IF (*-1C TRANS-V - Imprt BARRIER SV-BOUNDARY OR NOT-NPMODADV OR XGO LINK NOT 0 FAUXV OR LOC-V LINK NEGATE *-1 Acc BARRIER SV-BOUNDARY OR (Neg Foc/Neg-ge))(NEGATE 0 (Sg Nom) LINK NOT 0 Px LINK *-1 V + Sg3  BARRIER SV-BOUNDARY)(NEGATE -1 PrfPrc LINK NEGATE *-1 REAL-COPULAS BARRIER SV-BOUNDARY)(NEGATE *-1 NUM BARRIER NOT-Attr LINK NOT 0 NUMUNIT)(NEGATE 0 PrfPrc OR Adv OR VFIN OR Loc LINK NOT 0 Px) ((*1C NP-BOUNDARY OR S-BOUNDARY OR TIME-N + Ill BARRIER WORD - (N Ess) - (A Ess) LINK NOT 0 Actio OR Inf) OR (0 Dem OR ("juobbá") OR ("nubbi" Sg) LINK 1 Acc) OR (1 Ess LINK 1 COMMA)) ;
	#$ Sus báhkkanii olles gorut go oinnii Lárina čuvges vuovttaid.
	#$ Easkka 1968:s oaččui vejolašvuođa vázzit jahkebeallásaš gávpeskuvlla Romssas, ja 1972:s lohkagođii gymnásafágaid privatistan.
	#$ Son vállje juoppá máná.
	#$ – In jáhke dan tabu-áššin, dušše dovddan ahte olbmot unnán muitalit dán birra.





#SELECT:duration (DURATION-V objeakta sáhttá leat akk hámis!)

#!! * **AccInf** Selects Acc if the verb to the left is TV + Inf (you are the obj of the Inf). Differs from the other rules by not beeing restricted by an Acc to the right *hállat eatnigiela*
SELECT:AccInf Acc IF (*-1 TRANS-V + Inf OR (V TV PrfPrc) + @<OBJ BARRIER NOT-NPMODADV LINK NEGATE *-1 FAUXV BARRIER NOT-ADV-PCLE)
(0 NP-HEAD-ACC LINK NEGATE *1 Acc BARRIER CC OR COMMA OR SV-BOUNDARY LINK NOT 0 (Pron Gen)) # LINK *1 N BARRIER NOT-NPMOD)
(NEGATE *-1 Num BARRIER NOT-Attr)(NEGATE 0 A)(NEGATE 0 ANIMATE + Gen OR Pron + Gen LINK 1 Ill)(NEGATE 1 (N Acc) OR Com LINK NEGATE 0 Com LINK 1 Com)(NEGATE 1 Gen LINK 1 Po)(NEGATE 0 (Pron Pers) LINK 1 (Pron Refl) LINK 1 Gen LINK 1 Po)(NEGATE 0 Adv)(NEGATE -1 COMMA LINK -1C Gen) ; #(Pron Dem) OR
	#$ Sii eai ožžon lobi hállat iežaset eatnigiela, eai bottuinge dahje ovdal ja maŋŋá skuvllaáiggi.
	#$ Oidnen dárbbu geahččat lagabui buot boasttuvuođaid maid sápmelaččat šadde gierdat.
	#$ Logi minuvtta maŋŋel nagodii Sørøy Glimt čiekči Stig Martinsen dulbet bohtosa ovttain moalain.
	#$ Ovddemus galge nannet dárogieloahpahusa juogidettiin vuosttaš ja nuppi jagi goabbat sadjái. 	
	#% 17. artihkkal sisttisdoallá mearrádusaid movt eananvuoigatvuođaid galgá juogadit sin iežaset mieldelahtuid hárrái.
	#% Daid ferte ferte árvvoštallat stáhta dálá geavada ja riikka lágaid ektui.
	#% Lávvardaga čájehuvvo ođđa dokumentárafilma maid leat gohčodan Smierru.

#!! * **AccCOP** Selects Acc if copulas to the left and nominative to the left of COP *gápmagat leat áhči*
REMOVE:AccCOP Acc IF (*-1 COPULAS + FMAINV BARRIER NOT-NPMOD OR CC LINK *-1 (N Nom) BARRIER NOT-ADV)(1 EOC OR COMMA OR NEGFOC)(NEGATE *-1 (N Nom) BARRIER S-BOUNDARY LINK -1 (Pron Rel))(0 (N Acc) LINK NEGATE *-1 Num BARRIER NOT-Attr)(NEGATE 0 Nom)(NEGATE 1 Nom OR Acc OR (N Ess) OR Loc)(NEGATE *1 FMAINV BARRIER COMMA OR S-BOUNDARY);
	#$ Diet gápmagat leat áhči.
	#$ Lihtti lea ravddaid dási.
	#Commet: (1 EOC OR COMMA OR NEGFOC) dárkkis dán oasi korpusis.. 

#!! ### Gen modifiers inside NP 


#!! * **GenNP1** Selects Gen for Pron Pers (modifier): if NP-BOUNDARY OR Acc (but not if the finite verb is TV) to the left and N to right 
SELECT:GenNP1 (Pron Pers Gen) IF (-1 NP-BOUNDARY - TRANS-V OR Acc OR Sem/Plc + Loc)(*1 N BARRIER NOT-NPMOD)(NEGATE 0 Gen LINK 1 Po) ;
	#$ Min mánáin leat ollu stohkosat.
	#$ Dál lea du vuorru.
	#$ Gáivuonas su áhčis leat guokte oambeali ja guokte vilbeali.

#!! * **GenNP2** Selects Gen for N (modifier): if CC "ja" immediately to your left and accusative to your right *ja sámi jurddašanvuogi*
SELECT:GenNP2 (N Gen)  IF (-1 ("ja"))(NEGATE 0 TITLE LINK 1 Prop)(1 (N Acc))(NEGATE 0 V OR ABBR OR Attr);
	#$ Miššonbarggu álggu rájes juo Leem háliidii oahppat sámegiela ja sámi jurddašanvuogi.
	#% Sii ožžot geavahit eatnama ja gáttiid ja elliideaset ja iežaset ealáhussii. # Deaivá dán vaikko eat hálit dan.

#!! * **GenNP3** Selects Gen (modifier): if first one to right is Nom or Loc *Norgga oaivegávpogis*
SELECT:GenNP3 Gen IF (*-1 BOS OR COMMA OR XGO BARRIER NOT-Attr)(0 Gen - Num LINK NOT 0 (A Attr))(1 (N Loc) OR (N Nom) OR N + Ess LINK NOT 0 Prop)(NEGATE 0 (Pron Interr) OR (Pron Rel))(NEGATE 0 TITLE + Nom LINK 1 Prop + Nom)(NEGATE 0 (Pron Dem) + Acc)(NEGATE 0 (Sg Nom) LINK *1 SG3-V BARRIER VFIN OR Nom OR Ess);
	#$ Ánde orru Oslos, Norgga oaivegávpogis.
	#$ Nieidda eadni ii sihtan vuolgit, muhto nisu hásttii.
	#$ Organisašuvnna bláđđi, man gohčodit Fokusin muitala ahte gonagas Olav su áiggistis lei bivdán beassat leat servodaga suodjaleaddji.
	#$ Jovsset, su irgi, gii lei rehálaš olmmái ii ge hálidan heahppašuhttit su, áiggui earránit sus suoli.
	#% Seminára váldogiella lea davvisámegiella.
	#% Čáhppesmurjjiid olbmot gazze milkkiin ovdal.

#!! * **GenNP4** (modifier) selects Gen -1 BOS or COMMA, 1 Nom *nissoniid bargu*
#SELECT:GenNP4 (N Gen) (*-1 BOS OR COMMA)(1 (N Nom))(NEGATE 0 Num)(NEGATE 1 Inf)(NEGATE 0 (Pron Pers)) ;
	#$ Galgágo buot bivttasráhkadeapmi leat nissoniid bargu?
	#% Mu oabbá lea bargan dán skuvlla rektorin.
	#% Sudno heasttat bivvet olgun vaikko leage buolaš. #Comment: This one is not meant for pronouns: they have GenNP1. 

#!! * **GenNPCo** (modifier) Selects Pron Pers Gen if Nom to the left of ja *Mun ja mu ustibat*
SELECT:GenNPCo (Pron Pers Gen) IF (-1 ("ja") LINK -1 Nom) ;
	#$ Mun ja mu ustibat manaimet kinoi geahččat ođđaseamos Harry Potter-filmma.

#!! * **GenRefl** (modifier) selects Gen in front of a noun in accusative or nominative case *iežaset oiviliid*
SELECT:GenRefl Gen IF (0 (Pron Refl) + Gen)(*1 N OR A BARRIER NOT-Attr LINK 0 Acc OR Nom) ;
	#$ Atrihkkalat leat čálliid iežaset ovddasvástádus ja lohkkit berrejit daid lohkat kritihkkalaččat ja hábmet iežaset oaiviliid historjjá birra ja otná ja ihttá sámi skuvlla birra.
	#$ Lehpetgo iežadet diŋggaid dolvon?
	#$ Vaikko leatge sámeaktivista, de it dárbbat iežat čivgga dahkat givssidanoaffarin.



#!! * **AccAfterCC** Select accusative: if genitiv to the left, and CC "ja" to the left of genitive *eamiálbmot- ja globaliserenprošeavtta koordináhtor*
#SELECT:AccAfterCC (N Acc)  IF (-1 (N Gen) LINK -1 ("ja"))(0 (N Acc))(NEGATE *1 (N Acc) OR (N Nom) OR (N Ess) OR (N Loc) BARRIER NOT-Attr OR PUNCT-LEFT)(NEGATE 0 V)(NEGATE 1 Gen LINK 1 (N Loc))(NEGATE *1 Gen LINK 1 Po);  
	#% Son oassásdoallá dál sámi historjjálaš girjebarggus ja lea máŋggafágalaš eamiálbmot- ja globaliserenprošeavtta koordináhtor. #Deaivá dán vaikko eat hálit dan.
	#% Lávdegoddi lea maiddái atnán áiggi ságastallat davviruoŧa čearuid ja Romssa boazoorohagaid gaskaneas váttisvuođaid birra. # Seamma.

# Alternatively this rule is not needed after removal of all Acc "KillAcc?", but for now it stays.
#SELECT:GenNP2 Gen (-1 NP-BOUNDARY)(*1 N BARRIER NOT-NPMOD)(NEGATE 0 Gen LINK 1 Po);
	#$ Soai lohkaba Harry Potter-girjji eaŋgalasgillii.
 # Jurddaš ná: Gii sáhttá oamastit gean/man? 
 


#!! ### Accusative in coordination

# All coordinated accusatives rely on a secure accusative somewhere in the s.

# COMMA

#!! * **CoAcc1** Selects Acc when NP inbetween commas *guolleoivviid, dáraid, debbuid, buđeittaid, boares rásiid*
SELECT:CoAcc1 Acc IF (*-1 COMMA BARRIER NOT-NPMOD LINK -1C NP-HEAD-ACC)(1 COMMA OR JA/DAHJE OR ("jnv") OR ("jna")) ;
	#$ Doppe moai sáhtiime oastit gáfe, deaja dahje sávtta.
	#$ Liepmasii olbmot bidje guolleoivviid, dáraid, debbuid, buđeittaid, boares rásiid ja rohkajáffuid.
	#% Dat guoskkai Čohkkirasa, Wilhelmina, Suorssá ja Deartná gielddaid sápmelaččaide. #Hits this, but it shouldn't. Choose gen coordination first?
	#% Konvenšuvdna galgá ovddidit ja ovdanahttit riikkaid, čearuid ja boazoorohagaid ovttasbarggu.
	
# COORDINATOR LEFT

#!! * **CoAcc2** Select Acc if coordinator to your left and accusative to the left of the coordinator *deaja dahje sávtta*
SELECT:CoAcc2 Acc IF ((*-1 CRD BARRIER NOT-NPMODADV LINK -1C Acc) OR (*-1 CRD BARRIER NOT-NPMODADV LINK -1 Inf LINK -1C Acc))((1 NP-BOUNDARY OR COMMA OR Ill LINK NOT 0 Po) OR (1 Attr LINK NEGATE *1 (N Acc) BARRIER NOT-A))(NEGATE 0 Inf OR Rel)(NEGATE 0 Nom LINK *1 VFIN BARRIER S-BOUNDARY)(NEGATE 0 Dem LINK 1 PrfPrc LINK *1 N BARRIER NOT-NPMOD);
	#$ Doppe moai sáhtiime oastit gáfe, deaja dahje sávtta. #maŋit objeakta
	#$ Ja de sii bidje čázi duoldat ja juopmomielkki dasa, ja de dan juhke.
	#$ Gielddaid, fuođđo- ja guolástaneiseválddiid jna. ja guoskevaš geavaheddjiid servviid galgá gullat ovddalgihtii.

 # COORDINATOR RIGHT

#!! * **CoAcc3** Selects Acc in front of *ja* if there is a secure Acc to the right *semináraid ja diehtojuohkinčoahkimiid*
SELECT:CoAcc3 Acc IF (0 Acc LINK 1 JA/DAHJE LINK *1C Acc BARRIER NOT-NPMODADV)(NEGATE *1 FMAINV BARRIER S-BOUNDARY)(NEGATE -1 Acc LINK *1 Acc OR Gen LINK 1 ("ja") LINK 1 Acc LINK 1 Ill)(NEGATE *-1 NUM BARRIER NOT-NPMOD LINK 0 NUM) ;
	#$ Norgga-ruoŧa boazoguohtunkommišuvdna lea doallan moanaid semináraid ja diehtojuohkinčoahkkimiid guoskevaš eiseválddiiguin, organisašuvnnaiguin ja dutkiiguin.	
	#$ Guovddáš lea ásahan alcces ovttasbargooktavuođaid ja fierpmádagaid relevánta fága- ja dutkanbirrasiiguin.
	#% Ii oktage fastivuođa ja giellása bálvaleaddji beasa sisa.
	#% De dat dagaha eahpesihkarvuođa skuvlla ja dan boahtteáiggi birra.

	
#!! * **CoAccJA** Selects Acc when "ja" to the left and comma to the left of "ja" with a secure Acc to the left of comma *sámegiela, ja heajos dárogiela.*
SELECT:CoAccJA Acc IF ((*-1 ("ja") BARRIER SV-BOUNDARY OR Nom OR FAUXV LINK -1 COMMA LINK -1C Acc) OR (*-1 ("ja") BARRIER SV-BOUNDARY OR Nom OR FAUXV LINK -1C Acc))(NEGATE 0 Num)(NEGATE 0 Gen LINK 1 Acc LINK 1 @CNP)(NEGATE 1 Gen LINK 1 Po)(NEGATE 1 Nom OR Acc OR Attr)(NEGATE *1 FMAINV BARRIER S-BOUNDARY OR COMMA)(NEGATE 1 (A Ess) LINK 1 Acc);
	#$ Mu givssidedje danin go hállen sámegiela, ja heajos dárogiela.
	#$ Gusa-, gálbbi- ja gáiccanáhkis mearragátti sámit ráhkadedje gápmagiid, ja sávzzanáhkis dorkkaid ja roavgguid.
	#% 1995 rájes son lea leamaš Sámi joatkkaskuvllas Kárášjogas, gos son lea oahpahan dearvvašvuođa- ja sosiálafága vuođđokurssa ja veahkkedivššár ja ambulánsa joatkkakurssa.

#!! * **CoAccJA2** Selects Acc in front of Gen + Po if ja in front of Acc *ja ruhtan sávzzaid ovddas*
SELECT:CoAccJA2 Acc IF (*-2 STRICT-TRANS-V BARRIER SV-BOUNDARY)(*-1 ("ja") BARRIER WORD - Acc LINK NEGATE -1 Gen)(0 Acc OR Gen)(*1 Gen BARRIER CC OR FMAINV LINK 1 Po LINK 1 EOC)(NEGATE 0 (A Ord))(NEGATE 0 HUMAN-INDIVIDUAL - HUMAN-LIKE OR Pron LINK *-2 HUMAN-INDIVIDUAL - HUMAN-LIKE + Acc OR Gen BARRIER SV-BOUNDARY)(NEGATE 1 Pr) ;
	#$ Mun attán heaggan, áhkkán ja ruhtan sávzzaid ovddas.
	#$ Olmmoš galgá čuovvut iežas guovllu gáktemálle, muhto sáhttá válljet liinni ja ivnni miela mielde.
	#$ Nu de mun fievrredin militearaseaŋgga ja bolstara burrerobi alde.
	#$ Dát dieđáhus joatká ulbmiliid ja bidjá eavttuid, strategiijaid ja váikkuhangaskaomiid nana guoddevaš boazodoalu váste.
	#$ Olgun Ulla basai čalmmiid ja giehtamáddagiid gáivvo luhtte ja bođii dasto sisa ja manai iežas latnjii.
	#% Váldde máná ja su eatni fárrui. #Shouldn't hit.
	#% Go bargen olles 37 jagi seamma skuvllas, de lean dieđusge vásihan ollu rievdadusaid, sihke skuvllaáiggi, oahpahusvugiid, oahpahusgiela ja ohppiid láhttema dáfus.

SELECT:CoAccEss Acc IF (-1 CC LINK -1 (N Ess) LINK -1 (N Acc) LINK -1 OPRED-V)(0 (N Gen) LINK 1 Ess);
		#$ Ipmil gohčodii čuovgga beaivin ja seavdnjadasa idjan.
	
#SELECT:CoAccr2455 Acc IF (*-1 VFIN OR MOD-ASP OR COPULAS BARRIER NOT-NPMOD)(1 CC)(*2 NP-HEAD BARRIER NOT-NPMOD LINK 1 Inf);
	#$ Son ii muitán ahte lea ferten niesteseahka dahje lávkka guoddit.	
	
#Missing rules for: 

#$ Ulbmil čoahkkimiiguin lea oažžut buoret oktavuođa EU:i, ovddidit sámepolitihka luondduvalljodatgeavaheami, davviguovlluid ovdánahttinplánaid, Davviriikalaš sámekonvenšuvnna ja ruoššabeale sámiid dilálašvuođa riektadili ektui.




#!! ### Intransitive verbs can sometimes be transitive

#!! * **IVasTV** Selects Acc if you are GEOGRAPHICAL-PLACE, ABSTR-ROUTE or EDUCATION and somewhere in the sentence is a intransitive verb acting as a transitive verb *sii vázzet skuvlla*
SELECT:IVasTV Acc IF ((*-1 JOHTIT + IV BARRIER SV-BOUNDARY OR Pr LINK 0 IV) OR (*1 MOVEMENT-V + IV - ("beassat") OR JOHTIT + IV BARRIER SV-BOUNDARY LINK NOT 0 ("mannat") OR ("vuolgit") OR ("boahtit")))(0 GEOGRAPHICAL-PLACE OR ROUTE OR EDUCATION OR Sem/Measr)(NEGATE 1 Po OR ROOM) ; #(NEGATE -1 CC LINK *1 VFIN) ;
#SELECT:IVasTV EDUCATION + Acc IF (*0 ("vázzit") BARRIER SV-BOUNDARY)(NEGATE 1 Po OR ROOM) ;
	#$ Čuoigat vári.
	#$ Mun váccán vákkiid, vuvddiid ja váriid.	
	#$ Fertiimet vázzit bálgáid geasset ja Deanu mielde dálvet.
	#$ Sii vázzet skuvlla.
	#$ Mun váccán skuvlla gávpogis.
	#$ Son váccii joatkkaskuvlla Girkonjárggas ja Kárášjogas ja buohccedivššárskuvlla Hámmárfeasttas, mas gearggai 1982:s.

	#% Mun váccán skuvlla šiljus. #Comment: This is not Acc, but Gen.
	


#!! * **IVisTrans** Selects Acc if you are *spábba* and somewhere is *viehkat* 
SELECT:IVisTrans Acc IF (0* ("viehkat") BARRIER S-BOUNDARY)(0 ("spábba")) ;
	#$ Son viehká spáppa.
	#$ Spáppa son viehká. #Comment: Iskka sáhttágo viiddidit.

#!! * **IVisTrans2** Selects Acc if you are SHOE or HUNT-ANIMAL or BOAZU and somewhere is *vázzit*
SELECT:IVisTrans2 Acc IF (0* ("vázzit") BARRIER S-BOUNDARY)(0 SHOE OR HUNT-ANIMAL OR BOAZU) ;
	#$ Vázzit rievssaha.
	#$ Rievssaha vázzit.

#!! * **IVceavzit** Selects Acc for ceavzit IV if you are *eksámen* and ceavzit is found somewhere in the clause 
SELECT:IVceavzit Acc IF (*0 ("ceavzit") BARRIER S-BOUNDARY)(0 ("eksámen")) ;
	#$ Ikte mun cevzen eksámena nuoraidskuvllas. #Comment: too narrow.


#!! * **IVnohkkat** Selects Acc if you are BEDCLOTHES  
SELECT:IVnohkkat Acc IF (0* ("nohkkat") OR ("oađđit") OR ("oađašit") OR ("oađástallat") OR ("nohkastit") BARRIER S-BOUNDARY)(0 BEDCLOTHES) ;
	#$ Son oađđá beaskka.

#!! * **IVsahttit** Selects Acc 
SELECT:IVsahttit Acc IF (-1 ("sáhttit") LINK NEGATE *1 FMAINV OR Inf BARRIER S-BOUNDARY)(0 (Pron Pers) OR HUMAN LINK 0 Gen)(*1 Ill OR DOHKO BARRIER NOT-ADV);   

#!! * **IVsahttit2** Selects Acc 
SELECT:IVsahttit2 Acc IF (0 (Pron Pers) OR HUMAN LINK 0 Gen)(*1 ("sáhttit") BARRIER S-BOUNDARY LINK NEGATE *1 FMAINV OR Inf BARRIER S-BOUNDARY)(*1 ("sáhttit") LINK *1 Ill OR DOHKO BARRIER NOT-ADV);  
	#$ Gii sáhtii du dohko?
	#$ Gii du nu johtilit sáhtii dohko?


#!! * **IVvaikkuhit** Selects Acc for váikkuhit IV   
SELECT:IVvaikkuhit Acc IF (*-1 ("váikkuhit") BARRIER SV-BOUNDARY OR Ill)(0 Gen LINK NEGATE 1 Acc OR Ill) ;
	#$ mo stáhta guhkesáigge dáruiduhttinpolitihkka lea váikkuhan ja ain váikkuha min.


#! !duššiid
SELECT:IVdussiid ("dušši" Pl Acc) (0 Gen LINK NOT 1 Po OR N)  ;   
	#$ Ale duššiid heahtádutta.
	
#!! ### Accusative or genitive in front of ALU and in front of adjectives


#!! ### Exceptional accusative attributes in front of ALU nouns.

#!! * **ALU** Selects Acc when Num and right is MEASURE LINK 1 ALU
SELECT:ALU Acc IF (0 Num LINK *1 MEASURE BARRIER NOT-A LINK 1 ALU);
	#$ Visti lei vihtta mehter alu.
	
#!! * **ALU2** Selects Acc when Num and not Adv, and 1 ALU
SELECT:ALU2 Acc IF (0 Num LINK NOT 0 Adv LINK 1 ALU);
	#$

#!! * **ALU3** Selects Acc for Num when right context Num ALU
SELECT:ALU3 Acc IF (0 Num LINK 1 Num LINK 1 ALU);
#Not-so-exceptional?

LIST PROS-ARAB = (".*%"r)  ;

#!! * **arabpros** Selects Nom
SELECT:arabpros Nom IF (0 N LINK *-1 PROS-ARAB BARRIER NOT-A);

#!! * **NumAcc** Selects Acc
SELECT:NumAcc Acc IF (0 Num LINK 1 N + Sem/Measr + Acc - ("jahki")) ;
SELECT:NumAcc Acc IF (0 Num LINK 1 N + ("siidu") + Acc)((2 Adv + Sem/Plc) OR (2 Gen LINK 1 Po)) ;
	#% Norgga álbmogis leat jo miljovdna ássi badjel 60 jagi.

#!! * **NumNom** Selects Nom
SELECT:NumNom Nom IF (0 Num LINK *1 N BARRIER NOT-Attr LINK NOT 0 TIME)(*-1 FMAINV - TRANS-V BARRIER SV-BOUNDARY)  ;
SELECT:NumNomFragm Nom IF (0 Num)(NEGATE *0 VFIN)  ;
	#$ 12 ođđa njoammuma Romssas.

#!! * **NumNom** Selects Nom
SELECT:NumNom Nom IF (*-1 COPULAS OR (IV) BARRIER NOT-ADV-PCLE)(0 NUM)(1 ("ruvdno"));
	#$  Doaibmabiju jahkásaš bušeahttarámma lea: 380.000 ruvnno.

#!! * **NumComplAcc** (complement of numerals) Selects Acc Sg when Num Sg to the left is Acc
SELECT:NumComplAcc (Sg Acc) OR CURRENCY + Acc  IF (0 (N Gen) LINK *-1C NUM + Sg + Acc BARRIER MEASUREADJS OR NOT-A - (Pron Indef Attr) - Gen OR COMMA OR (":") LINK NOT 0 Sem/Year)(NEGATE -1 CURRENCY)(NEGATE 0 Nom + Sg LINK -1 Sem/Time-clock LINK *2 VFIN + Sg3)(NEGATE 0 N LINK NOT 0 Sem/Time LINK 1 V + Pl3)(NEGATE 0 Gen LINK 1 N + Acc)(NOT 0 NATION) ;
	#$ Go máŋga vahkku eai leat beassan olbmuid bániid divvut ja dikšu.
	#% Orohagas leat maid guokte luonddureserváhta hui lahkalaga, Sørlenangsbottena ja Jægervassbottena gaskkas.
	#% Dušše 24 hearggi leat kvalifiseren iežaset finálii.

#!! * **NewGen** (complement of numerals) Selects Gen Sg when Num Sg to the left *guhtta kilu*
SELECT:NewGen (Sg Gen) OR CURRENCY + Gen  IF (0 (N Acc) LINK *-1 NUM + Sg + Nom BARRIER MEASUREADJS OR NOT-A - (Pron Indef Attr) - Gen OR COMMA OR (":") OR CC LINK NOT 0 Acc OR Sem/Year)(NEGATE -1 ("moadde") OR CURRENCY)(NEGATE 0 Nom + Sg LINK -1 Sem/Time-clock LINK *2 VFIN + Sg3) ;
	#$ Juohke heasta borrá sullii guhtta kilu suinniid beaivái.
	#$ Dán skuvllas ledje jahkásaččat sullii 20 oahppi geat iešdáhtolaččat bohte deike, ja buohkat ledje lullisámit.
	#$ Do bohtet guokte olbmo.
	#% Vuoiti oažžu 500 ru.
	#% Finnmárkku fylkkagielda lea jagi 1988 rájes ovddidan doaimmaid sámi álbmoga várás spesialistadearvvašvuođabálvalusas, ja ráhkadii jagi 1991 fylkkagieldda sámi dearvvašvuođa- ja sosiálaplána.
	#% 2: Duommá ja Máret leaba gárdimis.
	#% 50 % bistevaš veahkkebargi.

	#Comment: NewGen always pics genitiv for the noun that follows a Sg Num. There are two varieties, depending on whether preceded by TV (a transitive verb) = akk+gen or COPULAS = nom+gen.


#!! * **NewGenCo** (coordinated complement of numerals) Selects Gen if Num Acc + NewGen found to the left of "ja" *máŋga dáhpáhusa ja digaštallama*
SELECT:NewGenCo (Sg Acc) OR CURRENCY + Acc  IF (0 (N Acc)) (-1 ("ja"))(-2C Acc)(*-3C NUM + (Acc Sg) BARRIER NOT-A - (Pron Indef Attr)) ; 
	#$ Dákkár dáhpáhusat, ja máŋga iežá dáhpáhusa ja digaštallama dahke munnje persovnnalaččat váttisin dáid gažaldagaid mat bohciidedje Gáivuonas.
	#% Go duiskalaččat válde Finnmárkku vuolláseaset 1940 giđa ja geasi, de váldigohte sii skuvllaid ja internáhtaid soalddáhiid ásodahkan ja eará geavahussii.

#!! * **ALU4** Selects Acc if you are Num and to your right Num Acc followed by MEASURE OR ALU/A *guokte golbma mehtara alu* 
SELECT:ALU4 Acc IF (0 NUM)(*1 (Num Acc) BARRIER NOT-COMMA LINK 1 MEASURE LINK 1 ALU OR A);
	#$ Guokte golbma mehtera guhku, ja guhkitge, sáhttá vuojahat leat. 

#!! * **ALU5** Selects Gen if Num to the right, followed by Num, followed by ALU/A 
SELECT:ALU5 Gen IF (1 (Num Sg Acc) LINK 1 (Num Sg Acc) LINK 1 MEASURE LINK 1 ALU OR A);
	#$ 


#Comment: NUM + ACC, no ALU/adjective here... Concider moving?
#!! * **NumTimeMannel** Selects Acc for Num before TIME MANNEL
SELECT:NumTimeMannel Acc IF (0 Num)(1 TIME)(2 MANNEL);
	#$ Logi minuvtta maŋŋel nagodii Sørøy Glimt čiekči Stig Martinsen dulbet bohtosa ovttain moalain.
	
#!! * **NumPageMannel** Selects Acc for Num before siiddu etc + MANNEL.
SELECT:NumPageMannel Acc IF (0 Num)(1 ("siidu") OR ("kapihtal"))(2 MANNEL);

#!! * **NumPageMannel2** Selects Acc for Num before ovdalis etc
SELECT:NumPageMannel2 Acc IF (0 Num)(1 ("siidu") OR ("kapihtal") LINK 2 ("maŋŋelis") OR ("ovdalis"));
	#$ Earát su luohkás ledje juo vissa njeallje siiddu su ovdalis matematihkka-girjjis.
	
	
#!! * **GenBoaris** Selects Gen in golbma *jagi* boaris
SELECT:GenBoaris Gen IF (-1 (Num Gen))(0 TIME + Sg)(1 ("boaris"));
	#$ Son lea viđa jagi boaris.
	#% Sápmelaččaid historjá álbmogin lea duháhiid jagiid boaris, muhto dálá ássanguovlluin sápmelaččat eai leat ássan nu guhká.	
	
#! * __ALU6__ Selects Gen if 0 MEASURE and Num right
#!!     - Ritva comment: Find a rule for "viđa" aswell, this hits "mehter" as it should
SELECT:ALU6 Gen IF (-1 (Num Gen))(0 MEASURE + Sg)(1 A OR ALU); 
	#$ Dohko manai viđa mehter govdosaš geaidnu.


#!! * **XXr2002** Selects genitive if there is a numeral immediately to your left, and you are TIME: *golbma jagi*
SELECT:XXr2002 Gen IF (-1 (Num Sg Acc))(0 TIME); #(1 ("maŋŋel"));
	#$ Dat dáhpáhuvai golbma jagi maŋŋil.
	


#!! ### Numerals 


#!! * **NumGenPo** Selects Gen for a numeral if a transitive verb to the left. To the right a Gen followed by a postposition *vuovdán 163 000 ruvnnu ovddas*
SELECT:NumGenPo Gen IF (*-1 TRANS-V BARRIER S-BOUNDARY OR Pr LINK 0 FMAINV OR Inf)(0 NUM + Sg LINK NOT 0 NUMUNIT)(1 Gen LINK 1 Po) ;
	#$ Mii leat vuovdán oktiibuot 163 000 ruvnnu ovddas.

#!! * **NumMoney** Removes Gen if you are a numeral and immediately to your right is CURRENCY *vihtta ruvnnu*
REMOVE:NumMoney Gen IF (0 Num)(1 CURRENCY);
	#$ 1500 ruvnno

#!! * **NumGitta** Selects Acc when you are a numeral with "gitta" immediately to your right followed by a numeral with acc-case *180 gitta 200*
SELECT:NumGitta Acc IF (0 Num)(1 ("gitta"))(2 (Num Acc));
	#$ Dát ledje gáddenuohtit mat ledje 180 gitta 200 sala guhku, ja 15 gitta 20 sala čieŋu.

# MAIN RULES: NumAcc1, NumAcc2, GenIfNum

#!! * **NumAcc1** Selects Acc if you have a transitive verb to the left and you are a numeral followed by a noun  *oste guokte mielkki*
SELECT:NumAcc1 Acc IF (*-1 TRANS-V BARRIER S-BOUNDARY OR Pr LINK 0 FMAINV OR Inf)(NEGATE -1 N LINK 2 VFIN)(NEGATE -1 CS OR ("jahki") OR GEASSET)(0 NUM LINK NOT 0 Adv OR NUMUNIT OR Sem/Year)(*1 (N Acc) OR (N Gen) BARRIER NOT-Attr)(NEGATE *1 Acc BARRIER S-BOUNDARY)(NEGATE *1 Ill OR Loc BARRIER NOT-A)(NEGATE 1 (N Gen) LINK 1 Po)(NEGATE *-1 CC BARRIER NOT-ADV LINK -1 Num + Sg)(NEGATE -1 BEAIVI + Gen)(NEGATE -1 MANNU LINK 1 (A Ord) LINK 1 (Adv ABBR)); 
	#$ Moai oste guokte mielkki gávppis.
	#$ Giliviessu geassá 200 olbmo.
	#$ De son barggai guokte jagi buohccidivššárin Kolvereidas, Trøndelágas.
	#% Sámediggi lea geigen jahkedieđáhusas departementii Sámedikki doaimmas 2005.
	#Comment: NumAcc1 is intended to work with NewGen: as in it is always followed by Gen.

#XX
#!! * **NumJahki** Removes Acc if you are a numeral and JAHKI-NUM is immediately to your left *mávssii mannan jagi 43 ruvnnu*
REMOVE:NumJahki Acc IF (-1 JAHKI-NUM LINK NOT -1 (PrfPrc @>N) OR Attr OR Dem)(0 Num);
	#$ Son mávssii mannan jagi 43 ruvnnu kilos.
	#Comment: what's the difference between this one and XXr2006 (put it the other way: why wasn't XXr2006 chosen over this?)
	
#!! * **NomIfNum** Removes Acc if Gen to the right (because you are Nom). Transitive verb with an Acc to the right *máŋga gávpeolbmá lonuhedje fáhcaid*
REMOVE:NomIfNum (Num Acc) IF (1 (N Gen))(2 NOT-FAUXV + TRANS-V)(*3 (N Acc) BARRIER NOT-NPMOD);
	#$ Máŋga gávpeolbmá lonuhedje fáhcaid gálvvuiguin.
	#Comment: The subject is a numeral.

#!! **NumGenMeasure** Genitive numerals in front of ruvdnosaš with friends
SELECT:NumGenMeasure (Num Gen) IF (1 MEASUREADJS)(2 N);
	#$ Lágamánneriekti maid dubmii dievddu máksit 130 000 kruvdnosaš buhtadusa gillájeaddjái.
	#Comment: Goldcorpus has Gen on the numeral, hence Gen.


#!! * **NumAcc2** Selects Acc for singular numerals if there is a transitive verb somewhere in the sentence and the numeral is followed by a noun *logi báhkkoma* OBS
SELECT:NumAcc2 Acc IF (*-1 NOT-FAUXV + TRANS-V BARRIER SV-BOUNDARY)(0 NUM + Sg LINK NOT 0 NUMUNIT OR Sem/Year)(*1 N + Sg + Acc BARRIER NOT-Attr - Gen LINK NOT 0 Ill)(NEGATE *-1 COPULAS + FMAINV BARRIER SV-BOUNDARY)(NEGATE *-1 Der/Pass OR (Dem Nom) BARRIER NOT-ADV)(NEGATE *1 COPULAS + FMAINV BARRIER COMMA OR SV-BOUNDARY)(NEGATE -1 NUMMAR)(NOT 1 MEASUREADJS);
	#$ Sii máhtte áddekeahttá bajillohkat logi báhkkoma ja Áhččámet.
	#$ Áiggun dušše geahččat moadde filmma.
	#$ Dákkár dáhpáhusat, ja máŋga iežá dáhpáhusa ja digaštallama dahke munnje persovnnalaččat váttisin dáid gažaldagaid mat bohciidedje Gáivuonas. 
	#% Oarje-Finnmárkkus leat dál registrerejuvvon 8.500 muohtaskohtera.
	#% Buot njeallje máná ledje náđđon eagi vuostá.
	#% Áirras nr. 10 Ole Henrik Magga oaččui permišuvnna.
	#% Son lea čohkken 14 eamiálbmoga biepmuid ovtta girjái.
	#Comment: Is intended to work with GenIfNum (or NewGen in some cases.)
	#$ Guovtti jagi badjel čuovvut guhtta verdde, golbma badjeolbmo ja golbma mearragátte ássi.
	
# Missing rule for coordination: 
#SELECT:NumCoord Acc (0 NUM + Sg LINK NOT 0 NUMUNIT)(1 ("ja") LINK *1 (Num Sg Acc) BARRIER WORD - Adv - A) ;
	#$ Juohke mánná oaččui eanemusat guokte ja eanemusat golbma jagi oahpahusa jagis.
	#Comment: Make it more general? 


#!! * **GenIfNum** (complement of numerals) Selects Gen Sg if there is a Num Sg to your left *guđa geardde* 
SELECT:GenIfNum (Gen Sg) OR CURRENCY + Gen  IF (*-1 NUM + Sg + Gen OR NUM + Sg + Nom BARRIER MEASUREADJS OR NOT-A OR (":") OR PUNCT LINK NOT 0 NUMUNIT OR Sem/Year OR Sem/Time-clock)(0 (N Gen))(NEGATE 1 Po)(NEGATE -1 CC) ;
	#$ Mii finaimet doppe guđa geardde.
	#$ Viđa máná biktasat ledje jávkan.
	#% Sii geat máhtte dušše moadde luođi, ohppe máŋga ođđa luođi internáhtas orodettiin.
	#% De ohccu moadde girjji.
	#Comment: GenIfNum always pics genitiv for the noun that follows a Sg Num. There are two varieties, depending on whether preceded by TV (a transitive verb) = akk+gen or IV (intransitive verb) = gen+gen.

#!! * **NumAccCo** (coordinated num) Selects Acc if you are Num Sg and to your right: CC with a Num to the right *guokte ja eanemusat golbma*
SELECT:NumAccCo Acc IF (0 NUM + Sg LINK NOT 0 NUMUNIT)(1 CC LINK *1C (Num Sg Acc) BARRIER NOT-ADV);
	#$ Juohke mánná oaččui guokte ja eanemusat golbma jagi oahpahusa jagis.
 
#!! * **NumAccIV** Selects Acc 
SELECT:NumAccIV Acc (*-1 (N Nom) OR (N Ess) BARRIER FMAINV)(*-2 ("leat" V IV PrfPrc) + FMAINV BARRIER SV-BOUNDARY)(0 NUM + Sg LINK NOT 0 NUMUNIT)(1 N + Gen) ;
	#$ Rátnogođđin lea leamaš mearrasámi árbevierru goitge 1300 jagi.
	#OR CLB OR @CNP OR @CVP

#!! * **NumAge** Selects Acc for Sg numerals if a time unit to the right is followed by boaris *vihtta jagi boaris*
SELECT:NumAge Acc (0 NUM + Sg LINK NOT 0 NUMUNIT)(1 TIME-UNIT + Gen LINK 1 ("boaris")) ;
	#$ Son lea vihtta jagi boaris.
	
# PLURAL 
 
#!! * **NumAccPlRight** Selects Acc when transitive verb to the left. You are Num Pl and to your right is Acc *goarui viđaid gápmagiid*
SELECT:NumAccPlRight Acc IF (*-1 TRANS-V BARRIER SV-BOUNDARY)(0 Num + Pl OR Indef + Pl + Foc/ge)(1 Acc) ;
	#$ Máret goarui viđaid gápmagiid.
	#$ Mun osten guvttiid buvssaid ja ovtta jáhka.
	#$ Mun osten dušše ovttaid sabehiid.
	#$ Son ii fuola ovttaidge sabehiid.
	#? Sin jahkásaš johtolagat sáhtte leat čuđiidge kilomehteriid, ja olle dálá Suomabeale vuovdeguovlluid dálveorohagain Jiekŋameara rittu geasseorohagaide.

#!! * **NumAccPlLeft** Selects Acc when tranistive verb to the right (same as the previous. Only differs in which direction the verb is found). *galliid sabehiid don ostet*	
SELECT:NumAccPlLeft Acc IF (*1 Acc BARRIER WORD)(*2 TRANS-V BARRIER SV-BOUNDARY)(0 Num + Pl) ;
	#$ Galliid sabehiid don ostet? 
	#$ Galliid heajaid dii lehpet doallan? 

#!! * **NumAccPlLeft** Selects Acc if you are N Acc Pl and to your left is Num Acc Pl *galliid sabegiid*		
SELECT:NumAccPl Acc IF (-1 (Num Acc Pl))(0 (N Acc Pl)) ;
	#$ Galliid sabehiid don ostet?
	#$ Sápmelaččaid historjá álbmogin lea duháhiid jagiid boaris, muhto dálá ássanguovlluin sápmelaččat eai leat ássan nu guhká.
	#$ Sin jahkásaš johtolagat sáhtte leat čuđiidge kilomehteriid, ja olle dálá Suomabeale vuovdeguovlluid dálveorohagain Jiekŋameara rittu geasseorohagaide.
	#$ Dat lea duháhiid jagiid boaris.
	#% Vaikko mánáin lei doarvái bargu skuvllas ja bihtáidbargamiin beaivvi mielde, de berre liikká dál maŋŋá fuomášuhttit ahte máŋggaid jagiid ii lean makkárge lágiduvvon asttuáigefálaldat mánáide.
SELECT:NumAccPl Num + Acc IF (0 (Num Acc Pl))(1 (N Acc Pl)) ;


# OKTA

#!! * **NumOktaAcc** Selects Acc if 0 okta followed by a noun. Transitive verb to the left *oidnen ovtta nieidda*
SELECT:NumOktaAcc Acc IF (*-1 TRANS-V + FMAINV BARRIER SV-BOUNDARY)(0 OKTA + Sg)(1 N LINK NEGATE 0 Gen LINK 1C Acc) ;
	#$ Mun oidnen dušše ovtta nieidda.
	#$ Árvaluvvon juohkin dáhpáhuvvá dáinnalágiin ahte juohke suohkan beassá vuos árvalit ovtta evttohusa stipendii.
	#% Dan eatnamis oažžu ovtta gusa fuođđara.

	
# Genitive after quantifier

	#REMOVE Gen IF (*-1C (@>N) BARRIER NPNH)(NEGATE *1 N BARRIER NOT-NP);
#SELECT:QUANr2135 Gen IF (*-1C (Num Nom Sg) BARRIER NOT-NPMOD OR Gen OR CC OR PUNCT OR REAL-CLB OR Ord LINK NOT 0 OKTA OR Ord OR (Num A) LINK NOT -1 ("jahki"))(0 (@Num<))(NEGATE 0 V OR ("nr") OR ("nummar") OR ("s"));
	#(NEGATE -1C (@>N) LINK NEGATE *1 N BARRIER NOT-NP);
	## Golbma čáppa, doaimmalaš nieidda livžo áhčiset riegádanbeaivve.
	
#SELECT:QUANr2136 Gen IF (-1 (@>N) LINK -1C Num LINK NOT 0 OKTA OR Ord OR (Num A) LINK NOT -1 ("jahki"))(0 (@Num<))(NEGATE 0 V OR ("nr") OR ("nummar") OR ("s"));
	
	 
#SELECT:QUANr2137 Gen IF (*-1C Num BARRIER NOT-NPMOD OR Gen OR CC OR PUNCT OR REAL-CLB OR Ord LINK NOT 0 OKTA OR Ord OR (Num A) LINK -1 ("jahki") LINK -1 (PrfPrc @>N) OR (A @>N) OR (Dem @>N))(0 (@Num<))(NEGATE 0 V OR ("nr") OR ("nummar") OR ("s"));
	## Son mávssii mannan jagi 43 ruvnnu kilos.
	 
  
#SELECT:QUANr2138 Gen IF (*-1 (Num Sg) BARRIER NOT-NPMOD OR Gen OR PUNCT OR REAL-CLB LINK NOT 0 OKTA OR Ord LINK *-1 REAL-COPULAS BARRIER NOT-ADV-PCLE)(NEGATE *1 TRANS-V BARRIER SV-BOUNDARY OR CC);
	## Joavkkus leat vihtta oahppi Kárášjogas.

#SELECT:QUANr2139 Gen IF (*-1 Num BARRIER NOT-NPMOD OR Gen OR PUNCT OR REAL-CLB LINK NOT 0 OKTA OR Ord LINK -1 CC OR CLB LINK *-1 (Num Nom) BARRIER NOT-NP)(NEGATE 0 Num LINK -1 CC OR CLB);
	## Joavkkus leat vihtta oahppi Kárášjogas, vihtta oahppi Muosáhis ja vihtta Bearalvágis.
 
#!! * **QUANgenCoord** Selects Gen for coordinated complement of a numeral
SELECT:QUANgenCoord Gen IF (-1 JA/DAHJE)(-2 Cmp/SplitR)(*-3 NUM + Acc BARRIER NOT-NPMOD OR PUNCT)(NEGATE 0 NUM);
	#$ Máŋga sámi dearvvašvuođa- ja sosiálabargi vásihit dan. 

#!! * **QUANgen1** Selects Gen if a numeral with Nom-case to the left and 3Pl-verb to the right
SELECT:QUANgen1 Gen IF (*-1 (Num Nom) BARRIER NOT-NP OR CLB)(NEGATE -1 CC)(*1 PL3-V BARRIER NOT-ADV-PCLE)(NEGATE 0 A);
	#Comment: Subject-numerals
	#$ Máŋga Romssa universitehta dutkanbirrasa leat ásahan ovttasbargguid...
	#$ Máŋga min sámegielat máná ledje beallegielagat.
	#% Midjiide ledje guokte smávva lohkanlanja maid ovdal min boahtima geavahedje oađđinlatnjan. 

#!! * **QUANr2142** Selects Gen if a numeral to the left and genitive to the right. Transitive verb not allowed to the left.
#SELECT:QUANr2142 Gen IF (*-1 NUM BARRIER CC OR CLB OR NOT-NPMOD OR PUNCT OR ("ruvdnu") OR HEADOFPARTS LINK 0 Nom LINK NOT -1 ABBR OR CLB)(NEGATE *-1 (Num Acc) BARRIER NOT-NPMOD LINK *-1 Nom BARRIER S-BOUNDARY OR CC)(NEGATE *-1 TRANS-V BARRIER SV-BOUNDARY)(*1 Gen BARRIER NOT-NPMOD);
	#$ Máŋga Romssa universitehta dutkanbirrasa leat ásahan ovttasbargguid...
	#% Ráhkkásiiddán, allet vajáldahte ahte Hearrái lea okta beaivi dego duhát jagi ja duhát jagi dego okta beaivi.
	
	
	
	
#!! ### Leftover accusatives

REMOVE:AccErrSub Acc + Err/Orth OR Acc + Err/Orth-spes (0 Nom) ;

#!! ***COMPInfAcc** Selects Acc if you are Gen and to the left is an Inf TV @COMP-CS<
SELECT:COMPInfAcc Acc IF (0 Gen LINK *-1 (Inf TV @COMP-CS<) BARRIER SV-BOUNDARY) ;
	#$ Ja de son riegádahtii máná jođidettiin, ja de ii lean iežá dorvu go coggat máná ohcii ja vuojehit ein bohccuid dassážii go ollejit dohko gosa gođiid leat dahkame.
	#$ Ja de son riegádahtii máná jođidettiin, ja de ii lean iežá dorvu go coggat ohcii máná ja vuojehit ein bohccuid dassážii go ollejit dohko gosa gođiid leat dahkame.


	# Acc, Gen or Nom; in front of Inf
 
#!! * **NomInf** Selects Nom
SELECT:NomInf Nom IF (-1 FAUXV LINK *-1 Acc)(NOT 0 Adv)(1 FMAINV + TV) ;
	#$ Muhto sámediggepresideantta ii hálit giellaguovddášjođiheaddji cuiggodit dan dihtii go presideanta ii sámástan ođđajagisártnistis.
	
#!! * **NomInf** Selects Nom
SELECT:NomInf Nom IF (*-1 BOS OR COPULAS + FMAINV BARRIER NOT-NPMOD)(0 MIELLA LINK 1 Inf) ;
	#$ – Stuora gudni oažžut dán bálkkašumi ❡ .

  
#!! * **AccInf2** Selects Acc if Inf immediately to the RIGHT *guliid čoallut*
SELECT:AccInf2 Acc IF (*1C TRANS-V + Inf OR (V TV PrfPrc) - (@>N) OR (V Actio Ess) BARRIER NOT-ADV-PCLE LINK NEGATE *1 Acc BARRIER NOT-Attr)(NEGATE 0 Adv)(NEGATE 0 Prop + Nom)(NEGATE -1 Num)(NEGATE *-1 COPULAS + FMAINV BARRIER SV-BOUNDARY )(NEGATE *-2 Loc BARRIER Nom OR SV-BOUNDARY)(NEGATE -1 ("ja") + @CNP LINK -1 Nom) ;
	#$ Son čájeha midjiide mo galgat guliid čoallut.
	#$ Mun láven basadanlanja bassat juohke bearjadaga.
	#$ Organisašuvnna bláđđi man gohčodit Fokusin muitala ahte gonagas Olav su áiggistis lei bivdán beassat leat servodaga suodjaleaddji.
	#? Gii lea cuigen din báhtarit boahttevaš moarreduomus?
	#? Guohtuma kvalitehta ii leat duššefal das gitta maid oaidná eatnamis, muhto gálgá vuhtiiváldit maiddái bohcco dárbbuid beassat earalágan eatnamiidda.
	#% Jagis 2007 álggii Giellatekno bargat interaktiivvalaš pedagogalaš prográmmaiguin.

#!! * **AccNomCOPconstr** Selects Acc in front of Inf; only if there is no chance for itself beeing Nom
SELECT:AccNomCOPconstr Acc IF (1 TRANS-V - FAUXV)(*-1 (N Nom) BARRIER NOT-NPMODADV LINK *-1 COPULAS + FMAINV BARRIER SV-BOUNDARY LINK *-1 HAB-ACTOR + Loc OR (N Loc) OR (N Nom) BARRIER BOC) ;
	#$ Divššohasas lea vuoigatvuohta iežas journála geahččat.
  
	
#!! * **AccTV4** Selects Acc if transitive mainverb to the left. Lots of restrictions to the right
SELECT:AccTV4 Acc IF (*-1 TRANS-V + FMAINV - ("bargat") BARRIER NOT-NPMODADV OR (":") LINK NOT 0 ("gullat") LINK NEGATE *-1 Acc BARRIER SV-BOUNDARY OR COMMA OR Ger)(NEGATE 0 Ord OR Adv OR Prop OR (N Prop) OR (Pron Dem) OR ("buot"))(NEGATE 0 Num + Sg LINK 1 Gen)(NEGATE 0 (Pron Refl) LINK 1 Ill)(NEGATE 0 (Pron Pers) LINK 1 (Pron Refl))(NEGATE 0 (Pron Refl) LINK 1 Com)(NEGATE 0 TITLE LINK 1 Prop)(NEGATE -1 Num LINK NOT 0 (Num Pl Acc))(NEGATE -1 (Pron Dem Nom))(NEGATE 1 Po OR Acc OR Ill)(NEGATE 1 Num OR Nom OR Ger)(NEGATE 0 Gen OR Nom LINK *-1 VERBAL-ACTIVITY-V OR HUMAN-AGENT-V BARRIER NOT-NPMOD)(NEGATE -1 COMMA LINK -1C Gen)
; 
	#$ Alimusriekti ii badjelgeahččange sápmelaččaid nu sakka go vuolit instánssa duopmu.
	#$ Silje lasiha 3-0 nuppi vuorus.
	#% Dáid doaibmabijuid viidáset ovdáneami, earret eará ođasmahttit sámegieloahppomateriála, sáhttá áimmahuššat universitehta bušeahttarámmaid siskkobealde.
	#% Leat maid bargame dálá boazodoallolága ođastemiin.
	#% Goalmmáda son guđii viesu seainni ovddabeallái čohkkát.


# Pron Rel

#!! * **AccPronRel** Selects (Pron Rel Acc) when a secure Acc or Nom to the left *gáibidedje internáhttaskuvlla man*
SELECT:AccPronRel (Pron Rel Acc)  IF ((-1C Acc OR Nom) OR (-1 COMMA LINK -1C Acc OR Nom))(0 ("mii"))(NEGATE *2 ("leat") + FMAINV BARRIER SV-BOUNDARY)(NEGATE 1 Nom LINK *2 Der/Pass BARRIER SV-BOUNDARY) ;
	#$ Lullisámit gáibidedje internáhttaskuvlla man stáhta jođihii.
	#$ Oahppit lohke jitnosit čálabáikkiid maid ledje bajil oahppan.
	#$ Dan čájehit guorahallamat maid Telemarksforskning lea čađahan Sámedikki ovddas.
	#% Son lea čállán mánáidgirjji man namma lea Mellet.
	#% Stáhta nammadan láhttu ja sadjásašláhttu galgaba deavdit gáibádusaid, maid Lahtut ja sadjásašláhtut nammaduvvojit gitta njealji jagi rádjai. #Odd sentence btw..
	
#!! * **AccPronRel2** Selects (Pron Rel Acc) when somewhere in the sentence is a Nom (barrier is sv-boundary), but only if leat isn't the main verb. *geaid eamiálbmogat* 
SELECT:AccPronRel2 (Pron Rel Acc) IF (*1 Nom BARRIER NOT-NPMOD LINK *1 TRANS-V + FMAINV BARRIER SV-BOUNDARY OR Acc LINK NEGATE *1 Acc BARRIER S-BOUNDARY OR COMMA) ;
	#$ Genadutkit geaid eamiálbmogat miehtá máilmmi leat vuosttaldan garrasit, leat dál jođus Sápmái, ja sii ohcet sámi dievdduid genadutkamii.
	#$ Juohke gielddas ožžot olbmot skoviid maid sii galget deavdit ja sii jearahallet maiddái olbmuid, muhto Isaksen imašta kommišuvnna bargovugiid.
	#$ Eadni dal ieš čohkká ommangáttis ja boraha uhca nieiddažis gean son doallá askkistis.
	
#!! * **AccPronRel3** Selects Acc if there is a (Pron Rel Nom) to the right. Obs: not hit nominatives, hence negations. *eanu mii šealgá*	
SELECT:AccPronRel3 Acc IF (0 (N Acc))(1C (Pron Rel) + Nom OR MO)(NEGATE *-1 BOS BARRIER NOT-NPMODADV)(NEGATE *-1 ("leat") + FMAINV BARRIER SV-BOUNDARY)(NEGATE 0 Prop)(NEGATE -1 Pr); 
	#$ De eŋgel čájehii munnje eallima čázi eanu mii šealgá dego kristálla..
	
#!! * **AccActioLoc** Selects Acc when transitive Actio Loc somewhere in the sentence *guldeleames muitalusaid*
SELECT:AccActioLoc Acc IF (*0 (TV Actio Loc) BARRIER S-BOUNDARY OR COMMA OR TRANS-V + FMAINV OR Inf OR Acc LINK NOT -1 Acc)(NEGATE 0 Num) ;
	#$ Son ii váiban goassege guldaleames áhčis muitalusaid.
	#$ Sii jáhkke ahte eatnigiela geavaheapmi hehtte oahppamis riikka váldogiela ja nu muđuige oahpahusa.
	#$ Lávdegoddi lea dasa lassin guorahallan báikkálaš riekteáddejumiid Finnmárkkus ja vuordimis áigu dán ilbmadit iežas boahtte čielggadeami mielddusin.
	#% Láhkaásahus gieldá earret eará jeahkáliid viežžamis Nordfjella ja Selbu guovllus.

SELECT:AccRel Acc IF (0 Rel LINK *1 STRICT-TRANS-V BARRIER SV-BOUNDARY) ;
	#$ Johan Ailo Kalstad muitala ahte son ávžžuhuvvui ohcat, muhto ii son lean dat áidna gean ledje ávžžuhan ohcat virgái.

	# OTHER

#!! * **AccAhte** Selects Acc when *ahte* is found to the right 
SELECT:AccAhte (N Acc) IF (0 Acc)(1 ("ahte"))(NEGATE *-1 BOC BARRIER NOT-Attr)(NEGATE *-1 ("leat") + FMAINV BARRIER SV-BOUNDARY)(NEGATE 0 ANIMATE LINK *-1 VERBAL-ACTIVITY-V OR DADJAT OR HUMAN-AGENT-V OR NOT-HUMAN-OBJECT-V BARRIER SV-BOUNDARY)(NEGATE 0 MAIN-ORGANIZATION LINK *-1 ("háliidit") OR ("sihtat") BARRIER SV-BOUNDARY) ;
	#$ Jagi maŋŋil oaččuimet Skuvvanváris gažaldaga ahte áiggošeimmet go fállat skuvllaálgiide dakkár oahpu.
	#% De vástidii Otterbech ahte Norgga sámemišuvnna stivra ii hálidan dan.
	#% Seammás go skuvlla, sámi oahpahusráđi ja Sámi boazodoallohálddáhusa ovddasteaddjit háliidedje ahte sámegielat oahppit ožžot oahpahusa sámegillii, de dajai Eanandoallooahpahusráđi jođiheaddji Ola Nystø ahte LOR bealis leat álo geahččan boazodoalloskuvlla fágaskuvlan boazodoalus.

#!! * **AccAux** Selects Acc if beginning of sentence to the right and aux, not leat,  is to the left. No Acc allowed to the left *láđđi fertejetne oastit*
SELECT:AccAux Acc IF (*-1 BOS BARRIER NOT-NPMOD)(0 (N Gen) OR (N Acc))(1 FAUXV - ("leat") - Neg LINK 1 TRANS-V + Inf LINK NOT 1 Acc)(NEGATE 0 ACTOR OR PLACE OR Rel OR (N ACR))(NEGATE *2 (Der/PassL) OR (Der/PassS) BARRIER SV-BOUNDARY)(NEGATE *1 (N Acc) BARRIER S-BOUNDARY); #(NEGATE *1 ("ahte") BARRIER S-BOUNDARY);
	#$ Dan ášši ferte čilget nállevealaheami ektui sierra.
	#$ Fága galgá ovddidit ovttasbarggus Sámedikkiin ja sámi dearvvašvuođa- ja sosiálabargiiguin.
	#% Divššohasbearráigeahčči galgá dahkat ortnega dovddusin.


#!! * **HabGenAdvl** Removes Acc; in a habitive adverbial construction with Gen, but only if there is no chans of 0 beeing Nom *Dat lea áhči*
REMOVE:HabGenAdvl Acc IF (-2 Nom LINK NEGATE 0 (Pron Rel))(*-1 ("leat") + FMAINV BARRIER SV-BOUNDARY OR Adv LINK NEGATE *1 VFIN OR Nom BARRIER S-BOUNDARY)(0 ANIMATE + Acc OR Gen LINK NOT 0 Nom)(1 EOS)(NEGATE 0 Adv)(NEGATE 1 Gen LINK 1 Po)(NEGATE *-3 @CVP BARRIER S-BOUNDARY) ;
	#$ Dat lea áhči.
	#% Norgga sámegielat-dárogielat guovlluin skuvl aovddastii dušše ovtta kultuvrra, ja dat lei dáru. 
	#Comment: Naba? Fanas, mii lea gáttis, lea mu.

#!! * **AccIll** Selects Acc if a strict transitive verb is found to the left and Ill to your right. You are not allowed to be a possible modifier of ill: Pron, Px. *buktán heasttaid meahccái*
SELECT:AccIll Acc IF (*-1 STRICT-TRANS-V + FMAINV BARRIER SV-BOUNDARY OR Acc)(1 Ill)(NEGATE *1 Acc BARRIER S-BOUNDARY OR COMMA)(NEGATE 0 Pron OR Px OR ABBR)(NEGATE -1 Gen)(NEGATE 0 Sem/Fem OR Sem/Mal + Nom OR Sem/Sur + Nom LINK *-1 VERBAL-ACTIVITY-V OR DADJAT OR HUMAN-AGENT-V BARRIER SV-BOUNDARY LINK *2 ("ahte") OR EOC) ; 
	#$ Leimmet buktán heasttaid meahccái, goivon ollu dávviriid eatnamii ja vuodján fatnasiid Detnui.
	#$ Mu váhnemat eaba nagodan doallat mánáid gymnásii ja dalle ii ožžon stipeandda iige loana dan dássái.
	#$ Mánáid orrun ja ovttastallan áhkuin ja ádjáin attii sudnuide eambo vejolašvuođaid giellaovdáneami ja vejolašvuođa oahppat eará doahpagiid sámegillii.
	#$ Lotnolasdiimmuin juhken olles joavkku guovtti oassái, 7. jahkeceahkki lei sierra.
 

 
 # GERUNDIUM
 
#!! * **Gerundium0** Selects Acc as the complement of Ger
SELECT:Gerundium0 Acc (*-1 (V TV Ger) BARRIER SV-BOUNDARY OR COMMA - @CNP)(0 Acc OR Gen)(NEGATE 0 (Pron Dem) LINK *1 N BARRIER NOT-Attr)(NEGATE 1 Acc); 
	#% Sus mun jerren ráđi go mun iežan barggus deaivvadin sámegiel hálli olbmuiguin.
	#% Nu ožžot sámi beroštumit mat ovddastuvvojit stuorát deattu deaivvadettiin riikkalaš ja stuorraservodaga beroštumiiguin.

#!! * **Gerundium1** Removes Gen if no other object available for the preceding tv-verb
REMOVE:Gerundium1 Gen (*-1 TRANS-V + VFIN BARRIER SV-BOUNDARY OR Acc LINK NEGATE *-1 Acc BARRIER S-BOUNDARY)(0 Acc OR Gen)(1 Ger) ;
	#$ Ja mii loahpaheimmet dán oahpu doaladettiin suollemas ja duođalaš válggaid mas oahppit oassádallet.
	#$ Ja de son riegádahtii máná jođidettiin.
	
	#$ Dálveskuovat godde maiddái julggiid váccidettiin.
	#$ De mun ohppen sin áddestaladettiin máttanorgga suopmana hállat. Note! Questionable..
	#$ Mun gal ohppen vuođđofága logadettiinan bargodisipliinna ja diekkáriid, maid in goassege lean dárbbašan oahppat oahpaheaddji skuvllas. Note! Questionable..
 
#!! * **Gerundium2** Selects Acc in front of Ger, but not if it is not HAB-ACTOR/Pron Pers. No transitive verb allowed to the left, exept it it has an object of its own.
SELECT:Gerundium2 Acc (0 Acc OR Gen)(1 Ger)(NEGATE 0 HUMAN OR (Pron Pers))(NEGATE *-1 TRANS-V + FMAINV BARRIER Acc LINK NEGATE *-1 Acc BARRIER S-BOUNDARY)  ;
	#$ Konvenšuvdnaevttohusa ráhkadettiin delegašuvnnat leat váldán vuhtii Ruoŧa ođđa boazodoallolága.
	#$ Norgga delegašuvdna, várrepresideanttain, doalai sártni dievasčoahkkimii čoahkkima loahpahettiin, mas doarjjui WHO boahtteáiggi barggu dearvvasvuođain.
	#% Báhpa boradettiin biigá lei bassaladdame.

#!! * **GerundiumTEST** Selects Acc 
SELECT:GerundiumTEST Acc (*-1 TRANS-V + FMAINV BARRIER Acc LINK NEGATE *-1 Acc BARRIER S-BOUNDARY)(0 Acc OR Gen)(1 (V Ger TV) LINK NEGATE *1C Acc BARRIER S-BOUNDARY);
  #!! * **GerundiumTEST** selects Gen for HAB-ACTOR and Pron Pers in front of Ger, but only if there is an Acc belonging to a transitive to the left
#SELECT:Gerundium3 Gen (*-1 Acc OR Gen BARRIER SV-BOUNDARY LINK *-1 TRANS-V + FMAINV BARRIER SV-BOUNDARY)(0 HAB-ACTOR OR (Pron Pers) + Gen)(1 (V TV Ger)) ;
	#$ Son muitalii dan buot olbmuid guladettiin.
	

# Rules needed for:
	#$ Son muitalii dan buot olbmuid guladettiin.	
	#$ Nu ožžot sámi beroštumit mat ovddastuvvojit stuorát deattu deaivvadettiin riikkalaš ja stuorraservodaga beroštumiiguin.
	#$ Vihtta bivdovuogi leat Deanus luosa bivddidettiin.
 
 
 

 
# Accusative or Nominative

##!! * **NomIfProp** Removes Acc and Gen when you Prop because you are Nom. To the left is a sg3-verb. Should not hit Prop that are Sem/Plc.
#REMOVE:NomIfProp Acc OR Gen IF (*-1 SG3-V BARRIER SV-BOUNDARY OR COMMA)
#(NEGATE *-1 Acc OR (A Attr) BARRIER SG3-V LINK *-1 Nom BARRIER SG3-V LINK *-1 SG3-V)
#(0 (N Nom) + Prop OR (N Acc) + Prop)(NEGATE 0 Prop + Sem/Plc)(NEGATE -1 Acc LINK *1 Ill BARRIER NOT-Attr)(NEGATE 0 Gen LINK 1 N - Prop - ABBR LINK *-2 SG3-V LINK -1 Nom) ;
#	#$ Logi minuvtta maŋŋel nagodii Sørøy Glimt čiekči Stig Martinsen dulbet bohtosa ovttain moalain.
#	#% Issát ásai Beer Lahai Roi birrasiin. 
#	#% Doppe Abram čuorvvui Hearrá nama. 
#
##!! * **NomIfProp2** Removes Acc and Nom when you are Prop Sem/Plc because you are Gen. To the left is a sg3-verb. To the right is a noun.
#REMOVE:NomIfProp2 (Nom Sem/Plc) OR (Acc Sem/Plc) (*-1 SG3-V BARRIER SV-BOUNDARY)(NEGATE 0 Sem/Fem OR Sem/Mal OR Sem/Sur)(0 (N Nom) + Prop + Sem/Plc OR (N Acc) + Prop + Sem/Plc)(*1 N BARRIER NOT-Attr) ;
#	#$ Issát ásai Røros birrasiin.
#	#$ Sidjiide lei maid liegga Ruija geasuheaddji guovlu.
#	#$ Vuosttasnaššuvnnaid čoagganeapmi ovddasta badjel 300 indiánaservodaga miehtá Kanada ja gáibida dál iešstivrejumi Davvi-Amerihká buot eamiálbmogiidda.
#
##!! * **NomSentFin** Selects Nom if you are Acc or Gen and EOS is to yoru right. Copulas is found to the left 
#SELECT:NomSentFin1 Nom (*-1 FMAINV + COPULAS BARRIER NOT-NPMOD)(0 Acc OR Gen LINK 1 EOC) ;
#SELECT:NomSentFin2 Nom (-1 VFIN + COPULAS)(0 Acc OR Gen LINK 1 FMAINV + COPULAS LINK 1 EOC) ;
#	#$ Organisašuvnna bláđđi man gohčodit Fokusin muitala ahte gonagas Olav su áiggistis lei bivdán beassat leat servodaga suodjaleaddji.
#
##!! * __jr_sr__ Selects (ABBR Nom) if you are *jr* or *sr* and first one to your left is (Sem/Sur Nom)
#SELECT:jr_sr (ABBR Nom)(0 ("jr") OR ("sr") LINK -1 (Sem/Sur Nom)) ;

#!! ## Accusative before @COMP-CS<
#!! ## Accusative before some A
#!! ## Accusative sentence-finally

#!! ## Genitive

#Not in use?
#!! * **r2143** The most frequent genitive rule: Gen when postpos immediately to the right:
SELECT:r2143 Gen IF (NEGATE 0 Adv OR PrfPrc OR (Num Nom))(1C Po)(NEGATE 1 Px OR ("eret")); 
	#Comment: Iskka doallágo deaivása.




#!! ## Nominative and accusative


#REMOVE:NAr2285 Gen IF (NEGATE *-1 Pr OR NUM BARRIER NOT-NPMOD)(NEGATE 0 TIME)(1 Inf LINK 0 V-TRANS-ACT)(1C REAL-V);
	## Vuoigatvuohta journála geahččat. AccInf2

#REMOVE:NAIfWeWantAcc Gen (0 Acc LINK NOT 0 TIME OR ROUTE LINK 1 Neg LINK *1 TRANS-V BARRIER SV-BOUNDARY);
	## Dáid guohtuneatnamiid ii sáhte dáhkidit sin atnui. topOBJ2

#!! * **NAr2266** Selects Nom
SELECT:NAr2266 Nom IF (-1 BOC)(0 N)(1 Rel)(2 VFIN LINK *1 COMMA BARRIER S-BOUNDARY LINK 1 VFIN LINK *1 NP-HEAD-ACC BARRIER NOT-NPMOD);
	#$ Ja Ipmil guhte suokkarda váimmuid, diehtá maid Vuoigŋa hálida, dasgo Vuoigŋa rohkadallá bassi olbmuid ovddas Ipmila dáhtu mielde. NAr2266

#!! ***NomIFInitialThenSg3** Selects Nom if -1 BOS and 1 oblique / Sg3 lookalike. Works in fragments.
SELECT:NANomIFInitialThenSg3 Nom IF (-1 BOC)(0 (N Acc))(1 ILL-V OR LOC-V OR COM-V LINK 0 SG3-V);
	#$ Liná liikui sárgut ja tennjet. (Liná)

#REMOVE:NAr2267 Gen IF (*-1 BOC BARRIER NOT-NPMOD OR Pl)(0 NP-HEAD-SG LINK 0 Gen)(NEGATE 0 TIME OR ROUTE)(1 (Pron Pers))(*1 VFIN); 
#REMOVE:NAr2268 Gen IF (*-1 BOC BARRIER NOT-NPMOD OR Sg)(0 NP-HEAD-PL LINK 0 Gen)(NEGATE 0 TIME OR ROUTE)(1 (Pron Pers))(*1 VFIN); 
	## Daid mun sáltejin. 
	## ...man mii dan áigge dovdat gonagasriika Norgan.
	## Daid heasttaid son lohká heivet buoremusat deike galbma guovlluide.

#!! * **NAAccEllipsis1** Selects Acc	
SELECT:NAAccEllipsis1 Acc (-1 Nom LINK -1 STRICT-TRANS-V LINK NEGATE *-1 Acc)(1 CS);

#!! * **NAAccEllipsis2** Selects Acc
SELECT:NAAccEllipsis2 Acc (-1 Nom LINK *-1 CS BARRIER NOT-NPMOD LINK -1 Acc)(0 Gen)(1 EOS);
	#$ Ovdal buorida Ipmil dálkkiidis go neavrres olmmoš dábiidis.

#REMOVE:NAr2269 Gen IF (-1 BOC)(1 Adv)(NEGATE 1 N OR A); 
	## Dieđuid maiddái ii dárbbaš addit go...

#REMOVE:NAr2271 Gen IF (*-1 TRANS-V BARRIER SV-BOUNDARY OR Acc OR COMMA LINK NEGATE *-1 Acc BARRIER SV-BOUNDARY)(NEGATE *-1 Pr OR NUM BARRIER NP-BOUNDARY)(NEGATE -1 ("mii"))(NEGATE *-1 CC BARRIER NOT-NP LINK *-1 GASKAL BARRIER S-BOUNDARY)(NEGATE 0 TIME OR ROUTE OR Refl)(1 Ill OR Loc OR Com OR Adv LINK NEGATE *1 Acc BARRIER TRANS-V)(NEGATE *2 REAL-V BARRIER S-BOUNDARY)(NEGATE *-1 HEADOFPARTS LINK 1 Gen LINK 1 Loc);
	## Dalle Jesus moaitigođii gávpogiid gos son lei dahkan eanas fámolaš daguidis.
	
#REMOVE:NAr2272 Gen IF (*-1 TRANS-V BARRIER SV-BOUNDARY OR Acc LINK NEGATE *-1 Acc BARRIER NOT-ADV-PCLE)(NEGATE *-1 Pr OR NUM BARRIER NP-BOUNDARY)(NEGATE -1 ("mii"))(NEGATE *-1 CC BARRIER NOT-NP LINK *-1 GASKAL BARRIER S-BOUNDARY)(NEGATE 0 TIME OR ROUTE)(1 Ill OR Loc OR Com OR Adv LINK *1 Gen BARRIER REAL-V LINK 1 Ill OR Loc OR Com)(NEGATE *-1 HEADOFPARTS LINK 1 Gen LINK 1 Loc);
	## Kripos lea dutkan jápminášši vuonas veahkkin NuortaFinnmárkku politiijaide.

#REMOVE:NAr2273 Gen IF (-1 (Dem Acc) LINK *-1 TRANS-V BARRIER SV-BOUNDARY OR Acc)(NEGATE *-1 Pr OR NUM BARRIER NP-BOUNDARY)(NEGATE -1 ("mii"))(NEGATE *-1 CC BARRIER NOT-NP LINK *-1 GASKAL BARRIER S-BOUNDARY)(NEGATE 0 TIME OR ROUTE)(1 Ill OR Loc OR Com OR Adv LINK NEGATE *1 Acc BARRIER TRANS-V)(NEGATE *-1 HEADOFPARTS LINK 1 Gen LINK 1 Loc);
	## Dalle Jesus moaitigođii daid gávpogiid gos son lei dahkan eanas fámolaš daguidis.

#REMOVE:NAr2274 Gen IF (*-1 go BARRIER Acc LINK -1 TRANS-V)(0 Acc)(1 REAL-CLB);	
	## Eai lean váldán go niestebohcco.
    ## Son gádju min eret min vašálaččain. # It shouldn´t hit this one.

#REMOVE:NAr2275 Gen IF (*-1 V-TRANS-ACT BARRIER SV-BOUNDARY)(*-1 Acc BARRIER V-TRANS-ACT LINK 1 CC)(NEGATE *-1 Pr OR NUM BARRIER NP-BOUNDARY)(NEGATE -1 ("mii"))(NEGATE *-1 CC BARRIER NOT-NP LINK *-1 GASKAL BARRIER S-BOUNDARY)(NEGATE 0 TIME OR ROUTE)(1 Ill OR Loc OR Com LINK NEGATE *1 Acc BARRIER TRANS-V)(NEGATE *-1 HEADOFPARTS LINK 1 Gen LINK 1 Loc);	
	## Mun vuovddán gusa ja áhči beatnaga oahpaheaddjái. 

#REMOVE:NAr2276 Gen IF (NEGATE *-1 Pr OR NUM BARRIER NOT-NPMOD)(0 N)(1 Dem);
	## Jahkásaš bušeahttarámma dán prošeavtta doaimmaide gártá oktiibuot 70.000 ruvnno. 

#REMOVE:NAr2277 Gen IF (NEGATE *-1 Pr OR NUM BARRIER NOT-NPMOD)(1 SP-BEAIVI);
	## Sii bivdet luosa mánnodaga.

#REMOVE:NAr2278 Gen IF (*-1 OPRED-V BARRIER NOT-NPMOD LINK NOT 0 N OR Der/Pass LINK NEGATE *-1 NP-HEAD-ACC BARRIER SV-BOUNDARY)(1 Ess);
	#$ Don galggat gohčodit su Jesusin.
	#$ Máhttodepartemeanta lea nammadan su stivrajođiheaddjin.

#REMOVE:NAr2279 Gen IF (*-1 TRANS-V BARRIER S-BOUNDARY OR Acc)(1 ("§"));
	## Vuoigatvuohta oažžut dearvvašvuođaveahki § 2-2.
 # This rule would work fine if we could make an earlier rule select Gen 
 # for all occurrences of compounds in -lága in front of§. 

#REMOVE:NAr2280 Gen IF (NEGATE *-1 Pr BARRIER NP-BOUNDARY)(0 Prop + Nom)(1 CLB)(NEGATE 2 Prop + Gen);
	## Gažaldat áirasis Egil Olli, Bargiidbellodaga sámediggejoavku.

#!! * **r2281** marginal
#REMOVE:NAr2281 Gen IF (*-1 VFIN BARRIER NOT-ADV-PCLE)(0 N)(1 CC)(2 (Pron Sg3 Gen));
	## Váldde máná ja su eatni fárrui.

#REMOVE:NAr2283 Gen IF (NEGATE 0 Dem)(*1 ("hávvi" N Sg Gen) BARRIER NOT-NPMOD);
	## ..geasa dearvvaávuođabálvalus addá dahje fallá veahki juohke áidna háve...

#REMOVE:NAr2284 Gen IF (NEGATE *-1 Pr OR NUM BARRIER NOT-NPMOD)(NEGATE 0 TIME OR Num)(1 CLB)(2 CC);
	## Dat váikkuha ollu dasa, mo olbmot vásihit bálválusaid, ja oažžu sin dovdat stuorát oadjebasvuođa.

#!! * **NAr2288** Removes Nom
REMOVE:NAr2288 Nom IF (*-1 Acc BARRIER SV-BOUNDARY LINK *-1 ACC-INF-V BARRIER SV-BOUNDARY OR PUNCT)(NEGATE *-1 COPULAS BARRIER NOT-ADV-PCLE)(NEGATE *-1 (Interr Acc) BARRIER Acc);
	#$ Hearrá, suova mu vuos mannat ruoktot hávdádit áhččán.

#REMOVE:NAr2290 Acc IF (NEGATE *-1 TRANS-V BARRIER SV-BOUNDARY)(NEGATE *-1 CS BARRIER V LINK 1 @COMP-CS<)(NEGATE *-1 (":") BARRIER TRANS-V)(NEGATE *-1 OLLU BARRIER NOT-NPMOD)(NEGATE 0 TIME OR Dem OR Num)(NEGATE 0 NUM LINK *1 TIME OR MEASURE BARRIER NOT-A)(NEGATE 0 ("dat") LINK 1 CS)(NEGATE *1 TRANS-V BARRIER S-BOUNDARY)(NEGATE 0 ABSTR-ROUTE)(NEGATE 1 V-TRANS-ACT + Loc)(NEGATE 0 MEASURE)(NEGATE 1 EOS OR COMMA)(NEGATE 1 (Pron Rel));
	## Sámedikkis lea leamaš čoahkkin 14.03.01.
	## Leago dudno mielas suohtas go sáhttá rabas julggiid vázzit?

SELECT:acc Acc IF (0 Nom LINK NOT 0 HUMAN LINK 1 STRICT-TRANS-V LINK 1 EOS)(NEGATE -1 Acc) ; #apertium krever NOT 0 HUMAN, 
	#% Sámediggeráđđi oaivvilda ahte mánnái lea buoremus maiddái sin kultuvrralaš ja gielalaš identitehta gozihit.


#!! ## Nominative


	# Nominative
	# ==========
	
	# Miscellaneous rules
	# Vocatives, subjects of sentence fragments
	# Nominative in titles and sentence fragments
	# Nominative after "go", "dego", "dugo" and "nugo"
	# Nominative subjects
		# Preverbal subjects
		# Postverbal subjects
	# Nominative predicatives
	# Nominative as objects in existential clauses
	# Nominative in coordination and apposition
	# Nominative in parallell constructions
	# Not nominative

#!! ### Miscellaneous rules
	# --------------------
#!! * **NDnom** Selects Nom
SELECT:NDnom Nom IF (0 Coll + Loc LINK *0 Du3) ;
	#$ Bođežeaba go Ánne-guovttos ovdal gaskaija?


SELECT:NDnom Coll IF (0 Loc LINK *0 Pl1) ;
        #$ Mii vuolgit njealjis biillain Agrai.

#!! * **NDr2300** Selects Nom if Gen immediately to the left. You are N-SG-NOM and to your right is SG3-V *Du ášši lea dehálaš*
SELECT:NDr2300 Nom IF ((-1 Gen) OR (-1 Po LINK -1 Gen) OR (-1 Adv LINK -1 Gen) OR  (-1 Adv LINK -1 Po LINK -1 Gen))(NEGATE -1 (Pron Dem Gen))(0 N-SG-NOM LINK NOT 0 Loc)(NEGATE 0 PrfPrc OR Ill OR Po)(1 SG3-V);
	#$ Du ášši lea dehalaš.
	#$ Dan ášši ferte dutkat.
	#$ Miššonbarggu álggu rájes juo Leem háliidii oahppat sámegiela ja sámi jurddašanvuogi.
 #Comment: HMM, brave.. - but it seems to work!
 #Commet: Note that the tag is selected, the set is in the condition.

#!! * **NDr2302** Selects Nom if immediately to the left is "ruvdno" and to the left of it is Num *70 ruvnno mehtar*
SELECT:NDr2302 Nom IF (-2 Num)(-1 ("ruvdno"));  # 0-3
	#$ Láđđi máksá 70 ruvnno mehtar.

#!! * **NDr2304** Selects Nom for (Num Sg Loc) if to the left is a spesific word and to the right is EOC 	
SELECT:NDr2304 (Num Sg Loc) IF (-1 ("álgit") OR ("boahtit") OR ("vuolgit") OR ("diibmu") OR ("biellu"))(1 EOC); # 0-3
	#$ Filbma álgá čiežas.
	
#!! * **NDr2305** Selects Nom for (Coll Nom) if to the left is (Pers Pl Nom) *mii golmmas*
SELECT:NDr2305 (Coll Nom) IF (-1 (Pers Nom))(-1 Pl1 OR Pl2 OR Pl3); # 0-2
	#$ De mii golmmas leansmánniin manaimet geahččat.

SELECT:CollNomEllipse (Coll Nom) IF (*-1 (Coll Nom) BARRIER Nom) ;
	#$ Guovttis bohte Finnmárkkus, guovttis Romssas, golmmas Nordlánddas, okta Davvi-Trøndelágas ja okta Lulli-Trøndelágas.

#!! * **NDr2306** Selects Nom for (N Nom) if to the left is "okta" or "nubbi" *okta lihtter*
SELECT:NDr2306 (N Nom) IF (*-1 OKTA + Nom OR ("nubbi" Nom) BARRIER NOT-NPMOD LINK NOT 0 Foc/ge);
	#$ Málli vuššojuvvui okta lihtter.


	# Nominative if two or more names in a row

#!! * **NDr2308** Selects Nom for PROP asdf 11231
SELECT:NDr2308 Nom IF (*-1 PROP OR (\?) BARRIER REAL-WORD-NOT-ABBR)(0 PROP)(NEGATE 0 Gen LINK 1 N)(*1 SG3-V);  # Uten barriere, men vi satser på at evt akkusativ allerede er valgt.
	#$ Birasgáhttenminister Siri Bjerke dajai čielgasit ahte...
	#$ Rolf Morten Anti Amundsen, dahje Rolffa nugo eatnašat dovdet su, lea musihkar gii vuittii dán jagáš Sami Music Awards komponistabálkkašumi.
 
 
 
#!! ### Vocatives, subjects of sentence fragments
	# - - - - - - - - - - - - - - - - - - - - - 
#!! * **NDr2309** Selects Nom
SELECT:NDr2309 Nom IF (*-1 BOS OR (":") BARRIER NOT-A)(0 Prop)(1 COMMA); 
	#$ Biera, du sabehiid!
	#$ Jovsset, Dávveda Bárdni!

#!! * **NDr2310** Selects Nom	
SELECT:NDr2310 Nom IF (NEGATE -2 HUMAN-INDIVIDUAL)(-1 COMMA)(0 Prop)(1 EOS); 
	#$ Boađe deike, Duommá!

#!! * **NDr2311** Selects Nom
SELECT:NDr2311 (Sg Nom) IF (*-1 MUN OR DON OR (Pron Sg3) BARRIER NOT-A LINK *-1 BOS OR (":") BARRIER NOT-A-CC)(1 EOS); # 3-0
	#$ Son celkkii: Don jalla!

#!! * **NDr2312** Selects Nom
SELECT:NDr2312 (Pl Nom) IF (*-1 MII-PERS OR DII OR SII BARRIER NOT-NPMODADV LINK *-1 BOS OR CLB BARRIER NOT-A-CC)(1 EOS); # 0-2
	#$ Dii guoktilaččat!

#!! * **NDr2313** Selects Nom
SELECT:NDr2313 (A Pl Nom) IF (-2 MII-PERS OR DII OR SII LINK *-1 BOS OR CLB BARRIER NOT-A-CC)(-1 Adv OR Indef)(1 EOS); # 0-3
	#$ Dii uhccán oskkolaččat!

#!! * **NDr2314** Selects Nom
SELECT:NDr2314 Nom IF (*-1 BOS OR PUNCT BARRIER NOT-NPMOD)(1 COMMA)(2 Imprt);
	#$ Sáhtán, gáidda eret!

#!! * **NDr2315** Selects Nom
SELECT:NDr2315 Nom IF (-1 BOS OR PUNCT OR REAL-CLB)(1 COMMA);
	#$ Oahpaheaddji, mun áiggun čuovvut du.


#!! ### Nominative in titles and sentence fragments
	# - - - - - - - - - - - - - - - - - - - - - -

	# A single word is nominative
#!! * **NDr2317** Selects Nom: A single word is nominative
SELECT:NDr2317 Nom IF (-1 BOS OR (":"))(NEGATE 0 VFIN)(0 N OR A OR Pron)(1 EOS); 
	#$ Katja.
	#$ Bálvaleaddjit manne dalle isideaset lusa ja jerre: Hearrá! 


	# A single word with a numeral in front of it is nominative
#!! * **NDr2318** Selects Nom: A single word with a numeral in front of it is nominative
SELECT:NDr2318 Nom IF (-2 BOS)(-1 Num)(1 EOS); 
	#$

	# An NP head with a genitive modifier is nominative
#!! * **NDr2319** Selects Nom: An NP head with a genitive modifier is nominative 
SELECT:NDr2319 Nom IF (-2 BOS)(-1 Gen)(0C N)(1 EOS);  #OBS
	#$

	# A title is nominative if it has a Nom reading at all.
#!! * **NDr2320** Selects Nom: A title is nominative if it has a Nom reading at all
SELECT:NDr2320 Nom IF (0 TITLE OR N + Sem/Hum LINK NOT 0 ABBR LINK 1 Sem/Fem OR Sem/Mal OR Sem/Sur LINK 0 Ill OR Loc OR Com);
	#$ Stuorra giitu maiddái professor Birger Stuevold Lassenii. # in this case the title of a person is meant, not a sentence fragment
 
	# An NP head with an Attr modifier is nominative.
#!! * **NDr2321** Selects Nom: An NP head with an Attr modifier is nominative
SELECT:NDr2321 Nom IF (-2 BOS)(-1 Attr)(1 EOS);
	#$

REMOVE:ipmi Foc/ba IF (0 FAMILY + Foc/ba LINK 0 FAMILY - Foc/ba) ;
	#$ Marit Kristine juoigá iežas ipmiba Nils Ante Triumf.


	# only propernouns are nominative
#!! * **onlyProp** Selects Nom
SELECT:onlyProp Prop + Nom IF (*-1 BOS BARRIER WORD - Prop - PROFESSION) (NEGATE *1 WORD - Prop - Arab - Sem/Year) ;
#SELECT:onlyProp Prop + Nom IF (*-1 BOS BARRIER WORD - Prop) (*1 EOS OR PUNCT BARRIER WORD - Prop - Arab - Sem/Year) ;
SELECT:onlyProp Prop + Nom IF (-1*  (":") OR LEFT BARRIER WORD - Prop) (*1 PUNCT BARRIER WORD - Prop - Arab - Sem/Year) ;
	#$ Paul Fjellheim, Røros 2003 (Govva: Basia Głowacka).

SELECT:ProfNom1 Nom (0 PROFESSION) (*1C Prop + Nom BARRIER WORD - Prop) ;
SELECT:ProfNom2 Nom (0 PROP-SUR) (-1C PROFESSION + Nom) ;

	# A propernoun followed by colon and new word is nominative	
#!! * **nomAuthor**
SELECT:nomAuthor Prop + Nom IF (1 (":"))(2 ("<[A-ZÁČŠ](.*)>"r));
	## Eino Koponen: Gielladikšuma hástalusat


	# A Gen series is broken by an intermediate Prop

#!! ### Nominative after "go", "dego", "dugo" and "nugo"
	# - - - - - - - - - - - - - - - - - - - - -
	
#!! * **NDr2324** Selects Nom
SELECT:NDr2324 Nom IF (*-1 go BARRIER NOT-NPMOD LINK NOT -1 VFIN)(*-1 COPULAS BARRIER NOT-AUX-V OR CS)(NEGATE 1 N OR A);
	#$ Jesus lea gievrrat go Sáhtán ja bahá vuoiŋŋat.

#!! * **NDr2325** Selects Nom
SELECT:NDr2325 Nom IF (*-1 go BARRIER NOT-NP LINK NOT -1 VFIN)(NEGATE 0 Acc OR VFIN)(1 EOC);
	#$ Ii leat almmustuvvan oktage stuorit go gásttašeaddji Johanas.

#!! * **NDr2326** Selects Nom
SELECT:NDr2326 Nom IF (*-1 XGO BARRIER NOT-NPMOD)(NEGATE 0 Attr LINK *1 N BARRIER NOT-NPMOD)(*1 NP-BOUNDARY OR CLB BARRIER WORD);
	#$ Su ámadadju báittii dego beaivváš.

#!! * **NDr2327** Selects Nom
SELECT:NDr2327 Nom IF (-2 XGO)(-1 Gen OR PrsPrc)(1 NP-BOUNDARY);
	#$ ...johtá dego gillju ledjon ozadettiin muhtuma... Comment: We are supposed to have full sentences without the "..." so we can check it with smedist whenever needed!

	#$ ...dego vuonccis čohkke čivggaidis soajáidis vuollái. Comment: We are supposed to have full sentences without the "..." so we can check it with smedist whenever needed!

#!! * **NumNomgo** Selects (Num Nom)
SELECT:NumNomgo (Num Nom) IF (-1 go LINK -1 Comp LINK NEGATE *-1 TRANS-V BARRIER NOT-ADV-PCLE) ;
	#$ ...go heaittihanáigemearri lea oanehat go vihtta jagi. Comment: We are supposed to have full sentences without the "..." so we can check it with smedist whenever needed!
	
#!! * **NumAccgo** Selects (Num Acc)
SELECT:NumAccgo (Num Acc) IF (-1 go LINK -1 Comp LINK *-1 TRANS-V BARRIER NOT-ADV-PCLE) ;
	#$ 
	 
	# Nominative of "nummar" preceding a cardinal
	# - - - - - - - - - - - - - - - - - - - - - - - - - - - - - 

	# Nominative subjects
	# -------------------

#!! ### Preverbal subjects
	# - - - - - - - - - -
	# The noun is very seldon, if it as at all.


#!! * **relNomVfin** Selects (N Nom)
SELECT:relNomVfin N + Nom IF (*-1 Rel - Nom BARRIER NOT-NPMODADV LINK -1 LEFT)(0 Sg LINK *1 VFIN + Sg3 BARRIER SV-BOUNDARY LINK 1 RIGHT);
	#$ Lea maid ágga čujuhit ahte vaikke vissis gáldus (masa advokáhta Enoksen čujuha) ii leat válddálaš namuhit dan.

#!! * **NDr2331** Selects (N Nom)
SELECT:NDr2331 (N Nom) IF (*-1 (Dem Nom) BARRIER NOT-A)(NEGATE 0 ("leapma" Der/Dimin) OR Px);
	#$ Noađđespagáin leat dat geažit guokte golbma tumá guhku.
	#$ ...ja ahte dat dáhttu čielgasit berre doahttaluvvot. 

#!! * **NDr2332** Selects (Num Nom)
SELECT:NDr2332 (Num Nom) IF (-1 (Dem Sg Nom));
	#$ Dát guokte ášši Siviilaáittardeaddji geavadis leat váldon...

#!! * **NDr2333** Selects (Num Nom)	
SELECT:NDr2333 (Num Nom) IF (*1 TIME + Gen BARRIER NOT-A LINK *1 ("leat") BARRIER N LINK *1 Nom BARRIER NOT-NPMOD) ;
	#$ Vuosttaš logi minuvtta lei buorre áigodat Nordlysa ektui.

#!! * **NDr2334** Selects Nom
SELECT:NDr2334 Nom IF (*-1 BOC OR PUNCT BARRIER NOT-NPMODADV OR (Dem Acc))(0 Sg OR ABBR LINK NOT 0 Ord)
(NEGATE 0 Adv - ("seammá") OR Loc OR Po OR VFIN)(NEGATE 0 Ess LINK *1 Nom BARRIER NOT-NPMOD) 
(NEGATE 0 Ess LINK *1 OPRED-V BARRIER SV-BOUNDARY LINK *0 Nom BARRIER WORD)
(NEGATE 0 Attr LINK *1 N BARRIER NOT-NPMOD) 
((*1C SG3-V BARRIER NOT-ADV-PCLE) OR (*1 (N Com) OR (N Loc) BARRIER WORD - Dem LINK *1C SG3-V BARRIER NOT-ADV-PCLE LINK NEGATE *1 (Sg Nom) BARRIER NOT-NPMOD)) 
(NEGATE 0 Acc LINK *1 TRANS-V BARRIER SV-BOUNDARY LINK NOT 1 Acc) ;
	#$ ...ahte doaimma jođihangoddi vejolaččat mearrida... Comment: We are supposed to have full sentences without the "..." so we can check it with smedist whenever needed!
	#$ Almmolaš sámi ságastallan lea vejolaš.
	#% Dán artihkkalis mun čájehan ahte málle sáhttá geavahit maiddái unnitlogugiela čálamáhtu konteavsttaid govvideamis.

SELECT:PropAge Nom IF (0C Prop)(*1 Num BARRIER WORD LINK *1 VFIN + Sg3 BARRIER WORD) ;
	#$ Mathias (15) vázzá áhčis bálgá.



#!! * **NomEss** Selects Nom when not copula
SELECT:NomEss Nom IF (*-1 BOC OR PUNCT BARRIER WORD - Attr)
(0 N)(NEGATE 0 Adv) (NEGATE 0 Ess LINK *1 OPRED-V BARRIER SV-BOUNDARY LINK *0 Nom BARRIER WORD)(NEGATE 0 Attr LINK *1 N BARRIER NOT-NPMOD) (1 Ess);
	#$ Dovddus juoigi váldoartistan.

#!! * **NDr2335** Selects Nom
SELECT:NDr2335 Nom IF (*-1 BOC OR PUNCT BARRIER NOT-NPMODADV OR (Dem Acc))(0 Sg)(NEGATE 0 Adv OR Loc)(NEGATE 0 Ess)(NEGATE 0 Attr LINK *1 N BARRIER NOT-NPMOD)(1 (Num Nom))(*2C SG3-V BARRIER NOT-ADV-PCLE);	
	#$ NOU 1995:6 cealká earret eará nu.

#!! * **NDr2336** selects (N Sg Nom) when 1 SG3-V
SELECT:NDr2336 (N Sg Nom) IF (NEGATE 0 Gen OR Po OR Loc)(*1 SG3-V BARRIER NOT-ADV-PCLE LINK NEGATE 0 N)(NEGATE *2C Nom BARRIER S-BOUNDARY) (NEGATE 0 Ess LINK *-1 Nom BARRIER REAL-V);
	#$ Doaibmaplána árvvoštallan čájeha maiddái dan.
	#% Dan ášši ferte čilget nállevealaheami ektui sierra. # Comment: Topicalised object.
	#% Dainnago sámegiela bissovašvuohta beaivválaš giellan lea boatkanan.

#!! * **NDr2337** Selects (N Sg Nom)	
SELECT:NDr2337 (N Sg Nom) IF (NEGATE 0 Acc)(1 ("oktan") LINK 1 Com)(3 SG3-V)(NEGATE 1 N)(NEGATE *4C Nom BARRIER S-BOUNDARY);
	#$ Ohcan oktan cealkámušaiguin sáddejuvvo boazodoallohálddahusa bokte Eanadoallodepartementii.

#!! * **NDr2338** Selects (N Sg Nom)	
SELECT:NDr2338 (N Sg Nom) IF (NEGATE *-1 (N Sg Nom) BARRIER S-BOUNDARY OR CC OR CLB)(NEGATE 0 Loc)(1 Neg + Sg3);
	#$ Sámi dutkan ii leat dan dihte soaittáhagas válljejuvvon dutkansuorgi.
 
#!! * **NDr2339** Selects (N Sg Nom)	
SELECT:NDr2339 Nom IF (*-1 BOC OR PUNCT BARRIER NOT-NPMODADV)(0 Pl)(NEGATE 0 Adv OR Ill OR VFIN)(*1 PL3-V BARRIER NOT-ADV-PCLE); 
	#$ ...go beaktilis sivat bealuštit dan. Comment: We are supposed to have full sentences without the "..." so we can check it with smedist whenever needed!


	#$ Ruovttus fárren sáhttá čuohcit olbmui sakka.
 #Comment: The idea here is that 0 is not accusative, since there is an ILL-V.
 #Comment: So we could alternativelyREMOVE:ND Acc. But the present rule seems to work.

#!! * **NDr2341** Selects Nom
SELECT:NDr2341 Nom IF (*-1 CS OR BOS OR CC OR CP BARRIER NP-HEAD - ("buot" Pron Indef Attr) OR VFIN)(0 NUM)(*1 PL3-V BARRIER SV-BOUNDARY)(NEGATE *1 Nom BARRIER S-BOUNDARY);
	#$ Guokte jahkásaš doavttergrádakurssa leat lágiduvvon.
	#$ Go máŋggas galget čállit seamma journálii, de... 
 #Comment: In the last example, the reading N Sg Nom is chosen because of the reading 
 #Commet: Num Sg Loc.
 	#$ Buot njeallje máná ledje náđđon eagi vuostá. # Comment: In order for the numeral to be the head in this sentence, I had to add - ("buot") to the BARRIER NP-HEAD which includes pronouns.

#!! * **NDr2341** Selects Nom
SELECT:NDr2342 Nom IF (*-1 CS OR BOS OR CC OR CP BARRIER NP-HEAD OR VFIN)(0 NUM)(*1 COPULAS BARRIER SV-BOUNDARY LINK 0 (Pl3) LINK NEGATE *1 PrfPrc OR Inf BARRIER SV-BOUNDARY);
	#$ Vihtta bivdovuogi leat Deanus luosa bivddidettiin. 

#!! * **NDr2343** Selects (Sg Nom)
SELECT:NDr2343 (Sg Nom) IF (*-1 CS OR BOS BARRIER NOT-ADV-PCLE)(NEGATE 0 Adv OR PrfPrc OR Pl + Nom)(NEGATE 0 Qst)(*1 SG3-V OR DU3-V BARRIER NOT-NP OR Nom OR Ess OR SV-BOUNDARY LINK NEGATE *1 NP-HEAD-SG-NOM BARRIER NOT-NPMODADV)(NEGATE 0 Gen LINK *1 N BARRIER NOT-NPMOD)(NEGATE 0 Ess)(NEGATE 0 Acc LINK *1 TRANS-V BARRIER SV-BOUNDARY LINK NOT 1 Acc) ;
	#$ Mii vuordit ahte stáhta sámi vuoigatvuođaid barggu oktavuođas čuovvu...
	#$ Gánda: "Vuoksá dan jugai."
	#% Dán artihkkalis mun čájehan ahte málle sáhttá geavahit maiddái unnitlogugiela čálamáhtu konteavsttaid govvideamis

#!! * **NDr2345** Selects Nom
SELECT:NDr2345 Nom IF (0 ("galle"))(*1 PL3-V BARRIER Nom OR VFIN);
	#$ ... ja albmada galle 'mearrasámi' ledje vearrováldit.

SELECT:NDr2346 Nom IF (0 Coll)(*1 PL3-V BARRIER NOT-ADV-PCLE);
	#$ Dakkár doaimmat, main máŋggas čállet dieđuid journálaide,...

	#$ Go logi jagi lea vássán maŋimus journálamerkesteami rájes,...

	#$ Man uhccán osku dus lea!

#!! * **NDr2350** Selects Nom
SELECT:NDr2350 Nom IF (0 Sg LINK 1 CC LINK *1 (Sg Nom) BARRIER NOT-NPMOD LINK *1 DU3-V BARRIER NOT-ADV-PCLE);
	#$ Silas ja Timoteus báziiga Bereai.
	#$ Tommi ja Annika dárkilit geahččaba Pippi oaidnin dihte mo diŋggaohccit barget.

#!! * **NDr2351** Selects Nom
SELECT:NDr2351 NP-HEAD-NOM IF (*-1 CC BARRIER NOT-NPMOD LINK -1 NP-HEAD-NOM)(1 PL3-V);
	#$ Árbevierru ja geavat sáhtášedje leat dulkonmomeanttat.
	#Comment: No examples in corpus?

#!! * **NDr2353** Selects Adv
SELECT:NDr2353 Adv IF (*-1 (N Pl Nom) BARRIER S-BOUNDARY OR COPULAS OR ("orrut"))(0 (A Pl Nom));	
	#$ Eamiálbmotlaš áššit jogo systemáhtalaččat dubmejuvvojit menddo siivut...

#!! * **NDr2354** Selects Adv - Outcommented: This rule does not function well
#SELECT:NDr2354 Adv IF (0 (A Pl Nom))(*1 NOT-AUX-V BARRIER S-BOUNDARY)(*1 (N Pl Nom) BARRIER S-BOUNDARY)(NEGATE 1 CC OR COMMA LINK *1 N OR A BARRIER NOT-NPMOD);
	#$ Nappo leat gažaldagas historjjálaččat háhkkojuvvon vuoigatvuođat.

#!! * **NDr2355** Selects Adv
SELECT:NDr2355 Adv IF (-1 BOC)(0 (A Pl Nom))(NEGATE 1 CLB)(NEGATE *1 DU3-V OR PL3-V BARRIER SV-BOUNDARY);
	#$ ...go cálálaččat lea registreren maid ja man ollu lea borran. Comment: We are supposed to have full sentences without the "..." so we can check it with smedist whenever needed!
	#$ Reálapolitihkalaččat gehččojuvvon, de lea mihtilmas... Comment: We are supposed to have full sentences without the "..." so we can check it with smedist whenever needed!

#!! * **NDr2357** Selects (A Pl Nom)
SELECT:NDr2357 (A Pl Nom) IF (*-1 BOC BARRIER NOT-ADV-PCLE)(*1 Neg OR COPULAS BARRIER NOT-ADV-PCLE LINK 0 Pl3)(NEGATE *1 NOT-AUX-V BARRIER S-BOUNDARY);
	#$ Nu maŋimuččat šaddet vuosttamužžan.

#!! * **NDr2358** Selects (A Pl Nom)
SELECT:NDr2358 (A Pl Nom) IF (*-1 BOC BARRIER NOT-ADV-PCLE)(*1 Neg OR COPULAS BARRIER NOT-ADV-PCLE LINK 0 Du3)(NEGATE *1 NOT-AUX-V BARRIER S-BOUNDARY); # 0-4
	#$ ...ahte oasálaččat eaba leat dássásaččat. Comment: Please add the whole sentence in stead of "..."!

#!! * **NDr2359** Selects (A Pl Nom)
SELECT:NDr2359 (A Pl Nom) IF (*-1 BOC BARRIER NOT-ADV-PCLE)(*1 Neg OR COPULAS BARRIER NOT-ADV-PCLE LINK 0 Pl3 LINK *1 CC BARRIER S-BOUNDARY LINK *1 VFIN BARRIER S-BOUNDARY); # 8-0
	#$ Geafit gal leat álo din luhtte ja sidjiide sáhttibehtet dahkat buriid.
 
 
#The end of accusatives: removes all accusative leftovers

 
#!! ### Postverbal subjects
	# - - - - - - - - - -

#!! * **NDr2360** Selects Nom
SELECT:NDr2360 Nom IF (*-1 VFIN BARRIER NOT-NPMOD OR Nom LINK NEGATE *-1 Nom BARRIER S-BOUNDARY)(*-1 VFIN BARRIER NOT-NPMOD OR Nom LINK *-1 NP-HEAD-ACC BARRIER S-BOUNDARY)(0 N LINK NOT 0 PrfPrc OR VFIN)(NEGATE *1 Nom BARRIER S-BOUNDARY LINK NOT 0 Po);
 	#$ Dán áiddi huksii stáhta.
 	#$ Liná liikui sárgut ja tennjet. (sárgut)
 	#% Sin bargun lei váldit fáŋgan Gonagasa. 

#!! * **NDr2361** Selects Nom
SELECT:NDr2361 (N Sg Nom) IF (*-1 SG3-V BARRIER NOT-NPMODADVIIP OR CP LINK NOT 0 (Imprt Sg2) LINK *-1 BOC BARRIER Nom LINK NOT 0 Nom)(NEGATE -1 ("buorre" Acc))(NEGATE 0 VFIN OR PrfPrc OR Indef OR Gen OR Ill OR Loc OR (Actio Ess) OR Po OR (A Nom))(NEGATE 1 ("ahte") OR Rel)(NEGATE 0 Loc LINK *1 NP-HEAD-NOM BARRIER NOT-NPMOD)(NEGATE 0 PrsPrc LINK 1 (N Sg Nom))(NEGATE 0 Attr LINK 1 N)(NEGATE 0 Adv LINK 1 A);
    	#$ Guovddážis dán barggus lea Sámi dutkamiid guovddáš.
    	#$ Riikkaid galgá Piera oaidnit.
    	#% Muhtun diliin lea várra váttis beassat eret das.
    	#% Moddjá ja čájeha Ávvirii diehtojuohkinplakáhta man ráhkadii go kránnjá kafea dáhtui su ráhkadit diehtojuohkima


#!! * **NDr2364** Selects (Sg Nom)
SELECT:NDr2364 (Sg Nom) IF (*-1 (Imprt Sg3) BARRIER NOT-NP)(NEGATE 0 Acc OR Ess);
	#$ Nu báitos din čuovggas ge olbmuide.

#!! * **NDr2366** Selects Nom
SELECT:NDr2366 Nom IF (*-1 Der/Pass BARRIER CC OR NOT-NPMODADV)(NEGATE *-1 Nom BARRIER S-BOUNDARY)(NEGATE -1 (Der/PassS PrfPrc) OR (Der/PassL PrfPrc) LINK NEGATE *-1 REAL-COPULAS BARRIER SV-BOUNDARY OR PUNCT)(NEGATE 0 Attr LINK *1 N BARRIER NOT-NPMOD)(NOT 0 Attr);
	#$ ...ahte lea ráhkaduvvon diehtojuohkinmateriála. Comment: Please add the whole sentence in stead of "..."!
	#$ 

#!! * **NDr2367** Selects Nom
SELECT:NDr2367 Nom IF (NEGATE *-3 Nom BARRIER S-BOUNDARY)(-2 Der/Pass)(-1 (Der/PassL PrfPrc) OR (Der/PassS PrfPrc));
	#$ Váldonjuolggadussan galgá geavahuvvot čohkkejuvvon journála... Comment: Please add the whole sentence in stead of "..."!

#!! * **NDr2368** Selects (N Pl Nom)
SELECT:NDr2368 (N Pl Nom) IF (*-1 PL3-V + IV BARRIER NOT-NPMODADVIIP LINK *-1 BOC BARRIER Nom)(NEGATE 0 PrfPrc OR Inf);
	#$ 1995:s gárvánedje rádiobuvttadeapmái lanjat.



#!! * **NDr2369** Selects (Pl3 Nom)
SELECT:NDr2369 (Pl3 Nom) IF (*-1 PL3-V BARRIER NP-HEAD-NOM LINK *-1 BOS OR CS OR CLB BARRIER NP-HEAD-NOM)(NOT 1 NP-HEAD-NOM);
	#Comment: No hits?

#!! * **NDr2370** Selects (Num Nom)
SELECT:NDr2370 (Num Nom) IF (*-1 ILL-V BARRIER NOT-NPMODADV LINK 0 (Pl3) LINK *-1 Ill BARRIER NOT-ADV-PCLE);
	#$ Eanetlohku, masa gulle Riektejoavkku 6 eará lahtu,... Comment: Please add the whole sentence in stead of "..."!

#!! * **NDr2372** Selects (Pron Pl Nom)
SELECT:NDr2372 (Pron Pl Nom) IF (*-1 PL3-V BARRIER NP-HEAD-NOM OR SV-BOUNDARY LINK *-1 BOC BARRIER NP-HEAD-NOM);
	#$ Eará osiid giddodagaid dáfus, leat dát dávjá dilit maid... Comment: Please add the whole sentence in stead of "..."!
	#$ Sáhttetgo earát gáibidit buhtadasa boahtá das makkár beroštumiin lea... Comment: Please add the whole sentence in stead of "..."!

#!! * **NDr2373** Selects Nom
SELECT:NDr2373 Nom IF (*-1C PL3-V BARRIER NOT-ADV-PCLE OR MO LINK *-1 BOC BARRIER Nom + Pl)(0 Coll OR NUM LINK NOT 0 Adv);
	#$ Mus báhce moadde girjji logakeahttá.
	#$ Go sotnabeaivi šattai, de čuoigaledje máŋggas várrái.
	#$ Eksámen vahku šaddet maid guokte oahpahusbeaivvi.
	#% De ohcot moadde girjji.
	#% Arkiivaávdnasat galget sáddejuvvot go leat sullii 25-30 jagi boarrásat. #Comment: Why is this marked? 

#!! * **app** Selects Nom
SELECT:app Nom IF (*-1 COMMA OR LEFT BARRIER NOT-NPMOD LINK NEGATE *-1 MAIN-V BARRIER S-BOUNDARY)(NEGATE *1 VFIN BARRIER S-BOUNDARY)(NEGATE 0 Adv OR ("leat" PrfPrc) OR Imprt OR Po OR VFIN)(NEGATE 0 Actio LINK 1 ("veara")) ;#(NEGATE 0 PrfPrc LINK *-1 MOVT LINK -1 ("nu") LINK -1 ("leat"));
	#$ Dá dat leat, 350 ruvnno.
	#$ Solveig Joks (váldodoaimm.)
	#% Eará guovlluin gos leat vel unnit sámit go Finnmárkkus, leamaš dákkár ákkain vel stuorát deaddu almmolaš ságastallamis.  # It should not take the latter one.

#!! * **dasalassinNom** Selects Nom
SELECT:dasalassinNom Nom IF (-1 ("dasa lassin") LINK *-1 Nom BARRIER Acc) ;
	#$ Doppe galgá sátnejođiheaddji Bernt Eirik Isaksen Lyngstad leat mielde, dasa lassin várresátnejođiheaddji Britt Pedersen ja fylkkaráđđejođiheaddji Bjørn Inge Mo


#!! * **NDr2376** Selects Nom
SELECT:NDr2376 Nom IF (*-1 VERBAL-ACTIVITY-V BARRIER NOT-NPMOD LINK -1 COMMA LINK *-1 VFIN)(NEGATE 1 Nom);
	#$ Don leat jallu, dadjá Tomi.
	#$ Don leat jallu, dadjá Tomi skihpár.
	
#!! * **PostVNom** Selects Nom if a singular third person verb to the left with no Nom to the left of it
SELECT:PostVNom (N Sg Nom)  IF (*-1C SG3-V BARRIER NOT-NPMODADV - Qst OR PrfPrc LINK NEGATE *-1 (Sg Nom) OR (Pron Sg3) BARRIER S-BOUNDARY LINK NOT 0 Sem/Year)(NEGATE 0 Gen LINK *1 N BARRIER NOT-NPMOD)(NEGATE 0 Loc LINK *1 (Sg Nom) BARRIER VFIN)(NEGATE 0 Po OR Loc)(NEGATE 0 Attr LINK 1 N);
	#$ Lávvui gullá maid uvssot. 
	#$ Jagis 2007 álggii Giellatekno bargat interaktiivvalaš pedagogalaš prográmmaiguin, álggos ráhkadeimmet davvisámegiel VISL-veršuvnna, ovttasbargoguoibmi dán barggus lei Syd-Dansk Universitet.
	#% Bovnnas lei viessu, ja viesus lei nuorra nisu mánnáváivvis.
	#% Son lea njulgen čielggi, ja lea nuorra ja searra.
	#% Son liiko guollái.

#!! * **PostVNomComp** Selects (N Sg Nom)
SELECT:PostVNomCop (N Sg Nom)  IF (*-1C COPULAS + Sg3 BARRIER NOT-NPMODADV - Qst OR PrfPrc LINK NEGATE *-1 (Sg Nom) OR (Pron Sg3) BARRIER S-BOUNDARY)(0 Nom LINK 1 (A Sg Nom));
	#$ Lea go Čáhppes Bearral buorre dutnje?



 
#!! ### Nominative predicatives
	# -----------------------

#REMOVE:NDr2377 A IF (NEGATE *-1 COPULAS OR OPRED-V BARRIER NOT-AUX-V)(0 N)(NEGATE 0 ("veara"))(1 CLB)(NEGATE 2 A OR PrsPrc);
	## Ulbmil lea ovddidit guovddáža eamiálbmotdutkamiid guovddážin.
 # This rule has to follow identification of subject.

#!! * **NDr2378** Selects (Sg Nom)
SELECT:NDr2378 (Sg Nom) IF (*-1 ("leat") BARRIER SV-BOUNDARY OR PUNCT OR NP-HEAD-NOM LINK 0 Sg3)(0 NP-HEAD OR (A Nom))(NEGATE 0 PrfPrc OR Der/NomAg OR Prop + Loc OR Ess)(NEGATE 0 N-PO)(NEGATE 0 Loc LINK *-1 Nom BARRIER S-BOUNDARY OR PUNCT)(NEGATE 0 A LINK 0 Ill)(NEGATE 0 Adv LINK 1 Pr OR VGen)(1C NP-BOUNDARY LINK NOT 0 TRANS-V);
	#$  Dasgo dus boahtá oaivámuš gii lea mu álbmoga Israela báimman.
	#$  Dat gal lei beare unnán. 
	#% Sámediggeráđđi oaivvilda ahte mánnái lea buoremus maiddái sin kultuvrralaš ja gielalaš identitehta gozihit.

#!! * **ND** selects Nom if; you are HUMAN and immediately to your right is a place. Leat is to the left, and there is HUMAN or Pers to the left of leat *Son lei oahpaheaddji Kárášjogas*
SELECT:ND Nom IF (*-1 ("leat") BARRIER NOT-NPMOD LINK *-1 HUMAN OR Pers)((0 HUMAN LINK 1 PLACE OR Sem/Plc) OR (0 HUMAN LINK 1 ("ja") LINK 1 HUMAN LINK 1 PLACE OR Sem/Plc)) ;
	#$ Bjørn Aaseth lei oahpaheaddji Kárášjogas 1949-73.
	#$ Bjørn Aaseth lei oahpaheaddji ja rektor Kárášjogas 1949-73.

#!! * **NDr2379** Selects (Sg Nom)
SELECT:NDr2379 (Sg Nom) IF (*-1 COPULAS BARRIER CC OR NOT-NPMODADV OR (Sg Nom))(*-1 SG3-V BARRIER NOT-AUX-V)(NEGATE 0 Loc OR Ess OR Adv)(NEGATE 0 N-PO)(*1C NP-BOUNDARY BARRIER WORD);
#SELECT:NDr2379 (Sg Nom) IF (*-1 COPULAS BARRIER CC OR NOT-NPMODADV OR (Sg Nom))(*-1 SG3-V BARRIER NOT-AUX-V)(NEGATE 0 Loc OR Ess OR Adv)(NEGATE 0 N-PO)((*1C NP-BOUNDARY OR COMMA BARRIER WORD) OR (*1 TIME-N BARRIER NOT-NPMOD LINK *1 NP-BOUNDARY OR COMMA));
	#$  Go šattai eahket, de... Comment: Please add the whole sentence in stead of "..."!

#!! * **NDr2380** Selects (Pl Nom)
SELECT:NDr2380 (Pl Nom) IF (*-1 COPULAS BARRIER SV-BOUNDARY OR PUNCT OR NP-HEAD-NOM LINK 0 PL-V OR DU-V)(0 A OR NP-HEAD LINK NEGATE *1 TRANS-V BARRIER SV-BOUNDARY)(NEGATE 0 Inf OR Indef); #(1C NP-BOUNDARY OR (N Pl Nom));
	#$  Vuollel 7% Finnmárkku eatnamiin leat vuovddit. 
	#$  Olmmošlassáneami geažil šadde eanebut geat galge juogadit buriid.
	#$  Ledje go hilbadat mánát?	
	#$ Muhto leimme gal bivnnuhat gili nieiddaid gaskkas.

#!! * **NDr2381** Selects (Pl Nom)	
SELECT:NDr2381 (Pl Nom) IF (*-1 COPULAS BARRIER SV-BOUNDARY OR PUNCT OR NP-HEAD-NOM LINK 0 Pl3)(0 A OR NP-HEAD)(NEGATE 0 Inf)(1 CLB)(2 NOT-NP);
	#$  ...main leat mielde dušše priváhta oasálaččat, ii leat... Comment: Please add the whole sentence in stead of "..."!

#!! * **NDr2382** Selects (Pl Nom)
SELECT:NDr2382 (Pl Nom) IF (*-1 COPULAS BARRIER NOT-ADV-PCLE LINK 0 Pl3)(NOT 0 Inf)(1 BOC OR COMMA);
	#$  ...seammás go nuppe dáfus leat veháš várrugasat, ja dadjet ahte... Comment: Please add the whole sentence in stead of "..."!

#!! * **NDr2383** Selects Nom
SELECT:NDr2383 Nom IF (*-1 COPULAS BARRIER NOT-NPMODADV OR PrfPrc LINK *-1 MUN BARRIER NOT-ADV-PCLE)(*-1 SG1-V BARRIER NOT-AUX-V)(NEGATE 0 PrfPrc OR (Actio Ess))(NEGATE 0 Loc LINK *-1 Nom BARRIER S-BOUNDARY OR PUNCT)(NEGATE 0 Ess)(NEGATE 1 N OR A)(NEGATE *1 PrfPrc BARRIER SV-BOUNDARY); # 0-3
	#$  Vaikko mun lean friddja in ge lean geange vuollásaš,... Comment: Please add the whole sentence in stead of "..."!

#!! * **NDr2384** Selects Nom
SELECT:NDr2384 Nom IF (*-1 COPULAS BARRIER NOT-NPMODADV OR PrfPrc LINK *-1 DON BARRIER NOT-ADV-PCLE)(*-1 SG2-V BARRIER NOT-AUX-V)(NEGATE 0 PrfPrc)(NEGATE 0 Loc LINK *-1 Nom BARRIER S-BOUNDARY OR PUNCT)(NEGATE 1 N)(NEGATE *1 PrfPrc BARRIER SV-BOUNDARY); # 0-3
	#$  Mii diehtit ahte don leat Israela oahpaheaddji.

#!! * **NDr2385** Selects Nom
SELECT:NDr2385 Nom IF (*-1 COPULAS BARRIER SV-BOUNDARY)(NEGATE *-1 NP-HEAD-NOM BARRIER NOT-NPMODADV)(NEGATE 0 REAL-V OR Adv OR Loc OR Ill OR Ess)(*1 S-BOUNDARY BARRIER NP-HEAD OR (A Nom));
	#$  ...maidda lágas eai leat sierra njuolggadusat, muhto... Comment: Please add the whole sentence in stead of "..."!

#!! * **NDr2386** Selects Nom
SELECT:NDr2386 Nom IF (-2 Nom)(-1 COPULAS)(0 N OR A)(NEGATE 0 PrfPrc OR Ess OR Prop + Loc)(1 EOS OR ("go"));
	#$  ...ahte dat leat earalágan go eará joavkkut. Comment: Please add the whole sentence in stead of "..."!
 #Commet: Hits here because "earálágan" does not agree?

#!! * **CollNom** Selects Nom
SELECT:CollNom Nom IF (*-1 COPULAS BARRIER NOT-ADV-PCLE)(0 Coll)(NEGATE 1 N);
	#$  Stobus leigga guovttis.
	#$  Mii leimmet golmmas.

#!! * **CollGen** Selects Nom
SELECT:CollGen Gen IF (*-1 COPULAS BARRIER NOT-ADV-PCLE)(0 Coll LINK 1 N);
	#$  lea go dievdduguoktá giehtačálus dárkkistuvvon ráppi báhpáris?


#!! ### Nominative as objects in existential clauses
	# --------------------------------------------

#!! * **NDSgr2388** Selects Nom
SELECT:NDSgr2388 Nom IF (0 Sg LINK *-1 COPULAS + Sg3 BARRIER NOT-NPMODADV OR Dem - Nom LINK *-1 NORM-LOC BARRIER N OR SV-BOUNDARY)(NEGATE 0 Adv OR Ess OR PrfPrc)(NEGATE *1 Nom BARRIER NOT-NPMOD)(NEGATE 0 Attr LINK 1 N);
	#$  Ii dus leat gávdni masage.

#!! * **NDPlr2388** Selects Nom
SELECT:NDPlr2388 Nom IF (0 Pl OR NUM LINK *-1 COPULAS + Pl3 BARRIER NOT-NPMODADV OR Dem - Nom LINK *-1 NORM-LOC BARRIER N OR SV-BOUNDARY)(NEGATE 0 Adv OR Ess OR PrfPrc)(NEGATE *1 Nom BARRIER NOT-NPMOD);
	#$  Mus leat golbma máná.

#!! * **NDr2389** Selects Nom
SELECT:NDr2389 Nom IF (*-1 COPULAS BARRIER NOT-NPMODADV OR Nom LINK *-1 Loc BARRIER N OR A OR SV-BOUNDARY)(NEGATE 0 PrfPrc)(1 NP-BOUNDARY)(NEGATE 0 Attr OR PrsPrc LINK *1 N BARRIER NOT-A-CC)(NEGATE 0 Adv LINK *1 Nom BARRIER S-BOUNDARY)(NOT 0 Ess);
	#$  Dat bulle ja goldne go dain ii lean ruohtas.
	#% – Sámedikkis lea leamaš vierrun ahte čoahkkinjođiheaddjevirgi gullá unnitlohkui.

#!! * **NDr2390** Selects Nom
SELECT:NDr2390 Nom IF (*-1 COPULAS BARRIER Inf OR PrfPrc OR SV-BOUNDARY OR Nom)(*-1 PL3-V BARRIER SV-BOUNDARY LINK *-1 Loc BARRIER NOT-ADV-PCLE)(0 NUM);
	#$  Davviriikkain leat eahpitkeahttá guokte álbmoga maid atnit álgoálbmogin.

#!! * **NDr2391** Selects Nom 
SELECT:NDr2391 Nom IF (*-1 Loc BARRIER SV-BOUNDARY OR Nom LINK *-1 COPULAS BARRIER SV-BOUNDARY OR Nom LINK NEGATE *-1 Nom BARRIER SV-BOUNDARY)(NEGATE 0 PrfPrc)(NEGATE 0 Adv LINK *1 Nom BARRIER S-BOUNDARY)(NEGATE 0 Attr LINK *1 N BARRIER NOT-NPMOD)(NEGATE 0 VFIN LINK -1 CLB)(1 NP-BOUNDARY); 
	#$  Ovdal go kristtalašvuohta bođii Sápmái lei noaiddis erenoamáš
	#$  rolla oaidnilin ja dálkkodeaddjin.

#!! * **NDr2392** Selects Nom
#"REMOVE:NDr2392 Acc IF (*-1 COPULAS BARRIER PrfPrc OR Inf OR (Actio Ess) OR SV-BOUNDARY)(NEGATE 0 TIME OR MEASURE)(1 CS OR EOS);
	#$  Dain áššiin lea Ruonáeatnama ruovttustivrras ráđđeaddi váldi. 

#"REMOVE:NDr2393 Nom IF (NEGATE 0 Dem)(1C (N Nom))(NEGATE 1 PUNCT OR Num OR Interr)(NEGATE *-1 COPULAS BARRIER SV-BOUNDARY)(NEGATE 0 ("son"))(NEGATE *1 COPULAS BARRIER SV-BOUNDARY LINK NEGATE *1 NOT-AUX-V BARRIER S-BOUNDARY)(NEGATE -1 Gen LINK 1 Actio LINK 1 N);
	#$  Divššohasjournála láhkaásahus.
	#$  ...maid buot journála geavaheaddji dearvvašvuođabargit áddejit.

#"SELECT:NDr2394 (Pl Nom) IF (*-1 COPULAS BARRIER SV-BOUNDARY OR go OR Nom LINK 0 Pl3)(1 EOS);
	#$  Váikkuhusat divššohasa lagamuččaide leat mearkkašahtti stuorrát. 
#"SELECT:NDr2395 (Pl Nom) IF (*-1 COPULAS BARRIER NOT-NPMODADV OR PrfPrc)(*-1 DU-PL-V BARRIER NOT-AUX-V OR CS)(*-1 PPRON-DU-PL BARRIER S-BOUNDARY LINK NEGATE *-1 (Pl Nom) BARRIER NOT-ADV-PCLE)(NEGATE *1 TRANS-V BARRIER SV-BOUNDARY);
	#$  Ehpet go dii leat ollu mávssolaččabut go dat?

#!! * **NDr2396** Selects (Pl Nom)
SELECT:NDr2396 (Pl Nom) IF (*-1 COPULAS BARRIER NOT-NPMODADV OR PrfPrc)(*-1 DU-PL-V BARRIER NOT-AUX-V OR CS)(NEGATE 0 Inf)(*-1 (N Sg Nom) BARRIER S-BOUNDARY LINK *-1 CC BARRIER NOT-NPMOD LINK -1 (N Sg Nom));
	#$  Anne ja Biret leaba goappašagat nu jallat.

#!! * **NDr2391** Selects Nom
SELECT:NDr2397 Nom IF (*-1 COPULAS OR (IV) BARRIER NOT-ADV-PCLE)(0 NUM)(1 ("ruvdno"));
	#$  Doaibmabiju jahkásaš bušeahttarámma lea: 380.000 ruvnno.

#!! ### Nominative in coordination and apposition
	# -----------------------------------------

#"REMOVE:NDr2398 Nom IF (*-1 VFIN LINK NOT 0 Pl)(0 Loc LINK 1 CRD LINK *1 Nom LINK NEGATE *1 VFIN);
  	#$  Plána lei ahte galggai čađahit johtaluslohkamiid diimmá jagi skuhtervuodjimis ja bievlavuodjimis.

#!! * **NDr2399** Selects Nom  
SELECT:NDr2399 Nom IF (*-1 CRD BARRIER NOT-NPMODADV LINK -1 Nom LINK NOT 0 Acc LINK NEGATE *-1 NOT-AUX-V BARRIER NOT-NPMODADV)(NEGATE 0 VFIN)(1C NP-BOUNDARY); 
	#$ Divššohas dahje su ovddasteaddji gii oaivvilda ahte... Comment: Please add the whole sentence in stead of "..."!
	#$ Riddu Riđu festiválajođiheaddji Sandra Márjá West ja Riddu Riđu stivrajođiheaddji Bente Ovedie Skogvang doalaiga lávvardaga preassakonferánssa.
	
#!! * **ProperNom** Selects Nom, adjusted for arabics in paranthesis between  
SELECT:ProperNom Nom IF (*-1 JA/DAHJE BARRIER NOT-NPMODADV LINK *-1 Nom BARRIER WORD - Arab LINK NOT 0 Acc LINK NEGATE *-1 NOT-AUX-V BARRIER NOT-NPMODADV)(0 Prop)(*1C NP-BOUNDARY BARRIER WORD - Arab); 
	#$ Sámi ofelaččaguovttos Elle Rávdná Näkkäläjärvi (20) ja Mirja Andersson Renander (19) leaba Norgga mátkkis ja dán vahkkus leaba Plassjes.
	
SELECT:coordFirst N + Nom IF (0 Gen)(1 CRD LINK *1C Nom + N BARRIER NOT-NPMOD)(NEGATE 0 Ess LINK 1 XGO); 	
	#$ Dan galgaba Anne Henriette Hætta Reinås ja Marita Kristin Eilertsen Tøsse muitalit Sápmái.
	#% Son lea juo vuorrasit beallái, 84-jahkásaš, muhto dovdá iežas nuorran dego giđđarássi.

#!! * **NDr2400** Selects Nom
SELECT:NDr2400 Nom IF (-2C HEAD-N-NOM)(-1 CRD)(NEGATE 0 Cmp/SplitR OR Der/NomAg OR Adv)(NEGATE 0 Attr OR Gen LINK 1 (N Nom))(NEGATE 0 PrfPrc LINK -1 CC LINK *-1 PrfPrc BARRIER REAL-V)(NEGATE 0 VFIN);
	#$  Eamiálbmogat ja riekti - muhtun prinsihpalaš vuolggasajit.

#!! * **NDr2401** Selects Nom
SELECT:NDr2401 Nom IF (-2 (Pron Nom))(-1 CRD);
	#$  Jesus Kristus ieš ja Ipmil.

#!! * **NDr2402** Selects Nom
SELECT:NDr2402 Nom IF (-1 CRD LINK *-1C (Num Nom) BARRIER NOT-N-A)(0 Num);
	#$  Journála divodeapmi ja sihkkun.
	#$  Joavkkus leat guokte Riikaoasselávdegotti ovddasteaddji ja 2 Sámedikkis.

#!! * **NDr2403** Selects Nom
SELECT:NDr2403 Nom IF (-2C Nom)(-1 CRD)(1 Gen LINK *1 Po BARRIER NOT-NPMOD)(NEGATE *1 Nom BARRIER Po);
	#$  Dearvvašvuođaásahusas mearriduvvo divvun dahje sihkkun dearvvašvuođabargiidlága § 44 mielde.

#!! * **NDr3529** Selects Nom	
SELECT:NDr3529 Nom IF (*-1 TITLE OR ("joavku") OR ("koarra") BARRIER NOT-NPMOD LINK 0 ADVLCASE)(0 Prop);
	#$  Son logai dan maiddái professoriidda Asbjørn Kjønstad ja Kristian Andenæs.	
	#$  Son logai dan maiddái professoriidda Kjønstad ja Andenæs.
	#$ 	Rolf Morten Anti Amundsen beakkihii beaškasiin nuhttejeaddjin ja lávllačállin joavkkuin rOlfFa jagi 2006.


#SELECT:NDr2404 Nom IF (*-1 CRD BARRIER NOT-NPMOD LINK *-1 Nom BARRIER WORD LINK NOT 0 PrfPrc)(1 CS OR EOS OR (PUNCT RIGHT))(NEGATE 0 Inf LINK -1 CC LINK -1 Inf);
	#$  ...Jáhkoba ja Jovssesa eadni, ja Salome. 

#SELECT:NDr2405 Nom IF (NEGATE 0 PrfPrc LINK *-1 COPULAS BARRIER NOT-ADV-PCLE)(1 CRD)(*2C Nom BARRIER W-NOT-ACC-GEN LINK NOT 0 Rel LINK 1 NP-BOUNDARY OR CLB OR PUNCT-LEFT OR PUNCT-RIGHT);
	#$  3) Oččodeapmi, diehtojuohkin ja ásahusovttasbargu.
	#$  Molssaevttolaš dálkkasdiehtaga našunála dutkanguovddáš ja Sámi dearvvašvuođadutkanguovddáš (goappašat fágabirrasat gullet Romssa universitehta vuollái) berrejit sáhttit oassálastit dán bargui.

#!! * **NDr2406** Selects Nom
SELECT:NDr2406 Nom IF (1 COMMA LINK *1 Nom BARRIER NOT-NP LINK 1 CC LINK *1 Nom BARRIER NOT-NP LINK *1 PL3-V BARRIER NOT-ADV-PCLE);
	#$  Sámedikki hálddahusa nannen, dearvvašvuođa- ja eallindiliiskkadeami 
	#$  ruhtadeapmi ja Sámi dearvvašvuođadutkanguovddáža doaimmaid viiddideapmi 
	#$  váldojit ovdan.

#!! * **NDr2407** Selects Nom
SELECT:NDr2407 Nom IF (NEGATE 0 Loc OR Adv)(1 COMMA)(*2 NP-HEAD-NOM BARRIER NOT-NPMOD LINK 1 CC LINK *1 NP-HEAD-NOM BARRIER NOT-NPMOD LINK 1 CC OR COMMA OR NP-BOUNDARY);
	#$  ...dikšu, dearvvašvuođabisuheaddji, veajuidahtti dahje dikšun- ja fuolahanulbmilat.

#!! * **NDr2408** Selects Nom
SELECT:NDr2408 Nom IF (1 COMMA)(*2C NP-HEAD-NOM BARRIER NOT-NPMOD LINK 1 COMMA LINK *1 NP-HEAD-NOM BARRIER NOT-NPMOD);
	#$  Romssa universitehtas lea sámi dutkan, gelbbolašvuođahuksen, 
	#$  oahpahus ja eará gaskkustepmi leamaš... Comment: Please add the whole sentence in stead of "..."!

#!! * **NDr2409** Selects Nom
SELECT:NDr2409 Nom IF (1 ("/"))(*2C Nom BARRIER NOT-NPMOD);
#"SELECT:NDr2410 Nom IF (-2C Nom)(-1 ("/"));
	#$  Vuoroheapmi lea: isit/eamit,...
	#$  Lea ráhkaduvvon diehtojuohkinmateriála/oččodanmateriála. 

#!! * **NDr2411** Selects Nom
SELECT:NDr2411 Nom IF (*-1 COMMA BARRIER VFIN LINK -1C NP-HEAD-NOM)(1 COMMA)(*2 NP-HEAD-NOM BARRIER NOT-NPMOD);
	#$  ..persovdnarievtti dábálaš láhkavuogádagat, bearašriekti, árberiekti,... 

#!! * **NDr2412** Selects Nom
SELECT:NDr2412 Nom IF (NEGATE 0 REAL-V OR (A Attr))(1 COMMA)(*2 NP-HEAD-NOM BARRIER NOT-NPMOD OR CC OR COMMA LINK 1 EOS OR S-BOUNDARY)(NEGATE 0 Acc LINK 1 COMMA LINK *2 NP-HEAD-ACC BARRIER NOT-NPMOD);
	#$  Jáhkobii riegádii Jovsset, Márjjá boadnjá.

#!! * **NDr2413** Selects Nom
SELECT:NDr2413 Nom IF (-2C Nom)(-1 COMMA)(1 EOC)(NEGATE 0 Adv LINK 1 CS)(NEGATE 0 VFIN); # 3-0
	#$ Olusat dadjet munnje dan beaivvi: Hearrá, Hearrá!

#!! * **NDr2414** Selects Nom
SELECT:NDr2414 Nom IF (-2C Nom)(-1 COMMA)(0 N)(NEGATE 0 Loc)(1 Inf); # 0-3
	#$ Muhto ruovttustivra ii leat ožžon mearridanválddi dakkár áššiid hárrái go stáhtavuođđoásahus, stáhtaborgárdoaba, pássa- ja visumváldi, váldi mearridit stáhtasymbolaid,... Comment: Please add the whole sentence in stead of "..."!

#!! * **NomCCNom** Selects Nom
SELECT:NomCCNom Nom IF (*-1 V + Pl3  BARRIER NOT-NPMOD LINK NEGATE *-1 (Pl Nom) OR (Pl3 Nom) BARRIER S-BOUNDARY)(1 CC LINK *1 Nom BARRIER NOT-NPMOD) ; 
	#$ ...maid liikká fertejedje leansmánni ja guokte albmá čujuhit. Comment: Please add the whole sentence in stead of "..."!

#!! * **NDr2416** Selects Nom
SELECT:NDr2416 Nom IF (*-1 COMMA BARRIER NOT-NPMODADV LINK -1C Nom)(NEGATE 0 VFIN)(1 CC);
	#$ ...geat leat divššohasa lagašolbmot, fuolaheaddji dahje veahkefuolaheddji. Comment: Please add the whole sentence in stead of "..."!

#!! * **NDr2417** Selects Nom
SELECT:NDr2417 Nom IF (1 CC)(2C (Pron Nom))(3 (Pron Nom));
	#$ Gonagas Agrippa ja dii buohkat geat lehpet dás minguin!

#!! * **NDr2418** Selects Nom
SELECT:NDr2418 Nom IF (1 CC)(2C (N Nom))(2 HEAD-N);
	#$ Jos dát leažžá dušše olbmuid dáhttu ja dahku,... Comment: Please add the whole sentence in stead of "..."!

#!! * **NDr2420** Selects Nom
SELECT:NDr2420 Nom IF (-2 Nom)(-1 NEGFOC);
	#$ Eai ovdabarggut, teoria iige geavat doarjjo klausula áddejumi. 

#!! * **NDr2421** Selects 
#SELECT:NDr2421 Nom IF (-1 PUNCT-LEFT)(0 KLASS LINK NOT 0C ADVLCASE)(*1 PUNCT-RIGHT BARRIER Po OR TV OR N);
  	#$ Sámediggi sáhttá maid buktit cealkámušaid guovlluid dearvvašvuođafitnodagaid jahkedieđáhusain ja plánaárvalusain (kap. 3.4).
	#$ (čuokkis 10)
  
#!! ### Nominative in parallell constructions
	# -------------------------------------

#!! * **NDr2422** Selects Nom
SELECT:NDr2422 Nom IF (*-1 BOS LINK *1 Gen LINK 1C NP-HEAD-NOM LINK *1 TV)(-1 Gen)(0 N LINK NOT 0 REAL-V LINK *1 TV);
	#$ Dearvvašvuođabargiilága § 10, mii gieđahallá dieđuid addima pasienttaide, ja pasientavuoigatvuođalága § 1-3, mii gieđahallá vuoigatvuođa informerejuvvon miehtamii, leat ovdamearkkat dakkár vuoigatvuođain.

#!! * **NDr2423** selects Nom if it finds a Nom to the left of CC and to the left of a verb. No verb allowed to the right *eamit barggai vuođđoskuvllas ja isit fas gymnásas*
SELECT:NDr2423 Nom IF (-1 CC LINK *-1 REAL-V LINK -1 Nom)(NOT 0 REAL-V OR Adv)(NEGATE *1 REAL-V BARRIER S-BOUNDARY)(NOT 0 (Num Sg Acc) LINK 1 (N Gen));
	#$ Nubbi goallostuvvo giehtahearggi lávžái ja goalmmát fas dan nuppi lávžái.
	#$ Bjørn Aarseth lei oahpaheaddji ja rektor Kárášjogas 1949-73.
	#$ Okta lei Hámmarfeastta gávpeguovddážis ja golbma eará fas Álttá-Dálbmeluovtta báhpasuohkanis.
	#$ Eamit barggai vuođđoskuvllas ja isit fas gymnásas.
	#% Juohke mánná oaččui guokte ja eanemusat golbma mánu oahpahusa jahkái.
	
#!! * **nomHnoun** Selects Nom
SELECT:nomHnoun Nom (NOT 0 Sem/Date)(*-1 BOS OR (":") BARRIER WORD)(1 (":") OR EOS) ;
	#$ Fáksa: 22242786

#!! * **SOV** Selects Nom in front of an Acc	
SELECT:SOV Nom IF (0 Nom LINK -1 BOS OR (":"))(1 Acc LINK NOT 0 Nom LINK 1 TRANS-V);
	#$ Gánda: "Ákšu dan čuohpai."
	#$ Gánda: "Vuoksá dan jugai."


#!! ### Not nominative
	# --------------
	#!! * **NDr2424** Removes Nom
REMOVE:NDr2424 Nom IF (*-1 NOT-AUX-V BARRIER S-BOUNDARY OR PUNCT LINK *-1 (Pron Nom) BARRIER S-BOUNDARY OR PUNCT OR COMMA)(NEGATE *-1 PrsPrc BARRIER NOT-NPMOD)(NEGATE 0 Adv)(NEGATE 0 Nom LINK *1 VFIN BARRIER S-BOUNDARY)(NEGATE -1 ("diibmu"))(NOT 0 Coll);
	#$ Maid don dainna dieđuin, vástidii Liná ja áiggui vázzát viidáseappot.
	#% Danne son oaččui nama Peres. Comment: We want Nom on this!

#!! * **NDr2425** Removes Nom
REMOVE:NDr2425 Nom IF (*-1 VFIN BARRIER NOT-ADV-PCLE LINK NOT 0 COPULAS OR FAUXV LINK *-1C Nom BARRIER NOT-ADV-PCLE)(*1 NOT-AUX-V BARRIER S-BOUNDARY)(NOT 0 Coll);
	#$ Dii galgabehtet vuđolaččat bargat.

#!! * **NDr2426** Removes Nom, but not Actio 
REMOVE:NDr2426 Nom - Actio IF (*-1 BOC BARRIER NOT-NP)(NEGATE 0 Adv)(NEGATE 1 Prop)(*1C (N Nom) BARRIER CC OR CLB OR NOT-NPMOD OR PUNCT LINK *1 VFIN BARRIER NOT-NP)(NEGATE 1 (Interr Nom))(NEGATE -1 Gen LINK 1 ABBR);
	#$ Romssa universitehta vuosttaš čielgadeapmi sámi dutkama birra dahkkui...

#!! * **NDr2427** Removes Nom
REMOVE:NDr2427 Nom IF (*-1 COPULAS BARRIER NOT-NPMODADV LINK *-1 Loc BARRIER NOT-ADV-PCLE)(*1 Nom BARRIER S-BOUNDARY OR NOT-NPMOD OR PUNCT);
	#$ Sámedikkiin lea ráđđeaddi váldi buot sámi beroštumiide guoskevaš áššiin.

#!! * **ND** Removes Nom	
REMOVE:ND Nom IF (0 HUMAN + Gen)(*1 BODY BARRIER NOT-NPMOD) ;
	#$ Moarsi oaivvis lea ruvdnu mas heaŋgájit guhkes hearvabáttit.
 
SELECT:ImprtSg2 Imprt (*-1 BOC BARRIER WORD)(0 Sg2 + TRANS-V LINK 1 Refl + PxSg2) ;
	#$ Suddje iežat ja geavat čalbmesujiid.
 
    
#!! * **ImprtAcc** removes Nom
REMOVE:ImprtAcc Nom IF (0 Acc LINK *-1 TRANS-V BARRIER NOT-NPMODADV OR @CVP);
	#$ Loga 2018 jahkehoroskohpa


#!! ## Comitative rules

#!! ### NP internal disambiguation of Com

#!! * **PlSg-W** removes Pl when SG-WORD
REMOVE:PlSg-W Pl IF (0 SG-WORD LINK NOT 0 VFIN)(NEGATE *-1 Ord BARRIER NOT-NPMOD)(*-1 BOS LINK NEGATE *1 PLURALIZER BARRIER S-BOUNDARY);
	#$ Go funet lea guolli goddomin, de fitná olmmoš buođu luhtte dušše oktii jándoris savdnjileamen joddu amas dat dierrut, suollemasat dainna doaivvuin ahte nago de lea boađistan.
	#$ Buoremus sámepolitihkka ožžojuvvo áigálii sámi veahkadaga ja ráđđehusa gaskaneas ipmárdusain.


#!! * **SgCom** removes Sg when PLURALIZER or OASSI OR HEADOFPARTS
REMOVE:SgCom1 (Sg Com)  IF (*-1 PLURALIZER OR OASSI OR HEADOFPARTS BARRIER NOT-NPMOD OR Num)(0 (Pl Loc) - SG-WORD );
	#$ Jus ulbmilat politihkain galget šaddat duohtan fertejit visot náššuvnnalaš unnitálbmogiid dárbbut ja beroštumit váldojuvvot vuhtii juohke oasis riikkas ja buot dásiin.
REMOVE:SgCom2 (Sg Com)  IF (*-1 OASSI BARRIER S-BOUNDARY OR Loc)(0 (Pl Loc) - SG-WORD );
	#$ Osiin gelbbolašvuođaguovddážiidda juolluduvvon ruđain sáhttá maid ruhtadit sadjásašgoluid hospiterema oktavuođas dahje lassigoluid, mat Kárášjogas / Leavnnjas šaddet joatkkaoahpus.
	#$ Nuba áddjá árvala ahte eiseválddit galggaše bálkáhit baicce vuorrolohkkiid, vai rehkenasto vuorruid mearri maiddái, iige dušše dat mearri guliin mii goddo deanus.
REMOVE:SgCom3 (Sg Com)  IF (-1 Sem/Plc + Gen LINK -1 PROSEANTA)(0 (Pl Loc) - SG-WORD) ;
	#$ Dutkamušat čájehit ahte olles 60 proseantta Norgga studeanttain eai birge ruđalaččat jus eai bargga seammás go studerejit.

#!! * **Locgoabbat** selects Pl Loc after goabbat Foc/ge
SELECT:Locgoabbat (Pl Loc)  IF (-1 ("goabbat") + Foc/ge)(0 (Sg Com)) ;
	#$ Goabbatge joavkkuin lonuhit čikčiid juohke viđát minuvttas.

#!! * **LocNames** selects Pl Loc 
SELECT:LocNames (Pl Loc)  IF (0 ("Davviriikkat")) ;


#!! * **NumCom** selects Num Com: guvttiin nieiddain if not plural-noun like: guvttiin heajain
SELECT:NumCom (Num Com)  IF (*1 Com BARRIER NOT-A LINK NOT 0 PL-N + Pl);  
	#$ Juhán vulggii viđain nieiddain.

#!! * **gástaCom** selects Com: Johánas gásta
SELECT:gástaCom Com  IF (0 ("gásta") LINK -1 ("Johanas"));  
	#$ Sii vástidedje: Johanasa gásttain.


#!! * **ComDemNum1** selects N Com if there is a Dem or Num or buorre + Com to the left: Exception for plural-nouns  
SELECT:ComDemNum1 (Sg Com) IF (0 (Pl Loc) LINK NOT 0 PL-N)(*-1 DAT-PRON + (Dem Com) OR (Num Com) OR ("buorre" Com) OR ("nubbi" Com) OR ("goabbá" Com) BARRIER NOT-A);
	#$ Romssa guovllubuohcceviessu lea bargamin dáinna áššiin.
	#$ Dainna sániin dadjat, de mearkkaša dat ahte ...
	#$ Ovttain sániin dadjat, de mearkkaša dat ahte ...
	#$ Mo sáhttá mottiin sániin dadjat...

#!! * **Comburiin** selects N Com if there is a safe N Com to the right: buriin vugiin  
SELECT:Comburiin (Sg Com) IF (0 (Pl Loc))(1C Com);
  	#$  Áššebealit galget juohke jagi šiehtadallat ekonomalaš doaimmaid, fágalaš sosiála áššiid ja eará gažaldagaid ja doaimmaid mat buriin vugiin ovdánahttet boazodoalu.


#!! * **ComCOM-A** selects Sg Com after COM-A
SELECT:ComCOM-A (Sg Com) IF (*-1 COM-A OR ("lihkku") BARRIER Com OR SV-BOUNDARY)(0 (Pl Loc));  
	#$ Ja son lei doppe oppa joavkkuinis.
	#$ Sii lohket iežaset leat oalle duhtavaččat dán jagáš básárdoaluin.


#!! * **Comduhtavas** selects Sg Com after duhtavaš
#SELECT:Comduhtavas (Sg Com) (*-1 ("duhtavaš") OR ("lihkku") BARRIER NOT-NPMOD)(0 (Pl Loc));  
	#$ Sii lohket iežaset leat oalle duhtavaččat dán jagáš básárdoaluin.

#!! * **ComComAdv1** selects Com after COM-ADV or juohke
SELECT:ComComAdv2 Com IF (*-1 COM-ADV OR ("juohke") BARRIER NOT-NPMOD);
	#$ Ii lean dárbu dábálaš eanavuovdimii, oktan opmodatregistreremiin. 

SELECT:ComFood Com IF (0 FOOD + (Pl Loc) LINK *0 ("vuostáiváldit")) ;
	#$ Dálueamit Ánná Nilsen vuostáiválddii njálgga mállásiin, - guossohii luosa ja dieđusge luopmániid.

SELECT:ComRapmi Com IF (0 ("rápmi") + (Pl Loc) LINK 0* ("čájehit")) ;

	
#!!	### ILLNESS-V
  	
SELECT:ComIllness (Sg Com) IF  (*-1 ILLNESS-V BARRIER SV-BOUNDARY)(0 ILLNESS);
	#$ Jus leimmet buohccán ovdamearkka dihtii beallje- dahje čottavulššiin, de manaimet guvhllára lusa gii "bosui" eret dávdda.


#!! * **vuoitit** select Com Sem/Time
SELECT:ComvuoititTime (Sg Com) IF (0 Sem/Time)((-1 ("vuoitit")) OR (-1 Num LINK -1 ("vuoitit")));
SELECT:ComvuoititAni (Sg Com) IF (0 (Sem/Ani-fish))((-1 ("vuoitit")) OR (-1 Num LINK -1 ("vuoitit"))  OR (1 ("vuoitit")) );
	#$ Gilosaš njágáin vuittii 15 000,-

SELECT:guovttis Com IF (0 HUMAN + (Pl Loc) LINK -1 GUOVTTIS) ;
	#$ Dát ordnašuvai, ja vuosttaš beaivve rájes leigga Sissel-guovttos Uhcaoappáin priváhtaoahpaheaddjin nubbi nubbái.

SELECT:ComOrth Com IF (0 ("čállinvuohki" Pl Loc) LINK *0 ("almmuhit") OR ("almmuhus") OR ("veršuvdna") OR ("olggosaddin")) ;
	#$ Dá lea ođđaset veršuvdna, dálá čállinvugiin.

SELECT:ComSemAct Com IF (0 Sem/Act + (Pl Loc) OR Der/NomAct + (Pl Loc))((*0 ("veahkki")) OR (*0 ("bargat") OR ("nákcet") OR COM-HEAD BARRIER SV-BOUNDARY)) ;
	#$ 

SELECT:ComSemAct Com IF (0  (Pl Loc))(0 ("veahkki") LINK -1 Gen) ;


SELECT:ComInstr (Sg Com) IF (0  Pl + Loc LINK 0 MUSIC-INSTRUMENT OR ("joavku") OR ("koarra") OR (Sem/Group_Hum) LINK *0 ("čuojahit") OR ("juoigat") OR ("lávlut")) ;
	#$ De son ozai eará sálmma ja galggai čuojahit urgguin.

#!! ### Disambiguation based upon verb valency

SELECT:com-head (Sg Com)(0 TOOL)((1 COM-HEAD) OR (-1 COM-HEAD)) ;
	#$ Lei álfárot gildojuvvon dolain duhkoraddamis, muhto moai ean goassege duostan luohttit dasa ahte oahppit čuvvo dán gildosa.

#!! * **comheaitit** select Sg Com if heaitit
SELECT:comheaitit (Sg Com)  IF (*-1 ("heaitit") BARRIER SV-BOUNDARY) ;


#!! * **LocLocVL1, LocLocVR** select Pl Loc if there is a LOC-V
SELECT:LocLocVL1 (Pl Loc) IF (NOT 0 VEHICLE OR Sem/Act) (*-1 LOC-V - ("heaitit") BARRIER SV-BOUNDARY OR COM-HEAD OR Loc - Attr LINK NOT 0 Adv)(NEGATE *-1 CC BARRIER S-BOUNDARY LINK -1 LOC-V)(NEGATE 0 Num OR Pron LINK *1C (N Com) BARRIER NOT-NPMOD)(0 Com OR Px) ;
	#$ Juohke dáhpáhusas ferte áibbas boastut leat beroškeahttá sámiid riekteáddejumiin.
	#$ Givssideapmi sáhttá boahtán das, go earránin nuppiin mánáin ja nuorain.
	#% Sihke mun ja mu oappát ja vieljat heittiimet etniin sámásteamis, in dieđe manne.

SELECT:LocLocVR (Pl Loc) IF (NEGATE 0 DAT-PRON OR Actio)(NEGATE *1C Com BARRIER NOT-NPMOD)(NEGATE 0 (Coll Nom) LINK 1 (Sg Com))(NEGATE 0 (Sg Com) LINK -1 (Coll Nom) OR ("buorre" Com))(*1 LOC-V BARRIER SV-BOUNDARY OR COMMA);
	#$ Maiddái sámi fágabirrasiin ja ásahusain sáhttá jearrat ráđiid.

#!! * **LLocAccLocVL** select Pl Loc if there is a ACC-LOC-V
SELECT:LocAccLocVL (Pl Loc) IF  (*-1 ACC-LOC-V BARRIER NOT-NP OR Loc - Attr)(NOT 0 TOOL)(NEGATE 1 (N Loc) OR (Actio Loc)) ;
	# Removed "viežžat" from ACC-LOC-V, because it can take Com: "Mun vižžen su biillain."

#!! * **Loc-v** select Sg Loc if LOC-V to the left in the clause. No mainverb to the right in the clause
SELECT:Loc-v (Sg Loc) (*-1 LOC-V BARRIER SV-BOUNDARY)(NEGATE 0 (Pron Dem) LINK 1 N)(NEGATE 0 (Pron Rel) OR Adv OR Sem/Fem OR Sem/Mal)(NEGATE *1 FMAINV BARRIER SV-BOUNDARY) ;
	#$ Son oaidná dili seavdnadin ja ballá heakka ala sisriikasoađis dahje dakkáris.
	#$ Eai suige stálut bala borggas.
	
#!! ### Disambiguation of Com depending on Adv or certain verb or N
  
#!! * **ComComAdv1** selects Com for ACTOR OR ACTOR-ROLE after og before COM-ADV 
SELECT:ComComAdv1 (Sg Com) IF (0 (Pl Loc) LINK 0 ACTOR OR ACTOR-ROLE)((-1 COM-ADV) OR (*1 COM-ADV BARRIER NOT-ADV));  
	#$ Leahppi go fárrolaga ádjáineatte?
	#$ Son fitná doppe juohke skuvllain.

#!! * **Comboahtit** selects riika Com when boahtit: boahtit riikkainis, which is a special construction
SELECT:Comboahtit (Sg Com) IF (0 ("riika") + Px LINK *0 ("boahtit") BARRIER SV-BOUNDARY) ;
	#$ Muhtumat eai muosát jápmima ovdal go oidnet Olbmobártni boahtimin riikkainis.

#!! * **Comjohtit** selects bihttá and čájálmas and čájáhus Com 
SELECT:Comjohtit (Sg Com) IF (0 ("bihttá") OR ("čájálmas") OR ("čájáhus") LINK *0 ("johtit") BARRIER SV-BOUNDARY) ;
	#$ Dál lea Beaivváš Sámi Našunálateáhter johtigoahtán iežaset ođđa bihtáin, «Johan Turi».


#!! * **Comnamma** selects namma  Com 
SELECT:Comnamma (Sg Com) IF (0 ("namma") LINK *-1 Prop BARRIER WORD) ;
	#$ Son šattai dovddusin «Hårfagre» namain.

#!! * **Combealli** selects riika Com when boahtit: boahtit riikkainis, which is a special construction
SELECT:Combealli (Sg Com) IF (0 ("bealli") OR Num OR Sem/Curr OR PROSEANTA) ((-1 ("unnut") OR ("lassánit") OR ("loktanit") OR ("stuorrut")   OR ("njiedjat") OR ("juohkit")) OR (*-1 ("unnut")  OR ("stuorrut") OR ("lassánit") OR ("loktanit")  OR ("njiedjat") OR ("juohkit") BARRIER WORD - Num) OR (1 ("unnut") OR ("lassánit") OR ("stuorrut")  OR ("loktanit") OR ("njiedjat") OR ("juohkit"))) ;
	#$ Rievssatbivdu unnon beliin.
	#$ Ruhtadeapmi lassána 2,6 miljovnnain.

    
#!! * **ComComplPl-N** selects Sg Com for HUMAN, ORGANIZATION, INSTITUTION, STATE, EVENT-TOOL-ACTIVITY, láhka when there is a COM-COMPL-N to the left or right
#SELECT:ComComplPl-N (Sg Com) IF (*0 COM-COMPL-N BARRIER SV-BOUNDARY)(0 HUMAN OR ORGANIZATION OR INSTITUTION OR STATE OR EVENT-TOOL-ACTIVITY OR ("láhka") OR Pron)(*0 REAL-V BARRIER SV-BOUNDARY) ;
  	#$  Dearvvašvuođabargiid doaibma adnojuvvo priváhtan, vaikke vel dearvvašvuođabargiin lea fástadoavtteršiehtadus dahje eará šiehtadus almmolašvuođain, muhto eai leat dattetge virggis almmolaš ásahusas.
	#$ Das lei ee. oktavuohta čakčamánu 7. b. 1854 lágain.
	#$ Várrepresideanta lea maid 05.01.00 leamaš čoahkkimis Sosiála- ja dearvvasvuođadepartemeanttain čilgen dihte Sámedikki juolludemiid sámi dearvvasvuođa- ja sosiálaplána čuovvuleamis .
	#$ Vuojadansearvi váldá din fitnodagain oktavuođa vahkku 20:s, gullat mo dii sáhttibehtet veahkehit.
	#% Giellabargu mánáidgárddis gáibida ollu barggu sihke bargiin ja vánhemiin.
	#% Váimmolaš dearvuođat mánáin, vivain, manjiin, mánáidmánáin ja máttarmánáin.
	
#!! * **Comoktavuohta** selects Sg Com when oktavuohta is to the left or right
SELECT:Comoktavuohta Com IF (*0 ("oktavuohta") BARRIER S-BOUNDARY OR COPULAS OR N);
	#$ Vuoigatvuohta oktavuođa váldit divššohasbearráigeahččiin.
 
#!! * **ComDU-NR** selects Sg Com after Pers dualis: moai áhčiin, munno vieljain
#SELECT:ComDU-NR (Sg Com)  IF ((-1 $$DU-NR + Pers LINK *1 Com BARRIER NOT-A LINK *1 $$DU-NR + V BARRIER SV-BOUNDARY OR CC) OR (-1 (Pers Gen) + DU-NR LINK 1 N LINK 1 N) OR (-1 BOC LINK 2 VFIN + DU-NR));
SELECT:ComDU-NR (Sg Com)  IF (0 HUMAN - Prop LINK *-1 DU-NR + Pers BARRIER NOT-A) ;
	#$ Moai áhčiin manaime dohko.
	#$ Dat lea munno vieljain dállu.
	#$ Son lei iežas isidiin fárrolaga vázzime luonddubálgá , go isidiinnis gulaiga imaš jienaid , jáhkkimis guovžža jienaid.
	#$ Munnos isidiin lei illun sin oažžut ustibin nuorravuođa rájes juo.

SELECT:PropFamily (Sg Com)  IF (0 N-PX OR ("joavku") OR ("bearaš") OR ("veahka") LINK *-1 Sem/Mal OR Sem/Fem BARRIER NOT-A LINK *-1 BOS BARRIER WORD - Prop) ;	
	#$ Biehtar-Uvllá nieiddainis.
      
#!! * **ComHumanOrg** selects HUMAN Sg Com after HUMAN, ORGANIZATION, INSTITUTION
SELECT:ComHumanOrg (Sg Com) IF (*-1 HUMAN OR ORGANIZATION OR INSTITUTION BARRIER NOT-NPMOD LINK 0 Nom LINK NOT 0 LOC-HEAD OR A)(0 (Pl Loc) + HUMAN)(*1 VFIN OR PrfPrc BARRIER WORD);
	#$ Norgga delegašuvdna, várrepresideanttain, doalai sártni dievasčoahkkimii čoahkkima loahpahettiin, mas doarjjui WHO boahtteáiggi barggu dearvvasvuođain.
  	
#!! ### Animate nouns
#!! * **ComAnimate** selecst Sg Com if there is an animate to the left, and the noun itself is not a ABSTR-TEXT,  TEXT,  PLACE,  INDUSTRY,  EDUCATION,  INSTITUTION,  ANIMATE
#SELECT:ComAnimateL (Sg Com)  IF (*-1 ANIMATE BARRIER NOT-NPMOD LINK 0 Nom OR Gen)(0 (Pl Loc) LINK NOT 0 ABSTR-TEXT OR TEXT OR PLACE OR INDUSTRY OR EDUCATION OR INSTITUTION OR ANIMATE)(1 CLB OR EOS)(NEGATE 0 (Pl Loc) LINK *-1 OKTA BARRIER NOT-NPMOD);   
	#$ Sii vástidedje: Johanasa gásttain.  
  	#$  1996:s rájes lea Romssa universitehtas leamaš Sámi dutkamiid guovddáža bokte formaliserejuvvon ovttasbargu St. Petersburga Hertzen universitehta Davviálbmogiid fakultehtain.
    # Does it really need (1 CLB OR EOS)?

#!! * **ComProp** selecst Prop Sg Com for person names. Exception for habitive constructions.
SELECT:ComProp Prop + Sg + Com IF (0 Sem/Mal OR Sem/Fem OR Sem/Sur OR Sem/Ani)(NEGATE 0 Loc LINK 1 COPULAS + Sg3 OR COPULAS + Pl3 LINK *1 Nom BARRIER NOT-NPMODADV) ; 
	#$ Nystø mielas leaba soai Lene Hanseniin rievttes olbmot ovddastit sámi álbmoga dáhtu Tromssa ohcamušas.
	#$ Moai Birehiin barggašeimme mánáidgárddis.
	#$ Moai Sisseliin leimme áidna oahpaheaddjit vuosttaš jagiid.

  

#!! ### HAB-ACTOR in habitive-constructions
	
#!! * **LocHab1, LocHab2** select Pl when HAB-ACTOR
SELECT:LocHab1 (Pl Loc) IF (0 (Sg Com) + HAB-ACTOR) ((*1 LEAT-FIN-NOT-IMPRT BARRIER SV-BOUNDARY LINK *1 NP-HEAD-NOM BARRIER NOT-NPMOD) OR (*-1 go + Qst LINK -1 AUX LINK *1 HAB-ACTOR LINK 1 ("leat" Inf)))(NEGATE *0 COM-V BARRIER SV-BOUNDARY) ;
	#$ Divššohasain lea vuoigatvuohta dearvvašvuođaveahkkái dan áigodagas. 
	#$ Searvegottiin lei dál ráfi miehtá Judea ja Galilea ja Samaria.
	#$ Galgá go báikkálaš olbmuin leat vuosttašvuoigatvuohta luondduriggodagaide?
	
SELECT:LocHab2 (Pl Loc)  IF (*-1 (Pron Rel) BARRIER NOT-NPMOD LINK -1 NP-HEAD-NOM)(0 (Sg Com) LINK 0 HAB-ACTOR)(*1 LEAT-FIN-NOT-IMPRT BARRIER SV-BOUNDARY LINK NEGATE *1 PRC-NOT-LEAT);
	#$ Orru čielga vuohki: Dat friddjavuohta mii guođoheddjiin dássážii lei leamaš, gáržžiduvvui dađistaga. 

#!! * **LocHab1, LocHab2** select Pl when HAB-ACTOR
SELECT:LocGenerell (Pl Loc) IF (0 (N Sg Com)) ((*1 LEAT-FIN-NOT-IMPRT BARRIER SV-BOUNDARY LINK *1 NP-HEAD-NOM BARRIER NOT-NPMODADV) OR (*1 Neg BARRIER NOT-ADV-PCLE LINK 1 ("leat" ConNeg) + FMAINV LINK *1 NP-HEAD-NOM BARRIER NOT-NPMODADV) OR (*-1 go + Qst LINK -1 AUX LINK *1 HAB-ACTOR LINK 1 ("leat" Inf)))(NEGATE *0 COM-V BARRIER S-BOUNDARY)(NEGATE 0 TOOL LINK *0 HUMAN-ACTIVITY-V BARRIER S-BOUNDARY) ;
SELECT:LocGenerell (Pl Loc) IF (0 (N Sg Com)) (*-1 LEAT-FIN-NOT-IMPRT BARRIER NOT-NPMOD)(*1 NP-HEAD-NOM BARRIER NOT-NPMODADV)(NEGATE *0 COM-V BARRIER S-BOUNDARY)(NEGATE 0 TOOL LINK *0 HUMAN-ACTIVITY-V BARRIER S-BOUNDARY) ;
	#$ Divššohasain lea vuoigatvuohta dearvvašvuođaveahkkái dan áigodagas. 
	#$ Searvegottiin lei dál ráfi miehtá Judea ja Galilea ja Samaria.
	#$ Galgá go báikkálaš olbmuin leat vuosttašvuoigatvuohta luondduriggodagaide?
	#$ Muhtun guovlluid gávttiin eai leat nu ollu čiŋat oskku geažil.
	 #$ Manne dis leat diekkár bahás jurdagat din váimmuin?
	
#!! * **LocGenerell** select Pl 
SELECT:LocGenerell (Pl Loc)  IF (*-1 (Pron Rel) BARRIER NOT-NPMOD LINK -1 NP-HEAD-NOM)(0 (N Sg Com))(*1 LEAT-FIN-NOT-IMPRT BARRIER SV-BOUNDARY LINK NEGATE *1 PRC-NOT-LEAT);
	#$ Dálveskuovain ledje stállerávddat ja go buot oahppit vihke feaskáriin, de sáhtten gullat ahte čolkkii.

#!! váldit vára + Loc
SELECT:Locválditvára (Pl Loc)  IF 
(*-1 ("vára" Adv) BARRIER NOT-NPMODADV LINK -1 ("váldit")) ;
	#$ Go bohcco lea njuovvan, de váldá vára čoliin oahppan dihte iešguđet suolbmudanoali osiid birra.

#!! dahkat earrodearvvuođat geainna nu
SELECT:Comdearvvuođaid (Sg Com) IF (0 HUMAN) ((-1 ("dearvvuohta" Acc) OR ("earrodearvvuođat" Acc) LINK -1 ("dahkat")) OR (-1 ("dahkat") LINK -1 ("dearvvuohta" Acc) OR ("earrodearvvuođat" Acc)) OR (1 ("dahkat") LINK 1 ("dearvvuohta" Acc) OR ("earrodearvvuođat" Acc))) ;
	#$ Dát lei vuogas vuohki maiddái ohppiide - earrodearvvuođaid dahkat ustibiin ja skuvlaskihpáriin.

#!! eallit mainna nu
SELECT:Comeallit (Sg Com) IF (0 OTHER-INDUSTRY LINK *0 ("eallit") OR ("heaitit") BARRIER SV-BOUNDARY) ;

#!! ### Disambiguation based upon verb valency 

#!! ### COM-V 
#!! * **ComVR, ComVL** select Com when COM-V
SELECT:ComVR (Sg Com)  IF (*-1 COM-V + REAL-V BARRIER SV-BOUNDARY OR Com - Num - Dem)(NEGATE 0 PLACE OR SITUATION)(NEGATE 0 ORGANIZATION LINK *-1 ("álggahit")) ;
	#$ Dieđut galget meannuduvvot várrugasvuođain.
	#$ Sámediggi lea váldán badjelasas váldoovddasvástádusa sihkkarastimis doaibmadoarjaga Nuortasámi museai (vrd. čoahkkimiin Njávdámis 05.06.01).
	#$ Sii čuvvot sámilága mearrádusaid, veardit siskkáldas goziheami geatnegasvuođain.
	#% Nissonat barget gávppiin. 
	#% Nu lei maiddái álkit gulahallat ja soabadit singuin skuvladiimmuin.
	
SELECT:ComVL (Sg Com)  IF (*-1 COM-V + REAL-V OR ("geargat") + REAL-V BARRIER SV-BOUNDARY)(0 POLITICAL-PLACE OR EDUCATION OR INSTITUTION OR INDUSTRY OR ORGANIZATION)(NEGATE 0 ORGANIZATION LINK *-1 ("álggahit")) ;
	#$ Sii áigot šiehtadit ođđasit skuvllain. 

SELECT:ComVX (Sg Com)  IF (*1 COM-V + REAL-V BARRIER SV-BOUNDARY)(NEGATE *1 COPULAS BARRIER REAL-V LINK *1 COM-V + Inf BARRIER NOT-ADV-PCLE)(NEGATE -1 OKTA LINK -1 COPULAS) ;

#!! * **ComVOktiiL** select Com when OKTII-V 
SELECT:ComVOktiiL (Sg Com) IF  (*-1 ("oktii") BARRIER NOT-NPMODADV LINK *-1 OKTII-V OR COM-V BARRIER NOT-ADV-PCLE); # (0 N BARRIER Loc);
	#$ Mii heive bures oktii ránes datneláiggiin?
  # we have to think about a barrier, another Loc to the right for example
  
#!! * **ComVOktiiR** select Com when OKTII-V 
SELECT:ComVOktiiR (Sg Com)  IF (1 OKTII-V LINK 1 ("oktii") LINK NEGATE *1 Com BARRIER S-BOUNDARY);
	#% Porsáŋggu loguin leat buot klássaid submi biddjon oktii.
  
#!! ### tools (concrete and abstract)

#!! * **ComTool1, ComTool2, ComToolCoord** select Com TOOL when ACTIVITY-V, MOVEMENT-V, PLACE-V-V 
SELECT:ComTool1 (Sg Com) + TOOL  IF (*-1 ACTIVITY-V OR MOVEMENT-V OR PLACE-V BARRIER SV-BOUNDARY) ;
	#$ Dan moai ráidniime skárbbaniin.
  
SELECT:ComTool2 (Sg Com) + TOOL IF  (*1 ACTIVITY-V OR MOVEMENT-V OR PLACE-V BARRIER NOT-ADV);
	#$ Mon in leat dugo Sivvar - mii láve bissuin ja beatnagiin mannat dugo livččii dáčča.
  
SELECT:ComToolCoord (Sg Com) + TOOL  IF (1 JA/DAHJE LINK *1 Com BARRIER NOT-NPMODADV)(*1 ACTIVITY-V OR MOVEMENT-V);
	#$ Mon in leat dugo Sivvar - mii láve bissuin ja beatnagiin mannat dugo livččii dáčča.
  
#!! * **ComHuman** selects Com ABSTR-TOOL OR SATNI when HUMAN-AGENT-V - does it function? 
SELECT:ComHuman (Sg Com)  IF (*-1 NP-HEAD-NOM + HUMAN BARRIER S-BOUNDARY)(0 ABSTR-TOOL OR SATNI)(*0 HUMAN-AGENT-V BARRIER SV-BOUNDARY);
	#$ Soađis ja ráfis presideanta mearrida miehtamiin.
	
#!! ### BODY as an instrument

#!! * **ComBodyVerbalV** selects Com BODY when VERBAL-ACTIVITY-V 
SELECT:ComBodyVerbalV (Sg Com) + BODY  IF (*-1 VERBAL-ACTIVITY-V BARRIER VFIN OR LOC-HEAD)(0 (Pl Loc));
	#$ Son rávvii buohkaid bissut Hearrá luhtte duođalaš váimmuin.
  # Idea: Body as an instrument to perform verbal activity.
  
#!! * **ComHumanVerbalV** selects Com HUMAN when VERBAL-ACTIVITY-V or báhcit
SELECT:ComHumanVerbalV (Sg Com) IF (0 HUMAN OR ("media") OR Sem/Ani) ((*-1 VERBAL-ACTIVITY-V OR ("báhcit") BARRIER VFIN OR LOC-HEAD OR (Dem Pl Loc)) OR (1 VERBAL-ACTIVITY-V))(0 (Pl Loc))(NEGATE -1 Num - Com);
	#% Máŋga jagi dan maŋŋil muitalii muhtin dain ohppiin munnje ahte son ii lean oahppan maidege.

#!! * Abstract-entity-com-verbs

#!! * **ComAbstract** selects Com if ABSTR-ENTITY-COM-V somwhere
SELECT:ComAbstract (Sg Com)  IF (*0 ABSTR-ENTITY-COM-V BARRIER SV-BOUNDARY)(0 (Pl Loc) LINK 0 ABSTR-ENTITY OR ACTOR);
	#$ Mii leat lihkostuvvan bures prošeavttain.

#!! * **ComOnlyPlaceV** is Only-place-loc-verb 
SELECT:ComOnlyPlaceV (Sg Com) IF (0 (Pl Loc) LINK NOT 0 PLACE OR ORGANIZATION OR HUMAN OR MATERIAL)(*0 ONLY-PLACE-LOC-V BARRIER SV-BOUNDARY LINK 0 FMAINV OR Inf);
	#$ ...go sápmelaččat duođas álge eanadoaluin bargat.
    # It can be ambiguity with ORGANIZATION.
 
#!! * **ComMaterial** selects Com Sem/Mat when some verbs 
SELECT:ComMaterial (Sg Com)  IF (0 Sem/Mat OR Sem/Substnc) (*-1 ("loavdit") OR ("gokčat") BARRIER VFIN OR LOC-HEAD)(0 (Pl Loc)) ;
	#$ Dan sáhttá maid loavdit bessiin ja darffiin.
  

LIST SEAHKKA = (".*seahkka"r) "lávka" (".*lásta"r) "rehppo" ;

SELECT:ComMovemVL (Sg Com)  IF (*-1 MOVEMENT-V BARRIER SV-BOUNDARY OR ("diehtu") LINK 0 FMAINV OR Inf LINK NOT 0 ("boahtit"))(0 (Pl Loc) + SEAHKKA);
	#$ Sudno geažil sáhtiime vuolgit Sirbmái veaháš báikkálaš diehtoseahkain ja ollu ráđiiguin.
  
  
#!! ### Dynamic-verbs
 
#!! * **LocdynamicVR, LocdynamicVL** select Pl Loc if there is a DYNAMIC-V and the noun itself is not a TOOL, ABSTR-TOOL, WRITING-TOOL, CONCEPT, HUMAN, VEHICLE, buorre, Der/NomAc
 
SELECT:LocdynamicVR (Pl Loc) IF  (*-1 DYNAMIC-V - ABSTR-ENTITY-COM-V OR ("dahkat") + Der/Pass BARRIER SV-BOUNDARY OR COM-V OR COM-HEAD OR (Pl Com) OR COM-ADV LINK 0 FMAINV OR Inf)(0 (Sg Com) LINK NOT 0 TOOL OR ABSTR-TOOL OR WRITING-TOOL OR CONCEPT OR SATNI OR ANIMATE OR VEHICLE OR BODY OR CLOTHES OR ("buorre") OR Der/NomAct);
	#$ Son beastá álbmogis sin suttuin.
	#$ Ikte moai finaime golmmain mállásiin.
	#$ Min mielas goit lea veara jurddašit nissongova birra mii boahtá ovdan jearahallamiin.
	#$ Lávdde luhtte juiget olbmot olles čoddagiin.
	#$ Terje hárve hupmá mediain, muhto son áinnas hupmá sápmelašvuođas birra.
	#$ Son ii daja njuolga rievttes namain.  
	#$ Dan geažil lea dehálaš ávžžuhit bargiid sámástit ráđđádallamiin.
	#$ Sii máinnašuvvojedje vearrologahallamiin earáid maŋŋá. 
	#$ Dálvet golggadettiin lea guovttegeardán skoađas birra gietkama, vuohččan lea sisti birra, mii gal adnojuvvo geasset ja dálvet, muhto dálvet adnojuvvo nubbi skoađas nalde, mii lea dahkkojuvvon gápmasiin. 
	#$ Oahppit bohte dábálaččat vuođđoealáhusain.
	#% Dan sáhttá maid hukset bessiin ja darffiin.
 # ABSTR-ENTITY-COM-V

SELECT:LocdynamicVL (Pl Loc) IF (0 (Sg Com) LINK NOT 0 TOOL OR ABSTR-TOOL OR WRITING-TOOL OR CONCEPT OR HUMAN OR VEHICLE OR BODY)(*1 DYNAMIC-V BARRIER SV-BOUNDARY LINK 0 FMAINV OR Inf);
	#$ Sámi guovlluin ožžo Sámeparlameantta Suoma beallái jagi 1976. 
  	#$ Mánáidgirjjiin muitaluvvo dávjá govt olbmuid mánát deivet nuppi máilmmi ássiid.
	#$ Guovlluid dearvvašvuođafitnodagain gáibiduvvojit dakkár rutiinnat.
	#$ Gielddat berrejit dearvvašvuođastašuvnnain deattuhit mánáid ja nuoraid váttisvuođaid ovddalgihtii eastadeami ja psykososiálabarggu mánáid ja nuoraid váras.
	#$ Dálá sámiid máidnasiin ja muitalusain muitaluvvo dološ oskui gullan hálddiid birra .
	#$ Muhto mii diehtit ahte fágagirjjit leat divrasat ja diibmá jagi bušeahtain ii sáhttán investeret.


	#$ Muhto Ipmil lea seammá sániin vurken dáláš almmiid ja eatnama duopmobeaivvi dola várás, go ipmilmeahttumat duššaduvvojit. #skal ikke treffe guvssiin
  	#$ Ja ovdal go háhppehin basttiin goivestit eret de Ánddar guvssiin bođii. #skal ikke treffe guvssiin
  	   	 
	
#!! ### Event-tool-actio
#!! Most actio can be both tool and event. 

	
#!! ### PLACE-V

#!! * **LocFurniture** select Pl Loc FURNITURE if there is a PLACE-V
SELECT:LocFurniture (Pl Loc)  IF (*-1 PLACE-V BARRIER SV-BOUNDARY)(0 FURNITURE);

#!! * **ComPlaceV** select Com ANIMATE, CONCEPT, TOOL, ABSTR-TOOL, EVENT-TOOL-ACTIVITY if there is a PLACE-V
SELECT:ComPlaceV (Sg Com)  IF (*0 PLACE-V BARRIER SV-BOUNDARY)(0 (Pl Loc) LINK 0 ANIMATE OR CONCEPT OR TOOL OR ABSTR-TOOL OR EVENT-TOOL-ACTIVITY);
	#$ Soai leigga čohkkámin fatnasis áhčiineaskka čiktimin firpmiid. 
	 #$ ..gohčodedje maid earáid go sámesogat olbmuid, geat orro sámesiiddain.
	 #$ Gávpotbussiin olbmot dávjá fertejit čuožžut.

#!! * **HumPxComPlaceV**  
SELECT:HumPxComPlaceV (Sg Com)  IF (*0 PLACE-V OR ("leat") + FMAINV BARRIER SV-BOUNDARY)(0 (Pl Loc) + FAMILY);
	#$ Son lei ruovttus etniinis.
SELECT:HumPxComFamily (Sg Com) IF (*-1 VFIN + Sg3 BARRIER VFIN OR ("diehtu") + Acc)(0 (Pl Loc) + FAMILY - ("váhnen"));
	#$ Sáriggá jugada káfe etniinis.
	#% Giellabargu mánáidgárddis gáibida ollu barggu sihke bargiin ja vánhemiin.
#SELECT:HumPxComXGOFamily (Sg Com) IF (-1 HUMAN LINK -1 XGO LINK *-1 VFIN + Sg3 BARRIER VFIN)(0 (Pl Loc) + FAMILY);

#!! * **HumPxComPlaceV**  
SELECT:HumPxComPlaceV (Sg Com)  IF (0 (Pl Loc) + FAMILY LINK -1 Sem/Time + Gen);
	#$ vihtta vahku vieljain.

  	
#!! * **LocInstitution** select Loc INSTITUTION if there is a ABSTR-PLACE-V
SELECT:LocInstitution (Pl Loc)  IF (*-1 ABSTR-PLACE-V BARRIER SV-BOUNDARY)(0 INSTITUTION);
	#$ Go sápmelaččat ohcet fuolahanbálkká lagaš olbmuideaset dikšumii, de galgá gielda juolludeamis váldit vuhtii, váilugo bálvalusain giella ja kulturgelbbolašvuohta.
  
#!! * **LocPlaceIndustry** select Loc GEOGRAPHICAL-PLACE if there is a INDUSTRY to the right
SELECT:LocPlaceIndustry (Pl Loc)  IF (*-1 INDUSTRY BARRIER NOT-NPMOD)(0 GEOGRAPHICAL-PLACE);
	#$ Guollebivdu jogain ja jávrriin.
	
#!! * **LocSourceVR** select (Pl Loc)  
SELECT:LocSourceVR (Pl Loc) IF (0 HUMAN OR ORGANIZATION)(*1 SOURCE-V BARRIER SV-BOUNDARY LINK NOT 0 Adv)(NEGATE *-1 CC BARRIER S-BOUNDARY LINK -1 LOC-V);

SELECT:LocSourceVL (Pl Loc) IF  (*-1 SOURCE-V BARRIER SV-BOUNDARY LINK NOT 0 Adv)(NEGATE *-1 CC BARRIER S-BOUNDARY LINK -1 LOC-V)(0 HUMAN OR ORGANIZATION); 
SELECT:LocHumanAgVR (Pl Loc)  IF (*-1 NP-HEAD-NOM LINK 0 HUMAN OR ORGANIZATION OR (Pron Rel))(0 ORGANIZATION OR HUMAN OR EVENT LINK 0 (Sg Com) LINK NOT -1 PRON-DU)(*1 HUMAN-AGENT-V - Actio LINK NEGATE *1 N BARRIER S-BOUNDARY OR COMMA);

#!! * **LocHumanAgVL** XXX This one was commented out (cf. 0 .. LINK ... BARRIER). Note that this rule did not affect the test result
#SELECT:LocHumanAgVL (Pl Loc) IF  (*-1 HUMAN-AGENT-V - Actio LINK NOT 1 N)(0 ORGANIZATION OR EVENT LINK 0 (Sg Com) BARRIER S-BOUNDARY OR COMMA LINK NOT -1 PRON-DU);
	#$ Departemeanta áigu ovttasbargat Sámedikkiin ja movttiidahttit gielddaid ovddidit doaimmaid, mat nannejit sámi mánáid ja nuoraid identitehta ja oktiigullevašvuođa sámi servodagain. # servodagain
  # This rules assumes that abstract activities are performed with a tool but not with the help of others in comitative case. The set of abstract-activity verbs might have to be restricted to those verbs that do have these features.
  
#!! * **LocHuman-agentV** XXX This one was commented out (cf. 0 .. LINK ... BARRIER). Note that this rule did not affect the test result
#SELECT:LocHuman-agentV (Pl Loc) IF (0 ORGANIZATION OR HUMAN OR EVENT LINK 0 (Sg Com) BARRIER S-BOUNDARY OR COMMA LINK NOT -1 PRON-DU)(*1 HUMAN-AGENT-V - Actio LINK NOT 1 N);
    	
#!! ### STATE-V (eallit)

SELECT:StateV (Pl Loc) IF (*0 STATE-V BARRIER SV-BOUNDARY)(0 (Sg Com) LINK 0 STATE);
	#$ Olbmot geat ellet náitoslágan dahje guimmešvuođalágan gaskavuođain.
	#% Buolvvaid gaskkas leat stuora erohusat, vuorrasat leat eallán vuođđoealáhusain ja dujiin.
  	
  	
#!!	### Periphrastic constructions
    
SELECT:ComActivityV (Sg Com) IF  (*-1 ACTIVITY-A BARRIER NOT-NPMOD LINK *-1 REAL-COPULAS BARRIER S-BOUNDARY OR REAL-V)(0 ANIMATE OR TOOL);
	#$ Mun ledjen hilbat mánáin.

#!!	### Idiomatic expressions

SELECT:ComOaivil (Sg Com)  IF (*-1 ("oaivil" Loc) BARRIER NOT-NPMOD)(0 TEXT OR ABSTR-TEXT OR ACTOR-ROLE);
	#$ Jus lea sáhka 12 jagi deavdán mánás, gii ii leat ovtta oaivilis mearrádusain, gozihanlávdegoddi galgá gieđahallat mearrádusa.

SELECT:ComUlbmil (Sg Com) IF (*-1 ("ulbmil" Nom) BARRIER NOT-NPMOD)(0 TEXT OR ABSTR-TEXT OR ("prošeakta") OR Sem/Prod-ling);
	#$ Eará guovddáš ulbmil prošeavttain lea fuomášahttit dan máhttodárbbu maid riikkaidgaskasaš ja sisriikkalaš birasulbmilat bohciidahttet.
   
   
#!!	### Verbal-activity-verbs

SELECT:ComVerbalactV- (Sg Com) + SATNI IF  (*0 VERBAL-ACTIVITY-V BARRIER SV-BOUNDARY LINK 0 FMAINV OR Inf)(0 (Pl Loc));   
    
    
SELECT:LocVerbalactV (Pl Loc) IF  (*-1 VERBAL-ACTIVITY-V BARRIER SV-BOUNDARY LINK 0 FMAINV OR Inf)(0 Com LINK 0 EVENT OR EVENT-TOOL-ACTIVITY);
  
SELECT:LocText (Pl Loc) IF  (NEGATE *-1 COM-HEAD BARRIER NOT-NPMOD)(0 TEXT OR ABSTR-TEXT)(*0 VERBAL-ACTIVITY-V BARRIER NOT-NPMOD LINK 0 Der/Pass );
  	
SELECT:LocWeather- (Pl Loc)  IF (*-1 PERCEPTION-V OR VERBAL-ACTIVITY-V BARRIER SV-BOUNDARY)(0 WEATHER-ENTITY OR SITUATION);

#!! ### Movement-verbs

SELECT:ComboahtitR (Sg Com) IF (*-1 ANIMATE OR PPRON-NOM-NOT-DAT BARRIER S-BOUNDARY LINK *1 ("boahtit") LINK 0 FMAINV OR Inf)(0 (Pl Loc) LINK NOT 0 PLACE OR ABSTR-PLACE OR EVENT-TOOL-ACTIVITY OR Sem/Group);
	#$ Son bođii biillain. 
	#$ Son bođii politihkkariin.
SELECT:ComboahtitV- (Sg Com) IF (0 (Pl Loc) LINK NOT 0 PLACE OR ABSTR-PLACE OR EVENT-TOOL-ACTIVITY)(*1 ANIMATE OR PPRON-NOM-NOT-DAT BARRIER NOT-NPMOD LINK *1 ("boahtit") BARRIER SV-BOUNDARY LINK 0 FMAINV OR Inf);

SELECT:ComMovemVL (Sg Com)  IF (*-1 MOVEMENT-V BARRIER SV-BOUNDARY OR ("diehtu") LINK 0 FMAINV OR Inf)(NEGATE 0 HUMAN LINK *-1 ("boahtit"))(0 (Pl Loc) LINK NOT 0 PLACE OR ABSTR-PLACE OR EVENT-TOOL-ACTIVITY);
	#$ Máhtte vujii biillain.
	#% Sáhttá viežžat dieđuid divššohasa oapmahaččain. 

SELECT:ComMovemVR (Sg Com) IF (0 (Pl Loc) LINK NOT 0 PLACE OR ABSTR-PLACE OR EVENT-TOOL-ACTIVITY)(*1 MOVEMENT-V BARRIER SV-BOUNDARY LINK 0 FMAINV OR Inf LINK NOT 0 ("boahtit")) ;

SELECT:LocMovemVL (Pl Loc) IF (*-1 ANIMATE OR PPRON-NOM-NOT-DAT BARRIER S-BOUNDARY LINK *-1 MOVEMENT-V LINK 0 FMAINV OR Inf)(0 (Pl Loc) LINK 0 PLACE OR ABSTR-PLACE OR EVENT-TOOL-ACTIVITY);
SELECT:LocMovemVR- (Pl Loc) IF (0 (Pl Loc) LINK 0 PLACE OR ABSTR-PLACE OR EVENT-TOOL-ACTIVITY)(*1 ANIMATE OR PPRON-NOM-NOT-DAT BARRIER NOT-NPMOD LINK *1 MOVEMENT-V BARRIER SV-BOUNDARY LINK 0 FMAINV OR Inf LINK *1 VEHICLE OR ANIMATE BARRIER S-BOUNDARY);  	
	#$ Suovva bođii biillain.	 #rule doesn't hit this one

SELECT:LocboahtitVL (Pl Loc)  IF (*-1 NP-HEAD-NOM BARRIER S-BOUNDARY LINK 0 CONCEPT LINK 1 ("boahtit"))(0 (Pl Loc) LINK NOT 0 TOOL OR ABSTR-TOOL);
	#$ Evttohus bođii politihkkariin.
SELECT:LocConcept (Pl Loc) IF (0 (Pl Loc) LINK NOT 0 TOOL OR ABSTR-TOOL)(*1 NP-HEAD-NOM BARRIER NOT-NPMOD LINK 0 CONCEPT);
	#$ Evttohus bođii suohkana politihkkariin.
    
    
#!! ### The super-set Dynamic-verb according to choose (Pl Loc) or (Sg Com)

#!! The idea is that the superset DYNAMIC-V are not connected to TOOL, ABSTR-TOOL or CONCEPT in (Pl Loc). This is the "minste felles multiplum". The sub-sets are different, f.i. many of them (but not all) are not connected to HUMAN in (Pl Loc), one is not connected to ABSTR-ENTITY and ACTOR in (Pl Loc). We work with negation so the rules don´t destroy analysis because of insufficent sets. 

#!! First the general-rules for selecting (Sg Com), then the more special rules for selecting (Sg Com), and then we selct (Pl Loc) for the rest of them under # Another round of locative rules.
   
    
#!! * **ComDynV** Dynamic-verbs selects Com when TOOL, ABSTR-TOOL, WRITING-TOOL, CONCEPT, EVENT-TOOL-ACTIVITY
SELECT:ComDynV (Sg Com) IF (*0 DYNAMIC-V - LOC-V OR ("bargat") BARRIER S-BOUNDARY LINK 0 FMAINV OR Inf)(0 (Pl Loc) LINK 0 TOOL OR ABSTR-TOOL OR WRITING-TOOL OR CONCEPT OR EVENT-TOOL-ACTIVITY);
	#$ Geahččoeiseválddit galget bearráigeahčuin váikkuhit dasa ahte dearvvašvuođa- ja sosiálabálvalusain váldet vuhtii geavaheaddjiid vuoigatvuođa oažžut áddehahtti informašuvnna ja vuoigatvuođa informerejuvvon miehtamii, ja fágalaččat dohkálaš doaimmaid giellagáibádusaid.
	#$ Gittabivdosiin ii leat lohpi bivdit deanu eambbo go beallái.
	#% SDD lea nammadan guovlluid dearvvašvuođafitnodagaid stivrraid, mat vástidit doaimmaineaset dušše stáhtii, mii daid eaiggáduššá.
    # "Minste felles multiplum" for the dynamic verbs. The same rules come the other way round after the comitative-chapter - after we have picked out the special cases.   
    

#!! * Dynamic-verb selects Com when HUMAN, but not for HUMAN-SOURCE-VEHICLE-V 
#SELECT:ComDynamisV (Sg Com) + HUMAN (*0 DYNAMIC-V BARRIER SV-BOUNDARY LINK 0 FMAINV OR Inf LINK NOT 0 HUMAN-SOURCE-VEHICLE-V)(0 (Pl Loc) LINK NEGATE *-1 Num - Com BARRIER NOT-NPMOD);
    # "Minste felles multiplum" for the from-human-verbs. The same rules come the other way round after the comitative-chapter - after we have picked out the special cases. 
    

#!! * **ComBody** Body-activity-verb Selects Com when BODY, for BODY-ACTIVITY-V or VERBAL-ACTIVITY-V
SELECT:ComBody (Sg Com) + BODY  IF (*0 BODY-ACTIVITY-V OR VERBAL-ACTIVITY-V BARRIER SV-BOUNDARY LINK 0 FMAINV OR Inf)(0 (Pl Loc));
	#$ Muhto ieža eai hálit daid lihkastahttit suorbmagežiin ge.
    # They belong to the dynamic-verbs, but they can also have BODY as a tool.    

#!! * **LocBody** deaddu Selects Loc when BODY
SELECT:LocBody (Pl Loc) + BODY  IF (*-1 ("deaddu") BARRIER NOT-NPMOD)(0 (Sg Com));
	#$ Suorggahahtti deattut kasserejuvvon gorudiin.


#!! * **ComVeh** Selects (Sg Com) if you are VEHICLE, default is Sg Com
SELECT:ComVeh- (Sg Com) + Sem/Veh IF (0 (Pl Loc) LINK NEGATE *0 PLACE-V BARRIER SV-BOUNDARY) ;

#SELECT:ComVeh- (Sg Com) + Sem/Veh  IF ((*0 VEHICLE-TOOL-V BARRIER SV-BOUNDARY LINK 0 FMAINV OR Inf) OR (-1 (".*mátki"r) OR ("johtu" Loc)))(0 (Pl Loc));
	#$ Sii bukte girjjiid biillain.
	#$ Go eanet geainnuid ráhkadedje, de sáhtiimet ordnet ruovttumátkkiid bussiin eanaš ohppiide.



#!! ### HUMAN-LOC-V 
#!! * **LOCsatni** Selects (Pl Loc)
SELECT:LOCsatni- (Pl Loc) IF (0 (Sg Com) + SATNI)(*1 HUMAN-LOC-V BARRIER SV-BOUNDARY LINK 0 FMAINV OR Inf LINK *0 WORD-PARTS OR SINGLE-SOUND BARRIER SV-BOUNDARY LINK 0 Acc OR Nom );
	#$ Váldonjuolggadus lea ahte báikenamain dábálaččat eat ane goallossázu.
	#$ Movt báikenamain geavahuvvo goallossáhcu čilgejuvvo 2.1.5 čuoggás. 

#!! * **LOCwordparts** Selects (Pl Loc)
SELECT:LOCwordparts- (Pl Loc) IF  (*0 HUMAN-LOC-V BARRIER SV-BOUNDARY LINK 0 FMAINV OR Inf LINK *1 WORD-PARTS OR SINGLE-SOUND BARRIER SV-BOUNDARY LINK 0 Acc OR Nom )(0 (Sg Com) LINK 0 CONCEPT OR HUMAN OR SATNI);
	#$ Atnit stuora bustáva lágaid namain:
	#$ Gravismearka adnojuvvo sámegielas dušše muhtun vierronamain.
    # Have to preceede the next Sg Com rules because they are an exception to it with respect to SATNI

#SELECT:ComHumanLocV (Sg Com) IF (0 (Pl Loc) LINK 0 CONCEPT OR HUMAN OR SATNI)(*0 HUMAN-LOC-V BARRIER SV-BOUNDARY LINK 0 FMAINV OR Inf);
	#$ Sámediggeráđđi šálloša ja imaštallá go departementa ii namut sámi skuvlla sániin ge skuvlla IKT-áŋgiruššanplánain.     
	#% Ollu ásahusat geavahit dan sin doaimmain.

  	
#!!	### COPULAS 
SELECT:ComCop (Sg Com)  IF (*-1 ("šiega") OR ("bahá") OR ("siivui") OR ("buorre") BARRIER SV-BOUNDARY LINK 1 COPULAS LINK 0 PrfPrc OR Inf OR ConNeg OR VFIN)(0 (Pl Loc) LINK 0 ANIMATE OR CONCEPT LINK *-1 A - Ord - Attr BARRIER NP-BOUNDARY);
#SELECT:ComCop (Sg Com)(0 (Pl Loc) LINK 0 ANIMATE OR CONCEPT LINK *-1 A BARRIER NP-BOUNDARY)(*1 COPULAS LINK 0 Inf OR ConNeg OR VFIN BARRIER S-BOUNDARY LINK NEGATE *1 REAL-V LINK NEGATE 0 VFIN LINK NOT 0 COPULAS); # This one is not good, it hits Hab
 	#$ Go dii ehpet leat šiega leamaš ealibiin.
	#$ Go dii lehpet šiega leamaš ealibiin.
	#$ Go dii lehpet šiega ealibiin.
	#$ Son lea šaddan beakkán su joavkkuinis.

 
SELECT:LocCop (Pl Loc) IF  (*-1 COPULAS BARRIER SV-BOUNDARY OR NP-HEAD-NOM OR COM-HEAD LINK NEGATE *-1 Loc BARRIER SV-BOUNDARY OR CC)(0 (Sg Com) LINK 0 EVENT OR PLACE OR ORGANIZATION);
	#$ Beassášmárkaniin fitne sullii 9000 guossi ja 3500 guossi ledje čakčamárkaniin.
	#$ Skuvlajagis 2001/2002 leat oktiibuot 469 oahppi suohkana vuođđoskuvllain.
    
#!! * bivvat - we don't need this any more
#SELECT:Combivvat (Sg Com)  IF (*0 ("bivvat") BARRIER SV-BOUNDARY LINK 0 FMAINV OR Inf)(0 (Pl Loc) LINK 0 CLOTHES OR BEDCLOTHES OR MATERIAL);
	#$ Beaskkain gal bivvá buolašin.

#!! * ealihit
SELECT:ComealihitL (Sg Com) IF  (-1 NP-HEAD-ACC LINK *-1 ("ealihit") BARRIER W-NOT-ACC-GEN)(0 (Pl Loc));	
SELECT:ComealihitR- (Sg Com) IF (0 (Der/NomAct Pl Loc))(1 NP-HEAD-NOM LINK 1 ("ealihit"));	
	#$ Sii ealihit sin heakkaset vuodjimiin ja geasehemiin.
	
#!! * ipmirdit / áddet
SELECT:Comipmirdit (Sg Com) IF (0 (Pl Loc) + Foc/ge)(1 ("ipmirdit") OR ("áddet") OR ("fáhtet"));	
	#$ In vel čilgemiinge ipmirdan.	 
	# It should be possible to claim that 0 has to be Der/NomAct , but the tags are not there because of lexical baseform:
  
#!! * ruhtadit
SELECT:Comruhta1- Com IF (0 ("ruhta" Com) LINK NEGATE *-1 HEADOFPARTS BARRIER S-BOUNDARY)(*1 ("ruhtadit"));
SELECT:Comruhta2- Com IF (0 HEADOFPARTS LINK 0 Com LINK *1 ("ruhta" Loc) BARRIER S-BOUNDARY)(*1 ("ruhtadit"));
  	#$ Osiin gelbbolašvuođaguovddážiidda juolluduvvon ruđain sáhttá maid ruhtadit sadjásašgoluid hospiterema oktavuođas dahje lassigoluid, mat Kárášjogas / Leavnnjas šaddet joatkkaoahpus.
  
#!! * ávvudit
SELECT:Comávvudit- Com IF (0 Sem/Food OR Sem/Event LINK NEGATE *-1 HEADOFPARTS BARRIER SV-BOUNDARY)(*-1 ("ávvudit") BARRIER SV-BOUNDARY);
SELECT:Comávvudit- Com IF (0 Sem/Food OR Sem/Event LINK NEGATE *-1 HEADOFPARTS BARRIER SV-BOUNDARY)(*1 ("ávvudit") BARRIER SV-BOUNDARY);
  
 
#!! * suokkardit and čielggadit
SELECT:Comsuohkkardit- (Sg Com) IF  (*0 ("suokkardit") OR ("čielggadit") BARRIER SV-BOUNDARY LINK 0 FMAINV OR Inf)(0 (Pl Loc) LINK 0 ACTOR);
  	#$ $SDD áigu suokkardit Sámedikkiin, sáhttágo dán plána doaimmaid čađaheami delegeret Helse Nordii.
   
   
#!! * haddegoargŋun 
SELECT:Lochaddegoargŋun (Pl Loc) IF (-1 ("haddegoargŋun"))(0 (Sg Com) LINK 0 Sem/Clth OR Sem/Food);
  	#$  Unnán haddegoargŋun biepmuin.
   
     
   
#!! * vástidit 
SELECT:Locvastidit1- (Pl Loc) IF (*-1 ("vástidit") BARRIER NOT-NPMOD LINK 0 FMAINV OR Inf LINK *1 Ill BARRIER NOT-NPMOD)(0 (Sg Com));
SELECT:Locvastidit2 (Pl Loc) IF (*-1 ("vástidit") BARRIER NOT-NPMOD LINK 0 FMAINV OR Inf)(0 (Sg Com) LINK *1 Ill BARRIER NOT-NPMOD);
  	#$  SDD lea nammadan guovlluid dearvvašvuođafitnodagaid stivrraid, mat vástidit doaimmaineaset dušše stáhtii, mii daid eaiggáduššá.
    
#!! * Coordination
SELECT:ComCoord Com IF (*-1 CRD2 BARRIER NOT-NPMOD LINK *-1C Com BARRIER WORD);
	#$ Das ráhkaduvvo vuojahat gátti guvlui nu galliin čuolddain (dahje oar-julggiin) go lea vuogas.
	#$ Bargiidbellodaga Per Nils Saari beasai suohkanstivrii ja válljii ovttasbargat olgešbellodagain, gurutbellodagain ja johttisámi listtuin vai ožžot eanetlogu suohkanstivrras.
		
		
		
SECTION 

# Accusatives

SELECT:miiPlAcc (N Pl Acc) OR (A Pl Acc) IF (*-1 ("mii") OR ("gii") OR ("mihkke" Foc/Neg-ge) OR ("guhte" Pl) BARRIER WORD - A + Attr)(1 NP-BOUNDARY OR Adv OR EOS OR COMMA); 
	#$ Maid ipmašiid doppe dagat?
	#$ Buot álbmogiid hearvásvuođat ja riggodagat buktojuvvojit gávpogii , muhto ii mihkkege buhtismeahttumiid beasa sisa.
	#$ Guđiid biktasiid mun galggan bassat?
	#$ Dát ii leat mihkkege ođđa áššiid minsiidda sámi vánhemiidda.
	#$ Ii leat miige heđiid.



#!! * **AccTV1NoC** was Eckhard's late version of AccTV1 without C. We will look at this.
SELECT:AccTV1NoC Acc IF (*-1 STRICT-TRANS-V BARRIER NOT-NPMODADV LINK NEGATE *-1 Acc BARRIER S-BOUNDARY OR CC)(0 NP-HEAD-ACC LINK NEGATE *1 Acc - Pron BARRIER S-BOUNDARY)(NEGATE *-1 NUM BARRIER NOT-Attr LINK NOT 0 NUMUNIT)(NEGATE 0 TIME)(NEGATE 0 ANIMATE + Gen OR Pron + Gen + Sg LINK *1 Ill + Sg BARRIER NOT-NPMOD)(NEGATE 0 (Pron Refl) LINK 1 (N Ess))(NEGATE 0 (Pron Refl) LINK 1 Gen LINK 1 Po)(NEGATE 1 (N Nom) OR Ger)(NEGATE -1 <vdic>) ; 

#!! * **AccEOS** is The Dangerous Rule: it is one of the last rules before removing all leftover Acc. It only selects Acc if Nom is not an option, dont change this btw, and the end of the sentence is the next one to the right
SELECT:AccEOS Acc IF (0 Acc OR Gen LINK NOT 0 Nom)((1 EOS OR (":") OR Dem + Gen LINK *-1 TRANS-V) OR (1 Ger OR VAbess LINK NOT -1 HUMAN) OR (1 COMMA LINK *1 Nom BARRIER NOT-Attr LINK NOT 0 Acc LINK 1 EOS))(NEGATE 0 TIME OR TIME-N OR Adv OR ("bassi" A))
(NEGATE -1 Pr)(NEGATE -1 Num LINK *-1 TRANS-V + FMAINV BARRIER SV-BOUNDARY) ;
	#$ Unná mánážat dulbmet eatni askki, stuora mánát fas váimmu.
	#$ Nu ožžot sámi beroštumit mat ovddastuvvojit stuorát deattu deaivvadettiin riikkalaš ja stuorraservodaga beroštumiiguin.
	#$ Don ráhkistat bilideaddji sárdnuma, behtolaš njuovčča.
	#$ Dán artihkkalis mun čájehan ahte málle sáhttá geavahit maiddái unnitlogugiela čálamáhtu konteavsttaid govvideamis vearditkeahttá nu ollu.
 	#$ Hástalussan šaddá ásahit buriid hálddašanstivrraid mat sihkkarastet resurssaid ceavzilis ávkkástallama dan ovdáneami ektui mii lea sávahahtti sámi servodahkii.
	#% Dat čohkkáiga oktii njuniid. <== Make a rule for this in the "acc/gen-section".
	#% Mánná viehkalii olggos álás julggiid. <== Note that both are IV-verbs.



#!! * **AccEllipse** 
SELECT:AccEllipse Acc IF (0 Acc OR Gen LINK NOT 0 Nom)(*-1 CC OR COMMA BARRIER NOT-ADV-PCLE LINK *-1C Acc BARRIER NOT-ADV-PCLE - CC - COMMA)(1 EOS OR CC OR COMMA OR Adv) ;  
	#$ Ailu (12) lea maiddái oaidnán luonddus máŋggaid ealliid, muhto maiddái riebaniid, ealggaid ja guovžžage.


#!! * **genRel** removes genitive if Rel OR @CVPg to your right *ožžot olbmot skoviid maid*
REMOVE:genRel Gen ((0 Acc LINK NOT 0 TIME-N LINK *1C Rel OR @CVP BARRIER WORD) OR (0 Acc LINK NOT 0 TIME-N LINK 1 ("dalle") LINK 1C Rel OR @CVP) OR (0 Acc LINK NOT 0 TIME-N LINK 1 COMMA LINK 1 Nom))(NEGATE *-1 (Num Nom) OR (Num Acc) BARRIER NOT-NPMOD)(NEGATE *-1 Pr BARRIER NOT-A)(NEGATE -1 COMMA LINK -1 Gen LINK 3 COMMA)  ;
# dalle go @CVP is not MWE in Apertium
	#$ Dan maid dalle dárbbaša fievrridit, ferte olmmoš noađuhit.
	#$ Juohke gielddas ožžot olbmot skoviid maid sii galget deavdit ja sii jearahallet maiddái olbmuid, muhto Isaksen imašta kommišuvnna bargovugiid
	#% Kárášjoga joavkku, Nordlysa, gánddat leat mielde joavkkus.


#!! * **genAcc** selects Acc 
SELECT:genAcc Acc (-1 BOC)(0 Nom)(1 Rel LINK *1 FMAINV LINK *1 TRANS-V BARRIER Nom LINK NEGATE *1 Acc) ;
	#$

#!! * **TopObj** selects Acc for Finnish-style topicalisation
SELECT:TopObj Acc (*-1 BOC BARRIER NOT-NPMODADVIIP)(0 Gen LINK NOT 0 Nom)(1 MOD-ASP)(2 Inf + TV) ;
	#$ Huksengohcimii dárkkuhuvvon ohcamušaid galgá doaimmahit čujuhussii:
	#% Stáda ferte vástidit Stálonjárgii.

SELECT:TopObjNegLeat Acc (*-1 BOC BARRIER NOT-NPMODADVIIP)(0 Gen)(1 Neg + Sg3)(2 ("leat"))(3 A OR N)(4 Inf + TV) ;
SELECT:TopObjLeat Acc (*-1 BOC BARRIER NOT-NPMODADVIIP)(0 Gen)(1 ("leat") + Sg3)(2 A OR N)(3 Inf + TV) ;
	#$ Erenoamážit muslimanissoniid ii leat lohpi govvet.
	#$ Árbevirolaš sosiolingvisttalaš oainnu mielde, de giela lea vejolaš dutkat.

SELECT:TopObjLeat Acc (-1C Po)(0 Gen)(1 ("leat") + Sg3)(2 A OR N)(3 Inf + TV) ;
	#$ Árbevirolaš sosiolingvisttalaš oainnu mielde giela lea vejolaš dutkat ovttadahkan mii geavahuvvo dihto hálliidjoavkku gaskkas.

#!! * **genNom** removes Acc
REMOVE:genNom Acc (-1 BOC)(0 Nom)((1 Rel LINK *1 FMAINV LINK NEGATE *1 TRANS-V) OR (1 Rel LINK *1 COMMA LINK *1 COPULAS BARRIER Nom));
	#$ Gávpot mii lea vári alde, ii sáhte leat čihkosis.

#!! * **makkárAcc** selects Acc after makkár, if not time or route
SELECT:makkárAcc Acc (0 Gen LINK -1 MAKKAR)(NOT 0 TIME OR ROUTE)(NEGATE 0 Gen LINK 1 Acc) ;
	#$ Logut čájehit makkár kilohatti gaskamearálaččat Finnmárkkus ferte máksit ealggabierggu ovddas.


#!! * **DemAcc** selects Den Acc after the last acc-disambiguation of nouns
SELECT:DemAcc (Dem Acc)(0 Gen LINK 1C (N Acc)) ;
	#$

SELECT:MeasureAndAcc Acc (*-3 TRANS-V BARRIER NOT-NPMODADV)(-2 Num)(-1 MEASUREADJS);
	#$ Lágamánneriekti maid dubmii dievddu máksit 130 000 kruvdnosaš buhtadusa gillájeaddjái.

SELECT:Acc Acc (0 Gen)(-1 ("/") LINK -1 Acc);
#SELECT:EllipseAcc Acc (0 Gen LINK NOT 0 Nom)(NEGATE 0* REAL-V OR N)(1 Adv);
	#$ Ja dovdduid maid.

#!! * **KillAcc** Removes Acc if you are Gen 
REMOVE:KillAcc Acc IF (0 Gen) ;

	#$ Mii leat geardumiin juo álgán, nággestii unna vieljaš.
	#$ Ehpet dii leat ieža sárdnumin, muhto Áhčádet Vuoigŋa sárdnu din bokte. 


SELECT:Po Po IF (-1 Gen)(0 Adv OR Pr) ;

#!! * **NumOktaGen** Selects Gen after okta gen 
SELECT:NumOktaGen Gen IF (-1 OKTA + Gen)(1 N) ;
	#$ Mo davvisámegiela aspektuála vearbasuorgásat geavahuvvojit ovtta máinnasteaddji idioleavttas?


SECTION 


#!! ### Locative and comitative - Disambiguation based upon coordination
	# ================================================================
 # The idea is to let the other rules work first, and then let the coordination follow suit.

SELECT:LocCoord6- Loc IF (*-1C Loc LINK *1 Rel BARRIER Com OR S-BOUNDARY)(0 Com LINK *-1 CRD2 BARRIER NOT-NPMOD);

SELECT:ComCoord1- (Sg Com) IF (*-1 CRD2 BARRIER NOT-NPMOD LINK -1 Com LINK NOT 0 Loc);
	#$  Ii lean dárbu dábálaš eanavuovdimii, oktan opmodatregistreremiin ja čuoldimiin.

SELECT:ComCoord2 (Sg Com) IF (1 CRD2)(*2 Com BARRIER NOT-NPMOD LINK NOT 0 Loc);
	#$  Fága galgá ovddidit ovttasbarggus Sámedikkiin ja sámi dearvvašvuođa- ja sosiálabargiiguin. 

#!! ### And then we remove the remaining Sg Com analysis

SELECT:NoVerbCom (Sg Com) IF (0 (Pl Loc) LINK *-1 ("mii" Interr Acc) BARRIER VFIN LINK *-1 BOC BARRIER WORD) ; 
	#$ Maid don heavvaliin?



REMOVE:KillLocPl (Pl Loc) IF (0 (Sg Com) LINK 0 TOOL OR Sem/Clth OR Sem/Body OR Sem/Money OR NUMUNIT OR Sem/Sign OR Sem/Perc-emo OR Sem/Lang OR Sem/Domain - ("ealáhus") OR VEHICLE OR PROSEANTA OR Sem/Feat-phys OR Sem/State-sick OR ("lasáhus") OR ("tihttel") LINK NOT 0 ("čalbmi") OR ("suotna") OR Sem/Hum) (NEGATE -1 (Dem Pl Loc))(NEGATE 0 (Pl Loc) LINK *-1 OKTA BARRIER NOT-NPMOD)  ;

REMOVE:KillComSg (Sg Com) IF (0 (Pl Loc)) ;
	#$ Giellakantuvrras ii leat formála váldi álggahit gielladoaimmaid etáhtain, muhto veahkehit.



#!! ## Essive   OBS

SELECT:ESSr2806 ESS-ADVL-N + Ess IF (NEGATE *-1 OPRED-V BARRIER S-BOUNDARY) ;
	#$ Seavdnjadin ii oainne gosa hearggit mannet.
	#$ Bassin ii galggaše kantuvrras čohkkát.
	#$ Beaskkain gal bivvá buolašin.

SELECT:EssOPREDV N + Ess OR A + Ess IF (*-1 OPRED-V OR ("doaibmat") BARRIER SV-BOUNDARY)(NOT 0 Pron) ;
	#$ Son doaibmá áššemeannudeaddjin ja čállin bargosajis.
	#$ Liv Eli Holmestrand doalaha ain iežas nuorran.
	#$ Son lea juo vuorrasit beallái, 84-jahkásaš, muhto dovdá iežas nuorran dego giđđarássi.


#SELECT:ESS2807 Ess IF (-1C Nom)(0 Nom);
	#$ Gáibádussan lea ahte olbmos lea sámegiella ruovttogiellan. # no need pga derNEss
	#% Earáid hárrái čálii skuvladirektevra Kvammen ahte sii leat miehtemielalaččat ođđa áigái.

SELECT:ESSOpredV Ess IF (*-1 Acc BARRIER NOT-NPMOD LINK *-1 OPRED-V BARRIER NOT-NPMODADV)(NEGATE 0 ("oktan"));
	#$ Máret anii Máhte jallan.
	#$ Sii dahke iežaset oađđin.
	
SELECT:ESS2809 Ess - Actio IF (*-1 OPRED-V BARRIER NOT-A-ADV LINK *-1 Acc BARRIER SV-BOUNDARY);
	#$ Duon mun gohčodan juoigamin.

#SELECT:r2810 Ess IF (0 (N Ess) OR (A Ess))(1 CC)(*2C Ess BARRIER NOT-NP);
	#$  Ovdal go kristtalašvuohta bođii Sápmái lei noaiddis erenoamáš rolla oaidnilin ja dálkkodeaddjin.
	
SELECT:ESS2811 Ess IF (-1 ("bissut"))(0 Loc);	
	
# Essive, not Actio Nom:	
SELECT:ESS2812 Ess IF (0 (Actio Nom))(1C NP-HEAD-NOM);
	#$  Nuorran son bođii dohko.	 #no need

#!! ## Late case rules (after other case rules have worked). 


#!! # VERBS PART 2, Section #22
# Disambiguating verbs - part 2

SECTION

#Lexical disambiguation of nouns

REMOVE ("vuorru" G3) (-1 Gen LINK NOT 0 TIME-N) ;

REMOVE:sørfra ("máddi") + Ess (0 ("máddin") LINK 1 Ill) ;

#!! ## Finite or not

#!! ### Finite
SELECT:leat SG2-V IF (0 ("leat") LINK 1 Nom + Sg);
 	#$ Maid mearkkaša go don leat journalista?

SELECT:r2906 SG2-V IF (*-2C SG2-V BARRIER SV-BOUNDARY OR PUNCT)(-1 Rel)(NEGATE -1 (Pl Nom));
 	#$ Don leat garra olmmái gii láddjet doppe gosa it leat gilván.

SELECT:r2820 VFIN IF (-1 NP-HEAD-NOM LINK NEGATE *-1 VFIN OR Acc)(0 PrsPrc LINK 0 TRANS-V)(*1 NP-HEAD-ACC BARRIER VFIN);
 	#$ Jus stálu beana njoallu stálu háviid, de hávit sávvojit.

SELECT:r2821 VFIN IF (*-1 BOC BARRIER REAL-V)(0 INF-V LINK NOT 0 Imprt)(NEGATE 0 ("manne") LINK *-1 BOS BARRIER NOT-CC)(*1 Inf BARRIER SV-BOUNDARY OR VFIN);
 	#$ Áigugo fas skuvlii álgit? 
 	#$ Sáhttit maid namahit ahte ...
 # Looks brave but works! OBS! Should not take: Mánná sáhtášii mannat olggos.

SELECT:r2822 VFIN IF (NEGATE *-1C VFIN BARRIER S-BOUNDARY)(-1 Loc)(NEGATE -1 (Interr Sg))(0 LOC-V); 
 	#$ Nuppi lađđasis čuovvu, ahte...


#!! ### Not Finite

REMOVE:r2823 VFIN IF (NEGATE -2 BOS OR CLB)(-1C Inf)(NEGATE 0 Qst);
 	#$ Lága juksanmearrin lea váikkuhit sihkkarastit álbmogii 
 	#$ ovttadássásaš dearvvašvuođaveahki.
 # Neg conditions because of:
 	#$ Suhpodit daddjojuvvo dat go dušše nođiid váldá olmmoš eret...
 	#$ Mearrideaddjin dasa ferte danin leat addojuvvogo... 

REMOVE:r2824 VFIN IF (*-1 Nom BARRIER NOT-ADV-PCLE LINK *-1C VFIN BARRIER NOT-ADV-PCLE OR CP);
 	#$ Lea dárbu nannet doaimmaid mat ovddidit sámi dutkama.

#REMOVE:r2825 VFIN IF (*-1 PrfPrc BARRIER HEAD-N OR (Pron Nom) OR SV-BOUNDARY LINK *-1 REAL-COPULAS BARRIER NOT-ADV-PCLE);
  	#$ Sin badjel geat orrot jápmima eatnamis ja suoivanis, lea ihtán čuovggas.
 	#$ Gii lea cuigen din báhtarit boahttevaš moarreduomus?

REMOVE:r2827 VFIN IF (NEGATE *-1 Rel OR CS OR COMMA BARRIER S-BOUNDARY)(*-1C VFIN BARRIER S-BOUNDARY OR PUNCT LINK NEGATE *-1 Interr BARRIER NOT-NP)(*1 S-BOUNDARY OR CC OR EOS BARRIER NP-HEAD-NOM OR NP-HEAD-ACC);
 	#$ Muhto sáđuid son boaldá čáskatmeahttun dolas.

REMOVE:r2828 VFIN - Qst IF (NEGATE 0 Imprt LINK *-1 BOS BARRIER NOT-ADV-PCLE)(NEGATE *-1 Rel BARRIER REAL-V)(NOT 0 N)(*1C VFIN BARRIER S-BOUNDARY OR PUNCT OR XGO OR ("galle" Num) LINK NOT 0 Qst)(NEGATE *1 Neg BARRIER VFIN)(NEGATE *1 VFIN BARRIER S-BOUNDARY LINK 1 go);
 	#$ Sámegillii galbet universitehta lea šaddan realitehta.
 	#$ Áldu mii dálvvi miehtá njamaha miesi gohčoduvvo čearpmateadnin.

REMOVE:r2829 VFIN IF (0 Loc)(*1 Neg BARRIER NOT-ADV-PCLE LINK *1 PrfPrc OR ConNeg BARRIER SV-BOUNDARY);	
 	#$ Lágas ii duššaduvvo oktage bustávaš dahje čuoggáš, ovdal go...
 	#$ Máhttájeaddjit orro jávohaga eai ge dalle muitalan geasage maid ledje oaidnán. 

REMOVE:r2832 ConNeg IF (*-1 PrfPrc BARRIER NOT-ADV-PCLE);
 	#$ Go geavahus ii duođaštuvvon leat álggahuvvon ovdal go... 
#SELECT:r2833 (@+FMAINV) IF (*-1 CS BARRIER S-BOUNDARY)(0C (Actio @>N) OR (PrfPrc @>N) OR (Gen @>N) OR (@+FMAINV))(*1 NP-HEAD-ACC)(NEGATE *1 VFIN BARRIER S-BOUNDARY);
 	#$ Go gulan olbmuid sámásteame, de lea dego doložii máhccat, go mánnán gullen áhči,  eagi ja Ristiinna-imi humadeame.

SELECT:r2834 VFIN IF (-1 BOS)(1 Adv)(2 (":"));
SELECT:r2835 VFIN IF (-1 BOS)(1 EOS);
SELECT:r2836 VFIN IF (-1 BOS)(1 ("go") LINK NEGATE *1 VFIN BARRIER S-BOUNDARY);
 	#$ Ozan ain: 

	#!! ### Indicative Negative
	# -------------------
 # Unique are Sg1, Sg3, Du1, Du2, Du3, Pl1, Pl2, Pl3
 # Sg2 "it" = ABBR.

	# Select Neg Sg2 instead of ABBR
REMOVE:r2837 ABBR IF (*-1 ConNeg OR PrfPrc)(0 Neg);  	#$ Dáppe don leamaš it.
REMOVE:r2838 ABBR IF (0 Neg)(*1 ConNeg OR PrfPrc);  	#$ Don it leat dál.


	#!! ### Infinitive
	#    ----------
 	
#!! * **InfComplToN** Inf when -1 N
SELECT:r2840 Inf IF (-1C (Sg Nom) LINK NOT 0 Num LINK NEGATE *-1 CC BARRIER NOT-NPMOD LINK *-1 Nom BARRIER VFIN)(NEGATE 0 PL3-V LINK *1 NP-HEAD-PL-NOM BARRIER NOT-NPMODADV);
 	#$ Mánáin lea vuoigatvuohta oažžut dearvvašvuođaiskkadeami.
 	#$ Mus ii leat báljo dilli vuolgit.
#fk7559
	
SELECT:r2841 Inf IF (*-1 COPULAS BARRIER NOT-ADV-PCLE)(0 VFIN);
 	#$ Gean vuorru lea dál bálkestit birccu?	
#fk2080
	
SELECT:Inf_ICL Inf IF (-1 Acc LINK NOT 0 Rel OR VFIN OR Adv OR Nom LINK *-1 VFIN BARRIER Acc OR S-BOUNDARY)(*1 ADVLCASE OR Acc BARRIER NOT-NPMOD);
 	#$ Son vuolggahii bálvaleaddjiidis rávkat bovdejuvvon gussiid heajaide.
 	#$ ...leago dieđiheamis doarvái vuođđu ášši váldit meannudeapmái.
 	#$ Vaikko ii loga makkárge revolušuvnna vuordit dásseárvopolitihkas iežas gielddas maŋŋel dán kurssa, de deattuha ahte buot dáhpáhuvvet proseassa čađa.


SELECT:r2845 Inf IF (-1 Nom LINK *-1 Loc BARRIER NOT-A-ADV LINK *-1 COPULAS BARRIER NOT-PCLE)(*1 Acc BARRIER NOT-A-ADV);
 	#$ Leatgo geavaheaddjis váttisvuođat buktit áššiid ovdan?

SELECT:r2846 Inf IF (-1 COMMA LINK *-1 (Pron Rel) LINK *-1 INF-V BARRIER REAL-V);
 	#$ Dalle fertejit buotlágan ávnnaslaš eavttut maid dárbbaša vai duođalaččat galgá sáhttit kultuvrras dikšut, gullat dása.

SELECT:r2847 Inf IF (-1 COMMA LINK *-1 (Pron Rel) LINK *-1 (Pron Rel) LINK *-1 INF-V BARRIER REAL-V);
 	#$ Eai galgga sii geat gullet dakkáraš minoritehtaide ovttas eará láhttuiguin mat gullet sin jovkui, manahit vuoigatvuođaideaset dikšut iežaset kultuvra, "mihkkege" Pron Indef Sg Acc ja geavahit iežaset oskku, dahje geavahit  iežaset giela.

SELECT:r2848 Inf IF (NEGATE *-1 (Pl Nom) BARRIER NOT-ADV-PCLE)(1 LEAT-FIN-NOT-IMPRT)(NEGATE 1 Qst);
 	#$ Dáidda heŋgejuvvojit gurpasat, ja maid dál de guottehit lea áigumin.

SELECT:r2849 Inf IF (*-1 COPULAS BARRIER NOT-ADV-PCLE LINK *-1 (A Sg Nom) BARRIER NOT-ADV-PCLE);
 	#$ Dárbbašlaš lea dikšut jávrriid ovdal go lea ekonomalaččat veara bivdit.

SELECT:r2850 Inf IF (*-1C NP-HEAD-NOM + Sg BARRIER REAL-V OR go OR S-BOUNDARY)(1 PUNCT OR EOS);
 	#$ Áigemearri váidalusa buktit.
 	#% In dieđe man buorit dat leat.

REMOVE:r2851 Inf IF (NEGATE *-1 VFIN BARRIER S-BOUNDARY)(0 ("leat"))(*1 PrfPrc BARRIER REAL-V);
 	#$ De leat máksán.

	# Coordinated infinitives
	# - - - - - - - - - - - -
SELECT:r2852 Inf IF (*-1 COMMA BARRIER REAL-V OR MO LINK -1C Inf)(NEGATE 0 (Neg Imprt))(NEGATE *-1 (Pl Nom) BARRIER S-BOUNDARY)(NEGATE *1 (Pl Nom) BARRIER S-BOUNDARY);
 	#$ Mun in leat boahtán gomihit, muhto ollašuhttit.
 	#$ Son galgá stuorrut, mun fas uhccut.

SELECT:r2853 Inf IF (*-1 JA/DAHJE BARRIER SV-BOUNDARY LINK *-1 Inf BARRIER WORD); 
 	#$ Hearrá Ipmilat galggat gudnejahttit, ja áivvestassii su bálvalit.

SELECT:r2854 Inf IF (*-2C Inf BARRIER REAL-V)(-1 JA/DAHJE);
 	#$ Duođalaš nana jáhku geažil lea divššohasas vuoigatvuohta biehttalit vuostáváldimis vara dahje varabuktagiid dahje biehttalit botkemis nealgudeami mii lea jođus.
 
SELECT:r2855 Inf IF (*-3 Interr LINK *-1 Inf BARRIER REAL-V)(-2 COMMA)(-1 JA/DAHJE)(NEGATE *1 Nom BARRIER NOT-NPMODADV);
 	#$ Divššohasbearráigeahččis lea vuoigatvuohta buktit oaiviliiddis dilálašvuođaid birra mat gullet bearráigeahčči bargoviidodahkii, ja evttohit konkrehtalaš buoridandoaimmaid.

	# Infinitives 
	# - - - - - -
REMOVE Inf (0 VFIN + Qst) ;

SELECT:r2856 Inf IF (*-1 BOS BARRIER NOT-ADV)(NEGATE 0 Imprt)(NEGATE 0 VFIN LINK *1 Nom BARRIER NOT-NPMOD);
#fk841 (kor mange treff på freecorpus)

SELECT:r2857 Inf IF (-2 ABBR)(-1 (PUNCT RIGHT));
#fk60

SELECT:r2858 Inf IF (-1 N)(0 V)(*1 Rel BARRIER NOT-NPMODADVII LINK NOT 0 Adv);
#fk797

SELECT Inf IF (-1 Nom LINK *-1 CC BARRIER NOT-NPMOD LINK -1 Nom LINK *-1 COPULAS BARRIER NOT-NPMODADV) ;
 	#$ Jagi 2005 Sámediggi álggahii Divvuma, mii lei prošeakta ja bargojoavku ráhkadit sámegielaid sátnedivvunprográmmaid.
#fk194

REMOVE:NomAgwhenInf  Actor  (0 Inf) ;


	# Infinitives in lists
	# - - - - - - - - - - 

SELECT:r2859 Inf IF (-2 VFIN)(-1 (":" CLB));
 	#$ Ulbmilat leat: eastadit soađi, nannet ráfi.

SELECT:r2860 Inf IF (*-2 Inf BARRIER VFIN LINK -1 (":" CLB) LINK -1 VFIN)(-1 ("\;" CLB));
 	#$ Ulbmilat leat: eastadit soađi; nannet ráfi.

SELECT:r2861 Inf IF (*-2 Inf BARRIER VFIN LINK -1 ("\;" CLB) OR ("," CLB) LINK *-1 Inf BARRIER VFIN LINK -1 (":" CLB) LINK -1 VFIN)(-1 ("\;" CLB) OR ("," CLB));
 	#$ Ulbmilat leat : eastadit soađi; nannet ráfi; lohkat girjiid. 
	#      L-1 L-1 L*-1    L-1 *L-2   -1  0

SELECT:r2862 Inf IF (*-2 Inf BARRIER VFIN LINK -1 ("\;" CLB) OR ("," CLB) LINK *-1 Inf BARRIER VFIN LINK -1 ("\;" CLB) OR ("," CLB) LINK *-1 Inf BARRIER VFIN LINK -1 (":" CLB) LINK -1 VFIN)(-1 ("\;" CLB) OR ("," CLB));
 	#$ Ulbmilat leat : eastadit soađi; nannet ráfi;  lohkat girjiid , borrat márffi.
	 #      L-1 L-1 L*-1    L-1 *L-2   L-1  L* -1     -1  0

	# Infinitive in sentence fragments
	# - - - - - - - - - - - - - - - - 

SELECT:r2863 Inf IF (-1C Acc LINK *-1 BOS BARRIER NOT-NPMOD)(1 EOS);
 	#$ Ruđa juogadit.

SELECT:r2864 Inf IF (-1C Ill LINK *-1 BOS BARRIER NOT-NPMOD)(0 ILL-V)(1 EOS);
 	#$ Mánáidgárdái hárjánit.

SELECT:r2865 Inf IF (-1 BOS)(*1C Acc BARRIER NOT-NPMOD LINK 1 EOS);
 	#$ Ruđa juogadit.

SELECT:r2866 Inf IF (-1 BOS)(0 ILL-V)(*1C Ill BARRIER NOT-NPMOD LINK 1 EOS);
 	#$ Hárjánit mánáidgárdái.

SELECT:r2867 Inf IF (-1 BOS)(0 LOC-V)(*1C Loc BARRIER NOT-NPMOD LINK 1 EOS);

SELECT:r2868 Inf IF (-1C Loc LINK *-1 BOS BARRIER NOT-NPMOD)(0 LOC-V)(1 EOS);



#!! ## Indicative or imperative

#!! ## Verbs according to person and number

#!! ### Sg1 - First person singular


#!! * **InitialLeanRule** selects lean when no VFIN to the left
SELECT:InitialLeanRule_r2900 Sg1 IF (NEGATE *-1 VFIN BARRIER S-BOUNDARY OR PUNCT)(0 LEAN);
	#$  Lean čiegas velohaddamen láhttis skuvlalávka oaivve vuolde.
	#$  Jesus celkkii: "Oanehis áiggi lean velá din luhtte."
# The function is in reality to remove Ess of Lea. Perhaps restrict to that.

#!! * **Sg1WhenAloneVfin** selects Sg1 when no other VFIN or PrfPrc
SELECT:Sg1WhenAloneVfin_r2901 SG1-V IF (NEGATE *-1 VFIN OR PrfPrc BARRIER S-BOUNDARY)(NEGATE 0 Der/Pass OR N)(NEGATE 0 (N Nom) LINK 0 LEX-N)(NEGATE *1 VFIN BARRIER S-BOUNDARY)(NEGATE 0 PrfPrc LINK -1 CC LINK *-1 PrfPrc BARRIER SV-BOUNDARY);
	#$  Oađidettiin gullen olbmo boahtimin.

# Trond 29.4. Next to still to evaluate:

SELECT:r2903 SG1-V IF (*-1 CC OR COMMA BARRIER REAL-V LINK *-1C SG1-V BARRIER REAL-V)(NEGATE 0 Der/Pass);
	#$  Bargen  doppe  beannot  jagi  sullii  bealle  virggis  ,  oahpahin  eanaš  matematihka  ja  luonddufága  .
	
REMOVE:r2905 SG1-V IF (0 PrfPrc OR Actio)(*1C VFIN BARRIER S-BOUNDARY);
	#$  Dálá fiinnuhan riektejurddašanvuogi mielde ii eahpiduvvo ahte dán livččii galgan dahkat.


#!! ### Sg2 - Second person singular

 # Sg2 = Inf (even syllables, copula)
 # Note the homonymy with derived verbs (jearat = jearadit + ...
 # Prt Sg2 = Prs Pl3 = Imprt Pl2 (even syll)  vulget
 # Prt Sg2 = Prs Pl3 = Prs Pl1 = Inf (odd syll)  muitalit
 # Prt Sg2 = Prs Pl3         (contracted) fertejit

SELECT:r2907_Pl3 (Prt Pl3) IF (*0 Pl)(*1 PAST-MARKER BARRIER SV-BOUNDARY);

#!! * --r2907**Sg2** Prt Sg2 if ikte etc.
SELECT:r2907_Sg2 Prt + Sg2 IF (NEGATE *0 Nom + Pl)(*1 PAST-MARKER BARRIER SV-BOUNDARY);
	#$ Vulget go ikte?


SELECT:r2908 SG2-V IF (*-1 CC BARRIER VFIN LINK *-1C SG2-V BARRIER SVF-BOUNDARY);
	#$ Dasgo sániidat mielde don dahkkojuvvot friddjan, ja sániidat mielde dubmejuvvot.
#fk 291

SELECT:r2909 SG2-V IF (*-1 CC BARRIER NOT-ADV LINK *-1 CP LINK *-1C SG2-V BARRIER NOT-ADV-PCLE);
	#$ Don oainnát mo olbmot bahkkejit du birra, ja de jearat vel...
#fk 16

SELECT:r2910 V + Sg2  IF (*-2C V + Sg2 BARRIER VFIN)(*-1 CS BARRIER VFIN)(NEGATE *1 PxPl3);
	#$ Don it beasa doppe olggos ovdal go leat máksán.
	#$ Dál beasat juo ráhkkanišgoahtit jus Kárášjohkii leat meinen beassážiid.
#fk 498

REMOVE:CopNotPl ("leat" Pl1) OR ("leat" Pl3) IF (*1 (Sg Nom) - Coll - Indef OR ("okto") BARRIER NOT-ADV-PCLE LINK NOT 0 NUM OR Pl1 LINK NOT 1 CC OR COMMA)(NEGATE -1 Pl);
	#$ Leatgo árgi bajándálkkis?
	#$ Leat go árgi bajándálkkis?
	#$ Leat go okto áigume herskkostallat? 
#fk 3152 

REMOVE:r2912 Prs + Pl1  IF (-2 BOS)(-1 ("jus") OR ("jos") OR ("juos") OR ("joas"))(0 (Prs Sg2))(*1 ("de") BARRIER (Sg2 Nom));
	#$  Jus leat buohcamin, de fertet...
#fk 135

REMOVE:r2913 V + Sg2 IF (1 CC)(2 VFIN)(NEGATE 2 Sg2);
	#$  Buođđomuoraid maid ráhkadit olbmot gárvvisin dassá go deatnu leahkkasa, čuolddaid čohket ja ráhkadit oar-julggiid.

REMOVE:r2914 V + Sg2 IF (0 COPULAS)(*1 (Pl Nom) BARRIER NOT-ADV-PCLE);
	#$  Leatgo doppe olu gávppit?

REMOVE:r2915 V + Sg2 IF (*-1 (Num Nom) BARRIER NOT-NPMOD);
	#$  Gitta guhtta hearggi sáhttet leat ovtta ráiddus.
	
SELECT:r2916 V + Sg2 IF (0 Pl1 OR Pl3)(*1C V + Sg2 OR PxSg2 BARRIER VFIN OR (":"));	
	#$  Jus dulvvi niegadat, de boađát gullat unohis šlárvaságaid.



#!! ### Sg3 - Third person singular
#!! Infinitive and clausal subject

#!! Rules that look backwards for a subject across a relative clause:

#!! Rules that look backwards for a subject across a subordinate clause (CP boundary):

#!! Extension possibilities: Coordination

#!! Son oaidná du ja mu ovdal go boahtit... 

#!! Coordinated Sg3 verbs

#!! Not V + Sg3 

#!! ### Du1 - First person dual 

 # Prs Du1 = Prt Pl3 (even syllable verbs); also similar to Prs Sg3 of other verbs.

#!! * **MunJaDonDu** selects Du1 if Mon V ja don V de V-Du2
SELECT:MunJaDonDu V + Du1  IF (*-1 CC OR ("de") BARRIER VFIN LINK *-1C V + Sg2 BARRIER VFIN LINK *-1C V + Sg1 BARRIER VFIN);

#!! * **DonJaMunDu** selects Du1 if Don V ja mun V de V-Du2
SELECT:DonJaMunDu V + Du1  IF (*-1 CC OR ("de") BARRIER VFIN LINK *-1C V + Sg1  BARRIER VFIN LINK *-1C V + Sg2 BARRIER VFIN);
	#$  Jus lean váiban ja don maid leat váiban, de manne nohkkat hui árrat.
	
#!! The previous two rules look marginal.	
	
#!! * **DuNotPrtIfToday** selects Du1 over Prt in the context of a present-marker.
SELECT:DuNotPrtIfToday_r2948 (Prs Du1) IF (0 Prt)(*1 PRESENT-MARKER BARRIER S-BOUNDARY);
	#$  Gergego vuolgit ihttin?
# This rule selects Prs over Prt. Better would be REMOVE Prt?

#!! * **Du1IfDu1** selects Du1 with a left context Du1 ... ja ... 
SELECT:Du1IfDu1_r2947 V + Du1  IF (*-1 CC BARRIER VFIN LINK *-1C V + Du1  BARRIER VFIN);
	#$  Moai bohte su lusa ja orrugohte su luhtte.


#!! * **NoDu1** removes Du1 if no MOAI or Du1 around.
REMOVE:NoDu1_r2949 V + Du1  IF (NEGATE *-1 MOAI OR Du1)(NEGATE 0 Imprt LINK *-1 BOC BARRIER NOT-ADV)(NEGATE *1 MOAI);
	#$  De bohte ruđat.
	#$  Nieiddat jerre.
	

#!! ### Du2 - Second person dual

#!! ### Rules for leahppi = ("leahppi" N Sg Nom)

#!! ### Du3 - Third person dual

#!! ### Pl1 - First person plural

#!! ### Pl2 - Second person plural

#!! ### Pl3 - Third person plural


#!! * **Pl3IfPlSubj** Pl3 if Pl noun to the left
SELECT:Pl3IfPlSubj PL3-V IF (*-1  NP-HEAD-PL-NOM BARRIER NOT-ADV-PCLE  LINK NOT 0 VFIN) ; # r2974
	#$  Muhto dorskit gal leat varrasat.
	#$  Divššohasaid birra geat leat uminddegin dahkkon.
	#$  Eatnagat bohtet nuortan ja oarjin.
	#$  Geafit gal leat álo din luhtte.

#!! * **Pl3IfPlSubj** Pl3 if safe plural (incl pron) to the left
SELECT:r2975 PL3-V IF (*-1C NP-HEAD-PL-NOM BARRIER (V PrfPrc) OR PPRON-NOT-SII OR (N Sg Nom) OR CS OR CC)(NEGATE *-1 V + Pl3  BARRIER SV-BOUNDARY OR PUNCT);
	#$  Makkár vuoigatvuođat dain leat riikavuložin? 
	
SELECT:r2976 PL3-V IF (*1C NP-HEAD-PL-NOM BARRIER PPRON-NOT-SII OR S-BOUNDARY) (NEGATE 0 Inf LINK -1 Acc LINK -1 DADJAT) ;
	#% Sii lohket iežaset leat oalle duhtavaččat dán jagáš básárdoaluin.


#!! * **Sg2LeftDon** selects Sg2 in Rel phrase if don to the left of it
SELECT:Sg2LeftDon SG2-V IF (*-1 Rel - Nom BARRIER (Pers Nom) LINK *-1 DON BARRIER (Pers Nom)) ;
	#$ Don galggat doallat dan maid leat vurdnon Hearrái.

# After everything has been done, we go for the third person.

#!! * **groupPl3** selects Prs Pl3
SELECT:groupPl3 Pl3 IF (0 VFIN LINK -1 Nom LINK 0 HUMAN-GROUP OR Sem/Org);
SELECT:NomgroupPl3 Nom IF (0 Sem/Group OR Sem/Org LINK 1 VFIN + Pl3);
	#$ Hakso ahte FeFo leat alcceseaset eanet ruđaid háhpohallame.
	

SELECT:groupPl3 Pl3 IF (0 VFIN LINK 1 Pr LINK 1 Num + Nom LINK NOT 0 OKTA);
	#$ Sihke ovdal ja maŋŋil dan leat gaskkal vihtta ja logi olbmo ožžon dávdda jagis.


SELECT:RightSubj Pl3 IF (NEGATE *-1 Pl)(0 ("leat"))(1 Pl + Nom);
       #$ Áidna mii váivvida leat albmát, geat čolget láhttái.

#!! * **allSg2leat** removes Sg2 if leat Prs Pl3
REMOVE:allSg2leat Sg2 IF (0 ("leat" Prs Pl3));


#!! * **allPrsPl3** selects and removes PrsPl3 if PrtSg2 initially
SELECT:HLallPrsPl3 Prs + Pl1  IF (0 Pl3 LINK *-1 DASH BARRIER NOT-ADV-PCLE LINK -1 BOS)(*1 HEADLINEMARK);
SELECT:HLallPrsPl3 Prs + Pl3 + Ind  IF (0 Prt + Sg2 LINK *-1 BOS BARRIER NOT-ADV)(NEGATE *1 ("?"));
REMOVE:allPrsPl3 Prs + Pl3 IF (0 Prt + Sg2 LINK *-1 BOS BARRIER NOT-ADV);
REMOVE:allPrsPl1 Prs + Pl1  IF (0 Prt + Sg2 LINK *-1 BOS BARRIER NOT-ADV);
	#$ Oidnet go dan?

SELECT:PrsPl3 Prs + Pl3 + Ind  IF (-1 CS LINK -1 @-FMAINV LINK *-1 VFIN + Pl3 BARRIER SV-BOUNDARY);	
	#$ Álget fas sáddet go vuojahit.

#!! * **allPrtSg2** removes PrtSg2 if PrsPl3
REMOVE:allPrtSg2 Prt + Sg2 IF (0 Prs + Pl3 OR Prs + Pl1 OR Imprt + Pl2);


#!! ## Rules for a special infinitive construction
#!! ## More finite verbs
#!! ## Passive
#!! ## Infinitive
SELECT Inf IF (0 VFIN LINK NOT 0 Pl3 LINK -1 (Pl Nom)) ;
SELECT Inf IF (0 VFIN LINK -1 COMMA LINK *-1 Inf BARRIER REAL-V) ;

#!! ## Present Participle
#!! ## Actio/Perfect Participle 


 # Actio = Prs Sg1 = PrfPrc (odd-syll verbs, contracted verbs)
 # Perfect Participle is also treated in "Disambiguating verbs - part 1".
 # The order of rules is of some importance here.
 # We do not SELECT Actio, we remove PrfPrc instead, since there might be a 
 # non-derived N reading alongside Actio. 


SELECT:ZRitvaTestLeksicalisation PrfPrc IF (*-1 BOC BARRIER NOT-NPMOD)(NEGATE 1 COMMA LINK 1 (@CNP) OR (@CVP))(*1 N BARRIER NOT-NPMOD LINK *1 VFIN BARRIER NOT-ADV-PCLE)(NOT 0 LEX-N + (Sg Nom))(NEGATE 0 (Actio Nom) LINK 1 (N Nom) LINK -2 HAB-ACTOR + Gen);
# Dan sajis go N, de geavhan LEX-N. Der/NomAct lea maid N.
REMOVE:ZRitvaTestChooseNoun PrfPrc IF (0 (Der/NomAct N Sg Nom) LINK *1 SG3-V BARRIER VFIN) ; 
# Lene: dás váilu ovdamearkacealkka. Ii galggašii deaivat: Váiban mielain jurddašallá ahte vare son livččii girdisáhpán.

#SELECT:r3041 PrfPrc IF (*-1 BOC BARRIER NOT-NPMOD)(NEGATE 1 COMMA LINK 1 (@CNP) OR (@CVP))(*1 N BARRIER NOT-NPMOD LINK *1 VFIN BARRIER NOT-ADV-PCLE);
  	#$  Sajáiduvvan fágadoahpagiid sáhttá dieđusge geavahit.

SELECT:ZPrfPrcMannat PrfPrc IF (0 ("mannat") LINK 1 ("čađa"));

SELECT:Zr3045 PrfPrc IF (*-1 LEAT-FIN-NOT-IMPRT BARRIER SVF-BOUNDARY OR PrfPrc)(NEGATE 0 Ess)(NEGATE 0 Actio LINK -1 PrsPrc)(NEGATE 0 Actio LINK 1 COMMA OR CC LINK 1 Actio)(NEGATE *1 PrfPrc BARRIER S-BOUNDARY) ;
	#$  Dasto lea Sámediggi ovttasráđiid Finnmárkku ja Tromssa fylkamanniiguin doallan fágaseminára.

SELECT:Zr3047 PrfPrc IF (-1 CC LINK *-1 PrfPrc BARRIER REAL-V) ;
	#$  Boazodoallolágat leat dievasmahttán ja dahkan dárbbašmeahttun...

REMOVE:Zr3049 PrfPrc IF (*-1 Gen BARRIER NOT-NPMOD LINK -1 BOC) ;
	#$  Divššohasjournálaid geahččan dálkkasgeahččalemiid oktavuođas...

REMOVE:Zr3050 PrfPrc IF (NEGATE *-1 VFIN)(NEGATE 0 Der/Pass OR ("leat" PrfPrc))(NEGATE *1 VFIN BARRIER S-BOUNDARY) ;
	#$  Miehtan nuoraid ovddas geain ii leat miehtangealbu.
	#$  Doppe leamaš sámiin stuora deaddu. # It should not take the latter one.

REMOVE:Zr3051 PrfPrc IF (*-1 PrfPrc OR ConNeg BARRIER NOT-NPMODADV LINK NOT 0 ("leat")) ;
	#$  Ii ávžžuhuvvo dieđuid geahččan sivas olbmuid geat čužžot divššohasa lahka.


	# Coordinated actios
	# - - - - - - - - - -

REMOVE:Zr3057 PrfPrc IF (NEGATE -1 LEAT-FIN-NOT-IMPRT LINK *-1 CP BARRIER VFIN)(1 CLB LINK *1 N BARRIER NOT-NPMOD LINK *1 CC BARRIER VFIN LINK *1 N BARRIER NOT-NPMOD LINK NOT 0 Loc) ;
	#$  Sámedikki hálddahusa nannen, dearvvašvuođa- ja eallindiliiskkadeami ruhtadeapmi ja Sámi dearvvašvuođadutkanguovddáža doaimmaid viiddideapmi váldojit ovdan 2002 bušeahta oktavuođas.




#!! ## Actio
#!! ## Selecting some more finite verbs

#REMOVE:InfifVFIN Inf (0 VFIN LINK NEGATE *-1 AUX) ;

#!! ## Lexical disambiguation of verbs 

	# arvit vs arvát vs arvat
	# ------------------------
IFF:Arvit ("arvit") IF (0 ("arvát") OR ("arvat"))(NEGATE *0 HUMAN LINK 0 SUBJ) ;

	# ballat vs. ballát
	# -----------------
REMOVE:LexSel ("ballát") IF (0 ("ballat")) ;
	# One can always (?) understand it as "ballat" if it is homonomy. 

	# bassit vs. bassat
	# -----------------
IFF:LexSel ("bassit") IF (0 ("bassat") LINK *0 Sem/Food + Acc) ;

	
	# báhcit vs báhccat
	# -----------------
REMOVE:LexSel ("báhccat") IF (0 ("báhcit" Imprt)) ;			
	
REMOVE:LexSel ("biestit") IF (0 ("bistit") LINK *1 TIME BARRIER SV-BOUNDARY) ;
REMOVE:LexSel ("biestit") IF (0 ("bistit") LINK 0* Sem/Event OR ("prográmma") OR ("sátta") OR ("guhká") BARRIER SV-BOUNDARY) ;

REMOVE:LexSel ("bivvát") IF (0 ("bivvat")) ;


	# beaivvis vs. beaivi 
	# ------------------
REMOVE:LexSel ("beaivvis") IF (0 ("beaivi") LINK -1 Num) ;			


	# boahtit vs. bohtat 
	# ------------------
	# boahtit 'come', bohtat 'swell'

	# These verbs have 5 homonym forms, Prs Du1/Pl3 bohte, Prt Sg1 bohten,
	# Prs Pl3/Prt Sg2 bohtet, Imprt Sg1 bohton, Imprt Pl1 bohtot
	# In almost all cases, the correct choice will be "boahtit", the 
	# challenge is to avoid removing the cases where it might be "bohtat"

LIST BOHTAT-NOUN = "bálká" "áiteuksa" "bunci" "dáigi" "fanas" "lássa" "láse" "luŋka" "stohpo#uksa" "uksa" "viessouksa" "vanca" ;


REMOVE:LexSel ("bohtat" Sg1) IF (0 ("boahtit")) ; # Will hit 'bohten' and 'bohton'.
REMOVE:LexSel ("bohtat" Sg2) IF (0 ("boahtit")) ; # Will hit 'bohtet'.
	#$  Go mun bohten du vissui,...
	#$  Journálat álmmolaš doaimmas bohtet arkiivalága vuollái.

REMOVE:LexSel ("bohtat" Du1) IF (0 ("boahtit")) ; # Will hit 'bohte'.
REMOVE:LexSel ("bohtat" Pl1) IF (0 ("boahtit")) ; # Will hit 'bohtot'.

REMOVE:LexSel ("bohtat" Pl3) IF (0 ("boahtit"))(*1 ("son" Pl3 Nom) BARRIER SV-BOUNDARY) ;
REMOVE:LexSel ("bohtat" Pl3) IF (*-1 ("son" Pl3 Nom) BARRIER NOT-ADV-PCLE)(0 ("boahtit")) ;

SELECT:LexSel ("boahtit") IF (0 ("bohtat"))(NEGATE *1 (Pron Pers) OR BOHTAT-NOUN BARRIER SV-BOUNDARY OR Neg OR NP-HEAD-NOM LINK 0 Nom)(NEGATE *-1 (Pron Pers) OR BOHTAT-NOUN BARRIER NOT-ADV-PCLE LINK 0 Nom)(NEGATE *-1 (Pron Rel) OR MO LINK *-1 BOHTAT-NOUN BARRIER SV-BOUNDARY LINK 0 Nom) ;
	# Only if there is a BOHTAT-NOUN Nom or "dat" Nom, we leave the possibility of "bohtat". But also in those cases, it cuold be "boahtit".

 	# borgit vs borgát
 	# ----------------
REMOVE:LexSel ("borgát") IF (0 ("borgit")) ;
	# One can always (?) understand it as "borgit" if it is homonomy. 		
 	
 
 	# buollit vs buollát
REMOVE:LexSel ("buollát") IF (0 ("buollit")) ;
	# One can always (?) understand it as "buollit" if it is homonomy. 		
 	
 
	# caggat vs.caggát
	# ----------------
	# caggat = 'tighten; keep open; hold back', caggát = 'be wide open'

REMOVE:LexSel ("caggát") IF (0 ("caggat"))(*1 NP-HEAD-ACC BARRIER NOT-NPMODADV) ;
	#$  Olmmoš caggá čuolu rávdnjái fávlli guvlui.



	# čohkkedit 
	# -------------------
SELECT:LexSel ("čohkkedit") IF (0 Imprt LINK *1 DOHKO BARRIER SV-BOUNDARY)  ;


	# čuohcat vs. čuohcit
	# -------------------
	# Prs Sg3 čuohcá.
	# Prs Pl3 čuhcet.

	# čuoigat vs čuoigát
	# ------------------
REMOVE:LexSel ("čuoigát") IF (0 ("čuoigat")) ;
	# One can always (?) understand it as "čuoigat" if it is homonomy.

	# čuollat vs čuollát
	# ------------------
REMOVE:LexSel ("čuollát") IF (0 ("čuollat") LINK *0 Acc BARRIER NOT-NPMODADV LINK NOT 0 TIME-N OR Sem/Route LINK NEGATE 0 Num LINK 1 TIME-N) ;
	#$ Go Niillas olle vuovdái ja čuollá ovtta muora, de son oaidná ahte ii doppe nagot buot muoraid čuollat, go vuovdi lea nu stuoris ahte geahči ii oidno.
	
	# čuovgat vs čuovgát
REMOVE:LexSel ("čuovgát") IF (0 ("čuovgat")) ;
	# One can always (?) understand it as "čuovgat" if it is homonomy. 		
	
    # divvut vs. dievvat
    # ------------------
SELECT:LexSel ("divvut") IF (0 ("dievvat" Der/PassL)) ;   	
SELECT:LexSel ("divvut") IF (0 ("dievvat") LINK *0 ACTOR-ROLE + Nom BARRIER SV-BOUNDARY) ;   	

 	# doadjit vs. dodjit
 	# ------------------
SELECT:LexSel ("doadjit") IF (*0 TOOL OR BODY OR PLANT BARRIER SV-BOUNDARY) ;

	# duđđat vs. duhtat
	# -----------------
	# duđđat = 'mess', duhtat 'be satisfied'

REMOVE:LexSel ("duđđat") IF (0 ("duhtat"))(*1 Ill BARRIER NOT-NPMODADV) ;
 	#$  Mun duđan dien vástádussii.

REMOVE:LexSel ("duđđat") IF (0 ("duhtat"))(1 go)(*2 Ill BARRIER NOT-NPMODADV) ;
	#$  Duđat go mu čállimii?

	# diehtit vs. dihttot
	# -------------------
	# diehtit is TV and takes an animate subject.
	# dihttot (vise seg, forekomme) is Passive and intransitive

SELECT:LexSel ("dihttot") IF (*-0 ("sátni") OR ("vearba") OR ("substantiiva") LINK 0 (@SUBJ)) ;
	#$  Sátni dihtto buot oarjesámegielain. # Needed here is a set of speach act and POS words.

	# dovdat vs. dovdát
	# -----------------

	#REMOVE ("dovdat") IF (-2 ("go"))(-1 ("vuos"))(0 ("dovdát" Sg3)) ;
  	#$  Go vuos dovdá ovtta unnitlohkoálbmoga, lea álki fuobmát oahpes
  	#$  minstariid maiddái eará unnitloguid diliin.
  # ii. dat ii doaimma ok, leago dat buoret? lea go MII buoret? MII? ahh..ii leat buoret. dat ferte leat "dovdat", muhto sahttasii maid leat "dovdát", muhto goitge "dovdat" orru leahkime buoret: 1.Hvis man først blir kjent med x, så er det lettere, 2. hvis man først kjenner et x, så er det lettere, 1. doaibmá maid
  # maid doai jáhkabeahtti?
  # datgo lei evttohus?
  # vurddes:
  # L: doaibmágo du mielas dadjat "go vuos dovdá" - dovdát - bli kjent med??
  # D: JEPP
  # L: muhto ii "go vuos dovdá" - dovdát - kjenne igjen ?
  # D: DAT NAI
  # (Duomma)

REMOVE:LexSel ("dovdát") IF (0 ("dovdat")) ;
	# One can always (?) understand it as "dovdat" if it is homonomy. 		

	# eallit vs. eallát
	# -----------------
	# These verbs have 2 homonym forms: Ind Prs Sg3 eallá, PrfPrc eallán
	# eallát = 'to recover, become alive again', eallit = 'to live'

REMOVE:LexSel ("eallát") IF (0 ("eallit"))(*1 ("eallin")) ;
	#$  Son eallá suttolaš eallima.
REMOVE:LexSel ("eallát") IF (0 ("eallit"))(*1 Loc OR ("siste") OR VAbess) ;
    	#$  Mun dovddan muhtun olbmo guhte eallá Kristusis. 
    	#$  Son eallá naitalkeahttá.
REMOVE:LexSel ("eallát") IF (0 ("eallit")) ;
	# One can always (?) understand it as "eallit" if it is homonomy. 		

#REMOVE:LexSel ("gallet") IF (0 ("galledit")) ;

	# fáhtet vs. fáhtit 
	# ----------------- 
SELECT:LexSel ("fáhtet") (0 ("fáhtit") LINK 1 ("gitta")) ;


	# gálgat vs. galgat 
	# ----------------- 
	# Added gálgat as subform of galgat

SELECT:LexSel ("galgat") (0 ("gálgat") LINK *1 Inf BARRIER SV-BOUNDARY) ;
	#$ Stállu ii šat diehtán, maid son gálggašii jearrat.

	# gárrat vs. gárrut 
	# ----------------- 
	# gárrat = 'bind sth to sth', gárrut = 'get sth done, +Inf'

#REMOVE:LexSel ("gárrat") IF (0 ("gárrut"))(*1 Inf BARRIER NOT-ADV-PCLE-NEG) ;
    	#$  Mii lávlluimet morašlávlagiid, ehpet ge dii gárron luoibmat.    	

	# "gártat" vs. "gártet" 


SELECT:LexSel ("gártat") (0 ("gártet") LINK *1 Ess BARRIER NOT-NPMOD) ;
	#$ Regiovdnaguovddážat gártet boaittobealguovlun.

    	
    # geargat IV vs. gerget TV
    # ------------------
	# gerget takes Acc     

IFF:LexSel ("gerget") IF (0 ("geargat"))(*1 Acc BARRIER NOT-NPMOD) ;
#REMOVE:LexSel ("gerget") IF (0 ("geargat"))(*1 Acc LINK 1 Inf BARRIER NOT-NPMOD) ;
#REMOVE:LexSel ("gerget") IF (0 ("geargat"))(*1 Acc LINK -1 Inf BARRIER NOT-NPMOD) ;

REMOVE:r3136 ("geavvat") IF (0 ("geavahit")) ;



	# geassit TV vs. geassut IV
REMOVE:LexSel ("geassut") IF (0 ("geassit") LINK *0 Acc OR ("vuorbi") BARRIER NOT-NPMODADV) ;		
	#$ Nordlysbearaš unnui dušše moatti olbmui ja maŋimuš vuorbi lei gesson.


	# gieldit vs. gildit 
	# ------------------
	# gieldit = 'deny sb sth', gildit = 'shine'
	# Here, we have a transitivity difference.
	# Homonym in the diphtong simplification cases: Prs Du1 gilde, Prs Pl3 gildet
	# all Prt forms, gilden, gildet, ..., all Pot forms gilddežan, ..., 
	# some of the Imprt forms. 

REMOVE:LexSel ("gildit") IF (0 ("gieldit"))(*1 Acc BARRIER VFIN) ;
    	#$  Jesus gilddii sudno garrasit:
    	#$  Muhto mađe eanet son gilddii, dađe eanet sii almmuhedje dan.
	# The second example is ambiguous.

SELECT:LexSel ("gieldit") IF (*1 (V Actio Loc) BARRIER SV-BOUNDARY) ;
	#$  Mun gilden Máhte boahtimis.

	# gillet vs giellat
	# ------------------ 
SELECT:LexSel ("gillet") IF (0 ("giellat") LINK *1 Inf BARRIER NOT-ADV-PCLE) ;
	#$ Vaikko Ánne Risten hálida boahtteáiggis eambbo gilvalit sabehiid nalde herggiiguin , de ii leat mihkkege dáhkádus man guhká gille doaibmat .
SELECT:LexSel ("gillet") IF (0 ("giellat") LINK *1 Acc BARRIER NOT-ADV-PCLE LINK 1 Inf + TV) ;
	#$ Dás lea sáhka das ahte gillego maidege bargat .

	# goaddat vs. goddit 
	# ------------------ 
	# goaddat = 'stick to, tarrautua', goddit = 'kill'
	# Homonym in the diphtong simplification cases.

REMOVE:LexSel ("goaddat") IF (0 ("goddit"))(*1 Acc BARRIER VFIN) ;
    	#$  Ja sii goddet su.
REMOVE:LexSel ("goaddat") IF (NEGATE *-1 Ill BARRIER SV-BOUNDARY)(0 ("goddit") LINK NEGATE *1 Ill BARRIER NOT-ADV-PCLE) ;   	

	# goarrut vs. gorrat
SELECT:LexSel ("goarrut") IF (*-1 BOC LINK *1 CLOTHES OR SHOE BARRIER SV-BOUNDARY LINK 0 Nom OR Acc)(0 ("gorrat")) ;		

	# goastit TV vs. goastat IV
REMOVE:LexSel ("goastit") IF (0 ("goastat") LINK NEGATE *0 Acc BARRIER NOT-NPMODADV LINK NOT 0 TIME-N OR Sem/Route LINK NEGATE 0 Num LINK 1 TIME-N) ;		
	#$ Gal moai goste iehčaneame.

	# goddet vs. goddit
	# -----------------
	# goddet = 'catch wild reindeer', goddit = kill

REMOVE:LexSel ("goddet") IF (0 ("goddit"))(*0 Acc BARRIER NP-BOUNDARY) ;
	#$  Vuovdái godde dán guovžža.

	# gođđat vs. gođđit 
	# ----------------- 
	# gođđat = 'spawn', gođđit = 'knit, weave'
SELECT:LexSel ("gođđat") IF (0 ("gođđit"))(*1 FISH BARRIER VFIN) ;
	#$  Ovddemus dáida oastit árppuid ja gođđá firpmiid.
SELECT:LexSel ("gođđit") IF (0 ("gođđat"))(*0 ("stovla") OR ("muorra") OR ("gođđinmuorra") OR ("sággi") OR Sem/Clth BARRIER VFIN) ;
	#$  Ovddemus dáida oastit árppuid ja gođđá firpmiid.

	# gullat vs. gullát vs. gullet
	# ----------------------------
	# gullat = 'hear; belong', gullát 'begin to hear', gullet 'fish'
   
SELECT:LexSel ("gullat") IF (0 ("gullát") LINK *0 SOUND OR ("sátni") BARRIER NOT-NPMOD) ;    
	#$ Mánná hárve gullá eatnigielas.

#SELECT:LexSel ("gullat") IF (0 ("gullát") OR ("gullet"))(*1 Ill OR ("vuollai") BARRIER SV-BOUNDARY LINK NOT 0 SOUND) ; # moved up
	#$  Dat gullá lunddolaččat dakkár áššiide go Sámedikkiid válganjuolggadusaide.

SELECT:LexSel ("gullat") IF (*-1 Ill BARRIER NOT-ADV-PCLE LINK NOT 0 SOUND)(0 ("gullát") OR ("gullet")) ;
	#$  Doaimma divššohasjournálaide gullá persovdnaregisttar.
	#$  Eanetlohku, masa gulle Riektejoavkku 6 eará lahtu,...

SELECT:LexSel ("gullat") IF (*1 ("ahte") BARRIER NOT-ADV-PCLE) ;
	#$  Mun gullen, ahte vulget ikte.

	# guođđit vs. guđđat
	# ------------------
	# guođđit = 'leave sth swh', guđđat = 'swarm' 

REMOVE:LexSel ("guđđat") IF (0 ("guođđit"))(*1 Acc BARRIER VFIN) ;
    	#$  De sii guđđet dan viesu ja gávpoga.

#REMOVE:LexSel ("guorrat") IF (0 ("guorahallat")) ;

	# háliidit vs. hálihit
	# - - - - - - - - - - - - - - -
REMOVE:LexSel ("hálihit") IF (0 ("háliidit")) ;	
  	#$  Son ii hálit dan dahkat.

	# huikit vs. huoikit
	# -------------------
SELECT:LexSel ("huikit") IF (0 ("huoikit")) ((1 (":")) OR (-1 COMMA LINK 2 (N Sg Nom) LINK *1 EOC BARRIER NOT-ADV)) ;	


	# joavdat vs. joavdit
	# -------------------
SELECT:LexSel ("joavdat") IF (0 ("joavdit"))(*1 Ill OR DOHKO BARRIER NOT-NPMODADV) ;	
	# juhkat vs. juohkit
	# ------------------
LIST JUOHKIN-ADV = ("hálddahuslaš" Adv) ("geográfa" Adv) ;
 
LIST JUOHKIN-SUBJ = "eiseváldi" ;
 
LIST JUOHKIN-PASS-SUBJ = "gávdnoštupmi" "eana" "čázádat" "vuoigatvuohta" ;


SELECT:LexSel ("juohkit") IF (0 ("juhkat"))(*0 Ill BARRIER Loc OR SV-BOUNDARY LINK NOT 0 TIME-N) ;	
SELECT:LexSel ("juohkit") IF (0 ("juhkat"))(*0 ("diehtu") OR TEXT OR ABSTR-TEXT OR CURRENCY OR Sem/Measr BARRIER NOT-NPMOD) ;		
SELECT:LexSel ("juohkit") IF (0 ("juhkat"))(*1 Ess BARRIER NOT-A-PCLE) ;	
SELECT:LexSel ("juohkit") IF (0 ("juhkat"))(*1 ("mielde") OR ("ektui") OR ("gaskkas") OR ("ovddas") OR ("gaskka") BARRIER NOT-NPMOD) ;	
SELECT:LexSel ("juohkit") IF (0 ("juhkat"))(*0 JUOHKIN-ADV BARRIER REAL-V) ;
SELECT:LexSel ("juohkit") IF (0 ("juhkat") LINK 0 Der/Pass LINK 0 (PrfPrc @>N)) ;
SELECT:LexSel ("juohkit") IF (*-1 JUOHKIN-PASS-SUBJ BARRIER Nom OR SV-BOUNDARY)(0 ("juhkat") LINK 0 Der/Pass) ;
SELECT:LexSel ("juohkit") IF (*-1 JUOHKIN-SUBJ BARRIER Nom OR SV-BOUNDARY LINK 0 (Pl Nom))(0 ("juhkat")) ;
	#$  Ruhta lea juhkkojuvvon máŋgga olbmui.
	#$  Eatnamat leat hálddahuslaččat juhkkojuvvon.	
	#$  Eatnamat leat juo juhkkojuvvon.
	#$  Eiseválddit juhke eatnamiid.
	
SELECT:LexSel ("juhkat") IF (0 ("juohkit") LINK -1 CC LINK -1 ("borrat")) ;
SELECT:LexSel ("juhkat") IF (0 ("juohkit") LINK 1 CC LINK 1 ("borrat")) ;
	#$  Ovdal stuoradulvvi sii borre ja juhke.	
	
SELECT:LexSel ("juhkat") IF (0 ("juohkit") LINK *0 LIQUID BARRIER Acc OR SV-BOUNDARY) ;
	#$  Sii juhket gáfe.	

SELECT:LexSel ("juhkat") IF (0 ("juohkit") LINK 1 ("buohkat")) ;
 	#$  Juhket buohkat dás!
 	#$  Ja sii juhke buohkat das.

	# laskat IV vs. lasket TV
SELECT:LexSel ("jullat") IF (0 ("lasket") LINK NEGATE *0 Acc BARRIER NOT-NPMODADV) ;


	# juollut IV vs. jullat IV
SELECT:LexSel ("juollut") IF (0 ("jullat")) ((*1 Inf BARRIER NOT-ADV-PCLE) OR (0 Imprt)) ;
 	#$ De jullot čoalkalit uksii vuos.


	# loktet vs. loaktit
SELECT:LexSel ("loktet") IF (*0 ("stáhtus") BARRIER SV-BOUNDARY) ;	
	#$ Dát sihke lasiha sámegiela geavaheami ja lokte giela stáhtusa ja árvvu.

	# lohkat vs. loahkkit
SELECT:LexSel ("lohkat") IF (0 ("loahkkit") LINK 0 Der/Pass) ;	

	# mannet vs. mannat
	# -----------------
REMOVE:LexSel ("mannet") IF (0 ("mannat") LINK 0 Sg1 OR Sg2) ; 
REMOVE:LexSel ("mannet") IF (0 ("mannat"))(*0 Ill BARRIER S-BOUNDARY) ;
REMOVE:LexSel ("mannet") IF (0 ("mannat"))(*0 ("dohko") OR ("maŋos") OR ("ruovtto#luotta") OR ("ruoktot") OR ("bajás") OR ("vulos") OR ("bokte") BARRIER S-BOUNDARY) ;
REMOVE:LexSel ("mannet") IF (0 ("mannat"))(*0 Inf BARRIER NOT-ADV) ;

	# mannut vs. mannat
	# -----------------
REMOVE:LexSel ("mannut") IF (0 ("mannat" Imprt)) ; 


	# máhccat vs. máhccut 
	# ------------------- #
	# máhccat = 'return, retreat', máhccut = 'make folds in'

	# máhttit vs máhttát
	# ------------------
REMOVE:LexSel ("máhttát") IF (0 ("máhttit")) ;
	# One can always (?) understand it as "máhttit" if it is homonomy. 		
	
	# muitit vs muitát
	# ----------------
REMOVE:LexSel ("muitát") IF (0 ("muitit")) ;
	# One can always (?) understand it as "muitit" if it is homonomy. 		
	
	# oađđit vs oađđát
	# --------------- #
REMOVE:LexSel ("oađđát") IF (*0 (AA Adv) BARRIER REAL-V)(0 ("oađđit")) ;		

	# ohcat vs oahcut
	# --------------- #
REMOVE:LexSel ("oahcut") IF (0 ("ohcat") LINK *1 Acc) ;
	#$  De ohcot moadde girjji!	
	
    # oastit vs. ostet
    # ---------------- #    
LIST OSTEN-NOUN = "náhkki" "siste#náhkki" "sisti" ;

SELECT:LexSel ("oastit") IF (0 ("ostet"))(*-1 BOS LINK NEGATE *1 OSTEN-NOUN LINK 0 Acc) ;     

	# orrut vs. orrot
	# ---------------- #
	# orrot = 'remain somewhere (not come back as expected), settle down'

SELECT:LexSel ("orrut") IF (0 ("orrot"))(1 (Actio Ess)) ;

SELECT:LexSel ("orrot") IF (0 ("orrut"))(-1 Ill) ;
SELECT:LexSel ("orrot") IF (0 ("orrut"))(*1 Ill BARRIER NOT-ADV) ;
REMOVE:LexSel ("orrut" Der/PassS) IF (0 ("orrut"))(1 ("go")) ;
REMOVE:LexSel ("orrut" Der/PassS) IF (0 ("orrut"))(*1 (Actio Ess) BARRIER NOT-ADV) ;
REMOVE:LexSel ("orrot") IF (0 ("orrut"))(NEGATE 1 ("go"))(NEGATE *1 (Actio Ess) BARRIER NOT-ADV)(NEGATE *-1 Ill BARRIER NOT-ADV) ;
REMOVE:LexSel ("orrot") IF (0 ("orrut"))(NEGATE 1 ("go"))(NEGATE *1 (Actio Ess) BARRIER NOT-ADV)(NEGATE *1 Ill BARRIER NOT-ADV) ;
  	#$  Mii áigut dohko orrot idjii.
  	#$  Buot dilálašvuođat main Ipmil orro leamen jávkosis. 
  	#$  Min Áigi jearrá orro go heivvolaš ahte son čállá bartnis birra.

	# rihkkut vs. riehkkat
	# ------------------- #
	# rihkkut = 'break, transgress, infringe, offens against, forfeit', riehkkat = 'work hard, make great efforts to..'

SELECT:LexSel ("rihkkut") IF (0 Der/Pass)(*-1 BOS LINK *1C LAHKA OR LAHKA-N) ;
  	#$  Láhka lea rihkkojuvvon.

# Not in use because of REMOVE Der1 IF (0 Der3 OR Der2) 
#REMOVE:LexSel ("ráfáiduvvat") IF (0 ("ráfáiduhttit")) ;
	#$  ráfáiduhttot
	
SELECT:LexSel ("savvot") IF (*-1 ("hávvi" Nom) BARRIER S-BOUNDARY)(0 ("savvit")) ;	

# Not in use because of Remove V-DER if Lex-v-rule.
#REMOVE:LexSel ("seahkut") IF (0 ("seaguhit")) ;	 

	# rahpat   vs rahppot  
	#---------------------
SELECT:LexSel ("rahpat") (*0 Sem/Buildpart OR Sem/Build) ;
	#$ Mun dieđán du daguid , ja mun lean bidjan du ovdii rahppojuvvon uvssa , man ii oktage sáhte dahppat .	
	

	# ruohttat IV vs ruhttet TV
	#---------------------

SELECT:LexSel ("ruhttet") IF (0 ("ruohttat") LINK *0 Acc BARRIER NOT-NPMODADV LINK NOT 0 TIME-N OR Sem/Route LINK NEGATE 0 Num LINK 1 TIME-N) ;	
SELECT:LexSel ("ruohttat") IF (0 ("ruhttet") LINK *0 Ill BARRIER NOT-NPMODADV) ;	


	# ruoidnat vs ruoidnát
	#---------------------
SELECT:LexSel ("ruoidnat") IF (0 ("ruoidnát")) ;	

	# sihtat vs. sihtit : D ja L have agreed to remove "sihtit". It´s not in KN.
	# -----------------
REMOVE:LexSel ("sihtit") IF (0 ("sihtat")) ; 

	 
	# sirdit vs. sirdát
	# -----------------
REMOVE:LexSel ("sirdát") IF (0 ("sirdit") LINK *0 Ill OR DOHKO OR ABSTR-ROUTE BARRIER SV-BOUNDARY) ; 
	#$  Son sirdá gitta Billavutnii. 
	
REMOVE:LexSel ("sirdát") IF (0 ("sirdit")) ;
	# One can always (?) understand it as "sirdit" if it is homonomy. 		
	
	# soahpat vs. sohpat
	# -----------------
REMOVE:LexSel ("sohpat") IF (0 ("soahpat") LINK NEGATE *0 ("dákti") + Acc OR Sem/Body + Acc) ; 
SELECT:LexSel ("sohpat") IF (0 ("soahpat") LINK *0 ("dákti") + Acc) ; 
	#$  Son láve sohpat dávttiid albma láhkai go lea boradeamen. 


	# váidit vs. váidut
	# -----------------

LIST VAIDUN-SUBJ = "ráfehisvuohta" "doahpun" "kulturiešmearrideapmi"  ;


REMOVE:LexSel ("váidut") IF (0 ("váidit") LINK NEGATE *-1 VAIDUN-SUBJ + Nom OR Sem/Prod-audio + Nom OR Sem/Wthr + Nom OR Sem/Feat-psych + Nom OR Sem/State + Nom BARRIER SV-BOUNDARY) ; 
	#$  Mannan ija leat moadde doarruma váidon Finnmárkkus. 
	
	 
	# vázzit vs. vázzát
	# -----------------
REMOVE:LexSel ("vázzát") IF (0 ("vázzit") LINK *0 Ill OR DOHKO OR ABSTR-ROUTE BARRIER SV-BOUNDARY) ; 
	#$  Son vázzá gávpogii. 

REMOVE:LexSel ("vázzát") IF (0 ("vázzit")) ;
	# One can always (?) understand it as "vázzit" if it is homonomy. 		
		
	 
	# vuodjat vs. vuodjit
	# ------------------
LIST VUODJIN-NOUN = "čáhcecrossa" "dáhkádus" "FMX" "mohtorsearvi" "motocross" "vuodjinlohpi" ;


SELECT:LexSel ("vuodjit") IF (0 ("vuodjat"))(0 Der/Pass) ;
SELECT:LexSel ("vuodjat") IF (0 ("vuodjit"))(*0 Sem/Ani + Nom) ;
SELECT:LexSel ("vuodjit") IF (0 ("vuodjat"))(*0 Sem/Veh OR Sem/Route LINK 0 Com OR Nom OR Acc OR Gen) ;
SELECT:LexSel ("vuodjit") IF (0 ("vuodjat") + Der/Pass)(*0 Sem/Veh + Ill) ;
SELECT:LexSel ("vuodjit") IF (0 ("vuodjat"))(*0 ("geaidnorasttildanbáiki") OR ("rasttildanbáiki") OR ("leahttu") OR (".*krossa"r) OR (".*cross"r)) ;
SELECT:LexSel ("vuodjit") IF (0 ("vuodjat"))(*1 CONCRETE-ROUTE OR ("bargu" Ill) OR ("ruoktot") OR ("siida" Ill) BARRIER VFIN) ;
SELECT:LexSel ("vuodjit") IF (0 ("vuodjat"))(*-1 BOS LINK *1 VUODJIN-NOUN) ; 
SELECT:LexSel ("vuodjit") IF (0 ("vuodjat"))(*1 Prop + Sem/Plc + Ill BARRIER VFIN) ;
REMOVE:LexSelPrfPrc ("vuodjat") IF (0 ("vuodjit") + PrfPrc) ;
REMOVE:LexSel Err/Orth IF (0 ("vuodjat") LINK 0 ("vuodjit")) ;
REMOVE:LexSel Err/Orth-spes IF (0 ("vuodjat") LINK 0 ("vuodjit")) ;
REMOVE:LexSel ("vuodjat") IF (0 ("vuojašit")) ;
#SELECT:LexSel VUODJIN-WORD IF (0 VUODJIN-WORD) ;
SELECT:LexSel ("vuodjat") IF (0 ("vuodjit"))(*0 WATER BARRIER SV-BOUNDARY LINK 0 Loc) ;
SELECT:LexSel ("vuodjat") IF (*-1 FISH OR ("vuogga") BARRIER NOT-AUX-V LINK 0 Nom OR Ess) ;
SELECT:LexSel ("vuodjat") IF (*0 ("beahcet") + Com) ;
	#$  Hálidatgo gullat go vudjen stuora sáhpana iežan sykkeliin?
	#$  Okta nubbi vuodjá tráktor máđiiráigge.
	#$  Ivdni lea deaŧalaš, muhto buot deaŧaleamos lea movt dat vuodjá čázis.
	#$  Sii vudjet bálgá mielde.
	#$  Sii vudjet Romsii.
	#$  Muhto min guovlluin eatnasat veajehin ja vudjet merrii maŋŋel go leat leamaš jagi jogas.


SELECT:default ("vuodjit") IF (0 ("vuodjat")) ;


	# vuohttit vs. vuohttát
	# ---------------------
	# vuohttá is sg3 of both these verbs
REMOVE:LexSel ("vuohttát") IF (0 ("vuohttit")) ;
	# One can always (?) understand it as "vuohttit" if it is homonomy. 		


	# vuolgit vs. vulgot
	# ---------------------
	
REMOVE:LexSel ("vulgot") IF (0 ("vuolgit") LINK *1 Ill OR PO-ILL BARRIER NOT-NPMODADV)  ;
	#$ Vulgot gávpogii, ovdalgo girjerádju manná gitta.
		
	# vuordit vs. vuordát
	# -------------------
REMOVE:LexSel ("vuordát") IF (0 ("vuordit"))(*0 TIME-N BARRIER S-BOUNDARY LINK 0 Acc OR Ill) ;	
REMOVE:LexSel ("vuordát") IF (0 ("vuordit"))(1 Num)(2 Po) ;
	#$  Son vuordá diimmu.
	#$  Son vuordá gitta mánnodahkii.
	#$  Doppe ledje geainnu vuordán 1924 rájes.
	
REMOVE:LexSel ("vuordát") IF (0 ("vuordit")) ;
	# One can always (?) understand it as "vuordit" if it is homonomy. 	



#!! # NOMEN
REMOVE ("gáfe") (0 ("gáffe") - Nom) ;
SELECT (Num Nom)((-1 ("diibmu") OR Num) OR (-2 ("diibmu")) OR (1 Num))   ;
	#$ Diibmu lea vihtta.

#!! ## Case rules

SELECT:NumGen (Num Gen) (-1 (Dem Gen)) ;
	#$ Dán čieža oassálasti searvvis ii lean oktage boazodoalli, muhto kurssa oahpaheaddjis, Kjell Smestadas, lea doaivva ahte boazodolliide maid galgá gávdnot sierra heivehuvvon fálaldat.
	
#!! ## Other rules for nouns and pronouns

#!! ## Determiners
#!! ## Adverbs and adjectives
SELECT:genAdv Adv (0 A LINK 1 SYN-V)(-1 Acc) ;	
	#$ Dihkkádeaddji rávve skohtervuddjiid várrugasat mátkkoštit, go dán jagi lea erenoamán heajos jiekŋa jogain.

#!! # NOUNS
SELECT:propBOC Prop (-1 BOC) ; 

	#$ Girjjis leat boares govat.

REMOVE (Pl Nom) (1 VFIN + Sg3) ;
REMOVE (Sg Nom) (1 VFIN + Pl3)(NEGATE 0 Sem/Org OR Sem/Group) ;

SELECT A (0 Attr LINK 0 N LINK 1 N) ;
	#$ Lea suohtas kurssas. <= her er ikke Attr analysen riktig

#!! * **derNEss** removes DER-N if lexicalised essives 
REMOVE:derNEss DER-N IF (0 DER-N LINK 0 Ess)(0 LEX-N LINK 0 Ess)(NEGATE 0 (NomAg Ess) OR (Der/NomAg Ess));
 	#$  Grammatihkkavuđot giellateknologalaš reaiddut teakstaanalyseremii ja giellaoahpahussii ja digitálalaš sátnegirjjit leat buot gielaide eaktun dasa ahte sáhttet ceavzit geavahangiellan ođđaáigásaš servodagas.
 	# Removes DER-N if O is also a lexical noun, and both nouns are essive.	
REMOVE:EssProNom Ess IF (0 (N Nom)) ; 
	#$ Su háliidusa mielde dollojuvvui Suonjila dálvesiiddas čoahkkin cuoŋománus 1938.

REMOVE:AdvSubqst Adv + Subqst + Foc/Neg-ge ; #((0 Foc/Neg-ge OR Foc/Pos-ge) OR (-1 ("nu")) OR (0 ("man"))) ;	
REMOVE:AdvSubqst Adv + Subqst + Foc/Pos-ge ; 
#SELECT:AdvSubqst Adv + Subqst  ;	

SELECT:AdvSubqst Adv + Subqst IF (NEGATE 0 Pron)(NEGATE 0 Sem/Sur LINK -1 ABBR OR Sem/Mal OR Sem/Fem) ;	
SELECT:AdvSubqst Adv + Subqst IF (0 Pron LINK 1 A + Attr OR A + Nom) ;	

SELECT:daninAdv ("danin" Adv)(1 VFIN) ; 
SELECT:daninAdv ("danin" Adv)(-1 BOS) ; 
IFF:daninAdv ("danin" Adv)(NEGATE *0 OPRED-V BARRIER SV-BOUNDARY) ; 

SELECT Adv IF (-1 N LINK -1 FMAINV)(0 A LINK 1 EOC) ;
SELECT:ADV Adv IF (-1 FMAINV)(0 (A Der/Comp) OR (A Pl) LINK *1 Acc BARRIER NOT-NPMOD  LINK NOT 0 TIME LINK NEGATE 1 go + @CNP) ;
	#$ Norgga láhkaaddit leat áigá jo suokkardan vuđolaččat kvenaid eamiálbmotstáhtusa gažaldaga.

AFTER-SECTIONS

#!! ## Variant lemmas
LIST LEMMA1 = "Annika" "álbmot" "árran" "bargi" "bealli" "beassášdoalut" "beassážat" "boadnji" "boalvvir" ("boaris" A Der/Comp) "buvssat" "čábmit" "čáppat" "čuohti" "dadjalit" "dahkamuš" "dálolaš"  ("dát" Pron Dem Pl Com)  "dološ" "eanaláigo#boahtu" "Elle" "gámmir" "girji" (".*geaisár"r) "guovssahasat" (".*gursa"r) "Jáhkob" "Jovnna" "kaleandar" (".*keallir"r) "lávži"
"lihpar" ("[0-9]*-lohku"r) "Máret" "máttut" (".*mállásat"r) ("mieđihit") (".*mirko"r) "moattis" "motocrossa" "moraš" (".*muslima"r) (".*Norga"r) "oapman" "oasálaš" (".*rádji"r) "rájes" ("rikkis" A Der/Comp Pl Nom) "ruošša#čuhti" "ruvdnu" "sáhpán" "smávvadálolaš" "speajal" "suoivva" "visot" "viššal" "vuollái" ;
    # first dictionary form

LIST LEMMA2 = "Annik" "álbmut" "ára" "bargit" "bealle" "beassášdoallu" ("beassi" Der/Dimin) "boadnjá" "boalvár" ("boaris" Der/Comp Der/AAdv Adv) "buksa" "čápmit" "čáppis" "čuođi" ("dadjat" Der/l) ("dahkat" Der/muš) ("dáigut" Der/NomAg N Sg Com) ("dállu" Der/lasj) ("doallu" Der/Dimin)   "eanaláigo#boađđu" "Elli"  "gámmár" "girje" (".*geaissir"r) "guovssahas" (".*gurse"r)  "Jáhkot" "Jovdna" "Jovna" "kaleanddar" (".*keallár"r) "lávžá"
"libar" ("[0-9]*-loahku"r) ("[0-9]*-logus"r) "Máre" "máddu" (".*mális"r) "mieđidit" (".*mirku"r) (".*mirkko"r) "moattes" "motocross" "mora" (".*muslim"r) (".*Norge"r) "oama" ("oassi" Der/lasj) (".*rádjá"r) "rájis" ("rikkis" Der/Comp Der/AAdv Adv) "ruošša#čuđđi" "ruvdno" "sábin" ("smávvadállu" Der/lasj) "speadjal" "suoivvan" "viso"  "viššalaš" "vuolli" ;

#!! * Remove lemma2 if lemma 1
REMOVE:lemma2 LEMMA2 IF (0 LEMMA1);
	#% Leago Čáhppes Bearral oahpis dutnje?

SELECT:beassážat SUB:1 ("beassážat") IF (0 N) ; #For Apertium
REMOVE:ArabErr Err/Orth IF (0 Arab) ; # 


IFF:doalut ("doalut") IF (0 ("doallu") LINK NEGATE *0 ("boazu"));

SELECT:miiSG ("mii") + Sg + Acc ; 

LIST TYP-A = "árat" "čeahppi" "čeavlái" ;

LIST A-PL-N = "bassi" "boaris" "buohcci" "duiskalaš" "duiska" "headju" "lagamuš" "lohkki" "njálggis" "norgalaš" "nuorra" "ovddasteaddji" "rikkis" "searra" "vašálaš" "velggolaš" ;
SELECT:NPlwhenAPl (N Pl) + $$CASE (0 A + $$CASE LINK NOT 0 TYP-A) ;
SELECT:APlwhenNPl (A Pl) + $$CASE (0 N + $$CASE LINK 0 TYP-A) ;
SELECT:APlwhenNPl (A Pl) (1 N + Pl LINK 1 COPULAS) ;

SELECT:PropAcc Acc (0 Prop LINK -1 Acc + N) ;

#SELECT:NomwhenAcc Acc (0 N LINK 0 Acc OR Gen) ;


SELECT:NomCCNom Nom (-1 CC LINK -1C Nom) ;
SELECT:AttrwhenAdv Attr (0 Adv - ("njuolga") LINK 1 N)(NEGATE -1 ("man") OR REAL-V)(NEGATE 1 Prop)(NEGATE 0 DEHALAS LINK *0 COPULAS + FMAINV BARRIER SV-BOUNDARY) ;
	#% – Lea hui somá beassat mielde go lean hárjehallan ja de olahan mihtuid, dadjá Lea-Christine movttegit Ávvirii.
SELECT:PlNomwhenAdv (N Pl Nom) (0 Adv)((*-1 V + Pl3  OR COPULAS + Inf BARRIER NOT-ADV-PCLE LINK NOT -1 (Nom Pl) OR (Nom Pl3)) OR (*1 V + Pl3  OR COPULAS + Inf BARRIER NOT-ADV-PCLE LINK NOT 1 (Nom Pl) OR (Nom Pl3)))(NEGATE -1 (Rel Pl))(NEGATE 0 ("eanebut")) ;
	#% Muhto beatnagat harre ja fáhkka čiellagohte issorasat.

REMOVE:Focge Foc/ge (0 Adv) ;
REMOVE:GenSplitR Cmp/SgGen (0 Cmp/SplitR) ; #This one doesn't function i apertium, why?
REMOVE:GenSplitR (cmp_sggen) (0 (cmp_splitr)) ; #For apertium 
#SELECT:AttrCmp (cmp_attr) (0 (cmp_sggen) OR (cmp_sgnom));
SELECT:AdvwhenAPl Adv (0 A + Pl OR Dem + Loc) ;
SELECT:AdvwhenAPl Adv (0 ("árat") LINK 1 Dem) ;
REMOVE:AdvwhenRefl Adv + Px (0 Refl) ;
SELECT:Attr (A Attr)(NOT 0 Adv OR DEHALAS LINK 1 N) ;
SELECT:Nom Nom (0 DEHALAS + Attr LINK -1 COPULAS + FMAINV)((NEGATE 1 Ess) OR (NEGATE 1 Nom LINK 1 COMMA)) ;
REMOVE:GenAllegro Allegro IF (0 Gen - Allegro)  ;
SELECT:NvsA1 A + Com (0 N)(1 N + Com) ;
SELECT:NvsA2 N (0 A + Sg) (*-1 Pl3 OR Sem/Rule BARRIER VFIN OR N) ;
SELECT:NvsA3 N (0 A LINK -1 N + Gen OR Pron + Gen)  ;
SELECT:NvsA4 A (0 N)((1 CC LINK 1 A) OR (-1 CC LINK -1 A)) ;
SELECT:NvsA5 A (0 N)((*-1 Ill OR ("orrut") OR COPULAS BARRIER SV-BOUNDARY OR N) OR (1 Ill))(NOT 1 Inf)(NOT -1 Num) ;
SELECT:NvsA6 N (0 A) ; 
SELECT:Adv Adv (0 A LINK *-1 REAL-V - COPULAS BARRIER NOT-ADV-PCLE) ; 
SELECT:Adv Adv (0 Superl LINK 1 EOC)(-1 ("buot") LINK *-1 REAL-V - COPULAS BARRIER SV-BOUNDARY) ; 
	#$ Ellen Katrine Hætta dine buot unnimus.

#!! * **cleanSemClass** cleans up if a word has more semclasses. This is just a start.
SELECT:cleanSemClass Sem/Fem OR Sem/Mal (1 FAMILY OR NON-FAMILY) ;
SELECT:cleanSemClass Sem/Fem OR Sem/Mal + (Sg Nom) (*1 VFIN + Sg3 BARRIER NOT-ADV) ;

SELECT:cleanSemClass Sem/Plc (0 Sem/Org OR Sem/Sur OR Sem/Fem OR Sem/Mal) ;
SELECT:cleanSemClass Sem/Mal (0 Sem/Sur) ;
SELECT:cleanSemClass TIME-N (0 Sem/Measr OR Sem/Org) ;
SELECT:cleanSemClass Sem/Ani (0 Sem/Hum) ;
	#$ Nussir

#!! # VERBS
#REMOVE:InfifVFIN Inf (0 VFIN + FMAINV LINK NOT 0 SUBJ OR OBJ LINK NEGATE *-1 AUX - Inf OR PrfPrc)(*0 REAL-V BARRIER S-BOUNDARY) ;


SELECT:SSg2Qst V + Sg2 (0 V + Pl1 )((0 Qst) OR (1 Qst)) ;
SELECT:SSg2Interr V + Sg2 (0 V + Pl1  LINK *-1 Interr) ;
	#$ Makkár musihka guldalat millosepmosit?

SELECT:Pl3 V + Pl3 (0 V + Sg2 LINK -1 S-BOUNDARY LINK *-1 Pl3) ;
	#$ Iskkadeapmi čájehii maiddái ahte oallugat dovdet ahte sii máhttet veaháš sámegiela, muhto eai máhte nu olu ahte dustet geavahit giela.

SELECT:SSg2Qst V + Sg2 (0 V + Pl1  LINK -1 CS)(NEGATE *0 Pl1) ;
SELECT:SSg2Pot V + Sg2 (0 V + Pot + Pl1) ;
SELECT:Pl3Pot Pl1 IF (0 Pot + Pl3 LINK *0 Pl1 OR Sg1) ;
SELECT:Pl3Pot Pl3 IF (0 Pot + Pl1 LINK *0 Pl3) ;
SELECT:Pl1 Pl1 IF (*-1C Pl1 LINK NOT 0 Loc) ;
SELECT:Pl1 Pl1 IF (-1 CS LINK NEGATE *-1 Pl3 OR Pl) ;
REMOVE:Pl1 Pl1 IF (0 Prs + Pl3 )((*-1 Loc OR (Pl Nom) OR (Sg Nom) OR Pl3 OR (Num Nom) BARRIER VFIN) OR (*1 ("?"))) ;
SELECT V + Pl1  (0 V + Pl3  LINK NOT 0 Inf) ;
REMOVE:ImprtifVFIN Imprt (0 VFIN LINK NOT -1 BOS)(NEGATE 0 Pl2 LINK -1 ("de") LINK *-1 Pl2)  ;
SELECT:Ind Ind (0 Imprt) ;
SELECT:dan-sadjái-go-sáddet (V Inf) (-3 ("<dan>")) (-2 ("<sadjái>")) (-1 ("<go>")) (0C V) ;
REMOVE Cmp/SgGen (0 Cmp/SgNom) ;	


REMOVE:Cit (Cmp/Cit) ; 
REMOVE:Cit SUB:1 (Cmp/Cit) ; 

REMOVE SUB:1 Cmp/SgGen (0/1 Cmp/SgNom) ;	#This for HFST
REMOVE SUB:2 Cmp/SgGen (0/2 Cmp/SgNom) ;	#This for HFST
REMOVE SUB:1 A - Cmp/Attr (0/* Cmp/Attr) ;	#This for HFST
#SELECT SUB:1 Prop - Attr (0/* Prop + Nom) ;	#This for HFST
REMOVE SUB:1 Prop + Attr (0/* Cmp/Hyph) ; # This for HFST

#!! Test: Go for minimal weight.
SELECT:minweight (<W=MIN>);

#!! ## Final removing rules

#!! * **TEST** selects some infinte verb readings in the cohort
SELECT:TEST OBJ + V OR SUBJ + V OR V + @>N ;
 	#$ Mii ávkkiid lea min doppe johtit?
 	

	
#!! # Removing Err/Orth
# Denne regelen fjerner Err/Orth når det er samme lemma, sjøl om morfologien er forskjellig.



REMOVE:errsub1 $$WORDLEMMA + Err/Orth (0 $$WORDLEMMA - Err/Orth ) ; #this one does not function in Apertium because there are no err_orth tags. Alternative rules:
REMOVE:errsub2 $$WORDLEMMA + Err/Orth-spes (0 $$WORDLEMMA - Err/Orth-spes ) ; #this one does not function in Apertium because there are no err_orth tags. Alternative rules:

REMOVE:err SUB:1 $$WORDLEMMA + Err/Orth  ; #this one does not function in Apertium because there are no err_orth tags. Alternative rules:
REMOVE:err2 SUB:2 $$WORDLEMMA + Err/Orth  ; #this one does not function in Apertium because there are no err_orth tags. Alternative rules:


REMOVE:errsub1 SUB:1 $$WORDLEMMA + Err/Orth (0/1 $$WORDLEMMA - Err/Orth ) ; 
REMOVE:errsub2 SUB:1 $$WORDLEMMA + Err/Orth-spes (0/1 $$WORDLEMMA - Err/Orth-spes ) ; 


REMOVE:alt_errsub1 $$WORDLEMMA + (allegro) (0 $$WORDLEMMA - (allegro) ) ;
SELECT:alt_errsub2 $$WORDLEMMA + (nom) (0 $$WORDLEMMA - (nom)) ((1 COPULAS) OR (-1 COPULAS)) ; #for apertium
REMOVE:alt_errsub2 $$WORDLEMMA + Gen + Ord (0 $$WORDLEMMA + Nom ) ;
SELECT:alt_errsub2 $$WORDLEMMA + (nom) + (np) (0 $$WORDLEMMA - (nom) LINK 1 CC ) ; #for apertium
SELECT:alt_errsub2 Prop + Attr (1 Prop OR (np) ) ; 

REMOVE VFIN + Err/Orth (0 VFIN - Err/Orth) ;

SELECT SEMTAGS + N (0C N) ;
SELECT SUB:1 SEMTAGS + (cmp_attr) ; #For Apertium
SELECT SUB:1 SEMTAGS + (n) ; #For Apertium
SELECT SUB:2 SEMTAGS + (n) ; #For Apertium
SELECT (cog) OR (top) OR (ant) ; #For Apertium


	# Disambiguating the semantic tags
	# ================================

	# Disambiguating symbols and punctuation marks #
	# Post-syntactic morphological disambiguation - Cycle 5 #
	# Tidying up some loose ends #
	# Substituting some syntactic tags #
	#=====#
      END #
	#=====#
