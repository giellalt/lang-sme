!Distributed under the terms of the GNU General Public License version 2 
! or any later version.

! ===================================== !
! Phonological converter for North Sámi !
! ===================================== !

! To eventual curious readers
! This file tries out conversion from Sámi orthography to a phonetic
! translation based upon SAMPA. There is a switch at the end to get IPA instead.

! Usage: Compile sme as usual:
! make TARGET=sme
! Pipe text through the analyser:
! cat text | preprocess | lookup -flags mbTT bin/phon-sme.fst | ..

!We may also add POS tagging:
! cat corp/pedkorpus.txt | preprocess | lookup -flags mbTT -utf8 bin/pos-sme.fst | tr '\t' '+' | cut -d"+" -f1,3 | uniq | lookup -flags mbTT -utf8 bin/phon-sme.fst | cut -f2 | l

! In order to make this better we should uniq the closed POS before giving them
! to the phonological analyser (TODO: Check whether output is ok).

! We might also add syllable boundaries. Here the simple version:
!cat corp/pedkorpus.txt | preprocess | lookup -flags mbTT -utf8 bin/hyphrules-sme.fst | cut -f2 | lookup -flags mbTT -utf8 bin/phon-sme.fst | cut -f2 | tr '\n' ' '  | see

! echo "dat lea 123" | preprocess | 
! lookup -flags mbTT -utf8 bin/sme-num.fst | cut -f2 |
! lookup -flags mbTT -utf8 bin/hyphrules-sme.fst | cut -f2 | 
! lookup -flags mbTT -utf8 bin/phon-sme.fst | cut -f2 | tr '\n' ' ' | l


echo <<defining Cns and Vow...>>

!define POSTAG %+N -> %+N,
!              %+A -> %+A, 
!              %+V -> %+V,
!              %+Adv -> %+X, 
!              %+Pron -> %+X,
!              %+CS -> %+X,
!              %+CC -> %+X,
!              %+Adp -> %+X,
!              %+Po -> %+X,
!              %+Pr -> %+X,
!              %+Interj -> %+X,
!              %+Pcle -> %+X,
!              %+Num -> %+X, 
!              %+CLB -> %+CLB ;

define Vow       [ a | á | e | i | o | u | y | æ | ø | å | ä | ö | 
                   A | Á | E | I | O | U | Y | Æ | Ø | Å | Ä | Ö | 
                   é | ó | ú | í | à | è | ò | ù | ì | ë | ü | ï | 
                   â | ê | ô | û | î | ã | ý |
                   É | Ó | Ú | Í | À | È | Ò | Ù | Ì | Ë | Ü | Ï | 
                   Â | Ê | Ô | Û | Î | Ã | Ý ] ;

define Cns [ b | c | č | d | đ | f | g | h | j | k | l | m | n | ŋ | 
             B | C | Č | D | Đ | F | G | H | J | K | L | M | N | Ŋ | 
             p | q | r | s | š | t | ŧ | v | w | x | z | ž |   
             P | Q | R | S | Š | T | Ŧ | V | W | X | Z | Ž ] ; 

define Stop [ p | t | k | c | č ] ;

define Alv [ t | T | d | D | ŧ | Ŧ | đ | Đ | s | S | l | L | r | R | n | N ] ;

define Son [ m | n | ŋ | l | r | j | v | đ ] ;

define Nas [ m | M | n | N | ŋ | Ŋ ] ;

define down [ A -> a, Á -> á, B -> b, C -> c, Č -> č, D -> d, E -> e, F -> f, 
              G -> g, H -> h, I -> i, J -> j, K -> k, L -> l, M -> m, N -> n, 
              Ŋ -> ŋ, O -> o, P -> p, Q -> q, R -> r, S -> s, Š -> š, T -> t, 
              U -> u, V -> v, W -> w, X -> x, Y -> y, Z -> z, Ž -> ž, Æ -> æ, 
              Ø -> ø, Å -> å, Ö -> ö, Ä -> ä  ] ;


echo << Rules>>


echo <<defining Cns and Vow...>>


define Syll Cns* Vow+ Cns* %^ Cns* ;


echo << Rules >>


! Here comes the other rules


define ijRule  i -> j || Vow _ ;

define slRule s -> š || [.#.|%#|%-] _ l ;


define Schwa  m %^ s -> m ə %^ s ,   
              m %^ š -> m ə %^ š , 

              n %^ v -> n ə %^ v ,   

              l %^ r -> l ə %^ r ,
              l %^ b -> l ə %^ b ,   
              l %^ f -> l ə %^ f ,   
              l %^ g -> l ə %^ g ,   
              l %^ j -> l ə %^ j ,   
              l %^ k -> l ə %^ k , 
              l %^ p -> l ə %^ p ,   
              l %^ s -> l ə %^ s ,   
              l %^ v -> l ə %^ v ,   
              l %^ š -> l ə %^ š , 
              l %^ z -> l ə %^ d s , 
              l %^ ž -> l ə %^ d š , 

              r %^ b -> r ə %^ b ,
              r %^ d -> r ə %^ d ,
              r %^ f -> r ə %^ f ,
              r %^ g -> r ə %^ g ,
              r %^ j -> r ə %^ j ,
              r %^ k -> r ə %^ k ,
              r %^ p -> r ə %^ p ,
              r %^ s -> r ə %^ s ,
              r %^ t -> r ə %^ t ,
              r %^ v -> r ə %^ v ,
              r %^ š -> r ə %^ š ,
              r %^ z -> r ə %^ d s ,
              r %^ ž -> r ə %^ d š ,

              đ %^ b -> đ ə %^ b , 
              đ %^ g -> đ ə %^ g , 
              đ %^ j -> đ ə %^ j , 
              đ %^ v -> đ ə %^ v                
                              || [.#.|%-] (Syll Syll) Cns* Vow+ _ Vow ;


define FinalLightT t -> h ||  _ .#. ;
define FinalLightD d -> h || Vow  j   _ .#. ;


define kcRule  k %^ c -> k %^ h c || Vow _ Vow ;
define ktjRule k %^ č -> k %^ h č || Vow _ Vow ;
define tkRule  t %^ k -> t %^ h k || Vow _ Vow ;
define tmRule  t %^ m -> t %^ h m || Vow _ Vow ;

define 3rdSylld d -> đ || .#. Syll Vow %^ _ Vow ;

define rSchwa r -> r ə || Vow _ %^ [ Cns - Alv | b m | d n | d n j | g ŋ ] Vow ;
define dSchwa đ -> đ ə || Vow _ %^ [ Cns - Alv | b m | d n | d n j | g ŋ ] Vow ;
define lSchwa l -> l ə || Vow _ %^ [ Cns - Alv | b m | d n | d n j | g ŋ ] Vow ;

!áhč^či -> áhh^či
define Preaspiration c -> h || h _ %^ c ,, 
                     č -> h || h _ %^ č ,,
                     p -> h || h _ %^ p ,,
                     t -> h || h _ %^ t ,,
                     k -> h || h _ %^ k ;

define jPreaspiration [..] -> h || Son %^ _ Stop Vow , Son
 _ Stop %^ Stop ;


! Note IPA/SAMPA value of c in next rule!!
define LongPalatalAffricate  d %^ d j -> J\ %^ c  || Vow _ Vow ;
define ShortPalatalAffricate d %^ j   -> c  %^ c  || Vow _ Vow ;

define PlosiveDevoicing d -> t , g -> k , b -> p || Vow _ %^ Nas ;

! Latusforlenging
define LatusLengthening a -> a a || .#. Cns* [a|e|o] (Cns) %^ Cns _ ;

define PrimaryLenghtening  h ->  h h || Vow _ %^ Stop [ i [.#.|Cns] | u | á | a a] ;
! johtit -> johhtit

! Triple length?
! Cns1 Cns2 Cns2 -> Cns1 Cns2 Cns2 Cns2 || _ i;
! Cns1 Cns2 Cns2 -> Cns1 Cns2 Cns2 Cns2 || _ u;
! Cns1 Cns2 Cns2 -> Cns1 Cns2 Cns2 Cns2 || _ á;

! vi må liste konsonantpar
!define TripleLength Cns2 Cns2 -> Cns2 Cns2 Cns2 || Cns1 _  [ i [.#.|Cns] | u | á | a a] ;
! + lignende regler for <pp tt cc kk> osv.
! Vi kan ikje ha variablar i uttrykka til venstre, dei kan berre vere i
! konteksten. Altså må vi skrive om.


! Sekundær forlenging (her må vi liste konsonantpara)
define SecondaryLengthening l r %^ r -> l r r %^ r ,
                            l b %^ b -> l p p %^ p ,   
                            l f %^ f -> l f f %^ f ,   
                            l g %^ g -> l k k %^ k ,   
                            l j %^ j -> l j j %^ j ,   
                          l h k %^ k -> l L L %^ k ,   
                          l h p %^ p -> l L L %^ p ,   
                            l s %^ s -> l s s %^ s ,   
                            l v %^ v -> l v v %^ v ,   
                            l š %^ š -> l š š %^ š , 
                          l h t %^ t -> l L L %^ t , 
                          l h c %^ c -> l L L %^ t s , 
                          l h č %^ č -> l L L %^ t š , 
                            l z %^ z -> l t t %^ t s , 
                            l ž %^ ž -> l t t %^ t š , 
              
                            r b %^ b -> r p p %^ p ,
                            r d %^ d -> r t t %^ t ,
                            r f %^ f -> r f f %^ f ,
                            r g %^ g -> r k k %^ k ,
                            r j %^ j -> r j j %^ j ,
                          r h p %^ p -> r R R %^ p ,
                            r s %^ s -> r s s %^ s ,
                          r h k %^ k -> r R R %^ k ,
                          r h t %^ t -> r R R %^ t ,
                          r h c %^ c -> r R R %^ t s ,
                          r h č %^ č -> r R R %^ t š ,
                            r v %^ v -> r v v %^ v ,
                            r š %^ š -> r š š %^ š ,
                            r z %^ z -> r t t %^ t s ,
                            r ž %^ ž -> r t t %^ t š ,

                            m b %^ b -> m p p %^ p ,
                            n d %^ d -> n t t %^ t ,
                            n g %^ g -> ŋ k k %^ k ,
                            n j %^ j -> n j j %^ j ,
                          m h p %^ p -> m M M %^ p ,
                            m s %^ s -> m s s %^ s ,
                            n s %^ s -> n s s %^ s ,
                          n h k %^ k -> ŋ Ŋ Ŋ %^ k ,
                          n h t %^ t -> n N N %^ t ,
                          n h c %^ c -> n N N %^ t s ,
                          n h č %^ č -> n N N %^ t š ,
                            m v %^ v -> m v v %^ v ,
                            m š %^ š -> m š š %^ š ,
                            n š %^ š -> n š š %^ š ,
                            n z %^ z -> n t t %^ t s ,
                            n ž %^ ž -> n t t %^ t š ,
              
                            đ b %^ b -> đ p p %^ p , 
                            đ g %^ g -> đ k k %^ k , 
                            đ j %^ j -> đ j j %^ j , 
                            đ v %^ v -> đ v v %^ v , 
                            
                            v t %^ t -> v V V %^ t ,
                            v k %^ k -> v V V %^ k ,
                            v p %^ p -> v V V %^ p ,
                            v c %^ c -> v V V %^ t s ,
                            v č %^ č -> v V V %^ t š
                               
              || Vow _  [ i [[ Vow - i ] | Cns | .#. ] | u | á | a a ] ;


define HighVowelLenghtening i -> i i , u -> u u || .#. Syll Cns _ ( [Cns - j] ) .#. ;

! Her må vi bygge inn en betingelse om at svakt trinn er én konsonant (altså
! <pallu> -> [pallluu], men <kolli> i akk forblir [kollii]
! Det ser ut til at dette er avhengig av leksikon. Neste prosjektrunde...





! Så må /h/ assimilere til en forangående sonorant
! IPA symbols...
!! define SonorantDevoicing h h -> M M || m _ %^ ,, Superfluous???
!!                          h h -> N N || n _ %^ ,, Superfluous???
!!                          h n -> Ŋ Ŋ || ŋ _ %^ ,, Superfluous???
define StrongNasalClusters m %^ p -> m m %^ M p ,
                           n %^ t -> n n %^ N t ,
                           n %^ k -> n n %^ N k ,
                           n %^ c -> n n %^ N c ,
                           n %^ č -> n n %^ N č 
                           || Vow _ Vow ;
! lampa
                          
! [laamhh.paa] -> [laamMM.paa]

!  Er dette berre ei formalisering av det over? Det ser sånn ut.
! h -> Sonh || Son _ ;
! h -> Sonh || Sonh _ ;
! 
! Diftongforkorting foran Cns Cns ^%
! Dette er eit problem, eg veit ikkje korleis vi skal representere korte diftongar

! Allegro former... Hva gjør vi med dem?
! Viss det er former som berre er allegro i visse delar av setninga, men ikkje
! elles, må vi ta dei som del av syntaksen på noko vis.
! Viss dei er allegro i samansetjingar må vi gå til leksikon

define VzzRule z %^ z -> d %^ t s , ž %^ ž -> d %^ t š , 
               b %^ b -> b %^ p   , d %^ d -> d %^ t   , g %^ g -> g %^ k 
               || Vow _ Vow ;
define zzRule  z %^ z -> t %^ t s , ž %^ ž -> t %^ t š ,
               b %^ b -> p %^ p   , d %^ d -> t %^ t   , g %^ g -> k %^ k ;
             ! Bled by previous

define bdgRule b -> p , d -> t , g -> k || [.#.|%-] _ Vow ;

define VzRule  z -> d s , ž -> d š  || Vow %^ _ Vow ;
define ttjRule č %^ č -> t %^ t š , c %^ c -> t %^ t s ;
define cRule   c      -> t s ,      č      -> t š ;

define VowelLengthening e -> e e , i -> i i , o -> o o , u -> u u 
                             || .#. (Cns*) _ (h) %^ Cns Vow [Cns | .#. ] ;


echo << Dialectal rules >>

!define dhRule đ -> r ; ! Moving from Karesuando to Kautokeino
!define thRule ŧ -> s ;

define eaRule a -> ä || e _ ;

define aaRule á -> a a ;

define PronounLenghtening u -> u u || .#. [m|t|s] _ .#. ;


echo << And now we go for some OUTPUTFORMAT or another >>

! The rules above are somewhat oriented towards the orthography, and rearrange
! based upon that. Now, we might want to have output in different formats for 
! different reasons:
! SAMPA   = for the Helsinki tts engine
! IPA     = so that we may read it during development
! FINNISH = to target the fallback Bitlips demo if we are not able to meet the
!           October deadline with the generated Sámi voice.

!The idea is to comment out two of the following three rules and keep the one intended.

! ... here outputformat is SAMPA:              
!define OUTPUTFORMAT ə -> %@ , ä -> %{ , æ -> %{ , ö -> 2 , ø -> 2 , a -> A , 
!                   n j -> J , ŋ -> N , š -> S , đ -> D ,  ŧ -> T , 
!              L -> l %_ %0 , R -> r %_ %0 , M -> m %_ %0 , N -> n %_ %0 , 
!              Ŋ -> N %_ %0 , V -> v %_ %0 ,
!               %^ -> %. ;

! ... here outputformat is IPA:              
 define OUTPUTFORMAT ä -> æ , ö -> ø , n j -> ɲ , š -> ʃ , đ -> ð, ŧ -> θ ,
                     L -> l̥ , R -> r̥ , M -> m̥ , N -> n̥ , Ŋ -> ŋ̊ , V -> v̥ , 
                     
                     a a -> a ː , e e -> e ː , i i -> i ː , o o -> o ː , u u -> u ː ,
                     b b -> b ː , d d -> d ː ,  f f -> f ː , g g -> g ː , 
                     h h -> h ː , j j -> j ː , k k -> k ː , l l -> l ː , m m -> m ː ,
                     n n -> n ː , p p -> p ː , r r -> r ː , s s -> s ː , t t -> t ː ,
                     v v -> v ː ,
                     l̥ l̥  -> l̥ ː , r̥ r̥  -> r̥ ː , m̥ m̥ -> m̥ ː , n̥ n̥ -> n̥ ː , 
                     ŋ̊ ŋ̊ -> ŋ̊ ː , v̥ v̥ -> v̥ ː , 

                     %^ -> %. ;

! ... here outputformat is Finnish orthography:              
! define OUTPUTFORMAT ŋ -> n g , ŧ -> s , đ -> r , š -> s ,
!                       %^ -> %.  ;
! This rule is incomplete!

echo <<Combining...>>

read regex [ 
!    POSTAG .o. 
    down 

.o. ijRule

.o. slRule

.o. FinalLightT
.o. FinalLightD
.o. Schwa
.o. kcRule  
.o. ktjRule 
.o. tkRule  
.o. tmRule  

.o. 3rdSylld
.o. rSchwa
.o. dSchwa
.o. lSchwa

.o. Preaspiration
.o. jPreaspiration

.o. LatusLengthening
.o. PrimaryLenghtening
.o. SecondaryLengthening
.o. PlosiveDevoicing
.o. HighVowelLenghtening
.o. StrongNasalClusters

.o. VzzRule
.o. zzRule
.o. VzRule
.o. bdgRule
.o. ttjRule
.o. cRule  

.o. VowelLengthening

.o. eaRule
.o. aaRule
.o. PronounLenghtening

.o. OUTPUTFORMAT

] ;


invert net ;


!  For reference: The SAMPA - IPA correspondence

!  SAMPA   IPA  Description
!  p       p    voiceless bilabial stop
!  b       b    voiced bilabial stop
!  t       t    voiceless alveolar or dental stop
!  d       d    voiced alveolar or dental stop
!  ts      ʦ    voiceless alveolar affricate
!  dz      ʣ    voiced alveolar affricate
!  tS      ʧ    voiceless postalveolar affricate
!  dZ      ʤ    voiced postalveolar affricate
!  c       c    voiceless palatal stop
!  J\      J    (overstroked j)    voiced palatal stop
!  k       k    voiceless velar stop
!  g       g    voiced velar stop
!  q       q    voiceless uvular stop
!  p\      ɸ    (Greek phi)    voiceless bilabial fricative
!  B       β    (Greek beta)    voiced bilabial fricative
!          ϐ    (Greek beta alt)    voiced bilabial approximant
!  f       f    voiceless labiodental fricative
!  v       v    voiced labiodental fricative
!  T       θ    (Greek theta)    voiceless dental fricative
!          ϑ    (Greek theta alt)    voiceless dental approximant
!  D       ð    (Icelandic eth)    voiced dental fricative
!          δ    (Greek delta)    voiced dental approximant
!  s       s    voiceless alveolar fricative
!  z       z    voiced alveolar fricative
!  S       ʃ    voiceless postalveolar fricative
!  Z       ʒ    voiced postalveolar fricative
!  C       ç    (cedilla)    voiceless palatal fricative
!  j\ (jj) ʝ    (j with crossed tail)    voiced palatal fricative
!  x       x    voiceless velar fricative
!  G       γ    (Greek gamma)    voiced velar fricative
!          ɰ    voiced velar approximant
!  X\      ħ    (overstroked h)    voiceless pharyngeal fricative
!  ?\      ʕ    (Inverted ?)    voiced pharyngeal fricative
!  h       h    voiceless glottal approximant
!  h\      ɦ    (h with upper tail to the right)    voiced glottal approximant
!  m       m    bilabial nasal
!  F       ɱ    (m with downward right tail)    labiodental nasal
!  n       n    alveolar or dental nasal
!  J       ɲ    (n with downward left tail)    palatal nasal
!  N       ŋ    (n with downward right tail)    velar nasal
!  l       l    alveolar lateral
!  L       ʎ    turned down y, alt. λ (Greek lambda)    palatal lateral
!  5       ɫ    (l with middle tilde)    velarized dental lateral
!  4 (r)   ɾ    (r without upper-left serif)    alveolar flap
!  r (rr)  r    alveolar trill
!  r\      ɹ    (r rotated 180°)    retroflexed alveolar approximant
!  R       ʀ    (small capital R)    uvular trill
!  P       ʋ    labiodental approximant
!  w       w    velo-labial approximant
!  H       ɥ    (turned down h)    palato-labial approximant
!  j       j    palatal approximant
!  
!  Vowels
!                front   near-front    central   near-back   back
!  close          i • y               1 • }                 M • u
!  near-close              I • Y                    U
!  close-mid      e • 2              @\ • 8                 7 • o
!  mid                                  @            
!  open-mid       E • 9               3 • 3\                V • O
!  near-open        {                    6           
!  open           a • &                                     A • Q



! More documentation

!! retroflex plosive, voiceless		t` (` = ASCII 096)	0288, 648
!! retroflex plosive, voiced		d`			0256, 598
!! labiodental nasal				F 			0271, 625
!! retroflex nasal					n` 			0273, 627
!! palatal nasal					J 			0272, 626
!! velar nasal						N 			014B, 331
!! uvular nasal						N\			0274, 628
!! 
!! bilabial trill					B\ 			0299, 665
!! uvular trill						R\ 			0280, 640
!! alveolar tap						4			027E, 638
!! retroflex flap					r` 			027D, 637
!! bilabial fricative, voiceless	p\ 			0278, 632
!! bilabial fricative, voiced		B 			03B2, 946
!! dental fricative, voiceless		T 			03B8, 952
!! dental fricative, voiced			D 			00F0, 240
!! postalveolar fricative, voiceless	S			0283, 643
!! postalveolar fricative, voiced		Z 			0292, 658
!! retroflex fricative, voiceless		s` 			0282, 642
!! retroflex fricative, voiced			z` 			0290, 656
!! palatal fricative, voiceless			C 			00E7, 231
!! palatal fricative, voiced			j\ 			029D, 669
!! velar fricative, voiced	        	G 			0263, 611
!! uvular fricative, voiceless			X			03C7, 967
!! uvular fricative, voiced				R 			0281, 641
!! pharyngeal fricative, voiceless		X\ 			0127, 295
!! pharyngeal fricative, voiced			?\ 			0295, 661
!! glottal fricative, voiced			h\			0266, 614
!! 
!! alveolar lateral fricative, vl.		K 
!! alveolar lateral fricative, vd.		K\
!! 
!! labiodental approximant				P (or v\) 
!! alveolar approximant					r\ 
!! retroflex approximant				r\` 
!! velar approximant					M\
!! 
!! retroflex lateral approximant		l` 
!! palatal lateral approximant			L 
!! velar lateral approximant			L\
!! Clicks
!! 
!! bilabial								O\	(O = capital letter) 
!! dental								|\
!! (post)alveolar						!\ 
!! palatoalveolar						=\ 
!! alveolar lateral						|\|\
!! Ejectives, implosives
!! 
!! ejective								_>	e.g. ejective p		p_>
!! implosive							_<	e.g. implosive b	b_<
!! Vowels
!! 
!! close back unrounded					M
!! close central unrounded 				1 
!! close central rounded				} 
!! lax i								I 
!! lax y								Y 
!! lax u								U
!! 
!! close-mid front rounded				2 
!! close-mid central unrounded			@\ 
!! close-mid central rounded			8 
!! close-mid back unrounded				7
!! 
!! schwa								@
!! 
!! open-mid front unrounded				E 
!! open-mid front rounded				9
!! open-mid central unrounded			3 
!! open-mid central rounded				3\ 
!! open-mid back unrounded				V 
!! open-mid back rounded				O
!! 
!! ash (ae digraph)						{ 
!! open schwa (turned a)				6
!! 
!! open front rounded					& 
!! open back unrounded	        		A 
!! open back rounded					Q
!! Other symbols
!! 
!! voiceless labial-velar fricative		W 
!! voiced labial-palatal approx.		H 
!! voiceless epiglottal fricative		H\ 
!! voiced epiglottal fricative			<\ 
!! epiglottal plosive					>\
!! 
!! alveolo-palatal fricative, vl. 		s\ 
!! alveolo-palatal fricative, voiced	z\ 
!! alveolar lateral flap				l\ 
!! simultaneous S and x					x\ 
!! tie bar								_
!! Suprasegmentals
!! 
!! primary stress						" 
!! secondary stress						% 
!! long									: 
!! half-long							:\ 
!! extra-short							_X 
!! linking mark							-\
!! Tones and word accents
!! 
!! level extra high						_T 
!! level high							_H
!! level mid							_M 
!! level low							_L 
!! level extra low						_B
!! downstep								! 
!! upstep								^	(caret, circumflex)
!! 
!! contour, rising						 
!! contour, falling						_F 
!! contour, high rising					_H_T 
!! contour, low rising					_B_L 
!! 
!! contour, rising-falling				_R_F 
!! (NB Instead of being written as diacritics with _, all prosodic marks can alternatively be placed in a separate tier, set off by < >, as recommended for the next two symbols.)
!! global rise				<R> 
!! global fall				<F>
!! Diacritics
!! 
!! voiceless				_0	(0 = figure), e.g. n_0
!! voiced					_v 
!! aspirated				_h 
!! more rounded				_O	(O = letter) 
!! less rounded				_c 
!! advanced					_+
!! retracted				_-
!! centralized				_" 
!! syllabic					=	(or _=) e.g. n= (or n_=) 
!! non-syllabic				_^ 
!! rhoticity				`
!! 
!! breathy voiced			_t 
!! creaky voiced			_k
!! linguolabial				_N 
!! labialized				_w 
!! palatalized				'	(or _j) e.g. t' (or t_j) 
!! velarized				_G 
!! pharyngealized			_?\
!! 
!! dental					_d 
!! apical					_a 
!! laminal					_m
!! nasalized				~	(or _~) e.g. A~ (or A_~) 
!! nasal release			_n
!! lateral release			_l 
!! no audible release		_}
!! 
!! velarized or pharyngealized		_e 
!! velarized l, alternatively		5 
!! raised							_r 
!! lowered							_o 
!! advanced tongue root				_A 
!! retracted tongue root			_q



