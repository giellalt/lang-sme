!Distributed under the terms of the GNU General Public License version 2 
! or any later version.

! ===================================== !
! Phonological converter for North Sámi !
! ===================================== !

! To eventual curious readers
! This file tries out conversion from Sámi orthography to a phonetic
! translation based upon the ordinary Finnish alphabet. This is due to our
! web demo, http://www.bitlips.fi/tts/demo.cgi

! Usage: Compile sme as usual:
! make TARGET=sme
! Pipe text through the analyser:
! cat text | preprocess | lookup -flags mbTT bin/phon-sme.fst | ..

!We may also add POS tagging:
! cat corp/pedkorpus.txt | preprocess | lookup -flags mbTT -utf8 bin/pos-sme.fst | tr '\t' '+' | cut -d"+" -f1,3 | uniq | lookup -flags mbTT -utf8 bin/phon-sme.fst | cut -f2 | l

! In order to make this better we should uniq the closed POS before giving them
! to the phonological analyser (TODO: Check whether output is ok).

echo <<defining Cns and Vow...>>

define POSTAG %+N -> %+N,
              %+A -> %+A, 
              %+V -> %+V,
              %+Adv -> %+X, 
              %+Pron -> %+X,
              %+CS -> %+X,
              %+CC -> %+X,
              %+Adp -> %+X,
              %+Po -> %+X,
              %+Pr -> %+X,
              %+Interj -> %+X,
              %+Pcle -> %+X,
              %+Num -> %+X, 
              %+CLB -> %+CLB ;

define Vow       [ a | á | e | i | o | u | y | æ | ø | å | ä | ö | 
                   A | Á | E | I | O | U | Y | Æ | Ø | Å | Ä | Ö | 
                   é | ó | ú | í | à | è | ò | ù | ì | ë | ü | ï | 
                   â | ê | ô | û | î | ã | ý |
                   É | Ó | Ú | Í | À | È | Ò | Ù | Ì | Ë | Ü | Ï | 
                   Â | Ê | Ô | Û | Î | Ã | Ý ] ;

define Cns [ b | c | č | d | đ | f | g | h | j | k | l | m | n | ŋ | 
             B | C | Č | D | Đ | F | G | H | J | K | L | M | N | Ŋ | 
             p | q | r | s | š | t | ŧ | v | w | x | z | ž |   
             P | Q | R | S | Š | T | Ŧ | V | W | X | Z | Ž ] ; 

define Stops [ c ( c ) | č ( č ) | t ( t ) | k ( k ) ] ;

define Alv [ t | T | d | D | ŧ | Ŧ | đ | Đ | s | S | l | L | r | R | n | N ] ;

define Nas [ m | M | n | N | ŋ | Ŋ ] ;

define down [ A -> a, Á -> á, B -> b, C -> c, Č -> č, D -> d, E -> e, F -> f, 
              G -> g, H -> h, I -> i, J -> j, K -> k, L -> l, M -> m, N -> n, 
              Ŋ -> ŋ, O -> o, P -> p, Q -> q, R -> r, S -> s, Š -> š, T -> t, 
              U -> u, V -> v, W -> w, X -> x, Y -> y, Z -> z, Ž -> ž, Æ -> æ, 
              Ø -> ø, Å -> å, Ö -> ö, Ä -> ä  ] ;


echo << Rules>>


echo <<defining Cns and Vow...>>


define Syll Cns* Vow+ Cns* ;


echo << Rules >>


! Here comes the other rules


define ijRule  i -> j || Vow _ ;

define slRule s -> š || [.#.|%#|%-] _ l ;


define Schwa  m s -> m ə s ,   
              m š -> m ə š , 
   
              n v -> n ə v ,   
   
              v d -> v ə d ,   
              v g -> v ə g ,   
              v j -> v ə j ,   
              v k -> v ə k ,   
              v l -> v ə l ,   
              v p -> v ə p ,   
              v r -> v ə r ,   
              v s -> v ə s ,   
              v t -> v ə t ,   
              v č -> v ə d š , 
              v š -> v ə š , 
              v z -> v ə d s , 
              v ž -> v ə d š , 
   
              j b -> j ə b ,   
              j c -> j ə c ,   
              j d -> j ə d ,   
              j f -> j ə f ,   
              j g -> j ə g ,   
              j k -> j ə k ,   
              j p -> j ə p ,   
              j s -> j ə s ,   
              j t -> j ə t ,   
              j v -> j ə v ,   
              j z -> j ə d s , 
              j ž -> j ə d j , 
              j l -> j ə l ,
              l r -> j ə r ,
   
              l b -> l ə b ,   
              l f -> l ə f ,   
              l g -> l ə g ,   
              l j -> l ə j ,   
              l p -> l ə p ,   
              l s -> l ə s ,   
              l v -> l ə v ,   
              l š -> l ə š , 
              l z -> l ə d s , 
              l ž -> l ə d š , 
   
              r b -> r ə b ,
              r d -> r ə d ,
              r f -> r ə f ,
              r g -> r ə g ,
              r j -> r ə j ,
              r p -> r ə p ,
              r s -> r ə s ,
              r v -> r ə v ,
              r š -> r ə š ,
              r z -> r ə d s ,
              r ž -> r ə d š ,
   
              š k -> š ə k , 
              š t -> š ə t , 
              š m -> š ə m , 
              š v -> š ə v , 
   
              đ b -> đ ə b , 
              đ g -> đ ə g , 
              đ j -> đ ə j , 
              đ v -> đ ə v , 
   
              j h l -> j ə h l  , 
              j h m -> j ə h m  , 
              j h n -> j ə h n  , 
              v h l -> v ə h l  , 
   
              đ b -> đ ə b  
              
!              d n   -> d ə n , 
!              b m   -> b ə m , 
!              g ŋ   -> g ə ŋ 
                              || Cns* Vow+ _ Vow ;


define FinalLightT t -> h || Syll Vow _ .#. ;
define FinalLightD d -> h || Vow  j   _ .#. ;

define LongAffricateDevoicing  z z -> c c , ž ž -> č č || Cns _ Vow ;
define ShortAffricateDevoicing z   -> c   , ž   -> č   || Cns _ Vow ;

define kcRule  k c -> k h t s || Vow _ Vow ;
define ktjRule k č -> k h t š || Vow _ Vow ;
define tkRule  t k -> t h k   || Vow _ Vow ;
define tmRule  t m -> t h m   || Vow _ Vow ;

define 3rdSylld d -> đ || .#. Syll Vow _ Vow ;

define PlosiveDevoicing d -> t , g -> k , b -> p ||     _ Vow ,,
                        d -> t , g -> k , b -> p || Vow _ Nas ;

define rSchwa r -> r ə || Vow _ [ Cns - Alv | b m | d n | d n j | g ŋ ] Vow ;
define dSchwa đ -> đ ə || Vow _ [ Cns - Alv | b m | d n | d n j | g ŋ ] Vow ; !??
define lSchwa l -> l ə || Vow _ [ Cns - Alv | b m | d n | d n j | g ŋ ] Vow ;

define Preaspiration c -> h || h _ c ,, 
                     č -> h || h _ č ,,
                     p -> h || h _ p ,,
                     t -> h || h _ t ,,
                     k -> h || h _ k ;


define dtjRule ž ž -> d t š ; 
define ttjRule č č -> t t š ;
define zzRule  z z -> d t s ;
define ccRule  c c -> t t s ;
define cRule   c   -> t s ;
define tjRule  č   -> t š ;
define zRule   z   -> d s ;
define djRule  ž   -> d š ;

define LongPalatalAffricate  d d j -> d t j || Vow _ Vow ;
define ShortPalatalAffricate   d j ->   t j || Vow _ Vow ;

define VowelLengthening e -> e e , i -> i i , o -> o o , u -> u u || .#. (Cns*) _ (h) Cns Vow [Cns | .#. ] ;


echo << Dialectal rules >>

define dhRule đ -> r ;
define thRule ŧ -> s ;
define shRule š -> S ;

define eaRule a -> ä || e _ ;

define aaRule á -> a a ;

define nngRule ŋ ŋ -> n n g ;
define ngRule  ŋ   -> n g ;

define PronounLenghtening u -> u u || .#. [m|t|s] _ .#. ;

define SchwaNormalisation ə -> e ; ! Awaiting a good solution


echo <<Combining...>>

read regex [ 
    POSTAG .o. 
    down 

.o. ijRule

.o. slRule

.o. FinalLightT
.o. FinalLightD
.o. Schwa
.o. kcRule  
.o. ktjRule 
.o. tkRule  
.o. tmRule  

.o. 3rdSylld
.o. PlosiveDevoicing
.o. rSchwa
.o. dSchwa
.o. lSchwa

.o. Preaspiration
.o. dtjRule
.o. ttjRule
.o. zzRule 
.o. ccRule 
.o. cRule  
.o. tjRule 
.o. zRule  
.o. djRule 
.o. VowelLengthening

.o. dhRule 
.o. thRule 
.o. shRule 

.o. eaRule
.o. aaRule
.o. nngRule
.o. ngRule
.o. PronounLenghtening
.o. SchwaNormalisation

] ;


invert net ;
