! ========================================================================== !
!                     North Sámi morphological analyser                      !
! ========================================================================== !
!
! sme-lex.txt
! The file contains morphological affixes for the nouns, verbs and adjectives
!
Multichar_Symbols
 +N +A +Adv +V
 +Pron +CS +CC +Adp +Po +Pr +Interj +Pcle +Num +Prop
 +Pers +Dem +Interr +Refl +Recipr +Rel +Indef
 +Ess +Sg +Pl +Nom +Gen +Acc +Ill +Loc +Com
 +isuf +asuf +Cmpnd
 +PxSg1 +PxSg2 +PxSg3 +PxDu1 +PxDu2 +PxDu3 +PxPl1 +PxPl2 +PxPl3
 +Comp +Superl
 +Clt +Attr +Card
 +Pass +Ind +Prs +Prt +Pot +Cond +Imprt
 +Sg1 +Sg2 +Sg3 +Du1 +Du2 +Du3 +Pl1 +Pl2 +Pl3
 +Inf +Act +Ger +ConNeg +Neg +ImprtII +PrsPrc +PrfPrc +Sup +VGen +VAbess
 +Actio +Actor +ABBR +CLB +PUNCT
 X1 X2 X3 X4 X5 X6 X7 X8 X9 Y1 Y2 Y3 Y4 Y5 Y6 Y7 Y8 Y9 
 Q1 Q2 Q3 Q4 Q5 Q6 Q7 Q8 Q9 W1 W2
 b3 c1 e1 d1 d3 g2 g3 h2 h3 i1 j3 m2 m3 n1 n2 n3 o1 o3 p3 
 s1 s3 t1 t3 u1 z1 z3 '1
 B3 C1 E1 D1 D3 G2 G3 H2 H3 I1 J3 M2 M3 N1 N2 N3 O1 O3 P3
 S1 S3 T1 T3 U1 Z1 Z3
 +adda +alla +d +eaddji +eamos1 +eapmi +g +geahtes +h
 +asti +heapmi +hudda +huhtti +huvva +j +l +las1 +ahtti +s1
 +meahttun +mus1 +st +stuvva +us +vuohta +goahti  +lágan
 +Dimin
 @P.NeedNoun.ON@  @D.NeedNoun.ON@  @C.NeedNoun@ ! (Dis)allowing compounds

! Testing instruction:
! For testing purposes, the string !^C^ is added to every line that causes 
! circularity. In order to use the "check-all" option of lexc (that lists all 
! generated forms), the file  should be trimmed by the command "grep -v '\^C\^'
! > noncirc-sme-lex.txt". The resulting file may then be compiled in lexc with 
! the relevant part(s) of the lexicon.

! Basic lexica, pointing to the other lexicon files
! =================================================


LEXICON Root 
 +Cmpnd+:%- NounRoot ;
 NounRoot ;		!^C^
 ProperNoun ;		!^C^
 AdjectiveRoot ;	!^C^
 VerbRoot ;		!^C^	
 Copula ;		!^C^
 Negativeverb ;		!^C^
 Adverb ;		!^C^
 Particles ;		!^C^
 Subjunction ;		!^C^
 Conjunction ;		!^C^
 Adposition ;		!^C^
 Interjection ;		!^C^
 Pronoun ;		!^C^
 Numeral ;		!^C^
 Acronym ;		!^C^
 Abbreviation ;		!^C^
 Punctuation ;		!^C^
 ! All POS have ^C^ tag, remove
 ! one or more for testing.


LEXICON R !^C^
 # NounRoot ;                      ! This is the normal case.		    ^C^
 #@P.NeedNoun.ON@  VerbRoot ;      ! Flag diacr declares that the compound  ^C^
 #@P.NeedNoun.ON@  AdjectiveRoot ; ! needs a +N tag later in the derivation ^C^
 #:%- NounRoot ;                        ! These 3 cases add a hyphen, so    ^C^
 #:%-@P.NeedNoun.ON@  VerbRoot ;        ! Sem-julggas1tus and the like	    ^C^
 #:%-@P.NeedNoun.ON@  AdjectiveRoot ;   ! is allowed.			    ^C^
 # ProperNoun ;     ! Now, let us allow proper nouns as the 2nd part of com-^C^
 #:%- ProperNoun ;  ! pounds. Thus: London-Hull, LondonHull, Hull-viessu    ^C^
                    ! and viessuLondon are now all ok. Some refinement could^C^
                    ! perhaps be done, but corpus research is needed.	    ^C^
 +Cmpnd:%- # ;     ! This to take care of Oahpo- ja dutkandept.		    ^C^


! It seems the P flag in the AdjectiveRoot entry is irrelevant, since there
! are no N + [A-N] compounds. The pointers to AdjectiveRoot are temorarily 
! (030129) left for inspection, they (line 3 and 6 of R) should be removed
! if no deadjectival nouns turn up.

! Some miscellanious lexica
! =========================

LEXICON AGNoun
 juolgi:juol'gi AGAdj "foot N" ;
 c1albmi AGAdj "eye N" ;
 njunni AGAdj ;
 gieh0ta:gieh'ta AGAdj ;
 c1ielgi:c1iel'gi AGAdj "back N" ;
 suorbma AGAdj "finger N" ;

LEXICON AGADJECT !Denominal Adj's with Deriv. -ag/og		!^C^
 agat:agag BEAKKAN "aged A" ;					!^C^




! Lexica for nominal declension classes
! =====================================

! Lexica for nouns
! ----------------

! Bisyllabic nouns
! - - - - - - - - 

LEXICON GOAHTI !Inanim. Bisyll. V-Nouns
 +N: DEVNVCASE ;
 +N: DenominalAdjsV ;					!^C^
 +N: DenominalNounsV ;					!^C^

LEXICON VIVVA !Animate Bisyll. V-Nouns
 +N+Sg+Gen:X4 R ;	!^C^
 GOAHTI ;

LEXICON STAHTA !Bisyll. Non-Gradating a-Nouns; a/i-Illative
 +N: STAHTACASE ;
 +N:X3 GOAHTAI ;
 +N: DenominalAdjsV ;					!^C^
 +N: DenominalNounsV ;					!^C^

LEXICON IIJA !Bisyll. Non-Gradating a-Nouns; a-Illative
 +N: STAHTACASE ;
 +N: GOAHTAI ;
 +N: DenominalAdjsV ;					!^C^
 +N: DenominalNounsV ;					!^C^

 LEXICON PIERA !Bisyll. a-Proper Names without Gradation; a/i-Illative
 +N: GOAHTAI ;
 +N: STAHTACASE ;
 +N+s1+Dimin:z1 DIMINC ;


! Trisyllabic nouns
! - - - - - - - - -

LEXICON MATTAR !Tris. Anim. Gradating C-Nouns
 +N+Sg+Gen:X2 R ;	!^C^
 MALIS ;

LEXICON MALIS !Tris. Inanim. Gradating C-Nouns
 +N:X4 MALIS0 ;
 +N:X8 MALES ;		!^C^
 +N:X2 MALLAS- ;
 +N:X4 DenominalAdjsC ;					!^C^
 +N:X2 DenominalNounsC ;				!^C^
 +N:X2 GEAHTES ;

LEXICON BEANA !Trisyll. Anim. Gradating 0-Nouns
 +N+Sg+Gen:X2 R ;	!^C^
 SEAMU ;

LEXICON SEAMU !Trisyll. Inanim. Gradating 0-Nouns
 +N:X4 BOAZU- ;
 +N:X8 MALES ;		!^C^
 +N:X2 MALLAS- ;
 +N:X2 DenominalNounsC ;				!^C^
 +N: DenominalAdjsV ;					!^C^

LEXICON GAHPIR !Trisyll. Non-Gradating C-Nouns
 +N: MALIS0 ;
 +N:X7 MALES ;		!^C^ ! This gives gahper and gahpir##
 +N: MALLAS- ;
 +N: DenominalNounsC ;					!^C^
 +N: DenominalAdjsC ;					!^C^
 +N:X2 GEAHTES ;

! What about Trisyll. Non-Gradating 0-Nouns ??? Are there some?


LEXICON NIILLAS !Trisyll. Non-Gradating C-Proper Names
 +N: MALIS0 ;
 +N: MALLAS- ;
 +N+s1+Dimin:az1z1 DIMINV ;



! Contracted nouns
! - - - - - - - - 

LEXICON BOAZU !Anim. Contracted 0-Nouns
! +N:X4 BOAZU- ;
! +N:X8 MALES ;		!^C^
! +N:Y5 BOHCCO ;
! +N:Y6 BOHCCU ;
! +N:Y5 DenominalNounsV ;
! +N: DenominalAdjsV ;
 +N+Sg+Gen:Y5 R ;	!^C^
 SUOLU ;

LEXICON SUOLU !Inanim. Contracted 0-Nouns
 +N:X4 BOAZU- ;
 +N:X8 MALES ;		!^C^
 +N:Y5 BOHCCO ;
 +N:Y6 BOHCCU ;
 +N:Y5 DenominalNounsV ;			!^C^
 +N: DenominalAdjsV ;				!^C^
! +N+Sg+Nom:X7 R ;	!^C^
! +N+Sg+Gen:Y5 R ;	!^C^

LEXICON SATTU
 +N:Y5 SADDO ;
 +N:Y6 SADDU ;
 +N:X4 SATTU- ;

LEXICON GISTTA !The Noun gistta, gist -
 +N:s'taX4 BOAZU- ;
 +N:stáY5 FALLA- ;
 +N:stáY5 DenominalNounsV ;
 +N:stá DenominalAdjsV ;

LEXICON FALIS !Contracted Anim. C-Nouns
 +N:X4 MALIS0 ;
 +N:X8 MALES ;		!^C^
 +N:Y5 FALLA- ;
 +N:Y5 DenominalNounsV ;			!^C^
 +N:Y5 DenominalAdjsV ;				!^C^

LEXICON LASIS !Contracted Inanim. C-Nouns
!Tentatively copying entries from FALIS
!This must be checked! 020207 TT.
 +N:X4 MALIS0 ;
 +N:X8 MALES ;		!^C^
 +N:Y5 FALLA- ;
 +N:Y5 DenominalNounsV ;			!^C^
 +N:Y5 DenominalAdjsV ;				!^C^

LEXICON GARGIA
! light fin syll
! gen = nom ill = -i
 +N+Sg+Nom: K ;
 +N+Sg+Nom:X7 R ;	!^C^
 +N+Sg+Gen: K ;
 +N+Sg+Gen:X7 R ;	!^C^
 +N+Sg+Acc: K ;
 +N+Sg+Ill:j K ;
 +N+Sg+Loc:s K ;
 +N+Sg+Com:jn K ;
 +N+Ess:n K ;
 +N+Pl+Nom:t K ;
 +N+Pl+Gen:jd K ;
 +N+Pl+Gen:jd R ;	!^C^
 +N+Pl+Acc:jd K ;
 +N+Pl+Ill:jdda K ;
 +N+Pl+Loc:jn K ;
 +N+Pl+Com:jgujn K ;
 +N+Sg+Nom: PxA ;
 +N+Sg+Gen: PxA ;
 +N+Sg+Acc: PxA ;
 +N+Sg+Ill:sa PxA ;
 +N+Sg+Loc:st PxC ;
 +N+Sg+Com:jn PxC ;
 +N+Ess:n PxC ;
! +N+Pl+Nom:t PxC ; ! No such form.
 +N+Pl+Gen:jd PxC ;
 +N+Pl+Acc:jd PxC ;
 +N+Pl+Ill:jddás PxC ;
 +N+Pl+Loc:jn PxC ;
 +N+Pl+Com:jd PxPlComC ;


LEXICON BUFFALO
! heavy fin syll
! gen = -a ill = -ai
! parallel to NYSTØ
 +N+Sg+Nom: K ;
 +N+Sg+Nom:X7 R ;	!^C^
 +N+Sg+Gen:a K ;
 +N+Sg+Gen:aX7 R ;	!^C^
 +N+Sg+Acc:a K ;
 +N+Sg+Ill:aj K ;
 +N+Sg+Loc:as K ;
 +N+Sg+Com:ajn K ;
 +N+Ess:an K ;
 +N+Pl+Nom:at K ;
 +N+Pl+Gen:ajd K ;
 +N+Pl+Gen:ajd R ;	!^C^
 +N+Pl+Acc:ajd K ;
 +N+Pl+Ill:ajdda K ;
 +N+Pl+Loc:ajn K ;
 +N+Pl+Com:ajgujn K ;
 +N+Sg+Nom: PxC ;
 +N+Sg+Gen: PxC ;
 +N+Sg+Acc: PxC ;
 +N+Sg+Ill:asa PxA ;
 +N+Sg+Loc:ast PxC ;
 +N+Sg+Com:ajn PxC ;
 +N+Ess:an PxC ;
 +N+Pl+Gen:ajd PxC ;
 +N+Pl+Acc:ajd PxC ;
 +N+Pl+Ill:ajddás PxC ;
 +N+Pl+Loc:ajn PxC ;
 +N+Pl+Com:ajd PxPlComC ;

 
! Miscellanious noun types
! - - - - - - - - - - - - 

LEXICON GADDI !Bisyll. V-Nouns with Comp. Forms
 +N: DEVNVCASE ;
 +N:X4 BU/MUS ;
 +N: DenominalNounsV ;				!^C^
 +N: DenominalAdjsV ;				!^C^

LEXICON OLLUVUOHTA !Exceptional vuohta-Nouns
 +N: VUOHTAMORPH ;

LEXICON MAGASH
 +N: MALIS0 ;
 +N: MALLAS- ;
! This lexicon is not correct. It is copied from DIMINC, 
! (except that the tags +N are added),  but in the 
! noun lexicon DIMINC is marked "pl.r.V)" and MAGASH is
! marked !(pl.r.). XXX Look into this.

LEXICON EGEZHAGAT
 +N+Pl+Nom:aX4t K ;
 +N+Ess:an K ;
 +N+Ess:an PxC ;
 +N:aX5 EGEZHAHKII ;

LEXICON EGEZHAHKII
 +Pl+Gen:jd K ;
 +Pl+Gen:jd R ;	!^C^
 +Pl+Gen:jd PxC ;
 +Pl+Acc:jd K ;
 +Pl+Acc:jd PxC ;
 +Pl+Ill:jde K ;
 +Pl+Ill:jdasa PxV ;
 +Pl+Loc:jn K ;
 +Pl+Loc:jn PxC ;
 +Pl+Com:jgujn K ;
 +Pl+Com:jd PxPlComC ;

! Sublexica for adjectival stem types
! -----------------------------------

! Bisyllabic adjectives 
! - - - - - - - - - - -

LEXICON ALKI ! Bisyll V-Adj, -es-Attr, no WeG.
 +A+Attr:W2s ATTR ;
 +A: LAIKI0 ;
 +A: BU/MUS ; ! no CG in comparative?

LEXICON SEARRA !Bisyll. V-Adj's with s-Attr in WeG.
 +A+Attr:W1s ATTR ;
 +A: LAIKI0 ;
 +A:X4 BU/MUS ;

LEXICON HOHPI !Bisyll. V-Adj's with s-Attr. in WeG & Adv.
 +A+Attr:X4s ATTR ; ! X4 => WeG, not W1 => no vow ch
 +A: LAIKI0 ;
 +A:X4 BU/MUS ;
 +A: ADVV ;

LEXICON LAIKI !Bisyll. V-Adj's with es-Attr. in WeG & Adv.
 +A+Attr:W1s ATTR ;
 +A: LAIKI0 ;
 +A:X4 BU/MUS ;
 +A: ADVV ;

LEXICON LODJI !bisyll V-Adj with -es and -is Attr in WeG
 +A+Attr:X4s ATTR ; ! #lojis
 +A+Attr:W1s ATTR ; ! #lojes
 +A: LAIKI0 ;
 +A:X4 BU/MUS ;
 +A: ADVV ;
! LODJI combines HOHPI and LAIKI
 
!LEXICON VEADDJI ! bisyll, s Attr, no CG
!K ; ne, det er JUHKKIS som er riktig leksikon.

LEXICON JUHKKIS !Bisyll. V-Adj. with s-Attr; no Adv.
 +A+Attr:s ATTR ;
 +A: LAIKI0 ;
 +A:X4 BU/MUS ;
! this lexicon is in conflict with dictionary info


LEXICON NUORRA !Bisyll. V-Adj. without Separate Attr; no Adv.
 +A+Attr: ATTR ;
 +A: LAIKI0 ;
 +A:X4 BU/MUS ;

LEXICON VIELG ! adj with -es -attrib. (cns final adj)
 +A+Attr:Y9es ATTR ;
 +A:ad VIELGAT ;

LEXICON VIELGAT
 EABBO/EAMOS ;
 GAPPUS- ;
 GAPPUS0 ;
! GARRAS- ; ! GAPPUS- instead
 
LEXICON CAHKK
! Double set of attr forms here
 +A+Attr:Y9es ATTR ;
 :ad JOHTIL ;

LEXICON JALGAT
 +A+Attr:a ATTR ;
 VIELG ;

LEXICON UHCC
 +A+Attr:aW1 ATTR ;
 +A:i LAIKI0 ;
 +A:iX4 BU/MUS ;



! Trisyllabic adjectives
! - - - - - - - - - - - 

LEXICON BEAKKAN !Trisyll. Non-gradating C-Adj. without Separate Attr.
 +A+Attr: ATTR ;
 +A: EABBO/EAMOS ;
! +A: GAPPUS- ;
 +A: GAPPUS0 ;
! +A: GARRAS- ; ! id to GAPPUS0
+A+Sg+Gen:a K ;
+A+Sg+Acc:a K ;
+A+Pl+Nom:at K ;

LEXICON JOHTIL !Trisyll. Non-gradating C-Adj. with is-Attr.
 +A+Attr:is ATTR ;
 +A: EABBO/EAMOS ;
 +A: GAPPUS- ;
 +A: GAPPUS0 ;
! +A:X4 GAPPUS0 ; ! No WeG here!
! +A:X2 GAPPUS- ; ! No X2 here, johtila, not johtála
! +A:X2 GARRAS- ; ! GARRAS- removed, pointing to GAPPUS- instead
!  +A:X4 GARAS0 ; ! GARAS0 also removed
! two continuation patterns because there are
! adjectives with both strong av weak form.

LEXICON GUOHCA !Trisyll. Gradating V-Adj., no sep. Attr.
 +A+Attr:X4 K ;
 +A: EABBO/EAMOS ;
 +A:X2 MALLAS- ;
 +A:X4 BOAZU- ;

LEXICON GARAS !Trisyll. Gradating C-Adj. with Bisyll. a-Attr. and final s
 +A+Attr: ATTR ;
 +A:sX2 EABBO/EAMOS ;
 +A:sX2 GAPPUS- ;
 +A:sX4 GAPPUS0 ;
! :sX4 GAPPUS ;
! Something is wrong here !! Both GARAS and GAPPUS point to ATTR.
! Thus, the 3 last lines of GARAS was inserted, and the line pointing
! to GAPPUA was removed, to avoid double pointing to an ATTR form.

LEXICON LOSSAT !Trisyll. Gradating C-Adj. with Bisyll. a-Attr. and final t
 +A+Attr: ATTR ;
 +A:dX2 EABBO/EAMOS ;
 +A:dX2 GAPPUS- ;
 +A:dX4 GAPPUS0 ;

LEXICON CIENAL !Trisyll. Gradating C-Adj. with Strong Grade is-Attr.
 +A+Attr:X2is ATTR ;
 +A:X2 EABBO/EAMOS ;
 +A:X2 GAPPUS- ;
! +A:X2 GARRAS- ; ! GARRAS- removed
 +A:X4 GAPPUS0 ;
! +A:X4 GARAS0 ; ! GARAS0 removed

LEXICON NJUORAS !Trisyll. Gradating C-Adj., with Strong Grade a-Attr.
 +A+Attr: ATTR ;
 :s GAPPUS ;
! "njuoras" was read as njuorra+A+A+Sg+Nom. Thus "+A" was removed from +A:s GAPPUS ;" 

LEXICON DILDDAS ! ,-ld-(#=is) Trisyll Grad., facult is-Attr.
 +A+Attr:X4is ATTR ;
 GAPPUS ;

LEXICON HEAHKAS ! ,-hkk-#=is~heahkka Trisyll Grad., is-Attr
! this gives heahkasis as attributive form. Check.
 +A+Attr:X4sis ATTR ;
 GARAS ;

LEXICON EATTAS ! ,-dd-#=is Trisyll. Grad. C-Adj. with WeG is-Attr.
 +A+Attr:X4is ATTR ;
 +A:X2 EABBO/EAMOS ;
 +A:X2 GAPPUS- ;
 +A:X4 GAPPUS0 ;

LEXICON BOAKKAS ! ,-gg-#boagge3- Trisyll
 +A:X2 EABBO/EAMOS ;
 +A:X2 GAPPUS- ;
 +A:X4 GAPPUS0 ;

LEXICON FARGAT ! :d#Ø Trisyll
 +A: EABBO/EAMOS ;
 +A: GAPPUS- ;
 +A: GAPPUS0 ;

LEXICON GAPPUS ! -bbo- Trisyll
 +A+Attr:X4 ATTR ;
 +A:X2 EABBO/EAMOS ;
 +A:X2 GAPPUS- ;
 +A:X4 GAPPUS0 ;

LEXICON VATTIS ! Trisyll CG, -es Attr
 +A+Attr:W1 ATTR ;
 +A:X2 EABBO/EAMOS ;
 +A:X2 GAPPUS- ;
 +A:X4 GAPPUS0 ;

LEXICON BIEKKUS ! ,-iggo-#=is Trisyll Grad, is-Attr, 
 +A+Attr:X4is ATTR ;
 +A:X2 EABBO/EAMOS ;
 +A:X2 GAPPUS- ;
 +A:X4 GAPPUS0 ;

LEXICON LIEKKUS ! ,-iggo-(#=is)
 +A+Attr:X4 ATTR ;
 BIEKKUS ;

LEXICON GUOROS 
! guoro3s,-u2rro- ~ guorus,-u2rro-#guoro3s
 +A+Attr:W1 ATTR ;
 +A+Ess:W1in K ;
 +A+Sg+Nom:W1 K ;
! NB only the ATTR form works for this lexicon!!

LEXICON NUOLUS ! ,-u2llo-(#nuolo3s)
 +A+Attr:W1 ATTR ;
 GAPPUS ;

LEXICON GEARGGUS ! ,-ergo-#gearggo3s
 +A+Attr:W1 ATTR ;
 +A:X2 EABBO/EAMOS ;
 +A:X2 GAPPUS- ;
 +A:X4 GAPPUS0 ;

LEXICON VUDDJII !
 +A+Adv:it K ;
 +A+Attr:Y5s ATTR ;
 +A+Sg+Nom:00 K ;
 +A:Y5 BU/MUS ;
 +A:Y5 VUDDJE- ;
 +A:Y6 VUDDJI- ;
 VUOHTA ;

LEXICON ISSORAS ! ( )
 +A+Attr: ATTR ;
 +A: EABBO/EAMOS ;
 +A: JOHTOLAT0 ;
 +A: VUOHTA ;
 +A:a ISSORASSA- ;
 :ass DEARVVASLAS ;
! The last line is cryptic.

LEXICON DABALAS ! ( )
 +A: EABBO/EAMOS ;
 :az1z1 DEARVVASLAS ;

LEXICON DEARVVASLAS ! ( ) 
 +A+Attr: ATTR ;
 +A: JOHTOLAT0 ;
 +A: VUOHTA ;
 +A:a ISSORASSA- ;

LEXICON ASEHAS
 +A+Attr:is ATTR ;
 +A: EABBO/EAMOS ;
 +A:is VUOHTA ;
 +A:az1z1 JOHTOLAT0 ;
 +A:az1z1a ISSORASSA- ;


LEXICON RAHKIS ! As BOARIS, but with facultative -is -Attr
 +A+Attr:is ATTR ;
 +A+Attr:es ATTR ;
 +A: EABBO/EAMOS ;
 +A:is VUOHTA ;
 +A:isX2 GAPPUS- ; ! as VATTIS
 +A:isX4 GAPPUS0 ; ! as VATTIS

LEXICON BOARIS ! As GAPPUS, but with different (stem and) attr.
 +A+Attr:es ATTR ;
 +A:ris EABBO/EAMOS ;
 +A:is VUOHTA ;
 +A:risX2 GAPPUS- ; ! as VATTIS
 +A:risX4 GAPPUS0 ; ! as VATTIS
 ! +A:az1z1 JOTHOLAT0 ;
 ! +A:az1z1a ISSORASSA- ; ! Wrong, essive and BU/MUS

LEXICON JEAGOHEAPMI 
 +A+Attr:is ATTR ;
 +A:eapmi HEAPMI ;
 +A: EABBO/EAMOS ;
 +A:is VUOHTA ;

LEXICON AKTIIVA
 +A+Attr: ATTR;
 +A: LAIKI0 ;
 +A:X4 BU/MUS ;
!Modeled on the basis of NUORRA

LEXICON ALLAT
 +A+Attr:a ATTR ;
 +A:iX4 BU/MUS ;
 +A:ad ALLAGA ;
 +A:ag ALLAGA ;

LEXICON ALLAGA
 00:i MALLASI-/NUORABU- ;
 EABBO/EAMOS ;
 GAPPUS- ;
 GAPPUS0 ;
 VUOHTA ;

 

! Contracted adjectives
! - - - - - - - - - - -

LEXICON FIINNIS ! ,-dná-(:Ø)#fiinna
 +A+Attr:aX4 ATTR ;
! +A:X4 GARAS0 ; ! removed
 +A:X4 GAPPUS0 ; ! replaced
 +A:isX2 EABBO/EAMOS_CONT ;
 +A:isX4 GAPPUS0 ;
 +A:isY5 BU/MUS ;
 +A:isY5 FIIDNA- ;

LEXICON SMAVIS ! as fiinnis, but with StrGr in Attr
 +A+Attr:a ATTR ;
! +A:X4 GARAS0 ; ! removed
! +A:X4 GAPPUS0 ; ! replaced
 +A:isX2 EABBO/EAMOS_CONT ;
 +A:isX4 GAPPUS0 ;
 +A:isY5 BU/MUS ;
 +A:isY5 FIIDNA- ;

LEXICON STUORIS ! As fiinnis, but with different comparation
 +A+Attr:aX4 ATTR ;
! +A:X4 GARAS0 ; ! removed
! +A:X4 GAPPUS0 ; ! stuoris different
 +A+COMP:it SHORTCOMP ;
 +A+COMP:át SHORTCOMP ;
 +A:isX4 GAPPUS0 ;
 +A:iX4 BU/MUS ; ! stuorit, stuorimus
 +A:áX4 BU/MUS ; ! stuorát, stuorámus
 +A:isY5 FIIDNA- ;
! Tentative, cf. FIINNIS and check.

LEXICON NJALGGAT
! ,-lgá-:Ø#njálgga ~ njálggis,-lgá-: Ø#njálgga ~ 
! njálgga,-lgá-:Ø#njálgga (komp., superl. njálgá- ~ njálga3s-)
 +A+Attr:X4 ATTR ;
! +A:X4 GARAS0 ; ! removed
 +A:X4 GAPPUS0 ; ! replaced
 +A:hX4 GAPPUS0 ;
 +A:hY5 BU/MUS ;
 +A:hY5 FIIDNA- ;

LEXICON JIEDNAI
 +A+Attr: ATTR ;
! +A:X4 MALIS0 ;
! +A:W1 MALES ;		!^C^
 +A:Y5 FALLA- ;
! let us try without CG
 +A:X4 MALIS0 ;
 +A:W1 MALES ;		!^C^

LEXICON RIKKIS
 +A+Attr:es ATTR ;
! XXX this one must be finished!!!
! rikkis A Nom
! riggá  A Gen
! rikkes A Attr



! Some adjectival lexica
! ----------------------

LEXICON OVDDIT ! Inherently comparative adjectives
 +A+Attr:b K ;
 +A:b      BUStem ;

LEXICON AGAdj
 +g+A:Q8g BEAKKAN ;
 +g+A:Q8g K ;

LEXICON ATTR
 -:#%- ProperNoun ;			!^C^
 :# AGADJECT ;				!^C^
 +lágan:lágan LAGAN ; ! tentative !	!^C^
 K ;

LEXICON LAIKI0
 DEVNVCASE ;
 VUOHTA ;

! Sublexica for nominal stems
! ---------------------------


LEXICON DEVNVCASE
 GOAHTI-NE ;
 GOAHTI-OBL ;
 GOAHTE- ;				!^C^

LEXICON GOAHTI-NE !Bisyll. V-Nouns; Nominative Sg. + Essive
 NomV ;
 EssV ;

LEXICON NomV
 +Sg+Nom: K ;
 +Sg+Nom: Px3V ;
 +Sg+Nom:X7 R ;	!^C^

LEXICON EssV
 +Ess:n K ;
 +Ess:n PxC ;

LEXICON GOAHTI-OBL
 :X2 GOAHTA- ; ! Px sublexicon
 :X3 GOAHTAI ; ! Ill sublexicon
 :X4 GOADI- ; ! weak grade abs decl
 :X5 GODII- ; ! weak gr dipth simpl
 :X6 GOADA- ; ! AG Px
 :X8 GOADE- ; ! Ditto (?)

LEXICON GOAHTE-		!^C^
 :X7 MALES ;		!^C^

LEXICON STAHTACASE
 GOAHTI-NE ;
 GOAHTA- ;
! :X3 GOAHTAI ; ! POINTED TO IN MOTHER LEXICA
 GOADI- ;
 GODII- ;
 GOADA- ;
 GOADE- ;
 MALES ;		!^C^


LEXICON JOHTOLAT
 +geahtes+A:#geahtes K ;
 @C.NeedNoun@ HEANDARAT-COMM ; 
! The flag is there because verbs are derived via +us suffix to
! JOHTOLAT, and then pick up their +N tag in HEANDARAT-COMM, but not
! via +geahtes.
 
LEXICON HEANDARAT-COMM ! this lexicon is differet from HEANDARAT in one
 +N:00 JOHTOLAT0 ;     ! respect: it does not have the +Prop tag.
 +N:a JOHTOLAHKA- ;
 +N+Demin:aX6z1 DIMINC ;
 
LEXICON JOHTOLAHKA-
 +Sg+Nom:00 Px3V ;
 +Ess:n K ;
 +Ess:n PxC ;
 GOAHTI-OBL ;




! Mainly adjectival sublexica
! ---------------------------

LEXICON ISSORASSA-
 +Ess:n K ;
 :X4 BU/MUS ;
 EABBUCASE2 ;

LEXICON LAS
 VUOHTA ;
 MUSH ;

LEXICON LEXMUSH
 +N: MUSH ;

LEXICON MUSH
 JOHTOLAT0 ;
 :a MUSSHA ;

LEXICON MUSSHA
 +Ess:n K ;
 EABBUCASE2 ;

LEXICON BUOREMUS
 JOHTOLAT0 ;
 VUOHTA ;
 :a BUOREMUSSA- ;

LEXICON BUOREMUSSA-
 +Ess:n K ;
 +Adv:X8t K ;
 EABBUCASE2 ;

LEXICON JOHTOLAT0
 +Sg+Nom: K ;
 +Sg+Nom: R ; 		!^C^! test this
! +Sg+Nom:X7 R ; 		!^C^! removed since cons-final.
! +Sg+Gen: K ;
! +Sg+Acc: K ;
! This must be a typo!

LEXICON LEXDIMINC
 +N: DIMINC ;

LEXICON DIMINC
 MALIS0 ;
 MALLAS- ;


LEXICON DIMINV
 JOHTOLAT0 ;
 :a JOHTOLAHKA- ;

LEXICON EABBO/EAMOS
 ADVC ;
 EABBO/EAMOS_CONT ;

LEXICON EABBO/EAMOS_CONT
 +Comp:it SHORTCOMP ;
 +Comp:et SHORTCOMP ;
 +Comp:ut SHORTCOMP ;
 +Comp:eabbu EABBU ;
 +Comp:ab'bu EABBU ;
 +Superl:amos EAMOS ;
 +Superl:eapmo1s EAMOS ;
 +Superl:eapmus EAMOS ;

LEXICON SHORTCOMP
 JOHTOLAT0 ;
 VUOHTA ;

LEXICON EABBU
 +Dimin:X6z1 DIMINC ;
 +Dimin:X6z1 VUOHTA ;
 +Dimin+Adv:X6z1it K ;
 EABBUCASE1 ;
 EABBUCASE2 ;
 +Adv:X8t K ;

LEXICON HEAPMI
 +Dimin:X6z1 DIMINC ;
 EABBUCASE1 ;
 EABBUCASE2 ;
 +Adv:X8t K ;

LEXICON EABBUCASE1
 +Sg+Nom:X1 K ;
 +Sg+Nom:X7 K ;
! +Sg+Nom:X1 Px3V ;
! +Sg+Nom:X7 Px3V ;
 +Sg+Nom: PxV ;
 +Ess:X1n K ;
 +Ess:X7n K ;
 +Ess:X1n PxC ;
 +Ess:X7n PxC ;

LEXICON EABBUCASE2
 +Sg+Gen:X4 K ;
 +Sg+Gen:X8 K ;
! +Sg+Gen:X4 PxV ;
! +Sg+Gen:X8 PxV ;
 +Sg+Gen:X2 Px1V ;
 +Sg+Gen:X6 Px2V ;
 +Sg+Gen:X4 Px3V ;
 +Sg+Acc:X4 K ;
 +Sg+Acc:X8 K ;
 +Sg+Acc:X2 Px1V ;
 +Sg+Acc:X6 Px2V ;
 +Sg+Acc:X4 Px3V ;
! +Sg+Acc:X4 PxV ;
! +Sg+Acc:X8 PxV ;
 +Sg+Ill:X3j K ;
! +Sg+Ill:s PxC ;
 +Sg+Ill:X2s PxC ;
 +Sg+Loc:X4s K ;
 +Sg+Loc:X8s K ;
 +Sg+Loc:X4st PxC ;
 +Sg+Loc:X8st PxC ;
 +Sg+Com:X5jn K ;
 +Sg+Com:X5jn PxC ;
 +Pl+Nom:X4t K ;
 +Pl+Gen:X5jd K ;
 +Pl+Gen:X5jd R ;	!^C^
 +Pl+Gen:X5jd PxC ;
 +Pl+Acc:X5jd K ;
 +Pl+Acc:X5jd PxC ;
 +Pl+Ill:X5jde K ;
 +Pl+Ill:X5jdasa PxV  ;
 +Pl+Loc:X5jn K ;
 +Pl+Loc:X5jn PxC ;
 +Pl+Com:X5jgujn K ;
 +Pl+Com:X5jd PxPlComC ;

LEXICON BU/MUS
 +Comp:b BUStem ;
 +Superl:muss BUOREMUS ;
 +Adv:t K ;

LEXICON BUStem
 +Sg+Nom: K ;
 VUOHTA ;
 +Sg+Gen:u K ;
 +Sg+Gen: K ;
 +Sg+Acc:u K ;
 +Sg+Acc: K ;
 +Pl+Nom:ut K ;
 +Ess:un K ;
 +Dimin:uz1z1 BUOREMUS ;
 :u MALLASI-/NUORABU- ;
 +Adv:ut K ;

LEXICON EAMOS
 :X4 MALIS0 ;
 :X8 MALES ;		!^C^
 :X2 MALLAS- ;
 :X8 VUOHTA ;
 :X2 ADVC ;

LEXICON VUDDJE-
 +Sg+Acc: K ;
 +Pl+Nom:t K ;
 +Attr:s K ;
 +Adv:t K ;

LEXICON VUDDJI-
 +Pl+Acc:jd K ;

LEXICON MALIS0
 +Sg+Nom: K ;
 +Sg+Nom:X7 R ;	!^C^
 +Ess:in K ;
 +Ess:ini PxV ;

LEXICON GAPPUS0
 +Sg+Nom: K ;
 +Sg+Nom:X7 R ;	!^C^
 VUOHTA ;
 +Ess:in K ;

LEXICON GAPPUS-
 MALLAS- ;
! +Sg+Nom:00 PxC ;
! +Sg+Gen:a K ;
! +Sg+Gen: K ;
! +Sg+Gen: PxC ;
! +Sg+Acc:a K ;
! +Sg+Acc: K ;
! +Sg+Acc: PxC ;
! +Pl+Nom:at K ;
! +Sg+Ill:asa PxV ;
! 00:i MALLASI-/NUORABU- ;

! Now that MALLASI- is deleted, 
! GAPPUS- and MALLAS- are
! identical. We check by pointing 
! GAPPUS- to MALLAS-. Look into this.
! and remove GAPPUS- for MALLAS-
! eventually.

LEXICON MALLAS-
 +Sg+Nom:00 PxC ;
 +Sg+Gen:a K ;
 +Sg+Gen: K ;
 +Sg+Gen: PxC ;
 +Sg+Acc:a K ;
 +Sg+Acc: K ;
 +Sg+Acc: PxC ;
 +Pl+Nom:at K ;
 +Sg+Ill:asa PxV ;
 :i MALLASI-/NUORABU- ;
! :i MALLASI- ;

LEXICON MALLASI-/NUORABU-
 +Sg+Ill:j K ;
 +Sg+Loc:s K ;
 +Sg+Com:jn K ;
 +Pl+Gen:jd K ;
 +Pl+Gen:jd R ;	!^C^
 +Pl+Acc:jd K ;
 +Pl+Ill:jdda K ;
 +Pl+Loc:jn K ;
 +Pl+Com:jgujn K ;
 +Sg+Loc:stti PxV ;
 +Sg+Com:jnni PxV ;
 +Pl+Gen:jddi PxV ;
 +Pl+Acc:jddi PxV ;
 +Pl+Ill:jddás PxC ;
 +Pl+Loc:jnni PxV ;
 +Pl+Com:jddi PxPlComV1 ;

LEXICON MEAHTTUN
 +Sg+Nom:00 K ;
 +Sg+Nom:X7 R ;	!^C^
 +Ess:in K ;
 +Sg+Gen:a K ;
 +Sg+Gen: K ;
 +Sg+Acc:a K ;
 +Sg+Acc: K ;
 +Pl+Nom:at K ;
 :i MALLASI-/NUORABU- ;
 VUOHTA ;

LEXICON EAMOSH
 +Sg+Nom:X4 K ;
 +Sg+Nom:X7 R ;	!^C^
 +Ess:X4in K ;
 +Sg+Gen:X2a K ;
 +Sg+Gen:X2 K ;
 +Sg+Acc:X2a K ;
 +Sg+Acc:X2 K ;
 +Pl+Nom:X2at K ;
 00:X2i MALLASI-/NUORABU- ;

LEXICON BOAHTIN
 +Sg+Nom: K ;
 +Sg+Nom:X7 R ;	!^C^
 +Ess:in K ;
 +Sg+Gen:a K ;
 +Sg+Gen: K ;
 +Sg+Acc:a K ;
 +Sg+Acc: K ;
 +Pl+Nom:at K ;
 00:i MALLASI-/NUORABU- ;

!LEXICON MALLASI-
! +Sg+Loc:stti PxV ;
! +Sg+Com:jnni PxV ;
! +Pl+Gen:jddi PxV ;
! +Pl+Acc:jddi PxV ;
! +Pl+Ill:jddás PxC ;
! +Pl+Loc:jnni PxV ;
! +Pl+Com:jddi PxPlComV1 ;
! Included in MALLASI-/NUORABU-
! KEEP IN MIND

LEXICON FALLA-
 BOHCCO ;
 BOHCCU ;

LEXICON BOAZU-
 +Sg+Nom: K ;
 +Sg+Nom:X7 R ;	!^C^
 +Ess:n K ;
 +Ess:n PxC ;

LEXICON BOHCCO
 +Sg+Nom: PxV2 ;
 +Sg+Gen: K ;
 +Sg+Gen: PxV2 ;
 +Sg+Gen: R ; 		!^C^! Already in LEXICON BOAZU
 +Sg+Acc: K ;
 +Sg+Acc: PxV2 ;
 +Sg+Ill:s PxC ;
 +Sg+Loc:s K ;
 +Sg+Loc:st PxC ;
 +Pl+Nom:t K ;

LEXICON BOHCCU
 +Sg+Ill:j K ;
 +Sg+Com:jn K ;
 +Sg+Com:jn PxC ;
 +Pl+Gen:jd K ;
 +Pl+Gen:jd R ;	!^C^
 +Pl+Gen:jd PxC ;
 +Pl+Acc:jd K ;
 +Pl+Acc:jd PxC ;
 +Pl+Ill:jde K ;
 +Pl+Ill:jdasa PxV ;
 +Pl+Loc:jn K ;
 +Pl+Loc:jn PxC ;
 +Pl+Com:jgujn K ;
 +Pl+Com:jd PxPlComC ;

LEXICON SADDO
 +Pl+Nom:t K ;

LEXICON SADDU
 +Pl+Gen:jd K ;
 +Pl+Gen:jd R ;	!^C^
 +Pl+Gen:jd PxC ;
 +Pl+Acc:jd K ;
 +Pl+Acc:jd PxC ;
 +Pl+Ill:jde K ;
 +Pl+Ill:jdasa PxV ;
 +Pl+Loc:jn K ;
 +Pl+Loc:jn PxC ;
 +Pl+Com:jgujn K ;
 +Pl+Com:jd PxPlComC ;

LEXICON SATTU-
 +Sg+Nom:X7 R ;	!^C^
 +Ess:n K ;
 +Ess:n PxC ;

LEXICON FIIDNA-
 +Sg+Gen: K ;
 +Sg+Acc: K ;
 +Sg+Loc:s K ;
 +Pl+Nom:t K ;
 +Sg+Ill:j K ;
 +Sg+Com:jn K ;
 +Pl+Gen:jd K ;
 +Pl+Gen:jd R ;	!^C^
 +Pl+Acc:jd K ;
 +Pl+Ill:jde K ;
 +Pl+Loc:jn K ;
 +Pl+Com:jgujn K ;

LEXICON MALES			!^C^
! +Sg+Nom:# K " R" ;		!^C^
 +Sg+Nom: R ;			!^C^
!typo on the previous one?

LEXICON GEAHTES
 +geahtes+A:geahtes K ;

LEXICON GOADE-
 +Sg+Gen: K ;   ! This escape hatch for X8 gives gieldda as Gen of gieldá.
 +Sg+Gen: R ;	!^C^

LEXICON GOAHTA-
 +Sg+Nom: Px1V ;
 +Sg+Gen: Px1V ;
 +Sg+Acc: Px1V ;
 +Sg+Nom: Px2V ;
 +Sg+Ill:s PxC ;

LEXICON GOAHTAI
 +Sg+Ill:j K ;

LEXICON GOADI-
 +Sg+Gen: K ;
 +Sg+Acc: K ;
 +Sg+Gen: R ;	!^C^
 +Sg+Gen: Px3V ;
 +Sg+Acc: Px3V ;
 +Sg+Loc:s K ;
 +Sg+Loc:st PxC ;
 +Pl+Nom:t K ;

LEXICON GODII-
 +Sg+Com:jn K ;
 +Sg+Com:jn PxC ;
 +Pl+Gen:jd K ;
 +Pl+Gen:jd R ;	!^C^
 +Pl+Gen:jd PxC ;
 +Pl+Acc:jd K ;
 +Pl+Acc:jd PxC ;
 +Pl+Ill:jde K ;
 +Pl+Ill:jdasa PxV ;
 +Pl+Loc:jn K ;
 +Pl+Loc:jn PxC ;
 +Pl+Com:jgujn K ;
 +Pl+Com:jd PxPlComC ;

LEXICON GOADA-
 +Sg+Gen: Px2V ;
 +Sg+Acc: Px2V ;

LEXICON ADVV
 +Adv:Q7t K ;

LEXICON ADVC
 +Adv:it K ;

LEXICON VUOHTA					!^C^
 +vuohta+N:#vuoh'ta@C.NeedNoun@ VUOHTAMORPH ;	!^C^

LEXICON VUOHTAMORPH
 NomV ;
 EssV ;
 GOAHTI-OBL ;
 GOAHTE- ;
 +Sg+Gen:X4 R ;	!^C^



! Possessive suffixes
! -------------------

LEXICON PxV
 :X2 Px1V ;
 :X2 Px2V ;
 :X1 Px3V ;

LEXICON PxV2
 Px1V ;
 Px2V ;
 Px3V ;

LEXICON Px1V
 +PxSg1:n K ;
 +PxDu1:me K ;
 +PxPl1:met K ;

LEXICON Px2V
 +PxSg2:t K ;
 +PxDu2:de K ;
 +PxPl2:det K ;

LEXICON Px3V
 +PxSg3:s K ;
 +PxDu3:ska K ;
 +PxPl3:set K ;

LEXICON PxA
! symposiaskka
 +PxSg1:n K ;
 +PxSg2:t K ;
 +PxSg3:s K ;
 +PxDu1:me K ;
 +PxDu2:tte K ;
 +PxDu3:skka K ;
 +PxPl1:met K ;
 +PxPl2:ttet K ;
 +PxPl3:set K ;

LEXICON PxC
 +PxSg1:an K ;
 +PxSg2:at K ;
 +PxSg3:is K ;
 +PxDu1:eame K ;
 +PxDu2:eatte K ;
 +PxDu3:easkka K ;
 +PxPl1:eamet K ;
 +PxPl2:eattet K ;
 +PxPl3:easet K ;

LEXICON PxPlComC
 +PxSg1:angujn K ;
 +PxSg2:atgujn K ;
 +PxSg3:isgujn K ;
 +PxDu1:eamegujn K ;
 +PxDu2:eattegujn K ;
 +PxDu3:easkkagujn K ;
 +PxPl1:eametgujn K ;
 +PxPl2:eattetgujn K ;
 +PxPl3:easetgujn K ;

LEXICON PxPlComV1
 :X2 PxPlCom12V ;
 :X1 PxPlCom3V ;

LEXICON PxPlCom12V
 +PxSg1:ngujn K ;
 +PxSg2:tgujn K ;
 +PxDu1:megujn K ;
 +PxDu2:degujn K ;
 +PxPl1:metgujn K ;
 +PxPl2:detgujn K ;

LEXICON PxPlCom3V
 +PxSg3:sgujn K ;
 +PxDu3:skagujn K ;
 +PxPl3:setgujn K ;



! Clitics
! -------

LEXICON K
 ENDLEX ;
 +Clt:#go ENDLEX ;	!^C^
 +Clt:#ge ENDLEX ;	!^C^
 +Clt:#naj ENDLEX ;	!^C^
 +Clt:#najgo ENDLEX ;	!^C^
 +Clt:#ba ENDLEX ;	!^C^
 +Clt:#be ENDLEX ;	!^C^
 +Clt:#han ENDLEX ;	!^C^
 +Clt:#goson ENDLEX ;	!^C^
 +Clt:#bat ENDLEX ;	!^C^

LEXICON K-son
 ENDLEX ;
 +Cltson:#son ENDLEX ;	!^C^
! This is a Continuation Class for Interrogative Pronouns and Adverbs like 
! gii(son), gosa(son) etc.. These have now been added to the lexicon(s).

LEXICON ENDLEX
 @D.NeedNoun.ON@ # ;
! This ENDLEX business in order to remove illegal compounds.


! Adjective declension
! --------------------

LEXICON BUORRE ! For this adj only
 +A:X7 NomV "good A" ;
 +A+Comp:X8b BUStem ;
 +A+Superl:X8muss BUOREMUS ;
 +A:X1 EssV "good A" ;
 +A: GOAHTI-OBL "good A" ;
 +A+lágan:X4lágan LAGAN ;			!^C^
 +A+lágan:X7lágan LAGAN ;			!^C^

LEXICON LAGAN					!^C^
 +Attr: K ;					!^C^
 EABBO/EAMOS ;					!^C^
 GAPPUS- ;					!^C^
 GAPPUS0 ;					!^C^
! GARRAS- ; ! GAPPUS- instead			!^C^
 -:#%- ProperNoun ;				!^C^			
 :# AGADJECT ;					!^C^

LEXICON AttributeFormList
!  garas+A+Attr:garra K "hard A" ;
  galmmas+A+Attr:galbma K "cold A" ;
  c1áppat+A+Attr:c1áppa K "beautiful A" ;
!  c1áppis+A+Attr:c1áppa K "beautiful A" ;
!  fiinnis+A+Attr:fijnna K "fine A" ;
! removed beacause lexicon FIINNA works

LEXICON DenominalNounsV		  !^C^
 +s1+Dimin+N:X6z1 DIMINC ;	  !^C^
! +Der/s1+N:X6z1 DIMINC ;
! This is what I want, do this at some point!
! +las1+N:laz1z1 MUSH ;
! this one was removed bec. of +las1+A in the DenominalAdjsV
! lexicon. But was there any good reason for the las1 -> MUSH
! pointer in the first place?

LEXICON DenominalAdjsV			!^C^
 +heapmi+A:X9heapmi HEAPMI ;		!^C^
 +heapmi+A+Attr:X9his K ;		!^C^
 +heapmi+A:X9his VUOHTA ;		!^C^
 +heapmi+A:X9h EABBO/EAMOS_CONT ;	!^C^
 +huvva+V:X9huvva SG3PASSV ;		!^C^
 +huhtti+V:X9huhtti DIEHTIStem ;	!^C^
 +hudda+V:X9hudda SG3PASSV ;		!^C^
 +las1:X6laz1z1 ISSORAS ;		!^C^
 +las1+A:X6l EABBO/EAMOS_CONT ;		!^C^

LEXICON DenominalNounsC			!^C^
 +as1+Dimin:az1z1 DIMINV ;		!^C^

LEXICON DenominalAdjsC			!^C^
 +las1:#laz1z1 ISSORAS ;		!^C^




! Verb conjugation
! ================


LEXICON ARVI !Bisyllabic Impersonal Verbs!
 +V+Ind+Prs+Sg3:Q1 K ; ! Q1 because arvi is wrong
 +V+Ind+Prt+Sg3:j K ;
 +V+Ind+Prs+ConNeg: K ;
 +V+Cond+Prs+Sg3:s1ij K ;
 +V+Cond+Prs+Sg3:lij K ;
 +V+Cond+Prs+ConNeg:s1e K ;
 +V+Cond+Prs+ConNeg:s1e K ;
 +V+Pot+Prs+Sg3:z1 K ; 
 +V+Pot+Prs+Sg3:z1a K ; 
 +V+Pot+Prs+ConNeg:z1 K ;
 +V+ImprtP+Prs+Sg3:jehkos K ;
 +V+PrfPrc:n K ;
 :X1 NominalFormsV1 ;		
 :X4 NominalFormsV7 ;		
 :Q6 NominalFormsV8 ;		

LEXICON GALGA
 +V: DIEHTIStem ;
! because modals do not participate in derivation

LEXICON FERTE
 +V: DOHPPEStem ;
! +V: DOHPPEINCH ;
 +V: DOHPPECnj ;    
! because modals do not participate in derivation
! if fertegoahtit is possible, if not, go right to DOHPPECnj

LEXICON DIEHTI !Bisyllabic Verbs with Personal Passive
 +V: DIEHTIStem ;
 +V: DeverbalVerbsDIEHTI ;

LEXICON DIEHTIStem
 :Y7j PASSIVE ;
 BOAHTIINCH ;

LEXICON BOAHTI !Bisyllabic Verbs without Personal Passive
 +V: BOAHTIStem ;
 +V: DeverbalVerbsBOAHTI ;

LEXICON BOAHTIStem
 SG3PASSV ;
 BOAHTIINCH ;

LEXICON BOAHTIINCH
 DeverbalNounsV ;			!^C^
 +goahti:goah'ti BOAHTICnj " INCH" ;
 BOAHTICnj ;

LEXICON BOAHTICnj
 +Ind+Prs: PrsV ;
 +Ind+Prt: PrtV ;
 +Pot+Prs:Q7z1 PrsC ;
 +Cond: CondV1 ;
 +Imprt: ImprtVA ;
 NominalFormsV ;		

LEXICON CIELGA 
+V: SG3PASSV ;
+V+goahti:goah'ti BOAHTICnj ;
+V: BOAHTICnj ;
+V: DeverbalNounsDOHPPE- ;		!^C^

LEXICON CondV1
 +Prs:X6s1 CondBOAHTI ;
 +Prs:Q5l CondBOAHTI ;

LEXICON CondV2
 +Prs:s1 CondBOAHTI ;
 +Prs:l CondBOAHTI ;

LEXICON CondBOAHTI
 PrtC1 ;
 PrtC2 ;
 +ConNeg:e K ;

LEXICON NominalFormsV
 :X1 NominalFormsV1 ;
 :X4 NominalFormsV2 ;			
 :Q6 NominalFormsV3 ;
 :X2 NominalFormsV4 ;
 :Q3 NominalFormsV5 ;
 :Y1 NominalFormsV6 ;

LEXICON NominalFormsC
 NominalFormsC1 ;
 NominalFormsC2 ;

LEXICON NominalFormsC1
 +Inf:it K ;
 +Act:an K ;
 +Act:an@C.NeedNoun@ R ;	!^C^
 +Act+Ess:eame K ;
 +Act+Ess:eamen K ;
 +Act+Loc:eames K ;
 +Ger:ettiin K ;
 +Ger:ettiin PxC ;
 +PrfPrc:an K ;
 +VAbess:#keahttá K ;
 +A+Attr:#keahtes K ;
 +VAbess:#haga K ;
 +VGen: K ;

LEXICON NominalFormsC2
 +PrsPrc:eaddji K ;

LEXICON NominalFormsVCT1
 NominalFormsV1 ;
 NominalFormsV2 ;
 NominalFormsV4 ;

LEXICON NominalFormsV1
 +Inf:t K ;
 +Act:n K ;
 +Act:n@C.NeedNoun@ R ;	!^C^
 +Act+Ess:me K ;
 +Act+Ess:min K ;
 +Act+Ess:men K ;
 +Act+Loc:mis K ;

LEXICON NominalFormsV2
 +Ger:dettiin K ;
 +Ger:dettiin PxC ;
 +VAbess:keahttá K ;
 +VAbess:haga K ;
 +A+Attr:keahtes K ;
 +VGen: K ;

LEXICON NominalFormsV3
 +Ger:dettiin K ;
 +Ger:dettiin PxC ;
 +VAbess:keahttá K ;
 +VAbess:haga K ;
 +A+Attr:keahtes K ;

LEXICON NominalFormsV4
 +PrfPrc:n K ;

LEXICON NominalFormsV5
 +Imprt+Prs+ConNegII:00 K ;

LEXICON NominalFormsV6
 +PrsPrc:¤ K ;

LEXICON NominalFormsV7
 +Ger:dettiin K ;
 +VAbess:keahttá K ;
 +VAbess:haga K ;
 +A+Attr:keahtes K ;

LEXICON NominalFormsV8
 +Ger:dettiin K ;
 +VAbess:keahttá K ;
 +VAbess:haga K ;
 +A+Attr:keahtes K ;

LEXICON GILLE !Contracted Verbs without Personal Passive
 +V+j:j SG3PASS ;
 +V: DOHPPEINCH ;
 +V: DeverbalVerbsGILLE ;

LEXICON DOHPPE !Contracted Verbs with Personal Passive
 +V: DOHPPEStem ;
 +V+j:j PASSIVE ;
 +V: DOHPPEINCH ;
 +V: DeverbalVerbsDOHPPE ;

LEXICON BORGE !Contracted Impersonal Verbs
 +V: ContrImpersonal ;
 +V+j:j SG3PASS ;
 +V: DOHPPEINCH ;
! +V: DeverbalVerbsBORGE ;				!^C^
 +V: DeverbalVerbsDOHPPE ;				!^C^
! Here I have exchanged Pekka's original BORGE with DOHPPE. DeverbalVerbsBORGE
! was not defined, and since they are deverbal verbs, intransitive impersonal 
! BORGE should behave like intransitive personal DOHPPE. TT 020108.

LEXICON DOHPPEStem				 	!^C^
 +j:j DeverbalNounsDOHPPEJ ;				!^C^
 DeverbalNounsDOHPPE- ;					!^C^

LEXICON DOHPPEINCH
 +goahti:goah'ti BOAHTICnj ; ! +goah0ti changed to +goahti, as 0 is bad in tag
 +goahti:s1goah'ti BOAHTICnj ; ! ditto
 DOHPPECnj ;

LEXICON DOHPPECnj
 :j jStemsDOHPPE ;
 :Y5 VStemsDOHPPE ;
 :Y6 VjStemsDOHPPE ;
!removed "+j" in front of ":j"

LEXICON MUITAL !Trisyllabic Verbs with Personal Passive
 +V: MUITALStem ;
 +V: DeverbalVerbsMUITAL ;				!^C^

LEXICON MUITALStem
 PASSIVE ;
 MUITALINCH ;
 DeverbalNounsC ;					!^C^

LEXICON ALIST !Trisyllabic Verbs without Personal Passive
 +V: ALISTStem ;
 +V: DeverbalVerbsALIST ;				!^C^

LEXICON ALISTStem
 SG3PASS ;
 MUITALINCH ;
 DeverbalNounsC ;					!^C^

LEXICON SG3PASSV
 :Y7j SG3PASS ;

LEXICON SG3PASS
 +Pass: ContrImpersonal ;

LEXICON ContrImpersonal
 +Ind+Prs+Sg3: K ;
 +Ind+Prs+ConNeg: K ;
 +Ind+Prt+Sg3:j K ;
 +Cond+Prs+Sg3:s1ij K ;
 +Cond+Prs+Sg3:lij K ;
 +Cond+Prs+ConNeg:s1e K ;
 +Cond+Prs+ConNeg:le K ;
 +Pot+Prs+Sg3:z1 K ;
 +Pot+Prs+ConNeg:z1 K ;
 +Imprt+Prs+Sg3:jehkos K ;
 +Imprt+Prs+ConNegII:jehko K ;
 +PrfPrc:n K ;
 NominalFormsV1 ;
 NominalFormsV7 ;
 NominalFormsV8 ;

LEXICON PASSIVE
 +Pass:uvvo DOHPPEINCH ;
 +Pass+meahttun+A:uvvomeahttum MEAHTTUN ;		   !^C^
 +Pass+PrfPrc:un K ;
 +Pass+PrsPrc:uvvi K ; ! XXX evaluate this
 +Pass+eaddji+N+Actor:uvvojeaddji¤@C.NeedNoun@ DEVNVCASE ; !^C^
 +Pass+upmi+N:upmi@C.NeedNoun@ DEVNVCASE ;		   !^C^

LEXICON MUITALINCH
 +goahti:is1goah'ti BOAHTICnj ;
 MUITALCnj ;

LEXICON MUITALCnj !Substems for Consonantal Verb Stems
 +Ind+Prs: PrsC ;
 +Ind+Prt: PrtC ;
 +Cond+Prs:ivc1c1e CondC ;
 +Pot+Prs+Sg3:eas1 K ;
 +Pot+Prs+Sg3:es1 K ;
 +Pot+Prs+ConNeg:eas1 K ;
 +Pot+Prs+ConNeg:es1 K ;
 +Pot+Prs:eaz1z1a PrsV ;
 +Imprt+Prs: ImprtSg2 ;
 ImprtC ;
 NominalFormsC ;

LEXICON CondC
 +ConNeg: K ;
 PrtV ;

LEXICON VStemsDOHPPE
 +Ind+Prs: VSufDOHPPE ;
 +Cond: CondV2 ;
 +Pot+Prs:z1 PrsC ;
 +Imprt+Prs: ImprtSg2 ;
 NominalFormsVCT1 ;

LEXICON VjStemsDOHPPE
 +Ind+Prt: PrtV1 ;

LEXICON jStemsDOHPPE
 +Ind: CSufDOHPPE ;
 ImprtC ;
 NominalFormsC2 ;

LEXICON VSufDOHPPE
 PrsV1 ;
 PrsV2 ;
 PrsV4 ;
 PrsV5 ;

LEXICON CSufDOHPPE
 +Prs: PrsC1 ;
 +Prt: PrtC1 ;

LEXICON PrsV !Present Tense in Vocalic Verb Stems
 :Q4 PrsV1 ;
 :Q1 PrsV2 ;
 :Q2 PrsV3 ;
 :Q6 PrsV4 ;
 :X1 PrsV5 ;

LEXICON PrsV1 !Present Tense Endings for Vocalic Verb Stems
 +Sg1:n K ;
 +Sg2:t K ;

LEXICON PrsV2 !Present Tense Endings for Vocalic Verb Stems
 +Sg3: K ;

LEXICON PrsV3 !Present Tense Endings for Vocalic Verb Stems
 +Du1: K ;
 +Pl3:t K ;

LEXICON PrsV4 !Present Tense Endings for Vocalic Verb Stems
 +ConNeg: K ;

LEXICON PrsV5 !Present Tense Endings for Vocalic Verb Stems
 +Du2:beahtti K ;
 +Du3:ba K ;
 +Pl1:t K ;
 +Pl2:behtet K ;

LEXICON PrsC !Present Tense in Consonantal Verb Stems
 PrsC1 ;
 PrsC2 ;

LEXICON PrsC1 !Present Tense in Contr/Non-Contr Consonantal Verb Stems
 +Du1:e K ;
 +Du1:edne K ;
 +Du1:etne K ;
 +Pl3:it K ;

LEXICON PrsC2 !Present Tense in Non-Contracted Consonantal Verb Stems
 +Sg1:an K ;
 +Sg2:at K ;
 +Sg3:a K ;
 +Sg3: K ;
 +Du2:eahppi K ;
 +Du3:eaba K ;
 +Pl1:it K ;
 +Pl2:ehpet K ;
 +ConNeg: K ;

LEXICON PrtV !Preterite Endings for Vocalic Verb Stems
 :X5 PrtV1 ;
 :Q2 PrtV2 ;

LEXICON PrtV1 !Preterite Endings for Vocalic Weak Grade Verb Stems
 +Sg3:j K ;
 +Du1:jme K ;
 +Du2:jde K ;
 +Du3:jga K ;
 +Pl1:jmet K ;
 +Pl2:jdet K ;

LEXICON PrtV2 !Preterite Endings for Vocalic Strong Grade Verb Stems
 +Sg1:n K ;
 +Sg2:t K ;
 +Pl3: K ;

LEXICON PrtC !Preterite Endings for Consonantal Verb Stems
 PrtC1 ;
 PrtC2 ;

LEXICON PrtC1 !Preterite Endings for Consonantal Contr./Non-Contr. Verb Stems
 +Sg1:in K ;
 +Sg2:it K ;
 +Pl3:e K ;
 +Pl3:edje K ;

LEXICON PrtC2 !Preterite Endings for Consonantal Non-Contr. Verb Stems
 +Sg3:ij K ;
 +Du1:eimme K ;
 +Du2:eidde K ;
 +Du3:eigga K ;
 +Pl1:eimmet K ;
 +Pl2:eiddet K ;

LEXICON ImprtVA !Imperative Forms for Vocalic Verb Stems
 +Prs: ImprtVB ;
 +Prs:Q6 ImprtSg2 ;

LEXICON ImprtVB !Imperative Forms for Vocalic Verb Stems
 ImprtV1 ;
 ImprtV2 ;

LEXICON ImprtV1 !Imperative Forms for Vocalic Verb Stems
 +Sg1:Q3n K ;
 +Sg3:Q3s K ;
 +Du2:Y1 K ;
 +Du3:Q3ska K ;
 +Pl1:Q3t K ;
 +Pl2:Q2t K ;
 +Pl2:Y3t K ;
 +Pl3:Q3set K ;
 +ConNegII:Q3 K ;

LEXICON ImprtV2 !Imperative Forms for Vocalic Verb Stems and Substems
 +Du1:Y2 K ;
 +Pl1:Q3t K ;
 +Pl1:Y4t K ;

LEXICON ImprtSg2 !Imperative Forms For Consonantal and Contracted Verb Stems
 +Sg2: K ;
 +ConNeg: K ;

LEXICON ImprtC !Imperative Substems for Consonantal Verb Stems
 +Imprt+Prs:eah'ki ImprtVB ;
 +Imprt+Prs:eatni ImprtV2 ;

LEXICON DeverbalNounsV	  	!^C^
 DeverbalNounsBOAHTI ;		!^C^
 DeverbalNounsDOHPPE- ;		!^C^

LEXICON DeverbalNounsC		!^C^
 DeverbalNounsMUITAL ;		!^C^
 DeverbalNounsDOHPPEJ ;		!^C^

LEXICON DeverbalNounsBOAHTI			!^C^
 +N+Actor:¤@C.NeedNoun@ DEVNVCASE ;		!^C^
 +las+A:X2lass LAS ;				!^C^

LEXICON DeverbalNounsDOHPPE-			!^C^
 +n+N+Actio:m@C.NeedNoun@ BOAHTIN ;		!^C^
 +mus1+N:mus1s1@C.NeedNoun@ MUSH ;		!^C^
 +meahttun+A:X7#meahttum MEAHTTUN ;		!^C^

LEXICON DeverbalNounsDOHPPEJ				!^C^
 +eaddji+N:eaddji¤@C.NeedNoun@ DEVNVCASE ;		!^C^
 +eaddji+N+s1+Dimin:eaddjáz1@C.NeedNoun@ DIMINC ;	!^C^

LEXICON DeverbalNounsMUITAL				!^C^
 +eapmi+N:eapmi@C.NeedNoun@ DEVNVCASE ;			!^C^
 +eamos1+N:eapmo1s1@C.NeedNoun@ EAMOSH ;		!^C^
 +meahttun+A:#meahttum MEAHTTUN ;			!^C^
 +us:X1uss JOHTOLAT ;					!^C^

LEXICON DeverbalVerbsBOAHTI			!^C^
 +st:X8st ALISTStem ;				!^C^
 +st+alla:X6stalla BOAHTIStem ;			!^C^
 +st+adda:X6stadda BOAHTIStem ;			!^C^
 +l:l ALISTStem ;				!^C^
 +l+adda:X2ladda BOAHTIStem ;			!^C^
 +l+ahtti:lahtti BOAHTIStem ;			!^C^
 +l+asti:las'ti BOAHTIStem ;			!^C^
 +h:X4h MUITALStem ;				!^C^
 +h+alla:X6halla DIEHTIStem ;			!^C^
 +h+adda:X6hadda DIEHTIStem ;			!^C^
 +h+asti:X4has'ti DIEHTIStem ;			!^C^
 +stuvva:X8stuvva SG3PASSV ;			!^C^
 +d:Q8d ALISTStem ;				!^C^

LEXICON DeverbalVerbsDIEHTI			!^C^
 +st:X8st MUITALStem ;				!^C^
 +st+alla:X6stalla DIEHTIStem ;			!^C^
 +st+adda:X6stadda DIEHTIStem ;			!^C^
 +l:l MUITALStem ;				!^C^
 +l+adda:X2ladda DIEHTIStem ;			!^C^
 +l+ahtti:lahtti DIEHTIStem ;			!^C^
 +l+asti:las'ti DIEHTIStem ;			!^C^
 +h:X4h MUITALStem ;				!^C^
 +h+alla:X6halla DIEHTIStem ;			!^C^
 +h+adda:X6hadda DIEHTIStem ;			!^C^
 +h+asti:X4has'ti DIEHTIStem ;			!^C^
 +stuvva:X8stuvva SG3PASSV ;			!^C^
 +d:Q8d MUITALStem ;				!^C^

LEXICON DeverbalVerbsDOHPPE			!^C^
 +st:st MUITALStem ;				!^C^
 +st+alla:stalla DIEHTIStem ;			!^C^
 +st+adda:stadda DIEHTIStem ;			!^C^
 +h:h MUITALStem ;				!^C^
 +h+alla:halla DIEHTIStem ;			!^C^
 +h+adda:hadda DIEHTIStem ;			!^C^
 +h+asti:has'ti DIEHTIStem ;			!^C^
 +stuvva:stuvva SG3PASSV ;			!^C^
 +d:d MUITALStem ;				!^C^

LEXICON DeverbalVerbsGILLE			!^C^
 +st:st ALISTStem ;				!^C^
 +st+alla:stalla BOAHTIStem ;			!^C^
 +st+adda:stadda BOAHTIStem ;			!^C^
 +h:h MUITALStem ;				!^C^
 +h+alla:halla DIEHTIStem ;			!^C^
 +h+adda:hadda DIEHTIStem ;			!^C^
 +h+asti:has'ti DIEHTIStem ;			!^C^
 +stuvva:stuvva SG3PASSV ;			!^C^
 +d:d ALISTStem ;				!^C^
				!^C^
LEXICON DeverbalVerbsMUITAL				!^C^
 +ahtti:ahtti DIEHTIStem ;				!^C^
 +alla:X1alla DIEHTIStem ;				!^C^
 +adda:X1adda DIEHTIStem ;				!^C^
 +asti:as'ti DIEHTIStem ;				!^C^
				!^C^
LEXICON DeverbalVerbsALIST				!^C^
 +ahtti:ahtti DIEHTIStem ;				!^C^
 +alla:X1alla BOAHTIStem ;				!^C^
 +adda:X1adda BOAHTIStem ;				!^C^
 +asti:as'ti BOAHTIStem ;				!^C^



