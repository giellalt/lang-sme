! Divvun & Giellatekno - open source grammars for Sámi and other languages
! Copyright © 2000-2010 The University of Tromsø & the Norwegian Sámi Parliament
! http://giellatekno.uit.no & http://divvun.no
!
! This program is free software; you can redistribute and/or modify
! this file under the terms of the GNU General Public License as published by
! the Free Software Foundation, either version 3 of the License, or
! (at your option) any later version. The GNU General Public License
! is found at http://www.gnu.org/licenses/gpl.html. It is
! also available in the file $GTHOME/LICENSE.txt.
!
! Other licensing options are available upon request, please contact
! giellatekno@uit.no or feedback@divvun.no

Multichar_Symbols

  +Use/NG    ! Do not generate, for isme-ped.fst and apertium
  +String    ! Tag to denote non-numeric strings
  +NumNum    ! Tag to denote real numbers in one form or another


! Note! the diacritic marks in this file are only part of the lower
! representation. This means they can only be used from number to letter
! conversion of date and month representation, not the other way around.
! In order to get a two-way conversion, they must be added to the
! upper level as well.


LEXICON Root

! < [a|b|c|d|e|f|g|h|i|j|k|l|m|n|o|p|q|r|s|t|u|v|w|q|y|z|æ|ø|å|ä|ö|á|č|đ|ŋ|A|B|C|D|E|F|G|H|I|J|K|L|M|N|O|P|Q|R|S|T|U|V|W|X|Y|Z|Æ|Ø|Å|Ä|Ö|Á|Č|Đ|Ŋ|Š|Ŧ|Ž]* > #             ; ! This first line is to allow all letter strings.

! The regex in the previous line is now commented out. If you want this fst to
! pass through all letters untouched (but transform all numbers)
! then comment it in again. It becomes critical for the invers sme-inum.fst, that
! does not work with this line. If it really is needed, we might filter it out
! for sme-inum.fst.

  DATEYEAR ; 
  PERIOD ;

LEXICON DATEYEAR
 1:         THOUSANDS      ; ! 1000 ->
 2:guokte   THOUSANDS      ; ! 2000 ->
 3:golbma   THOUSANDS      ; ! etc.->
 4:njeallje THOUSANDS      ;
 5:vihtta   THOUSANDS      ;
 6:guhtta   THOUSANDS      ;
 7:čieža    THOUSANDS      ;
 8:gávcci   THOUSANDS      ;
 9:ovcci    THOUSANDS      ;
            YEARTEENS      ; ! 1000 - 1999
            YEARHUNDREDS   ; ! 100-999
            TENS           ;
            ONES           ;

LEXICON THOUSANDS      ! x > 1
 :duhát  THOUSAND ;

LEXICON THOUSAND
         UNDERTHOUSAND ; ! 1100-1999
     %0: TENS          ; ! 1020-1099
     %0: TEENS         ; ! 1010-1019
   %0%0: ONES          ; ! 1001-1009

!===========================
!Here starts the 999 numbers
!===========================

LEXICON UNDERTHOUSAND
          HUNDREDS ;
 1:čuođi  HUNDRED  ;

LEXICON HUNDREDS
 2:guokte   CUODI ;
 3:golbma   CUODI ;
 4:njeallje CUODI ;
 5:vihtta   CUODI ;
 6:guhtta   CUODI ;
 7:čieža    CUODI ;
 8:gávcci   CUODI ;
 9:ovcci    CUODI ;

LEXICON CUODI
  :čuođi  HUNDRED ;

LEXICON HUNDRED
       TENS  ;
       TEENS ;
   %0: ONES  ;

LEXICON TEENS
 1: TEEN ;

LEXICON TEEN
 1:okta        LOHKAI ;
 1:akta+Use/NG LOHKAI ;
 2:guokte      LOHKAI ;
 3:golbma      LOHKAI ;
 4:njeallje    LOHKAI ;
 5:vihtta      LOHKAI ;
 6:guhtta      LOHKAI ;
 7:čieža       LOHKAI ;
 8:gávcci      LOHKAI ;
 9:ovcci       LOHKAI ;



LEXICON TENS
   1%0:logi     ENDLEX    ;
     2:guokte   LOGI     ;
     3:golbma   LOGI     ;
     4:njeallje LOGI     ;
     5:vihtta   LOGI     ;
     6:guhtta   LOGI     ;
     7:čieža    LOGI     ;
     8:gávcci   LOGI     ;
     9:ovcci    LOGI     ;


LEXICON LOGI
       :logi  ONES        ;




LEXICON ONES
 1:okta        ENDLEX ;
 1:akta+Use/NG ENDLEX ;
 2:guokte      ENDLEX ;
 3:golbma      ENDLEX ;
 4:njeallje    ENDLEX ;
 5:vihtta      ENDLEX ;
 6:guhtta      ENDLEX ;
 7:čieža       ENDLEX ;
 8:gávcci      ENDLEX ;
 9:ovcci       ENDLEX ;
 %0:       ENDLEX ;


LEXICON LOHKAI
 :nuppelohkái  ENDLEX ;


LEXICON YEARTEENS
 1: YEARTEEN ;

LEXICON YEARTEEN
 1:okta        YEARLOGAI ;
 1:akta+Use/NG YEARLOGAI ;
 2:guokte      YEARLOGAI ;
 3:golbma      YEARLOGAI ;
 4:njeallje    YEARLOGAI ;
 5:vihtta      YEARLOGAI ;
 6:guhtta      YEARLOGAI ;
 7:čieža       YEARLOGAI ;
 8:gávcci      YEARLOGAI ;
 9:ovcci       YEARLOGAI ;

LEXICON YEARLOGAI
 :nuppelot  YEARHUNDRED ;
! :nuppelotčuođi%  TENS ;

LEXICON YEARHUNDREDS
 1:            YEARHUNDRED ;
 1:            YEARHUNDRED ;
 2:guokte      YEARHUNDRED ;
 3:golbma      YEARHUNDRED ;
 4:njeallje    YEARHUNDRED ;
 5:vihtta      YEARHUNDRED ;
 6:guhtta      YEARHUNDRED ;
 7:čieža       YEARHUNDRED ;
 8:gávcci      YEARHUNDRED ;
 9:ovcci       YEARHUNDRED ;

LEXICON YEARHUNDRED
   %0:čuođi  ONES    ;
     :čuođi  TENS    ;
     :čuođi  TEENS    ;
 %0%0:čuođi  ENDLEX ;

!===========================
!Here starts the period, e.g. 2010-2022
!===========================


LEXICON PERIOD
! :jagiid%  PDATEYEAR ; !mulighet
   PDATEYEAR ;

LEXICON PDATEYEAR
 1:         PTHOUSANDS      ; ! 1000 ->
 2:guokte   PTHOUSANDS      ; ! 2000 ->
 3:golbma   PTHOUSANDS      ; ! etc.->
 4:njeallje PTHOUSANDS      ;
 5:vihtta   PTHOUSANDS      ;
 6:guhtta   PTHOUSANDS      ;
 7:čieža    PTHOUSANDS      ;
 8:gávcci   PTHOUSANDS      ;
 9:ovcci    PTHOUSANDS      ;
            PYEARTEENS      ; ! 1000 - 1999
            PYEARHUNDREDS   ; ! 100-999
            PTENS           ;
            PONES           ;
 1:         HOUNDRED-THOUSAND ;
 2:guovtte  HOUNDRED-THOUSAND ; ! 2000 ->
 3:golmma   HOUNDRED-THOUSAND ; ! etc.->
 4:njealje  HOUNDRED-THOUSAND ;
 5:viđa     HOUNDRED-THOUSAND ;
 6:guđa     HOUNDRED-THOUSAND ;
 7:čieža    HOUNDRED-THOUSAND ;
 8:gávcci   HOUNDRED-THOUSAND ;
 9:ovcci    HOUNDRED-THOUSAND ;



LEXICON PTHOUSANDS      ! x > 1
 :duhát  PTHOUSAND ;

LEXICON PTHOUSAND
         PUNDERTHOUSAND ; ! 1100-1999
     %0: PTENS          ; ! 1020-1099
     %0: PTEENS         ; ! 1010-1019
!   %0%0: PONES          ; ! 1001-1009

!===========================
!Here starts the 999 numbers
!===========================

LEXICON PUNDERTHOUSAND
          PHUNDREDS ;
 1:čuođi  PHUNDRED  ;

LEXICON PHUNDREDS
 2:guokte   PCUODI ;
 3:golbma   PCUODI ;
 4:njeallje PCUODI ;
 5:vihtta   PCUODI ;
 6:guhtta   PCUODI ;
 7:čieža    PCUODI ;
 8:gávcci   PCUODI ;
 9:ovcci    PCUODI ;

LEXICON PCUODI
  :čuođi  PHUNDRED ;

LEXICON PHUNDRED
       PTENS  ;
       PTEENS ;
   %0: PONES  ;

LEXICON PTEENS
 1: PTEEN ;

LEXICON PTEEN
 1:okta        PLOHKAI ;
 1:akta+Use/NG PLOHKAI ;
 2:guokte      PLOHKAI ;
 3:golbma      PLOHKAI ;
 4:njeallje    PLOHKAI ;
 5:vihtta      PLOHKAI ;
 6:guhtta      PLOHKAI ;
 7:čieža       PLOHKAI ;
 8:gávcci      PLOHKAI ;
 9:ovcci       PLOHKAI ;



LEXICON PTENS
   1%0:logi     TO ;
   1%0:logi     FROM ;
     2:guokte   PLOGI ;
     2:guovtte  PONLYLOGI ;
     3:golbma   PLOGI ;
     3:golmma   PONLYLOGI ;
     4:njeallje PLOGI ;
     4:njealje PONLYLOGI ;
     5:vihtta   PLOGI ;
     5:viđa   PONLYLOGI ;
     6:guhtta   PLOGI ;
     6:guđa   PONLYLOGI ;
     7:čieža    PLOGI ;
     7:čieža    PONLYLOGI ;
     8:gávcci   PLOGI ;
     8:gávcci   PONLYLOGI ;
     9:ovcci    PLOGI ;
     9:ovcci    PONLYLOGI ;

LEXICON PLOGI
       :logi  PONES ;

LEXICON PONLYLOGI
       %0:logi  FROM ;
       %0:logi  TO ;



LEXICON PONES
 1:ovtta        FROM ;
 1:avtta+Use/NG FROM ;
 2:guovtti      FROM ;
 3:golmma      FROM ;
 4:njealji    FROM ;
 5:viđa      FROM ;
 6:guđa      FROM ;
 7:čieža       FROM ;
 8:gávcci      FROM ;
 9:ovcci       FROM ;
 1:ovtta        TO ;
 1:avtta+Use/NG TO ;
 2:guovtti      TO ;
 3:golmma      TO ;
 4:njealji    TO ;
 5:viđa      TO ;
 6:guđa      TO ;
 7:čieža       TO ;
 8:gávcci      TO ;
 9:ovcci       TO ;


LEXICON PLOHKAI
 :nuppelogi  FROM ;
 :nuppelogi  TO ;


LEXICON PYEARTEENS
 1: PYEARTEEN ;

LEXICON PYEARTEEN
 1:okta        PYEARLOGAI ;
 1:akta+Use/NG PYEARLOGAI ;
 2:guokte      PYEARLOGAI ;
 3:golbma      PYEARLOGAI ;
 4:njeallje    PYEARLOGAI ;
 5:vihtta      PYEARLOGAI ;
 6:guhtta      PYEARLOGAI ;
 7:čieža       PYEARLOGAI ;
 8:gávcci      PYEARLOGAI ;
 9:ovcci       PYEARLOGAI ;

LEXICON PYEARLOGAI
 :nuppelot  PYEARHUNDRED ;

LEXICON PYEARHUNDREDS
 1:            PYEARHUNDRED ;
 1:            PYEARHUNDRED ;
 2:guokte      PYEARHUNDRED ;
 3:golbma      PYEARHUNDRED ;
 4:njeallje    PYEARHUNDRED ;
 5:vihtta      PYEARHUNDRED ;
 6:guhtta      PYEARHUNDRED ;
 7:čieža       PYEARHUNDRED ;
 8:gávcci      PYEARHUNDRED ;
 9:ovcci       PYEARHUNDRED ;

LEXICON PYEARHUNDRED
   %0:čuođi  PONES    ;
     :čuođi  PTENS    ;
     :čuođi  PTEENS    ;
             TO ;


LEXICON HOUNDRED-THOUSAND
 %0%0%0:duhát FROM ;
 %0%0%0:duhát TO ;
 %0%0:čuođi FROM ;
 %0%0:čuođi TO ;

LEXICON FROM
-:% rájes%  PDATEYEAR ;

LEXICON TO
  :% rádjai ENDLEX ;

LEXICON ENDLEX
  # ;
