# ==================================================================== #
# ==================================================================== #
#        N O R T H E R N   S Á M I   D I S A M B I G U A T O R         #
# ==================================================================== #
# ==================================================================== #

#    Sámi language technology project 2003-2006, University of Tromsø  #


            # =========================================== #
            #               Table of contents             #
            # =========================================== #
                 
            # Delimiters
            # Tags and sets
	        # Mapping rules
            # Disambiguation rules
              # One-cohort disambiguation - cycle 0
              # Local disambiguation - cycles 1 and 2
                # Cycle 1a: Special cases 
                  # Personal pronouns
                  # Px
                  # Certain verb readings
                # Cycle 1b: Cleaning up after the special cases
                # Cycle 2: Other local disambiguation         
                  # Noun or not?
		  # Adjectives and adverbs
                  # Disambiguating clitics
                  # Disambiguating adpositions
                  # Disambiguating subjunctions
                  # Disambiguating adverbs
                  # Disambiguating pronouns
                  # Disambiguating adjectives
                  # Disambiguating verbs - part 1
                  # Disambiguating nouns
                  # Disambiguating verbs - part 2
                  # Residual cases
                # Cycle 3: Global disambiguation              
		# Cycle 4: Syntactic disambiguation   
		# Cycle 5: Post-syntactic morphological disambiguation
		  # (Cycle 5 is still EMPTY!!)  


# ========== #
# Delimiters #
# ========== #


DELIMITERS = "<.>" "<!>" "<?>" "<¶>";


# ============= #
# Tags and sets #
# ============= #


SETS # (Remove all lines that start with SETS if you want to use mdis.)

# I define end of clause and beginning of clause in a way so that the file
# may be read both by the CG-2 and the vislcg formalisms.

LIST BOS = (>>>) (<s>); LIST EOS = (<<<) (</s>); # vislcg and CG-2 together.
#LIST BOS = (>>>); LIST EOS = (<<<); # end and beg. of sentence. for vislcg.


# Tags declared as single-membered LISTs 
# ======================================

# Parts of speech
# ---------------

LIST N = N ; LIST A = A ; LIST Adv = Adv ; LIST V = V ; LIST Pron = Pron ;
LIST CS = CS ; LIST CC = CC ; LIST Po = Po ; LIST Pr = Pr ;
LIST Pcle = Pcle ; LIST Num = Num ; LIST Prop = Prop ;

# POS sub-categories
# ------------------

LIST Pers = Pers ; LIST Dem = Dem ; LIST Interr = Interr ;
LIST Indef = Indef ;
LIST Recipr = Recipr ; LIST Refl = Refl ;     # LIST Rel = Rel ; 
LIST Interj = Interj ; LIST Adp = Adp ; 

# Morphosyntactic properties
# --------------------------

LIST Nom = Nom ; LIST Acc = Acc ;
LIST Gen = Gen ; LIST Ill = Ill ;
LIST Loc = Loc ; LIST Com = Com ;
LIST Ess = Ess ;

LIST Sg = Sg ; LIST Pl = Pl ; LIST Du = Du ;

LIST Cmpnd = Cmpnd ;

LIST PxSg1 = PxSg1 ; LIST PxSg2 = PxSg2 ; LIST PxSg3 = PxSg3 ; 
LIST PxDu1 = PxDu1 ; LIST PxDu2 = PxDu2 ; LIST PxDu3 = PxDu3 ; 
LIST PxPl1 = PxPl1 ; LIST PxPl2 = PxPl2 ; LIST PxPl3 = PxPl3 ;

LIST Comp = Comp ; LIST Superl = Superl ;

LIST Attr = Attr ; LIST Card = Card ; LIST Ord = Ord ;
LIST Qst = Qst ; LIST Foc = Foc ;

LIST IV = IV ; LIST TV = TV ;
LIST Pass = Pass ;
LIST Prt = Prt; LIST Prs = Prs ; 

LIST Ind = Ind ; LIST Pot = Pot ; LIST Cond = Cond ;
LIST Imprt = Imprt ; LIST ImprtII = ImprtII ;

LIST Sg1 = Sg1 ;  LIST Sg2 = Sg2 ; LIST Sg3 = Sg3 ;
LIST Du1 = Du1 ;  LIST Du2 = Du2 ; LIST Du3 = Du3 ;
LIST Pl1 = Pl1 ;  LIST Pl2 = Pl2 ; LIST Pl3 = Pl3 ;

LIST Inf = Inf ; LIST ConNeg = ConNeg ;  LIST Neg = Neg ;
LIST PrfPrc = PrfPrc ; LIST VGen = VGen ;
LIST PrsPrc = PrsPrc ; 

LIST Ger = Ger ; LIST Sup = Sup ; LIST Actio = Actio ;
LIST Actor = Actor ; LIST VAbess = VAbess ;

# Derivation
# ----------

LIST aš = "aš" ;



# Syntactic tags

LIST @+FAUXV = @+FAUXV ; LIST @+FMAINV = @+FMAINV ; LIST @-FAUXV = @-FAUXV ; 
LIST @-FMAINV = @-FMAINV ; LIST @-FSUBJ = @-FSUBJ ; 

LIST @ADV-A = @ADV-A ; LIST @ADV-ADV = @ADV-ADV ; LIST @ADVL = @ADVL ; 

LIST @AN> = @AN> ; LIST @ActioN> = @ActioN> ; LIST @CC = @CC ; LIST @CMPND = @CMPND ; LIST @CS = @CS ; 
LIST @DN> = @DN> ; LIST @GA> = @GA> ; LIST @GN> = @GN> ; LIST @GP< = @GP< ; LIST @GP> = @GP> ; 
LIST @HNOUN = @HNOUN ; LIST @INTERJ = @INTERJ ; LIST @MEASURE = @MEASURE ; LIST @NNum> = @NNum>; LIST @NumN< = @NumN<;
LIST @NPRON< = @NPRON< ;
LIST @NQ< = @NQ< ;  LIST @NUM-PRON = @NUM-PRON ; LIST @NUMBER = @NUMBER ; LIST @OBJ = @OBJ ; 
LIST @OPRED = @OPRED ; LIST @PCLE = @PCLE ; LIST @PCLE-COMPL = @PCLE-COMPL ; LIST @PROP> = @PROP> ; 
LIST @PrcN> = @PrcN> ; LIST @PronN< = @PronN< ; LIST @PronN> = @PronN> ;  LIST @QN< = @QN< ; 
LIST @QN> = @QN> ; LIST @SPRED = @SPRED ; LIST @SUBJ = @SUBJ ; LIST @SUBJ-QH = @SUBJ-QH ; 

LIST @TIME = @TIME ; LIST @TITLE = @TITLE ; LIST @VOC = @VOC ; LIST @X = @X ;

# Semantic tags
# -------------

LIST Ani = Ani ; LIST Fem = Fem ; LIST Mal = Mal ;
LIST Obj = Obj ; List Org = Org ; LIST Plc = Plc ;
LIST Sur = Sur ; LIST Tit = Tit ;


# Other tags
# ----------

LIST ABBR = ABBR ; LIST ACR = ACR ; LIST CLB = CLB ;
LIST PUNCT = PUNCT ;


# Single-word sets
# ----------------
 
LIST COMMA = "," ; LIST ¶ = ¶;

LIST OKTA = "okta"; LIST go = "go" ; 

# Initials
# --------

LIST INITIAL =  "a" "b" "c" "d" "e" "f" "g" "h" "i" "j" "k" "l" "m"
                "n" "o" "p" "q" "r" "s" "t" "u" "v" "w" "x" "y" "z"
                "á" "æ" "ø" "å" "ö" "ä" ;




# Tag sets
# ========
   SETS
# ========

LIST WORD = N A Adv V Pron CS CC Po Pr Interj Pcle Num ABBR ACR ; # any word


# Derivational affixes
# --------------------

LIST V-DER = adda alla d g h asti hudda huhtti huvva j l ahtti st
              stuvva goahti ;
LIST N-DER = eaddji eamoš eapmi heapmi š muš upmi us vuohta Actor Actio;
LIST A-DER = geahtes keahtes heapmi lágan laš meahttun ;

#SET DER = V-DER | N-DER | A-DER ;


# Case sets
# ---------

LIST ADVLCASE = Ill Loc Com Ess ;

LIST CASE = Nom Acc Gen Ill Loc Com Ess ;

SET NOT-NOM = CASE - Nom ;
SET NOT-GEN = CASE - Gen ;
SET NOT-ACC = CASE - Acc ;


# Noun sets
# ---------

LIST N-SG-NOM = (N Sg Nom); LIST N-PL-NOM = (N Pl Nom);
LIST N-NOM = (N Nom) ; LIST N-GEN = (N Gen);

SET LEX-N = N - N-DER;

LIST PROP = (N Prop);
SET CNOUN = N - Prop;
SET CNOUN-PL-NOM = (N Pl Nom) - Prop ;
SET HNOUN = N - Cmpnd ;
SET HNOUN-NOM = (N Nom) - Cmpnd ;
SET N-NOT-GEN = N - (N Gen) ;


# Verb sets
# ---------

# Finiteness and mood
# - - - - - - - - - -

SET V-IND-FIN = Prs | Prt ; # Problem: "In boahtán" is an invisible indicative

SET V-MOOD = Ind | Pot | Imprt | ImprtII | Cond ;

SET VFIN = V-MOOD - ConNeg | ("gč");

SET VFIN-POS = V-MOOD - ConNeg - Neg ;

#SET VFIN-NOT-IMP =  Ind | Pot | Cond ;
SET VFIN-NOT-IMP = VFIN - Imprt ;


# Person
# - - - - 

LIST V-SG1 = (V  Ind Prs Sg1) (V Ind Prt Sg1) (V Cond Prs Sg1) 
             (V Cond Prt Sg1) (V Pot Prs Sg1) (V  Neg Ind Sg1); 
LIST V-SG2 = (V  Ind Prs Sg2) (V Ind Prt Sg2) (V Cond Prs Sg2) 
             (V Cond Prt Sg2) (V Pot Prs Sg2) (V  Neg Ind Sg2); 
LIST V-SG3 = (V  Ind Prs Sg3) (V Ind Prt Sg3) (V Cond Prs Sg3) 
             (V Cond Prt Sg3) (V Pot Prs Sg3) (V  Neg Ind Sg3); 
LIST V-DU1 = (V  Ind Prs Du1) (V Ind Prt Du1) (V Cond Prs Du1) 
	     (V Cond Prt Du1) (V Pot Prs Du1) (V  Neg Ind Du1); 
LIST V-DU2 = (V  Ind Prs Du2) (V Ind Prt Du2) (V Cond Prs Du2) 
	     (V Cond Prt Du2) (V Pot Prs Du2) (V  Neg Ind Du2); 
LIST V-DU3 = (V  Ind Prs Du3) (V Ind Prt Du3) (V Cond Prs Du3) 
	     (V Cond Prt Du3) (V Pot Prs Du3) (V  Neg Ind Du3); 
LIST V-PL1 = (V  Ind Prs Pl1) (V Ind Prt Pl1) (V Cond Prs Pl1) 
             (V Cond Prt Pl1) (V Pot Prs Pl1) (V  Neg Ind Pl1); 
LIST V-PL2 = (V  Ind Prs Pl2) (V Ind Prt Pl2) (V Cond Prs Pl2) 
             (V Cond Prt Pl2) (V Pot Prs Pl2) (V  Neg Ind Pl2); 
LIST V-PL3 = (V  Ind Prs Pl3) (V Ind Prt Pl3) (V Cond Prs Pl3) 
             (V Cond Prt Pl3) (V Pot Prs Pl3) (V  Neg Ind Pl3); 

 # Note that imperative verbs are not included in these sets!


# Some subsets of the VFIN sets
# - - - - - - - - - - - - - - -

SET V-DU = V-DU1 | V-DU2 | V-DU3 ;
SET V-PL = V-PL1 | V-PL2 | V-PL3 ;

SET V-DU-PL = V-DU1 | V-DU2 | V-DU3 | V-PL1 | V-PL2 | V-PL3 ;

SET V-NOT-SG1 = VFIN-NOT-IMP - V-SG1 ;

SET V-NOT-SG3 = V-SG1 | V-SG2 | V-DU1 | V-DU2 | V-DU3 |
                V-PL1 | V-PL2 | V-PL3 ;

SET V-1-2 = V-SG1 | V-SG2 | V-DU1 | V-DU2 | V-PL1 | V-PL2 ;
SET V-3   = V-SG3 | V-DU3 | V-PL3 ;


# Sets consisting of forms of "leat" (these ones need to be rewritten)
# - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -

SET LEAN = ("leat" V Ind Prs Sg1)|("leat" V PrfPrc);
SET LEAI = ("leat" V Ind Prt Sg3)|("leat" V Imprt Prs Sg3);
SET LEAT = ("leat" V Ind Prs Pl1)|("leat" V Ind Prs Pl3)|
           ("leat" V Ind Prs Sg2)|("leat" V Inf)|("leat" V Ind Prs ConNeg);

SET LEAT-DU-PL = ("leat" V Du1)|("leat" V Du2)|("leat" V Du3)|
                 ("leat" V Pl1)|("leat" V Pl2)|("leat" V Pl3);

SET LEAT-DU3-PL3 = ("leat" V Du3) | ("leat" V Pl3); # Not referred to by any rule.

LIST LEAT-FIN-NOT-IMP = ("leat" V Ind) ("leat" V Cond) ("leat" V Pot); 

LIST LEAN-PRC = ("leat" V PrfPrc); # Not referred to by any rule.

LIST LEAN-SG1 = ("leat" V Ind Prs Sg1);


# Pronoun sets
# ------------
SETS

SET MUN = (Pron Pers Sg1 Nom);
SET DON = (Pron Pers Sg2 Nom);
SET SON = ("son" Pron Pers Sg3 Nom);
SET MOAI = (Pron Pers Du1 Nom);
SET DOAI = (Pron Pers Du2 Nom);
SET SOAI = (Pron Pers Du3 Nom);
SET MII-PERS = (Pron Pers Pl1 Nom);
SET DII = (Pron Pers Pl2 Nom);
SET SII = ("son" Pron Pers Pl3 Nom);

SET PPRON-DU-PL = MOAI | DOAI | SOAI | MII-PERS | DII | SII ;

SET PPRON-NOT-SII = MUN | DON | SON | MOAI | DOAI | SOAI | MII-PERS | DII ;

LIST PPRON-GEN = (Sg1 Gen) (Sg2 Gen) (Sg3 Gen) (Du1 Gen) (Du2 Gen) (Du3 Gen) 
	(Pl1 Gen) (Pl2 Gen) (Pl3 Gen);

SET PPRON-NOT-GEN = (Pron Pers) - PPRON-GEN ;

SET DEM-SG = (Pron Dem Sg Nom);
SET DEM-PL = (Pron Dem Pl Nom);
SET NOT-DEM = WORD - Dem ;


# Adjectival sets and their complements
# -------------------------------------

SET A-ATTR = (A Attr) ; # Used in one rule. Not needed? 

SET LEX-A = A - A-DER ;

SET A-CASE = A - Attr - Adv ;

LIST A-CC = A CC ;

SET NOT-ADJ = WORD - A ;
SET NOT-Attr = WORD - Attr ;
SET NOT-ADJ-PCLE = WORD - A - Pcle ;
SET NOT-ADJ-CC = WORD - A-CC ;

SET NOT-AA = WORD - A - Adv ;

LIST NOMINAL-ADJ = "guoktilaš" "lámis" "oasálaš" ("suddu" laš) "viissis"; 
 # and many others

 
# Adverbial sets and their complements
# ------------------------------------

SET LEX-ADV = Adv - (A*) ;
# LIST LEX-ADV = "gusto";

SET ADVERBIAL = Adv | Ill | Loc | Com | Ess ;

SET NOT-ADV = WORD - Adv;
SET NOT-ADV-PCLE = WORD - Adv - Pcle ;
SET NOT-ADV-INDEF = WORD - Adv - Indef ;
SET NOT-ADV-PCLE-ILL = WORD - Adv - Pcle - Ill ;
SET NOT-ADV-PCLE-INDEF = WORD - Adv - Pcle - Indef ;
SET NOT-ADV-PCLE-NEG = WORD - Adv - Pcle - Neg ;
SET NOT-ADVL-PCLE-NEG = WORD - @ADVL - @GP< - Pcle - Neg ; #Ask Eckhardt.

LIST IKTE = "ikte" "dolin" "dovle" ;

LIST MO = "goas" "gokko" "gos" "gosa" "makkár" "manne" "mo" "mot" "movt" 
	"nugo" (V Qst); 
 # Introduce finite clauses.


# Sets for adverbs that have lookalikes
# - - - - - - - - - - - - - - - - - - -
 # Here come some adverbs that have identical twins in other POS. 
 # If these are found in Adv contexts, we treat them as adverbs.

LIST ADV-NOT-VERB = "dušše" ;

LIST ADV-NOT-NOUN = "easka" "várra" ;

LIST ADV-NOT-NAMES = "Ain" "Anne" "Diego" "Dieppe" "Enge" "Galle" "Haga"
                      "Joba" "Johan" "Liikka" "Mai" "Mannes" "Mo" "Mot" "Naba"
                      "Nan" "Oktan" "Sierra" "Sokka" "Villa" ;

SET ADV-NOT-OTHER-POS = ADV-NOT-VERB | ADV-NOT-NOUN | ADV-NOT-NAMES ;



# Sets of elements with common syntactic behaviour
# ================================================

SETS

# VERBS
# -----

SET VERB = V - (V N);   # V is all readings with a V tag in them, VERB should
                        # be the ones without an N tag following the V

SET LEX-V = VERB - V-DER;             # VERB but not V-DER

SET V-NOT-CMPND = V - Cmpnd; # Not in use.


LIST COPULAS = "dáidit" "bissut" "gártat" "leahkit" "leat" "orrut" "šaddat" ;
 # 'Dáidit' can appear without 'leat'.

SET V-NOT-COP = V - COPULAS ;

LIST MOD-ASP = "berret" "dáidit" "fertet" "galgat" "gillet" "lávet" "sáhttit" 
		"seahtit" "soaitit" "viggat" ;
 # Verbs that never have arguments of their own - so we leave out "áigut", for ex.

LIST AUX-OR-MAIN = "áigut" "beassat" "hálidit" "háliidit" "máhttit" "nagodit" 
			"orrut" "sihtat" "veadjit";
 # These verbs can take arguments, so they do not belong in the AUX group, 
 # but they are nevertheless mapped to (@+FAUXV). 

SET AUX = COPULAS | Neg | MOD-ASP | AUX-OR-MAIN ; 

SET V-NOT-AUX = V - AUX ;  # The set of verbs that cannot be auxiliaries. 
			   # Does not include AUX-OR-MAIN.

SET VFIN-NOT-AUX = VFIN - AUX ;

SET V-MAIN = V - COPULAS - Neg - MOD-ASP | ("orrut") ;  # The set of verbs that can be 
							# main verbs. Includes 
							# AUX-OR-MAIN.

SET V-DER-PASS = V + V-DER + Pass;    # + is intersection, this should work
				      # The set is used in one rule with no hits.
	
SET V-PASS = (V Pass);

SET V-NOT-AUX-PASS = V - AUX - V-PASS ; # No longer in use.

SET V-ACT = V - Pass; # No longer in use.

SET ACT-PRFP = PrfPrc - V-PASS; # Appears in one rule.

SET V-TRANS-ACT = TV - V-PASS ; 
 # All active verbs with a TV tag, including AUX-OR-MAIN.



# Sets for verbs choosing oblique objects or adverbials
# - - - - - - - - - - - - - - - - - - - - - - - - - - - 

LIST LOC-VERB = "ballat" "beassat" "beroštit" "biehttalit" "bihtit" "boahtit" 
		"ceavzit" "čuoččut" "čuovvut" "dolkat" "fuollat" "geargat" 
		"heaitit" "ilbmat" "jearrat" "nohkkot" "váruhit" "veallát";

LIST COM-VERB = "árvalit" "árvvohuššat" "bargat" "bártašuvvat" 
		"háladit" "hilbošit" "humadit" "meannudit" "náitalit" "riidalit" 
		"ságastaddat" "ságastallat" "šiehtadit" ;

LIST ILL-VERB = "áibbašit" "álgit" "báitit" "beassat" "čohkkedit" "čujuhit" "čuohcit" 
		"deaivat" "doaškut" "dorvvastit" "došket" "duhtat" "geahččat" 
		"gullat" "guoskat" "gustot" "heivet" "irggástallat" "jáhkkit" 
		"liikot" "luohttit" "mannat" "ollet" "oskut" "riepmat" "ráhkkanit" 
		"soahpat" "suhttat" "vuolgit";


LIST ACC-LOC-VERB = "biehttalit" "earuhit" "gieldit" "jearrat" "viežžat" ;

LIST ACC-ILL-VERB = "addit" "bidjat" "mearkkašit";


# Other verb sets
# - - - - - - - -

LIST INF-VERB = "adnot" "astat" "ádjánit" "áigut" "álgit" "ásahit" "bágget" 
		"bávččagit" "beassat" "berret" "bivdit" "bivvat" "bistit" 
		"boahtit" "bovdet" "čohkkát" "čohkkedit" "čohkkánit" "čuoččahit" 
		"čuoččastit" "čuorvut" "čurggodit" ("dadjat" Pass) "dagahit" 
		"dáhttut" "dáidit" "dárbbašit" "diktit" "doaivut" "doapmat" 
		"duostat" "fertet" "fuobmát" "fuolahit" "galgat" "gáibidit" 
		"gárrut" "gártat" "geahččalit" "geatnegahttit" "gierdat" "gillet" 
		"gohččut" "háhppehit" "hálidit" "háliidit" "hárjánit" "heivet" 
		"lávet" "mannat" "máhttit" "máššat" "movttiidahttit" "muitit" 
		"nagodit" "navdit" "oahpahit" "oahppat" "oažžut" "ollet" "ribahit" 
		"riepmat" "sáhttit" "seahtit" "sihtat" "soaitit" "suovvat" "šaddat" 
		"stađđat" "veadjit" "viggat" "viššat" "vuogáiduvvat" "vuolgit" 
		"vuollánit" "vuordit";

LIST ACC-INF-VERB = "bivdit" "čuorvut" "dagahit" "diktit" "gohččut" "movttiidahttit" 
		"sihtat" "suovvat" ;

 # These are verbs taking accusative objects and infinitives; 
 # a subset of INF-VERB.
	## Ii suova nieiddas náitalit.


LIST OPRED-VERB = "atnit" "dadjat" "dahkat" "dulkot" "geavahit" "gohčodit" "gohččut" 
	"válljet";
 # Verbs that occur with objects that have secondary predicates.

LIST MOVEMENT-VERB = "mannat";
 # These verbs typically combine with place adverbials in the genitive case 
 # (i.e. not accusative).



# NOUNS
# -----

# NP sets defined according to their morphosyntactic features
# - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - 

SET NP-HEAD = Pron | HNOUN ;
SET NP-HEAD-SG = (Pron Sg) | (Pron Sg3) | (N Sg) - Cmpnd;
SET NP-HEAD-PL = (Pron Pl) | (Pron Pl3) | (N Pl) - Cmpnd;
SET NP-HEAD-SG-NOM = (Pron Sg Nom) | (Pron Sg3 Nom) | (N Sg Nom) - Cmpnd;
SET NP-HEAD-PL-NOM = (Pron Pl Nom) | (Pron Pl3 Nom) | (N Pl Nom) - Cmpnd;
SET NP-HEAD-NOM = NP-HEAD-SG-NOM | NP-HEAD-PL-NOM; 
SET NP-HEAD-ACC = (Pron Acc) | (N Acc) - Cmpnd;
SET NP-HEAD-GEN = (Pron Gen) | (N Gen) - Cmpnd;


# The PRE-NP-HEAD family of sets
# - - - - - - - - - - - - - - - -

SET PRE-NP-HEAD = (Prop Attr) | A | (Pron Pers Gen) | (N Gen) | Num | Cmpnd | CC |
                  (Pron Dem)  | (Pron Refl Gen) | (Pron Indef) | PrfPrc | PrsPrc ;

# There must be a sensible naming convention for these sets...
SET NPNH = WORD - PRE-NP-HEAD | ABBR ;     # NPNH = "NOT-PRE-NP-HEAD". 
SET NPNHA = WORD - PRE-NP-HEAD - Adv ;     # NPNHA = "NOT (PRE-NP-HEAD-OR-ADV)"
SET NPNHAI = WORD - PRE-NP-HEAD - Adv - Indef ; # NPNHAI " "...-OR-INDEF"
SET NPNHAII = WORD - PRE-NP-HEAD - Adv - Indef - Ill ; # Illative indir.obj.
SET NPNHAIIP = WORD - PRE-NP-HEAD - Adv - Indef - Ill - Pcle ;
SET NPNHC = WORD - PRE-NP-HEAD - Com ; # "njaŋggofirpmiiguin bivdin"


# Other negatively defined sets
# - - - - - - - - - - - - - - -

SET NOT-N =  WORD - N ;
SET NOT-NA = WORD - N - A ;
SET NOT-NP = VFIN | ConNeg | Inf | Pcle | Interj | CS ; # The set NP-BORDER is bigger. 


# Nominal sets defined according to their lexical properties
# - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -

LIST MANNU = "ođđa#jage#mánnu" "ođđa#jagi#mánnu" 
             "guovva#mánnu" 
             "njukča#mánnu" "njuvčča#mánnu" "márs#mánnu" 
             "cuoŋo#mánnu" "cuoŋos#mánnu" "gáranas#mánnu" 
             "miesse#mánnu" "mái#mánnu" 
             "geasse#mánnu" 
             "suoidne#mánnu" 
             "borge#mánnu" "šnjilča#mánnu" "šnjilža#mánnu" 
             "čakča#mánnu" 
             "golggot#mánnu" 
             "skábma#mánnu" "november#mánnu" "ritne#mánnu" 
             "juovla#mánnu" ;

LIST VAHKKU = "áigi" "beaivi" "bodda" "čakča" "čakča#geassi" "čuohte#jahki" 
		"dálvi"	"diibmu" "dbm" "geardi" "geassi" "giđđa" "hávvi" "idja" "jahki" 
		"jahke#čuohti" "mánnu" "riegádan#beaivi" "tiibmu" "vahkku" jahki ;
 # The quoteless jahki here is since the preprocessor is not fine-tuned
 # yet, it still gives '"1870-" # jahki N Sg Com'.
 # lets see if "dbm" works

 
LIST BEAIVI =	"mánnodat" "vuoss#árga" "disdat" "maŋŋeb#árga" "gaska#vahkku"
		"duorastat" "bearjadat" "lávvardat" "lávvordat" "sotna#beaivi" 
		"duopmo#beaivi" "sábbát";

SET TIME = MANNU | VAHKKU | BEAIVI;

SET NOTIME = N - TIME ;

LIST GEASSET = "dálvet" "dálvit" "giđđat" "geasset" "geassit" "čakčat" ;

LIST GUOVLU = "ássan#guovlu" "sámi#guovlu" "báiki" "gávpot" "guovlu" "sádji" 
	"viessu" ;

LIST ROUTE = "fávli" "geaidnu" "guorra" "luodda" "miila"; # and others!

LIST MEASURE = "miila" "kilo#mehtar" "mehtar" "mehter" "centi#mehtar" "sente"
	"cm" "milli#mehtar" "tumá" "állan" "salla" "lihtter" "gilo" ;

LIST PL-NOUN = "beassaš" "čalbme#láse" "gálssot" "headja" "sabet" "sisttet" 
	"skárri"; 
 # Nominals that appear with plural numerals.

LIST PREGEN = ("álgogeahčen")("bealli" N Sg Loc)("gaskavuohta" N Sg Loc)("guoskevaš")
	("loahpageahčen")("miella" N Sg Loc)("oktavuohta" N Sg Loc);
 # Nouns and adjectives that take a preceding genitive.

LIST N-NOPX = "lohku";
 # Nominals that probably do not have a Px ('1600-logus').

LIST N-PX = "áhčči" "bárdni" "eadni" "nieida" "viellja" "oabbá";
 # Nominals that well may have a Px.

LIST ALU = ("allu" Gen)("assu" Gen)("čiekŋu" Gen)("gassu" Gen)("govdu" Gen)
	   ("guhkku" Gen)("lossu" Gen)("stuorru" Gen)("viidu" Gen);

LIST TITLE = "ámta#mánni" "direktevra" "eanan#hearrá" ("gásttašit" eaddji) "gonagas" 
	"justitiarius" "korefereanta" "ossodat#direktevra" 
	"profehta" "professor" "ráđđe#addi" "stipendiáhta" "sundi" 
	"universitehtalektor" "vuosttas#konsuleanta";

LIST COUNTABLE = "oassi" "kapihttal" "paragráfa" ;


# Miscellaneous sets
# ------------------

LIST Clt = Qst Foc ; 

LIST Px = PxSg1 PxSg2 PxSg3 PxDu1 PxDu2 PxDu3 PxPl1 PxPl2 PxPl3 ;

#SET PPOS = Po | Pr ;

LIST POST-A = "rájes" ;   
LIST POST-B = "rádjái" ;

#LIST WACKERNAGEL = "go" "ba" "bat" "be" "bahan" "son" ;

LIST GASKAL = "gaskal" "gaskkal" "gaskii" "gaskka" "gaskkas" ; 
 # p-positions that like coordination

SET NUMERALS = Num - OKTA ;

SET NOT-NUMERALS = WORD - Num ;

LIST MANGA = "máŋga" "galle" ; # Not referred to by any rule.

SET NOT-CC = WORD - CC ;

SET NOT-PCLE = WORD - Pcle ;

LIST COMPAR = "dávjjit" "eanet" "earalágan" "eará" "earret" "seammás" 
		"seammalágan" "seammalágán" "seammaláhkai"; 
 # These combine with "go" Pcle, but COMPAR stands for 'comparison' rather than
 # 'comparative'.

LIST CONTRA = "muhto" ; # In lean muitalan, muhto dál muitalan.

LIST PROSEANTA = "proseanta" "%" ;

SET REALCLB = CLB - COMMA ;


# Border sets and their complements
# ---------------------------------

SET CP = (Pron Interr) | MO ;

SET NP-BORDER = BOS | EOS | REALCLB | VFIN | Inf | (Actio Ess) | ConNeg | VGen | 
	Sup | PPRON-NOT-GEN | Recipr | Po | Pr | Pcle | ("jed") | Interj | CS | CP ;

 # CLB removed from set in order to allow for NP-internal commas.
 # Refl is removed because of "sin iežaset vieruid" etc.



# Syntactic sets
# --------------


LIST ALLSYNTAG = (@+FAUXV) (@+FMAINV) (@-FAUXV) (@-FMAINV) (@-FSUBJ)
                 (@ADV-A) (@ADV-ADV) (@ADVL) (@AN>) (@ActioN>) (@CC)
                 (@CMPND) (@CS) (@DN>) (@GA>) (@GN>) (@GP<) (@GP>)
                 (@HNOUN) (@INTERJ) (@MEASURE) (@NPRON<) (@NQ<) 
                 (@NUM-PRON) (@NUMBER) (@OBJ) (@OPRED) (@PCLE) 
                 (@PCLE-COMPL) (@PROP>) (@PrcN>) (@PronN<) (@PronN>) 
                 (@QN<) (@QN>) (@SPRED)
                 (@SUBJ) (@SUBJ-QH) (@TIME) (@TITLE) (@VOC) (@X);

# SET ASSIGNEDSYN = ALLSYNTAG - @X ;
# funzt nicht.

# ============= #
# ============= #
# Mapping rules #
# ============= #
# ============= #
    MAPPINGS    #
# ============= #

# Mappings of minor categories
# ----------------------------

MAP (@CC) TARGET CC ;
	## Na muhto don it leat dasa nama čállán!

MAP (@CS) TARGET CS ;
	## Leago guhkes áigi dassá go Máreha oidnet?

MAP (@INTERJ) TARGET Interj ;
	## Maid, iigo leat boahtán?

MAP (@INTERJ) TARGET ("surgat") IF (-1 ("vuoi") OR ("hei"));
	## Vuoi surgat dan gándda!

MAP (@INTERJ) TARGET ("beargalat") IF (*-1 Nom BARRIER CS OR CP LINK NOT 0 (Dem Nom));
	## Gosa don leat beargalat mu reaga dahkan?

MAP (@PCLE) TARGET Pcle ;
	## Amma mii eat leat máksán?


# Determiner mappings
# -------------------

MAP (@DN>) TARGET (Dem Sg Nom) IF (*1C (N Sg Nom) BARRIER NP-BORDER);
	## Váidalusas galgá leat namahuvvon dat dilálašvuohta.

MAP (@DN>) TARGET (Dem Nom) IF (*1 Num OR Pron OR A OR N BARRIER NP-BORDER LINK 0 Nom)
	(NOT 1 Interr OR COMMA OR Dem);
	## Jogo gikse dat guolit?

MAP (@DN>) TARGET (Dem Acc) IF (*1 Num OR Pron OR A BARRIER NP-BORDER OR (N Acc) 
	LINK 0 Acc)(NOT *1 CC BARRIER WORD);
	## Mun lean oastán ja klipsen dáid njiŋŋelasaid.

MAP (@DN>) TARGET (Dem Acc) IF (*1 (N Acc) BARRIER NP-BORDER);
	## Naba daid stuora dorskiid, gosa daid bidjet?

MAP (@DN>) TARGET (Dem Gen) IF (*1 Num OR Pron OR A OR N BARRIER NP-BORDER 
	LINK 0 Gen OR ADVLCASE)(NOT 1 Po);
	## Mun bessen dan bargui.

MAP (@DN>) TARGET (Dem Attr) IF (*1 N BARRIER NP-BORDER);
	## Makkár vaikkuhusat leat jus veahkki ii addojuvvo?

MAP (@DN>) TARGET (Dem Sg Com) IF (*1 (N Sg Com) BARRIER NP-BORDER);
	## Dat ásahuvvojedje aiddo dainna ulbmiliin, ahte...

MAP (@DN>) TARGET (Dem Pl Ill) IF (*1 (N Pl Ill) BARRIER NP-BORDER);
	## De lassánii maid sisafárren dáidda guovlluide.

MAP (@DN>) TARGET (Dem Pl Loc) IF (*1 (N Pl Loc) BARRIER NP-BORDER);
	## ...ahte eamiálbmotriekti sáhttá leat okta dain speadjaliin,...

MAP (@DN>) TARGET (Dem Pl Com) IF (*1 (N Pl Com) BARRIER NP-BORDER);
	## ...de offiseara bijai muhtun ustibiid Jesusa lusa dáiguin sániiguin...

MAP (@DN>) TARGET (Dem Pl Com) IF (*1 (N Pl Com) BARRIER NOT-AA);
	## Ain ferte joatkit daiguin sierra doaibmabijuiguin.


# Attribute mappings
# ------------------

MAP (@ADV-A) TARGET ("beare") OR ("eanet" Adv) OR ("erenoamáš") OR ("earenoamáš") OR 
	("hirbmat") OR ("hui") OR ("menddo") OR ("mii" Sg Gen) OR ("nu") OR ("oba") 
	OR ("oppa" Adv) OR ("veaháš" Adv) IF (1 A)
	(NOT 0 ("eanet") LINK 1 ("uhcit") OR ("unnit"));
	## Soai leaba goappašagat nu jallat.
	## In dieđe man buorit dat leat.

MAP (@ADV-A) TARGET ("mihá") OR ("ollu" Adv) IF (1 Comp OR COMPAR);
	## Dakkár almmuhemiid leat mihá divraseabbo ráhkadit.

MAP (@ADV-A) TARGET ("buot") IF (1 Superl);
	## Guhtemuš báhkkon lea buot mávssolaččamus?

MAP (@ADV-A) TARGET ("mii" Sg Gen) IF (1 (Pron Pers Nom))
	(*1 (A Sg Nom) BARRIER V-NOT-AUX);
	## Leatgo don fuomášan dan máilmmi, man dat lea váralaš?

MAP (@ADV-ADV) TARGET ("eanet" Adv) OR ("erenoamáš") OR ("earenoamáš") OR 
	("hirbmat") OR ("hui") OR ("menddo") OR ("mii" Sg Gen) OR ("nu") OR ("oba") 
	OR ("oppa") IF (1 Adv)(NOT 1 ("fal") OR ("maiddái") OR ("mo"))
	(NOT 0 ("mii" Sg Gen) LINK 1 Po)(NOT 0 ("eanet") LINK 1 ("uhcit") OR ("unnit"));
	## Man dávjá mun livččen dáhtton čohkket du mánáid.

MAP (@ADV-ADV) TARGET Adv IF (0 Indef)(1 VGen)(NOT 1 Sg3);
	## Dát ađa lea uhccán vuošša.

MAP (@ADVL) TARGET ("ihkinassii") IF (-1 ("gosa"));
	## Mun áiigun čuovvut du gosa ihkinassii manažat.

MAP (@PronN>) TARGET ("juohke");
	## Galgá gávdnot divššohasbearráigeahčči juohke fylkkas. 

MAP (@PronN>) TARGET (Pron Attr) ;
	## Leago guhkki eará dáluide?

MAP (@PronN<) TARGET ("eatnat") IF (-1 ("giittos"));
	## Giittos eatnat luoikamis.

MAP (@PronN<) TARGET (Indef Pl Nom) IF (*-1 PPRON-DU-PL OR (N Pl) LINK 0 Nom
	BARRIER CS)(NOT -1 CC OR CLB);
	## Soai leaba goappašagat nu jallat.

MAP (@PronN<) TARGET (Refl Sg Nom) IF (*-1 MUN OR DON OR (Pron Sg3 Nom) OR 
	(Pron Sg Nom) OR (N Sg Nom) BARRIER CS OR CP OR REALCLB);
	## Dasgo mun lean ieš gohčohallama vuloš. 

MAP (@PronN>) TARGET (Refl Sg Nom) IF (*1 MUN OR DON OR (Pron Sg3 Nom) OR 
	(Pron Sg Nom) OR (N Sg Nom) BARRIER NOT-ADV-PCLE);
	## Ieš han don dadjet dan. 

MAP (@PronN<) TARGET (Refl Pl Nom) IF (*-1 PPRON-DU-PL OR (N Pl) BARRIER CS OR CP
	OR REALCLB LINK 0 Nom);
	## Mii han leat ieža gullan dan su iežas njálmmis.

MAP (@PronN>) TARGET (Refl Pl Nom) IF (*1 PPRON-DU-PL OR (N Pl) 
	BARRIER NOT-ADV-PCLE LINK 0 Nom)(NOT *1 VFIN OR Inf BARRIER NOT-ADV-PCLE);
	## ...muhto ieža dii ehpet guoskkat daid suorpmain ge.

MAP (@PronN>) TARGET Indef IF (1 N)(NOT 0 Nom LINK 1 Acc)(NOT 0 ADVLCASE)
	(NOT 1 (N Sg Gen) LINK *1 N BARRIER NPNH OR CC)
	(NOT 1 VFIN OR Ess OR ("veara"));
	## Son galgá beassat buktit oaivilis buot gažaldagaide.

MAP (@PronN>) TARGET Indef IF (0 Nom)(*1 (N Nom) BARRIER NOT-ADJ)
	(NOT 0 Pl LINK 1 Inf);
	## Goabbáge báddi galgá leahkit 60 alen guhku.

MAP (@PronN>) TARGET Indef IF (1 A-CASE)(NOT 1 ("veara"));
	## Sáhttáge ollu buriid dadjat bohtosiid birra.

MAP (@PronN>) TARGET Indef IF (NOT 0 ("iešguhtet") OR ADVLCASE)(1 Num);
	## Buot oktanuppelohkái máhttájeaddji vulge Galileai.

MAP (@PronN>) TARGET ("buot") IF (1 Dem OR Indef);
	## Buot dáid mun attán dutnje.
	## Sii ledje eanet sivalaččat go buot earát Jerusalemis.

MAP (@PronN>) TARGET Indef IF (*1 N BARRIER NP-BORDER OR REALCLB)
	(NOT *-1 ("leat") BARRIER NOT-ADV-PCLE LINK *1 Indef LINK *1 PrfPrc
	BARRIER VERB)
	(NOT *-1 INF-VERB BARRIER NOT-ADV-PCLE LINK *1 Indef LINK *1 Inf
	BARRIER VERB)(NOT 0 ADVLCASE)(NOT 1 (Pers Gen) LINK *1 N BARRIER NOT-NP OR CLB 
	LINK NOT 0 Loc)(NOT 1 Adv LINK 1 N)
	(NOT 1 (N Sg Gen) LINK *1 N BARRIER NPNH)(NOT 1 VFIN OR Ess OR ("veara"));
	## Ollu dáin doaibmabijuin sáhttá álggahit dálá rámmaid siskkobealde.

MAP (@PronN>) TARGET ("mii" Interr Pl Nom) IF (*1C (N Pl Nom) BARRIER NP-BORDER);
	## ...dan hárrái, mat riektegáldut leat, mo daid berre dulkot, ja ...

MAP (@PronN>) TARGET Interr IF (NOT 0 ("gii"))(1 N)
	(NOT 0 ("mii") LINK NOT 1 (Pl Gen))(NOT 0 (Sg Nom) LINK 1 V-SG3)
	(NOT 1 Gen LINK *1 N BARRIER NPNH);
	## Guđe sihkarvuođanjuolggadusat ledje biddjon...

MAP (@PronN>) TARGET Interr IF (0 ("guhtemuš"))(*1 (Pers Loc) BARRIER NOT-PCLE);
	## Guhtemuš ba dis sáhttá...

MAP (@PronN>) TARGET ("buot") IF (*1 (N Pl Nom) OR (N Pl Acc) BARRIER NP-BORDER);
	## Ámtamánnii gulai "bearráigeahččat buot min gullevaš eatnamiid, gittiid 
	## ja opmodagaid".

MAP (@AN>) TARGET (A Attr) ;
	## In fuobmán dadjat ahte Márehis lea ođđa irgi.
	## Dalle gusto vuosttas, nubbi ja goalmmát teakstaoassi seammaláhkái.

# buorre
MAP (@AN>) TARGET Nom IF (0 ("buorre"))(*1 (N Nom) BARRIER NPNH);
	## Leatgo buorit guolit?

MAP (@AN>) TARGET Acc IF (0 ("buorre"))(*1 (N Acc) BARRIER NPNH);
	## Doaivvutgo oažžut buriid guliid?

MAP (@AN>) TARGET (Sg Gen) IF (0 ("buorre"))
	(*1 (N Sg Gen) OR (N Sg Ill) OR (N Sg Loc) BARRIER NPNH);
	## Dearvvašvuođabargiidlága § 40 vuosttas lađđasa mielde galgá 
	## journála čállojuvvot buori fidnodábi mielde.

MAP (@AN>) TARGET (Pl Gen) IF (0 ("buorre"))
	(*1 (N Pl Gen) OR (N Pl Ill) OR (N Pl Com) BARRIER NPNH);
	## Giittán maiddái universitehtalektora Fredrik Harhoff 
	## buriid ságastallamiid ovddas.

MAP (@AN>) TARGET (Sg Com) IF (0 ("buorre"))(*1 (N Sg Com) BARRIER NPNH);
	## Jos mii addet buriin dáhtuin...

MAP (@AN>) TARGET (Pl Ill) IF (0 ("buorre"))(*1 (N Pl Ill) BARRIER NPNH);
	## Son lea sivdnidan min Kristus Jesusis buriide daguide.

MAP (@AN>) TARGET (Pl Loc) IF (0 ("buorre"))(*1 (N Pl Loc) BARRIER NPNH);
	## Son ferte leat dovddus buriin daguin. 

MAP (@AN>) TARGET (Pl Com) IF (0 ("buorre"))(*1 (N Pl Com) BARRIER NPNH);
	##

MAP (@AN>) TARGET Ess IF (0 ("buorre"))(1 (N Ess));
	## Maŋemus son šattai buorrin oahpaheaddjin.

MAP (@PrcN>) TARGET PrfPrc IF (-1 Dem OR Num OR Attr)(NOT -1 Pers)(0 Pass)(1 N);
	## ..maid almmolaš dohkkehuvvon dearvvašvuođabargit addet,..
	## Galgá leat okta čohkkejuvvon divššohasjournála. 

MAP (@PrcN>) TARGET PrfPrc IF (*-1 PrfPrc BARRIER NPNHA LINK 0 V-NOT-AUX 
	LINK NOT 0 Actio)(*1C N BARRIER NOT-ADJ);
	## Ii leat boahtán mannan váhkus.

MAP (@PrcN>) TARGET PrfPrc IF (NOT *-1 ("leat") BARRIER PrfPrc OR (Actio Ess))
	(NOT *-1 PrfPrc LINK 0 (Actio Nom) LINK NOT *-1 Nom BARRIER CS LINK 
	NOT 0 ("leat"))(0 Pass)(*1C N BARRIER NPNH); 
	## ...dan mii lei dáhpáhuvvan bosttahuvvon olbmuide.

MAP (@PrcN>) TARGET PrfPrc (-1 CLB OR go)(1 TIME);
	## Dalle šattašii liikka buorre dálki go mannan sotnabeaivve.

MAP (@PrcN>) TARGET PrfPrc IF (*-1 BOS OR REALCLB BARRIER NOT-NP)(NOT 0 ("leat"))
	(*1 N BARRIER NPNH LINK NOT 0 TIME LINK NOT 1 TIME);
	## Ovdamearkan oaivvilduvvon fálaldahkii lea sámi kulturdieđu lohkanbaji.

MAP (@PrcN>) TARGET PrfPrc IF (*-1 Po BARRIER NOT-NP LINK *-1 BOS OR REALCLB 
	BARRIER NOT-NP)(NOT 0 ("leat"))(*1 N BARRIER NPNH LINK NOT 0 TIME LINK
	NOT 1 TIME);
	## Sámiid várás oaivvilduvvon doaimmaid hárrái galggašii...
 # For those cases where the Po is homonymous with verb, and therefore a barrier
 # in the preceding rule.

MAP (@PrcN>) TARGET PrfPrc IF (*-1 Pr BARRIER NOT-NP LINK -1 NP-BORDER LINK 
	NOT 0 ("leat"))(*1 (N Gen) BARRIER NOT-NP);
	## 1978 láhka balansere gaskal sámi vuoigatvuođaid boarásmuvvan oainnu,...

MAP (@PrcN>) TARGET PrfPrc IF (*-1 V-NOT-AUX BARRIER CS OR CP OR AUX 
	LINK NOT 0 A OR N)(0 Pass)(1 N)(NOT 1 TIME);
	## Vuoigatvuohta dearvvašvuođaveahkkái gusto riikka almmolaš ruhtaduvvon 
	## spesialistadearvvašvuođabálvalussii.

MAP (@PrcN>) TARGET PrfPrc IF (-1 Adv LINK *-1 COPULAS BARRIER NOT-ADV-PCLE LINK 
	*-1 Nom BARRIER V-NOT-AUX OR CC OR CLB OR CS OR CP)
	(*1 Nom BARRIER NPNHA LINK 1 NP-BORDER);
	## Ruonáeana lea čielgasit ráddjejuvvon geográfalaš guovlu.

MAP (@PrcN>) TARGET PrfPrc IF (NOT *-1 Neg BARRIER VERB)(NOT 0 AUX)(1C (N Nom));
	## Son lea oahppan olmmoš.

MAP (@PrcN>) TARGET PrfPrc IF (0 ("mannat"))(1 TIME);
	## Ii leat boahtán mannan váhkus.

MAP (@PrcN>) TARGET PrfPrc IF (-1 ("nu"))(0 ("gohčodit" Pass));
	## Dáin áššiin sáhttet Sámedikkit várra nu gohčoduvvon friija 
	## árvvoštallama mielde mearridit.

MAP (@PrcN>) TARGET PrfPrc IF (*-1 CC BARRIER NPNH LINK -1 Acc LINK NOT 0 PrfPrc)
	(1 (N Acc));
	## Divššohasas, gii dárbbaša guhkit áiggi ja oktiiheivehuvvon dearvvašvuođa-
	## bálvalusaid,...

MAP (@PrcN>) TARGET PrsPrc IF (*1 N BARRIER NOT-ADJ);
	## Boađe boahtti vahku.

MAP (@PrcN>) TARGET PrsPrc IF (1 CC)(2 Attr)(3 N);
	## Guoski dahje lagas eatnamat.

MAP (@ActioN>) TARGET (Actio Nom) IF (-1 Gen)(1 N)(NOT 1 PrfPrc);
	## Sii orro sin iežaset dahkan darfegođiid siste.
	## Sámegiela geavaheapmi journálain lea lassánan.
	## Bálvalusaid ovdánahttin riikkarájáid rastá.

 # Since we cannot at present distinguish between long and short Actios,
 # this rule hits not only the short Actio in the first example, which is 
 # correct, but also the long Actio in the second example, which is wrong.

 # Problem: At present (051106), the rule hits neither, which also makes it hard
 # to document the tag. It hits the third sentence, though, together with @GN>.

ADD (@TITLE) TARGET TITLE IF (1 Prop);
	## Stuorra giitu maiddái professor Birger Stuevold Lassenii.
 # ADD so that argument mappings can apply later.


MAP (@MEASURE) TARGET MEASURE IF (-1 Num)(1 ALU OR A);
	## Min viessu lea vihtta mehtara alu.
	## Sus lea njellje mehtara guhkkosaš biila.

MAP (@MEASURE) TARGET MEASURE IF (-1 Num)(1 Adv OR Pr);
	## Ádjá viessu lea guokte miilla bajábeal márkana.

MAP (@MEASURE) TARGET TIME IF (-1 Num)(1 ("boaris") OR ("nuorra"));
	## Mánná lea golbma jagi boaris.

MAP (@MEASURE) TARGET TIME IF (-1 Num)(1 ("maŋŋil"));
	## Dat dáhpáhuvai golbma jagi maŋŋil.

MAP (@MEASURE) TARGET N IF (-1 Num)(1 ("velggolaš"));
	## Ánde lea minnje vihttalot ruvnno velggolaš.

MAP (@MEASURE) TARGET (N Sg Acc) IF (-1 (Num Acc))(0 MEASURE)(1 (N Acc));
	##


# Genitive mappings
# -----------------

MAP (@GN>) TARGET (Pron Sg1 Gen) IF (1 (Refl PxSg1));
	## Orru lossat geardduhit mu iežan ávžžuhusa.

MAP (@GN>) TARGET (Pron Sg2 Gen) IF (1 (Refl PxSg2));
	## Diet lea du iežat ášši.

MAP (@GN>) TARGET (Pron Sg3 Gen) IF (1 (Refl PxSg3));
	## Mii han leat gullan dan su iežas njálmmis. 

MAP (@GN>) TARGET (N Sg Gen) IF (1 (Refl PxSg3));
	## Dát lea Hearrá iežas dahku.

MAP (@GN>) TARGET (Pron Du1 Gen) IF (1 (Refl PxDu1));
	##

MAP (@GN>) TARGET (Pron Du2 Gen) IF (1 (Refl PxDu2));
	##

MAP (@GN>) TARGET (Pron Du3 Gen) IF (1 (Refl PxDu3));
	##

MAP (@GN>) TARGET (Pron Pl1 Gen) IF (1 (Refl PxPl1));
	## Min iežamet posišuvdna lea nu ahte...

MAP (@GN>) TARGET (Pron Pl2 Gen) IF (1 (Refl PxPl2));
	## Bestojupmi ii leat din iežadet dahku.

MAP (@GN>) TARGET (Pron Pl3 Gen) IF (1 (Refl PxPl3));
	## Sii orro sin iežaset dahkan darfegođiid siste.
 
MAP (@GP>) TARGET Gen IF (NOT 0 Num LINK 1 Ill)
	(NOT 0 (V Actio) LINK 1 ("dihte") OR ("dihtii"))(1 Po);
	## Na mun gal hupmen dan birra.

MAP (@GP>) TARGET Gen IF (NOT 0 Cmpnd)(1 CC)(*2 Gen BARRIER NPNHA LINK 1 Po)
	(NOT 0 (V Actio) LINK 1 CC LINK 1 (V Actio) LINK 1 ("dihte") OR ("dihtii"));
	## Divššohas galgá maiddái oažžut dieđuid vejolaš riskkaid ja 
	## lassiváikkuhusaid birra.

MAP (@GP>) TARGET Gen IF (1 (Neg Foc))(2 Gen)(3 Po);
	## Ii leat váldojuvvon ovdan Ruoŧa iige Suoma bealde.

MAP (@GP>) TARGET Gen IF (0 TIME)(1 Num)(2 Po);
	## Finnmárkku fylkkagielda lea jagi 1988 rájes ovddidan doaimmaid... 

MAP (@GP<) TARGET Gen IF (*-1 Pr BARRIER NOT-NP OR CLB)(NOT 1 (N Gen))
	(NOT 0 TIME LINK *-1 Po BARRIER NP-BORDER LINK -1 Gen)
	(NOT 0 Num LINK *1 (N Gen) BARRIER NP-BORDER)
	(NOT -1 Gen OR CLB LINK 2 Po);
	## Lága mearrádusat galget váikkuhit luohttevašvuođa gaskal 
	## divššohasa ja dearvvašvuođabálvalusa.

MAP (@GP<) TARGET Gen IF (*-1 CC BARRIER CS LINK *-1 GASKAL BARRIER CS)
	(1 NP-BORDER);
	## Dábálaččat earuha váldolaččat gaskal dábálaš (totala) geavahan-
	## vuoigatvuođaid ja sierranas (partiála) geavahanvuoigatvuođaid.

MAP (@GP<) TARGET (Pl Gen) IF (*-1 GASKAL BARRIER NOT-NP LINK NOT -1 (Pl Gen))
	(1 NP-BORDER);
	## Finnmárkkus ii goassige leat leamaš ságastallan gaskal muhtun muddui 
	## seammadássásaš oasálaččaid.
 
MAP (@NQ<) TARGET Gen IF (*-1 NUMERALS BARRIER NP-BORDER OR N OR REALCLB OR PUNCT
	LINK 0 Nom LINK NOT 0 Ord)(NOT 1 A OR N);
	## Mun máksen 100 ruvnno duvle.

MAP (@NQ<) TARGET (N Gen) IF (-1 NUMERALS LINK 0 Nom LINK NOT 0 Ord 
	LINK *-1 go BARRIER NOT-ADV-PCLE LINK -1 V-TRANS-ACT)
	(1 Acc LINK 1 CC OR CLB);
	##  ...addet go máŋga dearvvašvuođabargiidjoavkku veahki ja fállojuvvo go...

MAP (@NQ<) TARGET Gen IF (-1 CC LINK -1 (N Gen) LINK *-1 NUMERALS BARRIER NP-BORDER 
	OR N OR REALCLB OR PUNCT LINK 0 Nom LINK NOT 0 Ord);
	## Mus leat čieža vielja ja oappá.

MAP (@NQ<) TARGET (N Gen) IF (*-1 NUMERALS BARRIER NOT-ADJ LINK 0 Nom)(1 Prop);
	## Joavkkus leat vihtta oahppi Kárášjogas.

MAP (@GN>) TARGET Gen IF (0 MANNU)(1 Ord);
	## ...ođđajagemánu 1. b. 2002.

MAP (@GN> @ADVL) TARGET Gen IF (0 TIME OR ROUTE)(NOT 1 CC)
	(*1 N BARRIER NP-BORDER OR REALCLB OR Po OR Pr);
	## Min áiggi boaresolbmot muitaladdet ahte...

MAP (@ADVL) TARGET Gen IF (0 TIME OR ROUTE);
	## Maid Biret-Elle áigu dán dálvve dahkat?
	## ...ja geasset vel moatte háve dađi miel go dárbu lea.

MAP (@-FSUBJ) TARGET Gen IF (1 (Actio Gen))(NOT 1 N)(2 N) ;
	## Gean ráhkadan fanas dat lea?

MAP (@-FSUBJ) TARGET Gen IF (1 Ger);
	## Máhte lihkai Máreha boađedettiin.

MAP (@-FSUBJ) TARGET Gen IF (1 VAbess);
	## Diet dáhpáhuvai mu dieđikeahttá.

MAP (@-FSUBJ) TARGET Gen IF (*-1 ("leat") BARRIER NPNHA)(1 (V Actio Loc));
	## Dat ii leat geafes olbmo nagodeames. 

MAP (@GA>) TARGET Gen IF (*-1 COPULAS BARRIER VERB OR CS OR CP)(1 (A Comp))
	(NOT 1 N);
	## Máret lea vieljaidis boarráset.

MAP (@GA>) TARGET Gen IF (1 (A Comp))(*2 COPULAS BARRIER VERB OR CS OR CC OR CP 
	LINK NOT *1 V-NOT-AUX BARRIER CS);
	## Duon guovtti girjji buoret dat gal lea.

MAP (@GA>) TARGET Gen IF (1 (A Comp))(NOT 1 VERB OR Adv)(NOT *2 N BARRIER NP-BORDER);
	## Mun jáhkán du iežan falibun.

MAP (@GA>) TARGET Gen IF (0 MEASURE)(1 A);
	## Suorpmas lea guokte millimehtara govdat.

MAP (@GA>) TARGET Gen IF (1 ("veara"));
	## Dasgo bargi lea bálkkás veara.

MAP (@GA>) TARGET Gen IF (1 CC)(2 Gen)(3 ("veara"));
	## ...buot dan mii leat ráhkistan ja gudnejahttin veara.

MAP (@GN>) TARGET Gen IF (NOT -1 NUMERALS LINK 0 Nom)
	(NOT 0 ALU OR Cmpnd OR Num)(*1 N BARRIER NOT-NP OR REALCLB)
	(NOT 1 PUNCT LINK 1 CC)
	(NOT 1 CC LINK *1 Gen BARRIER NOT-NP LINK 1 Po)
	(NOT 1 COMMA LINK 1 Gen LINK 1 CC LINK 1 Gen LINK 1 Po)
	(NOT 0 Actio LINK 1 ("dihte") OR ("dihtii"));
	## Leatgo oaidnán Máreha misiid?

MAP (@GN>) TARGET Gen IF (NOT -1 NUMERALS LINK 0 Num)
	(NOT 0 ALU OR Num OR Cmpnd)(*1 N BARRIER NOT-AA OR CLB);
	## Vuosttaš vuojahaga fávllimus čuolda lea goaratčuoldan.

MAP (@GA>) TARGET Gen IF (1C A)(1 (Sg Loc))(2C NP-BORDER); #  (NOT 0 Interr OR ALU)
	## ...dan čuldii mii lea joddočuoldda lagamusas.

MAP (@NPRON<) TARGET (N Pl Gen) IF (-1 ("mii" Interr) OR ("mihkkege") OR ("gii"));
	## Maid ipmašiid doppe dagat?
	## Ii šaddan mihkkege gávnnademiid.

 
# Quantifier mappings
# -------------------

MAP (@AN>) TARGET (Ord);
        ## Dat lei 33. teakstaoassi.

MAP (@PCLE-COMPL) TARGET (Num Nom) IF (*-2 V-TRANS-ACT BARRIER VERB LINK 
	NOT 0 ("eanet"))(-1 ("go"))(NOT 1 N LINK 0 ADVLCASE)
	(NOT *1 VFIN BARRIER CS);
	## Ii heargi gal nagot guoddit eambbo go guokte diekkára. 

MAP (@PCLE-COMPL) TARGET (Num Nom) IF (-1 ("dego"))(NOT *1 VFIN BARRIER CS);
	## Ráhkkásiiddán, allet vajáldahte ahte Hearrái lea okta beaivi 
	## dego duhát jagi ja duhát jagi dego okta beaivi.

MAP (@QN>) TARGET (Num Acc) IF (*1 (N Acc) BARRIER NP-BORDER OR N);
	## Mun máksen 100 ruvnno duvle.

MAP (@ADVL) TARGET (Num Gen) IF (-1 ("b" ABBR) OR ("beaivi"))(-2 Ord);
	## Artihkkal lea šaddan fas áigeguovdilin maŋŋá go heivehuvvui 
	## norgalaš riektái miessemánu 21. b. 1999 nr 30 lága bokte.

MAP (@QN>) TARGET (Num Gen) IF (*1 (N Gen) BARRIER NP-BORDER OR N);
	## Galgá čájehit vuollegašvuođa juohke ovtta divššohasa eallimii.

MAP (@QN>) TARGET (Num Com) IF (*1 (N Com) BARRIER NP-BORDER OR N);
	## Das ráhkaduvvo vuojahat gátti guvlui nu galliin čuolddain 
	## (dahje oar-julggiin) go lea vuogas.

MAP (@QN>) TARGET (Num Acc) IF (*1 ALU BARRIER NP-BORDER OR N);
	## Noađđespagáin leat dat geažit guokte golbma tumá guhku.

MAP (@QN>) TARGET (Num Acc) IF (1 MEASURE OR TIME)(2 A OR Adv OR Po);
	## Suorpmas lea guokte millimehtara govdat.

MAP (@QN>) TARGET (Num Acc) IF (1 (N Sg Gen))(2 (A Comp));
	## Son vuvddii daid biergguid vihtta ruvnno hálbbibui.

MAP (@QN>) TARGET (Num Gen) IF (1 TIME)(2 ("boaris"));
	## Son lea viđa jagi boaris.

MAP (@QN>) TARGET Num IF (1 ("jahkásaš"));
	## Golmma-njealje jahkásaš mánná biddjojuvvo giissá sisa čohkkut.

MAP (@QN>) TARGET (Num Gen) IF (1 MEASURE)(2 ALU OR A);
	## Min viessu lea viđa mehtara alu.

MAP (@NUM-PRON) TARGET (N Num Nom) IF (-1 (Pers Nom))(NOT -1 Dem)
	(-1 Pl1 OR Pl2 OR Pl3);
	## Mii golmmas oktan du vieljain finaimet Niillas-čeazi geahčen.

MAP (@SUBJ @SPRED) TARGET (N Num Nom);
	## Go sotnabeaivi šattai, de čuoigaledje máŋggas várrái.
	
MAP (@NumN<) TARGET (Num Nom) IF (-1 ("nr" Nom))(-2 N OR A OR ABBR OR ACR);
    ## Mun lean ilus go beasan ovdanbuktit St.dieđ. nr. 33.
#ADD (@NumN<) TARGET (Num Nom) IF (-1 ("nr" Nom));
    #Maybe it has to be (N @ADVL).

MAP (@SUBJ-QH @SPRED) TARGET (Num Nom) IF (0 NUMERALS)
	(*1 Gen BARRIER NP-BORDER LINK NOT 0 ROUTE OR TIME OR Loc)
	(NOT 1 CC)(NOT *1 ALU BARRIER NP-BORDER)(NOT -1 (@NNum) LINK -2 (@OBJ));
	## Dá dat leat, 350 ruvnno.
	#Maybe "OR (N @ADVL)" has to be added.

MAP (@SUBJ-QH @SPRED) TARGET (Num Nom) IF (1 (Dem Pl Loc));
	## Okta dain doaimmain lea redigeret ja čorget journála.
	## ...ahte eamiálbmotriekti sáhttá leat okta dain speadjaliin...

MAP (@SUBJ-QH) TARGET (Num Nom) IF (1 TIME)(*2 ("vássit") BARRIER V-NOT-AUX);
	## ...ovdalgo 10 jagi leat vássán...

MAP (@SUBJ-QH) TARGET (Num Nom) IF (*-1 ("vássit") BARRIER VERB OR CLB)(1 TIME);
	## Muhto dál leat juo vássán golbma beaivvi dan rájes go dát dáhpáhuvai.

MAP (@SUBJ-QH) TARGET (Num Nom) IF (-1 CC)(1 Prop);
	##

MAP (@TIME) TARGET Num IF (-1 ("jahki" Gen));  # Not the best term?
	## Sápmelaččat leat jagi 1897 lihkostuvvan cealkit riektái, ahte...

MAP (@NUMBER) TARGET Num IF (NOT 0 Nom OR Acc OR Gen OR ADVLCASE);
	## Viidáseappot mearrida dearvvašvuođabargiidlága § 10 vuosttas lađđasa...
 # Should hit numerals that only have to do with numbering, not with amount. 

MAP (@NUMBER) TARGET Num IF (-1 ("s") OR ("nr") OR ("nummar") OR ("§") OR ("kapihttal") OR ("oassi"))(NOT -2 N OR A OR ABBR OR ACR);
	## Dat lea s. 240.
	#We have to see if A, ABBR and ACR work.

MAP (@NUMBER) TARGET Num IF (-3 ("s") OR ("nr") OR ("nummar") OR ("§") OR ("kapihttal") OR ("oassi"))
	(-2 Num)(-1 CC);
	## ...gč. dearvvašvuođabargiidlága kapihttal 6 ja 7.
	
#MAP (@NNum>) TARGET ("nr" Nom) IF (1 Num);
MAP (@NNum>) TARGET ("nr" Attr) IF (1 WORD OR Num);
    ##Mun lean ilus go beasan ovdanbuktit St.dieđ. nr. 33.

MAP (@QN>) TARGET Num IF (NOT -1 Num)(NOT 0 ADVLCASE)(*1 Gen OR Ill OR Loc OR Com 
	BARRIER NPNH OR REALCLB LINK NOT 0 Interr)(NOT 0 (N Num) LINK 1 (Sg Com))
	(NOT 1 Pers LINK 0 Sg1 OR Sg2 OR Sg3)
	(NOT 1 CC LINK 1 Num LINK 1 NP-BORDER);
	## Jus divššohas lea gaskal 12 ja 16 jagi boaris,...

MAP (@QN>) TARGET Num IF (*1 Ess BARRIER NPNH LINK NOT 0 (V Actio));
	##

MAP (@QN>) TARGET ("okta" Nom) IF (*1 (N Nom) BARRIER NPNH)(NOT 1 Prop OR Dem);
	## Sámegiela doaibmaplánas lei okta doaibmabidju sámegillii jorgalit
	## dieđalaš artihkkaliid.

MAP (@QN>) TARGET ("okta" Gen) IF (*1 (N Gen) OR (N Acc) OR (N Ill) OR (N Loc) 
    BARRIER NPNH)(NOT 1 Prop OR Dem);

MAP (@QN>) TARGET ("okta" Com) IF (*1 (N Com) BARRIER NPNH)(NOT 1 Prop OR Dem);
    ## Dát lea faskkon maid geavaha dušše ovttain gieđain.
    
MAP (@QN>) TARGET ("okta" Ess) IF (*1 (N Ess) BARRIER NPNH)(NOT 1 Prop OR Dem);
    # # No example yet.

# To be evaluated against the previous 4 rules.
#MAP (@QN>) TARGET ("okta") IF (*1 N BARRIER NPNH)(NOT 1 Prop OR Dem);
#	## Sámegiela doaibmaplánas lei okta doaibmabidju sámegillii jorgalit
#	## dieđalaš artihkkaliid.

MAP (@QN<) TARGET Num IF (-1 ("diibmu" Nom) OR ("dbm" Nom));
	## Mun boađán diibmu vihtta/viđas.

MAP (@ADVL) TARGET Num IF (-1 MANNU)(1 NP-BORDER);
	## Dat hávdáduvvojedje Álahedjui skábmamánus 1997.
 

# Fragment mappings
# -----------------

MAP (@HNOUN) TARGET (N Nom) IF (NOT *-1 VFIN OR ("leat" PrfPrc) OR CP)
	(NOT *1 (N Nom) BARRIER CS OR CC)
	(NOT *1 VFIN BARRIER CS)(NOT 1 go OR COMMA);
	## Luossa-bidin jođđu-buođuin Deanus.

MAP (@HNOUN) TARGET (N Nom) IF (NOT *-1 VFIN)(NOT *-1 ("leapma" Dimin))
	(1 Inf)(NOT *2 VFIN)(NOT 0 Pl LINK 1 V-PL3)
	(NOT *-1 CC BARRIER NOT-NP LINK -1 Nom);
	## Stipeanda čállit prošeaktaohcamiid.

MAP (@HNOUN) TARGET (N Nom) IF (NOT *-1 VFIN OR ("leat" PrfPrc))(1 EOS);
	## Vuosttaš boddu.

MAP (@HNOUN) TARGET (N Sg Nom) IF (*-1 BOS BARRIER NOT-NP)
	(NOT *-1 CC BARRIER NOT-NP LINK -1 Nom)
	(NOT 1 CC LINK *1 Nom BARRIER NOT-NP)
	(*1 Inf BARRIER VERB LINK 1 EOS OR PUNCT);
	## Áigemearri váidalusa buktit.

MAP (@HNOUN) TARGET (N Sg Nom) IF (*-1 BOS BARRIER VFIN)(1 CC)
	(*1 (N Nom) BARRIER NOT-NP LINK *1 EOS BARRIER VFIN);
	## Váidalusa hápmi ja sisdoallu § 7-4.

MAP (@HNOUN) TARGET (N Sg Nom) IF (*-1 CC BARRIER NOT-NP LINK -1 Nom 
	LINK *-1 BOS BARRIER NOT-NP)(1 EOS OR PUNCT);
	## Suhttu ja riidu.

MAP (@VOC) TARGET Nom IF (-1 BOS OR REALCLB)(1 COMMA)(2 Nom)(NOT 2 Interr)
	(NOT 3 COMMA OR CC LINK *1 Nom BARRIER NPNH);
	## Hearrá, mun lean gullan olusiid muitaleamen dán. 

MAP (@VOC) TARGET (N Sg Nom) IF (-1 BOS OR REALCLB)(1 COMMA)
	(*2 (Pron Sg2))(NOT 2 Interr);
	## Hearrá, du ráhkis ustit lea buohcci.
	## Ustiban, in mun daga dutnje vearrut.

MAP (@VOC) TARGET Nom IF (-1 BOS OR REALCLB)(1 COMMA)(2 VFIN);
	## Ustiban, in mun daga dutnje vearrut.

MAP (@VOC) TARGET Nom IF (*-1 COMMA BARRIER CC OR NOT-NP OR CONTRA 
	LINK *-1 (V Sg2) OR (V Du2) OR (V Pl2) OR (Pron Sg2) OR (Pron Du2) OR 
	(Pron Pl2) LINK NOT 0 ("leat"))(1 EOS)
	(NOT 0 Sg LINK *-1 V-SG3 BARRIER VERB);
	## Vuoi du, Betsaida!

MAP (@VOC) TARGET Nom IF (-1 COMMA)(0 Pers)(0 Sg2 OR Du2 Or Pl2)
	(*1 Nom BARRIER NPNH LINK 1 EOS);
	## Gáidet eret mu luhtte, dii verrošeaddjit! 

MAP (@VOC) TARGET (N Nom) IF (-2 BOS)(-1 (Imprt Sg2))(NOT -1 Adv OR Qst)(1 EOS);
	## Gula eadni!

MAP (@VOC) TARGET (N Nom) IF (-3 BOS)(-2 (Imprt Sg2) OR (Sg2 Qst))(NOT -2 Nom)
	(-1 COMMA)(1 COMMA);
	## Buvttes, Máhtte, goikemuoraid!

MAP (@VOC) TARGET (N Nom) IF (-4 BOS)(-3 (V Sg2))(-2 go)(-1 COMMA)(1 COMMA);
	## It go, bártnážan, boađáše fargga ruoktot?

MAP (@CMPND) TARGET Cmpnd;
	## Dát ii guoskka dikšui mii boahtá mánáid- ja nuoraidpsykiatriija 
	## vuollái.

MAP (@PROP>) TARGET (Prop Attr);
	## Rektor Tove Bull álgaga mielde...


# Verb mappings
# -------------
MAP (@+FAUXV @+FMAINV) TARGET VFIN IF (NOT *-1 ("leat") OR INF-VERB OR Neg 
	BARRIER VERB OR CS OR CP)(0 ("orrut"));
	## Sii orro darfegođiid siste.

MAP (@-FAUXV @-FMAINV) TARGET ("orrut");
	## Olbmot eai orron čadnon dušše iežaset mearragáddái bivdit.

MAP (@-FAUXV @-FMAINV) TARGET Inf IF (*-1 INF-VERB BARRIER VFIN)
	(0 AUX-OR-MAIN);
	## Son galgá beassat buktit oaivilis.

MAP (@+FAUXV @+FMAINV) TARGET VFIN IF (NOT *-1 VFIN BARRIER CS OR CP)
	(0 AUX-OR-MAIN);
	## Maid Máret áiggui goarrumis?

MAP (@-FAUXV) TARGET ConNeg IF (*-1 Neg BARRIER VERB)(0 AUX-OR-MAIN)
	(*1 Inf BARRIER VERB OR CS OR CP);
	## Eai guovttis ge nagot loktet dan geađggi. 

MAP (@-FAUXV @-FMAINV) TARGET AUX-OR-MAIN IF (NOT 0 VFIN);
	## In beassan dal deatnojohtima oaidnit.

MAP (@-FMAINV) TARGET AUX-OR-MAIN IF (*-1 ("leat") BARRIER VERB)
	(NOT *1 VERB BARRIER CS);
	## Sámegiela lea ávkkálaš máhttit.

MAP (@-FAUXV) TARGET AUX IF (*-1 Neg BARRIER CS OR CP)(0 ConNeg OR PrfPrc);
	## Eat mii sáhte vuolgit.

MAP (@-FAUXV) TARGET PrfPrc IF (0 ("leapma" Dimin));
	## Máŋgga oktavuođas lea leamaš stuora jearru ja dárbu
	## eamiálbmotdutkamiid fágaidgaskasaš semináraide.

MAP (@-FAUXV) TARGET Inf IF (*-1 INF-VERB BARRIER VFIN OR CLB OR CS OR CP)(0 AUX);
	## Dat áigu leat ruovttus.

MAP (@+FAUXV) TARGET VFIN IF (0 AUX);
	## Don šattat vel joksat Sirpmás gohcciid.

MAP (@-FAUXV) TARGET AUX ;
	## Mun in šaddan vuolgit.

MAP (@+FMAINV) TARGET VFIN-NOT-AUX ;
	## Boađe boahtte vahku.

MAP (@-FMAINV) TARGET PrfPrc IF (*-1 ("leat") BARRIER PrfPrc OR (Actio Ess))
	(1 NP-BORDER);
	## Ja Biret-Elle lea easka skuvllas geargan.

MAP (@-FMAINV) TARGET PrfPrc IF (*-1 ("leat") BARRIER PrfPrc OR (Actio Ess))
	(1 Num);
	## Lean mun gal oaidnán čakčaseavdnjadiin dola rievdamiin čázi alde Deanus.

MAP (@-FMAINV) TARGET PrfPrc IF (*-1 VFIN BARRIER REALCLB OR go LINK 0C VERB)
	(NOT *-1 PrfPrc BARRIER VERB OR N);
	## Ii dal láhppán gal leat.
	## Ii son liikon dohko.

MAP (@-FMAINV) TARGET PrfPrc IF (*-1 (Actio Nom) BARRIER NOT-ADV-PCLE LINK
	*-1 ("leat" Sg3) BARRIER NPNHA LINK NOT *-1 Nom BARRIER CS);
	## Leago miehtan vižžojuvvon divššohasas...

MAP (@-FMAINV) TARGET PrfPrc IF (*-1 ("leat") BARRIER CS)(NOT 0 Pass);
	## Dalle livčče njuolggadusaid giehtaguššan issorasat geahpidan 
	## guolástanvuoigaduvvomiid logu.

MAP (@-FMAINV) TARGET PrfPrc IF (-2 PrfPrc)(-1 CC);
	## Artihkal lea sámegillii jorgaluvvon ja almmuhuvvon Romssa universitehta 
	## Juridihka fakultehta čálaráiddus.

MAP (@-FMAINV) TARGET PrfPrc IF (*-1 ("dušše") BARRIER VERB LINK 
	*-1 ("leat") BARRIER VERB);
	## Čuollun sáhttá leat dušše okta fierbmi gurgaluvvon.

 # Since "dušše" is also VFIN, we need a special rule to find "leat" across it.

MAP (@-FMAINV) TARGET PrfPrc IF (*-1 ("go" Pcle) BARRIER VERB OR CS OR CP LINK
	-1 ("leat") LINK 0 VFIN-NOT-IMP);
	## Leat go don máistán dien bierggus?

MAP (@-FMAINV) TARGET PrfPrc IF (-1 ("leat"));
	## Leatgo dahkkojuvvon gustojeaddji vuoigatvuođat nugo...

MAP (@-FMAINV) TARGET Inf;
	## Manaigo Biret-Elle viežžat boastta?

MAP (@-FMAINV) TARGET ConNeg ;
	## Iigo dutnje šat boađe áviisa?

MAP (@-FMAINV) TARGET (V Actio Ess);
	## Oađidettiin gullen olbmo boahtimin.

MAP (@-FMAINV) TARGET (V Actio Gen) IF (1 ("dihte") OR ("dihtii"));
	## Boađán du lusa čállin dihte dan reivve.

MAP (@-FMAINV) TARGET (V Actio Gen) IF (1 CC)(2 (V Actio Gen))
	(3 ("dihte") OR ("dihtii"));
	## ...seailluhan, nannen ja ovddidan dihte sámi servodaga.

MAP (@-FMAINV) TARGET (V Actio Gen) IF (1 COMMA)(2 (V Actio Gen))(3 CC)
	(4 (V Actio Gen))(5 ("dihte") OR ("dihtii"));
	## ..seailluhan, nannen ja ovddidan dihte sámi servodaga.

MAP (@-FMAINV) TARGET (V Actio Loc) IF (*-1 V-NOT-AUX BARRIER NOT-ADV-PCLE);
	## Juhán heittii suhkamis.

MAP (@-FMAINV) TARGET (V Actio Loc) IF (*-1 NP-HEAD-ACC BARRIER VERB 
	LINK *-1 V-NOT-AUX OR ("jaska") BARRIER NPNHA);
	## Dat galgá hehttet guoli jorggiheames vulos.

MAP (@-FMAINV) TARGET Sup;
	## Áddjá bázii mállása vuoššažit. 


# Complements of particles in comparison
# --------------------------------------

MAP (@PCLE-COMPL) TARGET Nom IF (*-1 ("go") BARRIER NOT-NP LINK NOT -1 VFIN)
	(1 EOS);
	## Dat galgá čuovvoluvvot nu guhkás go vejolaš.

MAP (@PCLE-COMPL) TARGET Nom IF (*-1 ("go") BARRIER NOT-NP LINK NOT -1 VFIN)
	(1 PUNCT)(2 EOS);
	## Ovdabarggut állanit seamma beallái go "láhkateavstta lunddolaš lohkan".

MAP (@PCLE-COMPL) TARGET Nom IF (*-1 ("go") BARRIER NOT-NP LINK NOT -1 VFIN)
	(1 CC)(NOT *1 VFIN-NOT-IMP);
	## Sin mearridanorgánain berre mearridanváldi leat nu viiddis go vejolaš 
	## ja nu čadni go vejolaš.

MAP (@PCLE-COMPL) TARGET Nom IF (*-1 ("go") BARRIER NOT-NP OR Nom 
	LINK NOT -1 VFIN OR BOS)(1 CLB LINK NOT *1 VFIN BARRIER CC);
	##  Muhto buođđogeažit ja giđđabuođut ráhkaduvvojit muđui seammá 
	## vugiid mielde go geassebuođut, ja danne heive buođđun birra 
	## obbalohkái juo muitaluvvot.

MAP (@PCLE-COMPL) TARGET Nom IF (*-1 ("go") BARRIER NOT-NP OR Nom 
	LINK *-1 Nom BARRIER CS OR CONTRA)(NOT *-1 go BARRIER NOT-NP 
	LINK -1 VFIN LINK *-1 BOS OR CS BARRIER NOT-CC)(1 EOS OR CLB)
	(NOT 1 CLB LINK *1 VFIN BARRIER CC)
	(NOT 1 CLB LINK *1 NP-HEAD-NOM BARRIER NPNH)
	(NOT 2 Interr LINK *1 VFIN LINK *1 VFIN BARRIER CS); 
	## ...muhto galjebut dat lea go guv-čalmmit.

MAP (@PCLE-COMPL) TARGET Nom IF (-1 ("dego"))
	(NOT *1 VFIN-NOT-IMP BARRIER CS OR CLB);
	## Sii leat dego eŋgelat almmis.

MAP (@PCLE-COMPL) TARGET Nom IF (*-1 ("dego") BARRIER NOT-NP)(1 EOS OR CLB);
	## Son oahppaladdá min allagasas dego beaivváža badjáneapmi.

MAP (@PCLE-COMPL) TARGET (N Nom) IF (*-1 ("go") BARRIER NPNH LINK -1 A)
	(NOT *1 VFIN BARRIER NOT-ADV-PCLE);
	## Máhttájeaddji ii leat stuorit go oahpaheaddjis.

MAP (@PCLE-COMPL) TARGET (Pron Nom) IF (*-1 ("go") BARRIER NPNH LINK -1 A)
	(NOT *1 VFIN BARRIER NOT-ADV-PCLE);
	## Dalle dat viežžá čieža eará vuoiŋŋa vel, bahábuid go son ieš. 

MAP (@PCLE-COMPL) TARGET Nom IF (*-1 ("dego") BARRIER NOT-NP)(1 (Pron Interr))
	(*2 VFIN LINK NOT *1 VFIN);
	## Dat lea dego olmmái gii manai olgoriikii.

MAP (@PCLE-COMPL) TARGET Nom IF (*-1 ("dego") BARRIER NOT-NP)
	(NOT 0 Attr LINK *1 Nom BARRIER NPNH)(NOT *1 VFIN-NOT-IMP BARRIER CS OR CLB)
	(NOT 1 ("go") OR ("dego"));
	## Dego njamaheaddji eadni dikšumin mánáidis...

MAP (@PCLE-COMPL) TARGET Nom IF (*-1 ("dego") BARRIER NPNH)(0 Sg)(1 CC)
	(*2 (Sg Nom) OR (Sg3 Nom) BARRIER NPNH LINK *1 V-SG3 BARRIER NOT-ADV-PCLE);
	## Dat guhte ii biso mu siste, bálkestuvvo olggos dego oaksi ja son goldná.

MAP (@PCLE-COMPL) TARGET Nom IF (-2 (@PCLE-COMPL))(-1 COMMA)(1 COMMA OR CC)
	(2 Nom);
	## Seamma gusto uhcit fuđđožiidda nu go neahti, čeavrris, mievri ja rieban.

MAP (@PCLE-COMPL) TARGET Nom IF (*-1 CC BARRIER NPNH LINK -1 (@PCLE-COMPL))
	(NOT *1 VFIN OR go OR ("dego") BARRIER CS);
	## Jesus lea gievrrat go Sáhtán ja bahá vuoiŋŋat. 

MAP (@PCLE-COMPL) TARGET Acc IF (-2 Acc)(-1 ("dego"));
	## Mun vuolggahan din dego sávzzaid gumppiid gaskii.


# Other special nominative mappings
# ---------------------------------

ADD (@ADVL) TARGET Nom IF (0 ("diibmu") OR ("dbm"))(1 Num);
	## Mun boađán diibmu vihtta.

MAP (@ADVL) TARGET Nom IF (-2 Num)(-1 ("ruvdno"))(NOT 0 ("velggolaš"));
	## Sallidat mákset 10 ruvnno bihttá. 


# Arguments and secondary predicates
# ----------------------------------

MAP (@OPRED) TARGET ("veara") IF (*-1 NP-HEAD-ACC BARRIER NOT-NP 
	LINK *-1 OPRED-VERB BARRIER NOT-NP);
	## Muhto mun in ane ealliman mange vearan.

MAP (@SPRED @OPRED) TARGET ("veara" Ess);
	## Dan birra in gávnnat vearan cealkit.

MAP (@SPRED @ADVL) TARGET ("veara"); 
	## Dat ledje vihttalogiduhát silbaruđa veara.			# @SPRED
	## Ohcamat filbmaulbmiliidda leat lassánan mearkkašahtti veara. # @ADVL

ADD (@ADVL) TARGET ("mearkkašan#veara");
	## Buhttenrievttálaččat ii nuppástuvvon mearkkašanveara...

ADD (@ADVL) TARGET ("namuhan#veara");
	##

MAP (@HNOUN) TARGET (Num Nom) IF (-1 BOS)(1 EOS);

MAP (@SUBJ @SPRED) TARGET Nom IF (NOT 0 ("diibmu") OR ("dbm") LINK *1 Num BARRIER NP-BORDER)
	(NOT 0 Num LINK *-1 ("diibmu") OR ("dbm") BARRIER NP-BORDER) (NOT -1 BOS)(1 EOS);
	## Manne ii leat áviisa boahtán?

MAP (@SPRED) TARGET PrfPrc IF (*-1 ("orrut") BARRIER NOT-ADV-PCLE)(1 NP-BORDER);
	## Olbmot eai orron čadnon dušše iežaset mearragáddái.

MAP (@SPRED @OPRED) TARGET PrsPrc IF (-1 ("nu"))(1 ("go" Pcle));
	## Mearridanváldi berre leat nu čadni go vejolaš.

MAP (@SUBJ) TARGET Indef IF (NOT *-1 Nom BARRIER NOT-ADV-PCLE)
	(NOT -1 ("mii" Interr Sg Gen))(NOT 0 Acc OR ADVLCASE)(1 V-SG3)
	(NOT *2 Nom BARRIER CS LINK NOT 0 PrfPrc)
	(NOT *-1 V-TRANS-ACT BARRIER CS)(NOT *1 V-TRANS-ACT BARRIER CS);
	## ...ovdal go buot lea dáhpáhuvvan.

MAP (@SUBJ) TARGET ("buot") IF (*1 V-SG3 BARRIER NOT-ADV-PCLE)
	(NOT *1 V-TRANS-ACT BARRIER CS OR CP)(NOT 1 ("buohkanassii"));
	## Buot lei varran.

MAP (@SUBJ) TARGET ("buot") IF (-1 COPULAS);
	## Danin lea buot nu olu álkit inuihtaide.

MAP (@OBJ @OPRED) TARGET Acc IF (NOT 0 TIME OR ROUTE);
	## Manaigo Biret-Elle viežžat boastta? 

ADD (@OBJ) TARGET Acc IF (*-1 V-TRANS-ACT BARRIER VERB OR CC OR CLB OR CS OR CP)
	(0 TIME OR ROUTE);
	## Stuorábu ráđđet beaivvi ja uhcibu ráđđet ija.

ADD (@OBJ) TARGET Acc IF (*-2 V-TRANS-ACT BARRIER VERB OR CC OR CLB OR CS OR CP)
	(-1 Indef)(0 TIME OR ROUTE);
	## Dat gáibida ollu áiggi.

ADD (@OBJ) TARGET Acc IF (0 TIME OR ROUTE)
	(*1 V-TRANS-ACT BARRIER NOT-ADV-PCLE LINK 0 VFIN OR Inf OR PrfPrc);
	## ...maŋŋá go lea 16 jagi deavdán.

MAP (@OBJ) TARGET Indef IF (*-1 V-TRANS-ACT BARRIER N OR CLB)
	(NOT 0 Gen OR ADVLCASE OR ("vaikko"))
	(NOT *-1 V-SG3 BARRIER VFIN)(NOT *1 NP-HEAD BARRIER NOT-NP); 
	## Go bohte gávpogii, de muitaledje buot.

MAP (@OBJ) TARGET ("ollu" Indef) IF (*-1 V-TRANS-ACT BARRIER NPNH 
	LINK *-1 NP-HEAD-NOM BARRIER CS OR CP);
	## Meardebuođđobivdu gal ii mearkkaš ollu álbmoga ealáhussii.

MAP (@OBJ) TARGET Indef IF (*-1 V-TRANS-ACT BARRIER CS)(NOT 0 ADVLCASE)
	(NOT *1 (N Acc) BARRIER NOT-ADJ);
	## Mun anán buot vahágin.

MAP (@OBJ) TARGET ("buot") IF (*-1 (@SUBJ) BARRIER CC OR CS OR CP)
	(*1 V-TRANS-ACT BARRIER NOT-ADV-PCLE);
	## It don galgga buot čuohpadit.

MAP (@SPRED) TARGET (N Ess) IF (*-1 BOS OR CS OR CP BARRIER NOT-NP)
	(*1 COPULAS BARRIER NOT-ADV-PCLE LINK 1 Inf);
	## ...man ulbmilin lei lohkat ja juogustit bohccuid. 

MAP (@SPRED) TARGET (N Ess) IF (*-1 BOS OR CS OR CP BARRIER NOT-NP)
	(*1 COPULAS BARRIER NOT-ADV-PCLE LINK *1 (N Nom) BARRIER NOT-NP);
	## Riektemetoda vuođđun lea našunálastáhtalaš riekteárbevieruid áddejupmi.

MAP (@SPRED) TARGET ALU ;
	## Dábálaččat leat joddut sullii ovtta guhku.

MAP (@SPRED) TARGET Gen IF (*-1 COPULAS BARRIER NOT-ADV-PCLE)(1 EOS OR CS);
	## Dat lea mu.

MAP (@SPRED) TARGET Gen IF (*-1 CC BARRIER NPNH LINK *-1 (@SPRED) BARRIER WORD)
	(1 EOS OR CS);
	## Áhká rumaš ii leat su iežas oapmi, muhto boatnjá.

MAP (@SPRED) TARGET (Interr Gen) IF (NOT 0 ("mii"))(NOT *1 A OR N BARRIER NPNH)
	(NOT *1 V-NOT-AUX BARRIER CS OR CP);
	## Gean dat lea?

MAP (@SPRED) TARGET Gen IF (NOT 0 TIME)(1 Pers)(2 COPULAS)
	(NOT *3 V-NOT-AUX BARRIER CS OR CP);
	## Dan guovttos dat leat.

MAP (@SPRED) TARGET (Pass PrfPrc) IF (-1 BOS OR REALCLB)(1 ("leat"))
	(*2 NP-HEAD-NOM BARRIER NOT-NP);
	## Buressivdniduvvon lehkos son guhte boahtá Hearránammii.

MAP (@SPRED) TARGET Indef IF (*-1 COPULAS BARRIER VERB LINK *-1 (@SUBJ) BARRIER 
	CS OR CP)(NOT 0 ADVLCASE)(NOT *1 VERB BARRIER CS);
	## Dakkár dálolaččat leat nu ollu dál Deanus, ahte...


# Adverbial mappings
# ------------------

MAP (@ADVL) TARGET Adv ;
	## Mun máksen 100 ruvnno duvle.

MAP (@ADVL) TARGET Po ;
	## Daid ovddas gal in máksán maidege.

MAP (@ADVL) TARGET Pr ;
	## Jus divššohas lea badjel 16 jagi boaris...

MAP (@ADVL) TARGET (VAbess) ;
	## Mus báhce moadde girjji logakeahttá.

MAP (@ADVL) TARGET VGen ;
	## Máhtte manai čuoigga skuvlii.

MAP (@ADVL) TARGET Ger ;
	## Biigá lei bassaladdame báhpa boađedettiin.

MAP (@ADVL) TARGET Ill ;
	## Cokka láibbiid sehkkii.
	
MAP (@ADVL) TARGET Loc ;
	## Maid dat Kárášjogas bargá?

MAP (@ADVL) TARGET Com ;
	## Dieđut galget meannuduvvot várrugasvuođain.

MAP (@ADVL) TARGET Ess IF (0 ("bievla") OR ("buolaš") OR ("čuoika") OR 
	("dálki") OR ("dulvi") OR ("seavdnjat"));
 # and more nouns
	## Ii leat bievlan vejolaš fievrridit gálvvu gerresiiguin. 

MAP (@ADVL) TARGET (N Ess) IF (0 ("bassi"));
	## Bassin it galggaše bargat.

MAP (@SPRED @OPRED) TARGET (A Ess) IF (0 ("bassi"));
	## Son gohčoduvvo bassin ja Ipmila Bárdnin.
 # "Bassin" can be predicative or adverbial. Therefore it is treated separately.

MAP (@SPRED @OPRED) TARGET Ess ;
	## Dat lea oahpaheaddjin doppe.

MAP (@ADVL) TARGET Acc IF (0 TIME OR ROUTE);
	## Beaivvi mun čohkkájin doppe.

MAP (@ADVL) TARGET Gen IF (1 NP-BORDER)(NOT 1 Adv);
	## Áddjá lei soggái čalmmiid.


# Default mapping
# ---------------

MAP (@X) TARGET WORD ;



# ==================== #
# ==================== #
# Disambiguation rules #
# ==================== #
# ==================== #

# =================================== #
# One-cohort disambiguation - cycle 0 #
# =================================== #
             CONSTRAINTS              #
# =================================== #

# Lexicalised names
# -----------------

REMOVE ACR IF (0 Prop) ;
    ## NRK lea čujuhan iežas áigut leat dehálaš seailluhit sámi servodaga.

# Lexicalised derivations
# -----------------------
# SELECT instead of REMOVE if one wants to investigate word structure.

REMOVE N-DER IF (0 LEX-N LINK NOT 0 Ess);
       ## Dat lea oahpaheaddjin doppe.
 # Since Actio was included in the N-DER set, this rule now removes many Actios.
 # This may be unfortunate. If so, the remedy is to remove Actio from the N-DER set.

REMOVE N-DER IF (0 N-DER LINK 0 Ess)(0 LEX-N LINK 0 Ess)(NOT 0 Actio);
	## Boarrásis leai dat vel eanaš ealáhussan ge.
 # Removes N-DER if O is also a lexical noun, and both nouns are essive.

REMOVE A-DER IF (0 LEX-A);
	## Dábálaš mearrádusat.

REMOVE V-DER IF (0 LEX-V)(NOT 0 (eaddji)); 
	## Maid don doaivvut?

 # The negative condition is to avoid ending up with only a PrfPrc reading 
 # e.g. for "registrerejeaddji" in 
 # "… dan láhkai ahte dieđuid registrerejeaddji, journálaovddasvástideaddji ja
 # bearráigeahččoeiseválddit sáhttet viežžat dieđuid dárbbu mield".
 

# Lexicalised adjectives
# ----------------------
REMOVE VGen IF (0 A);
	## Na varas guolli dal lea buorre.

REMOVE PrsPrc IF (0 A);
	## Áššis biddjui mearrideaddji deaddu dasa ahte...  

REMOVE (V* A) IF (0 LEX-A);
	## Boradeapmi basakeahtes gieđaiguin ii daga olbmo buhtismeahttumin.

SELECT A IF (0 ("sivaheapmi"));
	## Ehpet livčče dubmen sivahemiid.
 # This rule removes the reading sivahit+V+eapmi+N+Actio.
 # A more general rule (SELECT A IF (0 (heapmi))(0 Actio) went wrong.


# Adjectives or nouns, not adverbs
# --------------------------------
 # Words that probably are not adverbs when they appear in the -aččat form.
 # Words that are most likely to be nouns are picked out by a later rule. 
 # This rule deals with those -aččat words that could be nouns or adjectives 
 # but not adverbs. 

REMOVE Adv IF (0 ("ášše#oassi" laš) OR ("bosnialaš") OR ("čuovvovaš") OR 
	("dánskalaš") OR ("Davvinorga" laš) OR ("davvi#norga" laš) OR 
	("davviriikkalaš") OR ("dehálaš") OR ("duiskkalaš") OR ("eahpeduhtavaš") OR 
	("egyptalaš") OR ("gávpi" laš) OR ("gávppašan#bealle" laš) OR 
	("geavahan#riekti" laš) OR ("greikalaš") OR ("gullevaš") OR ("hollándalaš") OR 
	("julev#sápmelaš") OR ("juvddálaš") OR ("kretalaš") OR ("lulli-#norga" laš) OR 
	("máksu" laš) OR ("norgalaš") OR ("nuortalaš") OR ("oarje#sápmelaš") OR 
	("ohcejohkalaš") OR ("palestinalaš") OR ("romalaš") OR ("sadjásaš") OR 
	("sápmelaš") OR ("sogalaš") OR ("sorjavaš") OR  ("sullasaš") OR ("suddu" laš) 
	OR ("vepselaš"));


# Lexicalised adverbs
# -------------------
REMOVE (A* Adv) IF (0 LEX-ADV) ;
	## Son dábálaččat ráhkkana dasa dálvvi mielde. 

REMOVE Foc IF (0 ("dieđusge"));
	## Romssa universitehta áigu dieđusge dán geahččalit áimmahuššat.

SELECT Adv IF (0 ("eahpitkeahttá"));


# Adverbs, not nouns or adjectives
# --------------------------------
 # Words that probably are adverbs when they appear in the -aččat form.
 # Since only the -aččat form gets the Adv tag, the rule will hit this form.

SELECT Adv IF (0 ("absoluhtalaš") OR ("agálaččat") OR ("aitosaččat") OR 
	("aktiiva" laš) OR ("aktiivva" laš) OR ("almmolaš") OR ("analogalaš") OR 
	("automáhtalaš") OR (álgoálgosaš) OR ("álgovuolggalaš") OR ("árbevirolaš") OR 
	("beaivválaččat") OR ("biologalaš") OR ("buhtten#riekti" laš) OR 
	("bustávva" laš) OR 
	("čálalaččat") OR ("čearddalaš") OR ("dábálaččat") OR ("duođalaččat") OR 
	("dutkan#politihkka" laš) OR ("eahpedábálaš") OR ("eahpeformálalaš") OR 
	("eahpekritihkalaš") OR ("eahpevirggálaš") OR ("eaktodáhtolaččat") OR 
	("ekologalaš") OR ("ekologálaš") OR ("ekonomálaš") OR ("eksplisihtalaš") OR 
	("elektrovdna" laš) OR ("etihkka" laš) OR ("etymologalaš") OR ("fágalaš") OR
	 ("fonehtalaš") OR ("fonologalaš") OR ("fonotávttalaš") OR ("formálalaš") OR
	 ("fysiologa" laš) OR ("gaskamearálaš") OR ("geavatlaččat") OR 
	("gealbu" laš) OR ("geografa" laš) OR ("geográfa" laš) OR ("giella" laš) OR 
	("girko#riekti" laš) OR ("globálalaččat") OR ("gorri" laš) OR 
	("govttolaš") OR ("hálddahuslaš") OR ("historjjálaš") OR ("ideologalaš") 
	OR ("iešheanálaš") OR ("implisihtalaččat") OR ("jahkásaččat") OR 
	("jeavddalaččat") OR ("jietnadat#oahppa" laš) OR ("juridihkka" laš) OR 
	("konkrehtalaš") OR ("kritihkka" laš) OR ("kronologalaš") OR 
	("kultur#historjá" laš) OR ("kultur#politihkka" laš) OR ("kultuvra" laš) OR 
	("logihkalaš") OR ("luohtu" laš) OR ("máŋgga#fága" laš) OR ("mekánalaš") OR 
	("metoda" laš) OR ("moralalaš") OR ("morálalaš") OR ("morfologalaš") OR 
	("našuvnnalaš") OR ("negatiivalaš") OR ("negatiivvalaš") OR 
	("njálmmálaččat") OR ("njuolgguslaččat") OR ("oaive#ášši" laš) OR 
	("obbalaš") OR ("oktasaččat") OR ("oktilaš") OR ("ollislaččat") OR 
	("oppalaččat") OR ("optimalalaččat") OR ("ovttabealálaš") OR 
	("ovttajienalaš") OR ("ovttamielalaš") OR ("pedagogalaš") OR 
	("politihkka" laš) OR ("praktihkka" laš) OR ("prinsihppa" laš) OR 
	("priváhta#riekti" laš) OR ("profehtalaš") OR ("psykologalaš") OR 
	("ráfálaččat") OR ("reála#politihkka" laš) OR ("riekte#historjá" laš) OR 
	("riekti" laš) OR ("riikkaidgaskasaš") OR ("sámegielalaš") OR 
	("riikka#goddi" laš) OR ("ruhta" laš) OR ("rumašlaš") OR 
	("sámepolitihkalaš") OR ("sekundáralaččat") OR ("semantihkalaš") OR 
	("servodat#ávki" laš) OR ("siviila#riekti" laš) OR ("sosiálalaš") OR 
	("stáhta#politihkka" laš) OR ("strategalaš") OR ("suorge#politihkka" laš) OR 
	("teknihkka" laš) OR ("teorehtalaš") OR ("topografa" laš) OR 
	("váldo#ášši" laš) OR ("virgi" laš) OR ("vierru" laš));

	## Oppalaččat daid gažaldagaid birra mat gieđáhallojuvvojit NAČ 1993:34,...


REMOVE (N Dimin) IF (0 Adv OR A);
	## Olu mearrádusat gártet iešguđet doaibmasurggiid gaskasaččat.

REMOVE (N* Dimin) IF (0 Adv OR A);
	## Mun vuolggán ihttážis mánnu.


# Lexical N versus Actio
# ----------------------

REMOVE (N Actio) IF (0 ("eallin") OR ("váldo#giella"));
	## Dat geaidnu lea seaggi mii doallu eallimii.
	## Ruovttugiella mearkkaša giela, mii lea leamaš váldogiellan ruovttus.
 # other nouns?


# Nouns, not adverbs or adjectives 
# --------------------------------
 # Words of which both the singular (-laš) and the plural (-laččat)
 # are probably nouns:

SELECT N IF (0 ("Álttá#sápmelaš") OR ("Anársápmelaš") OR ("badje#sápmelaš") 
	OR ("boazo#sápmelaš") OR ("dálolaš") OR ("Deanu#sápmelaš") OR 
	("duottar#sápmelaš") OR ("eana#láigolaš") OR ("Guovdageaidnu" laš) OR 
	("johtti#sápmelaš") OR ("láddelaš") OR ("mearra#sápmelaš") OR 
	("Ohcejoga-#sápmelaš") OR ("Porsáŋggu#sápmelaš") OR ("riddosápmelaš") OR 
	("Ruošša#sápmelaš") OR ("samarialaš") OR ("skoalta#sápmelaš") OR 
	("Várjjat-#sápmelaš") OR ("Várjjat#sápmelaš") OR ("várre#sápmelaš") 
	OR ("vuotna#sápmelaš"));

	## ...gos boazosápmelaččat sáhttet jođihit boazodoaluset.


 # Words which are probably nouns when they appear in the plural form (-laččat),
 # but could be adjectives when they appear in the singular form. The rule is 
 # designed to hit the (Pl Nom) forms.

SELECT (N Pl Nom) IF (0 ("egyptalaš") OR ("greikalaš") OR ("julev#sápmelaš") OR 
	("juvddálaš") OR ("kretalaš") OR ("nuortalaš") OR ("oarje#sápmelaš") OR 
	("romalaš") OR ("sápmelaš") OR ("sogalaš"));

	## Sápmelaččat orro maid skandinávalaš njárgga siskkimus guovlluin.
	## ...ja doppe ásse dušše nuortalaččat. 


SELECT (N Pl Nom) IF (0 (eaddji));
	## Dat geađgi man huksejeaddjit hilgo,...


# Nouns, not verbs
# ----------------

REMOVE VAbess IF (0 ("orohat"));
	## Guovlu lea orohaga olggobealde.


# Not noun
# --------
REMOVE ("duđđa" N Pl);
	## Duđat go mu čállimii?


# Temporal expressions with locative case
# ---------------------------------------
SELECT (N Sg Loc) IF (0 MANNU); # To avoid "skábma#mánus" N Sg Nom.
	## Dat hávdáduvvojedje Álahedjui skábmamánus 1997.


# Adposition + Px
# ---------------
SELECT Adp IF (0 ("gaska" N Ess)); ## Sii árvališgohte gaskaneaset.


# Some verb rules
# ---------------

REMOVE Imprt IF (0 Qst);
REMOVE Imprt IF (0C Pass);
	## Bijatgo don sohkkara dedjii?
	## Jos fávleoalggis lea guolli, loktestuvvo dat dušše čázeoaivái.

REMOVE Imprt IF (0  ("berret") OR ("dáidit") OR ("fertet") OR ("galgat") OR 
	("lávet") OR ("sáhttit"));
	## Báldáhan ferte diŋgot.
	## Lága mearrádusat galget váikkuhit luohttevašvuođa.
 # And more verbs, but not the whole AUX set (not "leat", for example).

REMOVE Inf IF (0 Qst);
	## Leatgo addojuvvon neavvagat ja dieđut divššohassii,...

REMOVE Foc IF (0 Actio OR PrfPrc OR VGen);
	## Studeanttat geat leat čađahan váldofágaeksámena...



# ===================================== #
# Local disambiguation - cycles 1 and 2 #
# ===================================== #

# --------------------------------------------------------- #
# Cycle 1a: cases that will be needed in careful mode later #
# --------------------------------------------------------- #
                             CONSTRAInNTS                    #
# --------------------------------------------------------- #
# The idea with cycle 1a is to disambiguate cases that are judged as easier and
# more secure than later cases, and that at the same may be used as context
# for other rules in later cycles.


# Personal pronouns
# -----------------

# Mon ( = Mo N Prop Ess)
# - - - - - - - - - - -

# Moai ( = Moa N Prop Ill)
# - - - - - - - - - - - - 
SELECT MOAI IF ( *1 V-DU1);     ## Moai čilge dan ášši.
SELECT MOAI IF (*-1 V-DU1);     ## Čilgego moai dan?


# mii ( = Interr)
# - - - - - - - -

# Left conditions
SELECT Pers IF (-3 BOS OR CS)(-2 V-PL1)(-1 go)(0 MII-PERS); 
SELECT Pers IF (-1 (V Ind Prs Pl1 Clt))(0 MII-PERS);
	## Eat go mii leat profehtastallan du nammii?
	##

SELECT Pers IF (-1 (V Neg Pl1))(0 MII-PERS);     
SELECT Pers IF (*-1 V-PL1 BARRIER NOT-ADV OR ("seamma"))(0 MII-PERS)(NOT 1 V-SG3);
	## Eat mii sáhte vuolgit.
	## Goas oinniimet mii du nelgon dahje goikan?

 # The last condition added because of the example below. Attempts to formulate 
 # a negative condition on V-PL1 instead (e.g. LINK NOT 0 A) did not work.
	## Dat geaidnu lea govdat mii doalvu gáđohussii. 

# Right conditions
SELECT Pers IF (0 MII-PERS)(1C Interr);
SELECT Pers IF (0 MII-PERS)(1 COMMA)(2C Interr);
SELECT Pers IF (0 MII-PERS)(*1 V-PL1 BARRIER VFIN OR CP OR CS LINK NOT 0 Adv);
	##
	## .. de mii, guđet leat eatnagat, leat okta rumaš.
	## Dasgo mii oinniimet su násti badjáneamen.

SELECT Pers IF (0 MII-PERS)(1 CC)(2 (N Nom));
	## Mii ja farisealaččat atnit borakeahttáivuođa.


# mis and sis
# - - - - - -
  # mun Pron Pers Pl1 Loc = mis Adv, corr. for son Pron Pers Pl3 Nom = sis Adv
  # This one is hard to resolve, as they have the same distribution.


# don ( = Pron Dem Sg Acc/Gen)
# - - - - - - - - - - - - - -
SELECT Pers IF (*-1 (V Sg2) BARRIER CS LINK NOT 0 Adv)(0 DON); 
SELECT Pers IF (0 DON)(*1 V-SG2 BARRIER PUNCT);  
	## Liikotgo don Abbai?
	## Don it leat nama čállán.


# son ( = Pcle) 
# - - - - - - -
SELECT Pers IF (*-1 V-SG3 BARRIER CLB OR CS OR CP)(NOT -1 Qst or ("go"))
	(0 ("son" Pron Pers Sg3 Nom)); 
	## Ii son liikon dohko.

SELECT Pers IF (0 ("son" Pron Pers Sg3 Nom))(1 (Pron Interr Sg));
	## Son gii addá veahki galgá mearridit.

SELECT Pcle IF (NOT -2 CLB)(-1 Qst or ("go"))(0 ("son"))(1 Nom)
	(NOT 1 Refl OR Inf OR Cmpnd);
	## Lea go son Máret boahtán?

SELECT (Pron Pers Sg3 Nom) IF (0 ("son"))(*1 V-SG3 BARRIER N);  
	## Son fertii vuolgit. 

  
# sutnje ( = forms of the verb "suotnjat")
# - - - - - - - - - - - - - - - - - - - -
SELECT (Pron Ill) IF (0 ("son"))(1 (Pron Interr Sg)); 
SELECT (Pron Ill) IF (*-1 (V Ind)) (0 ("son"));  # Barrier?
	## Dearvvašvuođabálvalus galgá addit sutnje gii ohca...
	## Son galgá addit sutnje dárbbašlaš dieđuid.


# danin (Pron Ess OR Adv)
# - - - - - - - - - - - -

SELECT (Pron Ess) IF (*-1 COPULAS BARRIER NOT-ADV-PCLE)(0 ("danin"))(1 EOS OR CLB);
	## ...jos oapmi fierpmit leat danin,...
  

# dat ( Pron Pers, Pron Dem, Pcle)
# - - - - - - - - - - - - - - - -
SELECT Pers IF (-1 Nom)(NOT -1 Pers)(0 ("dat" Nom))(*1 ("leat") BARRIER NOT-ADV-PCLE 
	LINK 1 CLB OR EOS);
	## Hei, soiddas dat lea, máistte don dan gáfe!
	## Gean mearkka bohccot dat ledje?

SELECT Pcle IF (-1 Nom)(NOT -1 A OR Interr OR PrfPrc)(0 ("dat" Nom))
	(NOT *1 N BARRIER NP-BORDER);
	## Máret dat boahtá.
	## Mun dat lean.

SELECT Pcle IF (NOT *-1 (V Sg3) OR (V Du3) OR (V Pl3) BARRIER CC OR CS OR CP)
	(NOT -1 BOS OR CS)(0 ("dat" Nom))(NOT *1 N BARRIER NOT-NP)
	(NOT *1 (V Sg3) OR (V Du3) OR (V Pl3) BARRIER CC OR CS OR CP);
	## Na vuolggán dat gal mun ge.

SELECT Pers IF (0 ("dat"))(NOT 0 ("dasgo") OR ("danin") OR ("dainna"))
	(NOT 1 A OR Num OR N OR Indef OR Attr)(NOT 1 PrfPrc LINK *1 N BARRIER NPNH)
	(NOT 0 Nom LINK 1 ("gal") OR Pcle LINK 1 (N Nom))
	(NOT 1 NP-HEAD-GEN LINK 1 (Actio Gen) LINK 1 N);
	## Dá dat leat.
	## Moai soabaime dasa.
 

# dasa ( = forms of the verb "dassat")
# - - - - - - - - - - - - - - - - - - 
REMOVE VGen IF (0 Pron);
	## Na muhto don it leat dasa nama čállán! 
	## Dat gal lei beare unnán.

# Earlier specific rules, to be reactivated if the previous one is too strong.

#SELECT Dem IF (0 ("dat" Pron Dem Sg Ill))(1 VFIN); ## Dasa lea guhkes historjá.
#SELECT Dem IF (0 ("dat" Pron Dem Sg Ill))(1 ("lassi" N Ess)); ## Dasa lassin.
#SELECT Ill IF (-1 ("sivva"))(0 ("dat")); ## Sivva dasa lea, ahte ...
#SELECT Ill IF (-1 PrfPrc)(0 ("dat")); ## Sii leat čujuhan dasa vuosttas jagi.


SELECT Ill IF (0 ("dat"))(1 ("lassi" Ess));
	## Dasa lassin buvttán dás lasseargumenttaid.  


# dáidda (N Sg Nom or Pron Dem Pl Ill)
# - - - - - - - - - - - - - - - - - - -
SELECT Ill IF (0 ("dáidda"))(*1 (N Pl Ill) BARRIER NPNH);
	## ...ja dáidda gielaide sáhttá čállit journálaid. 

SELECT Ill IF (*-1 ILL-VERB BARRIER NOT-ADV-PCLE)(0 ("dáidda"));
	## ...mat gullet dáidda 1972-konvenšuvnna mielde.

REMOVE Nom IF (-1 NP-HEAD-NOM)(0 ("dáidda"));
	## Sus lea álgovuorus dušše guohtunvuoigatvuohta dáidda. 

REMOVE Nom IF (*-1 V-TRANS-ACT BARRIER CS OR CP OR VFIN)(0 ("dáidda"));
	## ...lea čađahan iskamiid muhtun orohagain ja ávžžuhan 
	## boazologu dáidda.

SELECT Ill IF (-1 ("lassi"))(0 ("dáidda"));
	## Lassin dáidda lea Sámi vuoigatvuođalávdegotti ovdaolmmoš...

SELECT Ill IF (0 ("dáidda"))(*1 ("bidjat") OR ("lasihit") BARRIER V-NOT-AUX); 
	## Ferte mearridit makkár deattu dáidda galgá bidjat.


# Other homonymies linked to personal pronouns, with non-obvious answers.
  # Munin = Pron Pers Sg1 Ess = Ess of N Prop Muni (!)
  # dudnon = Pron Per Du2 Ess = dudnu N Sg ... PxSg1 (covered by Px rule)
  # Sunin = Pron Pers Sg3 Ess = Ess of N Prop Sun  


# An early rule for "eanaš"/"eanas"
# ----------------------------------
SELECT Pron IF (0 ("eanaš") OR ("eanas"))(1 (Dem Loc) OR (Pers Loc));
	## Eanaš dáin gálduin leat hui unnán dieđuid.



# Px constraints
# --------------

# First select some clear Px cases
# Let these come directly after local disambiguation.

# PxSg1
# - - -
SELECT (Nom PxSg1) IF (-1 BOS)(NOT 0 VFIN)(1 EOS);
	## Ustibažžan!

SELECT (PxSg1) IF (-2 MUN)(-1 V-SG1)(NOT -1 ("leat"))
	(NOT *1 ("ahte") BARRIER WORD);
	## Mun attán heaggan sávzzaid ovddas.

SELECT (Nom PxSg1) IF (-1 PUNCT OR CLB)(0 N-PX)(1 COMMA);
	## Bárdnán, leage dorvvolaš.


# PxSg2
# - - -
SELECT (PxSg2) IF (-1 V-SG2)(NOT -1 ("leat"))(-2 DON);
	## Don attát heakkat mu ovddas?

SELECT (PxSg2) IF (*-1 (Imprt Sg2) BARRIER NPNHA LINK 0 V-TRANS-ACT LINK
	NOT 0 ("dassat") OR Pass LINK -1 CC OR CLB OR CS OR BOS)(NOT 0 Adv)
	(NOT 0 (N Pl Nom) LINK *-1 CC OR CLB BARRIER NPNH LINK -1 Nom)
	(NOT 0 (N Pl Nom) LINK -1 Attr LINK 0 Imprt)
	(NOT 0 (N Pl Nom) LINK *1 V-PL3 BARRIER NOT-ADV-PCLE);
	## Buvtte dasto oaffarskeaŋkkat!
	## Lihka bajás, váldde seaŋggat ja vázzil!

#	(NOT 0 Prop OR (V Sg2))
#	(NOT 0 Inf LINK *-1 INF-VERB BARRIER VERB)


SELECT (PxSg2) IF (0 (N Pl Nom))(*1C (V Sg2) BARRIER NOT-ADV-PCLE OR CLB);
	## Hearrá Ipmilat galggat gudnejahttit.

SELECT (Gen PxSg2) IF (*-1 Sg2 BARRIER CS OR CP)(0 (N Pl Nom))(1 Po)(NOT 1 N)
	(NOT *2 NP-HEAD-GEN BARRIER NPNH);
	## Go don rohkadalat, de mana gámmárat sisa.

SELECT PxSg2 IF (-1 ("hearrá" Sg))(0 ("ipmil"));
	## It galgga geahččalit Hearrá Ipmilat.


# PxSg3
# - - -
SELECT (PxSg3) IF (*-2 ("son") BARRIER NOT-ADV-PCLE)(-1 V-SG3 LINK 0 V-TRANS-ACT)
	(NOT 0 N-NOPX)(NOT 0 Attr LINK *1 N BARRIER NPNH);
	## Son beastá álbmogis sin suttuin.

SELECT (PxSg3) IF (*-2 (V Sg3) BARRIER V-NOT-AUX)
	(-1 PrfPrc LINK 0 V-TRANS-ACT LINK NOT 0 LOC-VERB)(0 N-PX)(NOT 0 Nom);
	## ...ovdal go Márjá lei riegádahttán bártnis.

SELECT (PxSg3) IF (*-3 NP-HEAD-SG-NOM BARRIER V-NOT-AUX)(-2 A)(-1 go)
	(NOT 0 Attr LINK *1 N BARRIER NPNH);
	## Máhttájeaddji ii leat stuorit go oahpaheaddjis, ii ge bálvaleaddji
	## stuorit go hearrás.

SELECT (PxSg3) IF (*-1 (Sg Nom) OR (Sg3 Nom) BARRIER Pers OR CS OR CP OR LOC-VERB
	LINK NOT 0 Attr)(NOT *-1 (Imprt Sg2) BARRIER VERB OR CS)
	(0 N-PX)(NOT 0 Loc LINK 1 Ill);
	## Dat guhte ráhkista bártnis dahje nieiddas eanebut go mu...

SELECT PxSg3 IF (*-1 (V Sg3) BARRIER CS OR REALCLB)(0 Gen)(1 Po)
	(NOT 1 Pr LINK *1 Gen BARRIER NPNH);
	## Dahje maid sáhttá olmmoš addit máksun heakkas ovddas?

SELECT (Gen PxSg3) IF (*-1 BOS OR CS OR CLB BARRIER NPNH)(1 Po)
	(*2 V-SG3 BARRIER NOT-ADV-PCLE LINK NOT 0 A LINK *1 N BARRIER NPNH);
	## Raporttas bokte dáhttu bargojoavku čalmmustahttit Sámedikki.

SELECT (PxSg3) IF (-2 N-PX)(-1 CC);
	## Mun lean boahtán bidjat soahpameahttunvuođa bártni ja áhčis gaskii.

REMOVE PxSg3 IF (0 ("ođđasis"));
	## Byrokratiijat hábmemušaideaset geažil dušše ráhkadit ođđasis
	## daid čuolmmaid.
 # We could perhaps have selected Adv instead.
 
REMOVE Px IF (0 ("oktavuohta" Loc));
	## Dán doaibmabiju eat váldde mielde dán oktavuođas.



# ------------------- #
     CONSTRAINTS      #
# ------------------- #

# Certain verb readings
# ---------------------

 # Here we pick out finite verbs that follow personal pronouns. 
 # This selection is quite safe at this point, and we get rid of several 
 # ambiguities that would otherwise be disturbing in the following cycles. 
 # Note that all the following rules, except the rule for Sg2, also select 
 # imperatives.   

# Sg1
# - -

SELECT (V Sg1) IF (*-1 MUN BARRIER Nom OR V-SG1 OR CS LINK NOT *-1 V-SG1 
	BARRIER NOT-ADV-PCLE)(NOT 0 PrfPrc LINK *-1 ("leat") BARRIER CS OR CP OR 
	CLB)(NOT 0 PrfPrc LINK *-1 V-SG1 BARRIER NOT-ADV-PCLE);
	## Dego mun dás vuollelis mottiin ovdamearkkain čájehan,...

SELECT (V Sg1) IF (*1 MUN BARRIER NOT-ADV-PCLE OR CLB);
	## Man guhká ferten mun gierdat din?


# Sg2
# - - 

SELECT V-SG2 IF (*-1 DON BARRIER V-SG2 OR ConNeg OR CS LINK NOT *-1 V-SG2 
	BARRIER CS OR CP OR CLB);
	## ...jos don fal luoitádat ja gudnejahtát mu.
	## Rohkadallet dasto ná: Áhččámet, don guhte leat almmis.

SELECT V-SG2 IF (*1 DON BARRIER CS OR CP OR CLB);
	## Leat go don dat guhte galgá boahtit? 


# Sg3
# - - 
SELECT (V Sg3) IF (*-1 SON BARRIER Nom OR V-SG3 OR CS OR PUNCT LINK NOT *-1 V-SG3 
	BARRIER NOT-ADV-PCLE)(NOT 0 Adv LINK *1 V-SG3);
	## Geas ležžet bealjit, son gullos!

SELECT (V Sg3) IF (*1 SON BARRIER NOT-ADV-PCLE OR go OR CLB)
	(NOT 0 CS)(NOT 0 Adv LINK *1 (V Sg3) BARRIER CC OR CS OR CP);
	## ...oažžu son atnit dušše dan čázis mii sutnje lea mearriduvvon.

REMOVE (V Sg3) IF (-1 (Pron Nom))(NOT -1 Sg OR Sg3);
	## Sii manne vissui.
	## Dii uhccán oskkolaččat. 


# Du1
# - - 
SELECT (V Du1) IF (*-1 MOAI BARRIER V-DU1 OR CS OR PUNCT LINK NOT *-1 V-DU1 
	BARRIER NOT-ADV-PCLE);
	## Moai dihte ahte dát lea munno bárdni.

SELECT (V Du1) IF (NOT 0 Adv LINK *-1 BOS OR PUNCT BARRIER NOT-CC)
	(NOT 0 Adv LINK *-1 (V Du1))(*1 MOAI BARRIER CLB OR CS);
	##


# Du2
# - - 
SELECT (V Du2) IF (*-1 DOAI BARRIER V-DU2 OR CC OR CS OR PUNCT LINK NOT *-1 V-DU2 
	BARRIER NOT-ADV-PCLE);
	## Manne doai ozaide mu?

SELECT (V Du2) IF (*1 DOAI BARRIER NOT-ADV-PCLE OR CLB);
	##


# Du3
# - -
 # The competition is -ba Foc.

SELECT (V Du3) IF (*-1 SOAI BARRIER V-DU3 OR CS OR PUNCT LINK NOT *-1 V-DU3 
	BARRIER NOT-ADV-PCLE);
	## Soai leaba dahkan čielga oavdudagu.
	## Soai ferteba mannat. 

SELECT (V Du3) IF (*1 SOAI BARRIER NOT-ADV-PCLE OR CLB);
	## Fertebago soai mannat? 

SELECT (V Du3) IF (*-1 ("guokte" Nom) BARRIER CS OR CP) ; 
	## Guokte nissona ferdneba milluin.

REMOVE (V Sg3) IF (0 Du3)(*1 ("guokte" Nom) BARRIER NPNHA);
	## Mus leaba dušše dát guokte oappá geaid don dás oainnát.

SELECT (V Du3) IF (-1 (N Nom))(*-2 CC BARRIER NPNH LINK -1 (N Nom));
	## Per Berg ja Rolf Olsen leaba doppe.
	## Deanu Searat ja Sámi Siida leaba ovttas ordnen dáid gilvvohallamiid.
        ## Stuorra nieida ja unna gánda ferteba leat doppe.
 # Note that 'Searat' is Pl, hence no number requirement in this rule.

SELECT (V Du3) IF (*1 (Sg Nom) BARRIER NPNHA LINK 1 CC LINK *1 (Sg Nom)
	BARRIER NPNH);
	## Dá leaba Brita ja Ole Kåven.

REMOVE (V Sg3) IF (*-1 (N Num Nom) BARRIER VERB OR CLB OR CC OR CS OR CP)
	(0 (V Du3));
	## Biera-guovttos Márehiin leaba daid mánáid váhnemat.


# Pl1
# - - 
SELECT (V Pl1) IF (*-1 MII-PERS BARRIER VFIN OR PUNCT LINK NOT *-1 V-PL1 
	BARRIER NOT-ADV-PCLE OR CLB);
	## Mii sápmelaččat gal leat vásihan dán...

SELECT (V Pl1) IF (*1 MII-PERS BARRIER NOT-ADV-PCLE OR CLB OR ("seamma") 
	LINK NOT *1 V-SG3 BARRIER NOT-AA OR CLB);
	## Galgat go mii vuordit muhtun eará? 


# Pl2
# - -
SELECT (V Pl2) IF (*-1 DII BARRIER NOT-ADV-PCLE OR CLB);
	##

SELECT (V Pl2) IF (*1 DII BARRIER NOT-ADV-PCLE OR MO OR CLB);
	## Addet dii sidjiide borramuša!


# Pl3
# - -
SELECT (V Pl3) IF (*-1 SII BARRIER V-PL3 OR CS OR PUNCT LINK NOT *-1 V-PL3 
	BARRIER NOT-ADV-PCLE)(NOT 0 Adv LINK *1 V-PL3 BARRIER CC OR CS OR CP);
	## ...vai sii áiggi mielde oččoše doarvái gelbbolašvuođa.

SELECT (V Pl3) IF (*1 SII BARRIER NOT-ADV-PCLE OR CLB)
	(NOT 0 Adv LINK *1 V-PL3 BARRIER CC OR CS);
	## Máhttego sii čilget áššiset dárogillii?

SELECT (Prt Pl3) IF (*-2C (Prt Pl3) BARRIER VFIN)(-1 CC);
	## Ja buot olbmot imaštalle sakka ja jerre. 

REMOVE (V Ind Pl3) IF (1C VFIN)(NOT 1 Pl3) ;
	## Manne ii leat áviisa boahtán?
	## Máhtte manai čuoigga skuvlii.
	## Vihtta bivdovuogi mun gal dušše namahin.  


# PrsPrc
# - - - -
SELECT PrsPrc IF (NOT 0 LEX-N)(1 CC OR COMMA LINK 1 A);
	## Soahpamušas váilu njuolggadusat smávit vahágahtti ja geavatlaččat
	## bággolonistemiide.
 # Early rule since many PrsPrc readings are removed later.

 
# A rule for "dušše"
# - - - - - - - - - -
REMOVE V IF (-1 CS)(0 ("dušše"))(*1 Acc BARRIER NPNHA); 
	## Suhpodit daddjojuvvo dat go dušše nođiid váldá olmmoš eret.

 # This rule has to precede particle rules, so that the verb reading of 
 # "dušše" will not force the CS reading of a preceding particle.


# ahte
# ----
SELECT CC IF (-1 COMPAR)(0 ("ahte"))(1 COMPAR);
SELECT CC IF (-1 Comp)(0 ("ahte"))(1 Comp);
	## Dárogiel sánit bohtet eanet ahte eanet sámegiela sisa.
	## Dat šattai heajut ahte heajut.

SELECT CS IF (0 ("ahte")); # All other occurrences of "ahte" are CSs.

 # These rules have to precede all rules referring to CC, so I put 
 # them here, although they logically belong with the subjunction rules 
 # in a later block.



# ---------------------------------------------------------------- #
# Cycle 1b: removing the readings that were left from the 1a rules #
# ---------------------------------------------------------------- #
                             CONSTRAINTS                           #
# ---------------------------------------------------------------- #

# Removing (nearly) all remaining Px readings
# -------------------------------------------
 # The remaining Px readings will be taken care of later.

REMOVE (PxSg1) ; # IF (0 Ess); # yes, kill'em all
	## Ale bala váldimis Márjjá ruktosat áhkkán.  


REMOVE (Nom PxSg2);
	## Su lusa bohte olbmot Jerusalemis.
	## Na dát ledje buorit mállásat. 

REMOVE (PxSg2) IF (NOT *-1 (Imprt Sg2) OR MOD-ASP BARRIER CS OR CP LINK 0 Sg2)
	(NOT *1 (V Sg2) BARRIER VFIN OR CS);
	## De bohte ruđat. 
	## Ja gillii fas dohko mannat. 
	## ...gii eanemus ja bistevaččat doallá oktavuođa divššohasain.

REMOVE (PxSg2) IF (*-1 (Sg Gen) OR (Sg3 Gen) BARRIER NPNH LINK -1 CC);
	## ...eará čuovvoleapmi mii johtui biddjo ja dan bohtosat.


REMOVE (PxSg3) IF (-1 Indef)(0 Loc);  # Possessed nouns do not go with Indef?
        ## Olmmoš ii eale dušše láibbis, muhto juohke sánis mii boahtá
        ## Ipmila njálmmis.
        ## Alimusriekti lea dattege muhtun duomus cealkán ahte...

REMOVE (PxSg3) IF (NOT *-1 (V Sg3) BARRIER CS)(NOT -1 go)
	(NOT *1 (V Sg3) BARRIER CS) ;
	## Maid don áiggut márkanis?
	## Divššohasas galget leat dat dieđut, ...
	## Nu guhkás go Gonagas mearrida...

REMOVE (PxSg3) IF (*-1 (@SUBJ) BARRIER CS OR CP OR (@SUBJ) OR Inf 
	LINK NOT 0 Sg OR (TV Sg3))
	(NOT -1 Inf LINK 0 V-TRANS-ACT LINK *-1 V-SG3 BARRIER VERB OR CC OR CLB 
	OR CS OR CP);
	## Láhka gusto olbmuide norgga skiippaid alde mat johtet olggoriikkas.
	## Son oastá dáid váldolinjáid hárrái Stuorradikkis.
	# The last sentence is problematic, telles us that (Sg3 TV) 
	
REMOVE (PxSg3) IF (*-1 VFIN BARRIER CC OR CLB OR CS OR CP LINK NOT 0 Sg3 OR Inf)
	(NOT *-1 ConNeg BARRIER CC OR CLB OR CP LINK *-1 (Neg Sg3) BARRIER VERB)
	(NOT *1 VFIN);
	## ...geat eai oro bissovaččat riikkas.

REMOVE (PxSg3) IF (*-1 Interr BARRIER VFIN)(*1 VFIN LINK NOT 0 Sg3);
	## ...man oaivilis sii leat dakkár áššiid hárrái.



# -------------------------------------------- #
# Cycle 2: Other cases of local disambiguation #
# -------------------------------------------- #
                   CONSTRAINTS                 #
# -------------------------------------------- #
# Note: Is it clearer to introduce a "CONSTRAINTS" boundary in front of Cycle 2?


# This is the main part of the local disambiguation.
# Here we go through the grammar in a systematic way.
# Cases that should be handled before or after the main disambiguation, 
# will be taken care of in cycles before or after this one.


# ============ #
# Noun or not? #
# ============ #


# Based on left context
# ---------------------

SELECT N IF (-1 (A Attr))(NOT 0 Inf OR VFIN OR ("veara") OR ("mearkkašan#veara") OR
	("namuhan#veara"))(NOT -1 Adv)(1 NP-BORDER);   
	## Dat lea čuovvovaš sajis.

SELECT (N Nom) IF (-2C (N Nom))(-1 CC)(NOT 0 VERB OR Pron)(NOT 0 (A Ess) LINK -2 Ess)
	(NOT 1 N OR Pron OR Num OR A OR Actio);  
	## Mánát ja nuorat leat dál.

 # Write rules for other case forms.

SELECT N IF (-2 BOS)(-1C Gen)(1 EOS);        ## Son lea Ruoŧa čálli.
                                              # (titles)

REMOVE N IF (-1 (N Gen) OR (Pron Pers Gen))(NOT -1 Nom)(0 Po)(NOT 0 ("guovddáš")); 
	## Daid ovddas gal in máksán maidege.

SELECT N IF (*-1 (Dem Acc) BARRIER NPNH LINK *-1C VFIN BARRIER NOT-ADV-PCLE)
	(0 Acc)(0 VFIN)(NOT 0 PrfPrc LINK *-1 ("leat"));
	## Áhčči dagai dan fatnasa nu oanehačča.

REMOVE PrsPrc IF (-1 A)(NOT -1 Adv)(NOT 1 Ess); 
	## Dainnna ii leat vissis mearri galljodagas.

SELECT A IF (-1 ("eambbo"));
	## Muhto eambbo eahpidahtti lea čielggadusa s. 17 cealkámuš.

REMOVE VFIN IF (*-2 VFIN BARRIER CS OR CP)(-1 PrsPrc)(NOT -1 LEX-N) ;
	## Johanas lei buolli ja báiti čuovggas.


# Based on right context
# ----------------------

SELECT N IF (NOT 0 Pers OR VFIN OR (A Ess))(1 CC)(2C N); 
	## Sápmelaččat ja dážat leat ovdal eallán ovttas.

SELECT N IF (NOT 0 PrfPrc LINK *-1 ("leat") BARRIER NOT-ADV-PCLE)(1 COMMA)
	(*2 HNOUN BARRIER NPNH OR CC LINK 1 CC);
	## ...ležžet sii dál sápmelaččat, dážat dahje kveanat.

SELECT N IF (NOT 0 Adv OR Inf OR PrfPrc OR ("hárrái") OR ("orrut") OR ("veara"))
	(1 COMMA)(2 CP);   
	## Doaimmat, maiguin mun barggan.
	## Gažaldat lea dušše, man ollu ... # NOT 0 Adv!
	## Lea miellagiddevaš lohkat, maid ...# NOT 0 Inf!

SELECT N IF (NOT 0 Po OR Pron OR Inf OR PrfPrc)(1C (Pron Interr)); 
	## Dát lea mu bálvaleaddji gean mun lean válljen.

SELECT N IF (-1 BOS OR CS)(1 (PUNCT LEFT))(*2 (PUNCT RIGHT) LINK 1 (Pron Interr));
	## Riehkit (riehkit ja fierváhirssat) mat bohtet beaŋkká siskkobeallai 
	## olggobeal priváhta eatnamiid gullet eanaeaiggádii.

REMOVE N IF (0 Pr)(NOT 0 ("guovddáš"))
	(*1 (N Gen) OR (Pron Pers Gen) BARRIER CC OR CS OR CP OR go 
	LINK NOT 0 Cmpnd BARRIER NPNH);
	## Jus divššohas sivas fysihkalaš vigiid,..   

REMOVE N IF (0 VFIN)(0 (TV Qst))(NOT *1 VFIN BARRIER VERB OR CS OR CP)
	(*1 Acc BARRIER NPNH);
	## Áiggutgo gáfe?

REMOVE N IF (0 VFIN)(0 V-TRANS-ACT)(1 ("go" Pcle))(*2 Acc BARRIER NPNH)
	(NOT *2 VFIN BARRIER VERB OR CS OR CP);
	## Áiggut go gáfe?

REMOVE A IF (0 N)(NOT 0 (@ADVL))(1 Po)(NOT 1 Pr LINK *1 NP-HEAD-GEN BARRIER NP-BORDER);
        ## Čohkkán dárolaččaid gaskkas.

REMOVE A IF (NOT -1 ("mii" Sg Gen))(0 N)(NOT 0 Comp OR Ess OR ("veara") OR 
	("mearkkašan#veara") OR ("namuhan#veara"))(1C VFIN)
	(NOT 1 ("leat") LINK *1 ("ahte") BARRIER NOT-ADV-PCLE)
	(NOT 1 go LINK *1 COPULAS BARRIER V-NOT-AUX  LINK *1 (@SUBJ) BARRIER NOT-NP)
	(NOT 0 Qst LINK *1 COPULAS BARRIER V-NOT-AUX  LINK *1 (@SUBJ) BARRIER NOT-NP); 
	## Dál go sápmelaččain leat bohccot.
	## Man guhkki lea du biila? # NOT -1 "mii" Sg Gen!	 

SELECT VFIN IF (1 go)(*2 ("ahte") BARRIER NOT-ADV);
	## Dáhtut go ahte mii gáikut dat eret?

REMOVE VFIN IF (-1 BOS OR CS)(0 NP-HEAD-SG-NOM)(1C V-SG3); 
	## Lásse lea mus reaŋgan.
  # Here I am careful, and require the V-SG3 to be a secure reading.

REMOVE VFIN IF (-1 BOS OR CS)(0 (Prop Nom))(NOT 0 ("Lea"))(1 V-SG3);	 
	## Máhtte ferte leat vuolgime.
  # Here I am less careful, and accept also ambiguous V-SG3, since I take it
  # that when the left context is a name, it is a more secure subject-candidate.
  # Alternatively, I might drop the 1C in the previous rule, or include the
  # pronouns.

REMOVE PrsPrc IF (NOT *1 N BARRIER NOT-ADJ-CC)(NOT -1 ("nu"))
	(NOT *-1 COPULAS BARRIER NOT-ADV-PCLE)(NOT 1 COMMA OR CC LINK 1 A) ;
	## Bearráigeahčči galgá doaimmahit bargguidis iešheanálaččat.
	## ...ja ahte dat dáhttu čielgasit berre doahttaluvvot.
 # This rule hits many nouns as well, like "dáhttu" in the last example. OK! 

REMOVE Adv IF (1C Po)(NOT 1 Px OR Adv OR ("mielde"));   
 # The last part of this rule, plus some modifications in Po rules, were necessary
 # to get the adverb analysis of "mielde" in
	## Mun vulgen dušše mielde.
 	## ... šattai Unjárga maid mielde... 

REMOVE A-CASE IF (*-1 BOS OR CS BARRIER NOT-DEM)(0 N)(NOT 0 ("buorre"))(1 N);
	## Sápmelaččaid historjá lea boaris.
 # No adjective, except buorre, agrees with a head noun in case. The correct
 # reading for a case-inflected prenominal word must thus be N.
 # (Problem: we have not considered a string predicative adj + other NP)

REMOVE (A Gen) IF (0 N)(NOT 0 Ord)(*1 N BARRIER NOT-ADJ);
	## Sámi dutkamiid guovddáža ásaheapmi.


# Preverbal subject
SELECT (Pl Nom) IF (NOT 0 Inf LINK -1 INF-VERB)(1C (V Pl3))(NOT 0 Adv);
	## Olu nuorat bohte naitalanahkái.
 # This is a brave rule, probably too strong. It comes this early in order to
 # precede other rules.
 # New: Modified because of
	## Prošeavtta áigut čađahit. # Added C to avoid áigut as subject.
	## ... geainna váimmut buoremusat lávggadit. # Buoremusat = Adv!


# Based on circumscribed context
# ------------------------------

REMOVE A IF (-1 Dem)(1 (Pron Interr));    
	## Son lea dan sápmelaš gean dovddan.

REMOVE (A Sg Nom) IF (NOT *-1 VFIN OR ("mii" Sg Gen))(0 (N Sg Nom))
	(*1 V-SG3 BARRIER NOT-ADV-PCLE LINK NOT 0 ("leat") LINK NOT *1 ("ahte")
	BARRIER NOT-ADV-PCLE);
	## Sápmelaš dál soittii dávjá dadjalit: gal han doppe ledje olbmot.

REMOVE (A Pl Nom) IF (NOT *-1 VFIN)(0 (N Pl Nom))(*1 V-PL3 BARRIER NOT-ADV-PCLE);
	## Dálá guovlluin sápmelaččat eai leat ássan nu guhká.

REMOVE Adv IF (NOT *-1 (Pl Nom) BARRIER CS)(0 (N Pl Nom))
	(*1C V-PL3 BARRIER NOT-ADV-PCLE OR CP);
	##  ...ahte nuorat eai nagodan identifiseret iežaset kulturduogážiinniset.

REMOVE V IF (-1C Dem)(1 V-IND-FIN);	
	## Dán báhkis šaddá čáppa gohppu.

REMOVE V IF (-1 BOS OR CS)(0 N)(1 ("go"))(2 VFIN);	
	## Ádjás go lea ođđa fanas?

REMOVE (V Ind) IF (-1 Dem)(0 N)(1C (V Ind));
	## Dakkár registreren ii leat goassege leamaš oassin árbevierus. 

REMOVE (V Ind) IF (-1 CS)(0 N)(NOT 0 (V Pl1)) (1 (Pron Interr) LINK *1 (V Ind)); 
	## Dasgo mánná mii lea su siste... 

REMOVE Adv IF (-1 BOS OR CS OR PUNCT)(0 (N Sg Nom))
	(NOT 0 Prop OR ("duo") OR ("várra"))(1 V-SG3);
	## Áigi lea dievvan.

REMOVE (N Ess) IF (NOT *-1 OPRED-VERB BARRIER VERB)(0 (V Actio Ess));
	## Son oinnii Ipmila Vuoiŋŋa boahtimin vuolás.
	## Luosahat adnojuvvojit dalle goas stuorát guolli lea vuodjamin.


# Some particular nouns
# ---------------------

# álbmot
# - - - -
SELECT N IF (-1 ("sápmi" Sg Gen))(0 ("álbmot"));
	## Dárbbašago sámi álbmot bálvalusfálaldagaid lasiheami?

# bealistis
# - - - - -
SELECT N IF (*-1 NP-HEAD-NOM BARRIER NOT-ADV-PCLE)(0 ("bealis"))(0 Px);
	## Stáhta bealistis vuođusta rievttis háldduiguin.

# fuolla (idiomatic)
# - - - - - - - - - 
SELECT (N Acc) IF (*-1 ("atnit") BARRIER VFIN OR CS OR CP)(0 ("fuolla")); 
	## Son ii ane fuola sávzzain.

# giella
# - - - -
SELECT ("giella") IF (-1 Attr);
	## Sámegiella hállet áidna giellan mánát, geat eai leat vel álgán skuvlii.

# guovddáš
# - - - - -
SELECT Attr IF (0 ("guovddáš"))(1 N); 
	## Guovddáš doaibmabidju lea vuođđudit sámegiela álgooahpahusa.

# guovttes
# - - - - 
REMOVE ("guovttes") IF (1 (Sg Gen));
REMOVE ("guovttes") IF (*1 (N Sg Gen) BARRIER NPNH);
	## Viesus leat guokte olbmo.
	## Sáhtášii juohkit guokte 5 mánnosaš stipeandda čállit ohcamiid. 

# imaš
# - - - 
SELECT N IF (-1 VFIN OR ConNeg OR A OR Dem)(0 ("imaš"));
	## Lei imaš, go buvddas eai vuovdde albma varramárffiid.
	## dakkár imaš ja ártegis dilli ahte ...

# jahke#čuohti
# - - - - - - -
SELECT ("jahke#čuohti");
	## Finnmárkolaččat ledje atnán vuvddiid jahkečuđiid.

# máŋga
# - - - -
REMOVE N IF (0 ("máŋga"))(NOT 0 Loc)(*1 N BARRIER NPNH);
	## Bargu ollašuhttit máŋga dáin doaibmabijuin lea juo álggahuvvon.

# oppalaš
# - - - -
SELECT Attr IF (0 ("oppalaš"))(*1 N BARRIER NPNH);
	## Sámi álbmoga bálvalusfálaldagaid dási oppalaš lokten eaktuda, ahte...

# vašálaš
# - - - - 
SELECT N IF (0 ("vašálaš"))(NOT *1 HNOUN BARRIER NPNH);
	## Mun bijan du vašálaččaid du julggiid vuložin.



# ====================== #
# Adjectives and adverbs #
# ====================== #

SELECT A IF (-2 BOS OR CS)(-1 ("mii" Interr Sg Gen));
	## Man guhkki lea girkui?

REMOVE VERB IF (-1 ("mii" Interr Sg Gen)); 
	## In dieđe man buorit dat leat.

REMOVE VERB IF (-1 ("mihá")); 
REMOVE VERB IF (-1 ("seamma"))(*1 go BARRIER NOT-NA LINK NOT 1 VFIN); 
REMOVE V IF (1 ("ihkinassii"));
	## Muhtun riggodagaid geavahit mihá unnit dál.
	## Bargiid guottut mearkkašit seamma ollu go fáktadieđut.
	## Mun áiggun čuovvut du ihkinassii manažat.

REMOVE N IF (-1 ("nu") OR ("mihá") OR ("hirbmat") OR ("hui"))(0 Adv OR A OR PrfPrc);
	## Mun lean nu suhttan. 
	## Lea hui easkka čuožžilan gažaldat.

SELECT Adv IF (-1 ("leat"))(1 PrfPrc);
SELECT Adv IF (-1 VERB OR Com)(0 ("ovttas"))(NOT 0 Foc); 
	## Divššohas lea duođalaččat roasmmohuvvan.
	## Bátnedoavttir galgá ovttas eará bargiiguin mearridit.

REMOVE Adv IF (0 ("nuorra"));
	## Nuorat dajai sutnje:...

SELECT (A Nom) IF (-1 ("vuoi"));
	## Vuoi surgat dan gándda!



# ========================== #
# Disambiguating clitics     #
# ========================== #

REMOVE Qst IF (0 ("dego"));
	## Su ámadadju báittii dego beaivváš. 

REMOVE Qst IF (0 Adv)(NOT *1 ("?")); 
	## Nugo dieđát, de lean dál.


# ========================== #
# Disambiguating Numerals    #
# ========================== #

SELECT Num IF (-1 COUNTABLE)(0 ABBR OR ACR) ;
	## Dieđáhusa oasis I ovdanbuktojuvvo Bb:a sámepolitihkalaš vuođđolávdi.

# ========================== #
# Disambiguating adpositions #
# ========================== #
        CONSTRAINTS          #
# ========================== #

# Rules relating to particular adpositions
# ----------------------------------------

# gaskan
# - - - -
SELECT Pr IF (0 ("gaskan"))(1 VGen);
	## Gaskan bora viehkalin olggos.

# miehtá
# - - - -
 # These rules are here because "miehtá" is also VFIN, and many of the Pr/Po
 # rules below include the condition (NOT VFIN). So "miehtá" goes wrong there.
	 
SELECT Pr IF (*-1 VFIN BARRIER CC OR CS OR CP OR CLB)(NOT -1 Gen)(0 ("miehtá"))
	(*1 Gen BARRIER NPNH);
	## Dalle bisttii luossaguolli miehtá dálvvi.

SELECT Pr IF (-1 Acc LINK *-1 V-TRANS-ACT BARRIER NPNHA)(0 ("miehtá"))
	(*1 Gen BARRIER NPNH);
	## Sii dieđihedje dan miehtá guovllu, ja sii bukte su lusa buot
	## buhcciid.

SELECT Pr IF (-1 Nom LINK *-1 COPULAS BARRIER NPNHA)(0 ("miehtá"))
	(*1 Gen BARRIER NPNH);
	## Searvegottiin lei dál ráfi miehtá Judea ja Galilea ja Samaria.

SELECT Pr IF (*-2 VFIN BARRIER CS)(-1 COMMA OR CC)(0 ("miehtá"))
	(*1 NP-HEAD-GEN BARRIER NP-BORDER LINK NOT 1 Po);
	##

SELECT VFIN IF (NOT *-1 VFIN BARRIER CS)(0 ("miehtá"));
	## Jus divššohas dasa miehtá, dahje dilálašvuohta lea nu,...
 
# vuostá
# - - - -

SELECT Adv IF (-1 ("váldit"))(0 ("vuostá"));
	## Sámi servodagas váilu árbevierru váldit vuostá almmolaš diehto-
	## juohkima dakkár hámis.


# Adpositions that take modifiers
# -------------------------------

SELECT Pr IF (-1 Gen)(0 ("bealde") OR ("beale") OR ("beallái") OR ("geahčai")
	OR ("geahčen") OR ("geaže"))(1 Gen)(NOT 1 TIME);
	## Daid sáhttá heaŋggastit dan heargái mii daid eará goahtemuoraid lea 
	## guoddimin, guokte bealji goappáge beallái hearggi. 


# The case of GASKAL
# ------------------

SELECT Pr IF (0 GASKAL)(1 (Pron Gen))(2 CC)(3 (Pron Gen));
	## Dat lei gaskal min ja din.

SELECT Po IF (-3 (Pron Gen))(-2 CC)(-1 (Pron Gen))(0 GASKAL);
	## Dat lei min ja din gaskkas.

SELECT Pr IF (0 GASKAL)(1 Num)(2 CC)(3 Num);
	## Gaskal 12 ja 16 jahkásaš mánáin lea muhtun muddui iešheanalis

SELECT Po IF (-3 Num)(-2 CC)(-1 Num)(0 GASKAL);
	## Alimusriekti veadjá leat jagiid 1854 ja 1897 gaskka doallan
	## siskkáldas čoahkkima.

SELECT Pr IF (0 GASKAL)(*1 NP-HEAD-GEN BARRIER VERB OR CS LINK 1 CC LINK 
	*1 NP-HEAD-GEN BARRIER VERB OR CS);
	## Ferte navdit ahte dáža duopmostuolut ja eará riektegeavaheaddjit 
	## dábálaččat dulkojit dáža siskkáldas rievtti nu ahte garvá vejolaš 
	## vuostálasvuođa gaskal dákkár álbmotriektenorpma ja siskkáldas 
	## norgga njuolggadusa.
 
SELECT Pr IF (0 GASKAL)(*1 (N Gen) BARRIER NPNH LINK *1 CC BARRIER NOT-NP 
	LINK *1 (N Gen) BARRIER NPNH LINK 0 HNOUN);
	## Lága mearrádusat galget váikkuhit luohttevašvuođa gaskal divššohasa
	## ja dearvvašvuođabálvalusa.
	## Dán láhkai lea leamaš muhtun muddui lotnolas doahttaluvvon dássádat-
	## dilálašvuohta gaskal stáhta almenneteaiggádin ja geavahanvuoigaduvvon 
	## giliássiid.

SELECT Pr IF (0 GASKAL)(1 Dem)(2 NUMERALS);
	## Ferte oaidnit oktavuođa gaskal dan guokte soahpamuša.

SELECT Pr IF (0 GASKAL)(1 A)(2 CC)(3 A LINK *1 (N Gen) LINK 0 HNOUN); # Barrier?
	## Galgá válljejuvvot gaskal vejolaš ja dohkálaš iskkadan- ja
	## dikšunvugiid.

SELECT Po IF (-1 (N Gen) LINK *-1 CC BARRIER NPNH LINK -1 (N Gen))(0 GASKAL);
	## Riiddut boazodolliid ja eará vuoigatvuođaoamásteddjiid gaskkas 
	## livčče lassánan. 

SELECT Po IF (-3 Inf)(-2 ("ja"))(-1 Inf)(0 GASKAL);
	## Sániid oamastit ja eaiggáduššat gaskkas geavahuvvo "ja".



# ===========
  CONSTRAINTS
# ===========

# Po or Pr?
# ---------

# Across parenthesis

SELECT Po IF (-1 (PUNCT RIGHT))(*-2 (PUNCT LEFT) LINK -1 Gen);
	## ...sáhttá dulkot telestudio dahje govvatelefovnna (ruoná
	## nummira) bokte.


# Requiring Genitive

REMOVE Po IF (NOT -1 (N Gen) OR (A Gen) OR (Num Gen) OR (Pron Gen) OR (Actio Gen));
REMOVE Po IF (-1 N)(NOT -1 Gen);   
REMOVE Pr IF (NOT *1 N OR A OR Num OR Pron BARRIER NPNH);
	## Jus divššohas lea badjel 16 jagi boaris,... 
	## Leansmánni lea kommišuvnnas fárus.
	## Na mun gal hupmen dan birra.

SELECT Pr IF (NOT -1 Gen OR (Pron Interr Nom))(NOT 0 A OR VFIN)
	(*1 (N Gen) BARRIER NPNH LINK 0 HNOUN);
	## Jus divššohas lea vuollil 16 jagi boaris,...

SELECT Pr IF (-1 ("dušše"))(*1 NP-HEAD-GEN BARRIER NPNH);
	## Dat lea dušše badjelaš állanbeali guhku.

SELECT Pr IF (NOT -1 Gen)(1 (Pron Gen));
	## ...muhto ovdal dan áiggi ledje "sápmelaččat boahtán dán guvlui". 

REMOVE Pr IF (NOT 1 (Num Gen))(NOT *1 (N Gen) BARRIER NPNH);
	## Olbmuin bisttii luossaguolli miehtá dálvvi. 
	## Ovdal dan aiggi ledje sápmelaččat boahtán dán guvlui.
	## Gonagas addá láhkaásahusa lága geavaheami birra Svalbarddas.

SELECT Po IF (*-1 BOS OR CS OR CLB BARRIER NPNH)(-1 Gen)(NOT 0 ("guovddáš"));
SELECT Po IF (-1 Gen)(1 CC)(NOT -1 Acc LINK *-1 CC BARRIER NOT-NP LINK -1 Acc LINK
	-1 V-TRANS-ACT BARRIER NPNHA);
SELECT Po IF (-1 Gen)(NOT -1 VERB OR Adv)(1 TIME); 
	## Dán vuostá sáhttá dadjat, ahte ...
	## Jos sáddo deavdá buođu vuollái dahje jos sáddo hávddarda buođu vuolde... 
	## Sámi guovlluin ožžo Sámeparlameantta Suoma beallái jagi 1976.

SELECT Po IF (-1 Gen)(NOT -1 ("leapma" Dimin))(1 Gen LINK *1 Po BARRIER NP-BORDER);
	## Dattetge ii leat muitaluvvon makkárge namahan veara geavahanriiddu birra 
	## dálolaččaid ja boazodoalu gaskkas.

SELECT Po IF (-1 (Interr Gen));
	## Vuoi dan olbmo gean bokte fillehusat bohtet.

SELECT Pr IF (NOT -1 Gen LINK *-1 BOS BARRIER (V TV) OR Pr) (NOT -1 TIME)
        (NOT 0 Attr OR VFIN)(NOT 1 VFIN)(*1 (N Gen) OR (Pron Gen) BARRIER PUNCT 
	LINK NOT 0 Cmpnd LINK NOT *1 N OR Po BARRIER CC OR CP OR CS OR CLB)(NOT -1 (@GP>));
	## Sáhttá dahttege vurket journála olggobealde doaimma. 
# denne fila her.
#SELECT Po IF (-1 (@GP>)); # more careful with -1C than with -1.
SELECT Po IF (-1C (@GP>));

SELECT Pr IF (1 (Num Gen));
	## Vuollel 12 jahkásaš mánáin ii leat iešheanalis geahččanvuoigatvuohta.

REMOVE Pr IF (NOT 1 Gen);
	## Geavatlaš sivaid geažil čatnasa bajimus submi namuhuvvon máksomearrái. 

SELECT Adv IF (-2 (Num Acc))(-1 (N Acc))(0 ("maŋŋil"));
	## Dat dáhpáhuvai golbma jagi maŋŋil.

SELECT Po IF (-1 Gen)(NOT -1 VFIN OR ("leat" PrfPrc) OR Adv)(NOT -1 Acc LINK *-1 CC 
	BARRIER NOT-NP LINK -1 Acc LINK *-1 V-TRANS-ACT BARRIER NPNHA)
	(NOT 0 Attr OR ("guovddáš"))(NOT 1C Po)(NOT 1 Gen);
	## Daid ovddas gal in máksán maidege.

SELECT Po IF (-1 Num)(0 ("rádjai")); # Make general rule!
REMOVE Po IF (NOT -1 Gen OR Num);
	## ... paragráfaide 3-18 rájes 3-21 rádjái.
	## Jus divššohas dasa miehtá,... 


# Removing some readings that compete with Po
# - - - - - - - - - - - - - - - - - - - - - -
REMOVE N IF (-1 Gen)(0 Po)(NOT 0 ("guovddáš"))(NOT 1C Po);
REMOVE V IF (-1 Gen)(0 Po)(NOT 1C Po);
	## ...maid ektui sáhtii leat buori oskkus.
	## Gávpot mii lea vári alde, ii sáhte leat čihkosis.


 
# =========================== #
# Disambiguating subjunctions #
# =========================== #
        CONSTRAINTS           #
# =========================== #

 # Subjunctions are disambiguated in two subcycles. This is the first.

SELECT CS IF (-1 COMMA)(0 ("de") OR ("vaikko"))(NOT 1 CS);
        ## Jos geahččá dearvvašvuođarievtti geahččanguovllus, de ii gáibádus
        ## áddehantti dieđuid oažžumis čatnas giellalága ráddjehusaide.

REMOVE CS IF (NOT *1 VFIN OR ("leapma" Dimin)); # The latter is "leamaš".
	## Sii manne meahccái, vaikko lei issoras buolaš.
	## Eai lean váldán go niestebohcco.

# amas
# - - -
# A, or Neg Sup, or CS (if followed by (nominative) subject and (infinitival) verb).

SELECT A IF (0 ("amas"))(1 CC)(2 A);
SELECT A IF (*-1 ("leat") BARRIER NOT-ADV OR CLB)(0 ("amas"));
	## Máŋgasat leat sakka fuolastuvvan amas ja dovddus vahágiin.
	## Ollu sápmelaččaide lea dál ain amas jurdda.

SELECT A IF (0 ("amas"))(1 N)(NOT *1 Nom BARRIER NOT-NP);
	## Eai dovdda amas olbmo jiena.

SELECT (Neg Sup) IF (0 ("amas"))(1 Inf);
	## Bávlos lei nappo mearidan gárvit Efesosa, amas ájihuvvot Asia eanan-
	## gottis. 

SELECT CS IF (0 ("amas"))(1 Neg);
	## Prinsihpa maid lean čuovvon lea juohkehačča ektui fuolahan, amas ii
	## vuovdi vahágahtto.

SELECT CS IF (0 ("amas"))(*1 Nom BARRIER NPNHA LINK *1 Inf);
	## Mánás leat juolggit biddjojuvvon man nu vuostái nođiid ala, amas juolggit
	## čerggiidit. 

# amat
# - - -
SELECT (Neg Sup) IF (0 ("amat"))(1 Inf); # Also Pcle?
	## Sii guddet du gieđaideaset alde, amat norddastit juolggát geađgái.

# daningo
# - - - -
# dasságo
# - - - -
 # These two are taken care of by the rule in the cycle 'Disambiguating clitics'. 

# dasgo
# - - -
SELECT CS IF (0 ("dasgo"))(NOT *1 ("?"));
SELECT CS IF (0 ("dasgo"))(1 Interr);
	## Dasgo Herodes lea ohcamin máná goddin dihtii dan.
	## Dasgo goabbá lea stuorit,...

# de
# - -
SELECT CS IF (*-1 ("jos") OR ("go") LINK -1 BOS OR CS)(-1 COMMA)(0 ("de"));
	## Jos dieđášin, de dajašin.
	## Go olbmot jaskkodit, de sáhpánat álget jursit.
	## Son diehtá, ahte jos galgá leat mielde, de ferte máksit.

# go
# - -
SELECT Pcle IF (-3 BOS OR CLB)(-2 CC)(-1 V)(0 go);
SELECT Pcle IF (-2 BOS OR REALCLB OR PUNCT)(0 go)(*1 ("?"));
	## Muhto boađát go?                                                  
	## Eai go báhkinat ge daga aiddo nu?

SELECT Pcle IF (-2 ("vai"))(0 go);
	## ...vai geahččaluvvo go gávdnat čovdosiid bissovaš rievttis,... 

SELECT Pcle IF (-2 VERB)(-1C VFIN)(0 go);
SELECT Pcle IF (*-2 VFIN BARRIER CC OR CP OR CS LINK NOT 0 Inf)(-1C VFIN)(0 go)
	(NOT 1 VFIN); 
	## Árvvoštaladettiin galgá go addit lobi lea dattege gažaldat das,...  
	## De oaidná olmmoš dárbbaša go oalgebáddi luoitiluvvot. 

SELECT Pcle IF (-1 (V Neg) OR MOD-ASP)(0 go);                
	## Son jerre it go boađe?
	## Sámedikki eará orgánat mearridit ieža galget go čoahkkimat leat
	## rabas vai gitta.

SELECT Pcle IF (*-3 ("go" Pcle) BARRIER VFIN OR CS OR CP LINK -1 VFIN)
	(-2 CC)(-1 VFIN)(0 go);
	## Addet go máŋga dearvvašvuođabargiidjoavkku veahki ja 
	## fállojuvvo go divššohasaide birrajándororrun.

SELECT Pcle IF (-2 Inf)(-1 VFIN)(NOT -1 PrfPrc)(0 go);
	## Farisealaččat atne Jesusa čalmmis oaidnit áigu go buoridit sábbáhin.

SELECT Pcle IF (*-1 A OR Adv LINK *-1 ("nu") OR ("mihá") BARRIER CS)(0 go);
SELECT Pcle IF (*-1 ("seammá"))(NOT -1 VFIN LINK NOT *-1 VFIN BARRIER CS)(0 go);  
	## Nu guhkás go Gonagas mearrida... 
	## Giđđabuođut ráhkaduvvojit seammá vuogi mielde go geassebuođut. 
	## Seammá gusto go... # NOT -1 VFIN!

SELECT Pcle IF (*-1 COMPAR BARRIER NOT-ADJ OR CLB)(0 go); 
SELECT Pcle IF (*-1 Comp BARRIER CS OR CP OR CONTRA OR PUNCT LINK NOT 0 VERB)(0 go)
	(NOT *1 VFIN BARRIER CS OR CP);
	## Dát dáhpáhuvva dávjjit go dat, maid ovdal namuhin.
	## Láhkaásahus addá máŋgga áššis dárkilit njuolggadusaid go láhka.

SELECT Pcle IF (-1 Comp LINK *-1 COPULAS BARRIER V-NOT-AUX OR CS)
	(NOT *-1 V-PL1 OR V-PL2 OR V-PL3 BARRIER V-NOT-AUX)(0 go);
	## Dasgo mun in leat álggage heajut go dat mahkáš alla apoastalat.

SELECT Pcle IF (*-2 Comp BARRIER NPNH LINK NOT 0 Inf)(-1 A OR N)(0 go);
	## Dii han lehpet eanet veara go ollu cizážat.

SELECT Pcle IF (-1 A OR Adv)(NOT -1 Po)(0 go)(*1 Interr BARRIER VFIN);
	## Joavku ii válddahala dát gažaldagaid dobbelii go maid Sámi
	## vuoigatvuođalávdegoddi lea dahkan.

SELECT Pcle IF (0 go)(*1 CONTRA OR EOS BARRIER VFIN); 
SELECT Pcle IF (0 go)(1 ("ahte"));
	## Mus ii leat go okta beana.
	## Lea váttis áddet earáláhkai, go ahte maiddái duopmostuollu rahčá
	## čađat dainna váttisvuođain. 

SELECT Pcle IF (-3 N OR Pron)(-2 ("ahte"))(-1 VFIN)(0 ("go"));
	## Politihkas lei 1866 rájes sáhka ahte galggai go stivrregoahtit 
        ## gii oažžu eatnama láigohit stáhtas.

SELECT Pcle IF (-1 N LINK *-1 BOS BARRIER NOT-NP)(NOT -1 ("hávvi"))(0 go)
	(*1 VFIN BARRIER CS OR CP)(*1 ("?"));
	## Eatni goarrun gákti go dus lea die?

SELECT Pcle IF (-2 (N Gen) LINK *-1 BOS BARRIER NOT-NP)(-1 Po)(0 go)
	(*1 VFIN BARRIER CS OR CP);
	## Girdi mielde go don manat Helssegii?

# vai
# - -
# This one may be CS (vai = 'in order to') or CC (vai = 'or').
SELECT CC IF (0 ("vai"))(1 Neg)(2 CLB);
SELECT CC IF (0 ("vai"))(1 Qst);
	## Leago sámeálbmogis vuoigatvuođat vai eai, dahje...
	## ... vai leago ..

SELECT CC IF (0 ("vai"))(2 ("go"));
	## Dáhpáhuvvá go dat ođđa lágaid bokte, vai geahččaluvvo go gávdnat
	## čovdosiid bissovaš rievttis? 

SELECT CC IF (0 ("vai"))(NOT *1 VFIN BARRIER CS); 
	## ...čállogo journála elektrovnnalaččat vai báhpárii.

SELECT CC IF (*-1 Inf BARRIER VERB OR CLB)(0 ("vai"))(*1 Inf BARRIER VERB);
	## Buriid dahkat vai baháid dahkat.

# vaikko
# - - - -
SELECT Pron IF (0 ("vaikko"))
               (1 ("mii") OR ("gii") OR ("guhte") OR ("goabbá") OR
                  ("guhtemuš") OR ("makkár"));
  # "vaikko mii", etc. is a pronoun. Other vaikko cases are CS.
  # This should probably rather be taken care of in the preprocessor.

#SELECT CS IF (-1 BOS OR CS)(0 ("vaikko"))(*1 ("de"));
 # See rule in the next cycle.



 # Subjunctions are disambiguated in two subcycles. This is the second.

# =========== #
  CONSTRAINTS # 
# =========== #

# CS selection for specific CS candidates
# ---------------------------------------
SELECT CS IF (0 go);       # All other go's are CS.
SELECT CS IF (0 ("vaikko"));
SELECT CS IF (0 ("vai"));
	## Na geasset go bođii diehtiht ahte ii beasa skuvlii.
	## Ja go beasai fas seammá bargui.
	## Ásahusa journálaovddasvástideaddji sáhttá liikká rievdadit journála
	## vaikko gustojit vuolláičállojuvvon journálamerkestemiide. 
	## Vai miehtan galgá leat lágalaš, ferte divššohas leat ožžon...

# CS removal for specific CS candidates
# -------------------------------------
REMOVE CS IF (0 ("de"));
	## De leat máksán.



# ====================== #
# Disambiguating adverbs #
# ====================== #
        CONSTRAINTS      #
# ====================== #


# General adverb rules
# --------------------

SELECT Adv IF (-2 VFIN)(-1 ("nu"))(1 COMMA)(2 ("ahte"));
	## Lohku lassánii nu johtilit, ahte ...

SELECT Adv IF (*-1C VFIN-NOT-AUX BARRIER NOT-ADV OR PUNCT)(0 A-ATTR)
	(NOT 0 Inf OR N)(NOT 0 ("eanaš") OR ("eanas"))(NOT 1 N)
	(NOT 1 CC LINK 1 (A Attr) LINK *1 N BARRIER NOT-ADJ);     
	## Mannet ja guorahallet dárkilit dán máná hárrái!
 # Adv if postverbally

SELECT Adv IF (-2 VERB)(NOT -2 COPULAS)(-1 ("hui") OR ("nu"))
	(NOT *1 N BARRIER NP-BORDER);
	## Son čállá hui čábbát.

SELECT Adv IF (*-1 (V Sg1) OR (V Sg3) BARRIER COPULAS OR Nom OR CS OR CP OR PUNCT 
	OR REALCLB OR CONTRA)(0 (A Pl Nom))(NOT 0 Inf)(NOT 1 N)
	(NOT *-1 COPULAS BARRIER NOT-ADV-PCLE LINK *-1 Nom BARRIER CS);
	## Dál ii leat oktage Romssa universitehta guovddášhálddahusas gii
	## hálddaša sámegiela njálmmálaččat ja čálálaččat.
	## Jus lea geavatlaččat vejolaš, galget divššohasat...
 # The last condition covers cases where 0 is a comparative adjective.

SELECT Adv IF (*-1 COPULAS BARRIER NOT-ADV-PCLE)(NOT *-1 V-PL3 BARRIER CS)
	(NOT *-1 ("leapma" Dimin) LINK NOT *-1 VFIN BARRIER CS)	
	(0 (A Pl Nom))(NOT 0 Inf)(1 A);
	## Dát mearrádus lei formálalaččat gustojeaddjin gitta dassáigo...

REMOVE Nom IF (-1 (Pron Interr Nom))(0 Adv);
	## Iige leat duopmu mii čielgasit ja njuolga mearrida...

REMOVE (Pl Nom) IF (*-1 V-SG3 BARRIER VFIN OR CP OR CS OR CONTRA OR REALCLB)
	(0 Adv); 
REMOVE (Pl Nom) IF (0 Adv)(*1C V-SG3 BARRIER NOT-ADV-PCLE OR CLB);
	## ...gos mánná ássá dahje gaskaboddasaččat orru. 
	## Son dábálaččat ráhkkana dasa dálvvi mielde. 

REMOVE Gen IF (0 Adv)(NOT 0 TIME)(NOT *-1 Pr BARRIER NPNH)
	(NOT *1 Po BARRIER NP-BORDER)(NOT *1 N BARRIER NP-BORDER);  
	## Dát dagut leat rasistalaččat.
  
REMOVE Adv IF (-1 (N Gen) OR (Pron Gen))(NOT -1 Adv OR VFIN OR ("leat" PrfPrc))
	(0 Po);
 # Po if after Gen
 # but no hits in corpus?

REMOVE Adv IF (-1 ("leat"))(0 (A Attr))(NOT 0 ("eanaš") OR ("eanas"))
	(NOT 0 (@ADV-A))(1 N)(NOT 2 VERB);  
	## Bussás leat alit čalmmit.

SELECT Adv IF (0 (Pron Indef))(1 CS);
	## Dat mearridit seammás ahte stáhtaeiseválddit galget ovddidit 
	## doaibmabijuid. 

#REMOVE Indef IF (-1 BOS OR CC OR ("go"))(0 (Pron Indef));
#REMOVE Indef IF (-1 BOS OR CC OR ("go"))(0 (Pron Indef Loc));
#SELECT Adv IF (-1 BOS OR CC OR ("go") OR (PUNCT RIGHT))(0 (Pron Indef));
SELECT Adv IF (-1 BOS OR CC OR ("go") OR (PUNCT RIGHT))(0 ("seammás") OR ("seammas"));
	## Dat mearridit seammás ahte stáhtaeiseválddit galget ovddidit 
	## doaibmabijuid. 
	# # There could be problems with PUNCT RIGHT.
	
SELECT (Indef Loc) IF (*-1 LOC-VERB OR ("orrut") BARRIER NPNH)(0 (Pron Indef)) ;	
    ## Moai orro seammás.

SELECT Adv IF (NOT -1 CC LINK -1 Indef)(0 (Pron Indef))(1 COMMA)(2 CS);
	## Muhto geavadis ráddjejuvvui vuoigatvuohta veaháš, go ráfáiduhttima
	## spiehkastat mearralottiid ektui gáržžui.

REMOVE Po IF (-1C NOT-NP)(0 Adv)(1 NOT-NP OR CLB);  
	## Mun in leat mielde.

REMOVE (N Gen) IF (-1 VFIN)(0 Adv)(NOT 1 N or Po); 
	## Diet lea beare unnán.

REMOVE (Pron Interr) IF (NOT *-1 Neg BARRIER VFIN)(0 ("maid"))(1 CLB);
	## Dá lea reivve maid.


# Adverbs, not interjections
# --------------------------

REMOVE Interj IF (NOT -1 Interj OR BOS)(0 Adv);      ## Dat dinii bures doppe.


# Adjectives, not adverbs
# -----------------------

REMOVE Adv IF (NOT *-2 V-NOT-AUX BARRIER NOT-ADV-PCLE)(-1 (Pron Indef))
	(0 (A Pl Nom));
	## Ehpet go dii leat ollu mávssolaččabut go dat?

SELECT (A Pl Nom) IF (*-1 BOS OR REALCLB)(1 COPULAS LINK *1 (Pers Nom) BARRIER
	NOT-ADV-PCLE LINK NOT *1 Nom BARRIER CS);
	## Ávdugasat lehpet dii go sii bilkidit ja doarrádallet din.


# Adverbs, not comparative or plural adjectives
# ---------------------------------------------

SELECT Adv IF (-1 BOS OR CS)(0 (A Nom))(NOT *1 (N Loc) BARRIER NPNH)
	(*1 VFIN BARRIER CS OR CP LINK *1C NP-HEAD-NOM BARRIER CLB OR CS OR CP)
	(*1 V-NOT-COP BARRIER CS OR CP LINK *1C NP-HEAD-NOM 
	BARRIER CLB OR CS OR CP OR go);
	## Geavatlaččat ja ruđalaččat sáhttá dát garrasit čuohcat.
  # The non-copula verb does not have to be finite, but there must be a finite verb.

REMOVE Comp IF (-1 ("hui") OR ("nu") OR ("seammá"))(0 Adv);
	## Dat lea hui čielgasit.

REMOVE Comp IF (-1 VFIN)(0 Adv)(NOT 0 PxPl3)(1 Inf);
	## Ráđđehus áigu erenomázit giddet fuopmášumi sámegillii.

REMOVE (A Comp) IF (-1 N OR Pron)(1 VFIN)(NOT 1 N);
	## ...ja ahte dat dáhttu čielgasit berre doahttaluvvot.
 
SELECT Adv IF (*-1 NP-HEAD-NOM OR (A Nom) BARRIER NOT-ADV-PCLE)
	(NOT 0 (Pron Interr))(1 PrfPrc)(NOT 1 Actio LINK 1 ("veara"));
	## ...leatgo sámit historjjálaččat adnon eamiálbmogin...

SELECT Adv IF (-2C Adv)(-1 CC)(1 VERB);
	## ...son gii eanemus ja bistevaččat doallá oktavuođa divššohasain.

SELECT Adv IF (0 (A Pl Nom))(*1C V-1-2 BARRIER NOT-ADV-PCLE);
	## Persovnnalaččat in rávveše geange risikeret vel nuppes kr. 255.307. 

SELECT Adv IF (*-1 ("leat") BARRIER NOT-ADV-PCLE)(0 (A Pl Nom))
	(*1 PrfPrc OR (Actio Ess) BARRIER VERB OR CC OR CLB OR CS OR CP);
	## ...dallego sii ledje lobálaččat bohccuiguin orrumin boazogoahtunorohagas 
	## gosa gulle.

REMOVE Attr IF (1 CC)(NOT 2 Adv OR PRE-NP-HEAD);
	## ...čállogo journála elektrovnnalaččat vai báhpárii.

REMOVE (A Nom) IF (0 Adv)(1 (A Nom) LINK NOT *1 N BARRIER NPNH);
	## Geainnut ledje maid guhkit rabas.


# Adverbs, not verbs
# ------------------

REMOVE (V Ind) IF (-1 Nom LINK *-1 (V Ind) BARRIER NPNHA)(0 ADV-NOT-VERB);
	## Oar-julggiid atnet olbmot dušše dalle go bodni lea nu garas ahte...

SELECT Adv IF (-1 (Pron Interr))(1 CC)(2C Adv)(NOT *3 A BARRIER NOT-ADV);
	## ... dan dearvvašvuođabálvalussii, mii ollásit dahje osohahkii... 

SELECT Adv IF (-1 VERB)(1 CC)(2 ("seammá"))(3C Adv)(NOT *4 A BARRIER NOT-ADV);
	## Njuolggadusat fertejit gustot ollásit ja seammá láhkai...

REMOVE VERB IF (*-1 INF-VERB BARRIER CS OR CP)(0 Adv)(1 Inf);
	## ...go duohta dilálašvuođaid ii sáhte ollásit čilget.

REMOVE VGen IF (*-1 ("leat") BARRIER NOT-ADV-PCLE)(0 Adv)(*1 N BARRIER VERB);
	## Lea čađat dárbu ođasmahttit jorgalusaid.


# Verbs, not adverbs
# ------------------

REMOVE Adv IF (0 ("addit"))(1 N);
	## Gonagas sáhttá addit láhkaásahusaid dan birra.

REMOVE Adv IF (-1C N)(0 ("addit"))(NOT *1 VERB BARRIER NOT-ADV-PCLE); 
	## Divššohasbearráigehčči galgá govttolašvuođain addit 
	## sutnje dieđuid gii jearrá.

SELECT V IF (0 ("addit"))(NOT 0 Actio)(*1 Acc OR Ill BARRIER NPNHA);
	## Son gohčui addit niidii dan.
 

# Adverbs, not pronouns
# ---------------------
SELECT Adv IF (0 (Pron Indef Nom))(NOT 0 Attr LINK *1 N BARRIER NPNH)(NOT 0 Num)
	(*1 (N Nom) BARRIER CC OR CS OR CP OR CLB LINK NOT 0 Inf);
	## ...go Sámedikki áššemeannudeamis unnán buvttaduvvojit čállosat 
	## sámegillii.
	## Muhto veajan gal eanaš biddjojuvvojit dakkár tiŋggat mat... 
  

# Adverbs, not nouns
# ------------------

SELECT Adv IF (*-1 VFIN)(0 GEASSET)(NOT 1 Acc);  ## Son lei doppe geasset.
SELECT Adv IF (0 GEASSET)(*1 VFIN);              ## Dálvet son vulggii meahccái.

SELECT Adv IF (*-1 VFIN BARRIER NOT-ADV)(0 ADV-NOT-OTHER-POS)(NOT 0 Num);
	## Lea dušše okta Ipmil.
        ## Ja Biret-Elle lea easka skuvllas geargan.
       # Move to cycle 1a?

SELECT Adv IF (-1 CS)(0 ADV-NOT-OTHER-POS)(1 Nom LINK 1 VFIN OR Interr);
	## Lahka mearridii, ahte dušše sii guđet máhte...
 # A bit half-hearted the previous LINK).

REMOVE Interj IF (-1 BOS)(0 ADV-NOT-NAMES);             ## De fidnejin.
	## No hits in corpus.

SELECT Adv IF (-1 (N Nom))(0 ADV-NOT-NOUN);
SELECT Adv IF (-1 CC)(0 ADV-NOT-NOUN)(NOT *1 VFIN); 
	## Dáin áššiin sáhttet Sámedikkit várra nu gohčoduvvon friija
	## árvvoštallama mielde mearridit. 
	## Dás lea unnimus 150 jagis sáhka ja várra 200 jagis.

SELECT Adv IF (NOT 0 ("okta" Ess))(0 Ess)(NOT 0 ("mii" Foc));
	## Eatnagat bohtet nuortan ja oarjin.
	## ...amadet gaikut nisuid bajás oktan árvvohis rásiiguin.
 # This rule also hits "ihttin", "guhkkin", "muhtumin", "iehčanassii",
 # It is possible that some conditions will have to be added.  


# Nouns, not adverbs
# ------------------

Remove Adv IF (-1 Gen)(NOT -1 Interr)(0 (N Loc)) ;   
	## Son bođii njukčamánu loahpas.
   

# Rules related to specific adverbs
# ---------------------------------

# álggage
# - - - -
SELECT Adv IF (*-1 Nom BARRIER CS)(0 ("álggage"));
SELECT Adv IF (0 ("álggage"))(*1 Nom BARRIER CS OR CP);
	## In mun leat álggage heajut go dát.
	## Sis, geain eai lean álggage njuovvanbohccot,...

# álgu
# - - -
SELECT N IF (-1 Gen OR (A Attr))(0 ("álgu"));  
	## Prošeavtta álgu ii lean álki.

# beare
# - - - 
SELECT Adv IF (0 ("beare"))(1 ("ollu"));  
	## Orohagas leat beare olu bohccot.

# beallái
# - - - -
 # The Po/Pr seems to be taken care of by the general rules in an earlier block.
 # But rules are needed that distinguish between N and Adv readings.

# čađat
# - - -
SELECT Adv IF (-1 VFIN)(NOT -1 Pl3 OR Neg)(0 ("čađat"));
SELECT Adv IF (0 ("čađat"))(1 VFIN)(NOT 1 Pl3); 
	## Lea čađat dárbu ođasmahttit jorgalusaid.
	## Son muitala ahte dii čađat jurddašehpet min iluin.

# dadjat
# - - - -
SELECT Adv IF (-1 COMMA)(0 ("dadjat"));
	## Muhto joddobuođuiguin bivdojuvvo luossa miehtá deanu, dadjat
	## njálmmi rájes gitta gierragii.

SELECT Adv IF (0 ("dadjat"))(1 ("juo"));
	## Háhkan lea šaddan dadjat juo cieggan riektedilli.

# dainna
# - - - -
SELECT Adv IF (0 ("dainna"))(1 go);
	## ...muhto sáhttá vuordit dainna go ferte vuoruhit dikšuma. 
 
# danin
# - - - 
SELECT Adv IF (0 ("danin"))(NOT *1 (N Ess) BARRIER NPNH)
		(NOT *-1 COPULAS BARRIER NOT-ADV)(NOT 1 CLB)
		(NOT *-1 OPRED-VERB BARRIER NOT-ADV)
		(NOT *1 OPRED-VERB BARRIER NOT-ADV);
        ## Danin badjánii jurdda lága ráhkadit.
	## Návetruitu lávejit danin atnit. # Essive - hence the restrictions.

SELECT Ess IF (*-1 COPULAS BARRIER NOT-ADV)(0 ("danin"))(1 CLB); 
	## ...jos fierpmit leat danin,...

SELECT Ess IF (0 ("danin"))(*1 OPRED-VERB BARRIER NOT-ADV-PCLE);
SELECT Ess IF (0 ("danin"))(*-1 OPRED-VERB BARRIER NOT-ADV-PCLE);   
	## Návetruitu lávejit danin atnit. 
	##

SELECT Adv IF (0 ("danin")); 
	## Sierralágan diehtojuohkingeahččaleamit leage danin dehálaččat.

# If there are more essives, give rules for them.

# dassá
# - - -
REMOVE VFIN IF (0 ("dassá"))(1 go)(*2 VFIN BARRIER CS);
	## Dassá go son jámii, lea guhkes áigi.

REMOVE VFIN IF (-1 TIME)(0 ("dassá"));
	## Vuosttažettiin adnui sámi kultuvra árvvoheabbon go dáža kultuvra 
	## 100 jagi dassá.

# dákko
# - - - 
REMOVE (V Imprt Prs Sg2) IF (0 ("dákko"))(*1 VFIN);
REMOVE (V Imprt Prs Sg2) IF (*-1 VFIN)(0 ("dákko"));
	## ... oaiviliid ja ákkaid bokte mat dákko ovddiduvvojit,...
	## Hammerfeastta suohkanbáhppá Harboe lea dákko maid, nu go 
	## eará oktavuođain, buorre gáldu.
  # Alternatively, on could remove all imperatives. Here, I remove the
  # imperative reading of "dággut" if there is a finite verb somewhere
  # else.
 
# duo
# - -
SELECT Adv IF (NOT -1 Dem or (A Attr))(0 ("duo"));
	## Áddjá go duo lea boahtime?

# duođaid
# - - - -
SELECT Adv IF (-1 BOS OR PUNCT OR CLB)(0 ("duođaid"))(1 COMMA);
	## Duođaid, mun cealkkán didjiide:...

# dušše
# - - - -
 # There is a rule "Lea dušše okta Ipmil" for the typical dušše cases.

REMOVE N IF (0 ("dušše")); 
REMOVE A IF (0 ("dušše"));
	## Vihtta bivdovuogi mun gal dušše namahin,...
	## Oar-julggiid atnet olbmot dušše dalle go bodni lea nu garas...  
 # Brutal, but the noun and the adjective are almost always "dušši".

REMOVE VFIN IF (*-1 VFIN BARRIER CP OR CS)(0 ("dušše"));
REMOVE VFIN IF (0 ("dušše"))(*1 VFIN BARRIER CP OR CS);
	## Nu eai dieđus dárbbaš skáhppot birra buot gal, dušše lasi 
	## dan sadjái mii mieská.
	## ...go dušše áibbas moatti sajis heive meardebuođđu adnojuvvot.

REMOVE VFIN IF (-1 ("leat"))(0 ("dušše"));
	## Leamaš dušše vihtta dorski nuhtiin.

REMOVE Actor IF (0 ("duššat")); 	# Make a set for non-agentive verbs?
	## Oktagaslaš olbmo lonuheami dohkálašvuohta lea stáhtii geavadis
	## duššin.

SELECT Adv IF (-1 BOS OR CS)(0 ADV-NOT-VERB);
SELECT Adv IF (-2 BOS OR CS)(-1 Pcle)(0 ADV-NOT-VERB);
	## Dušše daid dieđuid, mat gusket konkrehta várraárvvoštallamii,
	## sáhttá čuoldit geahččanlobis.
	## No hits for the last rule?

# eambbo
# - - - - 
SELECT A IF (0 ("eambbo"))(1 N);
	## Dingojitgo eambbo biergu?

SELECT Adv IF (0 ("eambbo"));
	## Gittabivdosiin ii leat lohpi bivdit deanu eambbo go beallái.

# eanaš/eanas
# - - - - - - -
SELECT Adv IF (*-1 NP-HEAD-NOM BARRIER CLB OR CS OR CP)
	(0 ("eanas") OR ("eanaš"))(NOT *1 N BARRIER NPNH);
	## Muhto báinnan dat gal eanaš lea.

SELECT Adv IF (*-1 COPULAS BARRIER NOT-ADV-PCLE LINK *-1 NP-HEAD-NOM BARRIER 
	NOT-ADV-PCLE)(0 ("eanaš") OR ("eanas"))(NOT 1 N LINK 1 N);
	## Áššemeannudeapmi lea eanaš dárogillii.
	## Sámegiella lea eanaš oktavuođain unnitlogugiella. (NOT 1 N LINK 1 N)!

SELECT Adv IF (*-1 NP-HEAD-NOM BARRIER NOT-ADV-PCLE LINK NOT 0 PrfPrc 
	LINK *-1 COPULAS BARRIER NOT-ADV-PCLE)(0 ("eanaš") OR ("eanas"));
	## Oahpahusossodagas lea áššemeannudeapmi eanaš dárogillii.

SELECT Adv IF (-1 COPULAS)(0 ("eanas") OR ("eanaš"))(1 N)(NOT 1 TIME)
	(*2 CS OR EOS BARRIER N);  
	## Dál lea eanaš ovttamielalasvuohta, ahte...
	## Ovdal ledje eanas vuonaid bivdán, muhto go skoavdefatnasat bohte... 
	## Ealus lea eanaš álddut.

SELECT Adv IF (0 ("eanaš") OR ("eanas"))(1 PrfPrc);
	## Nuppádassii leat čielga gáibadusat eaktodáhtolaš oskui, mii 
	## guoská giddodahkii, eanaš vuođđoduvvon registrerema oktavuođas. 

# eanet
# - - -
SELECT Adv IF (0 ("eanet"))(1 CC)(2 ("eanet"))(3 A);
SELECT Adv IF (-2 ("eanet"))(-1 CC)(0 ("eanet"))(1 A); 
	## Dutkangaskkusteapmi ja jearru dutkanbohtosiidda šaddá eanet ja 
	## eanet guovddáš oassi dutkama árgabeaivvis.

SELECT Adv IF (0 ("eanet"))(1 ("uhcit") OR ("unnit"));
SELECT Adv IF (-1 ("uhcit") OR ("unnit"))(0 ("eanet"));
	## ...muhto dál lea eanet uhcit jávkan.  
	## Go vierroopmodaga geavaha ja eaiggát unnit eanet čielgasit
	## dasa leat miehtán,...

SELECT Adv IF (0 ("eanet"))(NOT 0 Pron)(NOT 1 Dem OR N OR A);
	## Guovlu atnigoahtá olu eanet.
	## Dat lea eanet go 10.
 # Ambiguous: ## ...hálida universitehta almmuhit eanet dieđalaš artihkkaliid
	      ## sámegillii.
 # - hence the condition (NOT 1 A).

SELECT Adv IF (0 ("eanet"))(1 A-CASE)(NOT *2 N BARRIER NPNH);
	## Báhkkodeamis ferte leat sáhka geavaheamis mii lea oktoráđđejeaddji
	## dahje ainjuo čielgasit eanemus čalbmáičuohcci.
 # But the A-CASE is often a wrong analysis.

SELECT Attr IF (0 ("eanet"))(1 Dem OR N);
	## Jos galgá eanet dutkiid ožžodit dán suorgái,... 
	## Noađđebealli oažžu deaddit eanemus viegu.
	## ...okta dahje eanet dain eará namuhuvvon doaimmain.

# erenoamáš
# - - - - - -
SELECT Adv IF (0 ("erenoamáš"))(1 A);
	## Doaibmabidju lihkostuvvui bures ja lei erenoamáš hutkkálaš.

# earenoamážit/erenoamážit
# - - - - - - - - - - - - - -
SELECT Adv IF (0 ("earenoamážit") OR ("erenoamážit"))
	(*1 Loc OR Ill OR Com BARRIER NPNHA);
	## ...ja earenoamážit dán gaskavuođas servodatlaš beliid.

SELECT Adv IF (0 ("earenoamážit") OR ("erenoamážit"))
	(*1 NP-HEAD-GEN BARRIER NPNHA LINK 1 Po);
	## ...earenoamážit gáhtten dihte Goahtečoruvuona gođđobáikkiid...
   
# easkka
# - - - -
SELECT Adv IF (0 ("easkka"))(*1 TIME BARRIER NPNHA);
	## ...mii easkka maŋimuš áiggiid lea jávkágoahtán. 
	## Dat lei boahtán ruoŧagielain easkka j. 1863.
 # Ambiguous: ## Dat čielgá dálá oasálaččaide easkka proseassas. 

SELECT Adv IF (0 ("easkka"))(1 Num OR ("maŋŋel"));
	## Muhto easkka 1898:s dovdagohte Láhppiolbmot dan mii maŋŋelis bođii.
	## ...easkka maŋŋel 1529 ásaiduvve olbmot duođas báikkiide.

# goas
# - - -
SELECT Adv IF (0 ("goas"))(NOT 1 go)(*1 ("<?>"));         
SELECT Adv IF (0 ("goas"))(1 CC)(2 CP);
	## Goasbat Máret fuobmái fas dohko vuolgit?
	## Goas ja mo dearvvašvuođaveahki lea addojuvvon,... 

# goassige
# - - - - -
SELECT Adv IF (*-1 Neg BARRIER VFIN)(0 ("goassige"));
	## Ii leat stáhtas várra goassige leamaš ruđalaš ávki dán ámmahis. 

# gosa
# - - -
SELECT Adv IF (0 ("gosa"))(*1 VFIN BARRIER CLB OR CS);
       ## Gosa don čihket dan?
       ## Son oinnii báikkiid gosa juo ledje ásaiduvvan. 
 # Here, the rule giving the verbal reading of "gosa" is missing. It must
 # be added.

# gosage
# - - - -
SELECT ("gosa" Foc) IF (*-1 Neg BARRIER VFIN-NOT-IMP);
	## Mii eat sáhttán vuolgit gosage.

SELECT ("gosa" Foc) IF (*-1 Qst BARRIER CS OR CP);
	## Leatgo don vuolgime gosage?

SELECT ("gosa" Foc) IF (*-1 ("go" Pcle) BARRIER CS OR CP);
	## Leat go don vuolgime gosage?

# gusto
# - - -
SELECT Adv IF (0 ("gusto"))(1 VFIN);
	## Don gal gusto beroštat čuovvut.

SELECT Adv IF (-1 VFIN)(NOT -1 Neg)(0 ("gusto"));           
	## Diet lea gusto ávkkálaš.

REMOVE VGen IF (0 ("gusto"));
	## Jeahkálat nohket gal dálvvis gusto.

# hárve
# - - -
SELECT Adv IF (*-1 VFIN BARRIER CS OR CP LINK NOT 0 Neg)(0 ("hárve"));
	## Prinsihpalaš gažaldagat leat leamaš baicce hárve Norgga duopmostuoluin.

# ikte
# - - -
SELECT Adv IF (0 ("ikte"))(*1 (V Prt));         ## Ikte lei hui buolaš.
SELECT Adv IF (*-1 (V Prt))(0 ("ikte"));        ## Ikte lei hui buolaš.

# jogo
# - - -
SELECT Qst IF (0 ("jogo"))(NOT *1 ("dahje"));
	## Jogo gikse dat guolit?

# juo
# - -
SELECT Interj IF (-1 BOS)(0 ("juo"))(1 COMMA);
	## Juo, dat gal heive.

# láhkai
# - - - -
SELECT N IF (-1 Dem OR Attr)(0 ("láhkai"));
	## Seammá láhkai go váldojournála ja lassijournála geavaheami oktavuođas...

# maid
# - - -
SELECT Adv IF (-1 INF-VERB)(0 ("maid"))(*1 Inf BARRIER CS OR CP);
	## Mun háliidan maid mannat.

SELECT Adv IF (-1 ("leat"))(0 ("maid"))(*1 PrfPrc BARRIER ("leat") OR CS OR CP);
	## Lea maid lassin gávnnahan metodalaččat váttisin mearridit...

SELECT Adv IF (-1 ("leat"))(0 ("maid"))(*1 (A Nom) BARRIER NOT-ADV);
	## Geainnut ledje maid guhkit rabas.

SELECT Adv IF (-1 N LINK *-1 BOS OR CS BARRIER NPNHA)(0 ("maid"))
	(NOT *1 VFIN LINK *1C VFIN BARRIER CS OR CP);
	## Suomas maid lei dilli váttis.
	## Nubbi maid fuobmáimet lei ahte... (Interr, hence the last condition.) 

SELECT Adv IF (*-1 V-PL3 BARRIER NOT-ADV-PCLE)(0 ("maid"))(*1 NP-HEAD-PL-NOM 
	BARRIER NPNHA LINK NOT *1 V-PL3 BARRIER NOT-ADV);
	## Riggodagaide gullet maid fuođđot ja lottit.

SELECT Interr IF (0 ("maid"))(*1 VFIN BARRIER CS OR CP LINK *1 COMMA 
	BARRIER CS OR CP LINK 1 VFIN)(NOT -1 (N Pl) LINK *2 V-PL3 BARRIER NOT-ADV);
	## Dan maid dalle dárbbaša fievrridit, ferte olmmoš noađuhit.

SELECT Interr IF (*-1 VFIN BARRIER CS OR CP LINK 0C VERB)(0 ("maid"))
	(*1 VFIN BARRIER CLB OR CS OR CP)
	(NOT *1 Inf BARRIER VERB OR Nom LINK *-1 INF-VERB BARRIER VERB)
	(NOT -1 ("leat" Pl3) LINK *2 (N Pl Nom) BARRIER NPNHA);
	## Don galggat doallat dan maid leat vurdnon Herrái.

SELECT Adv IF (-2 BOS OR CS)(-1 (Pron Pers) OR N)(0 ("maid"))
	(NOT *1 VFIN LINK *1C VFIN BARRIER CS OR CP);
	## Sidjide maid lei Ruija.

SELECT Adv IF (-1 COPULAS LINK 0 Pl3)(0 ("maid"))(*1 (N Pl Nom) BARRIER NPNHA);
SELECT Adv IF (-1 Loc LINK *-1 COPULAS BARRIER NPNHA OR VERB)(0 ("maid"))
	(*1 (N Nom) BARRIER NPNHA); 
	## Romssa universitehtas leat maid eará plánat álggahit fálaldagaid.
	## Leago stáhtas maid geatnegasvuohta doarjut doalahit dán kultuvrra.

SELECT Adv IF (*-1 CS BARRIER VFIN OR CLB)(NOT -1 ("go"))(0 ("maid"))
	(*1 VFIN LINK NOT *1 VFIN);
	##... nu ahte dat maid siskkilda ávnnalaš kultureavttuid.    

SELECT (Pron Interr) IF (-1 BOS OR PUNCT)(1 Nom)(*1 ("?"));
SELECT (Pron Interr) IF (1 CC)(2 (Pron Interr));
 	## Maid mii dál vihtaniiguin?
	## Lea registreren maid ja man ollu lea borran. 

SELECT Adv IF (0 ("maid"))(NOT *1 VFIN);
SELECT Adv IF (0 ("maid"))(NOT *1 VFIN BARRIER CC OR CS OR CP);
	## Dán oktavuođas čujuhuvvo maid oanáduslistui ja gáldolistui.
	## Sápmelaččat oasálaste maid dasa, muhto lassin ledje sis vel eará 
	## resursat.

SELECT Adv IF (*-1 INF-VERB BARRIER VERB)(0 ("maid"))(*1 Inf BARRIER VERB);
	## Sápmelačcat galge maid vearu máksit Danmárku-Norgii. 
 
SELECT Adv IF (*-1 (V Sg3) BARRIER NOT-ADV-PCLE LINK NOT *-1 Nom BARRIER CS)
	(0 ("maid"))(*1 NP-HEAD-SG-NOM BARRIER NPNHA);
	## Dattege bohciida maid sierra gažaldat, 

SELECT Interj IF (-1 BOS)(0 ("maid"))(1 COMMA);
	## Maid, iigo leat boahtán?

# manne
# - - -
SELECT Adv IF (-2 BOS OR CS OR CLB OR PUNCT)(-1 CC)(0 ("manne"))(*1 VFIN);     
SELECT Adv IF (*-1 BOS OR CS OR CLB OR PUNCT BARRIER NOT-ADV-PCLE)(0 ("manne"))
	(*1 VFIN BARRIER CC OR CS OR CP LINK NOT 0 Inf); 
	## Ja manne moraštehpet biktasiid dihtii? 
	## Naba dii, manne dii ieža rihkkubehtet Ipmila báhkkomiid?
	## Sii manne isideaset lusa ja muitaledje buot. # BARRIER CC!

SELECT Adv IF (*-1 BOS OR CS OR CLB OR PUNCT BARRIER NOT-ADV-PCLE)(0 ("manne"))
        (*1 LEAT BARRIER CC OR CS OR CP LINK *1 (N Pl Nom) BARRIER CC OR CS OR CP OR CLB OR PUNCT);
	## Manne dis leat diekkár bahás jurdagat din váimmuin?

SELECT Adv IF (*-1 VFIN BARRIER CS OR CP OR CC OR CLB LINK NOT 0 Neg)(0 ("manne"));
	## Jos oktage jearaš dudnos manne doai dahkabeahtti dan,...

# mielas
# - - - -
REMOVE Adv IF (-1 Gen)(0 ("mielas"));   
	## Mu mielas don sáhtát mielas boahtit.

# muhtumin
# - - - - -
SELECT Adv IF (0 ("muhtumin"));
	## Muhtumin lea deaŧalaš čállit journála dalán.

# naba
# - - -
SELECT Adv IF (0 ("naba"))(NOT 1 V-SG3);
       ## Naba don, man ollu don leat dahkan.
  # "Naba" is a proper noun also, but hardly relevant in a Sami context.
  # Thus, it could perhaps be deleted in all contexts. The intended context
  # here is one of the subject, but it is somewhat halfheartedly written.

# ođđasit
# - - - -
SELECT Adv IF (-1 ("fas"))(0 ("ođđasit"));
	## Elia gal boahtá dahkat buot fas ođđasit.

SELECT Adv IF (*-1 Nom BARRIER CS OR CP)(0 ("ođđasit"))
	(*1 V-NOT-AUX BARRIER CS OR CP);
	## Eat mii sáhte ođđasit ásahit vuođđu.

# oktan/ovttas
# - - - - - - -
SELECT Adv IF (0 ("oktan") OR ("ovttas"))(*1 Com BARRIER NP-BORDER);
	## Divššohas lea vuoigatvuohta iežas journála geahččat oktan
	## mildosiiguin. 

SELECT Adv IF (-1 (Pl Nom))(0 ("ovttas"))(1 V-PL3);
	## Ferte oaidnit ollislaš gova maid soahpamušat ovttas vuođđudit.
 
SELECT Adv IF (*-1 NUMERALS BARRIER NPNH LINK 0 Nom)(0 ("ovttas"))
	(*1 V-PL3 BARRIER NOT-ADV-PCLE);
	## Ferte oaidnit ollislaš gova maid dát guokte soahpamuša ovttas vuođđudit.

# oktii
# - - -
REMOVE V-SG3 IF (*-1 V-SG3 BARRIER CS OR CP)(-1 ("dušše"))(0 ("oktii"));
	## Vuoigatvuohta gusto dušše oktii.

REMOVE V IF (*-1 VFIN BARRIER CC OR CS OR CP)(0 ("oktii"));
	## Vuoigatvuohta gusto dušše oktii seammá dilálašvuođa ovddas.

SELECT Adv IF (-1 BOS OR CS)(0 ("oktii"))(1 go);
	## Oktii go leimmet mannamin rohkadallanbáikái, bođii min ovddal 
	## muhtun šlávvanieida. 

# okto
# - - -
SELECT Adv IF (*-1 VFIN BARRIER CS OR CP OR CC)(0 ("okto"));
SELECT Adv IF (0 ("okto"))(*1 VFIN BARRIER CS OR CP OR CC); 
	## ...ahte fertejit leat okto geavahan dahje čielgasit eanemusat
	## geavahan,
	## ...maid badjel sii ovdal okto leat ráđđen.

# ollásit
# - - - -
SELECT Adv IF (-1 VERB)(NOT -1 INF-VERB)(0 ("ollásit" Adv));
	## ...ahte historjjálaš ovdáneapmi čielggaduvvo ollásit.  

SELECT Adv IF (0 ("ollásit" Adv))(*1 Loc OR Ill BARRIER NPNHA);
	## Dát eatnamat eai gula datte ollásit dábálaš njuolggadusaide
	## stáhta eatnamiid namas. 

SELECT Adv IF (0 ("ollásit" Adv))(*1 NP-HEAD-GEN BARRIER NPNHA LINK 1 Po);
	##

# oppa
# - - -
SELECT Adv IF (0 (@ADV-A))(0 ("oppa"));
	## Dat lea oppa juhkkis olmmoš.

# rádjái/rádjai
# - - - - - - -
SELECT Po IF (-1 Num)(0 ("rádjai") OR ("rádjái"));
	## Njuolggadusat nággolága § 204:s § 209 rádjái ožžot seammá geavahusa.
 # This rule should be extended to all postpositions likely to occur after
 # number expressions. Todo: Make a set.

# siivot
# - - - -
SELECT Adv IF (-1 VFIN)(0 ("siivot"));
	## Vácce siivot amat boktit máná.

# sullii
# - - - -
SELECT Adv IF (0 ("sullii"))(1 Num);    
	## Bušeahtas lea sullii 650.000 ruvnno leamaš várrejuvvon.

 # This rule must be generalised on both conditions: "sullii" must be extended
 # to a set of adverbs denoting quantification, and Num must be extended to
 # more measures (or parallel rules must be written).

SELECT Adv IF (-1 ("leat"))(0 ("sullii"));
	## Spahká lea sullii dušše suorbmabeali asu gežiid bokte.
 # The idea is that "leat" doesn't subcategorise for illatives.

# unnán
# - - - 
SELECT Adv IF (0 ("unnán"))(1 VFIN OR PrfPrc)(NOT 1 N);
	## Eanaš duopmárat unnán dovdet eamiálbmogiid historjjá.
	## Dat orru oppanassii unnán čilgejuvvon 1800-logu gaskamuttu.

# uhcit/unnit
# - - - - - - 
SELECT Adv IF (-1 ("eanet"))(0 ("uhcit") OR ("unnit"));
SELECT Adv IF (0 ("uhcit") OR ("unnit"))(1 ("eanet") OR ("eambbo"));
	## Dál lea eanet uhcit jávkan.
	## Go eaiggát eanet unnit čielgasit dasa lea miehtán,... 

# várra
# - - -
SELECT Adv IF (0 ("várra"))(*1 (N Sg Nom) BARRIER CC OR CLB OR CS OR CP);
	## Mearkkašahttimus Ruonáeatnama ruovttustivrii dál várra lea váttisvuohta 
	## ruhtadit buot áššesurggiid hálddašeami.

SELECT Adv IF (*-2 A BARRIER WORD)(-1 CC)(0 (várra))
	(*1 PrfPrc BARRIER NOT-ADV-PCLE); 
	## 

SELECT Adv IF (-1 CLB)(0 ("várra"))(NOT *1 V-SG3);
	## Sápmelaččat geat ledje birgegoahtán dušše boazudoaluin, várra
	## ledje buot friddjamusat.

# vehážiid
# - - - - -
SELECT Adv IF (0 ("vehážiid"))(1 ("vehážiid"));
SELECT Adv IF (-1 ("vehážiid"))(0 ("vehážiid"));
	## ...mii easkka maŋimuš áiggiid lea vehážiid vehážiid jávkkagoahtán.

# visot
# - - -
SELECT Adv IF (0 ("viso"))(NOT 1 (N Pl));
	## Ii leat vel visot čohkkejuvvon.
	## Visot dan maid NRK sádde digitála fierpmis sáhttá maid gullat Nettradios.
 # This is not a particularly good rule for 'visot'.

# vuhtii
# - - - - 
SELECT Adv IF (-1 ("váldit"))(0 ("vuhtii"));
	## ...muhto ferte váldit vuhtii máná agi ja láttasvuođa.



# ======================= #
# Disambiguating pronouns #
# ======================= #
       CONSTRAINTS        # 
# ======================= #

# Interrogative pronouns
# ======================

SELECT Interr IF (-1 BOS OR (":"))(*1 ("?"));
SELECT Interr IF (-2 BOS)(-1 Pcle)(*1 ("?"));
SELECT Interr IF (-2 BOS OR CLB)(-1 PUNCT);
	## Maid don doaivvut, mun gielistan?
	## Na maid don diŋgojit?
	## Man uhccán osku dus lea!
 # The last example is an exclamation. Hit because no ? required.

REMOVE Interr IF (-1 Interr); 
	## Dat lea čuonžža, mii maid gorgŋu detnui čakčageasi.

SELECT Interr IF (NOT -1 V-PL1)(0 ("mii"))
        (*1 V-SG3 BARRIER V-PL1 LINK NOT 0 (Pl1));
	## Dát ii guoskka dikšui mii boahtá psykiatriija vuollái.

SELECT Interr IF (*-1 V-NOT-AUX BARRIER CS OR CP LINK *-1 NP-HEAD-NOM 
	BARRIER CS OR CP)(*1C NP-HEAD-NOM BARRIER CS OR CP);
	## ...ahte olbmot besset oaidnit maid sii sáhttet vuordit.

SELECT (Pron Interr Sg) IF (-1C NP-HEAD-SG OR (Card Sg))
	(NOT *-2 NUMERALS BARRIER NPNH);
SELECT (Pron Interr Pl) IF (-1 NP-HEAD-PL)(NOT -1 NP-HEAD-SG);
	## ...go lei sáimmaiguin bivdán jávrris maid stáhta lei láigohan. 
	## Doaibma gullá doaimmaide maid ulbmil lea movttiidahttit sámi nuoraid... 

SELECT Pl IF (*-1 NUMERALS BARRIER NPNH LINK NOT 0 Ord)(0 (Pron Interr));
	## Davviriikkain leat guokte álbmoga maid atnit álgoálbmogin.

SELECT Sg IF (NOT -3 CC)(-2C NP-HEAD-SG)(-1 COMMA)(0 (Pron Interr));
SELECT Pl IF (-2C NP-HEAD-PL)(-1 COMMA)(0 (Pron Interr));
	## Deaŧalaš lea ahte čállojuvvojit dakkár gillii, maid buot bargit
	## áddejit. 
        ## Dat divššohasat, geaid leat geavadis vejolaš joksat,...

SELECT Sg IF (-2 Sg LINK NOT *-1 NUMERALS BARRIER NPNH)(-1 Po)(0 (Pron Interr));
SELECT Sg IF (-3 Sg LINK NOT *-1 NUMERALS BARRIER NPNH)(-2 Po)(-1 COMMA)
	(0 (Pron Interr));
	## Mandáhta mielde maid Sámi vuoigatvuođalávdegoddi lea addán...
	## ...ja dan dearvvašvuođaveahki birra, maid son oažžu,... 

SELECT Pl IF (-2 Pl)(-1 Po)(0 (Pron Interr));
SELECT Pl IF (-3 Pl)(-2 Po)(-1 COMMA)(0 (Pron Interr));
	## ...ahte resurssaid ektui maid livččii dárbbašan atnit... 
	## Sivaid geazil, maid berre čuovvolit,... 

SELECT Pl IF (-1 Po LINK *-1 NUMERALS BARRIER NPNH)(0 (Pron Interr));
SELECT Pl IF (-2 Po LINK *-1 NUMERALS BARRIER NPNH)(-1 COMMA)(0 (Pron Interr)); 
	## No hits? 
	## No hits?

REMOVE Pl IF (*-1 BOS OR CS BARRIER NOT-ADV-PCLE)(0 ("mii" Pron Interr))
	(NOT *1 (N Pl Acc) OR (N Pl Gen) BARRIER NPNH);
	## Maid don doaivvut, mon gielistan?

REMOVE Pl IF (*-1 VERB BARRIER NOT-ADV-PCLE LINK NOT -1 (N Pl))
	(0 ("mii" Pron Interr))(NOT *1 (N Pl Acc) OR (N Pl Gen) BARRIER NPNH);
	## Ii nagot áddet maid miehtán sisdoallá.

REMOVE Pl IF (NOT *-2 ("mii" Pron Interr) BARRIER CS)(-1 CC)(0 ("mii" Pron Interr))
	(NOT *1 (N Pl Acc) OR (N Pl Gen) BARRIER NPNH);
	## Daidda heŋgejuvvojit gurpasat, ja maid dál de guottehit lea áigumin.

REMOVE Sg IF (-2 (N Pl))(-1 Inf OR PrsPrc)(0 ("mii" Pron Interr));
	## Ovddasteaddjis lea vuoigatvuohta dieđuid geahččat maid divššohas
	## biehttaluvvo oaidnimis.

SELECT (Sg Gen) IF (0 ("mii" Interr))(1 A)(NOT 1 Comp OR Superl);
SELECT (Sg Gen) IF (0 ("mii" Interr))(1 Adv)(NOT 1 Superl);
	## Man guhkki lea skuvlii?
	## It go don gula man garrasit sii sivahallet du?
	
SELECT Gen IF (0 (Pron Interr))(1 Po); 
	## Son gean birra dieđut leat,...  

REMOVE Acc IF (0 (Pron Interr))(*1 NP-HEAD-NOM BARRIER NPNH LINK *1 COPULAS BARRIER
	NOT-ADV-PCLE LINK NOT *1 PrfPrc BARRIER CS OR CP);
	## Doaibma gullá doaimmaide maid ulbmil lea movttiidahttit sámi nuoraid...



# Reflexive pronouns
# ==================
  # iežas is Gen or Acc
  # ieža is Pl or Du

# Reflexive iežas
# ----------------
SELECT Gen IF (-1 Acc)(NOT -1 PrfPrc)(0 ("ieš" Gen))(1 HNOUN);
	## ... muhto it fuomáš hirssa iežat čalmmis?

# Emphatic ieš
# -------------
  # Sg ieš is unique, there is homonymy Du/Pl for ieža.
 # Problem: Prs Du1 = Prt Pl3.

SELECT Pl IF (*-1 V-PL OR (V Imprt Pl2) OR MII-PERS OR DII OR SII OR 
	(Pron Interr Pl Nom) BARRIER CS OR CP OR PUNCT)(0 ("ieš" Pron Refl Pl Nom))
	(NOT *1 V-DU BARRIER NOT-ADV-PCLE);
	## Allet dubme, amadet ieža dubmejuvvot!
	## ...muhto leat maiddái muhtumat geat ieža leat dahkan iežaset 
	## dohkketmeahttumin...

SELECT Pl IF (0 ("ieš" Pron Refl Pl Nom))(*1 V-PL BARRIER CS OR CP);
	## ...ja didjiide mihtiduvvo seammá mihtuin mainna ieža ge mihtidehpet.

SELECT Du IF (*-1 V-DU OR MOAI OR DOAI OR SOAI BARRIER CS OR PUNCT)
	(0 ("ieš" Pron Refl Du Nom));
	## Doai galgabeahtti ieža mannat.

SELECT Du IF (0 ("ieš" Pron Refl Du Nom))
	(*1 V-DU OR MOAI OR DOAI OR SOAI BARRIER CS OR CP);
	## Ieža soai muitaleigga mii lei dáhpáhuvvan mátkkis.

          

# Reciprocal pronouns
# ===================

SELECT Recipr IF  (1 Recipr); 
SELECT Recipr IF (-1 Recipr); 
	## ...de dadje guhtet guoibmáseaset:...  
	## Gielddat berrejit ásahit fásta dulkaveaga, kánske ovttasbarggus nubbi
	## nuppiin.



# =========== #
  CONSTRAINTS
# =========== #

REMOVE Recipr ;
	## Jos dajan nubbái: Mana! de son manná.



# Numerals
# ========
 # These rules have to precede the rules for indefinite pronouns, or the Ord 
 # reading of "nubbi" will be removed where it should remain.


# General rules
# -------------

REMOVE Nom IF (-1 ABBR OR ACR OR ("§") OR ("paragráfa") OR ("kapihttal") OR 
	("kapihtal") OR ("jahki") OR GEASSET)(NOT -1 ("nr"))(0 Num);
	## 
	
#REMOVE Nom IF (-1 ABBR OR ACR OR ("§") OR ("paragráfa") OR ("kapihttal") #OR 
	#("kapihtal") OR ("jahki") OR GEASSET)(0 Num);

REMOVE Acc IF (-1 ABBR OR ACR OR ("§") OR ("paragráfa") OR ("kapihttal") OR 
	("kapihtal") OR ("jahki") OR GEASSET)(0 Num);
	
REMOVE Gen IF (-1 ABBR OR ACR OR ("§") OR ("paragráfa") OR ("kapihttal") OR 
	("kapihtal") OR ("jahki") OR GEASSET)(NOT -1 ("b" ABBR))(0 Num);
	## Rievdaduvvon lágain geassemánu 15. b. 2001 nr 93.
	## ...maid KITH gárvvistii ja almmuhii geasset 2001.
 # These rules leave only the caseless Num reading. # Well, now we have case everywhere
 # SELECT Nom is much stronger than

SELECT (Num Acc) IF (-1 (Num Acc))(0 ("duhát"))(1 (N Acc));
	## Dat máksá guokte duhát ruvnno.

SELECT Num IF (1 ("ruvdnu"));
	## Dainna čuđiin ruvnnuin áiggun bierggu oastit.


# Rules related to specific numerals
# ----------------------------------

# galle
# - - - 
SELECT Indef IF (*-1 Neg BARRIER CS OR CP)(0 ("galle"));
	## Eaige sámiid ássanguovllus leat galle gieldda, mat...
 
REMOVE Indef IF (0 ("galle"));
SELECT Num IF (NOT -1 Neg)(0 ("galle"))(*1 N BARRIER NPNH);
	## ...ja govve galle joga leat Deanučázádaga njuoskkadagas.


# nubbi
# - - -
SELECT Ord IF (*-2 ("vuosttas") OR ("vuosttaš"))(-1 CC OR COMMA)(0 ("nubbi"));
SELECT Ord IF (0 ("nubbi"))(*1 CC OR COMMA LINK 1 ("goalmmát")); 
	## Vuosttaš vuojahaga fávllimus čuolda lea goaratčuoldan ja
	## nubbi čuolda fas gáddeoal-čuoldan.
	## Nubbi noađđeheargi goallustuvvo giehtahearggi lávžái ja goalmmát
	## fas dan nuppi lávžái.

SELECT Gen IF (0 ("nubbi"))(1 N);
	## Son oaččui čáda ášši fas nuppi Stuorradikkis.


SELECT Indef IF (0 ("nubbi"))(*1 CC OR COMMA LINK 1 ("nubbi"));
SELECT Indef IF (0 ("nubbi"))(*1 COMMA LINK 1 CC LINK 1 ("nubbi"));
        ## Nubbi lei dáža, muhto nuppi bearrašis lei isit sápmelaš.
	## Nubbi váldojuvvo, ja nubbi báhcá.

SELECT Indef IF (*-2 ("nubbi"))(-1 CC OR COMMA)(0 ("nubbi"));
	## Čielggadeapmi lea guovtteoasát - nubbi ieš vuoigatvuođa-
	## gažaldagaid birra ja nubbi dán suorggi siskkabeale hálddahus-
	## ortnegiid birra.

SELECT Indef IF (*-2 ("okta" Num))(-1 CC OR COMMA)(0 ("nubbi"));
	## Okta ášši lea mii lágas mearriduvvui, nubbi lea man kommišuvnnat 
	## čuvvo mearrádusaid.
	## Riektegeavadis sáhtášii danin geavahit guokte váldolinjjá,
	## okta ovdal ja nubbi fas maŋŋá 1970.

SELECT Indef IF (-1 ("unnimustá") OR ("unnimusat"))(0 ("nubbi"));
	## Mánáin lea ovttastallanvuoigatvuohta unnimustá nuppiin váhnemiin.

REMOVE Attr IF (0 ("nubbi"))(1 Dem OR Adv);
	## ... ja nubbi dán suorggi siskkabeale hálddahusortnegiid birra.
	## ...nubbi fas rávesolbmuid psykiatriijas.


# Remove the nominal reading of "nubbi"
# - - - - - - - - - - - - - - - - - -  -

 # The idea is that "nubbi" is a noun only when it is not followed by
 # a noun with which it agrees in case. Some of the case combinations may
 # still hide a noun "nubbi", but no errouneous hits were found in the corpus.

REMOVE N IF (0 ("nubbi" Nom))(*1 (N Nom) BARRIER NOT-ADJ);
REMOVE N IF (0 ("nubbi" Gen))(*1 (N Gen) BARRIER NOT-ADJ);
        ## 22. b. 1902 lága nuppi lađđasa a bustávas. 

REMOVE N IF (0 ("nubbi" Acc))(*1 (N Acc) BARRIER NOT-ADJ);
REMOVE N IF (0 ("nubbi" Gen))(*1 (N Ill) BARRIER NOT-ADJ);
        ## Vuosttas ja nuppi lađđasii:

REMOVE N IF (0 ("nubbi" Gen))(*1 (N Loc) BARRIER NOT-ADJ);
        ## ... ja nuppi teakstaoasis lea válddálas

REMOVE N IF (0 ("nubbi" Com))(*1 (N Com) BARRIER NOT-ADJ);
        ## Nuppiin sániiguin daddjon lea sáhka oasis gáddevuoigatvuođas.

REMOVE N IF (0 ("nubbi" Ess))(*1 (N Ess) BARRIER NOT-ADJ);
        ## Rávdná lea leamaš SÁNSa jođiheaddjin ja NSRa nubbin jođiheaddjin.

#REMOVE N IF (-1 Gen OR Dem)(0 ("nubbi" Ess))(*1 N BARRIER NOT-ADJ);
        ## Rávdná lea leamaš SÁNSa jođiheaddjin ja NSRa nubbin jođiheaddjin.
 # I tried CASE instead of Ess, but then the rule did not fire.

# okta
# - - -
SELECT Gen IF (-1 BOS OR CS OR REALCLB)(0 ("okta"))(*1 Loc BARRIER NOT-ADJ);
	## Ovtta ovddasteaddjis divššohasa ovddas lea vuoigatvuohta...


# Indefinite pronouns
# ===================

# General rules
# -------------
SELECT (Pron Attr) IF (0C Pron)(*1C N BARRIER NOT-ADJ-PCLE OR go);
        ## Dan muitalii muhtin mátkkošteaddji gean namma lei Keilhau.
        ## Njuolggadusain ledje eará ge mearrádusat.

SELECT Attr IF (0 Pron)(1 (A Attr))(*2 HNOUN BARRIER NP-BORDER);
	## ...je eará almmolaš dahje priváhta doaibma.

SELECT Attr IF (1 ("láhkai"));
	## Seammá láhkái go váldojournála ja lassijournála geavaheami oktavuođas...
REMOVE Attr IF (0 Pron)(1 (Pron Interr));
	## Lea unnán maid sáhttit dahkat.

SELECT Indef IF (*-1 Neg)(0 (Interr Foc));  
SELECT Indef IF (-1 Indef)(0 (Interr Foc));
	## Ale divtte geange badjelgehččat du.
	## Várut dadjamis maidege geasage dan birra! 

SELECT Indef IF (*-1 ("jus") OR ("jos") BARRIER CS)(0 Interr);  
SELECT Indef IF (-1 ("menddo"))(0 N);
	## Jus guhtege dadjá áhččásis dahje eadnásis:...
	## ...ahte doaimmat eai juogaduvvo menddo ollugiidda.

SELECT Indef IF (0 (Interr Foc))(NOT *1 VFIN);
	## Dat oste guđege sabehiid.

SELECT Indef IF (-1 BOS OR CS)(*1 N BARRIER NPNH);
	## Ollu amerihkálaččat doivot Oslo Ruoŧa oaivegávpogin.

REMOVE Adv IF (*-1 ("leat") BARRIER VERB OR CC OR CLB OR CS OR CP)(0 Indef)
	(NOT 1 A)(NOT *1 VERB BARRIER CC OR CLB OR CS OR CP);
	## Doppe ledje ollu olbmot.
	## Diet gal lei beare unnán.
 # Indefinite pronouns, not adverbs, if there is no verb other than 'leat'.

REMOVE Adv IF (*-1 CC OR CLB BARRIER NOT-ADV)(0 Indef)(NOT 0 Loc)
	(*1 N BARRIER NPNH);
	## Dán áššis, nugo eanaš eará áššiin,...

REMOVE Adv IF (-1 ("sihke"))(0 Indef)(*1 N BARRIER NP-BORDER);
	## Dat gáibida almmatge sihke ollu áiggi ja resursaid.


# Particular indefinite pronouns
# ------------------------------

# eará
# - - -
SELECT (Indef Attr) IF (0 ("eará"))(1 PrfPrc)(2 N);
	##  ...ja eará vuostáiváldojuvvon dokumeanttaid...

# juoga
# - - -
SELECT (Indef Nom) IF (-1 CLB)(0 ("juoga"))(1 Interr);
	## ..., juoga mii boahtá ovdan Romssa universitehta plánain.

# ollu
# - - -
SELECT Pron IF (-2 Pron)(-1 CC)(0 ("ollu"));
	## Váilos unnán dahje ollu.

# seamma/seammá
# - - - - - - - 

#SELECT Attr IF (-1 Num)(0 ("seamma") OR ("seammá"))(1 A OR N ); # Old version.
SELECT Attr IF (0 ("seamma") OR ("seammá"))(*1 A OR N BARRIER NOT-NUMERALS );
	## Dasto smiehttatge fas seamma sullasaš jearaldagaid. 
	## Sáhttá leat ulbmillaš, ahte seamma olmmoš áimmahuššá máŋga
	## dain doaimmain.

SELECT Attr IF (0 ("seamma") OR ("seammá"))(1 Adv)(NOT 1 VERB OR ("gusto")) ;
	## Mu oainnu mielde sáhttá seammá bures oaidnit dan láhkai, ahte... 
    #  The "gusto" here should be removed by a set of sentence adeverbials

# Then we kill the rest of the attributes...
# But did we catch enough attributes?    

#REMOVE Attr IF (0 ("seamma") OR ("seammá")) ;
	## Seammá gusto poliklinihkalaš divššus. 
    ## Seamma gusto poliklinihkalaš divššus. 

# unnán
# - - -
SELECT Indef IF (*-1 COPULAS BARRIER V-NOT-AUX)(0 ("unnán"))
	(NOT 1 PrfPrc LINK *1 NP-HEAD-ACC BARRIER NPNH);
	## Dat gal lei beare unnán.
	## Sii leat hui unnán dahkan guorahallat "sámi vieruid ja riekteáddejumiid".
	## Eanetlohku lea menddo unnán bidjan Finnmárkku mihtilmas diliid vuođđun.

 # In the last example "unnán" is an Adv. Hence the last condition. 
 # Alternative: (NOT 1 PrfPrc LINK 1 Inf)?


# Demonstrative pronouns
# ======================

REMOVE Pcle IF (-1 Interr OR CC)(0 ("dat"));
	## Maid dat Kárášjogas bargá?
	## Dahje dat gávccenuppelogis geat sorbmašuvve...

SELECT Dem IF (NOT -1 VERB)(1 (Indef Attr));
	## Nuppi geahči dan seammá lávžžis fas biddjojuvvo bađa birra.

REMOVE (Dem Sg) IF (0 ("dat" Sg))(NOT *1 (N Sg) BARRIER NOT-NP LINK 0 HNOUN)
	(NOT 1 PrfPrc LINK *1 (N Sg) BARRIER NP-BORDER)(NOT 1 (Indef Sg));  
	## .. ja dan golut leat govttolaččat veahki ektui.

REMOVE (Dem Pl) IF (0 ("dat" Sg))(NOT *1 (N Pl) OR (Indef Pl) OR NUMERALS 
	BARRIER NOT-NP)(NOT 1 NP-HEAD-GEN LINK 1 (Actio Gen) LINK 1 (N Pl));
	## ...ja ahte dat dáhttu čielgasit berre doahttaluvvot. 

REMOVE Dem IF (0 Pers)(1 Dem);
	## ...beroškeahttá das makkár geavahus leamaš dolin.


# DP-internal number concord
# --------------------------
SELECT Sg IF (0C (Pron Dem))(*1C (N Sg) BARRIER NPNH);
	## Dát olmmái bilkida Ipmila.

SELECT (Dem Sg) IF (*-1 BOS OR CS BARRIER NOT-ADV-PCLE)
	(*1 (N Sg) BARRIER NPNH LINK NOT 0 (N Pl))(NOT 1 VFIN)
	(NOT 0 Pers LINK 1 CC);
	## Dát ohcan lea juhkkojuvvon golmma departementii.

SELECT Pl IF (0C (Pron Dem))(*1C (N Pl) BARRIER NOT-ADJ LINK 0 HNOUN);
	## Naba daid stuora dorskiid, gosa daid bidjet? 

SELECT (Dem Sg Nom) IF (0 ("dat"))(1 ("gal") OR Pcle)(2 (N Sg Nom));
	## Láve dat gal njiŋŋelas boazu ge muhtomin adnojuvvot noađi guoddit.


# DP-internal case concord
# ------------------------

# More rules for Dem in front of NUMERALS must be added.

SELECT (Dem Sg Nom) IF (0 ("dat"))(*1 (N Sg Nom) BARRIER NOT-ADJ 
	LINK 0 HNOUN LINK *1 V-SG3 BARRIER CC OR CS OR PUNCT);
SELECT (Dem Sg Nom) IF (0 ("dat"))(*1C HNOUN BARRIER NOT-ADJ LINK 0 (Sg Nom)
	LINK *-1 V-SG3 BARRIER CS OR CP);
	## Dalle dat geahppaset gurppas daddjojuvvo veajan. 
	## Ii han ovttasge du sogas leat dat namma. 

SELECT (Dem Pl Nom) IF (*1 (N Pl Nom) BARRIER NOT-ADJ LINK 0 HNOUN 
	LINK *1 V-PL3 BARRIER CS);
SELECT (Dem Pl Nom) IF (*1 (N Pl Nom) BARRIER NOT-ADJ LINK 0 HNOUN 
	LINK *-1 V-PL3 BARRIER CS OR CP);
	## Dát vejolašvuođat leat dehálaš eallindiliid eavttut álbmogii.
	## Noađđespagáin leat dat geažit guokte golbma tumá guhku.

SELECT (Dem Pl Nom) IF (*1 (N Pl Nom) BARRIER NOT-ADJ LINK 0 HNOUN
	LINK *1 V-DU3 BARRIER CS);
SELECT (Dem Pl Nom) IF (*1 (N Pl Nom) BARRIER NOT-ADJ LINK 0 HNOUN 
	LINK *-1 V-DU3 BARRIER CS OR CP);
	## Go dat geažit mat geaigut leaba bajás, gohčoduvvoba spahká-oaivin.
	## No hits in corpus for the last rule, but it looks OK!

SELECT (Dem Pl Nom) IF (0 ("dat"))(1 NUMERALS LINK 0 Nom);
	## ...ožžo dat 50 vuotnaguolásteaddji Gáivuonas buhtadasa.

SELECT (Dem Pl Nom) IF (1 Nom)(2 (Pron Interr Pl));
	## No examples in corpus? 

SELECT (Dem Acc) IF (0 ("dat"))(1C (Num Acc) OR (Indef Acc));
	## Tabealla vuosttaš ceakkočuolddas oaidnit dan golbma bajitdási
	## váldofáddajoavkku.

SELECT (Dem Gen) IF (0 ("dat"))(1C (Num Gen))(NOT 0 (Dem Pl) LINK 1 ("okta"));
SELECT (Dem Gen) IF (NOT *-1 VERB OR CC BARRIER NOT-ADV-PCLE)(0 ("dat"))
	(*1C (N Gen) BARRIER NOT-ADJ LINK 0 HNOUN);
	## Davimus guovllut ledje dálá Ruošša ja daid guovtti davvi-
	## ovttastumiid oktasaš vearroeanan.
	## ...de lei stáhta dan eatnama oamasteaddji maid. 

SELECT (Dem Sg Gen) IF (NOT *-1 VERB BARRIER NOT-ADV-PCLE)(0 ("dat"))
	(*1C (N Sg Ill) BARRIER NPNH);
	## Láhka gusto daidda dearvvašvuođahálddahusa dearvvašvuoda-
	## dieđuid gieđahallamiidda.

SELECT (Dem Pl Ill) IF (0 ("dat"))(*1C (N Pl Ill) BARRIER NOT-NP);
	## Nubbi noađđeheargi goallustuvvo giehtahearggi lávžái ja goalmmát
	## fas dan nuppi lávžái.

 # Disambiguating (Pl Ill) is only a question of distinguishing between 
 # Pers and Dem. Accordingly, the barrier can be less strict.

SELECT (Dem Sg Gen) IF (NOT *-1 V-TRANS-ACT BARRIER (@OBJ) LINK NOT *-1 (@OBJ) 
	BARRIER CS OR CP)(0 ("dat"))(*1C (N Sg Loc) BARRIER NPNH);
	## Ja dan báhkka cizas lea fierpmit dassá go dat čoasku.

SELECT Dem IF (0 ("dat"))(0C Loc)(*1 (N Pl Loc) BARRIER NPNH); 
	## Seamma olmmoš áimmahuššá máŋga dain doaimmain dahje rollain. 

SELECT Dem IF (0 ("dainna"))(*1 (N Sg Com) OR (N Pl Loc) BARRIER NPNH);
	## ...suollemas dainna doaivvuin ahte... 
	## Guhtemuš dainna bivdovugiin guhkimus leaš geavahuvvon...

 # No disambiguation between Sg Com and Pl Loc here. All we do is 
 # distinguish between Dem and Pers.
 # As for Dem Pl Com, it is taken care of by the first rule for DP-internal
 # number concord above. 

REMOVE Dem IF (0 ("dainna"))(NOT *1 (Sg Com) OR (Pl Loc) BARRIER NPNH);
	## Dainna Máret lávii čiŋadit.

SELECT (Dem Pl Gen) IF (0 ("dat"))(*1C (N Pl Com) BARRIER NPNH);
	## ...ja daid lunddolaš rájáiguin ássanguovlluid siiddaid gaska. 

SELECT (Dem Pl Com) IF (*1C (N Pl Com) BARRIER NPNH);
	## Sámediggi čujuhii ahte ain ferte joatkit daiguin sierra doaibmabijuiguin.

REMOVE Acc IF (*-1 BOS OR CS BARRIER NOT-ADV-PCLE)(0 Dem)
	(*1 (N Loc) OR (N Com) BARRIER NPNH);
	## Dán mearrádusas ii leat lagabuidda definerejuvvon...

REMOVE (Dem Acc) IF (0 ("dat"))(1 ADVLCASE);
	## De našunálastáhtat válde daid háldoseaset.

REMOVE (Dem Acc) IF (NOT *-1 V-TRANS-ACT BARRIER CS OR CP)(1 N)(1 ADVLCASE);
	## Mun vuolggán vel dán vahkus.

REMOVE (Dem Acc) IF (*-1 V-TRANS-ACT BARRIER NOT-ADV-PCLE)(1 N)(1 ADVLCASE)
	(*1 NP-HEAD-ACC BARRIER CS OR CP OR CLB OR CC);
	## ...geas lea ovddasvástádus ollašuhttit dán suorggis giellalága 
	## gáibadusaid.

REMOVE (Dem Nom) IF (0 ("dat"))(NOT *1 (N Nom) BARRIER NPNH);
REMOVE (Dem Sg Loc) IF (0 ("dat"));
REMOVE (Dem Sg Ill) IF (0 ("dat"));
	## Maid dat Kárášjogas bargá?
	## ...ja lea vuoigatvuohta oažžut das máŋgosa sierranas jearrama
	## vuođul.
	## Olggosaddin sáhttá liikká dáhpáhuvvat jos gávdnojit dasa
	## beaktilis sivat.


# Some rules for personal pronouns again (will hit "dat", except the last one)
# ---------------------------------------------------------------------------- 

SELECT (Sg3 Nom) IF (*-1 (V Sg3) BARRIER CC OR CS OR CLB OR CP OR PUNCT)
	(NOT *1 (N Sg Nom) BARRIER NPNH); 
	## Iihan dat liikon Kárášjohkii?

SELECT (Sg3 Nom) IF (*1 (V Sg3) BARRIER VFIN OR CS OR CP LINK NOT 0 (N Pl))
	(NOT *-1 V-SG3 BARRIER NOT-ADV OR CLB)
	(NOT 0 Pl3 LINK *1 V-DU3 BARRIER VERB OR CS OR CP);
	## Ja dan báhkka cizas leat fierpmit dassá go dat čoasku.

SELECT (Pl3 Nom) IF (*-1 (V Pl3) BARRIER CC OR CS OR CLB OR CP OR HNOUN-NOM);
SELECT (Pl3 Nom) IF (*1 (V Pl3) BARRIER VFIN OR CS OR CP);
	## Eai dat leat vel giksan.
	## Dathan gal leat buorit guolit.

SELECT (Pl3 Nom) IF (*1 V-DU3 BARRIER VERB OR CS OR CP);
	## Duo duon rukses biillas dat čohkkába.

SELECT (Pron Sg3) IF (1 (Pron Interr Sg));
SELECT (Pron Sg3) IF (1 COMMA)(2 (Pron Interr Sg));
	## ...go dušše dat mii guoská reguleremii.  
	## Dat, gii ákkastallá...
 
SELECT (Pron Pl3) IF (1 (Pron Interr Pl));
SELECT (Pron Pl3) IF (1 COMMA)(2 (Pron Interr Pl));
	## Dat mat sáhttet háhkkojuvvot, leat ovdalis juo namuhuvvon.
	## ...go dat, mat leat namahuvvon vuosttas lađđasis, galget...

SELECT (Pron Sg3 Nom) IF (*1 ("ahte") BARRIER WORD);
	## Dat, ahte Nielsenii eai lean čájehan mákkarge hávdesajiid, 
	## sáhttá čuovvut máŋgga áššis.
 
REMOVE (Pers Acc) IF (*1C (N Acc) BARRIER (Actio Loc) OR CS OR CP OR Po OR ("dego")
	LINK NOT 0 TIME);
	## De lea sihke divššohas ja su lagamus oapmahaččas vuoigatvuohta 
	## oažžut dieđuid.
	## Min áiggi lea dat gildojuvvon.
	## Go ii oktage vuosttildan dan vuoigatvuođadili...


# Disambiguating against other POS
# --------------------------------
 # Getting rid of problematic names.
 # Problematic here are the names Dan, Dien, and the essive of the noun duo.

REMOVE (Prop) IF (-1 BOS)(0 Dem)(1 N)(NOT 1 Prop); 
	## Dan nieidda mun dovddan.  # Does this rule ever hit?

REMOVE (Dem Sg Nom) IF (NOT *-1 V-SG3 BARRIER CS)(NOT *1 (N Sg Nom) BARRIER NPNH)
	(NOT *1 V-SG3 BARRIER CS);
	## Dasto leat dát iešguđetge doaibmabijut juhkkojuvvon...

REMOVE (Dem Pl Nom) IF (NOT *-1 V-PL3 BARRIER CS)(NOT *1 (N Pl Nom) OR NUMERALS
	BARRIER NPNH)(NOT *1 V-PL3 BARRIER CS);  
	## Amma duot lei Niillasa guollebiila?

 # The verb is there to capture intransitive Dem.




# ========================= #
# Disambiguating adjectives #
# ========================= #
         CONSTRAINTS        #
# ========================= #

# Rules related to specific adjectives
# ====================================


# alit
# - - 

SELECT ("allat" A Comp Attr) IF (*1 ("oahpu") OR ("dutkan") BARRIER V);
 # Too weak barrier...
	## Dutkanministtar lea ráhkadan diđáhusa alit sámi oahpu ja dutkama birra.

# dološ
# - - - - 
SELECT A IF (0 ("dološ"))(1 N);
	## ...go lea gažaldat háhkamis dološ áiggi rájes geavaheami...

# garra
# - - -
 # These rules are here to remove "garra" N Sg Nom.
SELECT Attr IF (0 ("garas"))(1 (N Nom)); 
SELECT Attr IF (NOT *-1 V-SG3)(0 ("garas" A Attr))(1 A OR N)(NOT *1 V-SG3);
	## Norggas lea leamaš gal garra digaštallan sámi oahppoplána hárrái.
	## Beatnagat eai biva viššat garra buollašiid.

# sierra
# - - - -
SELECT (A Attr) IF (0 ("sierra"))(*1 N BARRIER NPNH);
	## Sámelága vuođul lea ásahuvvon sierra ovddasteaddji orgána.

# stuoris
# - - - -
SELECT Attr IF (-1C N)(0 ("stuorát"))(1C (N Pl))(1 HNOUN); 
	## ...leat viiddis njuolggadusat áššemeannudeapmái stuorát 
	## čázádatmuddemiid oktavuođas.

# surgat
# - - - -
SELECT (A Sg Nom) IF (0 ("surgat"))(0 (@INTERJ));
	## Hei surgat, lea divrras!

# veara
# - - -

REMOVE N IF (-1 (Actio Gen))(0 ("veara"));
	## ...buot dan mii leat ráhkistan ja gudnejahttin veara.

REMOVE N IF (*-1 (@OBJ) BARRIER NOT-NP)(0 ("veara"));
	## De bođii geafes leaska ge gii bijai guokte smávvaruđaža, 
	## moatte šlántte veara.

REMOVE N IF (0 ("mearkkašan#veara") OR ("namuhan#veara"));
	## Buhttenrievttálaččat ii nuppástuvvon mearkkašanveara...

 # We would like to have a more general rule that could hit all compounds 
 # with "-veara", but since that is not possible yet we list some relevant 
 # words instead.

# vuolit
# - - - -
SELECT Attr IF (0 ("vuolit"))(1 ("gráda") OR ("dássi"));
	## Skuvla dušše addá oahpu vuolit dásis. 


# Comparative
# ===========

SELECT Comp IF (NOT -1 ("nu") OR ("seamma") OR CC)
	(NOT 0 (Pl Nom) LINK -1 Comp OR COMPAR LINK *-1 COPULAS BARRIER 
	NOT-ADV-PCLE)(*1 ("go" Pcle) BARRIER NOT-N);
        ## Dážat navde Finnmárkku buoret guovlun go iežaset ruovttubáiki.

SELECT Comp IF (-1 ("mihá"));
SELECT Comp IF (-1 ("olu"))(NOT 0 (Pl Nom) LINK *1 V-PL3 BARRIER NOT-ADV-PCLE);
	## Nuppe dáfus ledje mihá eanebut geat bivde lagabus gátti.
	## ...ja danin lea buot nu olu álkit inuihtaide. 

REMOVE Adv IF (*-1 ("go" Pcle) BARRIER NPNH OR CC)(*1 N BARRIER NOT-ADJ-CC);
	## Alimusriekti ii badjelgeahččange sápmelaččaid nu sakka go vuolit
	## instánssa duopmu.


# Attribute disambiguation
# ========================

# Rules for Attr between Dem and N
# --------------------------------
SELECT Attr IF (*-1 (Dem Sg Nom) BARRIER NPNH)(*1 (N Sg Nom) BARRIER NPNH);
SELECT Attr IF (*-1 (Dem Pl Nom) BARRIER NPNH)(*1 (N Pl Nom) BARRIER NPNH);
	## Dalle dat geahppaset gurppas daddjojuvvo veajan.
	## ...jus dát máinnašuvvon árbevirolaš dilálašvuođat eai
	## geahččaluvvo čuvgejuvvot.

SELECT Attr IF (*-1 (Dem Sg Acc) BARRIER NPNH)(*1 (N Sg Acc) BARRIER NPNH);
SELECT Attr IF (*-1 (Dem Pl Acc) BARRIER NPNH)(*1 (N Pl Acc) BARRIER NPNH);
	## ...ja son doaimmaha dán dábálaš dahje eanaš geavaheami.
	## Nuppe beales geahččalit dahkat dáid siskkáldas áddejumiid
	## duođaštanfáddán.

SELECT Attr IF (*-1 (Dem Sg Gen) BARRIER NPNH)(*1 (N Sg Gen) BARRIER NPNH);
SELECT Attr IF (*-1 (Dem Pl Gen) BARRIER NPNH)(*1 (N Pl Gen) BARRIER NPNH);
	## Dán konkrehta háve gustojedje gáržžideamit nappo earáide go
	## boazosapmelaččaide.  
	## No cases left after the Dem Pl Acc-Attr-N Pl Acc rule? 

SELECT Attr IF (*-1 (Dem Sg Gen) BARRIER NPNH)(*1 (N Sg Ill) BARRIER NPNH);
SELECT Attr IF (*-1 (Dem Pl Ill) BARRIER NPNH)(*1 (N Pl Ill) BARRIER NPNH);
	## Dás lei maiddái gažaldat geavahanvuoigatvuođas vuovdái ja dan
	## vejolaš viidodahkii. 
	## ...mat gullet dáidda ja eará lunddolaš sullasaš doaimmaide. 

SELECT Attr IF (*-1 (Dem Sg Gen) BARRIER NPNH)(*1 (N Sg Loc) BARRIER NPNH);
SELECT Attr IF (*-1 (Dem Pl Loc) BARRIER NPNH)(*1 (N Pl Loc) BARRIER NPNH);
	## Ja dan báhkka cizas leat firpmit dassá go dat čoasku.
	## Sápmelaččat ledje vuosttasin dahje okta dain vuosttaš álbmot-
	## joavkkuin,... 
	## ...dainna váilevaš vejolašvuođain...

SELECT Attr IF (*-1 (Dem Pl Com) BARRIER NPNH)(*1 (N Pl Com) BARRIER NPNH);
	## Geainnuin livččii eambbo turistajohtolat suohkana čoahkke-
	## ássanguovddášiid čađa daiguin ekonomalaš oalgeváikkuhusaiguin.

SELECT Attr IF (*-1C Dem BARRIER NPNH)(*1 N BARRIER NOT-ADJ-CC)
	(NOT 0 N LINK *1 VFIN BARRIER NOT-ADV);
	## Mo Alimusriekti meannudii vaikkoba dakkár vuolit dási olbmuid
	## vihtančilgehusaigun?


# Other attribute rules
# ---------------------
REMOVE VFIN IF (-1C VFIN)(NOT 0 ConNeg);
	## Mun áiggun oastit biilla.
	## Gos dáppe lea máddi?
	## Ánne-Sofe buvttii varas biergguid.
	## Máhtte manai čuoigga skuvlii.
	## Dan dihte go lea čađat dárbu ođasmahttit jorgalusaid.
 # As we see, this rule can lead to a verbal, a nominal, an adjectival or an
 # adverbial reading. It comes here because this is where it is first needed.

REMOVE (A Attr) IF (1 CC)(2 N)(NOT 2 (A Attr))
	(NOT 2 PrfPrc LINK *1 N BARRIER NPNH);
	## ...jus doaimma ovttadagat sihke fágalaččat ja lágideami bokte leat...

SELECT (N Attr) IF (NOT 0 Prop)(1 N);
	## Máttimus sápmelaččat eahpidedje dáža dutkiid eanet go davit sápme-
	## laččat.

REMOVE Adv IF (NOT *-1 VERB BARRIER CS OR CP)(NOT 0 ("gosa"))
	(NOT *1 VERB BARRIER CS OR CP);
	## Diehtojuohkin divššohasa lagamuš oapmahažžii.
	## Stuorát guolit mat eai bisán čalmmiide, mannet čađa.
 
SELECT Attr IF (*-1 BOS OR CS BARRIER NPNHA)(NOT 0 VERB)(NOT 1 Dem OR VERB)
	(NOT 0 Prop LINK -1 (Prop Attr) LINK 2 CC)
	(NOT 0 Prop LINK 1 CC LINK 1 Prop LINK NOT 1 Prop)
	(*1 N BARRIER NPNH LINK NOT 1 Po)(NOT 1 COMMA LINK 1 N);
	## Golbma čáppa, doaimmalaš nieidda livžo áhčiset riegádanbeaivve.

SELECT Attr IF (-1 COPULAS)(1 A OR N LINK NOT *1 Po OR Loc OR Inf BARRIER NP-BORDER)
	(NOT 1 Adv OR VERB)(*2 go BARRIER NOT-NP);
	## Sis-Finnmárkkus lea vuolit gaskamearálaš bruttoboahtu go fylkkas...

SELECT Attr IF (*-1 COPULAS BARRIER NOT-ADV-PCLE LINK *-1C (@SPRED) BARRIER 
	NOT-ADV-PCLE)(NOT 1 CLB LINK 1 N)(*1 N BARRIER NPNH LINK NOT 1 Po OR Loc);
	## Dieđuid diehtogieđahallama ovddasvástideaddjin leat guovlluguovdasaš 
	## dearvvašvuođadoaimmat.

SELECT Attr IF (*-1 Num BARRIER NPNH)(*1 N BARRIER NPNH)(NOT 0 Prop LINK 1 CC);
	## Dan dihte ohcat guokte lassi stipendiáhttavirggi sámi/eamiálbmot-
	## guoskevaš dutkamiidda.

SELECT (A Attr) IF (-1 Adv)(NOT 0 Inf)(*1 N BARRIER NPNH)(NOT 1 CLB LINK 1 N)
	(NOt 1 Prop);
	## Mánná viehkalii olggos álás julggiid.
	## Universitehtas lea maiddái čielga nášunála ovddasvástádus...

SELECT Attr IF (NOT *-3 COPULAS BARRIER NOT-ADV-PCLE)(-2 Attr OR PrfPrc OR PrsPrc)
	(-1 CC)(1 N LINK NOT 0 Nom)(NOT 0 (Prop Gen) LINK NOT 1 Prop);
	## Soahpamušas váilot njuolggadusat smávit vahágahtti ja geavatlaččat 
	## bággolonistemiide.
 
REMOVE (A Nom) IF (0 (A Attr))(NOT 0 (Pl Nom) LINK *-1 (V Pl3) BARRIER NOT-ADV-PCLE
	LINK NOT *-1 (Pl Nom) BARRIER CS OR CP OR CC)
	(NOT 0 (Pl Nom) LINK *-1 COPULAS BARRIER NOT-ADV-PCLE LINK 0 Pl3)
	(*1C N OR Cmpnd BARRIER NOT-ADJ OR (PUNCT RIGHT) LINK NOT 1 Po OR PREGEN)
	(NOT 1 Prop OR Ill)(NOT 1 COMMA LINK 1 N)
	(NOT 1 Gen LINK *1 Po OR PREGEN BARRIER NP-BORDER);
	## Dat lea čuovvovaš sajis.

REMOVE (A Gen) IF (0 (A Attr))(*1 N BARRIER NOT-ADJ LINK NOT 1 Po);
REMOVE (A Acc) IF (0 (A Attr))(*1 N BARRIER NOT-ADJ LINK NOT 1 Po);
        ## Sámegiella ja dárogiella leat ovttaárvosaš gielat.
	## Lea deaŧalaš movttiidahttit studenttaide gazzat doavttergrádaoahpu. 
 # The careful mode (*1C N) was removed because of the second example.
 # But reintroduced because of the third example.

 
REMOVE V IF (NOT -1 (N Nom))(0 (A Attr))(*1 N BARRIER NOT-ADJ LINK NOT 1 Po)
	(NOT 0 Inf)(*2 VFIN BARRIER CC OR CLB OR CP OR CS);
        ## Olles doaibmaviessu lea sullii 2300 njealjehasmetera stuoru. 

REMOVE N IF (0 (A Attr))(NOT 1 VFIN OR PrfPrc OR CLB)(NOT 1 PREGEN) 
	(NOT 0 (Sg Nom) LINK *-1 (V Sg3) BARRIER NOT-ADV-PCLE LINK 
	NOT *-1 Nom BARRIER CC OR CP OR CS OR REALCLB)
	(NOT 0 (Pl Nom) LINK *-1 (V Pl3) BARRIER NOT-ADV-PCLE LINK 
	NOT *-1 Nom BARRIER CC OR CP OR CS OR REALCLB)
	(*1 N BARRIER NOT-ADJ LINK NOT 1 Po);
	## ...muhto dakkár radikála rievdadus eahpitkeahttá buktá garra
	## reakšuvnnaid,...

SELECT (A Attr) IF (NOT *-1 COPULAS BARRIER NP-HEAD-NOM OR V-NOT-AUX)
	(NOT 0 Num OR Inf OR Comp OR ("nubbi"))(NOT 0 Adv LINK 1 Gen LINK 1 N)
	(1 N)(NOT 1 VFIN OR Inf OR PREGEN)
	(NOT 0 (N Sg Nom) LINK *-1 V-SG3 BARRIER NPNHA LINK NOT *-1 (@SUBJ)
	BARRIER CS OR CP);
	## Gonagas sáhttá mearridit sierranaš njuolggadusaid báikkalaš
	## dilálašvuođaid ektui.

SELECT (A Attr) IF (NOT 0 Adv OR Inf)(1 A-CASE LINK 1 CLB  LINK 1 NP-BORDER); 
	## ...main leat mielde dušše priváhta oasálaččat, ii leat...

REMOVE Adv IF (-2 A OR N)(-1 ("go" Pcle))(1 N);
	## No longer needed after the introduction of the previous rule?

REMOVE PrsPrc IF (0 (A Attr))(1 N);
        ## Sidjiide maid lei guollerikkis Ruija geasuheaddji guovlu.
 
REMOVE (A Loc) IF (0 Attr)(1 N);
	## Olggosaddin sáhttá liikká dáhpáhuvvat jos gávdnojit dasa beaktilis sivat.


SELECT (A Attr) IF (*-1 BOS OR CS BARRIER NPNHA)(NOT 0 VFIN OR Inf)
	(*1 HNOUN BARRIER NPNH)(NOT 1 VFIN)(NOT 1 COMMA LINK 1 N);
	## Sámedikki vuosttamuš dábálaš dievasčoahkkima maŋŋa...  

REMOVE N IF (-1 Gen OR Dem OR Indef)(0 Attr)(1 N)(NOT 1 VFIN);
	## Oktasaš vuoigatvuođat ledje šaddan dološ áiggi rájes...

REMOVE VFIN IF (-1 Gen OR Dem OR Indef)(0 Attr)(1 N);
	## Lávži goallostuvvo dan ovddit noađđehearggi čeabehii.

REMOVE Adv IF (NOT -2 V-NOT-AUX)(-1 Gen OR Dem OR Indef)(NOT -1 PrfPrc)
	(0 Attr)(1 N);
	## ...galget divššohasa lagamus oapmahaččat oažžut dieđu...

SELECT (A Attr) IF (1 (PUNCT LEFT) LINK *1 (PUNCT RIGHT) LINK 1 N);
	## Dábálaččat earuha váldolaččat gaskal dábálaš (totala) geavahan-
	## vuoigatvuođaid ja sierranas (partiála) geavahanvuoigatvuođaid.

SELECT (A Attr) IF (-1 COMPAR)(1 ("go" Pcle))(2 (A Attr))(*3 N BARRIER NOT-ADJ);
	## Sámedikki ásaheapmi lea eanet ideologalaš go čielga mearrádus.

SELECT (A Attr) IF (NOT 0 Adv OR Num)(1 TIME);
	## Váilui guhkit áiggi lotnolaš oaivila. 

SELECT (A Attr) IF (*-1 ("lassi" Ess) BARRIER NPNH)(*1 Ill BARRIER NPNH);
	## Vuoigatvuohta leat Sámedikkiid jienastusloguin lea lassin dábálaš 
	## jienastanvuoigatvuođa eavttuide.

 
# Coordinated attributes
# ----------------------
SELECT Attr IF (NOT 0 Prop)(1 COMMA)(2 Attr)(3 CC LINK *1 HNOUN BARRIER NPNH);
	## ...go viidáset gaskkusta oppalaččat álbmoga historjjálaš,
	## kultuvrralaš ja politihkalaš dieđuid.

SELECT Attr IF (NOT 0 Prop)(1 COMMA)(2 Attr OR PrsPrc)(NOT 3 N LINK 1 COMMA) 
	(*3 HNOUN BARRIER NPNH);
	## Ferten maid imaštallat go dehálaš, sámepolitihkalaš birrasat
	## barget nu áŋgirit dán ovdii.

SELECT Attr IF (-2 Attr)(NOT -2 Prop)(-1 CC)(1 N OR Actio);
	## Divššohasain lea vuoigatvuohta oažžut čilgejuvvot álkit ja
	## oanehaččat journálas geavahuvvon fágadoahpagiid.

SELECT PrfPrc IF (*-1 CC BARRIER NOT-ADV LINK -1 A);
	## Dilálašvuođaid álgu sáhttá leat dovdameahttun dahje jávkan historjjá
	## seavdnjatvuhtii.

 # The last rule does not hit only prenominal PrfPrc. It is nevertheless useful,
 # since PrfPrc but not Actio coordinates with adjectives in predicate position. 

SELECT (A Attr) IF (1 CC)(2C (A Attr));
SELECT (A Attr) IF (1 CC)(2 (A Attr))(NOT 2 VERB)(3 N OR Cmpnd)(NOT 4 Po); 
	## ...go galgá válljejuvvot gaskal vejolaš ja dohkkalaš iskkadan-
	## ja dikšunvugiid. 
	## Olbmot geat ellet náitoslágan dahje guimmešvuođalágan gaskavuođain.

SELECT (A Attr) IF (NOT -1 ("leat"))(1 CC)(*2 PrfPrc BARRIER NOT-ADV)
	(*3 N BARRIER NPNH);
	## Johtalus rabas dahje galbmon mearas lea muddejuvvon.

SELECT Attr IF (*-1 Neg BARRIER VFIN OR CS OR CP)
	(*1 CONTRA BARRIER WORD LINK 1C Attr);
	## ILO-konvenšuvdna nr 169 ii vuođđuduva formála, muhto funktionála
	## oamastanvuoigatvuođa doahpagii.


# Special rules for 'buorre' (the only adjective showing case agreement)
# ----------------------------------------------------------------------
SELECT (Pl Nom) IF (0 ("buorre"))(*1 (N Pl Nom) BARRIER NOT-ADJ-CC);
	## Leatgo buorit guolit?

SELECT Acc IF (0 ("buorre"))(NOT 0 Comp OR Superl)
	(*1C (N Acc) BARRIER NOT-ADJ-CC);
	## Doaibmaplána lea bidjan buori vuođu.
	## Doaivvutgo oažžut buriid guliid?

SELECT Gen IF (0 ("buorre"))(NOT 0 Comp OR Superl)
              (*1C (N Gen) BARRIER NOT-ADJ-CC LINK 0 HNOUN);
	## ...galgá journála čállojuvvot buore fidnodábi mielde.


# Comparatives
# ------------
 # This block of rules is there to ensure case agreement for attributive
 # comparatives.

REMOVE Gen IF (0 Comp)(*1 HNOUN BARRIER NPNH LINK 0 NOT-GEN); 
REMOVE Acc IF (0 Comp)(*1 HNOUN BARRIER NPNH LINK 0 NOT-ACC);
	## Oppalaččat daid gažaldagaid birra mat gieđahallojuvvojit... 
	## ...oktiibuot 3.000.000 ruvnno jahkásaččat 5 jagi badjel.

REMOVE Nom IF (0 Comp)(*1 HNOUN BARRIER NPNH OR PUNCT LINK 0 NOT-NOM LINK 
	NOT 0 Inf)(NOT 1 COMMA OR CC)(NOT 0 (Pl Nom) LINK *-1 (V Pl3) 
	BARRIER NPNHA)(NOT 0 (Sg Nom) LINK *-1 V-SG3 BARRIER NPNHA)
	(NOT 1 Gen LINK *1 Po OR PREGEN BARRIER NP-BORDER);
	## Álggahit eará doaibmabijuid gáibida baicca lassi árjja...
   

# Adjectives as predicates
# ========================

SELECT (A Pl Nom) IF (*-1 COPULAS BARRIER NOT-ADV-PCLE LINK *-1 (N Pl Nom) OR
	(Pron Pl Nom) OR (Pl1 Nom) OR (Pl2 Nom) OR (Pl3 Nom) BARRIER CS 
	OR (Sg Nom))(NOT 0 N)(NOT 1 A)(NOT 1 Adv LINK 1 A);
	## ...ja dan golut leat govttolaččat veahki ektui.

SELECT (A Pl Nom) IF (*-1 COPULAS BARRIER NOT-ADV-PCLE LINK *-1 (Du1 Nom) OR 
	(Du2 Nom) OR (Du3 Nom) BARRIER CS)(NOT 0 N)(NOT 1 VERB OR A);
	## I have found no examples, but the rule should work! 

SELECT (A Pl Nom) IF (*-1 COPULAS BARRIER NOT-ADV-PCLE LINK *-1 Nom BARRIER CS 
	LINK *-1 CC BARRIER NPNH LINK -1 Nom)(NOT 0 N)(NOT 1 VERB OR A);
	## Sámegiella ja dárogiella leat ovttaárvvosaččat.  

SELECT (A Sg Nom) IF (*-1 COPULAS BARRIER NOT-ADV-PCLE LINK *-1 (Sg Nom)
	BARRIER NOT-ADV-PCLE LINK *-1 BOS OR CS OR CP BARRIER NOT-ADV-PCLE)
	(1 CLB OR EOS);
	## Jos viessu lea dohkálaš, de ráfi orru dan badjel.
	## Vuoigŋa gal lea geargat, muhto oažži lea headju.

SELECT (A Sg Nom) IF (*-1 COPULAS BARRIER NOT-ADV-PCLE LINK *-1 (Sg Nom)
	BARRIER NOT-ADV-PCLE LINK *-1 BOS OR CS OR CP BARRIER NOT-ADV-PCLE)
	(*1 COPULAS BARRIER NOT-ADV-PCLE LINK *1 CLB OR EOS BARRIER NOT-ADV-PCLE);
	## ...dan dihte go geassegollebivdu lei áibbas heittot leamaš,...

SELECT (A Sg Nom) IF (*-1 COPULAS BARRIER NOT-ADV-PCLE LINK *-1 (N Sg Nom) OR
	(Pron Sg Nom) OR (Sg1 Nom) OR (Sg2 Nom) OR (Sg3 Nom) BARRIER CC OR CS LINK
	NOT 0 Ess)(NOT 0 N OR Inf OR Adv)(NOT 1 VERB)(NOT 1 CC LINK 1 Adv)
	(NOT *1 N BARRIER NPNH);
	## Mun in leat dohkálaš ahte don boađášit mu vissui.

SELECT (A Sg Nom) IF (*-1 COPULAS BARRIER Nom OR PrfPrc OR CS OR MO 
	LINK 0 Sg1 OR Sg3)(NOT 0 Adv OR N OR Inf)(NOT 1 VERB OR Num OR A)
	(NOT 1 ("nu") OR ("hui") LINK 1 A)(NOT 1 CC LINK 1 Adv)
	(NOT *1 N BARRIER NPNH); 
	## Lea ártet go it gallet fulkkiin.

SELECT (A Sg Nom) IF (*-1 COPULAS BARRIER VERB OR CS OR MO LINK *-1 (Neg Sg1) OR 
	(Neg Sg2) OR (Neg Sg3) BARRIER NOT-ADV-PCLE)(NOT 0 N)(NOT 1 N OR A)
	(NOT 1 ("nu") OR ("hui") LINK 1 A);
	## Ii leat vuogas geavahit hálduiváldima dálá fásta opmodagaide.

SELECT (A Sg Nom) IF (*-1 COPULAS BARRIER NOT-AA LINK 0 Sg3)(NOT 0 Inf)
	(0 (N Sg Loc) OR (Actor) OR (N* A))(*1 EOS OR CLB BARRIER NOT-ADV-PCLE); 
	## Lea veahá eahpidahtti, ahte eanaš oassi ođđa govvadáidagis,...  
	## Dávjá lea lunddolaš, ahte doaktáris lea vástádusrolla.

SELECT (A Sg Nom) IF (-1 NP-HEAD-SG-NOM)(*-2 COPULAS BARRIER NOT-ADV-PCLE)
	(NOT 0 Attr LINK *1 (N Sg Nom) BARRIER NPNH OR CLB);
	## Dábálaččat lea čuollu guhkit, golbma, njellja firbmebaji guhku.

SELECT (Sg Nom) IF (*-1 Ill BARRIER NOT-ADV-PCLE LINK *-1 COPULAS 
	BARRIER NOT-ADV-PCLE)(*-1 V-SG3 BARRIER V-NOT-AUX)(NOT 0 PrfPrc)
	(NOT 0 NUMERALS LINK *1 ALU OR ("velggolaš") BARRIER NP-BORDER);
	## ...muhtun gii lei sutnje velggolaš logi duhát taleantta.
	## Mus lea dutnje juoga dadjamuš.

SELECT (A Pl Nom) IF (*-1 (N Pl Nom) BARRIER NOT-ADV-PCLE OR CLB LINK *-1 COPULAS
	BARRIER V-NOT-AUX OR CC OR CS OR CP);
	## Mánnáguoddi-spagáin leat spahká-oaivvit sakka guhkibut.
	## Dán oktavuođas leat dološ geavahusa njuolggadusat miellagiddevaččat.

SELECT (A Pl Nom) IF (*-1 COPULAS BARRIER VERB OR CS OR MO LINK *-1 (V Neg) 
	BARRIER NOT-ADV-PCLE LINK 0 V-DU-PL)(NOT 0 N)(NOT 1 VERB OR A); #(NOT 1 N);
	## Sis-Finnmárkku dikkit maŋŋilgo Norga badjelasas válddii sorjjas-
	## meahttunvuođa dán guovllus, eai šat lean seamma dehálaččat 
	## sápmelaččaid gaskkas go ovdal.

SELECT (A Sg Nom) IF (*-1 BOS OR REALCLB BARRIER NOT-CC)
	(1 (Dem Sg Nom) OR MUN OR DON OR SON); 
	## Ja ávdugas son guhte oskkui.
 
SELECT (A Pl Nom) IF (*-1 BOS OR REALCLB BARRIER NOT-CC)
	(*1 NP-HEAD-PL-NOM BARRIER NPNH);
	## Ávdugasat sii geat moraštit.

REMOVE VERB IF (-1 COPULAS)(0 Nom)(NOT 0 PrfPrc)(1C (Pron Interr));
REMOVE Attr IF (-1 COPULAS)(0 Nom)(1C (Pron Interr)); 
	## Dat geaidnu lea govdat mii doalvu gáđohussii.
	## Dát dáidá leahkit duohta máid boazodoalli lohká.

REMOVE VFIN IF (*-1 COPULAS BARRIER NOT-ADV-INDEF)(0 Attr)(1 N OR EOS);
	## Joddu suhppejuvvo go doaris lea gárvvis.	

SELECT A IF (*-1 COPULAS BARRIER NOT-ADV-INDEF)(NOT 0 VERB OR Ord OR N)
	(1 N OR EOS);
	## ...de dilli orru leamen earalágan.

SELECT A IF (-2 (N Sg Nom))(-1 ("leat" Sg3))(NOT 0 N OR PrfPrc OR Inf)
	(NOT 0 Ill LINK 1 PrfPrc)(NOT 0 Adv LINK 1 A); 
	## Sin váilevaš oahppu ja giellamáhttu lea guhkit áiggi leamaš
	## heađušteaddjin. 	

REMOVE Attr IF (0 A)(1 COMMA)(NOT 2 A OR PrsPrc)(NOT 2 CC LINK 1 Attr);
	## Jos dat ii leat vejolaš, de mii fertet mannat.

REMOVE Attr IF (0 A)(NOT 1 A OR N OR Num OR Actio OR PrfPrc OR PrsPrc OR 
	CC OR COMMA); 
	## Dat galgá čuovvoluvvot nu guhkás go vejolaš.

REMOVE Attr IF (0 A-CASE OR Adv)(1 CC)(NOT *2 N BARRIER NP-BORDER);
	## ...berre mearridanváldi leat nu viiddis go vejolaš ja nu
	## čadni go vejolaš.

REMOVE (A Gen) IF (*-1 COPULAS BARRIER NOT-ADV-PCLE)(1 Inf); 
	## Lea hávski oažžut reivve.

SELECT (A Sg Nom) IF (*-1 COPULAS BARRIER NOT-ADV LINK 0 V-SG3 LINK NOT *-1 Loc
	BARRIER N)(NOT -1 Gen)(NOT 0 Inf OR Attr)
	(NOT 0 Adv LINK *1 VERB BARRIER NOT-ADV)(NOT 1 PrfPrc)
	(NOT 1 CC LINK 1 Adv);
	## Buođđu lea dál gárvvis.
	## Dalle lea lunddolaš, ahte...

SELECT (A Pl Nom) IF (*-1 COPULAS BARRIER NOT-ADV-PCLE)
	(*-1 V-DU-PL BARRIER V-NOT-AUX LINK NOT *-1 Neg BARRIER NOT-ADV-PCLE);
	## Dán máilmmi mánát leat čeahpibut soabadit gaskaneaset.

SELECT (A Pl Nom) IF (1 (Pron Pl2))(*2 COPULAS BARRIER VERB)
	(*2 V-PL2 BARRIER NOT-ADV-PCLE);
	## Man čeahpit dii lehpet hilgut Ipmila báhkkomiid! 

SELECT (A Pl Nom) IF (-1 (N Pl Nom))(*-2 COPULAS BARRIER NOT-ADV-PCLE LINK 0 Pl3)
	(NOT 1 PrfPrc);
	## ...vai leago eanavuovdinorgánat geatnegasat luobahit eatnamiid...

SELECT (A Pl Nom) IF (*-1 ("orrut") BARRIER NOT-ADV-PCLE)
	(*-1 V-PL3 BARRIER CS OR CP);
	## Riekteháhkama eavttut geavahusa njuolggadusaid mielde eai oro absoluhtat.

SELECT (A Pl Nom) IF (-2 (A Pl Nom) LINK *-1 COPULAS BARRIER NOT-ADV-PCLE)(-1 CC);
	## ...jus dat leat čuožžovaččat ja dárbbašlaččat.

REMOVE (A Pl Nom) IF (*-1 COPULAS BARRIER VERB)(*-1 V-SG3 BARRIER V-NOT-AUX)
	(*1 (A Sg NOM) BARRIER Nom OR VERB OR CS OR CP OR CC);
	## No hits?


# Coordination
# ------------
SELECT (A Sg Nom) IF (-2 (A Sg Nom))(-1 CC OR COMMA)(1 CC OR COMMA)(2 (A Sg Nom));
SELECT (A Sg Nom) IF (-2 (A Sg Nom))(-1 CC)(1 CS OR EOS)(NOT 0 Inf LINK -2 Inf);
	## e. e. man bissovaš, dovddus ja bindil dat lea.
	## Vare livččet jogo galmmas dahje liekkas!

REMOVE Attr IF (1 COMMA OR CC)(2 A)(NOT *1 N BARRIER NP-BORDER);
	## ...man bissovaš, dovddus ja bindil dat lea.

SELECT (A Pl Nom) IF (-2 (A Pl Nom))(-1 CC OR COMMA)(1 CC OR COMMA)(2 (A Pl Nom));
	## Lehket álo ustitlaččat, vuollegaččat ja guhkesmielalaččat.

SELECT (A Pl Nom) IF (-2 (Pl Nom))(NOT -2 Adv)(-1 CC)(1C NP-BORDER);
	## Ollu profehtat ja vanhurskásat leat áibbašan oaidnit dan.

SELECT (Pl Nom) IF (-2 (Pl Nom))(NOT -2 Adv OR Inf)(-1 CC)(1 CLB OR CC)
	(2 NP-BORDER);
	## De bohte ollu tuollárat ja suttolaččat ja čohkkájedje beavddis. 

SELECT (Pl Nom) IF (1 CC)(2C (Pl Nom))(NOT 2 Interr);
	## Dušše inuihtat ja sápmelaččat sáhttet adnojuvvot Davviriikkaid
	## eamiálbmogin.

 
# And now some rules for adverbs that modify adjectives
# =====================================================

SELECT Adv IF (0 A)(NOT 0 Inf)(1C A)(NOT *1 N BARRIER NPNH);
	## ...jus ii leat čielgasit dárbbašmeahttun dahje geavatlaččat
	## veadjemeahttun.
	## ...ahte váikkuhusat divššohasa lagamuččaide leat mearkka-
	## šahtti stuorrát.  

SELECT Adv IF (*-1 COPULAS BARRIER NOT-ADV-PCLE)(1 A);
	## Sámi riekteáddejupmi ii lean doarvái miellagiddevaš 
	## suokkarduvvot ovttas dainna gažaldagain.

SELECT Adv IF (-2C Adv)(-1 CC)(0 A);
	## Maŋŋá soađi lea sámi álbmot rahčan ollu vai dat adnojuvvošii
	## seammá dássásažžan, sihke duođalaččat ja rievttálaččat.

SELECT Adv IF (0 A)(1 CC)(2C Adv);
	## ...dan muddui go dat lea geavatlaččat ja ruđalaččat vejolaš.




# ============================= #
# Disambiguating verbs - part 1 #
# ============================= #
             CONSTRAINTS        #
# ============================= #

# In this cycle we apply some safe verb rules.


# ConNeg forms
# ------------

SELECT (Imprt ConNeg) IF (*-1 (Neg Imprt) BARRIER VFIN OR ConNeg OR CS OR CP);
	## Ale ge jorggit selggiid sutnje gii bivdá dus loanas.

SELECT (Ind ConNeg) IF (*-1 (Neg Ind) BARRIER VFIN OR ConNeg OR PrfPrc OR CS OR CP)
	(NOT 0 N LINK *1 ConNeg BARRIER NOT-ADV-PCLE)
	(NOT 0 Loc LINK -1 (Neg Foc) LINK -1 Loc);
	## Eai dat dohkke šat maninge.

SELECT (Cond ConNeg) IF  (*-1 (Neg Ind) BARRIER VFIN OR ConNeg OR CS OR CP);
	## Ii go son ollu eanet gárvvohivčče din?

REMOVE ConNeg IF (NOT *-1 Neg BARRIER CS OR CONTRA OR ("go"));
REMOVE (ConNegII) IF (NOT *-1 (Neg Imprt) BARRIER VFIN OR CS);
	## Dasgo mii oinniimet su nástti badjáneamen ja leat boahtán...
	## Stáhta galgá fuolahit ahte gávdno divššohasbearráigeahčči 
	## juohke fylkkas.

SELECT (Imprt ConNeg) IF (*-1 CC BARRIER NOT-ADV LINK *-1 (Imprt ConNeg) 
	BARRIER VERB);
SELECT (Ind ConNeg) IF (*-1 CC BARRIER NOT-ADV LINK *-1 (Ind ConNeg) BARRIER VERB);
	## Allet nappo moraš ja cealkke...
	##

SELECT ConNeg IF (*-1 PrfPrc BARRIER NOT-ADV-PCLE LINK *-1 Neg BARRIER VERB OR CS)
	(0 ("leat"))(NOT *1 PrfPrc BARRIER NOT-ADV-PCLE);
	## Ii dál láhppán gal leat.
# Need more advanced rule to select ConNeg when there is more
# material between Neg and ConNeg.


REMOVE ConNeg IF (NOT *-1 (Neg) BARRIER CLB OR V-IND-FIN)
	(NOT *-1 Nom LINK -1 Neg); 
	## Juohke muorra mii ii šaddat buriid šattuid, čullojuvvo ja 
	## bálkestuvvo dollii.
	## Gávpot mii lea vári alde, ii sáhte leat čihkosis.


# Imperative
# ----------

REMOVE Imprt IF (1 ("go" Pcle));
REMOVE Imprt IF (*-1 (Neg Ind) BARRIER CLB OR V-IND-FIN);
REMOVE Imprt IF (*-1 ConNeg BARRIER VFIN OR COMMA OR CS OR CP);
	## Go bohciida soahpameahttunvuohta das ahte galget go journála... 
	## Mánát eai gillen dálu čorget.
	## Na muhto don it leat dasa nama čállán!

REMOVE Imprt IF (-1 N OR A OR Po);
	## Čuorvvas gullo Ramas. 
	## Boarrásis leai dat vel eanaš ealáhussan ge.
 # This will work fine when headings no longer interfere.

REMOVE Imprt IF (*1C VFIN BARRIER NOT-ADV-PCLE OR CLB);
	## Lágas ii duššaduvvo oktage bustávaš dahje čuoggáš, ovdal go...

REMOVE Imprt IF (*-1 (PUNCT LEFT) BARRIER (PUNCT RIGHT))(0 N);
	## Láigolihtus eai čuovo makkárge sierravuoigatvuođat gáddái (buo.
	## riehkit, stárat).

SELECT Imprt IF (-1 BOS OR PUNCT OR REALCLB)(0 ("leat"))(1 A);
	## Leage buorre!


# Infinitive
# ----------

SELECT Inf IF (*-1 (Neg Sup) BARRIER CLB); 
SELECT Inf IF (*-1 ("amas" CS) BARRIER VERB);
	## ...amaset sin čalmmit oaidnit,... 
	## ...amas lojes hearggit livvut. 

SELECT Inf IF (*-1 NP-HEAD-SG-NOM BARRIER NOT-ADV-PCLE LINK *-1 CC BARRIER NPNHA
	LINK *-1 Inf BARRIER NOT-ADV);
	## Dearvvašvuođaveahkkedárbu galgá árvvoštallojuvvot ja diehto addo-
	## juvvot.
 # This rule has to come before the ones below.
 

# Rules that prevent later selection of Inf for a finite verb in the frame 
# INF-VERB...CC... ____							   
# - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - 
 
REMOVE Inf IF (*-1 NP-HEAD-PL-NOM BARRIER NPNHA LINK -1 CC)
	(NOT *-1 A BARRIER NOT-ADV-PCLE)(NOT *-1 INF-VERB BARRIER VERB);
REMOVE Inf IF (*-1 (Num Nom) BARRIER NOT-NA LINK -1 CC LINK NOT *-1 Inf 
	OR INF-VERB BARRIER VFIN);
	## ...ja ollugat biehttalit iežaset sámevuođa.
	## ...ja máŋga Davviálbmogiid fakultehta studeantta leat oasi oahpus
	## gazzan Romssa universitehtas.

REMOVE Inf IF (-1 CC LINK NOT *-1 Inf BARRIER CS);
REMOVE Inf IF (-3 (A Attr))(-2 CC)(-1 PrfPrc)(0 N);
	## Áirasat geat vuostálastet eavttuhusa jienastit nu ahte deaddilit
	## boalu "vuostá" ja čuožžilit.
	## Mearrasápmelaččat ledje áŋgiris ja oahppan bivdit.


# Now we select Inf 
# - - - - - - - - -

 # The first rule seems much better without the NOT 0 Adv.

SELECT Inf IF (*-1 INF-VERB BARRIER CS OR CLB OR CP OR Neg);#(NOT 0 Adv);
	## Skuvla galggai oahpahit sámemánáide dárogiela.
        ## Prošeavtta áigut čađahit.

SELECT Inf IF (*-1 INF-VERB BARRIER VERB OR NP-HEAD-PL-NOM OR CONTRA OR CS OR CP
	LINK 0 VFIN OR PrfPrc OR Inf);
	## Son oažžu vuokkain ja luovosfirpmiin, namalassii golgadagain,
	## bivdit miehtá deanu.

SELECT Inf IF (-2 Inf)(-1 COMMA)(1 COMMA OR CC);
	## Luondduresursaealáhusat sáhttet álggahuvvot, bisuhuvvot ja ovddiduvvot.


# This rule looks backwards across a relative clause:
SELECT Inf IF (*-1 VFIN BARRIER CS OR CP OR CONTRA LINK *-1 (Pron Interr) 
	BARRIER CS LINK *-1 INF-VERB BARRIER VFIN OR Inf OR CS OR CP);
	## ...galgá sihke divššohas ja váhnemat dahje earát geain lea váhnen-
	## ovddasvástádus oažžut dieđu.

SELECT Inf IF (*-1 ACC-INF-VERB BARRIER Inf OR CS OR CP OR Nom OR Neg);
	## Lea deaŧalaš movttiidahttit studeanttaid gazzagoahtit doavttergráda-
	## oahpu.

SELECT Inf IF (-1 (A Nom))(*-2 COPULAS BARRIER NOT-ADV-PCLE);
	## Dál lea váttis dadjat juste beaivvi goas váttisvuođat čoavdašuvvet.

SELECT Inf IF (*-1 ("leat") BARRIER NOT-ADV-PCLE OR CLB	LINK *-1 Loc 
	BARRIER NOT-ADV-PCLE)(NOT 0 Adv);
	## ...vaikkovel sis livččege ovdal leat leamaš geavahanvuoigatvuohta.

SELECT Inf IF (*-1 ("leat" Sg3) BARRIER NOT-ADV)(*1 ("ahte") BARRIER WORD);
	## Doaibmaplána ulbmil lea sihkkarastit, ahte...

SELECT Inf IF (-3 ("dat" Sg3 Gen))(-2 ("ala"))(-1 ("ahte"));
	## Sámediggi áigu bargat dan ala ahte oččodit ruovttoluotta...


# Verbgenitive
# ------------
SELECT VGen IF (-1 ("gaskan"));
	## Gaskan viessobasa bohte guossit.

SELECT (VGen) IF (*-2 VFIN BARRIER CS)(NOT -1 CC)(-1 ("nu") OR ("hui"))(NOT 0 Adv); 
 # Probably also after other modifiers.
	## Dat hálai nu huikki.

REMOVE VGen IF (0 N)(NOT 0 TV LINK *1 Acc BARRIER NOT-NP);
	## Dakko ii leat menddo fávlái buođu bissehit.

REMOVE VGen IF (0 Inf);
	## Ja dalle lea dušše rohttet spahkagežiid vuolil.

REMOVE VGen IF (-1 BOS)(0 Imprt);
	## Várut beatnagis!

REMOVE VGen IF (NOT *-1 V-IND-FIN BARRIER CLB OR CS)
	(NOT *1 V-IND-FIN BARRIER CLB OR CS);
	## Vuoigatvuohta beassat válljet buohcceviesu.

REMOVE (Pass VGen);
	## Didjiide mihtiduvvo seammá mihtuin.

REMOVE VGen IF (0 ("ovttas"));
	## Lea heivvolaš gieđahallat daid ovttas.

REMOVE VGen IF (0 ("hárve"));
	## Seammás jođálmahttá passivitehta dementiija ovdáneami, eandalii 
	## juos hárve gullá eatnigielas.

REMOVE VGen IF (0 ("njuolgga"));
	## Nubbi gažaldat lea, sáhttágo konvenšuvnna mearrádusaid iskat 
	## njuolgga Norgga duopmostuolus.


# Perfect Participle
# -------------------

SELECT PrfPrc IF (0 Pass);
	## ...nu go namahuvvon vuosttas ja nuppi teakstaoasis.
 # Exceptions might turn up.

REMOVE PrfPrc IF (*-1 CC BARRIER NPNH LINK -1 N LINK NOT *-1 ("leat") BARRIER NPNHA)
	(0 N)(NOT 0 ("leapma" Dimin))(NOT -1 CC LINK *-1 PrfPrc BARRIER VERB);
  	## Journála sirdin ja luoikan.
	## ...de lassánii maid sisafárren ja koloniseren dáidda guovlluide. 

REMOVE PrfPrc IF (*-1 Gen BARRIER NPNH LINK *-1 CC BARRIER NPNH LINK -1 N LINK 
	NOT *-1 ("leat") BARRIER NPNHA)(0 N)(NOT 0 ("leapma" Dimin));
	## Iige njuovvan ja bierggu rádjan leat mihkkege dearvvašvuođa-
	## váttisvuođaid.
 # The reason for the specific mention of Gen in this rule is that at this stage, 
 # the nominals in question are both Acc and Gen. Since Acc is NPNH, it acts as a 
 # barrier for the previous rule.  

SELECT PrfPrc IF (-1 Neg);
	## Buorre lea go it fuobmán.

SELECT PrfPrc IF (*-1 Neg BARRIER PrfPrc OR CONTRA OR VFIN OR CS)
	(NOT *-1 ConNeg BARRIER Neg LINK NOT 0 ("leat"))(NOT 0 Ess)
	(NOT *1 ConNeg BARRIER CC OR CS OR CP OR CLB); 
	## Iihan dat liikon Kárášjohkii.

SELECT PrfPrc IF (*-1 Neg BARRIER NOT-ADV-PCLE)(0 INF-VERB)
	(*1 Inf BARRIER VERB OR CS);
	## No hits any more?

REMOVE (N Ess) IF (*-1 Neg BARRIER NOT-ADV-PCLE)(0 PrfPrc)
	(NOT *1 ConNeg OR PrfPrc BARRIER CS);
	## Manne son ii ohcan dan ođđa virggi?

SELECT PrfPrc IF (*-1 Neg BARRIER VERB OR CS OR CP)
	(NOT *1 ConNeg OR PrfPrc BARRIER VERB OR CS OR CP OR ("nugo"));
	## In leat gal gullan inge lohkan.

REMOVE PrfPrc IF (*-1 ("leat") BARRIER NPNHA OR Nom LINK *-1C (N Ess) 
	BARRIER NOT-ADV LINK *-1 BOS OR CS OR CP BARRIER NPNH)(0 N)
	(NOT 0 ("leapma" Dimin));
	## Dasa lassin álggahuvvojit movttiidahttin ja nannendoaimmat, maid 
	## vuođđun lea dutkan.

SELECT PrfPrc IF (*-1 ("leat") BARRIER NOT-ADV-PCLE)
	(NOT 0 Ess LINK 1 (A Nom) OR PrfPrc)
	(NOT 0 Actio LINK 1 PrfPrc LINK 0 (@-FMAINV))(NOT 1 ("veara"))
	(NOT 1 CC LINK 1 Actio LINK NOT 0 PrfPrc)
	(NOT 0 Actio OR N LINK *1 ("leat" PrfPrc) BARRIER COPULAS);
	## Norgga láhkaaddit leat áigá jo suokkardan vuđolaččat kvenaid 
	## eamiálbmotstáhtusa gažaldaga.

SELECT PrfPrc IF (*-1 (@OBJ) BARRIER NOT-ADV LINK *-1 ("leat") BARRIER NPNHA)
	(NOT 0 Actio LINK -1 PrsPrc)(NOT 1 COMMA LINK *1 Actio BARRIER NPNH)
	(NOT 0 Actio LINK *1 PrfPrc BARRIER NOT-ADV-PCLE);
	## Leago barggu ohcan?

SELECT PrfPrc IF (-1 (@SUBJ) LINK *-1 ("leat") BARRIER NOT-NP)
	(NOT 0 Actio LINK -1 PrsPrc)(NOT 0 Actio LINK 1 CC LINK 1 Actio LINK 
	*1 N BARRIER NOT-NP);
	## Leatgo sii njuovadan juo?

SELECT PrfPrc IF (*-1 ("leat") BARRIER VERB OR CS OR CP OR CC LINK NOT 0 PrfPrc)
	(NOT 0 Actio LINK *1 PrfPrc BARRIER NOT-ADV-PCLE LINK NOT 0 (@PrcN>))
	(NOT 0 Ess LINK 1 (A Nom) OR PrfPrc)(NOT 1 ("dihte") OR ("dihtii"))
	(NOT 1 COMMA OR CC LINK *1 Actio BARRIER NPNH)(NOT 1 ("veara"));
	## Biret-Elle lea easka skuvllas geargan.

SELECT PrfPrc IF (*-1 VFIN BARRIER CLB OR CS)(NOT 0 N OR Actio OR Ess)
        (NOT *-1 CC BARRIER VFIN);
	## Sámediggi sáhttá mearridit meannudit ášši giddejuvvon uvssaid duohken. 
        
  # PrfPrc if there is not a Sg1 to the left, but if there is a CC to
  # the right of a possible Sg1, then we forget about the Sg1.

SELECT PrfPrc IF (-1 NP-HEAD-GEN LINK *-1 ("dadjat") BARRIER NPNHA)(1 N)
	(2 NP-BORDER);
	## Dat dajai iežas gávdnan golli.
 # More verbs should be listed here.

SELECT PrfPrc IF (0 ("mannat"))(1 TIME);
	## Ii leat boahtán mannan vahkus.
	## Mannan čuohtejagi gaskkamuttos lei stuorra gilvu.

REMOVE (V Pass Sg1) IF (*1 (N Nom) BARRIER CS OR CP);
	## Juolluduvvon permišuvnnat.


# "Leamaš" without finite verb
# - - - - - - - - - - - - - -
SELECT PrfPrc IF (-1 NP-HEAD)(NOT -1 Gen)(0 ("leapma" Dimin)); 
SELECT PrfPrc IF (0 ("leapma" Dimin))(*1 A-CASE BARRIER NOT-ADV-PCLE);
	## Muhto dat leamaš vissa gobmi.
	## Árbevirolaččat leamaš várrugasat addit ovttaskas olbmuide...

SELECT PrfPrc IF (0 ("leapma" Dimin))(*1 Nom BARRIER NOT-ADV-PCLE);
SELECT PrfPrc IF (NOT *-1 VFIN BARRIER CS)(0 ("leapma" Dimin))
	(NOT *1 VFIN BARRIER CP OR CS);
	## Muhto leamaš gal ovdal okta bivdovuohki vel.
	## Sápmelaččat je eiseválddiid gaskkas leamaš kulturboatkka, mii...

SELECT PrfPrc IF (0 ("leapma" Dimin))(*1 (V Actio Ess) BARRIER NOT-ADV-PCLE);
	## Dáidet leamaš buohcame?

SELECT PrfPrc IF (*-2 PrfPrc BARRIER VERB OR CC OR CP OR CS)(-1 CC)
	(0 ("leapma" Dimin));
	## Doaibmaplána lea bidjan buori vuođu ja leamaš mielde rievdadeame...


# Actio
# -----
REMOVE PrfPrc IF (-1 NP-HEAD-GEN)(0 Actio)(1 N)(2 NP-BORDER OR Dem);
	## Háliidit go sii konvenšuvnna mearridan vuoigatvuođaid sirdo-
	## juvvon earáide?


# Present participle
# ------------------
SELECT PrsPrc IF (-1 ("hui"))(*1 N BARRIER NPNH);
	## Mudden ferte dagahit janza ja hui vahágahtti meassamiid.

SELECT PrsPrc IF (-1 ("nu"))(1 ("go" Pcle));
	## Mearridanváldi berre leat nu čadni go vejolaš.

REMOVE PrsPrc IF (0 N)(1C NP-BORDER);
REMOVE PrsPrc IF (0 N)(1 CLB)(2 NP-BORDER);
	## Ii dain leat áiti ii ge láhtu.
	## Sii geain lea váldi, gohčoduvvojit buorredahkkin.


# Rules for "orrut"
# -----------------
SELECT V-SG3 IF (0 ("orrut"))(*1 PrfPrc OR (Actio Ess) BARRIER VFIN OR CS OR CP);
	## Elásttalaš hálddašanvuoigatvuohta čuoččuhus orru 
	## mielddisbuktimin ahte...

SELECT VERB IF (0 ("orrut"))(*1 A BARRIER NOT-ADV-PCLE);
	## Sus lea vuoigatvuohta orrut anonyman. 

SELECT V-SG3 IF (0 ("orrut"))(*1 ("dego") OR CS BARRIER NOT-ADV-PCLE);
  	## Dás orru datte ahte...
	## Orru dego boazodoallu ain geahččaluvvosii hálddašuvvot ja stivrejuvvot...
 
SELECT Ind IF (0 ("orrut"))(*1 ("leat" Inf) BARRIER VERB OR CS OR CP);
	## Min oktavuođas orru omd. boazodoallu leat leamen...
 

# Rules for "addit" (which is an adjective, but more often a verb)
# ----------------------------------------------------------------

SELECT Inf IF (*-1 COPULAS BARRIER NOT-ADV)(0 ("addit"))
	(NOT *1 Nom BARRIER CS OR CP OR REALCLB);
	## ...man ulbmil lea addit oppalaš ipmárdusa sámi kultuvrras.




# ==================== #
# Disambiguating nouns #
# ==================== #
      CONSTRAINTS      #
# ==================== #


# Proper nouns
# ============

# Prop or not
# -----------

SELECT Prop IF (NOT *-1 BOS OR (":") OR (""") BARRIER WORD); 
	## Áirras Egil Olli nammaduvvui Sámedikki ovddasteaddjin.
 # Capitalised nouns in the middle of a sentence are names.

REMOVE (Prop Pl) IF (0 (Prop Sg))(NOT 0 ("Sámediggi") OR (laš));    
	## Marit lea dál.
  # Marit N Sg, not Mari+t Pl Nom

REMOVE (Prop Pl) IF (-1 BOS)(0 CNOUN-PL-NOM);        
	## Sámit leat álgoálbmot.

REMOVE (Prop Pl Nom) IF (-1 BOS)(NOT *1 V-PL3 BARRIER CS);	     
	## Unnit nuppástusaiguin.	

REMOVE (Prop Pl Acc) IF (-1 BOS)(0 CNOUN);
	## Gunaid galgá leat várrogas atnit.

SELECT Prop IF (-1 BOS)(0 Loc)(1 COMMA)(2 MANNU)(3 Num) ;
SELECT Prop IF (-1 BOS)(0 Loc)(1 COMMA)(2 Num) ;
	## Deanus, borgemánu 31. b. 2005.

# Removing proper nouns that are lookalikes
# - - - - - - - - - - - - - - - - - - - - -

REMOVE ("Dan" Attr) IF (NOT 1 Prop);
REMOVE ("Dan" Prop) IF (1 CNOUN); # Dan is Nom or Attr.

REMOVE ("Hu" Ill) IF (1 A or Adv);

REMOVE Prop IF (-1 BOS)(0 ("Lassi")); 
#REMOVE Prop IF (0 ("Lassi"))(1 Pron);
#REMOVE Prop IF (0 ("Lassi"))(*1 N BARRIER NPNH);
	## Lassi dutkandoarjagat sáhttet leat mielde... 
	## Lassin dasa ferte váldit vuhtii sierralágan dilálašvuođaid.
	## Lassin jorgalandoarjagii, de lea dárbu sihke...
  	## Lassin leat oalle viiddis boazuguohtunguovllut.

SELECT Prop IF (0 ("Lásse"))(*1 VFIN BARRIER CS);
	## Lásse dat leigga sihtan mu váldit lusan.
 
REMOVE ("Lea") IF (-1 BOS OR PUNCT)(NOT 1 V-SG3); 
REMOVE ("Lea") IF (-1 BOS OR PUNCT OR (":"))(1 A); 
	## Lea dušše okta Ipmil. Lea idja.
	 ## Lea buolaš. Son dajai: Leage gierdávaš.
 
REMOVE ("Man") IF (-1 BOS OR PUNCT OR (":"))(0 (V Imprt));
	## De son celkkii sutnje: "Mana basadit Siloaláddos."
  # Cannot think of sensible conditions.

REMOVE Prop IF (-1 BOS)(0 ("Mo"))(NOT 1 go)(*1 ("?"));
REMOVE Prop IF (-2 CLB)(-1 PUNCT)(0 ("Mo"));
	## Mo sáhttá su riika dalle bissut?

REMOVE Prop IF (0 ("oassi"));
	## Osiid ektui, dárkojuvvo Finnmárku muđui Almennehin,...


# Selecting proper nouns
# - - - - - - - - - - - -
SELECT (Prop Nom) IF (0 ("Rabbi"))(1 COMMA);
	## Rabbi, lea buorre go mii leat dáppe.

SELECT Prop IF (0 ("Ruoŧŧa"))(1 CC)(2 Prop);
	## Ruoŧa ja Suoma beale riektedilli lea measta ovttalágan.


# Prop as Attr or not
# -------------------

SELECT (Prop Attr) IF (NOT -1 Prop)(1 CC)(2 (Prop Attr))(3 INITIAL OR (N Prop));
        ## Dá leaba Brita ja Ove Kåven.

REMOVE (Prop Attr) IF (NOT 1 Prop OR INITIAL); 
	## Trond Trosterud lea dál.
        ## Olles doaibmaviessu lea sullii 2300 njealjehasmetera. 

SELECT Attr IF (0 Prop)(1 INITIAL)(2 Prop);
SELECT Attr IF (0 Prop)(NOT 0 Loc)(1C (N Prop))
	(NOT 0 (@OBJ) LINK *1 (@OPRED) BARRIER NOT-NP); 
	## Áirras nr. 17 Randi S. Pedersen oaččui permišuvnna.
	## Áirras nr. 10 Ole Henrik Magga oaččui permišuvnna.


# Choosing between different proper nouns
# ---------------------------------------

# Jáhko vs. Jáhkob
# - - - - - - - - -
REMOVE ("Jáhko") IF (0 ("Jáhkob"));
	## De oinnii Jáhkoba ja su vielja Johanasa.

# Johan/Johana/Johanas
# - - - - - - - - - - -
SELECT ("Johanas" Nom) IF (1 V-SG3)(*1 VFIN-NOT-AUX);
	## Daid beivviid gásttašeaddji Johanas bođii ja sárdnidišgođii. 

SELECT ("Johanas" Nom) IF (*-1 COPULAS BARRIER NOT-NP)
	(NOT *1 V-TRANS-ACT BARRIER CS OR REALCLB);
	## Su namma galgá leat Johanas.

# Nilse vs. Nilsen
# - - - - - - - - -
REMOVE ("Nilse") IF (0 ("Nilsen" N Prop Sg Nom));
	## Tor Nilsen lea boahtán.

# Norga vs. Norge
# - - - - - - - -
REMOVE ("Norge") IF (0 ("Norga"));
	## Mun vuolggán Norgii.
	
	
# Disambiguating the semantic tags
# ================================

REMOVE Plc (-1 (Prop Mal Attr) OR (Prop Fem Attr))(0 Sur);
    ## Trond Trosterud lea dál.	
	


# Nouns versus adjectives
# =======================
 # Note that although the following rules are single cohort rules, they could 
 # not come very early, since correct readings might then be removed. Also note 
 # that we do not remove (A Sg Nom) or (A Pl Nom).

REMOVE (A Sg Acc) IF (NOT *-1 COPULAS BARRIER NOT-ADV-PCLE)(0 (N Sg Acc));
REMOVE (A Sg Gen) IF (0 (N Sg Gen))(NOT 0 Ord);
	## Beaivváš addá eatnamii lieggasa. 
	## Muhtomin ferte guokte gurpasa bidjat oktan noađđebeallin.

REMOVE (A Sg Loc) IF (0 (N Sg Loc));
REMOVE (A Sg Ill) IF (0 (N Sg Ill));
REMOVE (A Sg Com) IF (0 (N Sg Com));
	## Ovtta ovddasteaddjis divššohasa ovttas lea vuoigatvuohta...
	## Juohke gurpasii biddjojuvvo guottahat.
	## Lean mun gal oaidnán čakčaseavdjadiin dola rievdamiin čázi alde.

REMOVE (A Pl Acc) IF (0 (N Pl Acc));
REMOVE (A Pl Gen) IF (0 (N Pl Gen));
	## Ulbmil lea movttiidahttit sámi nuoraid gazzagoahtit universitehtaoahpu.
	## ...de lea inuihtaid dilli áibbas eará go sápmelaččaid. 

REMOVE (A Pl Loc) IF (0 (N Pl Loc));
REMOVE (A Pl Ill) IF (0 (N Pl Ill));
REMOVE (A Pl Com) IF (0 (N Pl Com));
	## Leigo sápmelaččain maid vuoigatvuohta bivdimii.
	## Sámi nuoraide láhččojuvvo vejolašvuohta oassálastit gieddebargguide.
	## Sii ledje gávpejasat oktavuoigatvuođain gávppašit sápmelaččaiguin.

SELECT (N Nom) IF (0 (A Nom))(*1 COPULAS BARRIER (N Nom) OR (Pron Nom) OR CC 
	OR CS OR CP OR CLB LINK NOT *1 V-NOT-AUX BARRIER CS OR CP) 
	(*1 (Pron Nom) OR (A Nom) BARRIER CS OR CP LINK NOT 0 Interr); 
	#LINK NOT *1 Nom BARRIER CS OR CP);
	## Divššohasa ovddasteaddji vuosttas teakstaoasi mielde lea son gii...

SELECT N IF (NOT 0 Po OR Adv OR VFIN OR Inf OR PrfPrc OR PrsPrc OR (Num Nom))
	(NOT 0 Attr LINK *1 N BARRIER NPNH)(1 PrfPrc LINK 0 V-NOT-AUX);
	## Go joddu lea čáhcái boahtán,..
 


# Case disambiguation
# ===================

# =========== #
  CONSTRAINTS #
# =========== #

# Special rules
# =============

# Accusative or genitive in front of ALU and in front of adjectives
# -----------------------------------------------------------------

SELECT Acc IF (0 NUMERALS)(1 MEASURE)(2 ALU OR A);
	## Visti lei vihtta mehter alu.

SELECT Acc IF (-1 (Num Acc))(0 MEASURE)(1 ALU OR A);
	## Min viessu lea vihtta mehtara alu.
	## Sus lea njeallje mehtara guhkkosaš biila.

SELECT Acc IF (-1 (Num Acc))(0 MEASURE)(1 Pr);
	## Ádjá viessu lea guokte miilla bajábeal márkana.

SELECT Acc IF (-1 (Num Acc))(0 TIME)(1 ("boaris"));
	## Mánná lea golbma jagi boaris.

SELECT Gen IF (-1 (Num Gen))(0 MEASURE)(1 A OR ALU);
	## Visti lei viđa mehter alu.
	## Dohko manai viđa mehter govdosaš geaidnu.

SELECT Gen IF (-1 (Num Gen))(0 TIME)(1 ("boaris"));
	## Son lea viđa jagi boaris.

SELECT Acc IF (-1 (Num Acc))(0 TIME)(1 ("maŋŋil"));
	## Dat dáhpáhuvai golbma jagi maŋŋil.

SELECT Acc IF (0 Num)(1 (N Acc))(2 ("velggolaš"));
SELECT Acc IF (-1 Num)(1 ("velggolaš"));
	## Ánde lea munnje vihttalot ruvnno velggolaš.

SELECT Gen IF (0 ("okta"))(1 ALU);
	## Dábálaččat leat joddut sullii ovtta guhku.

SELECT Acc IF (0 Num)(1 ("gitta"))(2 (Num Acc));
	## Dát ledje gáddenuohtit mat ledje 180 gitta 200 sala guhku, ja 15 gitta 
	## 20 sala čieŋu.


# Locative, not PxSg3
# -------------------

SELECT Loc IF (NOT *-1 Sg3)(0 PxSg3);
	## Mun borren áhkus luossaláibbi.


# Genitive
# ========
 # Since disambiguation between accusative and genitive is very difficult, 
 # let us pick out as many genitives as possible here.

# Various genitive rules
# ----------------------

# Genitive after quantifier
# - - - - - - - - - - - - - 
SELECT Gen IF (*-1C (Num Nom) BARRIER NPNH OR Gen OR CC OR PUNCT OR REALCLB 
	LINK NOT 0 ("okta") OR Ord)(NOT 0 VERB OR ("nr") OR ("nummar") OR ("s"));
	## Golbma čáppa, doaimmalaš nieidda livžo áhčiset riegádanbeaivve.

SELECT Gen IF (*-1 (Num Nom) BARRIER NPNH OR Gen OR PUNCT OR REALCLB LINK 
	NOT 0 ("okta") OR Ord LINK *-1 ("leat") BARRIER NOT-ADV-PCLE)
	(NOT *1 V-TRANS-ACT BARRIER CLB OR CC OR CS OR CP);
	## Joavkkus leat vihtta oahppi Kárášjogas.

SELECT Gen IF (*-1 (Num Nom) BARRIER NPNH OR Gen OR PUNCT OR REALCLB LINK 
	NOT 0 ("okta") OR Ord LINK -1 CC OR CLB LINK *-1 (Num Nom) BARRIER NOT-NP)
	(NOT 0 Num LINK -1 CC OR CLB);
	## Joavkkus leat vihtta oahppi Kárášjogas, vihtta oahppi Muosáhis ja 
	## vihtta Bearalvágis.
 
SELECT Gen IF (-1 CC)(-2 Cmpnd)(*-3 NUMERALS BARRIER NPNH OR PUNCT LINK 0 Nom);
	## Máŋga sámi dearvvašvuođa- ja sosiálabargi vásihit dan. 

SELECT Gen IF (*-1 (Num Nom) BARRIER NOT-NP OR CLB)(NOT -1 CC)
	(*1 V-PL3 BARRIER NOT-ADV-PCLE);
	## Máŋga Romssa universitehta dutkanbirrasa leat ásahan ovttasbargguid...

SELECT Gen IF (*-1 NUMERALS BARRIER NPNH OR CC OR CLB OR PUNCT OR ("ruvdnu") LINK
	0 Nom LINK NOT -1 ABBR OR CLB)
	(NOT *-1 (Num Acc) BARRIER NPNH LINK *-1 Nom BARRIER CC OR CLB OR CS OR CP)
	(NOT *-1 V-TRANS-ACT BARRIER VERB OR CS OR CP OR CC OR CLB)
	(*1 Gen BARRIER NPNH);
	## Máŋga Romssa universitehta dutkanbirrasa leat ásahan ovttasbargguid...


# Genitive in front of postposition
# - - - - - - - - - - - - - - - - - 
SELECT Gen IF (NOT 0 Adv OR PrfPrc)(1C Po)(NOT 1 Px);    
SELECT Gen IF (NOT -1 ("§"))(0 Num)(*1 (N Gen) BARRIER NPNH LINK 1 Po);
	## Lávdegottiid čoahkkimat dollojit giddejuvvon uvssaid duohken.
	## Divššohasas lea vuoigatvuohta oažžut dearvvašvuođadilis árvvo-
	## štallojuvvot 30 bargobeaivvi sisa.

SELECT Gen IF (*-1 BOS BARRIER NOT-CC)(1 (Po Qst));
	## Girdi mieldego don manat?

SELECT Gen IF (*-1 BOS BARRIER NOT-CC)(1 Po)(2 go);
	## Girdi mielde go don manat?

SELECT Gen IF (1 CC)(*2 Gen BARRIER NPNH LINK 1 GASKAL);
	## Juos fágabargi ja geavaheaddji gaskka leat dasa lassin gielalaš
	## dahje kultuvrralaš gulahallanváttisvuođat,...

SELECT (V Actio Gen) IF (1 ("dihte" Po) OR ("dihtii"))(*1 (N Acc) BARRIER NOT-NP);
	## Vuovdon earit máhcahovvojit sámi guovlluide nannen dihte báhcán doaluid.

SELECT (V Actio Gen) IF (1 CC)(2 (V Actio Gen))(3 ("dihte" Po) OR ("dihtii"));
	## ...seailluhan, nannen ja ovddidan dihte sámi servodaga.

SELECT Gen IF (0 Actio)(1C Po);
	## Válddán dan ášši guorahallan vuollái.

SELECT Gen IF (*-1 Po OR PREGEN BARRIER NOT-NP OR PUNCT OR REALCLB)
	(NOT 1 COMMA LINK 1 Gen LINK NOT *1 CC BARRIER Po)(NOT 1 Pers)
	(*1 Po OR PREGEN BARRIER NOT-NP OR REALCLB);
	## ...dan vuolde sisdoalu birra divššohasjournálain, čállima, divodeami,
	## sihkkuma, vurkema, sirdima, journála beassanvejolašvuođa ja dušša-
	## deami birra.

SELECT Gen IF (1 (PUNCT LEFT))(*2 (PUNCT RIGHT) LINK 1 Po);
	## Dearvvašvuođaregisttarláhka lea sierraláhka heaggadieđuidlága (cuoŋo-
	## mánu 14. b. 2000 láhka nr 31 heaggadieđuid gieđahallama birra) ektui.

SELECT Gen IF (1 CC)(*2 NP-HEAD-GEN BARRIER NPNHA LINK 1 Po);
	## Seammás áiggun maiddái ovdanbuktit dihto reflekšuvnnaid sihke eaŋkil 
	## rievttálaš geavatlaš gažaldagaid ja eambbo prinsihpalaš gažaldagaid hárrái.


# Genitive after preposition
# - - - - - - - - - - - - - -
SELECT Gen IF (*-1 Pr BARRIER NPNH OR REALCLB OR CONTRA)(NOT 0 VFIN)
	(1 NP-BORDER OR CC OR COMMA OR ("ieš")); 
	## Earret mu ii giige lea boahtán.
	## Sáhka su birra viidánii miehtá Syria, ja ...
	## ...gaskal vejolaš ja dohkálaš iskkadan- ja dikšunvugiid.
	## ...ja vulggii rastá jávrri iežas gávpogii. 

SELECT Gen IF (*-1 Pr BARRIER NOT-NP OR Po)(*1 Interr BARRIER WORD);
	## Sáhttá dadjat, ahte jagi 1978 láhka balansere gaskkal sámi 
	## vuoigatvuođaid boarásmuvvan oainnu, mii lei ráđđejeaddjin 
	## "vigihis ávkkástallanrievtti áigodagas", ja eanet ođđaáigásat 
	## vuoigatvuođaid dovddasteaddji oainnu.

SELECT Gen IF (-1 Pr)(0 Num);
	## Jus divššohas lea badjel 16 jagi boaris... 

SELECT Gen IF (-3 GASKAL)(-2 (Num Gen))(-1 CC)(0 Num);
	## Jus divššohas lea gaskal 16 ja 18 jagi boaris,...


# Genitive inside a two-part Po
# - - - - - - - - - - - - - - -
SELECT Gen IF (-1 POST-A)(1 N)(2 POST-B);
	## Nu leat 40 buolvva Dávveda rájes Babylonia fáŋgavuođa rádjái.


# Genitive attribute of adposition.
# - - - - - - - - - - - - - - - - -
SELECT Gen IF (1 ("bealde" Pr) OR ("beale" Pr) OR ("beallái" Pr) OR ("geahčai" Pr)
	OR ("geahčen" Pr) OR ("geaže" Pr));
	## Ánde boahtá don bealde joga.


# Genitive in front of "guvlui"
# - - - - - - - - - - - - - - -
SELECT Gen IF (NOT 0 Dem OR Loc)(1 ("guovlu" N Sg Ill));
	## Áddjá lei nohkkan, láse guvlui oaivi.


# Genitive in front of "láhkái"
# - - - - - - - - - - - - - - - 
SELECT (Actio Gen) IF (1 ("láhki" N Sg Ill));
	## ...go dearvvašvuođabargit iežaset árvvoštallama mielde gávnnahit...
	## Dat ráhkaduvvo gáttis gárvvisin ja gessojuvvo vatnasii suohppun
	## láhkái.

SELECT (N Gen) IF (1 ("láhki" N Sg Ill));
	## Dat lea áhčis láhkai.


# Genitive with "dan mađe N"
# - - - - - - - - - - - - -
SELECT (Gen Sg) IF (0 ("dat"))(2 ("mađe"));


# Genitive with "veara"
# - - - - - - - - - - -
SELECT Gen IF (NOT 0 Nom)(*1 COPULAS BARRIER V-NOT-AUX OR CS 
	LINK *1 ("veara") BARRIER VERB OR CS);
	## Min ii leat veara dohko vuolgit.
	## Dasgo bargi lea bálkkás veara. # NOT 0 Nom!


# Genitive in comparison
# ----------------------
SELECT Gen IF (NOT *-1 V-TRANS-ACT BARRIER NOT-ADV-PCLE)(NOT 0 VERB)(1 (A Comp))
	(NOT 1 VERB OR Adv)(NOT 2 go);
	## Mun jáhkán du iežan falibun.

	
# Genitive relative pronouns
# - - - - - - - - - - - - - -
SELECT (Pron Interr Gen) IF (NOT 0 Indef OR ("mii"))(1 N)
	(*1 COPULAS BARRIER CLB)(NOT *1 V-TRANS-ACT BARRIER CLB);
        ## ...ja gii galgá árvvoštallat guđe dieđut galget leat journálas.
	## ...ja guđe árvvoštallamiid vuođul doaibmabidju lea álggahuvvon.

SELECT (Pron Interr Gen) IF (0 ("mii"))(1 (N Nom))
	(*1 COPULAS BARRIER CLB)(NOT *1 V-TRANS-ACT BARRIER CLB);
 	## Jerusalemis lea láttu man namma hebreagillii les Betesda.


# Genitive indefinite pronouns
# - - - - - - - - - - - - - - -
SELECT Gen IF (0 Indef)(1 (N Ill) OR (N Loc) OR (N Com));
	## De galget ollu dearvvašvuođabargit iešguđet fidnojoavkkuin dan
	## geavahit. 


# Genitive in proper name sequences
# - - - - - - - - - - - - - - - - - 
REMOVE Acc IF (NOT *-1 V-TRANS-ACT BARRIER NOT-ADV-PCLE)(0 Prop)(1 Prop)
	(NOT 1 ADVLCASE);          
	## Dat lea Norgga Röros ja Máhte Piera.

# The idea behind the following rule pair is to that in a (Spec + N) 
# sequence, the Prop is the possessor rather than the other way around.
# XXX these two rules should be generalised to star notation.
REMOVE Acc IF (*-1 V-TRANS-ACT BARRIER NOT-ADV-PCLE)(0 Prop)(*1 (N Acc) BARRIER NPNH);#(NOT *1 Po BARRIER N);
	## Mun oainnán Máhte beatnaga Máreha biilla duohken.
	## Mun oainnán kánske Máhte beatnaga Máreha biilla duohken.
	## Son oaidná min Máhte biilla duohken.
	#  0 = Máhte

#REMOVE Acc IF (*-1 V-TRANS-ACT BARRIER NOT-ADV-PCLE)(0 Prop)(1 Gen LINK 1 Po BARRIER N);
	## Mun oainnán Máhte beatnaga Máreha biilla duohken.




# Genitive after "mii" and "mihkkege"
# - - - - - - - - - - - - - - - - - -
SELECT (N Pl Gen) IF (-1 ("mii") OR ("gii"))(1 NP-BORDER)(*1 ("?")); 
	## Maid ipmašiid doppe dagat?
 # Embedded questions are also possible, but harder to formulate a rule. 

SELECT (N Pl Gen) IF (-1 ("mihkkege"));
	## Iige njuovvan ja bierggu rádjan leat mihkkege dearvvašvuođa-
	## váttisvuođaid.


# Genitive subjects of non-finite verbs
# - - - - - - - - - - - - - - - - - - -
SELECT Gen IF (NOT *-1 ("leat") BARRIER CS)(NOT 0 Interr OR Dem)(1 Actio)
	(NOT 1 Ess OR Com)(NOT 1 (V Actio Loc))
	(NOT 1 PrfPrc LINK *1 NP-HEAD-ACC BARRIER NPNH)(NOT 2 Po);
	## Eatni dahkan láibi lei buorre.

SELECT Gen IF (0 Interr)(1 (Actio Gen))(2 N);
	## Gean ráhkadan fanas dat lea?

SELECT Gen IF (*-1 ("leat") BARRIER NPNHA)(1 (V Actio Loc));
	## Dat ii leat geafes olbmo nagodeames. 

SELECT Gen IF (*-1 (@OBJ) BARRIER VERB LINK *-1 V-TRANS-ACT BARRIER NPNHA)
	(1 Ger);
	## Son muitalii dan buot olbmuid guladettiin.


# Accusative after actio (exceptions to next rule)
# - - - - - - - - - - - - - - - - - - - - - - - - 
SELECT Acc IF (*-1 ("dihte") OR ("dihtii") BARRIER NPNH LINK -1 Actio)
	(1 NP-BORDER);
	## ... diehtin dihtii iežas dili ja veahki sisdoalu.

SELECT Acc IF (*-1C Actio BARRIER HNOUN-NOM OR (Pron Nom) OR VERB OR CC OR CS OR CP 
	LINK NOT 0 Cmpnd OR Gen OR IV)(NOT -1 CC LINK -1 Actio LINK NOT 0 Cmpnd)
	(NOT 0 TIME OR ROUTE OR Loc)(NOT 0 Num LINK -1 ("§"))(1 NP-BORDER)(NOT 1 N); 
	## ...dahje biehttalit botkemis nealgudeami mii lea jođus. 


# Accusative after "vuoi" (exceptions to REMOVE Acc-rule below)
# - - - - - - - - - - - - - - - - - - - - - - - - -
SELECT Acc IF (*-1 ("vuoi") BARRIER NPNH)
	(NOT 0 TIME LINK *-1 (N Acc) BARRIER ("vuoi"))
	(NOT *1 N BARRIER NPNH OR CC OR CLB);
	## Vuoi du, Betsaida!

SELECT Acc IF (*-1 ("vuoi") BARRIER NOT-NP OR CLB OR CP)
	(NOT 0 TIME LINK *-1 (N Acc) BARRIER ("vuoi"))(1 NP-BORDER);
	## Vuoi dán máilmmi ja dan fillehusaid!
 
SELECT Acc IF (*-1 ("vuoi") BARRIER NOT-NP)(0 Dem);
	## Vuoi surgat dan gándda!

SELECT Acc IF (*-1 CC BARRIER NPNH LINK -1 Acc LINK *-1 ("vuoi") BARRIER NOT-NP)
	(NOT 0 Dem OR Pers);
	## Vuoi áhpehemiid ja njamaheddjiid daid beivviid! 

SELECT Acc IF (*-1 Pers BARRIER NPNH OR CLB LINK *-1 ("vuoi") BARRIER NOT-ADV);
	##  Muhto vuoi din riggáid, dasgo dii lehpet juo ožžon iludet.


# Accusative after adjectives and nouns
# - - - - - - - - - - - - - - - - - - -

SELECT Acc IF (*-1 A BARRIER NPNH OR CC OR CLB OR PUNCT LINK 0 Nom LINK 
	NOT 0 Attr OR ("buorre") OR Adv OR N)(NOT 0 Px OR PrfPrc OR Adv)
	(1 EOS OR CLB); #(NOT 0 TIME);
	## Mánát leat oažžulat guliid.

SELECT Acc IF (-1 ("ahkit"));
	## Mánás lea ahkit eatni.


# Genitive (or nominative) in clause without full verb
# - - - - - - - - - - - - - - - - - - - - - - - - - - -
REMOVE Acc IF (NOT *-1 V-TRANS-ACT BARRIER CS)
	(NOT *-1 (Pass PrfPrc) BARRIER NPNH LINK *-1 V-TRANS-ACT BARRIER NPNHA)
	(NOT -1 CC LINK -1 Acc LINK -1 ("ollu"))(NOT 0 TIME)
	(NOT 0 NUMERALS LINK 1 TIME OR MEASURE)
	(NOT *1 V-TRANS-ACT BARRIER CS LINK 0 VFIN OR (@-FMAINV)) # Ignores PrcN>
	(NOT *1 CLB BARRIER NOT-NP LINK *1 (Pron Acc) BARRIER NOT-ADV-PCLE);
	## Leago doppe kafea?
	## Amma duot lei Niillasa guollebiila?
	## Láhka divššohasvuoigatvuođaid birra.
	## ...jus divššohas oažžu ávkki dearvvašvuođaveahkis. 
	## Sámedikki čoahkkimat dollojuvvojit rabas uvssaid.

 # This rule also (correctly) removes Acc from many elements that will 
 # eventually be analysed as verbs.

REMOVE Acc IF (*-1 CP BARRIER VERB)(NOT 0 TIME)
	(*1 COPULAS BARRIER V-NOT-AUX LINK NOT *1 VERB);
	## ...ja makkár váldi dain nuppádassii berre leat.
	## ...main sámi ja sámi guoskevaš fáttát leat guovddážis.
 # This rule removes Acc in subordinate clauses that has no full verb.

REMOVE Acc IF (*-1 COPULAS BARRIER NOT-AA LINK *-1 (@SUBJ) BARRIER V-NOT-AUX 
	LINK *-1 BOS OR CLB BARRIER NPNH)(1 CLB OR EOS);
	## Ale váldde, dat lea mu!


# Genitive in front of "oktavuođas" etc.
# - - - - - - - - - - - - - - - - - - -
SELECT Gen IF (1 PREGEN);
	## ...go sii bargguset oktavuođas Sámedikkis ožžot dakkár dieđuid.
	## ...geat barget erenoamážit sámi guoskevaš čuolbmačilgehusaiguin.


# Genitive between Dem and N
# --------------------------
 # Here I have left out all rules that would involve an Acc/Gen demonstrative,
 # since these demonstratives could be objects not belonging with the N.
 
REMOVE Acc IF (*-1 (Dem Nom) BARRIER NPNH)(*1C (N Nom) BARRIER NPNH OR Dem);
	## Dát eaiggáduššanvuoigatvuođa doaba de geahččaluvvo...

REMOVE Acc IF (*-1 (Dem Sg Com) BARRIER NPNH)(*1 (N Sg Com) BARRIER NP-BORDER OR Dem);
 	## No examples in corpus, but the rule should work. 

REMOVE Acc IF (*-1 (Dem Pl Ill) BARRIER NPNH)(*1 (N Pl Ill) BARRIER NP-BORDER OR Dem);
	## Dearvvašvuođaregisttarláhka gusto daidda dearvvašvuođa-
	## hálddahusa dearvvašvuođadieđuid gieđahallamiidda.

REMOVE Acc IF (*-1 (Dem Pl Loc) BARRIER NPNH)(*1 (N Pl Loc) BARRIER NP-BORDER OR Dem);
	## No examples in corpus.

REMOVE Acc IF (*-1 (Dem Pl Com) BARRIER NPNH)(*1 (N Pl Com) BARRIER NP-BORDER OR Dem);
	##

REMOVE Acc IF (*-1 ("oktan") OR ("ovttas") OR ("ovttasráđiid") BARRIER NPNH)
	(*1 (N Com) BARRIER NP-BORDER);
	## Eamiálbmotguovddáža ásaheami sáhttá geahččat ovttas universitehta
	## Áivan dutkamiid guovddáža ohcamiin.


# Genitive in temporal adverbials
# -------------------------------
 # A 'measure' time adverbial is Acc, others are Gen. 

SELECT (@GN>) IF (0 MANNU)(1 (Num Ord));    
	##  Dál lea miessemánu 1. beaivvi.

SELECT Gen IF (-1 Ord)(0 ("beaivi") OR ("vahkku"));
	## Evaluerenraporta addojuvvui GOD:ii geassemánu 20. beaivvi 2000.

SELECT Gen IF (-3 MANNU)(-2 Ord)(-1 ("beaivi") OR ("b" ABBR))(0 Num);
	## Sámedikki bargiidčoahkkimis Guovdageainnus juovlamánu 7. beaivvi 2001
	## mearridii...

SELECT Gen IF (-1 ("boahtte") OR ("boahtit" PrsPrc) OR ("ovddit" Attr))(0 TIME);
	## Boađe boahtte vahku.

SELECT Gen IF (-1 COMMA)(0 TIME)(1 EOS);
	## Mun vuolggán ihttin, bearjadaga.

SELECT Gen IF (-1 BOS OR CS)(0 BEAIVI);
	## Bearjadaga mii vuolgit oaggut.

SELECT Gen IF (-1 NP-BORDER)(0 BEAIVI);
	## Mun boađán bearjadaga.
 # The idea here is that the BEAIVI should not be modified by e.g. 'olles'.

SELECT Gen IF (0 Card)(1 (N Gen));
	## Dat lea duháhiid jagiid boaris.

SELECT Gen IF (0 Num)(1 TIME)(1 ADVLCASE);
	## Journálat sáhttet lágiduvvot fylkkamánne rádjui 10 jahkái.

SELECT Gen If (-1 (@ADVL))(NOT -1 Pr OR Attr)(0 TIME);
	## Diddi vuodjala detnui mihcamárain maŋás ja guvžá fas čavččabeallái geasi.
	## Čuonžžá goargŋu detnui čakčageasi.

SELECT Gen IF (-1 ("juohke") OR ("seammá" Attr))(0 TIME); 
	## ...álbmotjoavkku mat ovdal dahje seammá áiggi...
 # Other attributes similar?

SELECT Gen IF (0 ("jahki"))(1 Num); 
	## Jagi 2002 bušeahttačoahkkima,...   

SELECT Gen IF (NOT 0 VERB)(1 ("áigi"))(NOT 1 (Sg Loc) LINK 1 ("áigi" Sg Ill));
	## Čoahkkimis lea lávdegoddi árvalan konfereanssa áiggi, báikki ja fáttáid.
	## ...oppa dan áiggi go dáláš konvenšuvdna lea doaibman.

SELECT Gen IF (-1 Gen)(0 ("áigi"));
	## ...gonagasa Herodesa áiggi,..

SELECT Gen IF (0 ("hávvi"))(*1C Acc BARRIER CS OR CP);
	## Váilevaš vejolašvuohta doaimmahit kultuvrra ávnnaslaš beliid sáhttá
	## muhtun háviid mearkkašit álbmotriekterihkkuma.

SELECT Gen IF (0 ("hávvi"))(NOT *-1 V-TRANS-ACT BARRIER CS)
	(NOT *1 V-TRANS-ACT BARRIER CS OR CP);
	## Fágalaš sudjadeapmi daid háviid go doavttir lea dahkan biehttaleami.

SELECT Gen IF (*-1 V-PASS BARRIER VFIN OR CS)(0 ("hávvi"))
	(NOT *1 V-NOT-AUX BARRIER CS OR CP);
	## Muhtun háviid buhtaduvvo meassamiid ruđalaš vahát.

 # The time expression "hávvi" is not Acc, but the problem is the homonymous noun. 

SELECT Gen IF (*-1 BOS OR CS BARRIER NPNHA)
	(0 ("áigi") OR ("bodda") OR ("geardi") OR ("hávvi"));
	## Eará háviid ii leat hoahpu journálačállimiin.

SELECT Gen IF (1 ("riegádan#beaivi"));
	## Golbma čáppa, doaimmalaš nieidda livžo áhčiset riegádanbeaivve.

SELECT Gen IF (-1 (Num Gen))(0 ("geardi"));
	## Galggat golmma geardde beaivvis borrat ovtta tableahta.

	
# Genitive in place adverbials
# ----------------------------
SELECT Gen IF (*-1 MOVEMENT-VERB BARRIER NPNHA)(0 ROUTE);
	## ...ja sii manne eará luotta ruoktot eatnamasaset.


# Genitive specifiers
# -------------------

REMOVE (N Acc) IF (*-1C NP-BORDER BARRIER WORD)(NOT -1 V)(NOT 1 VFIN)
	(*1 N BARRIER NPNH OR CC LINK *1 VFIN BARRIER CC OR CS OR CP)
	(NOT *1 (@SUBJ) BARRIER NOT-NP OR CC LINK *1 V-TRANS-ACT BARRIER 
	CC OR CS OR CP LINK NOT *1 (OBJ) BARRIER CLB OR CC OR CS OR CP)
	(NOT 0 ("hearrá" Acc) LINK 1 ("ipmil" Acc))
	(NOT 1 Ill LINK *1 VFIN BARRIER NOT-ADV-PCLE);
	## Ášši meannudeapmi loahpahuvvui guovvamánu 20. b. dii. 09.15. 

REMOVE (N Nom) IF (*-1 NP-BORDER BARRIER WORD)(NOT -1 COPULAS)(0 Gen)
	(NOT 0 TITLE)(NOT 1 COPULAS OR Inf)(*1 (N Nom) BARRIER NOT-ADJ OR 
	CLB OR ("/") LINK *1 VFIN BARRIER CC OR CS OR CP OR CONTRA OR PUNCT)
	(NOT 1 VFIN BARRIER NOT-ADV-PCLE);
	## Ášši meannudeapmi loahpahuvvui guovvamánu 20. b. dii. 09.15. 
	## Maiddái álgoálbmotfestivála Riddu Riđđu doarjja unnui.
 
REMOVE (Pron Pers Acc) IF (-1 NP-BORDER)(*1 N BARRIER NOT-Attr LINK *1 VFIN
	BARRIER CC OR CS OR CP);
	## ...lea go sin doaibma vuoigaduvvon oažžut doarjaga.

SELECT Gen IF (*-1 BOS OR CS BARRIER NPNHA)(NOT 0 Adv OR ("mii" Interr) OR 
	Dem OR Attr OR PrsPrc OR PrfPrc OR VFIN)(NOT 0 TITLE LINK 1 Prop)
	(1C (N Nom) OR (N Acc));
	## Sámi parlamentáralaš ráđi doaibmaáigi čuovvu Sámedikki
	## válgaáigodaga.

SELECT Gen IF (*-2 BOS OR CS BARRIER NOT-ADV-PCLE)(*-1 Num BARRIER NPNH)
	(*1 Loc BARRIER NPNH OR Dem LINK NOT 0 Interr);
	## Okta Sámedikki stuora hástalusain lea ain bargat buoridit... 
	## Okta ášši mas lea stuora mearkkašupmi, lea ahte... #NOT 0 Interr!

SELECT Gen IF (*-1 VFIN BARRIER NOT-ADV-PCLE OR CLB)
	(NOT 0 Nom OR Num OR Dem OR VGen OR PrfPrc OR PrsPrc OR Indef OR Interr)
	(NOT 1 CLB)(*1C (N Nom) OR (N Acc) BARRIER NPNH OR PrfPrc OR CC OR PUNCT LINK 
	NOT 0 Prop OR TIME OR ROUTE);
	## Lávdegottiid doaibmaáigi čuovvu Sámedikki válgaáigodaga.

SELECT Gen IF (NOT 0 Dem OR Num OR Attr OR Indef OR VERB OR ("ahkit" Nom))
	(NOT 0 (N Acc) LINK 1 (Prop Acc))(NOT 0 ("hearrá") LINK 1 ("ipmil"))
	(*1C Acc BARRIER NPNH OR PrfPrc OR CLB OR CC OR PUNCT LINK 1 EOS);
	## Divššohas galgá ieš beassat mearridit dearvvašvuođaveahki
	## miehtama.
	## Bistevažžan dahkat sámi kulturdieđu lohkanbajioahpu.

SELECT Gen IF (NOT 0 A OR Dem OR Num)(*1 (N Acc) BARRIER NPNH OR CC LINK 1 PrfPrc);
	## Tromssa fylkagieldda joatkkaskuvladási skuvlastruktuvrra árvvoštallan.
	## Dearvvašvuođaregisttarlága mielde leat divššohasjournálat
	## ásahuvvon dikšuma várás.
 # Many of the PrfPrcs referred to will turn out to be Actios. That is OK.

SELECT Gen IF (0 Interr)(1 (N Ess))(NOT 1 V-SG1 LINK NOT 1 VFIN);
	## ...man ulbmilin lea lohkat ja juogustit bohccuid.

SELECT Gen IF (*-1 (@OBJ) BARRIER CS OR CP)(0 (Pers Sg2))(1 (Refl Gen PxSg2));
	## Hearrá, mun lean gullan olusiid muitaleamen dán olbmá ja buot baháid 
	## birra maid son lea dahkan du iežat olbmuide Jerusalemis.

SELECT Gen IF (*-1 COPULAS BARRIER NOT-ADV-PCLE LINK *-1C Nom BARRIER CS OR CP OR
	CC OR CLB)(NOT 0 Loc)(*1C ADVLCASE BARRIER NPNH LINK NOT 0 Ess)
	(NOT 1 Inf OR PrfPrc LINK 0 TV)(NOT 1 CC);
	## Arkelaus lei šaddan Judea gonagassan.
	## Don it leat guhkkin eret Ipmila riikkas.

SELECT Gen IF (*-1 ACC-ILL-VERB BARRIER NOT-ADV-PCLE)
	(*1 Ill BARRIER NPNH LINK *1 Acc BARRIER NPNH OR CC LINK NOT 0 Interr)
	(NOT *1 Ill BARRIER NPNH LINK *1 Acc BARRIER NPNH LINK 1 Po)
	(NOT 0 (Dem Pl) LINK *1 (Sg Ill) BARRIER NPNH);
	## Láhkaásahus ii bija doaimma guđege oassái ovddasvástádusa ásahit
	## journálavuogádaga.

SELECT Gen IF (*-1 ("dahkat") BARRIER VERB OR CS OR CP)(*1 Com BARRIER NPNH);
	## Ii go mus leat lohpi dahkat iežan opmodagain mo ieš dáhtun?

SELECT Gen IF (0 ("okta"))(1 (N Gen) LINK *1 N BARRIER NOT-ADJ);
	## Dan eatnamis oažžu ovtta gusa fuođđara.

SELECT Gen IF (0 ("sápmi"))(1 N);
	##  Dalle čujuhuvvui ahte lea dárbu nannet doaimmaid mat ovddidit 
	## sámi dutkama, fágaidgaskasaš ovttasbarggu ja sámi áššiid oahpahusa.

REMOVE (Pers Acc) IF (-1 Acc)(NOT *1 V-TRANS-ACT BARRIER CS);
	## Mun oidnen ovtta du vieljain.

REMOVE Acc IF (*-1 CLB BARRIER WORD)(NOT 0 Interr)(NOT 1 CC OR CLB)
	(*1 Nom BARRIER NPNH OR CC LINK NOT 0 Interr);
	## Sámi dáidda- ja kulturpolitihkka.
	## Sámiid válgalihttu evttoha ahte...

REMOVE Acc IF (*-1 ILL-VERB BARRIER NPNHA)(*1 Ill BARRIER NPNH);
	## Ehpet eisege beasa almmi riikii.

REMOVE Acc IF (*-1 Ill BARRIER VERB OR CC OR CLB OR CS OR CP)
	(*1 ILL-VERB BARRIER V-NOT-AUX OR CC OR CLB OR CS OR CP);
	## Dan válddi, masa Ruonáeatnama ruovttustivra lea ollen,...

REMOVE Acc IF (*-1 Nom BARRIER NOT-ADV-PCLE LINK *-1 ILL-VERB 
	BARRIER NOT-ADV-PCLE)(*1 Ill BARRIER NPNH);
	## ...mat gullet sámilága giellanjuolggadusaid hálddašanguvlui.

REMOVE Acc IF (*-1 LOC-VERB BARRIER NPNHA OR Loc OR CLB)
	(*1 Loc BARRIER NOT-NP LINK NOT 0 (TV Actio) OR Prop);
	## Divššohasas lea vuoigatvuohta biehttalit journála olggosaddimis.

REMOVE Acc IF (*-1 Nom BARRIER NOT-ADV-PCLE LINK *-1 LOC-VERB 
	BARRIER NOT-ADV-PCLE)(*1 Loc BARRIER NPNH);
	## 

REMOVE Acc IF (*-1 LOC-VERB BARRIER NOT-ADV-PCLE OR go LINK NOT 0 ACC-LOC-VERB
	LINK *-1 Loc BARRIER V-NOT-AUX OR (@SUBJ) OR CC OR CLB OR CS OR CP);
	## Das čuovvu mu oainnu mielde dat, ahte...

REMOVE Acc IF (*-1 ILL-VERB BARRIER NOT-ADV-PCLE OR go LINK NOT 0 ACC-ILL-VERB
	LINK *-1 Ill BARRIER V-NOT-AUX OR (@SUBJ) OR CC OR CLB OR CS OR CP);
	## ...masa gullet Kárášjoga, Guovdageainnu, ...ja Gáivuona gielddat.
	
SELECT (Attr) IF (0 ("nr"));

CONSTRAINTS

#SELECT Gen IF (*-1 Ill BARRIER NPNH LINK NOT 0 Px)
	#(*1 Po BARRIER NPNH LINK NOT 0 Px);
	## Son bijai su alimussii tempela geađgemuvrra ala.
	#Might have to be checked.

SELECT Gen IF (*-1 Loc BARRIER NPNH LINK NOT 0 Actio)(NOT 0 VFIN)
	(NOT 1 Pers)(*1 Po BARRIER NPNH);
	## ...mii lea leamaš čihkosis máilmmi sivdnideami rájes.

SELECT Gen IF (*-1 CC OR COMMA BARRIER NPNH LINK -1 Loc)(NOT 0 VERB)
	(*1 Loc BARRIER NOT-NP OR REALCLB LINK NOT -1 CC);
	## Su lusa bohte olbmot Jerusalemis ja oppa Judeas ja Jordana guovlluin.

SELECT Gen IF (0 ("láhka") OR ("mánáidsuodjalus#láhka") OR ("ávžžuhus"))
	(1 ("§") OR ("mearrádus")); 
	## ...gč. dearvvašvuođabargiid lága§ 45.

SELECT Gen IF (-1 CC)(0 (N Prop))(*1 (N Nom) BARRIER NPNH OR COMMA OR CC);
	## ...ja Romssa universitehta lea doaibmaplána bokte...

SELECT Gen IF (1 ("/"))(2 Attr)(NOT 2 Prop);
	## Lea dárbbašlaš erenoamážit vuoruhit sámi/eamiálbmotguskevaš dutkamiid.

SELECT Gen IF (0 ("sápmi" Sg))(1 CC)(2 (A Attr));
	## ...váfistan dihte sámi ja eamiálbmotguskevaš dutkama.

SELECT Gen IF (-1 Indef)(0C Pers)(*1 (N Pl Loc) BARRIER NPNH);
	## Muhtun kretalaš, muhtun sin iežaset profehtain, lea dadjan...
 
SELECT Gen IF (*-1 ("dego") BARRIER NOT-NP)(*1 (N Nom) BARRIER NOT-NP OR CLB);
	## Máret livžu dego logi máná eadni.

REMOVE Acc IF (-2 ADVLCASE)(-1 ("go" Pcle))(*1 ADVLCASE BARRIER NPNH);
	## Dat gullá lunddolaččat dakkár áššiide go Sámedikkiid válganjuolggadusaide.

REMOVE Acc IF (-1 ("sárdnut") BARRIER NOT-ADV-PCLE)(0 Gen)
	(*1 ("birra") BARRIER NPNH);
	## Mii gullat sin sárdnumin Ipmila stuora daguid birra min iežamet gillii.

REMOVE Acc IF (*-1 ("birra") BARRIER NPNHA LINK *-1 ("sárdnut") BARRIER VERB
	OR CC OR CLB OR CS OR CP);
	## Mii gullat sin sárdnumin Ipmila stuora daguid birra min iežamet gillii.


# Genitive in coordination
# ------------------------

SELECT Gen IF (*-1 BOS OR CS BARRIER NOT-ADV-PCLE)(NOT 0 (Pers Nom))
	(NOT 0 (Prop Nom) LINK 1 CC LINK 1 (Pers Sg3 Gen))(1 CC)
	(2 NP-HEAD-GEN LINK *1 (N Nom) BARRIER NPNH LINK NOT 1 Loc);
	## Kanáda ja Austrália riekteapparáhtat leat fuomášan...  
	## Jekonja ja su vieljat leat dál. # (NOT 0 (Prop Nom)...

SELECT Gen IF (NOT 0 Nom LINK *-1 VFIN BARRIER NPNHA)(1 CC)
	(*2 Po OR PREGEN BARRIER NPNH OR Nom LINK NOT *1 Interr OR VFIN OR Inf 
	BARRIER WORD);
	## Divššohas galgá maiddái oažžut dieđuid vejolaš riskkaid ja
	## lassiváikkuhusaid birra.

SELECT Gen IF (1 COMMA)(*2 NP-HEAD-GEN BARRIER NPNH LINK 1 CC 
	LINK *1 Po BARRIER NPNH);
	## Dat čuožžila vuođđolága ásaheaddji, láhkaásaheaddji, ráđđe-
	## husa ja majestehta vuostá.

SELECT Gen IF (1 COMMA)(*2C NP-HEAD-GEN BARRIER NPNH LINK 1 COMMA
	LINK *1 NP-HEAD-GEN BARRIER NPNH LINK 1 COMMA OR CC OR NP-BORDER);
	## ...nannet máhtu sámi historjjá, kultuvrra, árbevieruid ja
	## bajásgeassinnorpmaid ja -minstariid birra.

SELECT Gen IF (1 COMMA)(2 CC)(*3 (N Gen) BARRIER NPNH LINK 1 Po);
	## ...dán vuolde mearrádusat rájadeami, hilguma ja buktima birra.

SELECT Gen IF (-2C NP-HEAD-GEN LINK *-1 NP-BORDER BARRIER NPNH)(-1 CC);
	## ...ahte kultuvrra ja eallinvuogi eaktivuohta ii galgga leat...  

SELECT Gen IF (-2C NP-HEAD-GEN)(-1 CC)(*1 NP-HEAD-GEN BARRIER NPNH);
	## ...šiehtadus Hálkavári ja Iŋggásguolbana báhčinguovlluid dáfus. 

SELECT Gen IF (-2C Gen)(-1 COMMA)(1 COMMA)(2 Gen);
	## ...masa gullet Kárášjoga, Guovdageainnu, Unjárgga, Porsáŋggu, Deanu
	## ja Gáivuona gielddat.

SELECT Gen IF (NOT *-1 V-NOT-AUX BARRIER NPNHA OR CLB)(NOT -1 Gen)
	(NOT 0 Nom OR PrsPrc)(1 CC)(2 Gen)(*3 Loc BARRIER NPNH OR CLB);
	## Tyrusa ja Sidona guovlluin bohte olbmot su lusa.

SELECT Gen IF (-2C Gen)(NOT -2 Cmpnd)(-1 CC)(*1 Loc BARRIER NPNH OR CLB);
	## ...muhto farisealaččaid ja saddukealaččaid oahpus.

REMOVE Acc IF (NOT -2 Acc LINK *-1 Inf BARRIER NPNHA)(-1 CC)(NOT 0 Interr)
	(*1C (N Nom) BARRIER NPNH);
	## Sámedikki čoahkkinjođihangotti ja lávdegottiid čoahkkimat dollojit... 

SELECT Gen IF (*-1 COMMA BARRIER NPNH LINK -1C NP-HEAD-GEN)(1 CC)
	(*2 NP-HEAD-GEN BARRIER NPNH);
	## Gielddaid, fylkkagielddaid ja guovlluid dearvvašfitnodagaid
	## bargoaddit galget...

SELECT Gen IF (-2 Ill)(-1 CC)(NOT 0 Cmpnd OR (@TITLE))(*1 Ill BARRIER NPNH);
SELECT Gen IF (-2 Com)(-1 CC)(NOT 0 Cmpnd)(*1 Com BARRIER NPNH);
	## Eahpitkeahttá lea ávkin boazodollui ja sámi beroštumiide dat.
	## Gáhttenplánain ja gáhttenplána evttohusain ii addojuvvo doarvái... 

SELECT Gen IF (-2 Loc)(-1 CC)(NOT 0 Cmpnd)(*1 Loc BARRIER NPNH);
SELECT Gen IF (-2 Ess)(-1 CC)(NOT 1 CC)(*1 Ess BARRIER NPNH LINK NOT 0 Actio);
	## Dál lea dárbu oažžut ollislaš geahčastaga ovdánanminstaris ja
	## otnáš areáladilis.
	## Sámit leat aktiivvalaš ovttasbargoguoibmin ja eavttuid biddjin. 

SELECT Gen IF (NOT *-1 V-TRANS-ACT BARRIER REALCLB OR PUNCT OR CS)
	(NOT 0 Nom LINK *1 Gen BARRIER NPNH)(NOT 0 VFIN LINK 1 Adv)
	(NOT 0 PrfPrc LINK 1 N)(*1 Po BARRIER NPNH OR REALCLB OR PUNCT OR (Pron Pers));
	## ...jus eai leačča dárbbašlaččat dearvvašvuođa vahátváikkuhusaid
	## eastadeami dihtii.

SELECT Gen IF (-1 (Pl3 Gen) LINK *-1 (@OBJ) BARRIER NOT-ADV-PCLE)(1 CC)(2 Gen)
	(NOT 2 Cmpnd)(*3 N BARRIER NPNH LINK NOT 0 Gen);
	## Boazosápmelaččain lea vuoigatvuohta geavahit eatnamiid ja gáttiid 
	## sin elliid ja sin iežaset ealáhussii.

SELECT Gen IF (1 ("/"))(2C Gen);
	## ...mii ii čuoza nu garrasit viidodaga/čikŋodaga ja guhkkodaga dáfus.


# Nominative and accusative
# =========================

REMOVE Gen IF (-1 BOS OR CS OR CLB)(NOT 0 TIME)(1 (Pron Pers))(*1 VFIN); 
	## Daid mun sáltejin. 
	## ...man mii dan áigge dovdat gonagasriika Norgan.

REMOVE Gen IF (-1 BOS OR CS)(1 Adv)(NOT 1 N OR A); 
        ## Dieđuid maiddái ii dárbbaš addit go...

REMOVE Gen IF (NOT 0 TIME)(1 CLB)(2 CC)(3 NPNH);#(NOT 3 Gen LINK *1 Po BARRIER NPNH);
	## Eará veahki sáhttá addojuvvot jus divššohas čájeha beroštumi, ja lea...

REMOVE Gen IF (NOT *-1 Pr OR NUMERALS BARRIER NP-BORDER)(NOT -1 ("mii"))
	(NOT *-1 CC BARRIER NOT-NP LINK *-1 GASKAL BARRIER CS)
	(NOT 0 TIME OR ROUTE)(1C NP-BORDER); 
        ## Eai lean váldán go niestebohcco.

REMOVE Gen IF (NOT *-1 Pr OR NUMERALS BARRIER NPNH)(0 N)(1 Dem);
	## Jahkásaš bušeahttarámma dán prošeavtta doaimmaide gártá oktiibuot
	## 70.000 ruvnno. 

REMOVE Gen IF (NOT *-1 Pr OR NUMERALS BARRIER NPNH)(1 BEAIVI);
	## Sii bivdet luosa mánnodaga.

REMOVE Gen IF (*-1 OPRED-VERB BARRIER NPNH LINK NOT *-1 NP-HEAD-ACC
	BARRIER V-NOT-AUX OR CC OR CLB OR CS OR CP)(1 Ess);
	## Don galggat gohčodit su Jesusin.

REMOVE Gen IF (*-1 V-NOT-AUX BARRIER CS OR CP)(1 ("§"));
	## Vuoigatvuohta oažžut dearvvašvuođaveahki § 2-2.
 # This rule would work fine if we could make an earlier rule select Gen 
 # for all occurrences of compounds in -lága in front of§. 

REMOVE Gen IF (NOT *-1 Pr BARRIER NP-BORDER)(0 (Prop Nom))(1 CLB)(NOT 2 (Prop Gen));
	## Gažaldat áirasis Egil Olli, Bargiidbellodaga sámediggejoavku.

REMOVE Gen IF (*-1 VFIN BARRIER NOT-ADV-PCLE)(0 N)(1 CC)(2 (Pron Sg3 Gen));
	## Váldde máná ja su eatni fárrui.

REMOVE (Interr Gen) IF (1 Dem);
	## Viiddodatrámmaid geažil, maid dát ovdanbuktit ferte doallat,...

REMOVE Gen IF (NOT 0 Dem)(*1 ("hávvi" N Sg Gen) BARRIER NPNH);
	## ..geasa dearvvaávuođabálvalus addá dahje fallá veahki juohke
	## áidna háve...

REMOVE Gen IF (NOT *-1 Pr OR NUMERALS BARRIER NPNH)(NOT 0 TIME)(1 CLB)(2 CC);
	## Dat váikkuha ollu dasa, mo olbmot vásihit bálválusaid, ja oažžu sin
	## dovdat stuorát oadjebasvuođa.

REMOVE Gen IF (NOT *-1 Pr OR NUMERALS BARRIER NPNH)(NOT 0 TIME)(1 Inf)(1C VERB);
	## Vuoigatvuohta journála geahččat.

REMOVE Gen IF (0 ("stáhtus"))(1 Ess)(*2 V-SG3 BARRIER NOT-ADV-PCLE);
	## Inuihtaid stáhtus eamiálbmogin ii leat vuođđolágas nannejuvvon.

REMOVE Gen IF (1 ("áigi" Sg Loc))(2 ("áigi" Sg Ill));
	## Lea stuorradiggi mii ferte mearrádusa áiggis áigái dulkot ja atnit.

REMOVE Nom IF (*-1 Acc BARRIER CS OR CP OR CONTRA OR VFIN LINK *-1 ACC-INF-VERB
	BARRIER VFIN OR CLB OR CS OR CP OR PUNCT OR REALCLB)
	(NOT *-1 COPULAS BARRIER NOT-ADV-PCLE)(NOT *-1 (Interr Acc) BARRIER Acc);
	## Hearrá, suova mu vuos mannat ruoktot hávdádit áhččán.
 
REMOVE Acc IF (NOT *-1 V-TRANS-ACT BARRIER CS)(NOT 0 TIME)
	(NOT 0 NUMERALS LINK 1 TIME OR MEASURE)
	(NOT *1 V-TRANS-ACT BARRIER CS);
	## Sámedikkis lea leamaš čoahkkin 14.03.01.  


# Predicates of accusative subjects
# ---------------------------------
SELECT (Sg Acc) IF (*-1 COPULAS BARRIER NOT-ADV-PCLE LINK 0 Inf 
	LINK *-1 (Sg1 Acc) OR (Sg2 Acc) OR (Sg3 Acc) BARRIER NOT-ADV-PCLE)
	(NOT 0 PrfPrc)(NOT 0 Attr LINK *1 N BARRIER NPNH);
	## ... de diktet mu leat jalla vai mun ge beasašin veahá rábmot! 
 # and more rules!

 # Rules for object predicatives are found in the accusative section, but 
 # this rule needs to come earlier since copulas are involved.



# =========== #
  CONSTRAINTS #
# =========== #
# Nominative
# ==========

# Miscellaneous  rules
# --------------------

REMOVE (Nom) IF (*-1 Nom BARRIER NPNHA OR CC OR CLB LINK NOT 0 Dem)(0 Attr)(1 N);
	## Sápmelaččat leat oadjebasat olggut birasáitagiid ektui.

REMOVE (N Nom) IF (*-1 (Pers Nom) BARRIER NOT-ADJ)(NOT 0 Num)(NOT 1 EOS);
	## Muhto son várra áinnas beroštii dakkár dilálašvuođaide. 
	## Don ipmilmeahttun jalla! # NOT 1 EOS!

REMOVE (Sg Nom) IF (NOT *-1 CC BARRIER NPNHA LINK *-1 Nom)(NOT 0 (N Num))
	(*1C V-DU-PL BARRIER NOT-ADV-PCLE OR CLB); 
	## Historjjálaš dilálašvuođat uhccán mearkkašit dálá dilálašvuhtii. 

REMOVE Nom IF (0 ("hearrá"))(1 ("ipmil"))(NOT 1 Nom);
	## Hearrá Ipmilat galggat gudnejahttit.

SELECT Loc IF (1 ("eret"));
	## Dáiddát don ge leat Galileas eret.

SELECT Nom IF (-1 Gen)(NOT -1 (Pron Dem Gen))(0 N-SG-NOM)(NOT 0 PrfPrc OR Ill)
	(1 V-SG3);
	## Du ášši lea dehalaš.
	## Dan ášši ferte dutkat.
 # HMM, brave.. - but it seems to work!
 # Note that the tag is selected, the set is in the condition.

SELECT Nom IF (NOT *-1 V-TRANS-ACT BARRIER CC OR CS OR CP)(-1C Gen)
	(0 ("dáidda"));
	## Deike lea máŋggaid jagiid čohkkejuvvon sámi dáidda.

SELECT Nom IF (-2 Num)(-1 ("ruvdno"));
	## Láđđi máksá 70 ruvnno mehtar.

SELECT (N Num Nom) IF (-1 (Pers Nom))(-1 Pl1 OR Pl2 OR Pl3);
	## De mii golmmas leansmánniin manaimet geahččat.

SELECT (N Nom) IF (*-1 ("okta" Nom) BARRIER NPNH LINK NOT 0 Foc);
	## Málli vuššojuvvui okta lihtter.

SELECT Nom IF (0 ("veara"))(1 VERB);
	## Eanetlohku ii oro namuhan veara berošteamen gielddaid dárbbuid.

# Nominative if two names in a row
SELECT Nom IF (-1 PROP)(0 PROP)(1 V-SG3); 
	## Birasgáhttenminister Siri Bjerke dajai čielgasit ahte...

 
# Vocatives, subjects of sentence fragments
# - - - - - - - - - - - - - - - - - - - - - 
SELECT Nom IF (*-1 BOS OR (":") BARRIER NOT-ADJ)(0 Prop)(1 COMMA); 
	## Biera, du sabehiid!
	## Jovsset, Dávveda Bárdni!

SELECT (Sg Nom) IF (*-1 MUN OR DON OR (Pron Sg3) BARRIER NOT-AA 
	LINK *-1 BOS OR (":") BARRIER NOT-ADJ-CC)(1 EOS);
	## Son celkkii: Don jalla!

SELECT (Pl Nom) IF (*-1 MII-PERS OR DII OR SII BARRIER NPNHA
	LINK *-1 BOS OR CLB BARRIER NOT-ADJ-CC)(1 EOS);
	## Dii guoktilaččat!

SELECT (A Pl Nom) IF (-2 MII-PERS OR DII OR SII LINK *-1 BOS OR CLB 
	BARRIER NOT-ADJ-CC)(-1 Adv OR Indef)(1 EOS);
	## Dii uhccán oskkolaččat!

SELECT Nom IF (*-1 BOS OR PUNCT BARRIER NPNH)(1 COMMA)(2 Imprt);
	## Sáhtán, gáidda eret!

SELECT Nom IF (-1 BOS OR PUNCT OR REALCLB)(1 COMMA);
	## Oahpaheaddji, mun áiggun čuovvut du.

SELECT Nom IF (*-1 (Pron Sg2) BARRIER NPNH LINK -1 COMMA)(0 N)(1 EOS);
	## Mun dubmen du iežat sániid mielde, don heajos bálvaleaddji!


# Nominative in titles and sentence fragments
# - - - - - - - - - - - - - - - - - - - - - -

# A single word is nominative
SELECT Nom IF (-1 BOS OR (":"))(NOT 0 VFIN)(0 N OR A OR Pron)(1 EOS); 
	## Katja.
	## Bálvaleaddjit manne dalle isideaset lusa ja jerre: Hearrá! 

# A single word with a numeral in front of it is nominative
SELECT Nom IF (-2 BOS)(-1 Num)(1 EOS);
	##

# An NP head with a genitive modifier is nominative
SELECT Nom IF (-2 BOS)(-1 Gen)(0C N)(1 EOS);
	##

# A title is nominative if it has a Nom reading at all.
SELECT Nom IF (0 (@TITLE));
	## Stuorra giitu maiddái professor Birger Stuevold Lassenii.
 
# An NP head with an Attr modifier is nominative.
SELECT Nom IF (-2 BOS)(-1 Attr)(1 EOS);
	##

# A clause-final numeral preceded by § is nominative.
SELECT Nom IF (-1 ("§") OR ("kapihtal") OR ("kapihttal"))(0 Num)(1 EOS); 
	## Vuoigatvuohta oažžut dárbbašlaš dearvvašvuođaveahki § 2-2.


# Nominative after "go", "dego" and "nugo"
# - - - - - - - - - - - - - - - - - - - - -
SELECT Nom IF (*-1 ("go" Pcle) BARRIER NPNH LINK NOT -1 VFIN)
	(*-1 COPULAS BARRIER V-NOT-AUX OR CS)(NOT 1 N OR A);
	## Jesus lea gievrrat go Sáhtán ja bahá vuoiŋŋat.

SELECT Nom IF (*-1 ("go" Pcle) BARRIER NOT-NP LINK NOT -1 VFIN)
	(NOT 0 Acc OR VFIN)(1 CLB OR CS OR EOS);
	## Ii leat almmustuvvan oktage stuorit go gásttašeaddji Johanas.

SELECT Nom IF (*-1 ("dego") OR ("nugo") BARRIER NPNH)
	(NOT 0 Attr LINK *1 N BARRIER NPNH)(*1 NP-BORDER OR CLB BARRIER WORD);
	## Su ámadadju báittii dego beaivváš.

SELECT Nom IF (-2 ("dego") OR ("nugo"))(-1 Gen OR PrsPrc)(1 NP-BORDER);
	## ...johtá dego gillju ledjon ozadettiin muhtuma...

REMOVE Imprt IF (*-1 ("dego") BARRIER VFIN OR CS OR CP);
	## ...dego vuonccis čohkke čivggaidis soajáidis vuollái.

SELECT Nom IF (-1 ("go" Pcle))(0 Num);
	## ...go heaittihanáigemearri lea oanehat go vihtta jagi.


# Nominative subjects
# -------------------

# Preverbal subjects
# - - - - - - - - - -
SELECT (N Nom) IF (*-1 (Dem Nom) BARRIER NOT-ADJ)(NOT 0 ("leapma" Dimin));
	## Noađđespagáin leat dat geažit guokte golbma tumá guhku.
	## ...ja ahte dat dáhttu čielgasit berre doahttaluvvot. 

SELECT (Num Nom) IF (-1 (Dem Sg Nom));
	## Dát guokte ášši Siviilaáittardeaddji geavadis leat váldon...

SELECT Nom IF (*-1 CS OR BOS OR CC OR CP OR PUNCT OR (":") BARRIER NPNHA)(0 Sg)
	(NOT 0 Adv OR Loc)(NOT 0 Ess LINK 0 (@ADVL))
	(NOT 0 Attr LINK *1 N BARRIER NPNH)(*1C V-SG3 BARRIER NOT-ADV-PCLE);
	## ...ahte doaimma jođihangoddi vejolaččat mearrida...

SELECT (N Sg Nom) IF (1 V-SG3)(NOT 1 N)(NOT *2C Nom BARRIER CS);
	## Doaibmaplána árvvoštallan čajeha maiddái ahte...

SELECT (N Sg Nom) IF (NOT *-1 (N Sg Nom) BARRIER CS OR CP OR CC OR CLB)
	(NOT 0 Loc)(1 (Neg Sg3));
	## Sámi dutkan ii leat dan dihte soaittáhagas válljejuvvon dutkansuorgi.
 	
SELECT Nom IF (*-1 CS OR BOS OR CC OR CP OR PUNCT OR (":") BARRIER NPNHA)(0 Pl)
	(NOT 0 Adv OR Ill)(*1 V-PL3 BARRIER NOT-ADV-PCLE); 
	## ...go beaktilis sivat bealuštit dan.

SELECT Nom IF (0 Actio)(*1 ILL-VERB BARRIER V-NOT-AUX OR Nom OR CC OR CS OR CP);
	## Ruovttus fárren sáhttá čuohcit olbmui sakka.
 # The idea here is that 0 is not accusative, since there is an ILL-VERB.
 # So we could  alternatively REMOVE Acc. But the present rule seems to work.

SELECT Nom IF (*-1 CS OR BOS OR CC OR CP BARRIER NP-HEAD OR VFIN)(0 NUMERALS)
	(*1 V-PL3 BARRIER CS)(NOT *1 Nom BARRIER CS);
	## Guokte jahkásaš doavttergrádakurssa leat lágiduvvon.
	## Go máŋggas galget čállit seamma journálii, de...  
 # In the last example, the reading N Sg Nom is chosen because of the reading 
 # Num Sg Loc.

SELECT Nom IF (*-1 CS OR BOS OR CC OR CP BARRIER NP-HEAD OR VFIN)(0 NUMERALS)
	(*1 COPULAS BARRIER VERB OR CS LINK 0 (Pl3) LINK NOT *1 PrfPrc OR Inf
	BARRIER VFIN OR CS OR CP);
	## Vihtta bivdovuogi leat Deanus luosa bivdidettiin. 

SELECT (Sg Nom) IF (*-1 CS OR BOS BARRIER NOT-ADV-PCLE)(NOT 0 Adv OR PrfPrc)
	(NOT 0 Qst)(*1 V-SG3 BARRIER NOT-NP OR Nom OR Ess OR CS OR CP LINK 
	NOT *1 NP-HEAD-SG-NOM BARRIER NPNHA)(NOT 0 Gen LINK *1 N BARRIER NPNH)
	(NOT 0 Ess);
	## Mii vuordit ahte stáhta sámi vuoigatvuođaid barggu oktavuođas čuovvu...

SELECT Nom IF (0 (N Num Sg Loc))(*1 V-DU3 BARRIER NOT-ADV-PCLE);
	## Máná-guovttos leigga sávrrit viehkat.
 # The formulation is designed to hit "guovttos", also in compounds.

SELECT Nom IF (0 ("galle"))(*1 V-PL3 BARRIER Nom OR VFIN);
	## ... ja albmada galle "mearrasámi" ledje vearrováldit.

SELECT Nom IF (0 (N Num))(*1 V-PL3 BARRIER NOT-ADV-PCLE);
	## Dakkár doaimmat, main máŋggas čállet dieđuid journálaide,...

SELECT Nom IF (0 NOTIME OR Pron)(NOT 0 ("leat") OR PrfPrc)
	(NOT 0 A LINK *1 N BARRIER NPNH)(NOT 0 Adv OR Attr OR VFIN)
	(NOT 1 CC LINK 1 VFIN)
	(*1C V-PASS BARRIER V-NOT-AUX OR NP-HEAD OR Po OR CS OR CLB LINK NOT 0 Inf);
	## Váfistan dihte ođđa bargovugiid lea komitea viiddiduvvon.
	## Bušeahtas lea sullii 650.000 ruvnno leamaš várrejuvvon dáidda.
	## Dat guhte osku ja gásttašuvvo, bestojuvvo.

SELECT Nom IF (0 Num)(1 TIME)(*2 ("vássit") BARRIER V-NOT-AUX OR CC OR CLB OR CS OR CP);
	## Go logi jagi lea vássán maŋimus journálamerkesteami rájes,...

REMOVE (Prs Sg3) IF (NOT *-1 (Sg Nom) BARRIER CS OR CP)(0 A OR Indef)
	(*1C (Prs Sg3) BARRIER CS OR CP OR CC OR CLB OR ("dego") LINK NOT 0 Qst);
	## Man uhccán osku dus lea!

SELECT Nom IF (0 Sg)(1 CC)(*2 (Sg Nom) BARRIER NPNH LINK *1 V-DU3 
	BARRIER NOT-ADV-PCLE);
	## Silas ja Timoteus báziiga Bereai.

SELECT NP-HEAD-NOM IF (*-1 CC BARRIER NPNH LINK -1 NP-HEAD-NOM)(1 V-PL3);
	## Árbevierru ja geavat sáhtášedje leat dulkonmomeanttat.
  
REMOVE Nom IF (*-1 BOS OR CS BARRIER NOT-ADV-PCLE)(NOT 0 (Pron Pers))(1C V-1-2);
	## No examples in corpus?

SELECT Adv IF (*-1 (N Pl Nom) BARRIER CS OR CP OR CC OR COPULAS OR ("orrut"))
	(0 (A Pl Nom));
	## Eamiálbmotlaš áššit jogo systemáhtalaččat dubmejuvvojit menddo siivut...

SELECT Adv IF (0 (A Pl Nom))(*1 V-NOT-AUX BARRIER CC OR CS OR CP)
	(*1 (N Pl Nom) BARRIER CS OR CP)
	(NOT 1 CC OR COMMA LINK *1 N OR A BARRIER NPNH);
	## Nappo leat gazaldagas historjjálaččat háhkkojuvvon vuoigatvuođat.

SELECT Adv IF (-1 BOS OR CS)(0 (A Pl Nom))(NOT 1 CLB)
	(NOT *1 V-DU3 OR V-PL3 BARRIER VFIN OR CS OR CP);
	## ...go cálálaččat lea registreren maid ja man ollu lea borran.
	## Reálapolitihkalaččat gehččojuvvon, de lea mihtilmas...

SELECT (A Pl Nom) IF (NOT *-1 (Pl Nom) OR (Pl3 Nom) BARRIER CC OR CS OR CP
	OR CLB)(0 NOMINAL-ADJ)(NOT 0 (N Pl Nom) OR (Pron Nom))
	(NOT 0 (A Comp Pl Nom))(NOT *1 (N Pl Nom) BARRIER CS OR CP OR CLB)
	(*1 V-PL3 BARRIER NOT-ADV-PCLE);
	## Go Herodes árvidii ahte viisát ledje beahttán su,...

SELECT (A Pl Nom) IF (*-1 BOS OR CS BARRIER NOT-ADV-PCLE)(*1 Neg OR COPULAS 
	BARRIER NOT-ADV-PCLE LINK 0 Pl3)(NOT *1 V-NOT-AUX BARRIER CS OR CP);
	## Nu maŋimuččat šaddet vuosttamužžan.

SELECT (A Pl Nom) IF (*-1 BOS OR CS BARRIER NOT-ADV-PCLE)(*1 Neg OR COPULAS 
	BARRIER NOT-ADV-PCLE LINK 0 Du3)(NOT *1 V-NOT-AUX BARRIER CS OR CP);
	## ...ahte oasálaččat eaba leat dássásaččat.

SELECT (A Pl Nom) IF (*-1 BOS OR CS BARRIER NOT-ADV-PCLE)(*1 Neg OR COPULAS 
	BARRIER NOT-ADV-PCLE LINK 0 Pl3 LINK *1 CC BARRIER V-NOT-AUX OR CS OR CP 
	LINK *1 VFIN BARRIER CS OR CP);
	## Geafit gal leat álo din luhtte ja sidjiide sáhttibehtet dahkat buriid.
 
REMOVE Nom IF (*1C V-NOT-AUX BARRIER CLB OR CC OR CS OR CP OR PUNCT 
	LINK *1C NP-HEAD-NOM BARRIER ("dego") OR CC OR CS OR CP OR CLB 
	LINK NOT 0 Interr)(NOT -1 COMMA LINK -1 (@SPRED));
	## Dakkár buođus gáržo guolli juo nu ahte dat deaddil joddu sisa.

 
# Postverbal subjects
# - - - - - - - - - -
SELECT (N Sg Nom) IF (*-1 V-SG3 BARRIER NPNHAIIP OR CP LINK NOT 0 (Imprt Sg2) 
	LINK *-1 BOS OR CS OR CLB BARRIER Nom)(NOT 0 VFIN OR PrfPrc OR Indef OR 
	Gen OR Ill OR Loc)(NOT 1 ("ahte"))(NOT 0 Loc LINK *1 NP-HEAD-NOM 
	BARRIER NPNH)(NOT 0 PrsPrc LINK 1 (N Sg Nom));
	## Guovddášis dán bárggus lea Sámi dutkamiid guovddáš.
        ## Riikkaid galgá Piera oaidnit.

SELECT (N Sg Nom) IF (*-1 V-SG3 BARRIER NPNHAIIP OR CP LINK *-1 BOS OR CS OR CLB
	BARRIER Nom)(-1 (A Attr));
	## Dasgo dalle han gaikkoduvvo ođđa bivttas, ja...
 
SELECT (Pron Sg Nom) IF (*-1 V-SG3 BARRIER Nom OR CC OR CS OR CP 
	LINK NOT 0 (Imprt Sg2) LINK *-1 BOS OR CS OR CLB BARRIER Nom)
	(NOT 0 Attr LINK *1 A OR N BARRIER NPNH);
	## Erenoamášit guoská dát dakkár áššiide, mat...
	## Ovdabargguin ii boađe ovdan eará, go ahte... 

SELECT (Sg3 Nom) IF (*-1 V-SG3 BARRIER CS OR PUNCT)(NOT *1 V-SG3 BARRIER CS OR CP);
	## Eahpitkeahttá lea ávkin sámi beroštumiide dat go guovllut...

SELECT (Sg Nom) IF (*-1 (Imprt Sg3) BARRIER NOT-NP)(NOT 0 Acc OR Ess);
	## Nu báitos din čuovggas ge olbmuide.

SELECT (Pl Nom) IF (*-1 (Imprt Pl3) BARRIER NOT-NP)(NOT 0 Acc);
	##

SELECT Nom IF (*-1 Pass BARRIER NPNHA OR CC)(NOT *-1 Nom BARRIER CS OR REALCLB)
	(NOT -1 (Pass PrfPrc) LINK NOT *-1 ("leat") BARRIER CS OR CP OR REALCLB 
	OR PUNCT)(NOT 0 Attr LINK *1 N BARRIER NPNH);
	## ...ahte lea ráhkaduvvon diehtojuohkinmateriála.

SELECT Nom IF (NOT *-3 Nom BARRIER CS OR REALCLB)(-2 Pass)(-1 (Pass PrfPrc));
	## Váldonjuolggadussan galgá geavahuvvot čohkkejuvvon journála...

SELECT (N Pl Nom) IF (*-1 V-PL3 BARRIER NPNHAIIP LINK *-1 BOS OR CS OR CLB 
	BARRIER Nom)(NOT 0 PrfPrc);
	## 1995:s gárvánedje rádiobuvttadeapmái lanjat.

SELECT (Pl3 Nom) IF (*-1 V-PL3 BARRIER NP-HEAD-NOM LINK *-1 BOS OR CS OR
	CLB BARRIER NP-HEAD-NOM);
	## No hits?

SELECT (Num Nom) IF (*-1 ILL-VERB BARRIER NPNHA LINK 0 (Pl3) LINK *-1 Ill 
	BARRIER NOT-ADV-PCLE);
	## Eanetlohku, masa gulle Riektejoavkku 6 eará lahtu,...

SELECT (Num Nom) IF (*-1 V-PL3 BARRIER VFIN OR CS OR CP LINK NOT *-1 Nom BARRIER
	CS OR CP)
	(NOT *-1 Inf BARRIER VFIN OR CS OR CP LINK *-1 VFIN BARRIER CLB OR CS OR CP 
	OR CS);
	## .. ja ahte sin bálvalit moadde olbmo.

SELECT (Pron Pl Nom) IF (*-1 V-PL3 BARRIER NP-HEAD-NOM OR CC OR CS OR CP
	LINK *-1 BOS OR CS OR CLB BARRIER NP-HEAD-NOM);
	## Eará osiid giddodagaid dáfus, leat dát dávjá dilit maid...
	## Sáhttetgo earát gáibidt buhtadasa boahtá das makkár beroštumiin lea... 

SELECT Nom IF (*-1C V-PL3 BARRIER NOT-ADV-PCLE OR MO LINK *-1 BOS OR CS OR CLB
	BARRIER Nom)(0 (N Num) OR NUMERALS);
	## Mus báhce moadde girjji logakeahttá.
	## Go sotnabeaivi šattai, de čuoigaledje máŋggas várrái.

SELECT (Pl Nom) IF (*-1 V-PL3 BARRIER NPNHAI LINK *-1 BOS OR CS BARRIER 
	NOT-ADV-PCLE)(0 NOMINAL-ADJ);
	## ...de bohte muhtun viisát nuortan Jerusalemii.

SELECT Nom IF (*-1 COMMA BARRIER NPNH LINK NOT *-1 V-MAIN BARRIER CS OR CP)
	(NOT *1 VFIN BARRIER CS OR CP)(NOT 0 Adv)(NOT 0 Actio LINK 1 ("veara"));
	## Dá dat leat, 350 ruvnno.

 
# Nominative predicatives
# -----------------------

REMOVE A IF (NOT *-1 COPULAS OR OPRED-VERB BARRIER V-NOT-AUX)(0 N)(NOT 0 ("veara"))
	(1 CLB)(NOT 2 A OR PrsPrc);
	## Ulbmil lea ovddidit guovddáža eamiálbmotdutkamiid guovddázin.
 # This rule has to follow identification of subject.

SELECT (Sg Nom) IF (*-1 COPULAS BARRIER VERB OR CLB OR CP OR CS OR PUNCT OR go OR 
	NP-HEAD-NOM LINK 0 Sg3)(0 NP-HEAD)(NOT 0 PrfPrc OR Actor OR (Prop Loc) OR Ess)
	(NOT 0 Loc LINK *-1 Nom BARRIER CS OR CP OR CLB OR PUNCT)
	(NOT 0 A LINK 0 Ill)(NOT 0 Adv LINK 1 Pr OR VGen)(1C NP-BORDER);
	## Dasgo dus boahtá oaivámuš gii lea mu álbmoga Israela báimman.
	## Dat gal lei beare unnán. 

SELECT (Sg Nom) IF (*-1 COPULAS BARRIER NPNHA OR CC OR (Sg Nom))
	(*-1 V-SG3 BARRIER V-NOT-AUX)(NOT 0 Loc OR Ess OR Adv)
	(*1C NP-BORDER BARRIER WORD);
	## Go šattai eahket, de...

SELECT (Pl Nom) IF (*-1 COPULAS BARRIER VERB OR CLB OR CP OR CS OR PUNCT OR go OR 
	NP-HEAD-NOM OR (@SPRED) LINK 0 Pl3)(0 A OR NP-HEAD)(NOT 0 Inf)(1C NP-BORDER);
	## Vuollel 7% Finnmárkku eatnamiin leat vuovddit. 
	## Olmmošlassáneami geažil šadde eanebut geat galge juogadit buriid.

SELECT (Pl Nom) IF (*-1 COPULAS BARRIER VERB OR CLB OR CP OR CS OR PUNCT OR go OR 
	NP-HEAD-NOM LINK 0 Pl3)(0 A OR NP-HEAD)(NOT 0 Inf)(1 CLB)(2 NOT-NP);
	## ...main leat mielde dušše priváhta oasálaččat, ii leat...

SELECT (Pl Nom) IF (-1 (Indef Attr) LINK *-1 COPULAS BARRIER NOT-ADV-PCLE 
	LINK 0 Pl3)(1 CLB)(2 NOT-NP OR CC);
	## ...seammás go nuppe dáfus leat veháš várrugasat, ja dadjet ahte...

SELECT Nom IF (*-1 COPULAS BARRIER NPNHA OR PrfPrc LINK *-1 MUN 
	BARRIER NOT-ADV-PCLE)(*-1 V-SG1 BARRIER V-NOT-AUX)(NOT 0 PrfPrc)
	(NOT 0 Loc LINK *-1 Nom BARRIER CS OR CP OR CLB OR PUNCT)
	(NOT 1 N OR A)(NOT *1 PrfPrc BARRIER VFIN OR CS);
	## Vaikko mun lean friddja in ge lean geange vuollásaš,...

SELECT Nom IF (*-1 COPULAS BARRIER NPNHA OR PrfPrc LINK *-1 DON 
	BARRIER NOT-ADV-PCLE)(*-1 V-SG2 BARRIER V-NOT-AUX)(NOT 0 PrfPrc)
	(NOT 0 Loc LINK *-1 Nom BARRIER CS OR CP OR CLB OR PUNCT)(NOT 1 N)
	(NOT *1 PrfPrc BARRIER VFIN OR CS);
	## Mii diehtit ahte don leat Israela oahpaheaddji.

SELECT Nom IF (*-1 COPULAS BARRIER V-NOT-AUX OR CS OR CC)
	(NOT *-1 Nom BARRIER NPNHA)(NOT 0 VERB OR Adv OR Loc OR Ill OR Ess)
	(*1 CLB OR CS BARRIER NP-HEAD OR (A Nom));
	## ...maidda lágas eai leat sierra njuolggadusat, muhto...

SELECT Nom IF (-2 Nom)(-1 COPULAS)(0 N OR A)(NOT 0 PrfPrc OR Ess OR (Prop Loc))
	(1 EOS OR ("go"));
	## ...ahte dat leat earalágan go eará joavkkut.
 # Hits here because "earalágan" does not agree?

SELECT Nom IF (*-1 COPULAS BARRIER NOT-ADV-PCLE)(0 (N Num));
	## Stobus leigga guovttis.
	## Mii leimmet golmmas.

REMOVE Loc IF (NOT -1 ("diibmu") OR ("dbm"))(0 (N Num Nom))(NOT 1 Num LINK 1 ("eret"));
	## Mii bođiimet golmmas.
	## Viđas golbma eret lea guokte. # Hence the last condition.


# Nominative as objects in existential clauses
# --------------------------------------------
SELECT Nom IF (-1 COPULAS LINK *-1 Loc BARRIER V-NOT-AUX OR N OR CS OR CP)
	(NOT 0 Adv OR Ess)(NOT *1 (N Nom) BARRIER NPNH);
	## Ii dus leat gávdni masage.

SELECT Nom IF (*-1 COPULAS BARRIER NPNHA OR Nom LINK *-1 Loc BARRIER N OR A OR 
	V-NOT-AUX OR CLB OR CC OR CS OR CP)(NOT 0 PrfPrc)(1 NP-BORDER)
	(NOT 0 Attr OR PrsPrc LINK *1 N BARRIER NOT-ADJ-CC)
	(NOT 0 Adv LINK *1 Nom BARRIER CC OR CLB OR CS OR CP);
	## Dat bulle ja goldne go dain ii lean ruohtas.

SELECT Nom IF (*-1 COPULAS BARRIER VFIN OR Inf OR PrfPrc OR CS OR CP OR Nom)
	(*-1 V-PL3 BARRIER VFIN OR CS LINK *-1 Loc BARRIER NOT-ADV-PCLE)
	(0 NUMERALS);
	## Davviriikkain leat eahpitkeahttá guokte álbmoga maid atnit álgoálbmogin.
 
SELECT Nom IF (*-1 Loc BARRIER VERB OR CLB OR CC OR CP OR CS OR Nom 
	LINK *-1 COPULAS BARRIER VERB OR Nom OR CLB OR CC OR CP OR CS LINK
	NOT *-1 Nom BARRIER VERB OR CC OR CLB OR CS OR CP)(NOT 0 PrfPrc)
	(NOT 0 Adv LINK *1 Nom BARRIER CC OR CLB OR CS OR CP)
	(NOT 0 Attr LINK *1 N BARRIER NPNH)(NOT 0 VFIN LINK -1 CLB)
	(1 NP-BORDER); 
	## Ovdal go kristtalašvuohta bođii Sápmái lei noaiddis erenoamáš
	## rolla oaidnilin ja dálkkodeaddjin.

REMOVE Acc IF (*-1 COPULAS BARRIER PrfPrc OR Inf OR (Actio Ess) OR VFIN 
	OR CC OR CLB OR CS OR CP)(NOT 0 TIME)(1 CS OR EOS);
	## Dain áššiin lea Ruonáeatnama ruovttustivrras ráđđeaddi váldi. 

REMOVE Nom IF (NOT 0 Dem)(1C Nom)(NOT 1 PUNCT OR Num OR Interr)
	(NOT *-1 COPULAS BARRIER V-NOT-AUX OR CS OR CP)(NOT *1 COPULAS BARRIER
	V-NOT-AUX OR CS OR CP LINK NOT *1 V-NOT-AUX BARRIER CS OR CP)
	(NOT -1 Gen LINK 1 Actio LINK 1 N);
	## Divššohasjournála láhkaásahus.
	## ...maid buot journála gevaheaddji dearvvašvuođabargit áddejit.

SELECT (Pl Nom) IF (*-1 COPULAS BARRIER VERB OR CS OR CP OR go LINK 0 Pl3)(1 EOS);
	## Váikkuhusat divššohasa lagamuččaide leat mearkkašahtti stuorrát.
	 
SELECT (Pl Nom) IF  (*-1 COPULAS BARRIER NPNHAI OR PrfPrc)
	(*-1 V-DU-PL BARRIER V-NOT-AUX OR CS)(*-1 PPRON-DU-PL BARRIER CS OR CP 
	LINK NOT *-1 (Pl Nom) BARRIER NOT-ADV-PCLE);
	## Ehpet go dii leat ollu mávssolaččabut go dat?

SELECT (Pl Nom) IF  (*-1 COPULAS BARRIER NPNHAI OR PrfPrc)
	(*-1 V-DU-PL BARRIER V-NOT-AUX OR CS)(NOT 0 Inf)
	(*-1 (N Sg Nom) BARRIER CS LINK *-1 CC BARRIER NPNH LINK -1 (N Sg Nom));
	## Anne ja Biret leaba goappašagat nu jallat.

SELECT Nom IF (*-1 COPULAS OR (IV) BARRIER NOT-ADV-PCLE)(0 NUMERALS)(1 ("ruvdno"));
	## Doaibmabiju jahkásaš bušeahttarámma lea: 380.000 ruvnno.


# Nominative in coordination and apposition
# -----------------------------------------

SELECT Nom IF (*-1 CC BARRIER NPNHAI LINK -1 Nom LINK NOT 0 Acc 
	LINK NOT *-1 V-NOT-AUX BARRIER NPNHA)(NOT 0 VFIN)(1C NP-BORDER);  
	## Divššohas dahje su ovddasteaddji gii oaivvilda ahte...

SELECT Nom IF (-2C HNOUN-NOM)(-1 CC)(NOT 0 Cmpnd OR Actor OR Adv)
	(NOT 0 Attr OR Gen LINK 1 (N Nom))
	(NOT 0 PrfPrc LINK -1 CC LINK *-1 PrfPrc BARRIER VERB);
	## Eamiálbmogat ja riekti - muhtun prinsihpalaš vuolggasajit.

SELECT Nom IF (*-1 CC BARRIER NPNHA LINK -1 Nom LINK NOT 0 Cmpnd)
	(1 CLB OR CS OR EOS)(NOT 0 Inf LINK -1 CC LINK -1 Inf);
	## Doai ja vel okta guossi, dii galgabehtet oažžut dán lanja.

SELECT Nom IF (-2 (Pron Nom))(-1 CC);
	## Jesus Kristus ieš ja Ipmil.

SELECT Nom IF (-1 CC LINK *-1C (Num Nom) BARRIER NOT-NA)(0 Num);
	## Journála divodeapmi ja sihkkun.
	## Joavkkus leat guokte Riikaoasselávdegotti ovddasteaddji ja 2 Sámedikkis.

SELECT Nom IF (-2C Nom)(-1 CC)(1 Gen LINK *1 Po BARRIER NPNH)
	(NOT *1 Nom BARRIER Po);
	## Dearvvašvuođaásahusas mearriduvvo divvun dahje sihkkun dearvvaš-
	## vuođabargiidlága § 44 mielde.

SELECT Nom IF (*-1 CC BARRIER NPNH LINK *-1 Nom BARRIER WORD LINK NOT 0 PrfPrc)
	(1 CS OR EOS OR (PUNCT RIGHT))(NOT 0 Inf LINK -1 CC LINK -1 Inf);
	##  ...Jáhkoba ja Jovssesa eadni, ja Salome. 

SELECT Nom IF (1 CC)(*2C Nom BARRIER NPNHAI LINK 1 NP-BORDER OR CLB);
	## 3) Oččodeapmi, diehtojuohkin ja ásahusovttasbargu.

SELECT Nom IF (1 COMMA LINK *1 Nom BARRIER NOT-NP LINK 1 CC LINK *1 Nom BARRIER
	NOT-NP LINK *1 V-PL3 BARRIER NOT-ADV-PCLE);
	## Sámedikki hálddahusa nannen, dearvvašvuođa- ja eallindiliiskkadeami 
	## ruhtadeapmi ja Sámi dearvvašvuođadutkanguovddáža doaimmaid viiddideapmi 
	## váldojit ovdan.

SELECT Nom IF (NOT 0 Loc)(1 COMMA)(*2 NP-HEAD-NOM BARRIER NPNH LINK 1 CC LINK
	*1 NP-HEAD-NOM BARRIER NPNH LINK 1 CC OR COMMA OR NP-BORDER);
	## ...dikšu, dearvvašvuođabisuheaddji, veajuidahtti dahje dikšun-
	## ja fuolahanulbmilat.

SELECT Nom IF (1 COMMA)(*2C NP-HEAD-NOM BARRIER NPNH LINK 1 COMMA LINK
	*1 NP-HEAD-NOM BARRIER NPNH);
	## Romssa universitehtas lea sámi dutkan, gelbbolašvuođahuksen, 
	## oahpahus ja eará gaskkustepmi leamaš...

SELECT Nom IF (1 ("/"))(*2C Nom BARRIER NPNH);
SELECT Nom IF (-2C Nom)(-1 ("/"));
	## Vuoroheapmi lea: isit/eamit,...
	## Lea ráhkaduvvon diehtojuohkinmateriála/oččodanmateriála. 

SELECT Nom IF (*-1 COMMA BARRIER VFIN LINK -1C NP-HEAD-NOM)(1 COMMA)
	(*2 NP-HEAD-NOM BARRIER NPNH);
	## ..persovdnarievtti dábálaš láhkavuogádagat, bearašriekti, árberiekti,... 

SELECT Nom IF (NOT 0 VERB)(1 COMMA)(*2 NP-HEAD-NOM BARRIER NPNH OR CC OR COMMA 
	LINK 1 EOS OR CP OR CS);
	## Jáhkobii riegádii Jovsset, Márjjá boadnja.

SELECT Nom IF (-2C Nom)(-1 COMMA)(1 CLB OR CS OR CP OR EOS)(NOT 0 Adv LINK 1 CS);
	## Olusat dadjet munnje dan beaivvi: Hearrá, Hearra!

SELECT Nom IF (-2C Nom)(-1 COMMA)(0 N)(NOT 0 Loc)(1 Inf);
	## Muhto ruovttustivra ii leat ožžon mearridanválddi dakkár áššiid 
	## hárrái go stáhtavuođđoásahus, stáhtaborgárdoaba, pássa- ja visumváldi, 
	## váldi mearridit stáhtasymbolaid,...

SELECT Nom IF (*-1 (V Pl3) BARRIER NPNH LINK NOT *-1 (Pl Nom) OR (Pl3 Nom) 
	BARRIER CS OR CP)(1 CC)(*2 Nom BARRIER NPNH);
	## ...maid liikká fertejedje leansmánni ja guokte albmá čujuhit.

SELECT Nom IF (*-1 COMMA BARRIER NPNHAI LINK -1C Nom)(1 CC);
	## ...geat leat divššohasa lagašolbmot, fuolaheaddji dahje veahke-
	## fuolaheddji.  

SELECT Nom IF (1 CC)(2C (Pron Nom))(3 (Pron Nom));
	## Gonagas Agrippa ja dii buohkat geat lehpet dás minguin!

SELECT Nom IF (1 CC)(2C N-NOM)(2 HNOUN);
	## Jos dát leažža dušše olbmuid dáhttu ja dahku,...

SELECT (Indef Nom) IF (-2 NP-HEAD-NOM)(-1 COMMA)(1 Pers);
	## Muhtun kretalaš, muhtun sin iežaset profehtain, lea dadjan... 

SELECT Nom IF (-2 Nom)(-1 (Neg Foc));
	## Eai ovdabarggut, teoria iige geavat doarjjo klausula áddejumi. 


# Not nominative
# --------------
REMOVE Nom IF (*-1 (Pron Nom) BARRIER COPULAS OR CLB OR CC OR CS OR CP
	LINK NOT 0 Interr)(NOT *-1 (Pron Attr) BARRIER NPNH)(0 N);
	## Ávdugasat sii geat duddjojit ráfi!

REMOVE Nom IF (*-1 V-NOT-AUX BARRIER CLB OR CC OR CS OR CP OR PUNCT 
	LINK *-1 (Pron Nom) BARRIER CLB OR CC OR CS OR CP OR PUNCT)
	(NOT *-1 PrsPrc BARRIER NPNH);
	## ...vai ollašuvašii dat mii lea celkojuvvon profehta Jesaja bokte.

REMOVE Nom IF (*-1 VFIN BARRIER NOT-ADV-PCLE OR REALCLB LINK *-1C Nom 
	BARRIER NOT-ADV-PCLE)(*1 V-NOT-AUX BARRIER CLB OR CS OR CP OR go);
	## ...jus sierranas sivat eai eará cealkke.
	## Dii galgabehtet vuđolaččat bargat.

REMOVE Nom IF (*-1 Inf BARRIER VFIN OR PrfPrc OR CS OR CP OR REALCLB OR go 
	OR ("dego") LINK 0 V-TRANS-ACT LINK -1 N)(NOT *1 VFIN BARRIER CS);
	## Universitehtii lea ain hastalussan jorgalit dárogielmateriála.

REMOVE Nom IF (*-1 BOS OR CS OR REALCLB BARRIER NOT-NP)(NOT 1 Prop)
	(*1C Nom BARRIER NPNH OR CC OR CLB OR PUNCT LINK *1 VFIN BARRIER NOT-NP)
	(NOT 1 (Interr Nom));
	## Romssa universitehta vuosttaš čielgadeapmi sámi dutkama birra dahkkui...

REMOVE Nom IF (*-1 COPULAS BARRIER NPNHA LINK *-1 Loc BARRIER NOT-ADV-PCLE)
	(*1 Nom BARRIER NPNH OR CC OR PUNCT OR CLB);
	## Sámedikkiin lea ráđđeaddi váldi buot sámi beroštumiide guoskevaš
	## áššiin.

REMOVE (Sg Nom) IF (*-1 VFIN BARRIER CS OR CP OR CONTRA OR REALCLB OR go OR ("dego") 
	LINK NOT 0 Sg3)(NOT -1 COMMA LINK *-1 (Imprt Sg2) BARRIER CS)
	(NOT 0 NUMERALS OR (Cmpnd) OR ("veara")); 
	## Dan mađe čuovggai mánnu, ahte earuhedje albmáid reagas.

SELECT Adv IF (NOT 0 Attr OR VFIN OR Inf OR VGen OR Indef OR N OR Loc OR Ill)
     (NOT 0 (Num Ess) LINK *1 Com OR Ess BARRIER NPNH)
	(*1 A OR N BARRIER NPNH)(NOT 1 CLB OR CC);
	## Farisealaččat ja erenoamážit sin čáláoahppavaččat nimmoredje.



# Accusative
# =========== #
  CONSTRAINTS #
# =========== #

# Miscellaneous rules related to accusative
# -----------------------------------------
 # The REMOVE Acc rules in this section are rules that do not make reference
 # to an unambiguous Acc in the environment. Rules that do so come at the end 
 # of the accusative chapter.
 # Also note that at present, causatives with two accusatives go wrong.

SELECT (N Sg Acc) IF (-1 ("čáhkket"))(0 ("sadji"));
	## Njálmmálaš duođaštusat fertejit čáhkket saji čálalaš kultuvrra
	## duođaštusaide.

REMOVE Acc IF (-1 Attr)(NOT -1 VERB)(*1 (N Nom) OR (N Acc) 
	BARRIER NPNH OR COMMA OR CC LINK 1 CC LINK NOT *1 Po BARRIER NPNH);
	## ...muhto dálá máilmmi váivvit ja riggodaga fillehus hávkada sáni.

REMOVE Acc IF (*-1 COPULAS BARRIER NOT-ADV-PCLE)(*1 NP-HEAD-NOM BARRIER NPNH);
	## ...ahte dat lea maiddái divššohasa dáhttu ja ahte...

REMOVE Acc IF (*-1 Nom BARRIER NOT-ADV-PCLE LINK *-1 COPULAS BARRIER NOT-ADV-PCLE)
	(*1 CS OR EOS BARRIER V-NOT-AUX);
	## Leat go don juvddalaččaid gonagas?

REMOVE Acc IF (*-1 COPULAS BARRIER NOT-ADV-PCLE)
	(*1 Po BARRIER NPNH LINK *1 Nom BARRIER NPNH);
	## ...šattai dan oainnu mielde lunddolaš, ahte...

REMOVE Acc IF (*-1C Pass BARRIER VFIN OR Inf OR CS OR CP OR CC OR CLB)
	(NOT -1 (Pass PrfPrc) LINK -1 CC LINK -1 Acc)
	(NOT *-1 (Pass PrfPrc) BARRIER NPNH LINK *-1 V-TRANS-ACT BARRIER NPNHA);
	## ...mii lea celkojuvvon profehta Jesaja bokte.

REMOVE Nom IF (*-1C Nom BARRIER CS OR CP OR CC OR CLB OR go OR ("dego") OR ("/"))
	(*-1 V-NOT-AUX BARRIER CS OR CP OR CC OR CLB LINK NOT 0 PrsPrc)
	(NOT 0 Num LINK -1 N OR ("§"))(NOT 1 CC LINK 1 Nom)
	(NOT *-1 VFIN BARRIER CS OR CP LINK *-1 VFIN BARRIER CS OR CP);
	## Na mun váccán joatkkaskuvlla doppe.
 # Two finite verbs, two Nom: "Nu guhkás go Gonagas mearrida gusto láhka..." 

REMOVE Nom IF (-1 (N Nom))(*1 V-NOT-AUX BARRIER NOT-ADV-PCLE);
	## Lagamus oapmahaččas lea vuoigatvuohta journála geahččat.

REMOVE Gen IF (NOT *-1 Pr OR Num BARRIER NPNH)(1 CC)(2 NP-BORDER);
	## Čuovvavaččat ovdanbuktit váldofáddájoavkkuid ja plánejuvvon 
	## doaibmabijuid.

REMOVE (N Gen) IF (NOT *-1 Pr OR Num BARRIER NPNH)(1 (Pron Pers Gen));
	## Gonagas sáhttá addit láhkaásahusaid dán birra.

#REMOVE (N Gen) IF (1C Gen LINK *1 Po OR N BARRIER V); 
REMOVE (N Gen) IF (-1 TV OR TIME)(1C Gen LINK *1 Po OR N BARRIER V); 
	##  Ráđđehus láhčá dilálašvuođaid sámi kultuvrra máŋggadáfotvuođa seailluheapmái.
	## Bargiidbellodaga sámediggejoavku divvu kritihkalaš gažaldagaid ráđi ruhtavuoruhemiid hárrái.
 # The barrier is too weak. The logic is that when we know sámi is Gen, then the
 # constituent before it cannot modify it, hence must be Acc or Nom.
 # The left context also is bad, since we can have a GGPo structure. VXXXPo, where X = A, G
 # can have different solutions.
 # XXX This is the core rule (set) for VX*P strings
 # Return to this point.


# Topicalised object
# ------------------

SELECT Acc IF (-1 BOS OR CS)(0 ("mii"))(1 (N Pl Gen))(2 NP-BORDER); 
	## Maid ipmašiid doppe dagat?

REMOVE Nom IF (NOT *-1 (V Pl3) BARRIER CS)(NOT -1 ("§") OR ("b"))(0 NUMERALS)
	(NOT 1 TIME)(NOT 1 CC)(NOT *1 (V Du3) OR (V Pl3) BARRIER CS);
	## Bargu ollašuhttit máŋga dáin doaibmabijuin lea juo álggahuvvon.

SELECT Acc IF (*-1 BOS OR CS BARRIER NPNH)(NOT 0 Nom)
	(1 COMMA LINK *1 COMMA LINK *1 V-TRANS-ACT BARRIER NP-HEAD)
	(NOT 1 COMMA LINK *1 NP-HEAD-GEN BARRIER NPNH LINK 1 COMMA OR CC);
	## Dáid doaibmabijuid viidáset ovdáneami, earret eará ođasmahttit 
	## sámegieloahppomateriála, sáhttá áimmahuššat universitehta bušeahtta-
	## rámmaid  siskkobealde.

SELECT Acc IF (-1 BOS)(0 (Pron Dem))(*1C Acc BARRIER NPNH) ;
	## Dán máŋggadáfotvuođa háliidit mii bisuhit.

 
# Accusative between Aux and main verb
# ------------------------------------

# After negation
# - - - - - - - -
SELECT Acc IF (*-1 (V Neg) BARRIER CS OR CP)(NOT *-1 ConNeg BARRIER Neg 
	LINK NOT 0 ("leat"))(NOT 0 Nom)(*1 ConNeg OR PrfPrc BARRIER NOT-ADV-PCLE 
	LINK NOT *1 Acc);
	## Na muhto don it leat dasa nama čállán!


# In front of an infinitive
# - - - - - - - - - - - - - 
SELECT Acc IF (*-1 VFIN BARRIER NPNHAI)(NOT 0 VERB)(NOT 0 Nom OR Loc OR Adv OR TIME)
	(*1 Inf BARRIER NOT-ADV-PCLE OR CLB);
	## Dasgo Herodes lei bidjan olbmuid váldit Johanasa gitta.

SELECT Acc IF (*-1 VFIN BARRIER CS OR CP OR PUNCT LINK *-1 Nom BARRIER CS OR CP)
	(NOT 0 PrfPrc OR Nom OR Loc OR Adv OR TIME)(1 Inf);
	## Almmolaš buohcceviessu dahje guovlopsykiatralaš guovddáš galgá
	## divššu čađahit.

SELECT Acc IF (*-1 VFIN BARRIER NPNH)(1 CC)(*2 NP-HEAD BARRIER NPNH LINK 1 Inf);
	## ...ahte lea ferten niesteseahka dahje lávkka guoddit.


# In front of a participle
# - - - - - - - - - - - - -
SELECT Acc IF (*-1 LEAT-FIN-NOT-IMP BARRIER NPNHAI)(NOT 0 Adv OR Nom OR TIME)
	(*1 PrfPrc LINK 0 V-TRANS-ACT BARRIER NOT-ADV LINK NOT *1 ("leat" PrfPrc) 
	BARRIER CS OR CP);
	## Leago barggu ohcan?

SELECT Acc IF (*-1 ("leat") BARRIER NOT-ADV-PCLE)(1 Ess)(2 PrfPrc);
	## Mun lean dan veahkkin ráhkadan.

SELECT Acc IF (1 ("jahki"))(2 ("deavdit"));
	## ...maŋŋá go lea 16 jagi deavdán.


# Accusative postverbally
# -----------------------

SELECT Acc IF (*-1 V-TRANS-ACT BARRIER VERB OR CC OR CLB OR CS OR (@OBJ) 
	OR PUNCT OR ("dego") LINK NOT 0 AUX OR (@ADVL) OR PrsPrc)(NOT 0 TIME)
	(NOT 1 N)(NOT *1 (N Acc) BARRIER NPNH)
	(NOT 0 (Acc PxSg3) LINK 0 Loc)
	(NOT 0 Nom LINK NOT *-1 Nom BARRIER CC OR CLB OR CS OR CP);
	## Ovttasbargu Ruošša dutkanásahusaiguin gáibida almmatge sihke ollu áiggi 
	## ja resurssaid.

SELECT Acc IF (*-1C V-TRANS-ACT BARRIER NPNHA OR CC OR CLB OR ("nugo") OR ("dego")
	LINK NOT 0 Gen)(NOT 0 (Sg Nom) LINK *-1 (V Sg3) BARRIER VFIN OR CS OR CP)
	(NOT -1 PrfPrc LINK NOT *-1 ("leat") BARRIER CS OR CP)
	(NOT 0 PrfPrc OR Adv OR VFIN OR Loc OR TIME)(1C NP-BORDER);
	## Sámegillii galbet universitehta lea šaddan realitehta.

SELECT Acc IF (*-1C V-TRANS-ACT BARRIER NPNHAIIP OR COMMA)
	(NOT -1 PrfPrc OR PrsPrc LINK -1 BOS OR CS)(0 NP-HEAD)
 	(NOT 0 PrfPrc or Adv or Dem or Nom OR Loc OR TIME) 
	(NOT 0 V-PL3 LINK -1 CC LINK *-1 V-PL3 BARRIER NPNHAIIP OR COMMA)	
	(*1C NP-BORDER OR (Num Nom) OR PUNCT BARRIER WORD)(NOT 1 Po) ;
	## Divššohasbearráigeahčči sáhttá váldit meannudeapmái áššiid, mat... 

 # The (Num Nom) is not optimal, we need to consider the Num Ord and Num Card
 # tagging. Now, 22. is given Num Ord and 22.10. is given Num Nom, this gives
 # the correct result, but is not satisfying, linguistically.

SELECT Acc IF (*-1C Po BARRIER NPNHAIIP LINK -1 N OR Pron LINK *-1C V-TRANS-ACT 
	BARRIER NPNHAIIP)(0 NP-HEAD)(NOT 0 Interr OR Refl OR VFIN OR PrfPrc or Adv 
	OR TIME)(1 NP-BORDER OR ADVLCASE OR Adv)(NOT 1 Po);
	## Boazosápmelaččat eai sáhte atnit dološ áiggi rájes geavaheami
	## riektevuođđun bohccuid guođoheapmái Aursunddas.        

 # Extended copy of the previous rule.
 # This rule is to tolerate a PP between the verb and the object.

SELECT Acc IF (*-1 V-TRANS-ACT BARRIER NOT-ADV-PCLE LINK NOT *-1 (@OBJ) BARRIER CS)
	(NOT -1 Gen)(0C N)(1 (Prop Gen));
	## ...go čielggadii oamastanvuoigatvuođa Finnmárkku eatnamiid hárrái.

REMOVE Nom IF (*-1 V-TRANS-ACT BARRIER NPNHA OR PUNCT OR CLB OR CS OR CP 
	LINK 0 VFIN LINK *-1 NP-HEAD-SG-NOM OR NP-HEAD-PL-NOM BARRIER NOT-ADV 
	OR CLB)(NOT *-1 CC BARRIER NPNH LINK -1 Nom); 
	## Seammá gusto jus mánná molsu skuvlla.

SELECT Acc IF (*-1C V-TRANS-ACT BARRIER NPNHAIIP OR COMMA)(0 NP-HEAD)
	(NOT 0 PrfPrc OR Adv OR Dem OR Inf )(1 CC)(*2 VFIN BARRIER NOT-ADV-PCLE);  
	## Bearašsuodjaluskantuvrrat fállet divššu ja rávvejit bearrašiid.
 # Could this rule be modified so that it accepts a subject between 0 and verb? 

REMOVE Gen IF (*-1 V-TRANS-ACT BARRIER NOT-ADV-PCLE)(1 Pers);
	## Sii bilkidit ja doarrádallet din mu dihtii.

REMOVE Gen IF (*-1 V-TRANS-ACT BARRIER NOT-ADV-PCLE)(1 Dem);
	## De gohčui doalvut iežas don beallái jávrri.

SELECT Acc IF (NOT *-1 Acc BARRIER CS OR CP)(NOT *-1 CC BARRIER NPNH LINK -1 Acc)
	(0 NP-HEAD)(NOT 0 VFIN)(1 (A Ess))(NOT *1 Actio BARRIER NOT-ADJ-CC);
	## ...go beaktilis almmolaš beroštumit dagahit dan govttolažžan.

SELECT Acc IF (*-1 ACC-LOC-VERB BARRIER NOT-ADV-PCLE LINK NOT 0 Pass)
	(*1 Loc BARRIER NOT-NP);
	## Sáhttá viežžat dieđuid divššohasa oapmahaččain.

SELECT Acc IF (*-1 ACC-ILL-VERB BARRIER NOT-ADV-PCLE LINK NOT 0 Pass)
	(*1 Ill BARRIER NPNH);
	## Ii oktage bija duohpakeahtes duogŋasa boares biktasii.

REMOVE Gen IF (*-1 V-TRANS-ACT BARRIER NPNHA)(0 N)(1 (Pron Indef))
	(NOT *1 Ill BARRIER NPNH LINK 1 Acc);
	## ...čájehit vuollegašvuođa juohke ovtta divššohasa eallimii.

REMOVE Gen IF (*-1 V-TRANS-ACT BARRIER NPNHA OR Acc LINK NOT 0 ILL-VERB 
	OR ACC-ILL-VERB OR VGen LINK NOT *-1 Acc BARRIER CS OR CP OR CC OR REALCLB)
	(NOT 1 Acc)(*1 Ill BARRIER NPNH LINK NOT 0 ("láhki"));
	## Dasto son vuolggahii sin Betlehemii ja dajai:...
  
REMOVE Gen IF (*-1 Nom BARRIER NOT-ADV-PCLE LINK *-1 V-TRANS-ACT BARRIER 
	NOT-ADV-PCLE LINK NOT 0 ILL-VERB LINK NOT 0 PrsPrc)(*1 Ill BARRIER NPNH);
	## Bijatgo don sohkkara dedjii?

REMOVE Gen IF (*-1 V-TRANS-ACT BARRIER NPNHA OR (@OBJ) OR CLB LINK NOT 0 LOC-VERB OR N)
	(NOT *-1 V-TRANS-ACT BARRIER NPNHA LINK *-1 (@OBJ) BARRIER NOT-ADV-PCLE)
	(NOT 0 Dem)(NOT *1 Acc OR Nom BARRIER NOT-NP OR CC)(*1 Loc BARRIER NPNH);
	## Sii manne vissui ja oidne máná etniinis. 
	## Skuvla galggai oahpahit dárogiela vissis guovlluin.  

SELECT Acc IF (*-1 Loc BARRIER NPNH LINK -1 LOC-VERB)(1 NP-BORDER OR CC OR CLB);
	## Divššohasas lea vuoigatvuohta biehttalit vuostáváldimis vara dahje
	## varrabuktagiid.

SELECT Acc IF (*-1 ("dihte") OR ("dihtii") BARRIER NOT-NP OR PUNCT LINK -1 Actio)
	(NOT 0 Inf)(1 NP-BORDER OR PUNCT);
	## ...ja ovdanbuktin dihte ođđa čoavddusevttohusaid.

SELECT Acc IF (*-1 V-TRANS-ACT BARRIER NPNHA)(1 (VAbess));
	## In mun dáhto vuolggahit sin borakeahttá ruoktut.

SELECT Acc IF (*-1 V-TRANS-ACT BARRIER NPNHA)(NOT 0 Adv)(1 Sup)(NOT 1 Neg);
	## Máhtte guđii bohccuid livadeažžat.

REMOVE Gen IF (*-1 V-TRANS-ACT BARRIER NPNHA)(NOT 0 TIME)(*1 CC BARRIER WORD 
	LINK *1 Nom BARRIER NPNH LINK *1 VFIN BARRIER NOT-ADV-PCLE);
	## Mun lohken girjji ja Niillas guldalii ođđasiid.
 

# Accusative sentence-finally
# ---------------------------

REMOVE Gen IF (NOT *-1 Pr OR Num OR CC BARRIER NPNH)(0 N OR Pron)(NOT 0 TIME)
 	(*1 CS OR CP OR EOS BARRIER NOT-ADV-PCLE) ;
	## Ferte čájehit várrugasvuođa go lea sáhka sihkkumis dakkár dieđuid.

 # CC in the preceding rule because of coordination after Pr. The following rule 
 # compensates for it.

SELECT Acc IF (NOT *-1 Pr OR Num BARRIER NPNH)(0 NOTIME OR Pron)
	(NOT 0 Nom OR Loc OR Adv OR VERB)
	(*1 CLB BARRIER NOT-ADV-PCLE LINK NOT *1 Gen BARRIER NPNH);
	## Jus divššohas roasmmohuvvá dahje oažžu duođalaš váttisvuođaid,...
	## Liggen dihtii viesuideaset, ferte ollusat muoraid boaldit.

REMOVE Gen IF (NOT *-1 Pr OR Num BARRIER NPNH)(0 NOTIME OR Pron)
 	(*1 PUNCT BARRIER NOT-ADV-PCLE LINK 1 NP-BORDER) ;
	## Dohkkehit persovnnalaš sávaldagaid geavahit álbmotdálkkodeami
	## ja guvhlláriid.

REMOVE Gen IF (NOT *-1 Pr OR CC OR (Num Nom) BARRIER NPNH)(-1 Gen)(0 NOTIME OR Pron)
	(NOT 0 ALU)
	(*1 CLB BARRIER NOT-ADV-PCLE)(NOT 1 COMMA LINK 1 Gen LINK 1 CC LINK 1 Gen);
	## ...ahte vuhtiiváldit divššohasa dárbbuid, beroštumiid ja riekte-
	## sihkarvuođa dearvvašvuođabálvalusa ektui.


# Accusative numeral
# ------------------

SELECT Acc IF (*-1 V-TRANS-ACT BARRIER NOT-ADV-PCLE LINK NOT *-1 Acc BARRIER CS OR CP)
	(0 Num)(1 (N Acc));
	## Galgá geavahit ovtta divššohasjournála juohke divššohasa nammii.
	## Juohke kursa lea árvvoštallojuvvon máksit 50.000 ruvnno.

SELECT Acc IF (0 Num)(*1C (N Acc) BARRIER NPNH OR Gen LINK NOT 0 (@MEASURE));
	## Láve dat gal olmmoš ovtta suorrerissi maiddái atnit.


# Accusative after accusative numerals
# ------------------------------------
SELECT Acc IF (*-1 (Num Acc) BARRIER NPNH OR N LINK NOT 0 Gen);
	## Go mánná lea deavdán 12 jagi, son galgá beassat buktit oaivilis.

SELECT (N Acc) IF (*-1C (Num Acc) BARRIER NPNH);
	##  Mihttomearrin lea buvttadit 2 sámegiel almmuheami jahkái.


# Objects with object predicatives
# --------------------------------
 # Note that a rule for accusative predicates following accusative subjects and 
 # copulas is found right before the nominative rule block.

SELECT Acc IF (*-1 OPRED-VERB BARRIER NPNHA LINK NOT 0 Pass OR Actio LINK 
	NOT *-1 NP-HEAD-ACC BARRIER V-NOT-AUX OR CS OR CP)(NOT 0 TIME)
	(NOT *1 Acc BARRIER NPNH OR PUNCT OR CC OR ("/"));
	## Dážat eai atnán sámiid buoret go beanan.

SELECT Acc IF (*-1 OPRED-VERB BARRIER NOT-NP OR PUNCT LINK NOT 0 Pass OR Actio)
	(NOT *-1C Acc BARRIER CC OR CS)(NOT *-1 Num BARRIER NPNH LINK -1 Acc)
	(1 Ess);
	## Lea deaŧalaš movttiidahttit vuolit gráda studeanttaid válljet 
	## sámi guoskevaš čuolbmačilgehusaid ja eamiálbmotáššiid váldofágafáddán.

SELECT Acc IF (*-1 OPRED-VERB BARRIER NOT-NP OR PUNCT LINK NOT 0 Pass OR Actio)
	(NOT *-1C Acc BARRIER CC OR CS)(1 PUNCT OR CLB OR EOS);
	## ...ja muđui atná son doavggusin oađđadagaid (duolji, ránu) ja 
	## biktasiid.

SELECT Acc IF (*-1 CC BARRIER NPNH LINK -1 Ess LINK -1 Acc)(NOT 0 PrsPrc)
	(1 A OR Ess);
	## ...dahkan doaimmaid vuoiggaleabbun ja sosiálabálvalusaid álkibut.

SELECT Acc IF (*-1 V-TRANS-ACT BARRIER NOT-ADV-PCLE OR CLB LINK NOT *-1 NP-HEAD-ACC
	BARRIER V-NOT-AUX OR CP OR CS)(1 Ess)(NOT 2 Inf);
	## Mun oidnen Máreha mánnán.
	## Mun oidnen Máreha boahtime.
 # The rule also hits objects in front of Actio Ess, which seems OK.

REMOVE Acc IF (*-1 Acc BARRIER NOT-NP OR Inf OR CLB LINK *-1 OPRED-VERB 
	BARRIER NPNH)(NOT -1 ("/") OR CC LINK -1 Acc);
	## ...go doavttir lea dahkan biehttaleami apotehka vuoigatvuođa vuostá.

REMOVE Acc IF (*-1 Acc BARRIER NOT-NP LINK *-1 OPRED-VERB BARRIER NOT-NP)
	(NOT *-1 COMMA OR ("/") OR (PUNCT LEFT) OR CC BARRIER NPNH LINK -1 Acc)
	(NOT 0 Interr);
	## Dát sáhttá dáhpáhuvvat go atná konkrehtalaš daguid/hilgumiid ovtta
	## dahje nuppi riekteáddejumi boađusin.
 

# Accusative subjects of infinite verbs
# -------------------------------------

SELECT Acc IF (*-1 V-TRANS-ACT BARRIER NOT-NP)(NOT 0 Gen LINK *1 (N Acc) 
	BARRIER NPNH OR CC)(NOT 0 ("leat"))
	(*1 (Actio Ess) BARRIER NOT-NP LINK NOT -1 CC);
	## De oinnii olbmá čohkkámin tuolloviesus.

SELECT Acc IF (*-1 V-TRANS-ACT BARRIER NOT-NP)(NOT 0 Gen LINK *1 (N Acc) 
	BARRIER NPNH OR CC)(*1 PrfPrc BARRIER NOT-ADV-PCLE);
	## Niillas logai Máhte geahččan Máreha ráfálaččan.


# Accusative in temporal adverbials
# ---------------------------------

REMOVE Nom IF (*-1 (@SUBJ) BARRIER CS OR CP)(NOT *-1 ("leat") BARRIER VERB)
	(0 NUMERALS)(1 TIME);
	## Fierbmi bistá guokte- golbma jagi.

SELECT Acc IF (NOT *-1 ("leat") BARRIER VERB)(0 Num)(1 TIME)(NOT 2 N OR Po);
	## Go fylkkadoavttir lea seailluhan journála 10 jagi,...

SELECT Acc IF (-1C NP-BORDER)(NOT -1 Interr OR Indef OR Pr)(0 TIME)(NOT 0 Loc)
	(0 ("beaivi") OR ("idja") OR ("vahkku") OR ("mánnu") OR ("dálvi") OR 
	("geassi") OR ("jahki"))(NOT *1 TIME BARRIER NPNH);
	## Máhtte duddjostalai beaivvi.

SELECT Acc IF (0 ("bodda"));
	## Mun finan gávppašeame dan botta.


# Accusative in measure expressions
# ---------------------------------

SELECT Acc IF (0 Num)(1 TIME OR MEASURE OR ("ruvnno"))(2 A);
	## Mánná lea golbma jagi boaris.


# Accusative in apposition
# ------------------------
SELECT (N Acc) IF (-1 (Pers Gen))(1C (Prop Acc));
	## Jesus válddii fárrosis Biehtára, Jáhkoba, ja su vielja Johanasa. 

SELECT Acc IF (*-1 V-TRANS-ACT BARRIER VERB OR CS OR CP OR CC)(1 COMMA)
	(*1C NP-HEAD-ACC BARRIER NPNH OR CC OR ("gč") LINK NOT 0 Interr);
	## Oidnet go don Mihkkala, min nuoramus bártni?

SELECT Acc IF (*-1 (PUNCT LEFT) BARRIER NOT-NP OR ABBR LINK -1C Acc)
	(1 (PUNCT RIGHT));
SELECT Acc IF (-2C Acc)(-1 (PUNCT LEFT))(NOT 1 Acc)
	(*1 (PUNCT RIGHT) BARRIER NOT-NP);
	## ...ja muđui atná son doavggusin oađđadagaid (duolji, ránu) ja 
	## biktasiid.


# Accusative indefinite pronouns
# ------------------------------
SELECT Acc IF (0 (Pron Indef))(*1C (N Acc) BARRIER NPNH OR N);
	## Galgetgo sii guldalit iešguđet geavaheaddjijoavkkuid. 


# Accusative in coordination
# --------------------------

# First part
# - - - - - -
SELECT Acc IF (0 N)(1 CC)(*2C NP-HEAD-ACC BARRIER NPNHA OR Gen LINK 1 NP-BORDER);
	## Dalle skáhppo biergasiid nugo firpmiid ja buođđomuoraid.

SELECT Acc IF (1 CC)(2C Acc)(3C NP-BORDER);
	## Bovde váivvážiid ja rámbbiid, lápmásiid ja čalmmehemiid.

SELECT Acc IF (*-1 V-TRANS-ACT BARRIER NPNHA OR CONTRA)(1 CC OR CLB)
	(*2C Acc BARRIER NOT-ADJ LINK 1 CC OR CLB OR NP-BORDER);
	## Dát guoská bargguide ásahit mastera ja bachelora,..

SELECT Acc IF (*-1 V-TRANS-ACT BARRIER NPNHA)(1 CC)(2 Acc)(3 CLB);
	## ...váldi mearridit mihtuid ja deattuid,...

SELECT Acc IF (*-1C V-TRANS-ACT BARRIER NPNHAII)(1 CLB)(2 Acc)(3 CC OR CLB)
	(NOT 4 Gen LINK *1 N BARRIER NPNH OR CC)
	(NOT 4 Indef LINK 1 Gen LINK *1 N BARRIER NPNH OR CC);
	## Jesus válddii fárrosis Biehtára, Jáhkoba ja Johanasa.

SELECT Acc IF (*-1C V-TRANS-ACT BARRIER NPNHAII)(0 N)(1 CC)
	(*2C NP-HEAD-ACC BARRIER NPNH OR Gen);
	## Mii guoská váldofáddájovkui mii gieđahallá dutkama ja oahpahusa, de...

SELECT Acc IF (1 ("/") LINK *1 NP-HEAD-ACC BARRIER NPNH LINK 1 NP-BORDER);
	## Lea dárbu ásahit sámegiela vierisgiellaoahpahusa/álgooahpahusa.
 
SELECT Acc IF (*-1 V-TRANS-ACT BARRIER NPNHAII)(0 N)(1 COMMA)
	(*2 NP-HEAD-ACC BARRIER NPNHA LINK 1 CC LINK 1 NP-HEAD-ACC BARRIER NPNHA 
	LINK 1 NP-BORDER);
         ## Jos juohká dážaid, sámiid ja láddelaččaid iešguđet sadjái, ...

SELECT Acc IF (1 COMMA)(*2 Acc BARRIER NPNH LINK 1 CC LINK 1 Acc LINK 1 NP-BORDER);
	## ...váfistan dihte Romssa universitehta sámi dutkama, oahpahusa ja
	## gaskkusteami.


# Second part
# - - - - - -
SELECT Acc IF (*-1 CC BARRIER NPNH LINK -1C Acc)(NOT 0 Inf)(1 NP-BORDER)
	(NOT 0 Dem LINK 1 PrfPrc LINK *1 N BARRIER NPNH);
	## ...ja atnigohte meahci ja vuovddi.
 
SELECT Acc IF (-1 CC OR COMMA BARRIER NPNH LINK *-1 VFIN BARRIER CS OR CP LINK 
	*-1 (Pron Interr) BARRIER VFIN OR CC OR CS OR CP LINK -1 NP-HEAD-ACC)
	(1 NP-BORDER OR CC OR CLB);
	## Divššohasbearráigeahčči galgá addit sutnje dieđuid gii jearrá, ráđiid 
	## ja bagadallama. 

SELECT Acc IF (*-1 CC BARRIER NPNH LINK -1C Acc)(1 COMMA);
	## Álgoálbmogii galgá sihkkarastit "rivttiid ja vejolašvuođaid, maid
	## našunála lágat sihkkarastet eará ássiide".

SELECT Acc IF (*-1 CC OR COMMA BARRIER NPNH LINK -1C NP-HEAD-ACC)(1 COMMA OR CC)
	(*2 NP-HEAD-ACC BARRIER NPNH LINK 1 CLB OR CC OR NP-BORDER);
 	## ...váfistan dihte Romssa universitehta sámi ja eamiálbmot-
	## guskevaš dutkama, oahpahusa ja gaskkusteami.

SELECT Acc IF (*-1 COMMA BARRIER NPNH LINK -1C Acc LINK *-1 V-TRANS-ACT 
	BARRIER NPNHAII)(1 CC OR COMMA);
        ## Jos juohká dážaid, sámiid ja láddelaččaid ieš guđet sadjái, ...

SELECT Acc IF (-2C Acc)(-1 ("/"));
	## Dát sáhttá dáhpáhuvvat go atná konkrehtalaš daguid/hilgumiid 
	## okta dahje nuppi riekteáddejumi boađusin.


# Third part
# - - - - - -
SELECT Acc IF (*-1 CC BARRIER NPNH LINK -1C Acc LINK -1 COMMA BARRIER NPNH 
	LINK -1C Acc)(1 NP-BORDER OR Gen);
	## Ferte vuhtiiváldit divššohasa dárbbuid, beroštumiid ja riektesihkar-
	## vuođa dearvvašvuođabálvalusa ektui. 


# Accusative in apposition
# -----------------------

SELECT Acc IF (*-1 COMMA BARRIER NPNHA LINK -1 Acc)(NOT 0 Gen LINK 1 N)
	(NOT 1 COMMA OR CC LINK 1 Gen)(*1 COMMA BARRIER VFIN);
	## Romssa universitehta ohcá dan dihte lassiruhtajuolludeami, oktiibuot
	## 3.000.000 ruvnno jahkásaččat 5 jagi badjel, váfistan dihte Romssa
	## universitehta sámi ja eamiálbmotguoskevaš dutkama, oahpahusa ja
	## gaskkusteami.

SELECT Acc IF (0 ("hearrá"))(1C ("ipmil" Acc));
	## It galgga geahččalit Herrá Ipmilat.

 # Special rule to avoid genitive reading of "Hearrá". Needed because "Hearrá Ipmilat"
 # is different from "Title Name" and "Firstname Lastname" combinations.
 
SELECT Acc IF (-1 ("hearrá" Acc))(0 ("ipmil"));
	## Don galggat ráhkistit Hearrá Ipmilat oppa váimmustat..


# Not accusative
# --------------

REMOVE Acc IF (-1 ("juohke"))(0 ("okta"))(*1 N BARRIER NOT-ADJ LINK NOT 0 Acc);
	## ...čájehit vuollegašvuođa juohke ovtta divššohasa eallimii.

REMOVE (N Acc) IF (*-1C (N Acc) BARRIER Inf OR Actio OR CS OR CP OR CC OR CLB 
	OR ("/") LINK NOT *-1 Interr BARRIER CS)(NOT 0 Prop LINK -1 (N Acc));
	## Mánáin lea vuoigatvuohta oažžut dárbbášlaš dearvvašvuođaveahki
	## maiddái dearvvašvuođaiskkadeami hámis...

REMOVE Acc IF (NOT 0 Dem OR Num)(*1C (N Acc) BARRIER NPNH OR PrfPrc OR COMMA OR CC 
	OR ("/") LINK NOT 0 TIME OR ROUTE)(NOT 0 (N Acc) LINK 1 (Prop Acc));
	## Divššohasas lea vuoigatvuohta iežas journála geahččat. 

REMOVE Acc IF (NOT 0 Dem OR Pers OR Interr)(*1C (Pron Acc) BARRIER CC OR CP 
	OR CS OR CLB OR Neg OR Actio OR go LINK NOT 0 Interr OR Refl)(NOT 1 Indef);
	## Golbma olbmá jearahit du.

REMOVE Acc IF (NOT *-2 Acc BARRIER NOT-NP)(-1 CC)(NOT 0 Interr)
	(*1 N BARRIER NPNH OR CC OR COMMA);
	## Buot borramuš ja siidda gálvvu, ja... 

REMOVE Acc IF (NOT *-1 V-TRANS-ACT BARRIER V-NOT-AUX OR Inf OR CS)(NOT 0 TIME)
	(NOT 0 Dem LINK *1 (N Acc) BARRIER NPNH)(NOT 0 NUMERALS LINK 1 TIME)  
	(NOT *1 V-TRANS-ACT BARRIER CS OR CP);
        ## Sápmelaččaid historjá álbmogin lea duháhiid jagiid boaris, muhto
        ## dálá ássanguovlluin sápmelaččat eai leat ássan nu guhká.

REMOVE Acc IF (NOT *-1 V-TRANS-ACT BARRIER V-NOT-AUX OR CS OR CP)(NOT 1 Ger)
	(*1 V-TRANS-ACT BARRIER CS OR CP LINK *1C Acc BARRIER CC OR CS OR CP);
	## Bivdi sáhttá vuordit guoli boahtit buđđui.

REMOVE Acc IF (*-1C Acc BARRIER CS OR CP OR REALCLB)(NOT *-1 CC BARRIER NPNH 
	LINK -1 Acc)(*1C A BARRIER NPNH LINK 1 NP-BORDER)(NOT 1 CC);
	## ...maid sáhttá geavahit eamiálbmotjoavkkuid miellahtuid buorrin.

REMOVE Acc IF (NOT *-1 V-TRANS-ACT BARRIER CS)(NOT 0 TIME)
	(*1 V-PASS BARRIER V-NOT-AUX OR CS OR CP);
	## ...ahte sámi nuoraide láhččojuvvo vejolašvuohta oassálastit... 

REMOVE Acc IF (*-1C V-PASS BARRIER VFIN OR Inf)(NOT 0 TIME)
	(NOT *1 V-TRANS-ACT BARRIER CS OR CP);
	## Dál nannejuvvui girku sámeguovlluin.

REMOVE Acc IF (NOT 0 TIME)(*1 V-TRANS-ACT BARRIER CLB OR CS OR CP OR CC 
	LINK *1 ("ahte") BARRIER NOT-ADV-PCLE OR ("nu"));
	## Mu referáhta namma dadja, ahte...
 # Removes accusative object if there is a clausal object.

REMOVE Acc IF (*-1 V-TRANS-ACT BARRIER NOT-ADV-PCLE)(0 Loc)(*1 ("ahte") BARRIER WORD);
	## Olmmoš galgá almmuhit skovis, ahte objektiivvalaš eaktu lea dievvan.
 # Same here.

REMOVE Acc IF (0 (N Sg Nom))(1 V-SG3)(NOT *1C Nom BARRIER CS);
	## ...mii lea mearriduvvon lágas dahje maid láhka doarju.

REMOVE Acc IF (NOT 0 TIME OR Dem)(*1C N BARRIER NPNH LINK 1C Inf);
	## Oainnán iežan bargun guossohastit veahá biepmu buohkaide. 

REMOVE Acc IF (0C HNOUN OR (Pron Pers))(NOT 1 CC OR PUNCT OR CLB)
	(*1C (N Acc) BARRIER NPNH LINK NOT 0 TIME LINK 1 NP-BORDER OR ADVLCASE);
	## Lea dárbu nannet hálddahuslaš bargiid vejolašvuođa oahppat sámegiela.
	## Dál lea stuorát dáhttu go ovdal atnit sápmelaččaid dološ 
	## geavahanávkkástallama dakkarin. 

REMOVE Acc IF (0C HNOUN OR (Pron Pers))(NOT 1 CLB)
	(*1 Acc BARRIER NPNH OR CC LINK 0C N LINK NOT 0 Loc OR TIME LINK 1 CLB);
	## Válljedettiin journálaovddasvástideaddji berre árvvoštallat su 
	## fágagealbbu.
	## ...de rahpá čoahkkima ovddit áigodaga Sámedikki čoahkkinjođiheaddji.

SELECT Gen IF (*-1 ("dihte") OR ("dihtii") BARRIER NPNH LINK -1 Actio)
	(NOT 0 Dem)(NOT 0 Inf LINK -1 CC)(NOT 1 CC)(*1 (N Acc) BARRIER NPNH);
	## ...váfistan dihte Romssa universitehta sámi dutkama.


# Illative
# ========
 # Sg Ill = Prt Sg3 (for some verbs), Pl Ill = Prt Du2 (for some verbs)
 
SELECT Ill IF (*-1 VFIN OR Inf BARRIER CC OR CLB OR CS OR CP)(0 (Prt Sg3))
	(NOT 0 Adv);
	## Dat lea sivalaš helveha dollii.  
 # The last condition because of "oktii" etc.

REMOVE (Prt Du2) IF (*-1 VFIN OR Inf BARRIER CS OR CP);
	## Fágabirrasat main lea gieddebárgu, bovdejuvvojit fállat giedde-
	## bárgguid sámi nuoraide. 

REMOVE (Du2) IF (*1 VFIN BARRIER CC OR CS OR CP OR REALCLB);
	## Jahkásaš bušeahttarámma dán prošeavtta doaimmaide gártá 70.000 ruvnno.

SELECT Ill IF (-2C Ill)(-1 COMMA OR CC);
	## ...vuoigatvuohta guohtumii, bivdui, guollebivdui, jna.

SELECT Ill IF (1 CC OR COMMA)(*2C Ill BARRIER NPNH);
	## Gohccái ja čujuheapmái nammaduvvui gonagaslaš vuovdefáldi.



# ================= #
      CONSTRAINTS
# ================= #

# Locative
# ========

# ToC for this section

    # NP-internal disambiguation of Loc
    # Disambiguation based upon type of NP
    # Disambiguation based upon verb valency
    # Other locative rules

# NP-internal disambiguation of Loc
# ---------------------------------
SELECT Loc IF (*-1C (Dem Pl Loc) BARRIER NPNH);
SELECT Loc IF (-2C (Dem Pl Loc))(-1 Adv);
	## Ollu dáin doaibmabijuin sáhttá álggahit...
	## ??
 # "Dain" is unambiguously Loc. "Dainna" is not.

REMOVE (Dem Pl Loc) IF (1 (Num Sg));
	## Dainna čuđiin ruvnnuin áiggun bierggu oastit.

SELECT Loc IF (*-1 ("bealli") OR ("guhtemuš") OR ("goappašat") OR ("oassi") OR 
	("ollu") OR ("ollut") BARRIER NPNH);
	## Guhtemuš dainna bivdovugiin guhkimus leaš gevahuvvon...
	## Dat sáhttet leat oassin árvvoštallamiin.

SELECT (Pl Loc) IF (-2 Num)(-1 PROSEANTA); 
	## Dat lea 50 % ruđain.


# Disambiguation based upon type of NP
# ------------------------------------

SELECT (Pl Loc) IF (0 (laš) OR ("sápmelaš"))(NOT 0 (A*));
	## Sápmelaččain sáhttet leat erenoamáš vuoigatvuođat.

SELECT Loc IF (NOT -1 Num)(0 VAHKKU);
	## 1870-jagiin lea čielggas ahte láddelaččaide guske ortnegat.
	## ...gal guhkui ruoššaid gávppašanriekti guvttiin vahkuin. NOT -1 Num!

SELECT (Pl Loc) IF (0 GUOVLU);
	## Sápmelaččat leat unnitlogus earret muhtun unnit guovlluin.

SELECT Loc IF (0 Num)(*1 PL-NOUN BARRIER NOT-ADJ);
SELECT Loc IF (0 PL-NOUN);
	## Mun ledjen golmmain heajain.

SELECT (Pl Loc) IF (0 ("beassáš"));
	## Dat bistá beassážiin hellodagaide.


# Disambiguation based upon verb valency
# --------------------------------------

SELECT Loc IF (*-1 LOC-VERB BARRIER CS OR CP OR VFIN LINK NOT 0 Adv)
	(NOT *-1 CC BARRIER CS OR CP LINK -1 LOC-VERB)
	(NOT 0 Num OR Pron LINK *1C (N Com) BARRIER NPNH);
	## Juohke dáhpáhusas ferte áibbas boastut leat beroškeahttá sámiid
	## riekteáddejumiin.

SELECT Loc IF (NOT 0 ("dat"))(NOT *1C Com BARRIER NPNH)
	(NOT 0 (N Num Nom) LINK 1 (Sg Com))(NOT 0 (Sg Com) LINK -1 (N Num Nom))
	(*1 LOC-VERB BARRIER CS OR CP OR V-NOT-AUX);
	## Maiddái sámi fágabirrasiin ja ásahusain sáhttá jearrat ráđiid.

SELECT Loc IF (*-1 ACC-LOC-VERB BARRIER NOT-NP);
	## Sáhttá viežžat dieđuid divššohasa oapmahaččain.


# Other locative rules
# --------------------

SELECT Loc IF (*-1 ("sáhka") BARRIER NPNH);
        ## Ii leat dušše sáhka olbmuid oktavuođain juohkebeaivválaš eallimis.

SELECT Loc IF (*-1 ("fuolahus") BARRIER NPNH);
	## Jus mánáidsuodjalusbálvalus lea váldán fuolahusa mánáin...

SELECT Loc IF (1 ("gos"));
SELECT Loc IF (1 COMMA)(2 ("gos"));  
	## Riikkain gos lea čearddalaš, oskkolaš dahje gielalaš minoritehtat,...
	## Sajiin, gos ii leat oktage priváhta eanaeaiggát,...

SELECT Loc IF (*-1 BOS OR CS OR REALCLB BARRIER NPNHAI)(NOT 0 (A Nom))
	(NOT 0 (N Num Nom) LINK *1 (V Du3) BARRIER NOT-ADV-PCLE)
	(*1 ("leat") BARRIER (@SUBJ) OR (@OBJ) OR (@SPRED) OR (@OPRED) );
#	(*1 ("leat") BARRIER NOT-ADV-PCLE-NEG ); # Check this rule with the positive barrier.
	## Ádjásgo lea ođđa fanas?
        ## Suomas maid lei dilli váttis.
        ## Lágas eai leat mearrádusat.
	## Daningo sámiin lea iežaset giella.

SELECT Loc IF (1 COPULAS)(*2 NP-HEAD-NOM BARRIER NPNHA LINK NOT *1 V-NOT-AUX
	BARRIER NOT-ADV-PCLE);
	## ..gos čoavjevuliin leat geažit latnjalagaid. 
        ## 70 proseanttas guovllu dearvvašvuođaásahusain leat sámegielat bargit.
         # But we do not catch the "of them" construction:
        ## Vihta dainna leat dál.

SELECT Loc IF (*-1 BOS OR CS OR REALCLB BARRIER NOT-NP)(*1 Interr BARRIER WORD
	LINK *1 VFIN BARRIER CS OR CP LINK *1 VFIN BARRIER CS OR CP LINK 0 ("leat")
	LINK *1 NP-HEAD-NOM BARRIER NPNHA LINK NOT *1 PrfPrc BARRIER NOT-ADV-PCLE);
	## Váhnemiin dahje earáin, geain lea váhnenovddasvástádus, lea vuoigat-
	## vuohta miehtat dearvvašvuođaveahki divššohasaide.


SELECT Loc IF (-1 ("leat" Clt) LINK *-1 BOS OR CS OR REALCLB BARRIER NOT-CC); 
SELECT Loc IF (-1 ("go" Pcle))(-2 ("leat") LINK *-1 BOS OR CS OR REALCLB 
	BARRIER NOT-CC); 
	## Leago dis ruđaid?
	## Lea go dis ruđaid?

SELECT (Sg Loc) IF (*-1 NP-HEAD-NOM BARRIER NPNH LINK *-1 COPULAS BARRIER NPNHA);
	## Dat lei váldooaidnu boazodoallolága dulkomis. 

SELECT Loc IF (*-1 COPULAS BARRIER NPNHA LINK NOT *-1 Nom BARRIER CS)
	(*1 Nom BARRIER NPNHA);
	## Dattege lea lassebeavdegirjjis stuorra ideologalaš mearkkašupmi 
	## sámi vuoigatvuođagáibádusaid hárrái.

SELECT (Prop Loc) IF (*-1 COPULAS BARRIER NOT-ADV-PCLE LINK NOT *-1 ("namma")
	OR ("áhčči") BARRIER CS OR CP);
	## Go sii ledje ain čoahkis Galileas, de...
	## Su namma galgá leat Johanas. NOT *-1 "namma"!

SELECT (Pl Loc) IF (1 LEAT-FIN-NOT-IMP)(*2 NP-HEAD-NOM OR (Num Nom) 
	BARRIER V-NOT-AUX OR CS);
	## Badjel 16 jahkásaš mánáin lea iešheanalis vuoigatvuohta...

SELECT Loc IF (0 ("láhka"))(1 ("§")); 
	## ...mat leat namahuvvon Dearvvašvuođabargiid lágas § 3.

REMOVE (Sg Loc) IF (0 (V Actio Loc))(*1 Acc BARRIER NPNH);
	## Ii leat sáhka juohkimis iešguđet journálačálli olbmuid ektui.

SELECT Loc IF (0 (Acc PxSg3))(1 NP-BORDER)(*-1C NP-HEAD-ACC BARRIER CC OR CS OR CP);
	## Beassá dárogiela maid vehá oahppat dulkomis.

REMOVE (Sg Com) IF (0 Pl)(1C (Interr Pl));
	## Mearrádusa ferte čađahit láhkaaddimiin ja eará doaimmain mat almmolaš 
	## eiseválddit mearridit.


# Plural locative as complement of quantifier

SELECT Loc IF (*-1 Num BARRIER NPNH)(0 Pl or Pl3) ;
        ## Mun dovdan vihtta dainna.


# Comitative
# ==========

# ToC for this section

     # Various rules
     # NP-internal disambiguation of Com
     # Disambiguation based upon type of NP
     # Disambiguation based upon verb valency
     # Coordination


# Various rules
# -------------
SELECT Com IF (*-1 ("ovttas") OR ("ovttamielas") OR ("oktan") OR ("searválagaid") 
	BARRIER (N Com));
	## Ii lean dárbu dábálaš eanavuovdimii, oktan opmodatregistreremiin.

SELECT Com IF (*-1 ("oktavuohta") OR ("gaskavuohta") OR ("ovttasráđiid") BARRIER 
	NPNH);
	## ...go sii váldet oktavuođa doaktáriin.

SELECT Com IF (*-1 ("váldit") BARRIER NPNH LINK -1 ("oktavuohta"));
	## Vuoigatvuohta oktavuođa váldit divššohasbearráigeahččiin.

SELECT Com IF (*-1 ("malssidit") BARRIER VERB);
	## Máhtte malssidii lávkkas Máreha lávkkain.

SELECT (Sg Com) IF (-1 (Pers Du1))(*1 V-DU1 BARRIER VERB OR CC OR CS OR CP);
	## Moai áhčiin manaime dohko.

SELECT (Sg Com) IF (-1 (Pers Du3))(*1 V-DU2 BARRIER VERB OR CC OR CS OR CP);
	##

SELECT (Sg Com) IF (-1 (Pers Du3))(*1 V-DU3 BARRIER VERB OR CC OR CS OR CP);
	##

SELECT (Sg Com) IF (-1 (Pers Gen) LINK 0 Du1 OR Du2 OR Du3)(1 N);
	## Dat lea munno vieljain dállu.


# NP-internal disambiguation of Com
# ---------------------------------
SELECT Com IF (*-1C (Dem Sg Com) BARRIER NPNH);
SELECT Com IF (-2C (Dem Sg Com))(-1 Adv);
	## Romssa guovllubuohcceviessu lea bargamin dáinna áššiin.
	## ??

SELECT Com IF (0 Num); # Cf. rules for PL-NOUN above.
SELECT Com IF (*-1C Num BARRIER NOT-ADJ OR ("jahkásaš"));
	## ...de son gal luoitá nođiid; ovttain sániin dadjat: luoitala.
	## Mo sáhttá mottiin sániin dadjat...


# Disambiguation based upon type of NP
# ------------------------------------ 
SELECT Com IF (0 Prop)(NOT 0 ("Sápmi") OR ("Sámediggi")); 
	##
 # Person names are normally sg.
	

# Disambiguation based upon verb valency
# --------------------------------------
SELECT Com IF (*-1 COM-VERB BARRIER VERB OR CS OR CP LINK 0 VERB)(NOT 0 GUOVLU);
	## Dieđut galget meannuduvvot várrugasvuođain.

SELECT Com IF (0 Sg)(*1 COM-VERB BARRIER CS OR CP LINK 0 VERB)
	(NOT *1 COPULAS BARRIER VERB LINK *1 COM-VERB BARRIER NOT-ADV-PCLE 
	LINK 0 Inf);
        ## ...go sápmelaččat duođas álge eanadoaluin bargat.


# Coordination
# ------------

SELECT Com IF (*-1 CC BARRIER NPNH LINK *-1C Com BARRIER WORD);
	## Das ráhkaduvvo vuojahat gátti guvlui nu galliin čuolddain 
	## (dahje oar-julggiin) go lea vuogas.



# =========== #
  CONSTRAINTS
# =========== #

# Locative and comitative - Disambiguation based upon coordination
# ================================================================
 # The idea is to let the other rules work first, and then let the
 # coordination follow suit.


SELECT (Sg Loc) IF (1 CC)(*2C Loc BARRIER NPNH);
	## Dan čuolddas dahje oar-juolggis ráhkadišgoahtá olmmoš doarrása.

SELECT (Sg Loc) IF (*-1 CC BARRIER NPNH LINK -1C Loc); 
	## Go 110a:s lea sáhka sápmelaččaid "ealáhusaid" sihkkarastimis
	## ja ovdánahttimis,...


SELECT (Pl Loc) IF (*-1 CC BARRIER NPNH LINK -1 Loc LINK NOT 0 Com);
	## Seamma olmmoš áimmuhuššá máŋga dáin doaimmain dahje rollain.

SELECT (Pl Loc) IF (1 CC)(*2 Loc BARRIER NPNH LINK NOT 0 Com);
	## Son lea ođđa mediain ja skuvllas.

SELECT (Sg Com) IF (*-1 CC BARRIER NPNH LINK -1 Com LINK NOT 0 Loc);
	## Ii lean dárbu dábálaš eanavuovdimii, oktan opmodatregistreremiin
	## ja čuoldimiin.

SELECT (Sg Com) IF (1 CC)(*2 Com BARRIER NPNH LINK NOT 0 Loc);
	## Fága galgá ovddiidit ovttasbarggus Sámedikkiin ja sámi dearvvašvuođa-
	## ja sosiálabargiiguin.


# Essive
# ======

SELECT Ess IF (-1C Nom)(0 Nom);
	## Gáibadussan lea ahte olbmos lea sámegiella ruovttogiellan.

SELECT Ess IF (*-1 Acc BARRIER NPNHA LINK *-1 OPRED-VERB BARRIER NPNHA);
	## Máret anii Máhte jallan.

SELECT Ess IF (1 CC)(*2C Ess BARRIER NOT-NP);
	## Ovdal go kristtalašvuohta bođii Sápmái lei noaiddis erenoamáš 
	## rolla oaidnilin ja dálkkodeaddjin.


# Late case rules (after other case rules have worked). 
# ===================================================== 
 # Case rules that make crucial reference to certain verb forms are placed after
 # the verb rule block.

SELECT (Num Nom) IF (*-1 CC BARRIER NOT-ADV-PCLE LINK *-1 (Num Nom) BARRIER NPNH);
	## Čieža, ja vel moatte uhca guoláža.

REMOVE (Dem Gen) IF (0 ("dat"))(*1C (N Acc) BARRIER NPNH);
	## Naba daid stuora dorskiid, gosa daid bidjet?

SELECT Nom IF (*1 Loc BARRIER NPNH LINK 1 COPULAS LINK *1 (A Nom) BARRIER 
	NOT-ADV-PCLE);
	## ...ahte boazosápmelaččaid luonddoriggodagaid ávkkástallan orohagain 
	## lei ovdal viidát go dat mii lea addon sidjiide.

SELECT Gen IF (NOT *-1 V-TRANS-ACT BARRIER NOT-ADV-PCLE)(0 Dem)
	(*1C (N Gen) OR (N Sg Ill) OR (N Sg Loc) OR (N Pl Com)
	BARRIER NOT-ADJ LINK NOT 0 Cmpnd);
	## Mii eat galgga dán digaštallamii searvat dán oktavuođas.

SELECT Acc IF (0 Indef)(1 (A Acc));
	## Dasgo mun in leat goassege borran maidege eahpebasiid.

REMOVE Gen IF (1 (Pers Gen));
	## Dii huksebehtet hávdemuittuid profehtaide geaid din máddarat godde! 

SELECT Gen IF (*-1 (Indef Acc) BARRIER NOT-ADV)(0 Pers);
	## Oktan suinna sii russiinávlejedje guokte rievvára, nuppi su olgeš ja 
	## nuppi fas su gurut beallái.



# ============================= #
# Disambiguating verbs - part 2 #
# ============================= #

# Finite forms #
# ============ #
   CONSTRAINTS #
# ============ #

# Finite or not
# =============

SELECT VFIN IF (*-1 BOS OR CS BARRIER VERB)(0 INF-VERB)
	(NOT 0 ("manne") LINK *-1 BOS BARRIER NOT-CC)
	(*1 Inf BARRIER VFIN OR CS OR CP);
	## Áigugo fas skuvlii álgit? 
	## Sáhttit maid namahit ahte ...
 # Looks brave but works!

SELECT VFIN IF (NOT *-1C VFIN BARRIER CS OR CP)(-1 Loc)(NOT -1 (Interr Sg))
	(0 LOC-VERB); 
	## Nuppi lađđasis čuovvu, ahte...

REMOVE VFIN IF (NOT -2 BOS OR CLB)(-1 Inf)(NOT 0 Qst);
	## Lága juksanmearrin lea váikkuhit sihkkarastit álbmogii 
	## ovttadássásaš dearvvašvuođaveahki.
 # Neg conditions because of:
	## Suhpodit daddjojuvvo dat go dušše nođiid váldá olmmoš eret...
	## Mearrideaddjin dasa ferte danin leat addojuvvogo... 

REMOVE VFIN IF (*-1 Nom BARRIER NOT-ADV-PCLE OR CLB LINK *-1C VFIN 
	BARRIER NOT-ADV-PCLE OR CP OR CLB);
	## Lea dárbu nannet doaimmaid mat ovddidit sámi dutkama.

REMOVE VFIN IF (*-1 PrfPrc BARRIER HNOUN OR (Pron Nom) OR VERB OR CC OR CS OR CP 
	OR CLB LINK *-1 ("leat") BARRIER NOT-ADV-PCLE);
  	## Sin badjel geat orrot jápmima eatnamis ja suoivanis, lea ihtán čuovggas.
	## Gii lea cuigen din báhtarit boahttevaš moarreduomus?

REMOVE VFIN IF (*-1C VFIN BARRIER CC OR CS OR CP OR CLB OR PUNCT LINK NOT *-1 Interr
	BARRIER NOT-NP)
	(*1 CC OR CLB OR CS OR CP OR EOS BARRIER NP-HEAD-NOM OR NP-HEAD-ACC);
	## Muhto sáđuid son boaldá čáskatmeahttun dolas.

REMOVE VFIN IF (NOT *-1 Interr BARRIER VERB)
	(*1C VFIN BARRIER CLB OR CC OR CS OR CP OR PUNCT OR go OR ("dego") OR 
	("galle" Num) LINK NOT 0 Qst)(NOT *1 Neg BARRIER VFIN)
	(NOT *1 VFIN BARRIER CLB OR CC OR CS OR CP LINK 1 go);
	## Sámegillii gálbet universitehta lea šaddan realitehta.

REMOVE VFIN IF (*1 Neg BARRIER NOT-ADV-PCLE OR CLB LINK *1 PrfPrc OR ConNeg 
	BARRIER VERB OR CLB OR CC OR CS OR CP);
	## Lágas ii duššaduvvo oktage bustávaš dahje čuoggáš, ovdal go...

REMOVE VFIN IF (-1 ("nu"))(*1 ("go" Pcle) BARRIER NOT-NA); 
	## No longer needed?

REMOVE Inf IF (-1 ("nu"))(*1 ("go" Pcle) BARRIER NOT-NA); 
	## No longer needed?

REMOVE ConNeg IF (*-1 PrfPrc BARRIER NOT-ADV-PCLE);
	## Go geavahus ii duođaštuvvon leat álggahuvvon ovdal go... 


# Indicative Negative
# -------------------
  # Unique are Sg1, Sg3, Du1, Du2, Du3, Pl1, Pl2, Pl3
  # Sg2 "it" = ABBR.

# Select Neg Sg2 instead of ABBR
REMOVE ABBR IF (*-1 ConNeg OR PrfPrc)(0 Neg); ## Dáppe don leamaš it.
REMOVE ABBR IF (0 Neg)(*1 ConNeg OR PrfPrc);  ## Don it leat dál.


# Infinitive
# ----------
SELECT Inf IF (*-2 INF-VERB BARRIER VFIN OR NP-HEAD-NOM OR CS)(-1 Po)
	(NOT *-1 CP BARRIER CS OR VFIN);
	## Dearvvašvuođabargit galget divššohasa agi, silolaš dilálašvuođa,
	## láddama ja vásáhusduogáža ektui láhčit dilálašvuođaid buoremus
	## lági mielde.

SELECT Inf IF (NOT *-2 CC BARRIER NPNH LINK -1 Nom)(-1C (Sg Nom))
	(NOT 0 V-PL3 LINK *1 NP-HEAD-PL-NOM BARRIER NPNHA);
	## Mánáin lea vuoigatvuohta oažžut dearvvašvuođaiskkadeami.
	## Mus ii leat báljo dilli vuolgit.

SELECT Inf IF (-1 Acc)(NOT -1 (Pl Nom))(*1 Acc BARRIER NPNH);
SELECT Inf IF (-1 Acc)(NOT -1 Interr)(*1 Ill BARRIER NPNH);
	## Son vuolggahii bálvaleaddjiidis rávkat bovdejuvvon gussiid heajaide.
	## ...leago dieđiheamis doarvái vuođđu ášši váldit meannudeapmái.

SELECT Inf IF (-1 COMMA LINK *-1 (Pron Interr) LINK *-1 INF-VERB BARRIER VERB);
	## Dalle fertejit buotlágan ávnnaslaš eavttut maid dárbbaša vai
	## duođalaččat galgá sáhttit kultuvrras dikšut, gullat dása.

SELECT Inf IF (-1 COMMA LINK *-1 (Pron Interr) LINK *-1 (Pron Interr) 
	LINK *-1 INF-VERB BARRIER VERB);
	## Eai galgga sii geat gullet dakkáraš minoritehtaide ovttas eará 
	## láhttuiguin mat gullet sin jovkui, manahit vuoigatvuođaideaset dikšut 
	## iežaset kultuvra, dovddastit ja geavahit iežaset oskku, dahje geavahit 
	## iežaset giela.

SELECT Inf IF (NOT *-1 (Pl Nom) BARRIER NOT-ADV-PCLE)
	(1 LEAT-FIN-NOT-IMP)(NOT 1 Qst);
	## Dáidda heŋgejuvvojit gurpasat, ja maid dál de guottehit lea áigumin.

SELECT Inf IF (*-1 COPULAS BARRIER NOT-ADV-PCLE LINK *-1 (A Sg Nom) BARRIER
	NOT-ADV-PCLE);
	## Dárbbašlaš lea dikšut jávrriid ovdal go lea ekonomalaččat veara bivdit.

SELECT Inf IF (*-1 (@HNOUN) BARRIER VERB)(1 PUNCT OR EOS);
	## Áigemearri váidalusa buktit.

REMOVE Inf IF (NOT *-1 VFIN BARRIER CS)(0 ("leat"))(*1 PrfPrc BARRIER VERB);
	## De leat máksán.


# Coordinated infinitives
# - - - - - - - - - - - -
SELECT Inf (*-1 COMMA BARRIER VERB OR MO LINK -1C Inf)(NOT 0 (Neg Imprt))
	(NOT *-1 (Pl Nom) BARRIER CS)(NOT *1 (Pl Nom) BARRIER CS);
	## Mun in leat boahtán gomihit, muhto ollašuhttit.
	## Son galgá stuorrut, mun fas uhccut.

SELECT Inf IF (*-1 CC BARRIER VFIN OR CS LINK *-1C Inf BARRIER WORD); 
	## Hearrá Ipmilat galggat gudnejahttit, ja áivvestassii su bálvalit.

SELECT Inf IF (*-2C Inf BARRIER VERB)(-1 CC);
	## Duođalaš nana jáhku geažil lea divššohas vuoigatvuohta biehttalit
	## vuostáváldimis vara dahje varabuktagiid dahje biehttalit botkemis
	## nealgudeami mii lea jođus.
 
SELECT Inf IF (*-3 Interr LINK *-1 Inf BARRIER VERB)(-2 COMMA)(-1 CC)
	(NOT *1 Nom BARRIER NPNHA);
	## Divššohasbearráigeahčis lea vuoigatvuohta buktit oaiviliiddis
	## dilálašvuođaid birra mat gullet bearráigeahčči bargoviidodahkii,
	## ja evttohit konkrehtalaš buoridandoaimmaid.


# Indicative or imperative
# ========================

REMOVE Imprt IF (-1 BOS)(0 Adv)(*1 ("?") BARRIER CS); 
	## Gosa don dál?

REMOVE Imprt IF (*-1 CS OR CP BARRIER PUNCT)(NOT -1 CLB OR ("de") OR ("nu"))
	(NOT -1 CC LINK *-1 Imprt BARRIER CS);
	## Jus divššohas roasmmohuvvá dahje oažžu duođalaš váttisvuođaid,...

 # Very unlikely that an imperative should show up in an embedded clause, 
 # except in quotations (...de son celkkii: Manni eret!). But it can be 
 # preceded by an embedded clause, for example a relative clause, in which 
 # case we normally find a comma in front of the imperative.


REMOVE Imprt IF (-1 Adv)(NOT -1 ("nu") OR ("de"));
	## Dalle skáhppo buođđobiergasiid.

REMOVE Imprt IF (-1 Imprt)(NOT 0 ConNeg); 
	## Geahča lagat Hálddašanjoavkku čielggadeami.

SELECT Imprt IF (-2C (N Nom) LINK *-1 BOS OR PUNCT BARRIER NPNH)(-1 COMMA)
	(NOT 0 MOD-ASP OR Pass)(NOT 0 (N Sg Nom) LINK *1 V-SG3 BARRIER CS OR VFIN)
	(NOT 0 (N Pl Nom) LINK *1 V-PL3 BARRIER CS OR VFIN)(NOT *1 ("?"));
	## Ustiban, daga dan man várás don leat boahtán!.

SELECT Imprt IF (-2C (N Nom) LINK *-1 COMMA BARRIER NPNH LINK -1 (N Nom) 
	LINK *-1 BOS OR PUNCT BARRIER NPNH)(-1 COMMA)
	(NOT 0 AUX OR Pass OR (N Nom))(NOT *1 ("?"));
	## Hearra, Davveda bárdni, árpmit mu!

SELECT Imprt IF (*-2 ("dadjat") OR ("cealkit") OR ("vástidit") BARRIER VERB)
	(-1 PUNCT)(NOT 0 Pass)(NOT 0 MOD-ASP);
	## Jesus celkkii sutnje: "Várut dadjamis maidege geasage dan birra!" 

REMOVE Imprt IF (-1 Interr OR CP OR CS)(NOT -1 ("de"));
	## Rohkadallet sin ovddas geat doarrádallet din.

REMOVE Imprt IF (*-1 VERB BARRIER NOT-ADV OR CLB LINK NOT 0 Neg);
	## Eamiálbmotriektái gullá maiddái muhtun muddui njulget vahágiid,...

SELECT (Imprt Sg2) IF (*-1 BOS BARRIER NOT-CC)(NOT 0 ("manne"))(NOT 0 Gen LINK 1 N)
	(NOT 1 EOS)(NOT *1 VFIN BARRIER CS);
	## Várut beatnagis!


# Some imperatives according to person and number.
# ------------------------------------------------

REMOVE (Imprt Sg2) IF (0 V-SG2)(NOT *1 COMMA OR CC BARRIER VFIN LINK 1C (Imprt Sg2));
	## Don gii gaikkodat tempela ja hukset dan fas ođđasit golmma beaivvis!

SELECT (Imprt Sg2) IF (NOT *-1 V-SG2 BARRIER VFIN)(*-1 PUNCT OR CLB 
	BARRIER NOT-ADV-PCLE)(1 (PxSg2));
	## Nahket miehkát fas dohppii.

REMOVE (V Ind Prs Sg3) IF (*-1 BOS BARRIER NOT-PCLE)(0 (V Imprt Prs Sg2))
	(NOT 0 A OR Foc)(NOT 1 ("."));
 	## Na attes munnje daid.

REMOVE (Imprt Du2) IF (*1 (Pron Pers Nom) BARRIER CS OR CP OR PUNCT LINK NOT 0 Du2);
REMOVE (Imprt Du2) IF (*1 Sg2 BARRIER CS OR CP OR PUNCT);
	## Dat guhte goargü eará báikki, son lea suola ja rievvár.
	## Velá okta váilu dus.

SELECT (Imprt Pl2) IF (*1 (PxPl2) BARRIER CC OR CS OR CP);
	## Váldet mu geassá badjelasadet.


# Imperative in coordination
# --------------------------
SELECT Imprt IF (NOT 0 V-SG3 LINK -1 COMMA LINK *-1 V-SG3 BARRIER VFIN OR CS)
	(-1 BOS OR COMMA)(*1 COMMA LINK 1 Imprt)(NOT 0 (A Attr) LINK 1 N);
	## Bohtet, goddot su, de mii oažžut su árbbi.

SELECT Imprt IF (NOT *-1 VFIN-NOT-IMP BARRIER CS OR CP)(NOT 0 PrfPrc)
	(*1 CC BARRIER VFIN LINK *1C Imprt BARRIER VFIN OR CS OR CP);
	## Čoavdi daid ja bukti mu lusa!

SELECT Imprt IF (-1 COMMA OR CC)(NOT 0 Pron)(*-2 Imprt BARRIER VFIN LINK 
	NOT *-1 CP BARRIER PUNCT OR REALCLB); 
	## Mannet dal ja guorahallet dán máná hárrái!

SELECT Imprt IF (*-2C Imprt)(-1 COMMA);
	## Allet almmi bokte, dasgo dat lea Ipmila truvdnu, allet ge eatnama bokte...

SELECT (Imprt Sg2) IF (*1 CC OR COMMA BARRIER VFIN LINK *1C (Imprt Sg2)
	BARRIER VFIN OR CS OR CP);
	## Gohčo bargiid boahtit ja atte dáidda bálkká!

SELECT (Imprt Sg2) IF (*-1 CC BARRIER NOT-ADV LINK *-1C (Imprt Sg2));
	## Váldde dan mii gullá dutnje ja mana!
 
SELECT (Imprt Pl2) IF (*1 CC OR COMMA BARRIER VFIN LINK *1C (Imprt Pl2)
	BARRIER VFIN OR CS OR CP);
	## Čatnet su gieđaid ja julggiid ja bálkestehket su olggobeallái..
 
REMOVE Imprt IF (*-1 CC BARRIER NOT-ADV-PCLE OR REALCLB LINK NOT *-1 Imprt)
	(NOT -1 CC LINK -1 BOS);
	## Sus lea goaivu gieđas ja áigu buhtistit gortniidis.


# Rule that finds a coordinated Imprt across a relative clause:
# - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
SELECT (Imprt Pl2) IF (-1 CC OR COMMA)(*-2 VFIN BARRIER CS OR CP OR CONTRA 
	LINK *-1 (Pron Interr) BARRIER CS LINK *-1 (Imprt Pl2) 
	BARRIER VFIN OR CS OR CP);
	## Buressivdnidehket sin geat garrudit din, dahket buriid sidjiide
	## geat vašuhit din, ja rohkadallet sin ovddas geat doarrádallet din.


# Negative imperative
# -------------------
  # Neg Imprt Pl2 = allet V Inf = Prs Pl1 = Prs Pl3

SELECT (Neg Imprt Pl2) IF (*1 ConNeg BARRIER Neg); 
	## Allet ge gátte ahte ...



# Verbs according to person and number
# ====================================

# First person singular - Sg1
# ---------------------------
 # Prs Sg1 = PtcPrf (odd-syllables), and Prs Sg1 = V Actio (even-syllables)
 # Past Sg1 is unique.

SELECT Sg1 IF (0 LEAN) (*1 PrfPrc BARRIER CS);
	## Dál lean cealkán dan dudnuide.

SELECT Sg1 IF (NOT *-1 VFIN BARRIER CS OR PUNCT OR REALCLB)(0 LEAN);
	## Jesus celkkii: "Oanehis áiggi lean velá din luhtte."

SELECT V-SG1 IF (NOT *-1 VFIN OR PrfPrc BARRIER CS OR REALCLB)(NOT 0 Pass OR N)
	(NOT 0 (N Nom) LINK 0 LEX-N)(NOT *1 VFIN BARRIER CS);
	## Oađidettiin gullen olbmo boahtimin.

SELECT V-SG1 IF (*-1 CS BARRIER VFIN LINK -1 BOS)(*1 COMMA BARRIER VFIN);
	## Ovdal go čielggadan dan lagabui,...

SELECT V-SG1 IF (*-1 CC BARRIER VERB LINK *-1C V-SG1 BARRIER VERB)(NOT 0 Pass);
	## Das maŋŋá dajan juoidá álbmotrievtti mearkkašumi birra, ja geavahan
	## das veahkkin ođđaseamos riektegáldu.

REMOVE (V Sg1) IF (*-1 CC BARRIER VERB LINK NOT *-1 V-SG1);
	## Olbmot ledje ija dolastallan ja oggon.

REMOVE V-SG1 IF (0 PrfPrc OR Actio)(*1C VFIN BARRIER CC OR CLB OR CS OR CP);
	## Dálá fiinnuhan riektejurddašanvuogi mielde ii eahpiduvvo ahte 
	## dán livččii galgan dahkat.


# Second person singular - Sg2
# ----------------------------
 # Sg2 = Inf (even syllables, copula)
 # Note the homonymy with derived verbs (jearat = jearadit + ...
 # Prt Sg2 = Prs Pl3 = Imprt Prs Pl2 (even syll)    vulget
 # Prt Sg2 = Prs Pl3 = Prs Pl1 = Inf (odd syll)   muitalit
 # Prt Sg2 = Prs Pl3                 (contracted) fertejit


SELECT V-SG2 IF (*-2C V-SG2 BARRIER VFIN OR CS OR CP OR PUNCT OR REALCLB)
	(-1 Interr)(NOT -1 (Pl Nom));
	## Don leat garra olmmái gii láddjet doppe gosa it leat gilván.

SELECT Prt IF (*1 IKTE BARRIER VFIN OR CS OR CP);
	## Vulget go ikte?

SELECT V-SG2 IF (*-1 CC BARRIER VFIN LINK *-1C V-SG2 BARRIER VFIN OR Inf OR CS);
	## Dasgo sániidat mielde don dahkkojuvvot friddjan, ja sániidat mielde
	## dubmejuvvot.

SELECT V-SG2 IF (*-1 CC BARRIER NOT-ADV LINK *-1 CP LINK *-1C V-SG2 BARRIER
	NOT-ADV-PCLE);
	## Don oainnát mo olbmot bahkkejit du birra, ja de jearat vel...

SELECT (V Sg2) IF (*-2C (V Sg2) BARRIER VFIN)(-1 CS)(NOT *1 PxPl3);
	## Don it beasa doppe olggos ovdal go leat máksán.

REMOVE ("leat" Pl1) IF (*1 (Sg Nom) BARRIER NOT-ADV-PCLE LINK NOT 0 NUMERALS);
	## Leatgo árgi bajándálkkis?
	## Leat go árgi bajándálkkis?

REMOVE ("leat" Pl3) IF (NOT *-1 (Pl Nom) OR (Pl3 Nom) BARRIER NOT-ADV-PCLE)
	(*1 (Sg Nom) BARRIER NOT-ADV-PCLE OR CLB LINK NOT 0 NUMERALS 
	LINK NOT *1 (Pl Nom) OR (Pl3 Nom) BARRIER NPNHA)
	(NOT *1 (Sg Nom) BARRIER NOT-ADV-PCLE OR CLB LINK 1 CC LINK *1 Nom 
	BARRIER NPNH);
	## Leatgo árgi bajándálkkis?
	## Leat go árgi bajándálkkis?

REMOVE (V Sg2) IF (1 CC)(2 VFIN)(NOT 2 Sg2);
	## Buođđomuoraid maid ráhkadit olbmot gárvvisin dassá go deatnu
	## leahkkasa, čuolddaid čohket ja ráhkadit oar-julggiid.

REMOVE (V Sg2) IF (0 COPULAS)(*1 (Pl Nom) BARRIER NOT-ADV-PCLE);
	## Leatgo doppe olu gávppit?

REMOVE (V Sg2) IF (*-1 (Num Nom) BARRIER NPNH);
	## Gitta guhtta hearggi sáhttet leat ovtta ráiddus.


# Third person singular - Sg3
# ---------------------------
 # Prs Sg3 = ConNeg (even)
 # Prs Sg3 = ConNeg = Imprt Sg2 (contracted)
 # Prs Sg3 = VGen
 # Prt Sg3 = Sg Ill for certain words!!
 # Prt Sg3 = Imprt Prs Sg3 (leai)
 # leat Prs Sg3 Foc = leat Prs Du3 (leaba) 

SELECT V-SG3 IF (*-1 (Sg Nom) BARRIER NOT-ADV-PCLE)
	(*1 (Neg Sg3) BARRIER VFIN LINK 1 ("ge"));
	## Rakel čierru mánáidis dihtii ii ge beroš jeđđehusas.

SELECT V-SG3 IF (*-1 (Sg Nom) BARRIER NOT-ADV-PCLE)
	(*1 (Neg Sg3 Foc) BARRIER VFIN);
	## Rakel čierru mánáidis dihtii iige beroš jeđđehusas.

REMOVE (V Sg3) IF (0 (Sg Nom))(*1C (V Sg3) BARRIER CLB OR CC OR CS OR CP OR go
	LINK NOT 1 ("go" Pcle));
	## Man uhccán osku dus lea!

SELECT V-SG3 IF (*-1 (Sg Nom) BARRIER NOT-ADV-PCLE)(NOT 0 Inf OR (N Pl Nom))
	(NOT 0 Ill LINK *-1 Gen BARRIER NOT-ADV-PCLE);
	## ...ja de olmmoš suohppu fávleoalggi nu guhkás vulos go...

SELECT V-SG3 IF (*-1 (Sg Nom) BARRIER Nom OR CC OR CLB OR CS OR CP)(1 CC);
	##

SELECT V-SG3 IF (*-1 (Pron Sg3 Nom) BARRIER NOT-ADV-PCLE)
	(NOT 0 Du3 LINK *-1 ("dat" Pl3 Nom) BARRIER NOT-ADV-PCLE) ;
	## Dat áigu leat ruovttus.

SELECT V-SG3 IF (*-1 Loc BARRIER NOT-ADV-PCLE)(*1 NP-HEAD-SG-NOM BARRIER NPNHA);
	## Dát leat sii geat sirrejit olbmuid; sii leat máilmmálaččat 
	## ja sis váilu Vuoigŋa.

SELECT V-SG3 IF (*-1 ("gii" Pron Interr Sg Loc) BARRIER CS OR CP); 
	## Sus geas ii leat, váldojuvvo velá dat ge eret mii sus lea.

SELECT V-SG3 IF (*-1 BOS OR CS BARRIER Nom)(NOT 0 Adv)
	(*1 (Sg3 Nom) OR (Pron Sg Nom) BARRIER NOT-ADV-PCLE LINK NOT 0 Refl);
	## ...ahte vuoigatvuhtii automáhtalaččat gullá dat mii guorrana...

SELECT (V Sg3) IF (*-1 BOS OR CS BARRIER Nom)(NOT 0 Adv OR Attr)
	(*1 (N Sg Nom) OR (A Sg Nom) BARRIER NPNHA);
	## Dán guovtti báhkkoma alde orru oppa láhka ja profehtat.

SELECT V-SG3 IF (-1 CLB)(NOT 0 Adv)(*1 (N Sg Nom) BARRIER NPNH);
	## Earáide fas, geat eai máhte sámegiela, orru vástádus eahpečielggas. 

SELECT V-SG3 IF (*-1 (N Sg Nom) BARRIER NOT-ADV-PCLE)(0 (N Sg Ill))
	(*1 Acc BARRIER NPNH);
	## Dalle isit gohčui bálvaleaddji fas lusas.

SELECT V-SG3 IF (NOT *-1 V-SG3)(*1 CC BARRIER V-SG3 LINK 1 V-SG3);
	## De gohčui sisa oskkolaččaid oktan leaskkaiguin ja čájehii sidjiide...

REMOVE Ill IF (NOT *-1 ("seammá") OR ("eará") BARRIER NPNH)(1 ("go" Pcle));
	## Gos lei riegádan, ja máhtii go dárogiela?

REMOVE (V Prt Pl3) IF (NOT *-1 (Pl Nom) OR (Pl3 Nom) BARRIER NOT-ADV-PCLE)
	(0 AUX)(0 (V Prs Sg3));
	## Giellabargguid ferte viidásetfievrridit.


# Infinitive and clausal subject
# - - - - - - - - - - - - - - - -
SELECT V-SG3 IF (NOT *-1 Nom BARRIER CS OR REALCLB)(NOT 0 AUX)
	(NOT 0 (Pl Nom) LINK *1 V-PL3 BARRIER NOT-ADV-PCLE)
	(NOT 0 INF-VERB LINK 0 (Ind Sg2))(*1 Inf BARRIER NOT-ADV-PCLE);
	## Eamiálbmotrievtti guovddášosiide gullá maiddái sihkkarastit...

SELECT V-SG3 IF (*1 ("ahte") BARRIER NOT-ADV-PCLE);
	## Dearvvašvuođabargiidlága § 26 olis čuovvu ahte...
 # Verbs with nominal or pronominal subjects are picked earlier.


# Rules that look backwards for a subject across a relative clause:
# - - - - - - - - - - - - - - - - - - - - - - - - - - - -
SELECT V-SG3 IF (-1 CLB LINK *-1 (Pron Interr) OR MO BARRIER CS LINK *-1 (N Sg Nom) 
	OR (Pron Sg3 Nom) BARRIER N OR VFIN LINK NOT *-1 V-SG3 BARRIER CS OR CP 
	OR CLB)(NOT 0 Qst);
	## Dat guhte ii čohkke muinna, biđge. 

SELECT V-SG3 IF (*-1 VFIN BARRIER Nom OR CS OR CP LINK *-1 (Pron Interr)
	BARRIER CS LINK *-1 (N Sg Nom) OR (Pron Sg3 Nom) BARRIER VFIN OR Nom 
	LINK NOT *-1 V-SG3 BARRIER CS OR CP)(NOT 0 Loc OR Ill OR A OR Adv OR Qst);
	## Son gii lea váldán Jesusa gitta doalvu su dál Kaifasa lusa.


# Coordinated Sg3 verbs
# - - - - - - - - - - -
SELECT V-SG3 IF (*-1 CC BARRIER NOT-ADV-PCLE LINK *-1 V-SG3 BARRIER VFIN OR CC)
	(NOT 0 Adv OR A)(NOT 0 N LINK *-1 CC BARRIER NPNH LINK -1 N);
	## Jus divššohas roasmmohuvvá dahje oažžu duođalaš váttisvuođaid...

SELECT V-SG3 IF (*-2 V-SG3 BARRIER VFIN OR CC OR REALCLB)(-1 COMMA)(NOT 0 Inf);
	## Son manai su lusa, dollii su gihtii ja čuoččáldahttii su.

SELECT V-SG3 IF (NOT *-1 V-SG3 BARRIER CS OR CP OR PUNCT LINK NOT *-1 CP 
	BARRIER VFIN)(1 CC)(*2 V-SG3 BARRIER CS OR CP OR VFIN)
	(NOT 0 N LINK 1 CC LINK *1 N BARRIER NPNH);
	## ...mo daid beliid berre dahje galgá áimmahuššat.

SELECT V-SG3 IF (-1 CC LINK *-1 V-SG3 BARRIER CC OR CLB OR CS OR CP OR VFIN)
	(NOT *1 V-SG3 BARRIER CC OR CLB OR CP OR CS)(NOT 0 (A Attr) LINK 1 N);
	## Dalle váldá lievssa ja bálkesta dan bodnái ja suohppu gáddeoalggi ja 
	## reasta fávleoalggis mohki rádjái.
 

# leage ("leat" or "leahki" N Sg Gen)
# - - - - - - - - - - - - - - - - - -
REMOVE ("leahki" N Sg Gen) IF (0 ("leat" Prs Sg3 Foc))(*1 (Sg Nom) BARRIER NPNH);
	## ...ja leage stuorradiggi mii ferte mearrádusa dulkot ja atnit.


# leai (Imprt Prs Sg3 and Ind Prt Sg3)
# - - - - - - - - - - - - - - - - - - 
SELECT Ind IF (*-1 N OR Pron OR Num BARRIER NOT-ADV-PCLE)(0 LEAI);
	## Dain leai seammalágan dálvesiidaortnet.
	## Guovvamánu 1. b. leai áigemearri.


# Not (V Sg3)
# - - - - - -
REMOVE V-SG3 IF (*1 NP-HEAD-PL-NOM BARRIER NOT-ADV-PCLE);
	## De leaba spagát giddalagaid bajil.

REMOVE V-SG3 IF (*-1 VFIN BARRIER CS OR CP)(0 (Sg Ill))(NOT 1 ("go" Pcle));
	## Sámekommišuvnna evttohus lei boaris ja báidnon dálá oidnui.


# First person dual - Du1
# -----------------------
 # Prs Du1 = Prt Pl3 (even syllable verbs); also similar to Prs Sg3 of other verbs.

SELECT (V Du1) IF (*-1 CC BARRIER VFIN LINK *-1C (V Du1) BARRIER VFIN);
	## Moai bohte su lusa ja orrugohte su luhtte.

REMOVE (V Du1) IF (NOT *-1 MOAI)(NOT *1 MOAI);
	## De bohte ruđat.
	## Nieiddat jerre.


# Second person dual - Du2
# ------------------------

# Rules for leahppi = ("leahppi" N Sg Nom)

SELECT Du2 IF (*-1 Du2)(0 ("leat" V Ind Prs Du2));
	## Manni muitalit maid leahppi oaidnán ja gullan.

SELECT Du2 IF (0 ("leat" V Ind Prs Du2))(*1 PrfPrc BARRIER NOT-ADV);
	## Gean nammii leahppi dahkan dan?


# Third person dual - Du3
# -----------------------
 # Du3 = PrsPrc Clt = Imprt Prs Du2 Clt
 # Du3 = Ind Prs Sg3 Clt "leaba, ferteba leat"

 # Most Du3 rules moved to earlier cycle, in order to hit before Sg3 + Foc "leaba".

SELECT V-DU3 IF (*-1 (Pl Nom) OR ("dat" Pl3 Nom) BARRIER NOT-ADV-PCLE);
	## Ja duot eará oskuvieljat leaba searvegotti áirasat.

REMOVE (V Sg3 Foc) IF (-1 CS OR CLB LINK *-1 (V Du3) BARRIER VFIN)(0 (V Du3));
	## Spahkágeažit šaddaba ruossalagaid geaigut bajás, go leaba boalo-
	## huvvon oktii.

REMOVE (V Sg3 Foc) IF (*-1 (Pl Nom) BARRIER CC OR CLB OR CS OR CP OR (Sg Nom))
	(0 (V Du3));
	## Geažit guovtte dakkár čoavjevuolisgorrojuvvoba oktii.

 
# First person plural - Pl1
# -------------------------
 # Pl1 = Inf for all stem classes
 # Pl1 = Inf = Pl3 = Prt Sg2 for odd syll
 # Pl1 = Inf = Pl3 = Sg2 = ConNeg (copula)

SELECT (V Pl1) IF (*-1 CC BARRIER NOT-ADV LINK *-1C (V Pl1) BARRIER VFIN OR Inf 
	OR CS);
	## Mii bidjat vuođđun luonddurievtti oainnuid ja vuordnut... 

SELECT V-PL1 IF (*-2C (V Pl1) BARRIER VFIN OR CS)(-1 Interr)(NOT -1 (Pl Nom));
	## Mii sárdnidit dan maid diehtit. 

SELECT (V Pl1) IF (*-2C (V Pl1) BARRIER VFIN OR CS)(-1 CS);
	## Eat ge vuoitte maidege jos borrat.

REMOVE (V Pl1) IF (NOT -2 (V Pl1))(-1 CC);
	## Eamiálbmogiid luondduriggodagat leat leamaš ja leat duoguštuvvon.

REMOVE (V Pl1) IF (*-1 (V Pl3) BARRIER CC OR CS OR CP);
	## Bártnit ledje mannán meahccái ozažit meahcce-elliid.

SELECT V-PL1 IF (*-1 Nom BARRIER NOT-ADV-PCLE LINK *-1 CC BARRIER NPNH
	LINK -1 (Pers Sg1 Nom) OR (Pers Du1 Nom) OR (Pers Pl1 Nom));
	## Mun ja Biera váhnemat leat boahtán.
 # Pl1 if a first person pronoun is coordinated with something else.


# Second person plural - Pl2
# --------------------------
 # Imprt Pl2 = Ind Prt Sg2 (even syllable verbs)
 # Imprt Pl2 = Imprt Pl1 (verbs ending in -ut)

SELECT (V Pl2) IF (*-2C (V Pl2))(-1 CS);
	## Go mieđihehpet, de dadjet dušše: Juo!

SELECT (V Pl2) IF (-1 BOS OR PUNCT)(*1 Pl2 OR PxPl2);
	## Rohkadallet, de oažžubehtet!
	## Gáidet eret mu luhtte, dii verrošeaddjit! 
	## Bissot olisin, nugo almmálaš Áhččádet ge lea olis.

SELECT (V Pl2) IF (*-1 CC BARRIER NOT-ADV LINK *-1C (V Pl2) BARRIER VFIN OR CS);
	## Váldet ja borret!


# Third person plural - Pl3
# -------------------------
 # Prs Pl3 = Prt Sg2 = Prs Pl1 = Inf (odd syll)   muitalit
 # Prs Pl3 = Prt Sg2 = Imprt Prs Pl2 (even syll)    vulget
 # Prs Pl3 = Prt Sg2                 (contracted) fertejit
 # Prt Pl3 = Prs Du1

# Preverbal subject
# - - - - - - - - - 

 # Single noun

SELECT V-PL3 IF (*-1 (Pl Nom) BARRIER NOT-ADV-PCLE);
	## Muhto dorskit gal leat varrasat.
	## Divššohasaid birra geat leat uminddegin dahkkon.
	## Eatnagat bohtet nuortan ja oarjin.
	## Geafit gal leat álo din luhtte.

SELECT V-PL3 IF (*-1C NP-HEAD-PL-NOM BARRIER PPRON-NOT-SII OR (N Sg Nom) OR CS)
	(NOT *-1 (V Pl3) BARRIER CS OR CP OR PUNCT);
	## Makkár vuoigatvuođat dain leat riikavuložin?
	
SELECT V-PL3 IF (*-1C (N Pl Nom) BARRIER MII-PERS OR MOAI OR Nom OR VFIN OR CLB OR CS);
	## Dárbbašlaš dieđut dakkár mediaid vuođul sáhttet čállojuvvot journálii. 


 # Pronoun

SELECT V-PL3 IF (*-1 (Pron Pl3 Nom) BARRIER MII-PERS OR MOAI OR CLB OR CS);
	## Dathan gal leat buorit guolit.

SELECT Pl3 IF (*-1 (Pron Interr Pl Nom) BARRIER CP OR CS OR VFIN);
	## Mu diehto-olbmát geat munnje leat muitalan guotteheami birra,...

SELECT Pl3 IF (*-1 (Pron Indef Pl Nom) BARRIER CP OR CS OR VFIN);
	## ...man muhtimat Norgga bajimuš oapmeriektečehpiin leat čállán. 


 # Numeral

SELECT V-PL3 IF (*-1 NUMERALS BARRIER CS OR REALCLB OR PPRON-NOT-SII OR (N Sg Nom)
	LINK 0 Nom LINK NOT *-1 (V Pl3) BARRIER CC OR CS);
	## Máŋga Davviálbmogiid fakultehta studeantta leat oasi oahpus gazzan... 

SELECT V-PL3 IF (*-1 (N Num) BARRIER NP-HEAD-NOM OR VFIN OR ConNeg OR CS OR CP 
	LINK 0 Nom);
	## Viđas sis leat juo gahččan. 


 # Pl3 verbs that follow relative clauses:

SELECT V-PL3 IF (*-2 VFIN BARRIER CS OR CP LINK *-1 (Pron Interr Pl) BARRIER CS OR 
	CP)(-1 COMMA)(NOT 0 Inf LINK *-2 Inf BARRIER VFIN);
	## Dieđut mat leat dárbbašlaččat, galget liikká addojuvvot.  

SELECT V-PL3 IF (-1 CLB LINK *-1 (Pron Interr) OR MO BARRIER CS LINK *-1 (N Pl Nom) 
	OR (Pron Pl3 Nom) BARRIER N OR VFIN LINK NOT *-1 V-PL3 BARRIER CS OR CP)
	(NOT 0 Qst);
	## Dát duovdagat gos jogat ja ádjagat, jávrrit ja eará čáhcečoahkut
	## leat, gullet daid duovdagiid eaiggádiidda.

SELECT V-PL3 IF (*-1 VFIN BARRIER Nom OR CS OR CP LINK *-1 (Pron Interr)
	BARRIER CS LINK *-1 (N Pl Nom) OR (Pron Pl3 Nom) BARRIER N OR VFIN 
	LINK NOT *-1 V-PL3 BARRIER CS OR CP)(NOT 0 Qst OR Adv);
	## Olbmot, geat oassálastet kulturdoaimmaide vásihit sosiála oassá-
	## lastima.


 # Coordinated subjects

SELECT (V Pl3) IF (*-1 Nom BARRIER NOT-ADV-PCLE LINK *-1 CC 
	BARRIER NOT-NP LINK -1 Nom);        
	## Gielda ja fylkkagielda galget plánet.


 # Coordinated Pl3 verbs

SELECT (V Pl3) IF (*-1 CC BARRIER NOT-ADV-PCLE LINK *-1C (V Pl3) BARRIER VERB OR CS);
	## Journála dieđut divvojuvvojit dahje sihkkojuvvojit njuolggadusaid mielde.

# Subordinate null subjects

SELECT (V Pl3) IF (-1 CS LINK -1C (V Pl3)) ;
	## Sii moaráhuvve go gulle dan.



# Postverbal subject
# - - - - - - - - - -

 # Single subject

SELECT V-PL3 IF (*1 (Pl Nom) BARRIER NPNHA)(NOT 0 ("leat" Imprt Pl2))
	(NOT 0 Inf LINK -1 ("mo"));
	## Ja dis leat velá buot oaivvi vuoktačalmmit lohkkojuvvon.

SELECT Pl3 IF (NOT *-1 VFIN BARRIER CS OR CP)(0C VERB)(*1 NP-HEAD-PL-NOM 
	BARRIER CS OR CP LINK NOT 0 Refl OR Interr);
	## Olggosaddin sáhttá liikká dáhpáhuvvat jos gávdnojit dasa beaktilis sivat.

SELECT V-PL3 IF (NOT *-1 INF-VERB OR ("leat") BARRIER CS OR CP)
	(*1 (Num Nom) BARRIER VFIN OR Inf OR PrfPrc OR Nom OR CLB OR CS OR CP
	LINK NOT 0 OKTA);
	## Dán guovllus orro sullii 20.000 olbmo duopmoáigodagas.

SELECT V-PL3 IF (*-1 CLB OR CC OR CS OR BOS BARRIER NOT-ADV-PCLE)
	(NOT 0 ("leat" Imprt Pl2))
	(*1 (Pl Nom) BARRIER VFIN OR Nom OR CS OR CP OR CLB);
	## ...de bohte muhtun viisát nuortan Jerusalemii ja jerre:..

SELECT (V Pl3) IF (0 INF-VERB)(*1 NP-HEAD-PL-NOM BARRIER NOT-ADV-PCLE LINK
	*1 Inf BARRIER CS OR CP);
	## Jus lea dárbu, fertejit lassidieđut vižžojuvvot.

SELECT V-PL3 IF (*-1 CLB OR CC OR CS OR BOS BARRIER NOT-ADV-PCLE)
	(*1 (Pron Pl3 Nom) BARRIER VFIN OR Nom OR CS OR CP OR CLB);
	## Jos dat ii leat vejolaš, searvvadit dat oktii geainna váimmut
	## buoremusat lávggadit.

SELECT V-PL3 IF (NOT *-1 VFIN BARRIER CS OR CP OR CC)
	(NOT -1 (N Nom))(NOT -1 CC LINK *-1 Inf BARRIER VERB)
	(*1 (PxPl3) BARRIER VFIN OR CS OR CP);
	## ...ja dasto leat massán hálddašeami iežaset guovlluid badjel. 

SELECT V-PL3 IF (NOT *-1 Nom BARRIER CS OR CONTRA)
	(NOT 0 Inf LINK *-1 ("leat") BARRIER CLB OR CC OR CS OR CP)
	(*1 (Num Nom) BARRIER V-TRANS-ACT OR CP OR CS);
	## Muhto ovtta čázis sáhttet leat máŋga buođđosaji.


 # Coordinated subjects

SELECT V-PL3 IF (NOT *-1 VFIN OR ConNeg BARRIER NOT-ADV-PCLE)
	(*1 Nom BARRIER NPNHA LINK 1 CC LINK *1 Nom BARRIER NPNH); 
	## Dá leat mu eadni ja mu vielljat.

SELECT V-PL3 IF (NOT *-1 VFIN OR ConNeg BARRIER NOT-ADV-PCLE)
	(*1 Nom BARRIER NPNHA LINK 1 COMMA LINK *1 Nom BARRIER NPNH
	LINK 1 CC OR COMMA); 
	## Bivdoearrái gullet dorski, diksu ja sáidi.


# Coordinated Pl3 verbs
# - - - - - - - - - - -

SELECT V-PL3 IF (*-1 CC OR COMMA BARRIER NOT-ADV LINK *-1C V-PL3 BARRIER VFIN OR CS);
	## Rivttiid háhkama eavttut leat heivehuvvon dahje heivehuvvojit ain.


# Rules for a special infinitive construction
# ===========================================
 # I made a separate rule for each form of the interrogative pronoun, 
 # in case different conditions are needed.

SELECT Inf IF (NOT -2 Pers OR REALCLB OR BOS)(-1 ("mii" Interr Sg Acc) OR 
	("gii" Sg Acc) LINK NOT *-1 (V Pl1) BARRIER VFIN)(NOT 0 AUX);
	## Sii eai diehtán maid vástidit sutnje.

SELECT Inf IF (NOT -3 Pers OR REALCLB OR BOS)(-2 ("mii" Interr Sg Acc) OR 
	("gii" Sg Acc) LINK NOT *-1 (V Pl1) BARRIER VFIN)(-1 ADVLCASE)
	(NOT 0 AUX);
	## Ii ge mus leat maid sutnje guossohit.

SELECT Inf IF (NOT -2 Pers OR REALCLB OR BOS)(-2 ("mii" Interr Sg Gen) OR 
	("gii" Sg Gen))(-1 Po)(NOT 0 AUX);
	## Ii Alimusrievttis leat mihkkege, man ovddas bivdit ándagassii.

SELECT Inf IF (NOT -2 Pers OR REALCLB OR BOS)(-1 ("mii" Interr Ill) OR 
	("gii" Ill))(NOT 0 AUX);
	## Máhtes ii lean šat oktage geasa dorvvastit.

SELECT Inf IF (NOT -2 Pers OR REALCLB OR BOS)(-1 ("mii" Interr Loc) OR 
	("gii" Loc))(NOT 0 AUX);
	## Son attii geafivuođastis buot dan mii sus lei mas eallit.
	## Máret ii gávdnan ovttage geas jearrat.

SELECT Inf IF (NOT -2 Pers OR REALCLB OR BOS)(-1 ("mii" Interr Com) OR ("gii" Com))
	(NOT 0 AUX);
	## Sus ii lean mihkkege mainna maksit.
	## Lei oktasaš riektegiella mainna gulahallat.


# More finite verbs
# =================

SELECT Prs IF (NOT *-1 V-IND-FIN BARRIER CS)(0 LEAT)(NOT *1 V-IND-FIN)  ;
	## De leat máksán.

REMOVE Inf IF (-2 CLB) (-1 CS); 
	## Dušše, juos leat doarvái...

REMOVE Inf IF (*-1 (Pron Interr) BARRIER VFIN)(NOT 1C VFIN)
	(NOT -1 PUNCT LINK 2 PUNCT);
	## ...maid Norgga bealde gohčodit "sámi guovddášguovlun".

REMOVE Inf IF (-1 Inf) ; 
	## No hits any more?


# Passive
# =======
REMOVE Pass IF (*1C Acc BARRIER VERB OR CLB OR CC OR CS LINK NOT 0 Interr);
	## Son han ii rihkkon dušše sábbáha.

REMOVE Pass IF (*-1 Neg BARRIER VERB)(0 ACT-PRFP)(NOT *1 N BARRIER NPNH);
	## Olbmot eai orron čadnon dušše iežaset mearragáddái bivdit.


# Non-finite verb forms #
# ===================== #

# ============== #
    CONSTRAINTS  #
# ============== #


# Infinitive
# ----------

REMOVE VFIN IF (*-1C VFIN BARRIER NOT-ADV-PCLE OR CLB LINK NOT *-1 CP BARRIER VFIN);
	## Journálaovddasvástádus mearkkaša maiddái árvvoštallat divvun-
	## gáibádusaid.
	## Ehpet ge gallen mu.

REMOVE VFIN IF (*-1 VFIN-NOT-AUX OR ConNeg OR PrfPrc BARRIER MII-PERS 
	OR SII OR CP OR CS OR CC OR CLB)(0 Inf)(NOT 0 LEAT);
	## Dát guoská bargguide ásahit mastera ja bachelora.
	## Universitehta oaidná deaŧalažžan doarjut doaimmaid mat...

REMOVE (N Pl Nom) IF (*-1 VFIN-NOT-AUX OR ConNeg OR PrfPrc 
	BARRIER MII-PERS OR SII OR CS OR CLB)(0 Inf)(NOT 0 LEAT);
	## Maid mearkkaša čuovvut Jesusa.
	## Dat láhčá vejolašvuođaid oassálastit eamiálbmotkonferánssain.

REMOVE (V Ind Prs ConNeg) IF (*-1 VFIN-NOT-AUX OR ConNeg OR PrfPrc
	BARRIER MII-PERS OR SII OR CS OR CLB)(0 Inf)(NOT 0 LEAT);
	## Eai leamaš váttisvuođat soahpat eaiggádiiguin.

SELECT Inf IF (*-1 Inf BARRIER VERB);
	## Dáhtut go munno rávkat dola álmmis vulos loahpahit sin?

SELECT Inf IF (*-2 ("leat") BARRIER VERB)(-1 Ess);
	## ...lea Sámedikkiin dáhpin geavahit... 

SELECT Inf IF (-1 ("leat"))(1 ("dego"));
	##

SELECT Inf IF (-1 ("mo"));
	## Dakkár doaimmat berrejit ráhkadit njuolggadusaid, mo gieđahallat
 	## journálaid.


# Infinitive after predicative
# - - - - - - - - - - - - - - -
 # There are two rules in the beginning, conflate when I see the effect.
 # The barrier may be too weak.

SELECT Inf IF (-1 A BARRIER VFIN OR CP LINK *-1 ("leat") OR ("šaddat") 
	BARRIER NOT-ADV)(NOT 0 (N Pl Nom));
	## Eará guovlluin lei vejolaš nannet dárogiela.

SELECT Inf IF (*-1 N BARRIER VFIN OR CS OR CP LINK *-1 ("leat") OR ("šaddat") 
	BARRIER NPNHAI)(NOT 0 (N Pl Nom));
        ## Jos leat jaska, de lea dárbu singuin gulahaddat.


# Infinitive in coordination
# - - - - - - - - - - - - - -

SELECT Inf IF (*-2 Inf BARRIER VERB)(-1 COMMA)(*1 CC BARRIER VERB LINK 1 Inf);
	## No longer needed?

SELECT Inf IF (*-2 Inf BARRIER VFIN OR CC OR CP)(-1 CC)(*1 Acc BARRIER NPNHAII 
	LINK 1 NP-BORDER);
	## Son lea vuolggahan mu gulahit friddjavuođa fáŋggaide ja oainnu
	## čalmmehemiide, friddjan dahkat sordojuvvon olbmuid ja sárdnidit
	## Hearrá árpmu jagi.

SELECT Inf IF (*-1 CC BARRIER NOT-ADV-PCLE LINK *-1 Inf BARRIER VERB); 
	## No longer needed?

SELECT Inf IF (1 CC)(2C Inf); 
	## Golbma nuora ledje mielde golmma vahkkosaš gieddebarggus Svalbárddas, 
	## man ulbmilin lei lohkat ja juogustit bohccuid, ja iskat ráppiid 
	## Adventdalenis.
	

# Present Participle
# ------------------
 # PrsPrc = Imprt Prs Du2 (even-syll)
 # PrsPrc = V+N+Actor, and often LEX-N.

 # An example: Njuolggadusat divššohasovddasvástideaddji doaktára birra.
 #				N Sg Gen OR V PrsPrc
 # Very difficult to come up with any general rules!


SELECT PrsPrc IF (-2 Attr)(NOT -2 Prop)(-1 COMMA OR CC)(1 N);
	## Čáppa ja fillejeaddji sániiguin...

REMOVE Nom IF (*-1 V-PL3 BARRIER NPNHA)(0 PrsPrc)(*1 N BARRIER NPNH);
	## ...mat rievdaduvvojit dahje sihkkojuvvojit journálaovddasvástideaddji 
	## redigerenbarggu oktavuođas.

SELECT PrsPrc IF (1 TIME);
	## Oaččut dan boahtti sotnabeaivái.

REMOVE PrsPrc IF (0 N)(1 COMMA)(2 NP-BORDER OR CC);
	## Eai dearvvaš olbmot dárbbaš dálkkasteddji, muhto buohccit.

REMOVE PrsPrc IF (0 N)(1 Indef);
	## Ii dus leat gávdni masage.


# Actio/Perfect Participle
# ------------------------
 # Actio = Prs Sg1 = PrfPrc (odd-syll verbs, contracted verbs)
 # Perfect Participle is also treated in "Disambiguating verbs - part 1".
 # The order of rules is of some importance here.
 # We do not SELECT Actio, we remove PrfPrc instead, since there might be a 
 # non-derived N reading alongside Actio. 

SELECT (V Actio Gen) IF (1 ("veara"));
	## Dát sátni lea vuostáiváldin veara.

SELECT PrfPrc IF (*-1 BOS OR CS BARRIER NPNH)(*1 N BARRIER NPNH LINK *1 VFIN
	BARRIER NOT-ADV-PCLE);
	## Sajáiduvvan fágadoahpagiid sáhttá dieđusge geavahit.

SELECT PrfPrc IF (*-1 PrfPrc BARRIER NOT-ADV-PCLE LINK NOT 0 Pass)
	(*1 N BARRIER NPNH OR Dem);
	## Čuoččuhuvvon boasttoáddejupmi lea šaddan cieggan vuoigatvuođa-
	## dilálašvuohta.

REMOVE PrfPrc IF (1C Po);
	## No longer needed?

REMOVE PrfPrc IF (1 CC)(2 Actio)(*3 PrfPrc BARRIER NOT-ADV LINK NOT 0 (Actio Acc));
	## Dušše dihto riddoguovlluin lea monnen ja uvjen leamaš stuorát mahtodagas.
 
SELECT PrfPrc IF (*-1 LEAT-FIN-NOT-IMP BARRIER PrfPrc OR Inf OR VFIN OR CS)
	(NOT 0 Ess)(NOT 0 Actio LINK -1 PrsPrc)
	(NOT 0 Actio LINK 1 COMMA OR CC LINK 1 Actio)
	(NOT *1 PrfPrc BARRIER CC OR CP OR CS);
	## Dasto lea Sámediggi ovttasráđiid Finnmárku ja Tromssa fylka-
	## manniiguin doallan fágaseminára.

REMOVE PrfPrc IF (*-1 ("leat" PrfPrc) BARRIER NPNH)(NOT -1 CC LINK -1 PrfPrc);
	## Doaibmaplána mávssoleamoš áŋgiruššansuorgi lea leamaš dási ja 
	## gelbbolašvuođa lokten.

SELECT PrfPrc IF (-1 CC LINK *-1 PrfPrc BARRIER VERB);
	## Boazodoallolágat leat dievasmahttán ja dahkan dárbbašmeahttun...

SELECT PrfPrc IF (1 CC)(2C PrfPrc)(3 CS OR EOS);
	## Dat boahtá dohko ja gávdná dan guorusin, suohpalastojuvvon ja
	## čiŋahuvvon.

REMOVE PrfPrc IF (*-1 Gen BARRIER NPNH LINK -1 BOS OR CS OR CLB);
	## Divššohasjournálaid geahččan dálkkasgeahččalemiid oktavuođas...

REMOVE PrfPrc IF (NOT *-1 VFIN)(NOT 0 Pass)(NOT *1 VFIN BARRIER CP OR CS);
	## Miehtan nuoraid ovddas geain ii leat miehtangealbu.

REMOVE PrfPrc IF (*-1 PrfPrc OR ConNeg BARRIER NPNHA LINK NOT 0 ("leat"));
	## Ii ávžžuhuvvo dieđuid geahččan sivas olbmuid geat čužžot 
	## divššohasa lahka.

REMOVE V-SG1 IF (*-1 VFIN BARRIER CLB OR CS OR CP);
	## Báikkálaš PPB-deaddočuokkiskantuvrrat jotket stáhta ruhtadan 
	## bálvalusaideaset.

SELECT Nom IF (0 Actio)(NOT 0 Ess LINK 0 (@ADVL))(1 V-SG3);
	## Doarjjaruđaid hálddašeapmi lea leamaš mávssolaš lávki Sámediggái 
	## dearvvašvuođa- ja sosiálasuorggi gelbbolašvuođa háhkamis.


# Coordinated actios
# - - - - - - - - - -

REMOVE PrfPrc IF (NOT *-1 ("leat") BARRIER VERB OR CS)(*1 Actio BARRIER NPNH
	LINK 1 CC);
	## Eiseválddiid oainnu mielde lea okta Sámedikki deaŧaleamos rollain 
	## bidjat eavttuid servodat hábmemis seailluhan, nannen ja ovddidan dihte 
	## sámi servodaga.

REMOVE PrfPrc IF (*-1 COMMA BARRIER NPNH LINK -1 Actio)(1 CC OR COMMA)
	(*2 Actio BARRIER NPNH);
	## Eiseválddiid oainnu mielde lea okta Sámedikki deaŧaleamos rollain 
	## bidjat eavttuid servodat hábmemis seailluhan, nannen ja ovddidan dihte 
	## sámi servodaga.

REMOVE VFIN IF (*-1 COMMA BARRIER NPNH LINK -1 Actio)(1 CC OR COMMA)
	(*2 Actio BARRIER NPNH);
	## Eiseválddiid oainnu mielde lea okta Sámedikki deaŧaleamos rollain 
	## bidjat eavttuid servodat hábmemis seailluhan, nannen ja ovddidan dihte 
	## sámi servodaga.

REMOVE PrfPrc IF (NOT -1 LEAT-FIN-NOT-IMP LINK *-1 CP BARRIER VFIN)
	(1 CLB LINK *1 N BARRIER NPNH LINK *1 CC BARRIER VFIN 
	LINK *1 N BARRIER NPNH LINK NOT 0 Loc);
	## Sámedikki hálddahusa nannen, dearvvašvuođa- ja eallindili-
	## iskkadeami ruhtadeapmi ja Sámi dearvvašvuođadutkanguovddáža 
	## doaimmaid viiddideapmi váldojit ovdan 2002 bušeahta oktavuođas.

REMOVE PrfPrc IF (1 CC)(2 Actio)(3 ("veara"));
	## ...buot dan mii lea ráhkistan ja gudnejahttin veara.


# Verbal versus nominal actio
# - - - - - - - - - - - - - -

SELECT (V Actio Loc) IF (*-1 ("ádjánit") OR ("beassat") OR ("biehttalit") OR 
	("dolkat") OR ("eastadit") OR ("geargat") OR ("gieldit") OR ("heaitit") OR 
	("hehttet") OR ("jaska") OR ("máššat") OR ("váibat") OR ("vajálduhttit") 
	BARRIER VERB);
	## Doaktárii dahje advokáhttii ii sáhte biehttalit dieđuid geahččamis.

SELECT (V Actio Ess) IF (*-2 ("leat") BARRIER NOT-ADV)(-1 ("mielde"));
	## Doaibmaplána lea bidjan buori vuođđu ja leamaš mielde rievdadeame
	## miellagottuid sámegiela geavaheami ektui.

SELECT (V Actio Ess) IF (*-2 V-TRANS-ACT BARRIER NPNH)(-1 Acc)(NOT 0 (N Ess));
	## Mii gulaimet soapmása čoalkkuheame uvssa.
 # If necessary, list the higher verbs that appear in this frame.

SELECT (V Actio Nom) IF (-1 NP-HEAD-GEN)(1 N)(2 NP-BORDER OR Dem)(NOT 2 Po);
	## Nieida ii čájehan geasage irggi čállin reivve.

 # In the so-called agens constructions, like "irggi čállin reive", we probably
 # have the verbal actio. However, unless we have a tag for short actios, it is 
 # not possible to write rules that distinguish between this construction, where 
 # the actio is always shortened, from constructions with the long nominative form, 
 # such as "sámegiela geavaheapmi journálain". Now both types get the analysis 
 # V Actio Nom. 


SELECT (V Actio Gen) IF (1 ("dihte" Po) OR ("dihtii"))(*2 Acc BARRIER NPNH);
	## Mun dál boađán du lusa čállin dihte dan reivve.

REMOVE (Actio Pl Loc) IF (0 (Actio Sg Com));
	## In vel čilgemiinge ipmirdan.

REMOVE (V Actio) IF (0 (N Actio));
	## Dáid plánaid ollašuhttin gáibida lassi doarjagiid.

 # This rule will work fine when all exceptions have been taken care of by the
 # rules above.


# Imperative II
# -------------


# Verbgenitive
# ------------

REMOVE VGen IF (NOT *-1 VFIN BARRIER NOT-ADV-PCLE)(0 VFIN)(*1 N BARRIER NOT-ADV);
	## Ja de čugge goaraha bodnái.

REMOVE VGen IF (*-1 CS OR CP OR BOS BARRIER VFIN)(0 VFIN)
	( *1 CS OR CP OR EOS BARRIER VFIN);
	## Báldáhan ferte diŋgot, jos galgá fidnet.

 # This rule assumes that VGen comes parasitically upon a finite verb,
 # and removes VGen if this is not the case. More rules are needed in order
 # to remove more cases.

REMOVE VGen IF (-1 Attr);
	## Son geas ledje leamaš buot buhtismeahttun vuoiŋŋat,...

REMOVE VGen IF (0 Num)(*1 (N Gen) BARRIER NPNH);
	## Son lei sutnje velggolaš čuođi denara.

SELECT VGen IF (-2 VGen)(-1 CC);
	## Olbmot bohte sihke vuoji ja vácci.
 

# --------------------------------------------------------------------
# Gerund, Verbabessive and Supine have unique forms and need no rules.
# --------------------------------------------------------------------


# Selecting some more finite verbs
# ================================

SELECT VFIN IF (NOT *-1 VFIN BARRIER CS OR CP)(NOT -1 BOS LINK 2 EOS)
	(NOT -1 ("mo"))(NOT 0 (Pass PrfPrc) OR N)(NOT 0 (A Attr) LINK 1 N)
	(NOT 0 Inf LINK *-1 BOS BARRIER NOT-NP)(NOT 1 ("§"))
	(NOT *1 VFIN BARRIER CS);
	## Dát máksá ahte buot universitehta oktavuođat sámegillii mannet Sámi 
	## dutkamiid guovddáža giellakonsuleantta bokte.
	## Gos don dál boađát? Siiddas.

 # BOS/EOS is there to allow for fragments like the "Siiddas" example.


SELECT VFIN IF (*-1 CP OR CS BARRIER VFIN)(NOT 1 (Pron Interr))
	(NOT *1 VERB BARRIER CS OR CP);
	## Son lea dan sápmelaš gean dovddan.

 # This rule finds the finite verbs in embedded finite clauses. 

SELECT VFIN IF (-2 CLB)(-1 ("nu"))(NOT *1 VERB BARRIER CS OR CP);
	## Nu go son lea bassi, nu šaddos bassin oppa din eallin ge.

SELECT VFIN IF (0 INF-VERB)(1 ("go" Pcle))(*2 Inf BARRIER VERB);
	## Lea go son vuolgimin min álbmotguimmiid lusa geat ásset 
	## greikalaččaid gaskkas, ja áigu go sárdnidit greikalaččaide ge?



# ------------------------------- # 
# Lexical disambiguation of verbs #
# ------------------------------- #

# bearrat vs. berret
# ------------------
# bearrat = 'inherit; demand' etc., berret 'ought'

REMOVE ("bearrat") IF (0 ("berret"))(*1 Inf BARRIER VERB OR CS);
	## Válljedettiin journálaovddasvástideaddji berre árvvoštallat su
	## fágagealbbu.


# beassat vs. besset
# ------------------
# beassat = 'get out of; get into' etc., besset 'strip off bark'

REMOVE ("besset") IF (0 ("beassat"))(*1 (V Actio Loc) BARRIER VERB OR CS);
	## Mun bessen vuolgimis.


# boahtit vs. bohtat 
# ------------------
# boahtit 'come', bohtat 'swell'

# These verbs have 5 homonym forms, Prs Du1/Pl3 bohte, Prt Sg1 bohten,
# Prs Pl3/Prt Sg2 bohtet, Imprt Sg1 bohton, Imprt Pl1 bohtot
# In almost all cases, the correct choice will be "boahtit", the 
# challenge is not to remove the cases where it might be "bohtat"
# TODO: Have native speakers make good 'bohtat' sentences.
# This rule tries to disambiguate lexically, it is thus a new type of
# rule, whether it is a good idea remains to see.  

REMOVE ("bohtat" Sg1) IF (0 ("boahtit")); # Will hit 'bohten' and 'bohton'.
REMOVE ("bohtat" Sg2) IF (0 ("boahtit")); # Will hit 'bohtet'.
	## Go mun bohten du vissui,...
	## Journálat álmmolaš doaimmas bohtet arkiivalága vuollái.

REMOVE ("bohtat" Du1) IF (0 ("boahtit")); # Will hit 'bohte'.
REMOVE ("bohtat" Pl1) IF (0 ("boahtit")); # Will hit 'bohtot'.
	## Moai bohte su lusa ja orrugohte su luhtte.
	## 
# With a personal subject, we go for "boahtit"

REMOVE ("bohtat") IF (0 ("boahtit"))(*1 ADVLCASE BARRIER CS) ;
REMOVE ("bohtat") IF (0 ("boahtit"))(*-1 ADVLCASE BARRIER CC OR CS OR CP);
	## Bohtetgo ruđat munnje?
	## Dasgo váimmus bohtet bahás jurdagat.
# When it is coming to or from, go for "boahtit"
# These last two rules may be riskier than the first.


# caggat vs.caggát
# ----------------
# caggat = 'tighten; keep open; hold back', caggát = 'be wide open'

REMOVE ("caggát") IF (0 ("caggat"))(*1 NP-HEAD-ACC BARRIER NPNHA);
	## Olmmoš caggá čuolu rávdnjái fávlli guvlui.


# duđđat vs. duhtat
# -----------------
# duđđat = 'mess', duhtat 'be satisfied'

REMOVE ("duđđat") IF (0 ("duhtat"))(*1 Ill BARRIER NPNHA);
 	## Mun duđan dien vástádussii.

REMOVE ("duđđat") IF (0 ("duhtat"))(1 go)(*2 Ill BARRIER NPNHA);
	## Duđat go mu čállimii?


# eallit vs. eallát
# -----------------
# These verbs have 2 homonym forms: Ind Prs Sg3 eallá, PrfPrc eallán
# eallát = 'to recover, become alive again', eallit = 'to live'

REMOVE ("eallát") IF (0 ("eallit"))(*1 ("eallin")) ;
	## Son eallá suttolaš eallima.

REMOVE ("eallát") IF (0 ("eallit"))(*1 Loc OR ("siste") OR (VAbess)) ;
        ## Mun dovddan muhtun olbmo guhte eallá Kristusis. 
        ## Son eallá naitalkeahttá.


# gieldit vs. gildit 
# ------------------
# gieldit = 'deny sb sth', gildit = 'shine'
# Here, we have a transitivity difference.
# Homonym in the diphtong simplification cases: Prs Du1 gilde, Prs Pl3 gildet
# all Prt forms, gilden, gildet, ..., all Pot forms gilddežan, ..., 
# some of the Imprt forms.  

REMOVE ("gildit") IF (0 ("gieldit"))(*1 Acc BARRIER VFIN) ;
        ## Jesus gilddii sudno garrasit:
        ## Muhto mađe eanet son gilddii, dađe eanet sii almmuhedje dan.
#  The second example is ambiguous.

SELECT ("gieldit") IF (*1 (V Actio Loc) BARRIER VERB OR CS OR CP);
	## Mun gilden Máhte boahtimis.


# goaddat vs. goddit 
# ------------------ 
# goaddat = 'stick to, tarrautua', goddit = 'kill'
# Homonym in the diphtong simplification cases.

REMOVE ("goaddat") IF (0 ("goddit"))(*1 Acc BARRIER VFIN) ;
        ## Ja sii goddet su.


# goddet vs. goddit
# -----------------
# goddet = 'catch wild reindeer', goddit = kill

REMOVE ("goddet") IF (0 ("goddit"))(*1C (@OBJ) BARRIER NPNHA);
	## Vuovdái godde dán guovžža.


# gođđat vs. gođđit 
# ----------------- 
# gođđat = 'spawn', gođđit = 'knit, weave'

REMOVE ("gođđat") IF (0 ("gođđit"))(*1 Acc BARRIER VFIN);
	## Ovddemus dáida oastit árppuid ja gođđá firpmiid.


# guođđit vs. guđđat
# ------------------
# guođđit = 'leave sth swh', guđđat = 'swarm' 

REMOVE ("guđđat") IF (0 ("guođđit"))(*1 Acc BARRIER VFIN) ;
        ## De sii guđđet dan viesu ja gávpoga.


# gullat vs. gullát vs. gullet
# ----------------------------
# gullat = 'hear; belong', gullát 'begin to hear', gullet 'fish'

 # Two rules that pick out 'gullat' in the 'belong' sense:

SELECT ("gullat") IF (0 ("gullát") OR ("gullet"))(*1 Ill BARRIER VERB);
	## Dat gullá lunddolaččat dakkár áššiide go Sámedikkiid válganjuolggadusaide.

SELECT ("gullat") IF (*-1 Ill BARRIER NOT-ADV-PCLE)(0 ("gullát") OR ("gullet"));
	## Doaimma divššohasjournálaide gullá persovdnaregisttar.
	## Eanetlohku, masa gulle Riektejoavkku 6 eará lahtu,...

REMOVE ("gullet") IF (*1 ("ahte") BARRIER NOT-ADV-PCLE);
	## Mun gullen, ahte vulget ikte.


# gárrat vs. gárrut 
# ----------------- 
# gárrat = 'bind sth to sth', gárrut = 'get sth done, +Inf'

REMOVE ("gárrat") IF (0 ("gárrut"))(*1 Inf BARRIER NOT-ADV-PCLE-NEG) ;
        ## Mii lávlluimet morašlávlagiid, ehpet ge dii gárron luoibmat.


# máhccat vs. máhccut 
# ------------------- #
# máhccat = 'return, retreat', máhccut = 'make folds in'

	
# sihtat vs. sihtit
# -----------------
# sihtat = 'want +Acc/+Inf', sihtit = 'luoda karvansa, puhdistua (oravan-
# nahasta syystalvella'
# This will in almost all cases mean 'sihtat'.




# ===================== #
#    Residual cases
# ===================== #
       CONSTRAINTS      #
# ===================== #


# Case rules
# ==========

REMOVE Nom IF (*-1 Inf BARRIER NPNH)(*1 CC OR CS OR CP OR EOS BARRIER WORD
	LINK 1 NP-BORDER);
	## Gonagas sáhttá viidáseappot mearridit dárkilet mearrádusaid mo geavahit 
	## elektrovnnalaš journála, ja das maiddái bidjat gáibádusaid oahpahussii.

SELECT Nom IF (-1 ("okta" Nom))(0 ("eará"));
	## Ii oktage eará go báhpat oaččo borrat.

SELECT Acc IF (*-1 (V Actio Loc) BARRIER NPNHA)(NOT *1 N BARRIER NPNH);
	## Álbmotčeardan geaid eallindilli hehttii sin ráhkadeames 
	## makkárge sierra stáhta.

SELECT Gen IF (0C Dem)(1 TIME)(1C Gen);
	## Maid Biret-Elle áigu dán dálvve dahkat? 

SELECT Gen IF (-1C NP-HEAD-ACC)(0 NP-HEAD-GEN)(*1 N BARRIER NPNH OR PUNCT 
	LINK 0 ADVLCASE);
	## Mii leat gullan dan su iežas njálmmis.

SELECT Gen IF (-1 Po)(*1 N BARRIER NPNH LINK 0 ADVLCASE)(NOT 0 N LINK 1 Indef);
	## Mii gullat sin sárdnumin Ipmila stuora daguid birra min iežamet gillii.

REMOVE Acc IF (-1C (Pron Interr Acc));
	## Mii hálidit muđui váldit ovdan ahte mii geat leat bargan prošeavttain 
	## leat deattuhan bidjat fuomášumi dasa maid min iežamet sajádagat 
	## mearkkašit guorahallanfáttá ektui.

REMOVE (Dem Acc) IF (*1C (N Gen) BARRIER NPNH LINK 0 HNOUN);
	## Fertejitgo máksit daid vuoivasiid ovddas?

SELECT Gen IF (NOT *-1 V-TRANS-ACT BARRIER CC OR CLB OR CS OR CP)
	(NOT -1 Loc)(0 Num)(*1 (N Sg Gen) BARRIER NPNH LINK 1 N);
	## ..., ahte unnimus 135 jagi geavahus lea doarvái.


# Other rules for nouns and pronouns
# ==================================

SELECT (N Sg Nom) IF (*-1C V-SG3 BARRIER NPNHA OR PrfPrc LINK NOT *-1 (Sg Nom) 
	BARRIER CS)(NOT 0 Acc LINK *-1 V-TRANS-ACT BARRIER NPNHA)
	(NOT 0 Gen LINK *1 N BARRIER NPNH);
	#(NOT 0 Loc LINK *1 (Sg Nom) BARRIER NPNHA);
	## Nuppe dáfus lea sámi nissoniid eallin rievdán. 

REMOVE NP-HEAD-PL-NOM IF (0 (Inf) OR NP-HEAD-SG-NOM)(1C V-SG3);
	## Dát lei deháleamos ágga.

SELECT (Interr Sg) IF (-1 CC LINK *-1 VFIN LINK *-1 (Interr Sg) BARRIER VFIN);
	## ...maid addet dearvvašvuođaásahusain dahje maid almmolaš
	## dohkkehuvvon dearvvašvuođbargit addet.
 # Coordinated relative clauses! Also write a rule for Pl?

SELECT Pers IF (-1 ACC-ILL-VERB)(0 Acc)(1 Ill);
	## ..gii dasto attii dan eadnásis.

REMOVE (Interr Pl) IF (-2 Sg3 OR Sg)(-1 Po)(0 (Interr Sg));
	## Dan vuođul maid Tønnesen lea ovdandoallan...


# Px rules
# ========

SELECT (PxSg2) IF (-1 Inf LINK 0 V-TRANS-ACT)
	(*-2 (Imprt Sg2) BARRIER NOT-ADV-PCLE);
	## ...muhto boađe bidjat gieđat su ala,...

REMOVE (PxSg2);
	## ...ahte báhpat juohke sábbáha barget tempelis...
	## Dát máksá ahte buot universitehta oktavuođat sámegillii mannet 
	## Sámi dutkamiid guovddáža giellakonsuleantta bokte.


REMOVE (Acc PxSg3) IF (*-1 V-NOT-AUX BARRIER VERB OR CC OR CLB OR CS OR CP 
	LINK NOT 0 V-TRANS-ACT);
	## ...de Hearrá eŋgel almmustuvai sutnje niegus ja celkkii:...

REMOVE (PxSg3) IF (*-1 Pers BARRIER CS OR CP OR CLB LINK NOT 0 Sg3 LINK -1 Pron);
        ## Goabbá munnos viehká čázis?

REMOVE (PxSg3) IF (0 Loc)(*1 CC BARRIER WORD LINK *1C Loc BARRIER NPNH);
	## Olmmoš ii eale dušše láibbis, muhto juohke sánis mii boahtá...

REMOVE (PxSg3) IF (*-1 Gen BARRIER VFIN OR Adv OR Nom) ;
        ## Piera borrá áhči lanjas.
        ## Piera borrá leaibbis.
        #  Not Px if there is a Genitive possessor in the same NP




# Determiners
# ===========

REMOVE Sg IF (0 DEM-PL)(NOT *1 V-SG3);
	## Dákkárat dat dahket olbmo buhtismeahttumin.

SELECT Pl IF (0 (Dem Nom))(*1 V-DU3 BARRIER VFIN OR CS OR CP);
	## Ja duot eará oskuvieljat leaba searvegotti áirasat Kristusa gudnin.

REMOVE Pl IF (0 DEM-SG)(NOT *1 V-PL3);
	## Gahččan, gahččan lea stuora Babylon, duot fuorrá.

SELECT Dem IF (1 ("bodda"));
	## Mun finan gávppašeame dan botta.


# Adverbs and adjectives
# ======================

SELECT Adv IF (-1C N)(0 A)(1C Inf);
    ## Dáidet ráđđehearrát duođaid dohkkehit ahte son lea Messias?

 # The idea is that we between a noun an an infinitive will have an adverb, 
 # and not an adjective.


REMOVE Adv IF (-1 (Dem Nom));
	## Buot dát bahát bohtet siskkildasas ja dahket olbmo buhtismeahttumin.

REMOVE (A Nom) IF (-1 Nom)(1 VFIN);
	## Máŋgga fágasuorggi siskkobealde lea dárbbašlaš erenoamážit
	## vuoruhit sámi/eamiálbmotguoskevaš dutkamiid.

REMOVE (Pl Nom) IF (0 Adv)(1 V-SG3);
	## Vaikko dákkár vuoigatvuohta nappo vánit oažžu buorre buhtadan-
	## suodjaleami,...

REMOVE Attr IF (1C V OR EOS OR ("nr"))(NOT 1 N OR PrfPrc);
	## Muhto son gilddii sin garrasit muitaleames geasage iežas birra.
  # When all rules are executed, remove the Attrs.
  # The double requirement is there because of the compounds.




# =============================== #
# Global disambiguation - cycle 3 #
# =============================== #
              CONSTRAINTS         #
# =============================== #

# This is the section for rules of more global scope.


# Nouns
# -----

 # These rules need to come late, allowing Actor and Actio readings to be 
 # picked out by earlier rules.

REMOVE (N Actor) IF (0 LEX-N); # The N reading should remain.
	## Mihttomearrin lea 5 jagi badjel buvttadit 2 sámegiel álmmuheami jáhkái.

REMOVE Actio IF (0 N)(NOT 0 Ess OR PrfPrc OR VFIN OR A); 
	## Journála sirdin, doaimma loahpaheapmi jna. # Same here.
 # NOT 0 A because e. g. "eavttuheapmi" is Actio or A. Not always correct to remove Actio.


# Verbs
# -----

REMOVE V-DER; # Removes only if there is another reading. 
	## Mánáin lea vuoigatvuohta oažžut dearvvašvuođaiskkadeami.
	## Dat ihte johtilit.



# =============================== #
# Disambiguating syntax - Cycle 4 #
# =============================== #
             CONSTRAINTS          #
# =============================== #

# Subject
# -------

SELECT (@SUBJ) IF (0 MOAI)(*1 ("leat" Du1) BARRIER NOT-ADV-PCLE) ;
SELECT (@SUBJ) IF (0 DOAI)(*1 ("leat" Du2) BARRIER NOT-ADV-PCLE) ;
SELECT (@SUBJ) IF (0 MII-PERS)(*1 ("leat" Pl1) BARRIER NOT-ADV-PCLE) ;
SELECT (@SUBJ) IF (0 DII)(*1 ("leat" Pl2) BARRIER NOT-ADV-PCLE) ;
	## Moai letne studeanttat.

SELECT (@SUBJ) IF (0 (N Num))(1 (Sg Com))(*1 V-DU3 BARRIER VERB OR CS OR CP);
	## Máhte-guovttos Lássiin eaba boahtán.

SELECT (@SUBJ) IF (0 (Interr Sg))(*1 V-SG3 BARRIER NOT-ADV);
	## Márjá riegádahtii Jesusa, guhte gohčoduvvo Kristusin.

SELECT (@SUBJ) IF (0 (N Sg Nom))(*1 (Neg Sg3) BARRIER NOT-ADV-PCLE OR CLB);
	## Sámi dutkan ii leat dan dihte soaittáhagas válljejuvvon dutkansuorgi.

SELECT (@SUBJ) IF (0 (N Pl Nom))(*-1 COPULAS BARRIER NPNH)
	(*-1 V-PL3 BARRIER VFIN LINK *-1 (Sg Nom) BARRIER NOT-ADV-PCLE
	LINK NOT *-1 CC BARRIER NPNH LINK -1 Nom); 
	## Erenoamáš ulbmiljoavku leat doavttergrádastudeanttat.
 
SELECT (@SUBJ) IF (*-1 CC BARRIER NPNH LINK -1 Nom)
	(*1 V-PL3 BARRIER NOT-ADV-PCLE);
	## Muhto buođđogeažit ja giđđabuođut ráhkaduvvojit muđui seammá vugiid 
	## mielde.

SELECT (@SUBJ) IF (*-1 CC BARRIER NPNH LINK -1 (@SPRED))
	(*1 (@SPRED) BARRIER NPNHA OR REALCLB OR PUNCT LINK NOT 0 Interr LINK
	NOT *1 COPULAS BARRIER Nom OR CS OR CP OR CLB);
	## Vuosttaš vuojahaga fávllimus čuolda lea goaratčuoldan, ja 
	## nubbi čuolda fas gáddeoal-čuoldan.

SELECT (@SUBJ) IF (*-1 COPULAS BARRIER NPNHA)(NOT 0 Ess)(NOT 1 COMMA)
	(*1 (A Nom) BARRIER NOT-ADV-PCLE OR go);
	## De lea cicha gárvvis.

SELECT (@SUBJ) IF (*-1 COPULAS BARRIER NPNH LINK *-1 ("veara") BARRIER NOT-ADV-PCLE);
	## Man ollu eanet veara lea olmmoš go sávza!

SELECT (@SUBJ) IF (0 (N Sg Nom) LINK 0 TIME)(*1 V-SG3 BARRIER NOT-ADV-PCLE);
	## Sii eai diehtán ahte mánnu šaddá.

SELECT (@SUBJ) IF (0 ("guovttis" Nom))(*1 Du3 BARRIER VERB OR CLB OR CC OR CS OR CP);
	## Guovttis jámiiga dan bárttis.

SELECT (@SUBJ) IF (NOT *-1 (@SUBJ) BARRIER CS)(*-1 ("leat") BARRIER VERB OR CS)
	(0 Actio)(*1 PrfPrc BARRIER VERB OR CC OR CS OR CP OR CLB);
	## Leago miehtan vižžojuvvon divššohasas dahje sus geas lea...


# Subject predicate
# -----------------

REMOVE (@SPRED) IF (*-1 ("leat") BARRIER NOT-ADV-PCLE OR ("dego"))
	(*1 PrfPrc BARRIER NOT-ADV-PCLE LINK NOT 0 Pass);
	## Sámi vuoigatvuođalávdegotti čállin leat čuovvovaččat maid doaibman.
	
SELECT (@SPRED) IF (0 A)(1 go)(*2 COPULAS BARRIER V-NOT-AUX LINK *1 (@SUBJ) BARRIER 
	NPNHA);
	## Guhkki go lea du biila?

SELECT (@SPRED) IF (0 (A Qst))(*1 COPULAS BARRIER V-NOT-AUX LINK *1 (@SUBJ) BARRIER
	NPNHA);
	## Guhkkigo lea du biila?

SELECT (@SPRED) IF (*-1 COPULAS BARRIER (@-FMAINV) OR Loc OR go OR ("dego") OR CC 
	OR CLB OR CS OR CP LINK *-1 (@SUBJ) BARRIER (@OBJ) OR CC OR CS OR CP 
	LINK NOT 0 Ess)(NOT 0 Loc OR Pers OR Interr OR Prop)(1 NP-BORDER)
	(NOT *-1 VFIN BARRIER CS LINK *-1 VFIN BARRIER CS LINK *-1 Interr BARRIER VERB);
	## Gustovaš riekteteoriija gaskavuođat sihke kultuvrralaš ja ekonomalaš 
	## konteavstta dáfus leat eahpečielgasat.

 # The last condition prevents the rule from hitting "vuoigatvuohta" here:
	## Divššohasas, gii dárbbaša guhkit áiggi ja oktiiheivehuvvon dearvvašvuođa-
	## bálvalusaid, lea vuoigatvuohta oažžut ráhkaduvvot oktagaslaš plána.
 
SELECT (@SPRED) IF (*-1 COPULAS BARRIER VERB OR (@SPRED) OR CLB OR CS OR CP OR go 
	OR ("dego") OR (@OBJ) LINK *-1 (@SUBJ) OR (@SUBJ-QH) BARRIER V-NOT-AUX OR 
	CLB OR CC OR CS OR CP LINK NOT 0 Ess LINK NOT *-1 (@SPRED) BARRIER CC OR CS)
	(NOT 0 Loc OR Prop OR Interr)(NOT 0 Actio OR Attr LINK 1 N);
	## Soai leaba goappašagat nu jallat.

SELECT (@SPRED) IF (*-1 COPULAS BARRIER NPNH OR (@SPRED) OR COMMA LINK 0 PrfPrc 
	LINK *-1 ("leat") BARRIER NOT-ADV-PCLE LINK *-1 
	Nom BARRIER VERB OR CS OR CP);
	## Arkelaus lei šaddan Judea gonagassan áhčis Herodesa maŋŋil.

SELECT (@SPRED) IF (*-1 (@SUBJ) BARRIER NPNHA OR CC OR CLB OR CS OR CP OR ("/")
	LINK *-1 COPULAS BARRIER NOT-ADV-PCLE)(NOT 0 Interr);
	## Dávjá lea riektevuogádat áidna mearridanvuogádat.

SELECT (@SPRED) IF (*-1 (N Nom) BARRIER NOT-ADV-PCLE OR go OR ("nugo") OR CLB OR 
	PUNCT OR CP LINK NOT 0 ("buorre") LINK *-1 COPULAS BARRIER NPNHA)
	(NOT 0 Interr OR Pers)(NOT 0 ("nr") LINK 1 @NumN<); 
	#  After "NPHNA": LINK NOT *-1 Loc BARRIER NOT-ADV-PCLE
	## Ledjego guolit divrasat?

SELECT (@SPRED) IF (*-1 COPULAS BARRIER VERB LINK 0 Sg3 LINK NOT *-1 Loc BARRIER
	NOT-ADV-PCLE)(0 A OR ("imaš"))(NOT 0 Attr LINK 1 N)
	(NOT *1 PrfPrc BARRIER NOT-ADV-PCLE);
	## Dutnje lea buoret ahte don massát ovtta lahtu.

SELECT (@SPRED) IF (*-1 COPULAS BARRIER VFIN OR CONTRA LINK *-1 AUX 
	BARRIER NOT-ADV-PCLE LINK 0 Sg3)(0 A OR ("imaš"))(NOT 0 N)
	(NOT 0 ("veara") LINK 1 PrfPrc OR (Actio Ess) OR N);
	## Boazosápmelaččain ii leat bievlan vejolaš fievrridit gálvvu
	## gerresiiguin.

SELECT (@SPRED) IF (NOT *-1 VFIN)(0 Pers)(1 Interr LINK *1 VFIN BARRIER CS OR CP 
	LINK *1 COPULAS BARRIER CS OR CP OR CLB LINK *1 (@SUBJ) BARRIER CS OR CP 
	OR CLB LINK 0 Prop LINK NOT *1 V-NOT-AUX);
	## Son gii ovddimus boahtá lea Máhtte. 
 
SELECT (@SPRED) IF (*-2 COPULAS BARRIER NOT-ADV-PCLE)(-1 Superl)
	(*1 (@SUBJ) BARRIER CC OR CLB OR CS);
	## Nugo namuhuvvui, lea nannoseamos ágga dása guhkes áiggi geavahus.

SELECT (@SPRED) IF (*-1 COPULAS BARRIER NOT-ADV-PCLE LINK 0 PrfPrc OR ConNeg LINK 
	*-1 Neg BARRIER NOT-ADV-PCLE)(0 A);
	## Ii dál leat álki bargui beassat.

SELECT (@SPRED) IF (*-1 (Pers Nom) BARRIER NPNHA OR CLB LINK *-1 COPULAS 
	BARRIER NOT-ADV-PCLE)(NOT 0 Interr);
	## Leat go don árgi bajándálkkis?

SELECT (@SPRED) IF (*-2 COPULAS BARRIER NOT-ADV-PCLE LINK *-1 (@SUBJ) 
	BARRIER V-NOT-AUX OR CS)(-1 (PrfPrc))(NOT -1 Pass)(0C Nom)(1 NP-BORDER);
	## Son lea oahppan olmmoš.

SELECT (@SPRED) IF (*-1 COPULAS BARRIER V-NOT-COP LINK 0 Pl3)(0 (A Pl))
	(*1 (@SUBJ) BARRIER NPNH LINK 0 Pl);
	## Leatgo hilbadat manat?

SELECT (@SPRED) IF (*-1 COPULAS BARRIER NOT-ADV-PCLE)(0 A)(1 (Pers Nom));
	## Eamiálbmogiidda leat oktasaš dat, ahte ...

SELECT (@SPRED) IF (*-1 COPULAS BARRIER VERB OR CS)(1 ("dego"));
	## Lehket dan dihtii gávvilat dego gearbmašat ja vigiheamit dego duvvát!

SELECT (@SPRED) IF (0 Ess)(*1 (@SUBJ) OR (@SUBJ-QH) BARRIER VERB OR CLB OR CS OR CP 
	LINK *1 COPULAS BARRIER V-NOT-AUX OR CS OR CONTRA LINK NOT *1 V-NOT-AUX 
	BARRIER CS)(NOT *1 (Interr Nom) BARRIER VERB OR CS);
	## Ja stuorra buressivdnádussan dat gal buođđobivdu leamašge Sámieatnan 
	## stuorámus deanu alde ássiide.

SELECT (@SPRED) IF (*-1 (@SUBJ) OR (@SUBJ-QH) BARRIER NPNH LINK *-1 COPULAS 
	BARRIER NOT-ADV-PCLE)(0 Ess);
	## Mus lea Ánde-máhka mielbargin.

SELECT (@SPRED) IF (*-1 COPULAS BARRIER NOT-ADV-PCLE LINK 0 Inf 
	LINK *-1 (@SUBJ) OR (@SUBJ-QH) BARRIER NOT-ADV-PCLE LINK *-1 AUX BARRIER VERB);
	## De ferte joddu ge leat dađi mielde, oanehit go dábálaš.

SELECT (@SPRED) IF (0 (A Sg Nom))(NOT *-1 Nom BARRIER CS)(NOT *1 Nom BARRIER CS)
	(NOT *-1 V-NOT-AUX BARRIER CS)(NOT *1 V-NOT-AUX BARRIER CS);
	## Go beare vejolaš lea, de ráhkaduvvo buođđu čuolddaiguin.
	## Buorre lea go it fuobmán.

SELECT (@SPRED) IF (*-1 COPULAS BARRIER VERB OR CS)(0 ALU);
	## Dábálaččat leat joddut sullii ovtta guhku.

SELECT (@SPRED) IF (*-1 COPULAS BARRIER NPNH OR CC LINK -1 ("dego"));
	## Sii válde mu gitta, dego livččen rievvár.

SELECT (@SPRED) IF (-2 (@SUBJ) LINK 0 Sg)(-1 ("nu") OR ("hui"))(0 (A Sg));
	## Márehis lea oaivvi nu bávččas.

SELECT (@SPRED) IF (-2 (@SUBJ) LINK 0 Pl)(-1 ("nu") OR ("hui"))(0 (A Pl));
	## Mus leat gieđat nu galbmasat.

SELECT (@SPRED) IF (-1 ("mii" Pron Interr Sg Gen))(0 A);
	## Man guhkki lea skuvlii?
	## In dieđe man buorit dat leat.

SELECT (@SPRED) IF (*-1 COPULAS BARRIER COMMA OR NPNHA OR go OR ("nugo") 
	LINK *-1 (Pers Nom) BARRIER V-NOT-AUX OR CS OR CP)(NOT 0 Interr)
	(NOT 0 Ess LINK 1 OPRED-VERB LINK NOT 0 Pass);
	## ...dan addejumis, man mielde dat leat eamiálbmotválljen orgánat ja ...

SELECT (@SPRED) IF (*-1 VFIN BARRIER Acc OR OPRED-VERB OR CS OR CP OR go OR ("dego") 
	OR (@SPRED) LINK NOT 0 ("dahkat") LINK *-1 Nom BARRIER NOT-ADV-PCLE LINK 
	NOT *-1 (@OBJ) BARRIER CS)(0 Ess)(NOT 1 Inf);
	## Máhtte álggii reaŋgan.

SELECT (@SPRED) IF (*-1 (@SUBJ) BARRIER NPNHA LINK *-1 VFIN BARRIER Nom OR CC OR 
	CS OR CP)(0 Ess)(NOT *-1 (@OBJ) BARRIER CS)(NOT *1 (@OBJ) BARRIER NPNHA);
	## ...mii muitala, doaibmago našunálastáhta siviliserejuvvon kulturservodahkan.

SELECT (@SPRED) IF (*-1 BOS OR CLB OR PUNCT BARRIER NOT-CC)(0 A)
	(1 (Pers Nom));
	## Heittogat dat leat maiddái.
	## Ávdugasat sii geat moraštit.

SELECT (@SPRED) IF (NOT 0 Loc)(*1 (Dem Nom) OR (Pers Nom) BARRIER NOT-ADV-PCLE 
	OR COMMA OR CP OR CC OR PUNCT LINK *1 COPULAS BARRIER V-NOT-AUX OR CS OR CP 
	LINK NOT *1 VERB BARRIER CS OR CP OR CC);
	## Gean ráhkadan beaskanáhkit dát leat?

SELECT (@SPRED) IF (NOT 0 ("namma"))(*1 (COPULAS V-3) BARRIER NOT-ADV-PCLE OR CLB 
	LINK *1 Prop BARRIER NOT-ADV-PCLE OR CLB LINK 0 Nom);
	## Salmai riegadii Boas, gean eadni lei Rahab.
	## Mun lean Trond.

SELECT (@SPRED) IF (NOT 0 ("namma"))(*1 COPULAS BARRIER NOT-ADV-PCLE OR CLB 
	LINK *1 Pers BARRIER NOT-ADV-PCLE OR CLB LINK 0 Nom LINK NOT 1 Interr);
	##

SELECT (@SPRED) IF (0 (Sg Nom))(*1 COPULAS BARRIER NOT-ADV-PCLE OR CLB LINK 0 (Pl3) 
	LINK *1 (Pl Nom) BARRIER NPNHA);
	## Erenoamáš ulbmiljoavku leat doavttergrádastudeanttat.

SELECT (@SPRED) IF (*-2C (@SPRED) BARRIER WORD)(-1 Neg)(NOT -1 Sup);
	## Máhtte lea dát iige duot.

SELECT (@SPRED) IF (*-1 ("leat" Imprt) BARRIER VERB OR Nom OR Ess OR PUNCT LINK 
	NOT *-1 (@SPRED))(NOT 0 Pers)(NOT *1 (A Nom) BARRIER VERB OR CS OR CP OR CC);
	## Leage buorre!

SELECT (@SPRED) IF (*-1 COPULAS BARRIER NOT-ADV-PCLE)(*-1 VFIN LINK NOT 0 Sg3 OR Pl3)
	(0 (A Sg Nom));
	## ...ahte áiggošin leat bahánihkkán, váttis dahje fuikolas.

SELECT (@SPRED) IF (0 ("veara"))(1 COPULAS LINK *1 (@SUBJ) OR (@SUBJ-QH) BARRIER NPNHA);
	## Man ollu eanet veara lea olmmoš go sávza!

SELECT (@SPRED) IF (-1 Actio LINK *-1 COPULAS BARRIER NPNHA)(0 ("veara"))
	(NOT *1 PrfPrc OR (Actio Ess) BARRIER NOT-ADV-PCLE)(NOT 1 N);
	## Lea fuopmášan veara, ahte...

SELECT (@SPRED) IF (-3 Actio LINK *-1 COPULAS BARRIER NOT-ADV-PCLE)(-2 CC)(-1 Actio)
	(0 ("veara"));
	## ...buot dan mii lea ráhkistan ja gudnejahttin veara.

SELECT (@SPRED) IF (*-1 COPULAS BARRIER NOT-ADV-PCLE)(0 ("veara"))(1 Inf);
	## Min mielas goit lea veara jurddašit nissongova birra mii boahtá ovdan 
	## jearahalamiin.

SELECT (@SPRED) IF (0 ("veara") OR ("mearkkašan#veara") OR ("namuhan#veara"))
	(*1 COPULAS BARRIER NOT-ADV-PCLE);
	## Mearkkašan veara lea ahte...
	## Erenomáš mearkkašanveara lea ahte...

SELECT (@SPRED) IF (*-1 COPULAS BARRIER NOT-ADV-PCLE)(0 A)(1 (N Nom));
	## Go sápmelaš lea alla váriid alde, de sus lea oppa čielggas jierbmi.

SELECT (@SPRED) IF (*-1 Interr BARRIER VFIN)(0 Ess)
	(*1 COPULAS BARRIER VERB LINK NOT *1 V-TRANS-ACT BARRIER CS);
	## ...mii dál min áiggi eanemus ávkin lea...

SELECT (@SPRED) IF (0 Ess)(*1 ("atnit") OR ("heivet") BARRIER V-NOT-AUX OR CS OR CP);
	## Maŋŋálasa gáddeoal-čuoldan heive dalle dábálaččat vuosttaš vuojahaga 
	## fávllimus čuolda.

SELECT (@SPRED) IF (*-1 ("gohčodit" Pass) OR ("dadjat" Pass) OR ("dahkat" Pass) 
	BARRIER NOT-ADV-PCLE)(0 Ess);
	## Son gohčoduvvo bassin ja Ipmila Bárdnin.

REMOVE (@SUBJ) IF (*-1 COPULAS BARRIER VERB OR CS OR CP)(0 ("buorre"));
	## Leat go buorre munnje?

SELECT (@SPRED) IF (-1 COPULAS LINK *-1 ("namma" Nom @SUBJ))(0 Prop) ;
	## Mu namma lea Søren.

SELECT (@SPRED) IF (-2 COPULAS)(-1 (":"))(0 Num);
	## Doaimmabiju jahkásaš bušeahttarámma lea: 380.000 ruvnno.

SELECT (@SPRED) IF (*-1 go BARRIER NOT-ADV LINK -1 COPULAS LINK *-1 BOS OR CC
	BARRIER NOT-CC)(0 A);
	## Leat go árgi bajándálkkis?

SELECT (@SPRED) IF (*-1 ("dahkat") BARRIER NOT-ADV-PCLE)(0 Ess)(NOT *-1 (@OBJ)
	BARRIER CS OR CP)(NOT *1 (@OBJ) BARRIER CS OR CP);
	## Ii dahkan gullin ge.
	## Problem: Son guhte dahká bassin.

SELECT (@SPRED) IF (*-1 ("leat" Sg1) BARRIER VERB OR CLB OR CS OR CP OR go OR ("dego") 
	OR (@OBJ))(NOT 0 MUN) ;
SELECT (@SPRED) IF (*-1 ("leat" Sg2) BARRIER VERB OR CLB OR CS OR CP OR go OR ("dego") 
	OR (@OBJ))(NOT 0 DON) ;
	## Mun lean Tarzan, don leat Jane.
 #  This rule did not work with COPULAS, for some reason.
 #  Idea: I am a predicate if I don't agree with the verb.

SELECT (@SPRED) IF (*-2 CC BARRIER N OR Loc OR Po OR V-NOT-AUX OR CP OR CS)(-1 COPULAS)
	(0 A)(1 CLB OR CC OR EOS);
	## Nugo dii bijaidet lahtuideattet buhtismeahttunvuođa ja lágahisvuođa háldui 
	## ja šattaidet lágaheapmin,...

SELECT (@SPRED) IF (*-1 COPULAS BARRIER NOT-ADV-PCLE LINK NOT *-1 V-NOT-AUX 
	BARRIER VFIN)(0 Ess)(*1 Qst BARRIER WORD);
	## Ruonáetnama hárrái šaddá váldoáššin, berrešiigo jienastuslohku ráddje-
	## juvvot nu, ahte...

SELECT (@SPRED) IF (0 A)(NOT 0 Adv)(*1 COPULAS BARRIER NOT-ADV-PCLE LINK *1 Inf 
	BARRIER NOT-ADV-PCLE);
	## Dárbbašlaš lea dikšut jávrriid ovdal go lea ekonomalaččat veara bivdit.

SELECT (@SPRED) IF (*-1 ("leat") BARRIER VERB)(0 Ess)(NOT 0 (@ADVL))
	(*1 Inf BARRIER VERB);
	## Universitehtii lea ain hástalussan jorgalit dárogielmateriála.

SELECT (@SPRED) IF (-2 Num)(-1 (@MEASURE))(0 A);
	## Arkiivaávdnasat galget sáddejuvvot go leat sullii 25-30 jagi boarrásat. 

SELECT (@SPRED) IF (-1 ("go" Pcle))(0 (A Sg Nom))(NOT 0 Attr LINK *1 N BARRIER NPNH);
	## Gielddat berrejit láhčit diliid nu ahte sámi boarrásat ja doaibmahehttejuvvon 
	## sápmelaččat sáhttet, nu guhká go vejolaš, orrut ruovttus.

SELECT (@SPRED) IF (NOT *-1 VERB BARRIER CS)(0 Ess)(*1 Pass BARRIER VERB);
	## Noađđeheargin dárbbašuvvojit sakka lojit hearggit go geresgeassin.

SELECT (@SPRED) IF (*-1C (@SUBJ) BARRIER go OR ("dego") OR CC OR CLB OR CS OR 
	CP OR PUNCT LINK NOT 0 Interr)(NOT -1 (@SUBJ))(0C (N Sg Nom));
	## Sámi dutkan ii leat dan dihte soaittáhagas válljejuvvon dutkansuorgi.
 # If -1 is (@SUBJ) we probably have apposition.
 
REMOVE (@SUBJ) IF (0 Ess)(*1C (@SUBJ) BARRIER CS OR CP LINK NOT 0 Interr);
	## Sámi vuoigatvuođalávdegotti čállin leat čuovvovaččat maid doaibman.
 
SELECT (@-FSUBJ) IF (NOT *-1 V-TRANS-ACT BARRIER CS)(1 VAbess);
	## Diet dáhpáhuvai mu dieđikeahttá.


# Coordinated SPRED
# - - - - - - - - -

SELECT (@SPRED) IF (-2C (@SPRED))(-1 CC)(NOT *1 VFIN BARRIER CS);
	## De adnojuvvojit dat vel čuollofirbmin ja buođo-al-fierbmin.

SELECT (@SPRED) IF (*-1 CC BARRIER NPNH LINK -1C (@SPRED))
	(NOT *1 VFIN BARRIER CS OR REALCLB)(NOT *1 (@SPRED) BARRIER NPNH)
	(NOT 1 (@ADVL));
	## Dalle ledje noaiddis erenoamáš rolla oaidnilin ja dálkkodeaddjin.

SELECT (@SPRED) IF (*-1 COMMA BARRIER NPNH LINK -1C (@SPRED))(0 A)
	(NOT *1 VFIN BARRIER CLB OR CS);
	## Guokte golbma čuoldabaji guhku, ja guhkitge, sáhttá vuojahat leat.


# Subject or not
# --------------

REMOVE (@SUBJ) IF (NOT *-1 VERB BARRIER CS OR CP)(0 Gen)(*1C (@SPRED) BARRIER NPNH);
	## Riektemetoda vuođđun lea našunálastáhtalaš riektaárbevieruid áddejupmi.

REMOVE (@SUBJ) IF (*-1C V-1-2 BARRIER NOT-ADV-PCLE)(0 Sg OR Pl);
	## Mii leimmet viđas.
	## Mun lean árgi.

REMOVE (@SUBJ) IF (0 ("mearkkašan#veara") OR ("namuhan#veara"));
	## Datte lea mearkkašanveara ahte son, stáhta ektui, orru oaivvildeamen...

REMOVE (@SUBJ) IF (*-1 (@SUBJ) BARRIER CS OR CP)(0 (@TITLE));
	## Stuorra giitu maiddái professor Birger Stuevold lassenii.

REMOVE (@SUBJ) IF (0 (@TITLE))(*1 (@SUBJ) BARRIER CS OR CP);
	## Go gonagas Herodes gulai dan, de son suorganii.

REMOVE (@SUBJ) IF (*-1 (@SUBJ) BARRIER CS)(0 Attr)(*1 N BARRIER NPNH);
	## Dát lea ee. kultuvrralaš gaskka duohken.

SELECT (@SUBJ) IF (*1C (@SPRED) BARRIER NPNH OR CC OR CLB);
	## Dalle adno nannejuvvon dulkon gustojeaddji riektin.


# Object and object predicate
# ---------------------------

SELECT (@OBJ) IF (NOT *-1 (@OBJ) BARRIER CS OR REALCLB)(0 ("hearrá"))
	(1 ("ipmil" Acc));
SELECT (@OBJ) IF (-1 ("hearrá") LINK 0 (@OBJ))(0 ("ipmil" Acc));
	## Don galggat ráhkistit Hearrá Ipmilat oppa váimmustat...

SELECT (@OPRED) IF (*-1 (@OBJ) BARRIER VERB OR CC OR CLB OR CP OR go OR PUNCT 
	LINK NOT 0 Dem)(*-1 OPRED-VERB BARRIER VERB OR CC OR CLB OR CS OR CP)
	(NOT 0 Gen LINK *1 N BARRIER NPNH)(NOT 0 Interr OR TIME OR ROUTE)
	(NOT 0 Num LINK 1 ROUTE OR TIME)(NOT 1 (TV Actio) LINK NOT *1 (@OBJ) BARRIER CS);
	## Divššohasbearráigeahčči galgá dahkat ortnega dovddusin.
 
SELECT (@OPRED) IF (*-1 (@OBJ) BARRIER NOT-NP OR Interr)(0 Ess)(NOT 1 V-SG3);
	## Doaibmaplána lea bidjan buori vuođu ja leamaš mielde rievdadeame 
	## miellaguottuid sámegiela geavaheami ja oainnusin dahkama ektui.

SELECT (@OPRED) IF (*-1 VFIN BARRIER Acc OR CS OR CP LINK *-1 Nom 
	BARRIER NOT-ADV-PCLE)(0 Ess)(1 Inf);
	## Fakultehtaid gáržžes ruhtadilli dahká váttisin lágidit dákkár kurssaid.

SELECT (@OPRED) IF (*-1 COPULAS BARRIER VERB LINK NOT 0 VFIN LINK *1 (@OBJ) 
	BARRIER NOT-ADV-PCLE)(NOT *1 V-TRANS-ACT BARRIER NP-BORDER);
	## Dat ii lohkan daid leat buriid.

SELECT (@OPRED) IF (*-1 (@OBJ) BARRIER CS OR CP OR CC LINK 0 N)(0 A OR Ess)
	(NOT 0 Adv)(NOT 0 Attr LINK *1 N BARRIER NPNH)
	(NOT *-1 COPULAS BARRIER VERB LINK NOT *1 VERB BARRIER CLB OR CC OR CS OR CP);
	## Gáhkuid son ráhkada hui buriid.

SELECT (@OPRED) IF (0 Ess)(NOT 0 Acc)(*1 OPRED-VERB BARRIER V-NOT-AUX OR CS OR CP 
	LINK NOT 0 Pass);
	## Buođđogeahčin gohčodit olbmot dakkár buođu mii dulvvi áiggi bidjaluvvo 
	## gáddegurii.

SELECT (@OPRED) IF (0 (A Acc))(*1 NP-HEAD-ACC BARRIER CS OR CP LINK *1 COPULAS 
	BARRIER NOT-ADV-PCLE);
	## Buriid dat ii lohkan daid leat.

REMOVE (@OPRED) IF (NOT *-1 V-TRANS-ACT BARRIER CS LINK NOT 0 AUX)
	(NOT *1 V-TRANS-ACT BARRIER CS LINK NOT 0 AUX);
	## Itgo boađáše munnje veahkkin?

REMOVE (@OBJ) IF (0 (@GN>))(NOT 0 Interr)(*1 (@SUBJ) BARRIER NPNH LINK *1C (@OBJ) 
	BARRIER CLB OR CS OR CP)(NOT 1 VFIN);
	## ...go su iežas anistumit gesset ja giktalit su.

REMOVE (@OBJ) IF (*-1C (@OBJ) BARRIER NPNH OR CC OR CLB OR PUNCT)
	(NOT 0 Interr)(NOT 1 (TV Actio) LINK NOT *1 (@OBJ) BARRIER CS);
	## Das maŋŋá dajan juoidá álbmotrievtti mearkkašumi birra.
 
REMOVE (@OBJ) IF (*-1C (@OBJ) BARRIER CS OR CP OR CC OR CLB OR Inf OR PUNCT 
	LINK NOT *-1 V-TRANS-ACT BARRIER NPNHA)(NOT 0 Interr OR Pers)(NOT 1 Inf);
	## ...dieđuid maid son dárbbaša iežas doaimmaheami dihtii.

REMOVE (@OBJ) IF (*-1 Inf BARRIER NPNHA LINK 0 V-TRANS-ACT LINK -1 (@OBJ)
	LINK NOT *-1 V-TRANS-ACT BARRIER NPNH)(0 Gen LINK *1 N BARRIER NPNH)
	(NOT 0 Interr);
	## Sámi studeanttaid oččodit Romssa universitehtii.

REMOVE (@OBJ) IF (*-1 V-TRANS-ACT BARRIER NPNHA LINK *-1 (@OBJ) BARRIER
	V-NOT-AUX OR CLB OR CC OR CS OR CP LINK NOT 0 (@GN>) LINK 
	NOT *-1 V-TRANS-ACT BARRIER NOT-ADV-PCLE)(0 (@GN>))(NOT 0 Interr);
	## Daid guovlluid sáhttit meroštallat sámi guovddášguovlun.

REMOVE (@OBJ) IF (*-1 V-NOT-AUX BARRIER NOT-ADV-PCLE LINK 0C IV LINK NOT 0 PrfPrc)
	(NOT *-1 CLB BARRIER NPNH)(NOT *1 V-TRANS-ACT BARRIER V-NOT-AUX OR CS OR CP);
	## Stuorát guolit mannet čađa dohko joddomohkái.

REMOVE (@OBJ) IF (*-1 ("gullat") BARRIER VERB OR ("gč") OR CS LINK *-1 Ill 
	BARRIER NOT-ADV-PCLE);
	## Dasa gullet maiddái riekteásahusat.

REMOVE (@OBJ) IF (0 Dem)(*1C NP-HEAD-ACC BARRIER V-TRANS-ACT OR go OR CS OR CP OR 
	CC OR CLB LINK NOT 0 Interr);
	## Mun in leat vel borran dan guolis maidege.

REMOVE (@OBJ) IF (-1 V-TRANS-ACT BARRIER NOT-ADV-PCLE LINK *-1 (@OBJ) BARRIER
	NOT-ADV-PCLE)(0 Adv);
	## In sáhtege buohkaid biebmat gallás.

SELECT (@OBJ) IF (*-1 Inf BARRIER NPNHA LINK 0 V-TRANS-ACT LINK NOT *-1 (@OBJ)
	BARRIER V-NOT-AUX OR CS OR CP)(NOT 0 Loc LINK *-1 (@OBJ) BARRIER VERB)
	(1 (@OPRED) LINK 1 NP-BORDER);
	## Divššohasbearráigeahčči galgá dahkat ortnega dovddusin.

SELECT (@OBJ) IF (*-1 V-TRANS-ACT BARRIER CS OR CP OR go OR (@OBJ) 
	LINK NOT *-1 (@OBJ) BARRIER CS OR CP)(NOT 0 Adv)
	(NOT 0 Nom LINK *-1 (TV N Gen) BARRIER NPNH)(NOT 0 TIME)
	(NOT 0 Dem LINK *1 N BARRIER NPNH)(NOT 0 Loc LINK *-1 (IV) BARRIER VERB)
	(NOT 0 (Acc PxSg3) LINK 0 Loc)(NOT 1 TV)
	(NOT *1 (@OBJ) BARRIER CC OR CS OR CP)
	(NOT 0 ("nr") LINK -1 N OR A OR ACR OR ABBR);
	## Mun oidnen guovžža golbma kilomehtara dábbelis.

REMOVE (@GN>) IF (*-1 V-TRANS-ACT BARRIER NOT-ADV-PCLE LINK 0 VERB 
	LINK NOT *-1 (@OBJ) BARRIER CS OR CP OR CC)(1 (@GP>));
	## ...ja maiddái muđui guorahallat soahpameahttunvuođa 
	## vuoigatvuođa ja duođalaš geavaheami gaskkas.

REMOVE (@GN>) IF (1 (@-FMAINV))(1 V-TRANS-ACT);
	## Divššohas dahje su ovddasteaddji galgá leat váidalusa vuolláičállán.

SELECT (@OBJ) IF (*-1 V-TRANS-ACT BARRIER VERB OR CS OR go)
	(NOT *-1 (@OBJ) BARRIER CS)(NOT 0 Loc)(NOT 0 Dem LINK *1 N BARRIER NPNH)
	(*1 ADVLCASE BARRIER NOT-ADV-PCLE LINK NOT *1 (@OBJ) BARRIER CS);
	## De olahit olbmot luosa buođđogežiin.

SELECT (@OBJ) IF (*-1 V-TRANS-ACT BARRIER NOT-ADV-PCLE)(1 Num)(2 TIME);
	## Go fylkkadoavttir lea seailluhan journála 10 jagi,...

SELECT (@OBJ) IF (NOT *-1 VFIN BARRIER CS)
	(1 COMMA LINK *1 COMMA LINK *1 V-TRANS-ACT BARRIER V-NOT-AUX);
	## Muhto gittabivdosa, nugo buođu ja njaŋgofierpmi, oažžu son atnit 
	## dušše dan čázis mii sutnje lea mearriduvvon.

SELECT (@OBJ) IF (*-1 ("deavdit") BARRIER NPNHA);
	## Go mánná lea deavdán 12 jagi,..

SELECT (@OBJ) IF (0 TIME)(*1 ("deavdit") BARRIER NOT-ADV-PCLE);
	## ...maŋŋá go lea 16 jagi deavdán.

SELECT (@OBJ) IF (1 (TV Sup) LINK NOT *1 (@OBJ) BARRIER CS OR CC);
	## Boađe sisa gáfe jugažit.

SELECT (@OBJ) IF (1 (TV Actio Loc))(NOT 1 N)(NOT *2 (@OBJ) BARRIER CS);
	## Manin don gielddát mu dan muitaleames?
 # This rule would work just fine if it were not for the fact that the 
 # verbal actio "-eames" (instead of "-eamis") is often used where we 
 # would expect a nominal actio.

SELECT (@OBJ) IF (-1 ("ollu") LINK *-1 V-TRANS-ACT BARRIER NOT-ADV-PCLE)
	(0 TIME)(1 NP-BORDER);
	## Dat gáibida ollu áiggi.

REMOVE (@GN>) IF (1 (PUNCT LEFT) LINK *1 (PUNCT RIGHT) LINK 1 NOT-NP);
	## Molssaevttolaš dálkkasdiehtaga našunála dutkanguovddáš ja Sámi 
	## dearvvašvuođadutkanguovddáš (goappašat fágabirrasat gullet Romssa 
	## universitehta vuollái) berrejit sáhttit oassálastit dán bargui.


# Coordinated objects
# - - - - - - - - - -

SELECT (@OBJ) IF (0 TIME)(1 CC)(*2 (@OBJ) BARRIER NPNH);
	## Dat gáibida almmatge sihke ollu áiggi ja resurssaid.

REMOVE (@GN>) IF (1 CC)(2C (@OBJ));
	## Ferte váldit vuhtii máná ági ja láttasvuođa ja muđui eará beliid.

SELECT (@OBJ) IF (-2C (@OBJ))(-1 CC)(1 Gen)(2 Po);
	## Jođiheaddjit galget čuovvolit mearrádusaid ja njuolggadusaid
	## giellageavaheame hárrái.


# Verbal functions
# ----------------

SELECT (@+FAUXV) IF (*1 Inf OR PrfPrc OR (Actio Ess) BARRIER CS OR CP OR VFIN);
	## Máhtte áigu heaitit vuola juhkamis.

SELECT (@+FAUXV) IF (*1 ConNeg BARRIER CS OR CP OR Neg);
	## Allos dattege šatta nu go mun dáhtun, muhto nugo don dáhtut.

REMOVE (@+FAUXV) IF (0 AUX-OR-MAIN)(NOT *1 Inf OR PrfPrc OR (Actio Ess) OR ConNeg 
	BARRIER CS OR CP);
	## Maid Máret áiggui goarrumis?

SELECT (@-FAUXV) IF (*1 Inf OR PrfPrc OR (Actio Ess) BARRIER CS OR CP);
	## In beassan dal deatnojohtima oaidnit.

REMOVE (@-FAUXV) IF (0 (@-FMAINV));
	## Sápmelaččat ledje orron dáin guovlluin 1600-logu birrasiin. 

SELECT (@-FMAINV) IF (*-1 (@SUBJ) BARRIER CS OR CP LINK 0 Sg)(0 Inf);
	## Vuoigatvuohta journála geahččat.

SELECT (@+FMAINV) IF (*-1 (@OBJ) BARRIER NOT-ADV-PCLE LINK *-1 (@SUBJ) 
	BARRIER NOT-ADV-PCLE LINK 0 Sg)(0 Sg3);
	## Juohke háve go olmmoš joddosaji molsu...

SELECT (@+FMAINV) IF (NOT *-2 VFIN BARRIER CS)(-1 (@ADVL))(NOT 0 Inf)
	(NOT *1 VFIN BARRIER CS);
	## Dalle bidjet buođđogeaži.


# Other grammatical functions
# ---------------------------


# Genitive
# - - - - -

REMOVE (@GN>) IF (1 CC)(2 ("dat" Gen));
	## Romssa universitehta sámi strategiijaplána ja dan ruhtadeami...

SELECT (@GN>) IF (NOT 0 Dem OR Interr OR PrsPrc OR (@ADVL))(*1C (@SUBJ) BARRIER NPNH)
	(NOT 1 CC OR COMMA);
	## Romssa universitehta sámi studeanttaid mátkedoarjjaortnet lea 
	## máŋga jagi leamaš mielde ásaheame...		# 4 hits here!!

REMOVE (@GN>) IF (0 (@TITLE));
	## ...maid profehta Jeremia lei sárdnon.

REMOVE (@GP<) IF (*-1C Po BARRIER NPNH);
	## Son lea dat gean birra profehta Jesaja sardnu.

REMOVE (@GP<) IF (NOT *-1 Pr BARRIER NPNHA);
	## ...ovdalgo 1883 láhka ásahuvvui.

SELECT (@GN>) IF (-1C (Num Gen))(NOT 0 Loc)(1 N);
	## Unnimus 135 jagi geavahus lea doarvái.

SELECT (@GN>) IF (NOT *-1 V-TRANS-ACT BARRIER CS)(0 N)(1 N)(*2 V-TRANS-ACT BARRIER
	V-NOT-AUX OR CC OR CS OR CP)(*2 (@OBJ) BARRIER CS OR CP);
	## Máŋga dehálaš servodatásahusa Norgga servodagas leat jitnosit dovddastan 
	## morálalaš ja politihkalaš ovddasvástádusaset duolbmamiin.

REMOVE (@OBJ) IF (-2 (A Attr) LINK NOT *-1 V-TRANS-ACT BARRIER CC OR CS OR CP)
	(-1 (N Actio));
	## ...ja nu leat mielde oppalaš gelbbolašvuođahuksemis
	## eamiálbmogiid saji ja vuoigatvuođaid ektui.
 # The Actio is nominal if preceded by (A Attr). Then a following noun is not 
 # the object of that Actio.

SELECT (@GN>) IF (NOT -1 NP-BORDER)(NOT -1 TIME)(0 TIME)(NOT 0 ("geardi"))(1 NOTIME);
	## Nugo namuhuvvui, lea nannoseamos ágga dása guhkes áiggi geavahus. 


# Attributes and quantifiers
# - - - - - - - - - - - - - -

SELECT (Prop Attr) IF (1 Prop)(1 (@SPRED));
	## Mun lean Marit Julien.

SELECT (@QN>) IF (0 Num)(*1 (@OBJ) BARRIER NPNH);
	## Dat gáibidii mis 5000 ruvnno.

SELECT (@QN>) IF (1 ("jahkásaš"));
	## Stuoret mánná, viđa-guđa jahkásaš, gal riide.

SELECT (@AN>) IF (0 ("veara"))(1 N);
	## Dattetge ii leat muitaluvvon makkárge namahan veara geavahanriiddu 
	## birra dálolaččaid ja boazodoalu gaskkas.

REMOVE (@AN>) IF (NOT *1 N OR A BARRIER NPNH);
	## Sii eat sáhte čielgasit diehtán ahte... 

SELECT (@NUMBER) IF (-2 (@NUMBER))(-1 CC);
	## ...gč. dearvvašvuođabargiidlága kapihttal 6 ja 7.
	
REMOVE (@QN>) IF (NOT 0 ("okta" Ess) LINK *1 Ess BARRIER NPNH)(0 (@ADVL))
    (1 N OR A OR Pron) ;	
	## Ráđđehus oktii áigodagas galgá bidjat ovdan dieđáhusa Stuorradiggái

SELECT  (@QN>) IF (0 ("okta" Ess) LINK *1 Ess BARRIER NPNH) ;
    ## Son vuodjá oktan guollin.
	


# Adverbials
# - - - - - -

SELECT (@ADVL) IF (0 (@SUBJ))(*-1C (@SUBJ) BARRIER CS OR CP OR CC);
	## Boazu dáidá buot ovddimus leat fievrun adnojuvvot dan láhkái ahte...
 
SELECT (@ADVL) IF (NOT 0 ("okta" Ess) LINK *1 Ess BARRIER NPNH)(0 Num)
    (*-1 V-NOT-AUX LINK 0 IV);
	## Fierbmi bistá guokte-golbma jagi.

SELECT (@ADVL) IF (*-2 (@OBJ) BARRIER CC OR CLB OR CS OR CP OR Pr)(-1 NUMERALS)
	(0 TIME OR MEASURE);
	## Mii doalvvuimet ealu guokte miilla dan beaivve. 

SELECT (@ADVL) IF (*-1 INF-VERB BARRIER NOT-ADV-PCLE)(0 Num)(1 TIME)
	(*2 Inf BARRIER NOT-ADV-PCLE);
	## ...mas sáhttá ain máŋga jagi addit gieldda ássiide boaldin...

SELECT (@ADVL) IF (*-1 (@OBJ) BARRIER NOT-ADV-PCLE OR go)(0 Num)(1 TIME);
	## Go fylkkadoavttir lea seailluhan journála 10 jagi, de...

SELECT (@ADVL) IF (0 ("diibmu") OR ("dbm"))(1 Num);
	## Mun boađán diibmu vihtta.

SELECT (@ADVL) IF (NOT *-1 ("deavdit") BARRIER VERB OR CC OR CLB OR CS OR CP)
	(-1 Num LINK NOT 0 (@GP<))(0 TIME LINK NOT 0 Nom)
	(NOT *1 ("deavdit") BARRIER NOT-ADV-PCLE)(1C NOT-NP)(NOT 1 ("dassá"));
	## Dábálaš noađđeráidduin vázzá olmmoš guokte- golbma tiimmu ovdal go
	## suhpoda.

SELECT (@ADVL) IF (0 TIME)(1 TIME);
	## Galggat golmma geardde beaivvis borrat ovtta tableahta.

SELECT (@ADVL) IF (*-1 VERB BARRIER NOT-ADV-PCLE)(0 ("jahki"))(1 Num);
	## ...ja ráhkadii jagi 1991 fylkkagieldda sámi dearvvašvuođa- ja 
	## sosiálaplána.

REMOVE (@SUBJ) IF (0 (@ADVL))(*1C (@SUBJ) BARRIER CC OR CLB OR CS OR CP);
	## Mo dal juo dilli leašge,...


# Various
# - - - -

REMOVE (@SUBJ) IF (*-1 (@SUBJ) BARRIER CC OR CS OR CLB LINK NOT 0 Gen)(0 Gen);
	## Salomo, gean eadni lei Uria áhkká,...

REMOVE (@OBJ) IF (0 Gen)(*1 Ill BARRIER NPNH LINK *1 V-TRANS-ACT BARRIER NOT-ADV-PCLE
	OR PUNCT OR CLB LINK 0 V-NOT-AUX LINK *1 Inf BARRIER NOT-ADV-PCLE);
	## Eamiálbmotrievtti guovddášosiide gullá maiddái sihkkarastit (dahje 
	## milloseappot jođálmahttit) kultuvrra earáláhkai.

REMOVE (@OBJ) IF (NOT *-1 V-TRANS-ACT BARRIER CC OR CLB OR CP OR CS)
	(*1 V-TRANS-ACT BARRIER CLB OR CS OR CP OR CC LINK 0 AUX 
	LINK *1 V-TRANS-ACT BARRIER NOT-ADV-PCLE LINK 0 V-MAIN LINK *1C (@OBJ) 
	BARRIER CLB OR CC OR CS OR CP);
	## Ii Finnmárkku eatnamiid oktasaš hálddašanortnegii sáhte laktit guovlluid, ...

REMOVE (@OBJ) IF (0 Gen)(*1 (@SUBJ) BARRIER NPNH LINK *1 ("gullat") BARRIER V-MAIN OR 
	CLB OR CC OR CS OR CP LINK *1 Ill BARRIER CLB OR CC OR CS OR CP);
	## ...maid oamastanvuoigatvuohta gullá konvenšuvnna vuođul sápmelaččaide.

SELECT (@GP<) IF (*-1 Pr BARRIER NPNH);
	## Dalle bisttii luossaguolli miehtá dálvvi bearaš atnui.

SELECT (@HNOUN) IF (*-1 BOS BARRIER NPNH)(1 EOS);
	## Riektejoavkku nammadeami duogáš.

 
# ----------- #
  CONSTRAINTS
# ----------- #

REMOVE (@+FAUXV) ;


REMOVE (@SPRED) ; #IF (NOT 0 PrfPrc);


REMOVE (@OPRED) ;

#REMOVE (@X) IF (0 (@+FAUXV) OR (@+FMAINV) OR (@-FAUXV) OR (@-FMAINV) OR (@-FSUBJ) OR
#                  (@ADV-A) OR (@ADV-ADV) OR (@ADVL) OR (@AN>) OR (@ActioN>) OR (@CC) OR
#                  (@CMPND) OR (@CS) OR (@DN>) OR (@GA>) OR (@GN>) OR (@GP<) OR (@GP>) OR
#                  (@HNOUN) OR (@INTERJ) OR (@MEASURE) OR (@NPRON<) OR (@NQ<) OR 
#                  (@NUM-PRON) OR (@NUMBER) OR (@OBJ) OR (@OPRED) OR (@PCLE) OR 
#                  (@PCLE-COMPL) OR (@PROP>) OR (@PrcN>) OR (@PronN<) OR (@PronN>) OR 
#                  (@QN<) OR (@QN>) OR (@SPRED) OR
#                  (@SUBJ) OR (@SUBJ-QH) OR (@TIME) OR (@TITLE) OR (@VOC)) ;



# ===================================================== #
# Post-syntactic morphological disambiguation - Cycle 5 #
# ===================================================== #
                      CONSTRAINTS                       #
# ===================================================== #



# ========================== #
# Tidying up some loose ends #
# ========================== #

REMOVE Gen IF (0 (ACR Nom));
REMOVE Acc IF (0 (ACR Nom));
    ## IT lea dehálaš.

REMOVE Gen IF (0 (Num Nom));
REMOVE Acc IF (0 (Num Nom));
 # This in order to constrain the proliferation of case. If no context has hit, 
 # then be it a stray nominative.

REMOVE (@X) ; # Kein Spur...
 # @X is our "didn§t find a syntactic tag -tag.
 # Here we remove it if we have any other suggestion.

#=====#
  END #
#=====#



# Notes for future thought:
# =========================

#Add a rule REMOVE (@X) at the end?
