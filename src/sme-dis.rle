# ==================================================================== #
# ==================================================================== #
#        N O R T H E R N   S Á M I   D I S A M B I G U A T O R         #
# ==================================================================== #
# ==================================================================== #

#    Sámi language technology project 2003-2006, University of Tromsø  #



# Quasicode for m4 addition
# =========================

# (This documentation shall be removed to after this ToC when the dust has settled)
# We will add m4 rules to this file, in order to open for the optional inclusion
# and exclusion of certain rules. The first rules to be tested are some rules
# that remove derived readings where there are non-derived ones available.

# Todo: The m4-code should give a command-line option to includ and exclude certain rules, e.g.
# vislcg --grammar=grammarfile --exclude=namnedoption ...
# where namedoption is to be specified.

# The relevant rules are identified below, as
# m4 starts (<optionname>) 
# m4 ends



            # =========================================== #
            #            Short table of contents          #
            # =========================================== #

            # Delimiters
            # Tags and sets
	        # Mapping rules
            # Disambiguation rules
              # One-cohort disambiguation - cycle 0
              # Local disambiguation - cycles 1 and 2
              # Cycle 1a: Special cases 
              # Cycle 1b: Cleaning up after the special cases
              # Cycle 2: Other local disambiguation         
              # Cycle 3: Global disambiguation 
	          # Cycle 4: Syntactic disambiguation 
	          # Cycle 5: Post-syntactic morphological disambiguation


            # =========================================== #
            #         Detailed table of contents          #
            # =========================================== #
                 
            # Delimiters
            # Tags and sets
	        # Mapping rules
	          # Mappings of minor categories
	          # Determiner mappings
	          # Attribute mappings
	          # Genitive mappings
	          # Quantifier mappings
	          # Fragment mappings
	          # Verb mappings
	          # Complements of particles in comparison
	          # Other special nominative mappings
	          # Arguments and secondary predicates
	          # Adverbial mappings
	          # Default mapping
             # Disambiguation rules
              # One-cohort disambiguation - cycle 0
                # Lexicalised names
                # Lexicalised derivations
                # Lexicalised adjectives
                # Adjectives or nouns, not adverbs
                # Lexicalised adverbs
                # Adverbs, not nouns or adjectives
                # Lexical N versus Actio
                # Nouns, not adverbs or adjectives 
                # Nouns, not verbs
                # Nouns, verbs or numerals
                # Not noun
                # Temporal expressions with locative case
                # Adposition + Px
                # Some verb rules
               # Local disambiguation - cycles 1 and 2
                # Cycle 1a: Special cases 
                  # Personal pronouns
                  # An early rule for "eanaš"/"eanas"
                  # Px constraints
                  # Certain verb readings
                  # A rule for "dušše"
                  # ahte
                # Cycle 1b: Cleaning up after the special cases
                  # Removing (nearly) all remaining Px readings
                # Cycle 2: Other local disambiguation         
                  # Noun or not?
		          # Adjectives and adverbs
		          # Disambiguating abbreviations
                  # Disambiguating clitics
                  # Disambiguating numerals
                  # Disambiguating adpositions
                  # Disambiguating subjunctions
                  # Disambiguating adverbs
                  # Disambiguating pronouns
                  # Disambiguating adjectives
                  # Disambiguating verbs - part 1
                  # Disambiguating nouns
                  # Disambiguating verbs - part 2
                  # Residual cases
                # Cycle 3: Global disambiguation 
                  # Nouns
                  # Verbs             
		        # Cycle 4: Syntactic disambiguation 
		          # Subject
		          # Subject predicate
		          # Subject or not
		          # Fragments
		          # Object and object predicate
		          # Verbal functions
		          # Other grammatical functions  
		        # Cycle 5: Post-syntactic morphological disambiguation
		          # (Cycle 5 is still EMPTY!! See src/sme-tdis.rle)  


# ========== #
# Delimiters #
# ========== #


DELIMITERS = "<.>" "<!>" "<?>" "<...>" "<¶>";



# ============= #
# Tags and sets #
# ============= #


SETS # (Remove all lines that start with SETS if you want to use mdis.)

# I define end of clause and beginning of clause in a way so that the file
# may be read both by the CG-2 and the vislcg formalisms.

LIST BOS = (>>>) (<s>); LIST EOS = (<<<) (</s>); # vislcg and CG-2 together.
#LIST BOS = (>>>); LIST EOS = (<<<); # end and beg. of sentence. for vislcg.


# Tags declared as single-membered LISTs 
# ======================================

# Parts of speech
# ---------------

LIST N = N ; LIST A = A ; LIST Adv = Adv ; LIST V = V ; LIST Pron = Pron ;
LIST CS = CS ; LIST CC = CC ; LIST Po = Po ; LIST Pr = Pr ;
LIST Pcle = Pcle ; LIST Num = Num ; LIST Prop = Prop ;
LIST LEFT = LEFT ; LIST RIGHT = RIGHT ;

# POS sub-categories
# ------------------

LIST Pers = Pers ; LIST Dem = Dem ; LIST Interr = Interr ;
LIST Indef = Indef ;
LIST Recipr = Recipr ; LIST Refl = Refl ; LIST Rel = Rel ; 
LIST Interj = Interj ; LIST Adp = Adp ; 

# Morphosyntactic properties
# --------------------------

LIST Nom = Nom ; LIST Acc = Acc ;
LIST Gen = Gen ; LIST Ill = Ill ;
LIST Loc = Loc ; LIST Com = Com ;
LIST Ess = Ess ;

LIST Sg = Sg ; LIST Pl = Pl ; LIST Du = Du ;

LIST Cmpnd = Cmpnd ; 
LIST SgNomCmp = SgNomCmp ; 
LIST SgGenCmp = SgGenCmp ;

LIST PxSg1 = PxSg1 ; LIST PxSg2 = PxSg2 ; LIST PxSg3 = PxSg3 ; 
LIST PxDu1 = PxDu1 ; LIST PxDu2 = PxDu2 ; LIST PxDu3 = PxDu3 ; 
LIST PxPl1 = PxPl1 ; LIST PxPl2 = PxPl2 ; LIST PxPl3 = PxPl3 ;

LIST Comp = Comp ; LIST Superl = Superl ;

LIST Attr = Attr ; LIST Card = Card ; LIST Ord = Ord ; 
LIST Date = Date ; LIST Range = Range ;
LIST Qst = Qst ; LIST Foc = Foc ;

LIST IV = IV ; LIST TV = TV ;
LIST Pass = Pass ;
LIST Prt = Prt; LIST Prs = Prs ; 

LIST Ind = Ind ; LIST Pot = Pot ; LIST Cond = Cond ;
LIST Imprt = Imprt ; LIST ImprtII = ImprtII ;

LIST Sg1 = Sg1 ;  LIST Sg2 = Sg2 ; LIST Sg3 = Sg3 ;
LIST Du1 = Du1 ;  LIST Du2 = Du2 ; LIST Du3 = Du3 ;
LIST Pl1 = Pl1 ;  LIST Pl2 = Pl2 ; LIST Pl3 = Pl3 ;

LIST Inf = Inf ; LIST ConNeg = ConNeg ;  LIST Neg = Neg ;
LIST PrfPrc = PrfPrc ; LIST VGen = VGen ;
LIST PrsPrc = PrsPrc ; 

LIST Ger = Ger ; LIST Sup = Sup ; LIST Actio = Actio ;
LIST Actor = Actor ; LIST VAbess = VAbess ;

# Derivation
# ----------

LIST Der/adda = Der/adda ;
LIST Der/ahtti = Der/ahtti ;
LIST Der/alla = Der/alla ;
LIST Der/asti = Der/asti ;
LIST Der/easti = Der/easti ;
LIST Der/aš = Der/aš ;
LIST Der/d = Der/d ;
LIST Der/eaddji = Der/eaddji ;
LIST Der/eamoš = Der/eamoš ;
LIST Der/amoš = Der/amoš ;
LIST Der/eapmi = Der/eapmi ;
LIST Der/g = Der/g ;
LIST Der/geahtes = Der/geahtes ;
LIST Der/gielat = Der/gielat ;
LIST !better: = !better: ;
LIST Der/NuA = Der/NuA ;

LIST Der/h = Der/h ;
LIST Der/heapmi = Der/heapmi ;
LIST Der/hudda = Der/hudda ;
LIST Der/huhtti = Der/huhtti ;
LIST Der/huvva = Der/huvva ;
LIST Der/halla = Der/halla ;
LIST Der/j = Der/j ;
LIST Der/l = Der/l ;
LIST Der/laš = Der/laš ;
LIST Der/las = Der/las ;
LIST Der/hat = Der/hat ;

LIST Der/meahttun = Der/meahttun ;
LIST Der/muš = Der/muš ;
LIST Der/n = Der/n ;
LIST Der/š = Der/š ;
LIST Der/st = Der/st ;
LIST Der/stuvva = Der/stuvva ;
LIST Der/upmi = Der/upmi ;
LIST Der/supmi = Der/supmi ;
LIST Der/us = Der/us ;
LIST Der/vuohta = Der/vuohta ;
LIST Der/goahti = Der/goahti ;

LIST Der/lágan = Der/lágan ;
LIST Der/lágán = Der/lágán ;
LIST Der/lágaš = Der/lágaš ;
LIST Der/jagáš = Der/jagáš ;

LIST Der/Dimin = Der/Dimin ;
LIST Der/viđá = Der/viđá ;
LIST Der/viđi = Der/viđi ;
LIST Der/veara = Der/veara ;
LIST Der/duohke = Der/duohke ;
LIST Der/duohkai = Der/duohkai ;
LIST Der/vuolle = Der/vuolle ;
LIST Der/vuollai = Der/vuollai ;
LIST Der/vuolde = Der/vuolde ;



# Syntactic tags

LIST @+FAUXV = @+FAUXV ; LIST @+FMAINV = @+FMAINV ; LIST @-FAUXV = @-FAUXV ; 
LIST @-FMAINV = @-FMAINV ; LIST @-FSUBJ = @-FSUBJ ; 

LIST @ADV-A = @ADV-A ; LIST @ADV-ADV = @ADV-ADV ; LIST @ADVL = @ADVL ; 

LIST @AN> = @AN> ; LIST @ActioN> = @ActioN> ; LIST @CC = @CC ; LIST @CMPND = @CMPND ; LIST @CS = @CS ; 
LIST @DN> = @DN> ; LIST @GA> = @GA> ; LIST @GN> = @GN> ; LIST @GP< = @GP< ; LIST @GP> = @GP> ; 
LIST @HNOUN = @HNOUN ; LIST @INTERJ = @INTERJ ; 
LIST @NNum> = @NNum>; LIST @NumN< = @NumN<;
LIST @NPron< = @NPron< ;
LIST @NQ< = @NQ< ;  LIST @NUM-PRON = @NUM-PRON ; #LIST @NUMBER = @NUMBER ;
LIST @OBJ = @OBJ ; 
LIST @OPRED = @OPRED ; LIST @PCLE = @PCLE ; LIST @CS-COMPL = @CS-COMPL ; LIST @PROP> = @PROP> ; 
LIST @PrcN> = @PrcN> ; LIST @PronN< = @PronN< ; LIST @PronN> = @PronN> ;  LIST @QN< = @QN< ; 
LIST @QN> = @QN> ; LIST @SPRED = @SPRED ; LIST @SUBJ = @SUBJ ; LIST @SUBJ-QH = @SUBJ-QH ; 
LIST @APP = @APP ;

# LIST @MEASURE = @MEASURE ; #Not in use anymore. We use @QN<.


LIST @TITLE = @TITLE ; LIST @VOC = @VOC ; LIST @X = @X ;

# Semantic tags
# -------------

LIST Ani = Ani ; LIST Fem = Fem ; LIST Mal = Mal ;
LIST Obj = Obj ; List Org = Org ; LIST Plc = Plc ;
LIST Sur = Sur ; LIST Tit = Tit ;


# Other tags
# ----------

LIST ABBR = ABBR ; LIST ACR = ACR ; LIST CLB = CLB ;
LIST PUNCT = PUNCT ;


# Single-word sets
# ----------------
 
LIST COMMA = "," ; LIST ¶ = ¶;

LIST OKTA = "okta"; LIST go = "go" ; 

# Initials
# --------

LIST INITIAL =  "a" "b" "c" "d" "e" "f" "g" "h" "i" "j" "k" "l" "m"
                "n" "o" "p" "q" "r" "s" "t" "u" "v" "w" "x" "y" "z"
                "á" "æ" "ø" "å" "ö" "ä" ;




# Tag sets
# ========
   SETS
# ========

LIST WORD = N A Adv V Pron CS CC Po Pr Interj Pcle Num ABBR ACR Date Range 
; # any word

SET REALWORD = WORD - Num - Ord ;

# Derivational affixes
# --------------------

LIST V-DER = Der/adda Der/alla Der/d Der/g Der/h Der/asti Der/hudda Der/huhtti Der/huvva Der/j Der/l Der/ahtti Der/st Der/stuvva Der/goahti ;

LIST N-DER = Der/eaddji Der/eamoš Der/eapmi Der/heapmi Der/š Der/muš Der/upmi Der/us Der/vuohta Actor ; # Actio ;

LIST A-DER = Der/geahtes Der/keahtes Der/heapmi Der/lágan Der/laš Der/meahttun ;

#SET DER = V-DER | N-DER | A-DER ;


# Case sets
# ---------

LIST ADVLCASE = Ill Loc Com Ess ;

LIST CASE = Nom Acc Gen Ill Loc Com Ess ;

SET NOT-NOM = CASE - Nom ;
SET NOT-GEN = CASE - Gen ;
SET NOT-ACC = CASE - Acc ;


# Verb sets
# ---------

# Verbs and their complements
# - - - - - - - - - - - - - -

SET NOT-VERB = WORD - V ;

# Finiteness and mood
# - - - - - - - - - -

SET V-IND-FIN = Prs | Prt ; # Problem: "In boahtán" is an invisible indicative

SET V-MOOD = Ind | Pot | Imprt | ImprtII | Cond ;

SET VFIN = V-MOOD - ConNeg | ("gč");

SET VFIN-POS = V-MOOD - ConNeg - Neg ;

#SET VFIN-NOT-IMP =  Ind | Pot | Cond ;
SET VFIN-NOT-IMP = VFIN - Imprt ;

SET VFIN-NOT-NEG = VFIN - (Neg Foc) ;
    # this might be to strict, besides, "iige" can be written "ii ge"


# Person
# - - - - 

LIST V-SG1 = (V  Ind Prs Sg1) (V Ind Prt Sg1) (V Cond Prs Sg1) 
             (V Cond Prt Sg1) (V Pot Prs Sg1) (V  Neg Ind Sg1); 
LIST V-SG2 = (V  Ind Prs Sg2) (V Ind Prt Sg2) (V Cond Prs Sg2) 
             (V Cond Prt Sg2) (V Pot Prs Sg2) (V  Neg Ind Sg2); 
LIST V-SG3 = (V  Ind Prs Sg3) (V Ind Prt Sg3) (V Cond Prs Sg3) 
             (V Cond Prt Sg3) (V Pot Prs Sg3) (V  Neg Ind Sg3); 
LIST V-DU1 = (V  Ind Prs Du1) (V Ind Prt Du1) (V Cond Prs Du1) 
	         (V Cond Prt Du1) (V Pot Prs Du1) (V  Neg Ind Du1); 
LIST V-DU2 = (V  Ind Prs Du2) (V Ind Prt Du2) (V Cond Prs Du2) 
	         (V Cond Prt Du2) (V Pot Prs Du2) (V  Neg Ind Du2); 
LIST V-DU3 = (V  Ind Prs Du3) (V Ind Prt Du3) (V Cond Prs Du3) 
	         (V Cond Prt Du3) (V Pot Prs Du3) (V  Neg Ind Du3); 
LIST V-PL1 = (V  Ind Prs Pl1) (V Ind Prt Pl1) (V Cond Prs Pl1) 
             (V Cond Prt Pl1) (V Pot Prs Pl1) (V  Neg Ind Pl1); 
LIST V-PL2 = (V  Ind Prs Pl2) (V Ind Prt Pl2) (V Cond Prs Pl2) 
             (V Cond Prt Pl2) (V Pot Prs Pl2) (V  Neg Ind Pl2); 
LIST V-PL3 = (V  Ind Prs Pl3) (V Ind Prt Pl3) (V Cond Prs Pl3) 
             (V Cond Prt Pl3) (V Pot Prs Pl3) (V  Neg Ind Pl3); 

 # Note that imperative verbs are not included in these sets!


# Some subsets of the VFIN sets
# - - - - - - - - - - - - - - -
SET V-SG = V-SG1 | V-SG2 | V-SG3 ;
SET V-DU = V-DU1 | V-DU2 | V-DU3 ;
SET V-PL = V-PL1 | V-PL2 | V-PL3 ;

SET V-DU-PL = V-DU1 | V-DU2 | V-DU3 | V-PL1 | V-PL2 | V-PL3 ;

SET V-NOT-SG1 = VFIN-NOT-IMP - V-SG1 ;

SET V-NOT-SG3 = V-SG1 | V-SG2 | V-DU1 | V-DU2 | V-DU3 |
                V-PL1 | V-PL2 | V-PL3 ;

SET V-1-2 = V-SG1 | V-SG2 | V-DU1 | V-DU2 | V-PL1 | V-PL2 ;
SET V-3   = V-SG3 | V-DU3 | V-PL3 ;


# Sets consisting of forms of "leat" (these ones need to be rewritten)
# - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -

SET LEAN = ("leat" V Ind Prs Sg1)|("leat" V PrfPrc);
SET LEAI = ("leat" V Ind Prt Sg3)|("leat" V Imprt Prs Sg3);
SET LEAT = ("leat" V Ind Prs Pl1)|("leat" V Ind Prs Pl3)|
           ("leat" V Ind Prs Sg2)|("leat" V Inf)|("leat" V Ind Prs ConNeg);

SET LEAT-DU-PL = ("leat" V Du1)|("leat" V Du2)|("leat" V Du3)|
                 ("leat" V Pl1)|("leat" V Pl2)|("leat" V Pl3);

SET LEAT-DU3-PL3 = ("leat" V Du3) | ("leat" V Pl3); # Not referred to by any rule.

LIST LEAT-FIN-NOT-IMP = ("leat" V Ind) ("leat" V Cond) ("leat" V Pot); 

LIST LEAN-SG1 = ("leat" V Ind Prs Sg1);

LIST HABEO-SG3 = ("leat" Sg3) (V Neg Sg3) ("šaddat" Sg3) ;

LIST HABEO-PL3 = ("leat" Pl3) (V Neg Pl3) ("šaddat" Pl3) ;

SET HABEO-3 = HABEO-SG3 | HABEO-PL3 ;


# Pronoun sets
# ------------
SETS

SET MUN = (Pron Pers Sg1 Nom);
SET DON = (Pron Pers Sg2 Nom);
SET SON = ("son" Pron Pers Sg3 Nom);
SET MOAI = (Pron Pers Du1 Nom);
SET DOAI = (Pron Pers Du2 Nom);
SET SOAI = (Pron Pers Du3 Nom);
SET MII-PERS = (Pron Pers Pl1 Nom);
SET DII = (Pron Pers Pl2 Nom);
SET SII = ("son" Pron Pers Pl3 Nom);

SET PPRON-DU-PL = MOAI | DOAI | SOAI | MII-PERS | DII | SII ;

SET PRON-DU = MOAI | DOAI | SOAI ;

SET PPRON-NOT-SII = MUN | DON | SON | MOAI | DOAI | SOAI | MII-PERS | DII ;

LIST PPRON-GEN = (Sg1 Gen) (Sg2 Gen) (Sg3 Gen) (Du1 Gen) (Du2 Gen) (Du3 Gen) 
	(Pl1 Gen) (Pl2 Gen) (Pl3 Gen);

SET PPRON-NOT-GEN = (Pron Pers) - PPRON-GEN ;

SET DEM-SG = (Pron Dem Sg Nom);
SET DEM-PL = (Pron Dem Pl Nom);
SET NOT-DEM = WORD - Dem ;


# Adjectival sets and their complements
# -------------------------------------

SET A-ATTR = (A Attr) ; # Used in one rule. Not needed? 

SET LEX-A = A - A-DER ;

SET A-CASE = A - Attr - Adv ;

LIST A-CC = A CC ;

SET NOT-ADJ = WORD - A ;
SET NOT-ADJ-COMMA = WORD - A - COMMA ;
SET NOT-Attr = WORD - Attr ;
SET NOT-ADJ-PCLE = WORD - A - Pcle ;
SET NOT-ADJ-CC = WORD - A-CC ;

SET NOT-AA = WORD - A - Adv ;

LIST NOMINAL-ADJ = "guoktilaš" "lámis" "oasálaš" ("suddu" laš) "viissis"; 
 # and many others

 
# Adverbial sets and their complements
# ------------------------------------

SET LEX-ADV = Adv - (A*) ;
# LIST LEX-ADV = "gusto";

SET ADVERBIAL = Adv | Ill | Loc | Com | Ess ;

SET NOT-ADV = WORD - Adv;
SET NOT-ADV-PCLE = WORD - Adv - Pcle ;
SET NOT-ADV-INDEF = WORD - Adv - Indef ;
SET NOT-ADV-PCLE-ILL = WORD - Adv - Pcle - Ill ;
SET NOT-ADV-PCLE-INDEF = WORD - Adv - Pcle - Indef ;
SET NOT-ADV-PCLE-NEG = WORD - Adv - Pcle - Neg ;
SET NOT-ADVL-PCLE-NEG = WORD - @ADVL - @GP< - Pcle - Neg ; #Ask Eckhardt.

LIST IKTE = "ikte" "dolin" "dovle" ;

LIST MO = "goas" "gokko" "gos" "gosa" "makkár" "manne" "mo" "mot" "movt" 
	"nugo" (V Qst); 
 # Introduce finite clauses.
 

# Coordinators
# - - - - - - -

LIST XGO = "dego" "nugo" ("seamma#ládjego" Qst) ("seamma#láhkai" Qst) ;
    # Compounds
LIST SEAMMAX = "seamma#ládje" "seamma#láhkai" ;
    # Those combine with go

# Sets for adverbs that have lookalikes
# - - - - - - - - - - - - - - - - - - -
 # Here come some adverbs that have identical twins in other POS. 
 # If these are found in Adv contexts, we treat them as adverbs.

LIST ADV-NOT-VERB = "dušše" ;

LIST ADV-NOT-NOUN = "easka" "várra" ;

LIST ADV-NOT-NAMES = "Ain" "Anne" "Diego" "Dieppe" "Enge" "Galle" "Haga"
                      "Joba" "Johan" "Liikka" "Mai" "Mannes" "Mo" "Mot" "Naba"
                      "Nan" "Oktan" "Sierra" "Sokka" "Villa" ;

SET ADV-NOT-OTHER-POS = ADV-NOT-VERB | ADV-NOT-NOUN | ADV-NOT-NAMES ;



# Sets of elements with common syntactic behaviour
# ================================================

SETS

# VERBS
# -----

SET VERB = V - (V N); # - (TV N) - (Actio Ill) - (Actio Nom) - (Actio Gen) - (Actio Acc) - (Actio Loc) - (Actio Com) ;
   # V is all readings with a V tag in them, VERB should
                        # be the ones without an N tag following the V

SET LEX-V = VERB - V-DER;             # VERB but not V-DER


SET V-NOT-CMPND = V - Cmpnd; # Not in use.


LIST COPULAS = "dáidit" "bissut" "gártat" "leahkit" "leat" "orrut" "šaddat" ;
 # 'Dáidit' can appear without 'leat'.

SET V-NOT-COP = V - COPULAS ;

LIST MOD-ASP = "berret" "dáidit" "fertet" "galgat" "gillet" "lávet" "sáhttit" 
		"seahtit" "soaitit" "viggat" ;
 # Verbs that never have arguments of their own - so we leave out "áigut", for ex.

LIST AUX-OR-MAIN = "áigut" "beassat" "hálidit" "háliidit" "máhttit" "nagodit"  
			"ollit" "orrut" "sihtat" "veadjit";
 # These verbs can take arguments, so they do not belong in the AUX group, 
 # but they are nevertheless mapped to (@+FAUXV). 

SET AUX = COPULAS | Neg | MOD-ASP | AUX-OR-MAIN ; 

SET V-NOT-AUX = V - AUX ;  # The set of verbs that cannot be auxiliaries. 
			   # Does not include AUX-OR-MAIN.

SET VFIN-NOT-AUX = VFIN - AUX ;

SET V-MAIN = V - COPULAS - Neg - MOD-ASP | ("orrut") ;  # The set of verbs that can be 
							# main verbs. Includes 
							# AUX-OR-MAIN.

SET V-DER-PASS = V + V-DER + Pass;    # + is intersection, this should work
				      # The set is used in one rule with no hits.
	
SET V-PASS = (V TV Pass);

SET V-NOT-AUX-PASS = V - AUX - V-PASS ; # No longer in use.

SET V-ACT = V - Pass; # No longer in use.

SET ACT-PRFP = PrfPrc - V-PASS; # Appears in one rule.

SET V-TRANS-ACT = TV - Pass ;#- (Actio Loc) - Actio ; #XXX Never acc-obj for a.l. or actor?
 # All active verbs with a TV tag, including AUX-OR-MAIN.

SET V-TRANS-ACT-SG3 = V-SG3 - Pass ; 
 
SET V-TRANS-ACT-NOT-AUX  = TV - V-PASS - AUX ;

SET V-TRANS-ACT-NOT-ACTIO = TV - V-PASS - Actio ;



# Sets for verbs choosing oblique objects or adverbials
# - - - - - - - - - - - - - - - - - - - - - - - - - - - 

LIST ST-VERB = "addit" "čuohppat" "dadjat" "dárbbahit" "dárbbašit" "deattuhit" "fidnet" "geavahit" "hábmet" "lágidit" "luoikat" "luoikkahit" "oaidnit" "oastit" "oažžut" "olahit" "ovddidit" "ráhkadit" "suoládit" "váidalit" "váldit" "vásihit" "váttásmahttit" "vuojuhit" ;
	# Strictly transitiv-verbs - a start. Borrat should be there.
	# These verbs: "addit", "fidnet" and "oažžut" can in special cases be without object: 
	## Son oaččui sus.  

LIST LOC-VERB = "ávkkástallat" "ballat" "beassat" "beroštit" "biehttalit" "bihtit" "boahtit" "ceavzit" "čuoččut" "čuovvut" "dinet" "dolkat" "eastadallat" "eastadit" "fuolahit" "fuollat" "geargat" "heaitit" "ilbmat" "jearrat" "luohpat" "nohkkot" "spiehkastit" "váruhit" "veallát" "oassálastit" "válga#oassálastit";
		#Note that "fuolahit" obviously has two homonymous variants: 
		#fuolahit1+Acc, fuolahit2+Loc

LIST COM-VERB = "árvalit" "árvvohuššat" "ávkašuvvat" "bargat" "bártašuvvat" "buohtastahttit" "deaivvadit" "háladit" "hilbošit" "humadit" "joatkit" "leaikkastallat" "meannudit" "molssodit" "náitalit" "riidalit" "ságastaddat" "ságastallat" "šiehtadit" ; 
		#Note that "leaikkástallat" has two homonymous variants:
		#leaikkástallat1 (IV), leaikkástallat2+Com

LIST ILL-VERB = "áibbašit" "álgit" "báitit" "beassat" "čohkkedit" "čujuhit" "čuohcit" "deaivat" "doaškut" "dorvvastit" "došket" "duhtat" "geahččat" "gullat" "guoskat" "gustot" "hárjánit" "heivet" "irggástallat" "jáhkkit" "liikot" "luohttit" "mannat" "ollet" "oskut" "riepmat" "ráhkkanit" "soahpat" "suhttat" "vuolgit"; 		

LIST TRANSL-VERB = "jápmit" "duššat" "hávkat" "náitalit" "gaskkalduvvat" "nohkat" "goarránit" "heahpanit" "boakčánit" "šlundut" "ráigánit" "vuollánit" "gopmánit" "fuotnánit" "roahppánit" "reakčanit" "bieđganit" "luoddanit" "guvggodit" "liegganit" "suorganit" "báhkkanit" "cuovkanit" "hedjonit" "lohppet" "goikat" "galbmot" "goallut" "ruostut" "buoidut" "guvggodit" "suvrut" "suddat" ;
    # Taken from Helander (2001) p. 89, might be useful in the future as those verbs typically take humans and places in illative case.


LIST ACC-LOC-VERB = "biehttalit" "earuhit" "gieldit" "jearrat" "viežžat" ;

LIST ACC-ILL-VERB = "addit" "bidjat" "mearkkašit";

LIST MIELDE-VERB = "addit" "bidjat" "dáidit" "leat" "oastit" "oažžut" "váldit" ;


# Other verb sets according to valency
# - - - - - - - - - - - - - - - - - - -

LIST INF-VERB = "adnot" "astat" "ádjánit" "áigut" "álgit" "ásahit" "bágget" "bávččagit" "beassat" "berret" "bivdit" "bivvat" "bistit" "boahtit" "bovdet" "čohkkát" "čohkkedit" "čohkkánit" "čuoččahit" "čuoččastit" "čuorvut" "čurggodit" ("dadjat" Pass) "dagahit" "dáhttut" "dáidit" "dárbbašit" "diktit" "doaivut" "doapmat" "duostat" "fertet" "fuobmát" "fuolahit" "galgat" "gáibidit" "gárrut" "gártat" "geahččalit" "geatnegahttit" "gierdat" "gillet" "gohččut" "háhppehit" "hálidit" "háliidit" "hárjánit" "heivet" "lávet" "mannat" "máhttit" "máššat" "movttiidahttit" "muitit" "nagodit" "navdit" "oahpahit" "oahppat" "oažžut" "ollet" "ribahit" "riepmat" "sáhttit" "seahtit" "sihtat" "soaitit" "suovvat" "šaddat" "stađđat" "veadjit" "viggat" "viššat" "vuogáiduvvat" "vuolgit" "vuollánit" "vuordit"; 

LIST ACC-INF-VERB = "bivdit" "čuorvut" "dagahit" "diktit" "gohččut" "movttiidahttit" "sihtat" "suovvat" ;

 # These are verbs taking accusative objects and infinitives; 
 # a subset of INF-VERB.
	## Ii suova nieiddas náitalit.


LIST OPRED-VERB = "atnit" "dadjat" "dahkat" "dulkot" "geavahit" "gohčodit" "gohččut" "ráhkadit" "válljet";
 # Verbs that occur with objects that have secondary predicates.
 
LIST TOGETHER-VERB = "goallostuvvot" "beaškkehat" "bidjat" "čatnit" "suddat" "heivet" "biddjojuvvot" "časkit" "geassit" "gullat" "rehkenastit" "ordnet" "soahpat" "gávnnadit" "heivehit" "čatnat" "doaibmat" "iskat" "laktit" "seaguhit";
 # Danger: This list could be to long, in certain respects. 
 # TODO: Think of counter examples, i.e. sentences where we have these verbs, but
 # where oktii is not an adverbial.
 
LIST SOURCE-VERB = "gáibidit" ;
# verbs that need a human locative argument as source
 
 
# Semantic verb sets according to content
# - - - - - - - - - - - - - - - - - - - -

# Hierarchy
# STATIC vs. DYNAMIC
# STATIC = HUMAN | NON-HUMAN
# DYNAMIC = EVENT | ACTIVITY

LIST MOVEMENT-VERB = "čuoigat" "čuoiggadit" "gálašit" "gállit" "girdit" "johtit" "mannat" "mátkkoštit" "njoammut" "ruohttat" "sihkkelastit" "suobbulit" "soabbut" "šloahtat" "váccášit" "vádjolit" "vájaldit" "vájuldit" "vánddardit" "vázzit" "viehkalit" "viehkat" "vuodjalit" "vuodjat" "vuodjit" "vuojadit" "vuojašit"  "vuolgit" ;
 # These verbs typically combine with place adverbials in the genitive case 
 # (i.e. not accusative) I am not sure if the verbs in the second line do that too. They denote movement anyway.
 
LIST ACTIVITY-VERB = "bivdit" "čállit" "čoaggit" "goddit" "goivet" "hervet" "luossa-#bivdit" "ráidnet" "duvdilit" "goddit" "guolástit" "luddet" "vadjat" "vikšet";
    # in contrast to abstract "activities" and static verbs. 
    
#LIST HUNTING-ACTIVITY = "bivdit" "goddit" "luossa-#bivdit";
#LIST KITCHEN-ACTIVITY = ;
#LIST WORKING-ACTIVITY = ;
#LIST WRITING-ACTIVITY = "čállit";
#LIST HANDICRAFT-ACTIVITY = "buddet" "botnit" "goarrut" "hervet" "vadjat" "vikšet" ;
LIST VERBAL-ACTIVITY-VERB = "čilget" "dadjat" "dáikit" "dárostit" "digaštallat" "digaštit"  "duoldat" "háleštit" "hállat" "hoallat" "humadit" "hupmat" "juoigat"  "lávlut" "logaldallat" "máinnašit" "muitalit" "muitaladdat" "savkalit" "sámástit" "sámistit" "ságastallat" "ságastit" "sárdnut" "sártnodit" "solžat" "suomastit" "šolžat" ;
#Under construction! (as most of the other LISTs)  

LIST ABSTRACT-ACTIVITY-VERB = "deattuhit" "dieđihit" "nannet" "oassálastit" "váikkuhit" ;

LIST INTELLECTUAL-ACTIVITY-VERB = "jurddašit" "smiehttat" ;

LIST PLACE-VERB = "buolvvastallat" "čippostallat" "čohkkát" "čuožžut" "orrut" "veallát" ;
    # Those get a locative in case of PLACE,ORGANIZATION,...

LIST EVENT-VERB = "dáhpáhuvvat" ;
    # TIME/AIGODAT are locative
    
#LIST EMOTION-VERB =
#LIST PERCEPTION-VERB =
#LIST SENSATION-VERB =

#LIST EMOTIONAL-ACTIVITY-VERB =

    
# HIERARCHY:

#LIST STATIC-VERB = PLACE-VERB | EMOTION-VERB | SENSATION-VERB ;

#LIST DYNAMIC-VERB = ACTIVITY-VERB | MOVEMENT-VERB | VERBAL-ACTIVITY-VERB | ABSTRACT-ACTIVITY-VERB | EMOTIONAL-ACTIVITY-VERB ;




# Noun sets
# ---------



# NP sets defined according to their morphosyntactic features
# - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - 

LIST N-SG-NOM = (N Sg Nom); LIST N-PL-NOM = (N Pl Nom);
LIST N-NOM = (N Nom) ; LIST N-GEN = (N Gen);

SET LEX-N = N - N-DER;

LIST PROP = (N Prop);
SET CNOUN = N - Prop;
SET CNOUN-PL-NOM = (N Pl Nom) - Prop ;
SET HNOUN = N - Cmpnd ;
SET HNOUN-NOM = (N Nom) - Cmpnd ;
SET N-NOT-GEN = N - (N Gen) ;


SET NP-HEAD = Pron | HNOUN ;
SET NP-HEAD-SG = (Pron Sg) | (Pron Sg3) | (N Sg) - Cmpnd;
SET NP-HEAD-PL = (Pron Pl) | (Pron Pl3) | (N Pl) - Cmpnd;
SET NP-HEAD-SG-NOM = (Pron Sg Nom) | (Pron Sg3 Nom) | (N Sg Nom) - Cmpnd;
SET NP-HEAD-PL-NOM = (Pron Pl Nom) | (Pron Pl3 Nom) | (N Pl Nom) - Cmpnd;
SET NP-HEAD-NOM = NP-HEAD-SG-NOM | NP-HEAD-PL-NOM; 
SET NP-HEAD-ACC = (Pron Acc) | (N Acc) - Cmpnd;
SET NP-HEAD-GEN = (Pron Gen) | (N Gen) - Cmpnd;


# The PRE-NP-HEAD family of sets
# - - - - - - - - - - - - - - - -

SET PRE-NP-HEAD = (Prop Attr) | (Prop @PROP>) | A | (Pron Pers Gen) | (N Gen) | 
                  Num | Cmpnd | CC | (Pron Dem)  | (Pron Refl Gen) | (Pron Indef) | 
                  (PrfPrc @AN>) |  (PrfPrc @PrcN>) | PrsPrc ;
                  # | (Ill LINK 1 (PrfPrc @AN>)) | (Com LINK 1 (PrfPrc @AN>)) | (Loc LINK 1 (PrfPrc @AN>))
SET NP-MEMBER   = PRE-NP-HEAD | N ;
SET PRE-A-N     = (Pron Pers Gen) | (Pron Indef) | Num | (A Ord) | (Pron Dem) | (Pron Refl Gen) ;
SET NOT-PRE-A-N = WORD - PRE-A-N ;
SET PRE-APP     = COMMA | (PUNCT LEFT) | PRE-NP-HEAD ; # This set ist not only for what can
                                                       # stand in front of appositions but also
                                                       #  postmodifiers.
SET PUNCT-LEFT = (PUNCT LEFT) ;
SET PUNCT-RIGHT = (PUNCT RIGHT) ;

# There must be a sensible naming convention for these sets...
SET NPNH     = WORD - PRE-NP-HEAD | ABBR ;              # NPNH = "NOT-PRE-NP-HEAD". 
SET NPNHA    = WORD - PRE-NP-HEAD - Adv ;               # NPNHA = "NOT (PRE-NP-HEAD-OR-ADV)"
SET NPNHAI   = WORD - PRE-NP-HEAD - Adv - Indef ;  # NPNHAI " "...-OR-INDEF"
SET NPNHAII  = WORD - PRE-NP-HEAD - Adv - Indef - Ill ; # Illative indir.obj.
SET NPNHAIIP = WORD - PRE-NP-HEAD - Adv - Indef - Ill - Pcle ;
SET NPNHC    = WORD - PRE-NP-HEAD - Com ; # "njaŋggofirpmiiguin bivdin"
SET NPNHCCC  = WORD - PRE-NP-HEAD - CC - COMMA ;
SET NAPP     = WORD - PRE-APP ;

# Set for NP with participle premodifier
# ... IF *-1 X BARRIER NPNH OR 
# LocComIll are ok, but only with a PrfPcc to the right



# Other negatively defined morphosyntactic noun sets
# - - - - - - - - - - - - - - - - - - - - - - - - - 

SET NOT-N =  WORD - N ;
SET NOT-A =  WORD - A ;
SET NOT-NA = WORD - N - A ;
SET NOT-NP = VFIN-NOT-NEG | ConNeg | Inf | Pcle | Interj | CS ; # The set NP-BORDER is bigger. 
SET NPROP = N - PROP ;
SET NOT-COMMA = CLB - COMMA | PUNCT-LEFT | PUNCT-RIGHT ;
 





# Nominal sets defined according to their morphophonological properties
# ---------------------------------------------------------------------


LIST XMEANTA = "dokumeanta" "birasgáhtten#departemeanta" "boles#departemeanta"  "dutkan#departemeanta" "eanandoallo#departemeanta" "energi#departemeanta" "finans#departemeanta" "gielda#departemeanta" "guolástus#departemeanta" "guovlo#departemeanta" "guovlu#departemeanta" "industri#departemeanta" "johtolat#departemeanta" "justiisa#departemeanta" "justis#departemeanta" "kultur#departemeanta" "politi#departemeanta" "sis#departemeanta" ; 

LIST XMENTA = "dokumenta" "birasgáhtten#departementa" "boles#departementa" "dutkan#departementa" "eanandoallo#departementa" "energi#departementa" "finans#departementa" "gielda#departementa" "guolástus#departementa" "guovlo#departementa" "guovlu#departementa" "industri#departementa" "johtolat#departementa" "justiisa#departementa" "justis#departementa" "kultur#departementa" "politi#departementa" "sis#departementa" ; 

#NOCGTWIN
#GCTWIN
#LIST KAPIHTAL = "bušeahta#kapihtal" "kapihtal" ;
#LIST KAPIHTTAL = "bušeahtta#kapihttal" "kapihttal" ;
# Those are sets for all consonant-gradiation forms that have a variant without consonant-gradiation.

#kahpittal:kapihtal
#kapihttal


LIST EAHTTA = "bálvalus#kvaliteahtta" "bušeahtta" "identiteahtta" "immuniteahtta" "intraneahtta" "kvaliteahtta" "magneahtta" "majesteahtta" "realiteahtta" "relativiteahtta" "seksualiteahtta" "universiteahtta";
LIST EHTTA = "bálvalus#kvalitehtta" "bušehta" "identitehta" "immunitehta" "intranehta" "kvalitehtta" "magnehta" "majestehta" "realitehta" "" "relativitehta" "seksualitehta" "universitehta";

#LIST AHTA = "advokáhta" "affrikáhta" "aggregáhta" "apparáhta" "automáhta" "direktoráhta"" "gáhta" "gemináhta" "internáhta" "kandidáhta" "klimáhta" "koordináhta" "lisensiáhta" "mandáhta" "notáhta" "olgoriikka#direktoráhta" "plakáhta" "referáhta" "saláhta" "senáhta" "spenáhta" "spináhta" "stipendiáhta" "sulfáhta" "tomáhta" "unduláhta" "čitna#hydráhta" ;
#LIST AHTTA = "advokáhtta" "affrikáhtta" "aggregáhtta" "apparáhtta" "automáhtta" "direktoráhtta"" "gáhtta" "gemináhtta" "internáhtta" "kandidáhtta" "klimáhtta" "koordináhtta" "lisensiáhtta" "mandáhtta" "notáhtta" "olgoriikka#direktoráhtta" "plakáhtta" "referáhtta" "saláhtta" "senáhtta" "spenáhtta" "spináhtta" "stipendiáhtta" "sulfáhtta" "tomáhtta" "unduláhtta" "čitna#hydráhtta" ;
# Verbs that have a GOAHTI/VIVVA and STAHTA variant

    
LIST NOM-GEN-NOUN = "ILO-tráktáhta" "TV-kamera" "agens" "agitáhtor" "ahkit"  "akkumuláhtor" "aksiálláger" "aktuáhtor" "album" "alladeattacirggon" "allodatsárggon" "almmuhantávval" "alpinsuodjegahpir" "amaryllis" "ametysta" "analfabehta" "analysáhtor" "antikristus" "apoastal" "apoasttal" "apostal" "apotehkár" "apparáhta" "arena" "aristokráhta" "artoteahka" "aske#dihtor" "asymtohta" "asynkron#mohtor" "atlas" "audition" "automáhta" "autoritehta" "avista#veksel" "azzil" "bahta#boskkan" "dearvvašvuođadutkan#guovddáš" "dutkan#guovddáš" "guovddáš" "identitehta" "identitehtta" "jolá" "journála" "lága" "pláhta" "plána" "psykiatriija" "stáhta" "ulbmil" "unduláhta" "universitehta" "vahkku" "valuhta" "velositehta" "videoteahka" "viskositehta" "vuolle#nohta" "ákšu" "ánnuitehta" "ássi" "ášši" "čitna#hydráhta" "čáhce#crossa" "šlipsa" "CD-siŋgel" "ILO-tráktáhta" "Kárášjoh#suopman" "TV-kamera" "Vær-varsom-plakáhta" "advokáhta" "affrikáhta" "agens" "aggregáhta" "agitáhtor" "ahkit" "akkumuláhtor" "aksiál#láger" "aktivitehta" "akto#ealit" "akto#váhnen" "aktuáhtor" "album" "ales#gahcin" "ales#gahtin" "alfabehta" "alin" "alken" "alla#deatta#cirggon" "allodat#sárggon" "almennet" "almennet" "almmi#seasan" "almmuhan#dávval" "almmuhan#távval" "alpin#suodje#gahpir" "aláš#gohcet" "aláš#sinut" "amaryllis" "ametysta" "analfabehta" "analysáhtor" "androgen" "anti#kristus" "aorta#uvssot" "apoastal" "apoasttal" "apostal" "apoteahka" "apotehka" "apotehkár" "apparáhta" "arena" "aristokráhta" "arsen" "artoteahka" "aske#dihtor" "asymtohta" "asynkron#mohtor" "atlas" "atoma#fápmo#rusttet" "audition" "automáhta" "autoritehta" "aviisa#doalan" "avista#veksel" "azzil" "bahta#boskkan" "bajit#forminttár" "baján" "ballin" "balsam" "balsáma#bohppel" "bargo#márkan" "bargo#átnjal" "baromehtar" "baromehter" "barta#gávpot" "baskkot" "bassaladdan#rusttet" "bassasat" "bađoš#gaccet" "bađoš#gaccet" "beahce#báhcat" "beahce#hurggán" "beahce#mihttár" "beahce#čukkon" "beahcet" "bealdo#biššan" "bealdo#duhpát" "bealdo#máđir" "bealdo#rettet" "bealdo#sennet" "bealdo#sennet" "bealdo#sáhpán" "beallje#boskkan" "beallje#muhtter" "beallje#rokkan" "beallje#skuhran" "beallje#suojan" "bearbil" "bearpmet" "bearral" "beasse#skoabit" "beatnat#sarrit" "beavkkan" "beavváš" "befála" "bejot" "bensin#mohtor" "benzen" "beron" "berylla" "bevkor" "beštor" "biblioteahka" "bibliotehka" "bidjet" "biebmo#prosessor" "biebmo#rieban" "biebmo#váhnen" "biebmo#váhnen" "biegga#skoarkil" "biehkan" "biehtan" "biehtán" "biellán" "biergo#guhppár" "biergo#átnjal" "bieskán" "bievval" "bieđan" "bihcabáhcat" "bihcir" "bihkka#suohpan" "bihppor" "biibal" "biibbal" "biihtár" "biika#náhkat" "bijat" "bijat" "bijat" "bijat" "bikket" "bilkkár" "bilkár" "billár" "bilttet" "bippal" "birra#jándor" "birrin" "bismmar" "bismmá#čoahkkin" "bismmáid#čoahkkin" "bisso#beahcet" "bitumen" "bivddár" "bivttas#bažán" "bivttas#božán" "bivval" "bivššut" "biđggon" "biškkon" "boahtal" "boaisán" "boaisán" "boalddán" "boaldin#mohtor" "boaldo#mohtor" "boalvár" "boaran" "boargar" "boargár" "boargár" "boasta#steampal" "boasta#steampil" "boazo#eaiggát" "boazo#ealát" "boazo#isit" "boazo#suohkan" "boađđo#civkkán" "boaššo#bearpmet" "bodne#rivot" "bohcce#rusttet" "bohcce#čuohpan" "bohppel" "bohtal#raban" "bohtehtos" "bohtás" "boimmon" "boksa#raban" "boles" "bolfe#gaccet" "bolfe#gaccet" "bolgen#šelggon" "bolot" "bonte#heavval" "bonus" "boran" "borgár" "borjat" "borran#átnal" "borran#átnjal" "bosan" "boson" "botehtos" "botkkon" "botnit" "bovkor" "bođos#vuohtan" "boŋkor" "božán" "brensel" "bubmol" "buddha#tempel" "buhtat" "buhtet" "buijár" "buikkár" "buikár" "buimman" "buktin#hámman" "bulddán" "bulet" "bulkor" "bulmmot" "bulžžet" "bumbol" "bumeránskka" "bummal" "bummol" "buohcce#divššár" "buohkaid#čoahkkin" "buohkkistáhta" "buoiddat" "buolaš#suohpan" "buollin#biškkon" "buolát" "buorat" "buoráš" "buoskár" "buovnnjat" "buovnnjat#sáhpán" "buozan" "buozanvuoh0ta" "buošan" "buošut" "buošát" "bupmol" "burbor" "burpur" "burrut" "busir" "bussá#geahpil" "buvro#risen" "buđet" "bužan" "bábir#ráiggan" "bábir#čanan" "báhcat" "báhkin" "báhkkol" "báhkkon" "báhpa#čeabet" "báhtter" "báhtter#gealddán" "báikki#isit" "báikki#kásus" "báimman" "báitat" "bákte#gáiskkit" "bákte#gájan" "bálges" "bálká#soalddát" "bállat" "bálvalan#ámmát" "bálát" "bárel" "bárggeš" "bárggon" "báron" "bárral" "báson" "básár" "bátne#rokkan" "báđar" "báŋko#eaiggát" "bážan" "cagan" "cakkan" "cakkat" "ceakko#kartotehka" "ceakko#symptohta" "centi#lihtter" "centi#mehtar" "centi#mehter" "centilittar" "chili#bihppor" "cieban" "ciebat" "cielan" "cievan" "cihran" "cinemateahka" "cirggon" "civkkan" "coagan" "cokkan" "cuppol" "dahku#magnehta" "dato#stempel" "davve#vizar" "deagga#fiskes#loppan" "deagga#livkkár" "deagga#máđir" "deahkke#tonus" "dearppát" "dearvvašvuođa#divššár" "deatta#reguláhtor" "deattán" "deattán" "deavut" "debehta" "dehkar" "dehálašvuođa#ortnet" "dekstor" "delár" "demens" "denara" "deret" "desilihtar" "desilihtter" "devnnet" "devon" "dia#projektor" "diamehtar" "diamehter" "diehppe#lieđat" "diehppe#lieđat" "dielko#čuoivvat" "diesel" "diesel#mohtor" "dievŋŋat" "diggár" "dihtor" "diibmo#duojár" "diibmo#dávval" "dikkel" "dikkel#botnet" "dikkár" "diktáhtor" "diktár" "dikšár" "dilot" "dines" "dipma#biššan" "direktora" "direktoráhta" "diret" "dirgget" "diskotehka" "divat" "divat" "diversitehta" "divššár" "doaibma#almmái#ortnet" "doaibma#olmmái#ortnet" "doaibma#operator" "doaibma#vádjet" "doaimmár" "doaján" "doalan" "doares#bielka#lovtton" "doarjja#ortnet" "doavkkan" "dohkká#teáhter" "dolla#rieban" "dollár" "domisiila#veksel" "dopmo#njaman" "doron" "dorre#sáhpán" "dorrát" "doškkon" "dronnet" "drumbu#goazan" "drámá#teáhter" "dudnar" "duhpát" "duhpát#guhkkár" "duhtodat#árvo#operáhtor" "duhát#juolggat" "duivil" "dulben#jurssan" "dulján" "duljár" "dulját" "dumon" "duodjár" "duojár" "duolba#gavre#heavval" "duolba#heavval" "duolba#luovččan" "duolbbár" "duolddan" "duollje#uvssot" "duollár" "duolmman" "duolmman#šelggon" "duolva#čuoivvat" "duolvvár" "duopmo#kapihttal" "duopmár" "duottar#bussá#geahpil" "duottar#hávvar" "duottar#sáhpal" "durbbon" "durján" "dustton" "dutkkon" "dutnar" "duvdda#bearpmet" "duvhllár" "duvssár" "duđan" "dádel" "dággir" "dáhpa" "dáhton" "dáiddár" "dáidár" "dáige#jierit" "dájet" "dájon" "dálar" "dálon" "dálssár" "dálu#eamit" "dálu#isit" "dálve#ruoivil" "dálvvet" "dándor" "dápmot" "dápmot" "dássádahttin#giikan" "dávllat" "dávllet" "dávttat" "dávval" "dávvir" "dávžžan" "eagáš" "eahket" "eahket" "eahkit" "eahpe#ipmil" "eahppel" "eaiggát" "ealet" "ealit" "eallin" "ealán" "ealát" "ealát" "eami#álbmot" "eamit" "eana#bihttá#eaiggát" "eana#eaiggát" "eana#muolddat" "eanan#muolddat" "eappel" "eappel#geađge#čukkon" "earán" "eatnan#eaiggát" "eavddár" "eaŋgal" "eaŋkal" "ebel" "ebmos#lieđat" "ebmos#lieđat" "edet" "ednen" "ehpel" "eisse#válde#geavat" "ekonomiija" "ekonomiija#ortnet" "eksamen" "eksempel" "eksos#gássa#rusttet" "eksosa#rusttet" "eksámen" "ekváhtor" "el#mohtor" "el#orgel" "el-botkkon" "el-mihttár" "el-molon" "el-távval" "el-visppon" "elektrihkkár" "elektrisitehta" "endor" "endossáhta" "entitehta" "eppel" "erttet" "essea" "estimáhtor" "etnisitehta" "ettet" "evangelium" "evaŋgelium" "eváŋgiljon" "examen" "eŋgel" "facultatum" "fahkon" "fakta" "faktora" "fakultehta" "falan" "falat" "fanas#mohtor" "faskkan" "faskkon" "fasttat" "feambil" "fearan" "fearkil" "fearrán" "fearrán" "fearán" "feber" "femininitehta" "feminitehta" "fenomen" "feodála#ortnet" "ferkkon" "fidjol" "fidnár" "fiehtar" "fiehtat" "fierán" "fievrran" "fihkon" "fiiber" "fiikkon" "fiikon" "filbma#projektor" "finddár" "finer" "firet" "firon" "fiskes#beštor" "fiskes#iris" "fiskes#loppan" "fiskes#máđir" "fiskes#vizar" "fiskár" "fitnet" "flundar" "flygel" "flánealla#távval" "foahpir" "fokus" "fokusáhtor" "folin" "formel" "forum" "fototeahka" "fránsos" "fuohpuš" "fuolke#álbmot" "fuođđar#rájan" "fábel" "fáhkan" "fáksa" "fáktor" "fálle#vizar" "fálskkár" "fálttet" "fálttet" "fánen" "fánten" "fápmo#rusttet" "fáskir" "fássol" "fássol#doalan" "fásta#doavtter#ortnet" "fáŋgal" "gaccat" "gaccet" "gacceš" "gaccir" "gaccon" "gahpan" "gahpir" "gahčan#partihkal" "gaibbat" "gaigŋir" "gaigŋir#bismmar" "gaikár" "galbma#gáhkkor" "galjat" "galjot" "galján" "gallup" "galmmihan#mekanihkkár" "galmmihan#skáhpe" "gardnjil" "gargŋil" "garra#sinut" "gaska#goartil" "gaskan" "gavja#njaman" "gazir" "gazza#sáhpal" "gazzaš" "geafe#guhkkár" "geahpil" "geahče#doalan" "geahče#dulben#jurssan" "geaidno#gievkkan" "geaigán" "geaisár" "gealddán" "gealut" "gearret" "gearret" "geaset" "geasse#gollet" "geasse#universitehta" "geasse#čoavjjet" "geasse#čuččot" "geastegiivvár" "geasán" "geavat" "geavllet" "gehpot" "gehtet" "gemináhta" "general#majora" "generáhtor" "gerddon" "geret" "geson" "giedde#máđir" "giedde#sinut" "giehta#goazan" "giehta#heavval" "giehta#láiddot" "gieimman" "gierdo#dagan" "gieres#sabet" "gierrán" "gievkan" "gievkkan" "gieđalat#láiddot" "gihlar" "gihtet" "giikan" "giikar" "giikkár" "giippár" "giittos#rohkos" "giljon" "gilkkan" "gilkor" "gilljo#čearret" "gilljo#čearrit" "gilljon" "gillár" "gilomehtar" "gilomehter" "gilon" "gilvva#sáhpal" "girdi#hámman" "girdi#hávvar" "girdi#sáhpán" "girje#doalan" "girje#gorddet" "girje#livkkár" "girkolaš#čoahkkin" "giron" "gisor" "gissel" "gissor" "gitta#dávvir" "gitta#čoahkkin" "givgás" "giškon" "gluten" "glykogen" "glyptoteahka" "goadjin" "goadjin" "goaffar" "goahppil" "goaivvan" "goaktil" "goallár" "goalšo#vizar" "goapmir" "goaran" "goarat" "goarka#goarvvet" "goartil" "goarvvet" "goaskin" "goaskit" "goatnil" "goatnjil" "goazan" "goađe#eamit" "goađe#isit" "goaŧŧo#čeabet" "goban" "godde#sáhpán" "goden" "gohpe#heavval" "gohpe#luovččan" "gohrat" "goidoš" "goidár" "goike#hivsset" "goike#sallit#bállat" "goikket" "goivvon" "golbma#čiehka#sárggon" "golddán" "golggiidus#rusttet" "golggiidus#rusttet" "golggot" "golle#hámster" "golle#rieban" "gollen" "golmma#čiegat" "golmmet" "golár" "gontor" "goppás" "goppát" "gorddet" "gorddán" "gorut" "gosttet" "gosttet" "gotte#sáhpal" "gottur" "govddon" "govllon" "govva#album" "govva#dáiddár" "gozihan#čoahkkin" "goččor" "goččár" "gođán" "granáhta" "gubar" "gudjor" "guhkan" "guhkat" "guhkes#heavval" "guhkes#áigge#ulbmil" "guhkkár" "guhppil" "guhppár" "guhtal" "guikkut" "gulbmár" "gulkkan" "gullo#suojan" "gulmmet" "gulmmit" "gummi#stevvel" "gummi#sáppát" "guoccat" "guodja#vizar" "guohkan" "guohppar" "guohár" "guoih#gáhrit" "guoik'#gáhrit" "guoimmáš" "guoirrat" "guolban" "guolbba#gorddet" "guole#beahcet" "guolle#goaskin" "guolle#guhppár" "guolle#ipmil" "guolle#átnal" "guolle#átnjal" "guolle#čearret" "guorbban" "guorbbat" "guorpmat" "guorvil" "guossa#gaccet" "guossa#gaccet" "guossa#vizar" "guostta#čurrot" "guotnjar" "guotta" "guottet" "guottet" "guovddut" "guovddáš#dopmo#njaman" "guovddáš#guppol" "guovssat" "guovtte#dávttat#mohtor" "guovtte#iđot" "guovtte#iđot" "guovžur" "guppar" "guppol" "guppás" "gurbbet" "gurbmon" "guret" "gurpmat" "gurra#luođđa#láger" "gurra#šluppot" "gurvvon" "gusa#sáhpal" "guvhllár" "guvllár" "guzat" "guččor" "guččár" "guđahas#mehtar" "guškil" "guššát" "gádjit" "gáfe#silon" "gáffal" "gáhkko#bažán" "gáhkko#božán" "gáhkkor" "gáhpol" "gáhrit" "gáhrot" "gáhta" "gáhtegismmos" "gáhtir" "gáiddus#dárkon" "gáiskkit" "gáisá#bussá#geahpil" "gájet" "gájot" "gákkan" "gálbbe#čeabet" "gálddat" "gálddát" "gálgan" "gálgan#doalan" "gálggan" "gálggan" "gálgoš" "gálko#rieban" "gálkon" "gálljin" "gállot" "gálssot" "gálva#gorddet" "gálvo#kartotehka" "gálán" "gámmir" "gámmár" "gándil" "gándin" "gáneš" "gántor" "gárde#sáhpán" "gárde#vizar" "gárdin" "gárdin" "gárdnagas" "gárjil" "gáron" "gársan" "gártnit" "gárven" "gáskkit" "gáskkát" "gásor" "gássa#duolmman" "gássor" "gásár" "gását" "gávgil" "gávlo#rievttat" "gávlo#vuoján" "gávpe#gámmár" "gávpot" "gávpot#teáhter" "gávra#" "gávre#heavval" "gávrret" "gávrret" "gávval" "gáŋgen" "gášan" "habitualitehta" "hallusinogen" "halogen" "hammond#orgel" "haran" "harem" "haŋkin#váiddon" "heagga#ustit" "heahkel" "heahpat" "heakkel" "hearta#vizar" "heasta#gohrát" "heavval" "heittot" "heittot" "heksaedar" "helikopter" "helvet" "herkken" "hertnet" "herttot" "heđen" "heŋggon" "hihtat" "hilbbot" "hilla#ruovssut" "hiller" "historihkkár" "hivsset" "hivsset" "hiŋkkan" "hoagan" "hoaiddár" "hoalan" "hoapmir" "hoavkkát" "hoavrran" "hoašša#háigir" "hombil" "hommet" "honet" "honnet" "hopmir" "hubat" "huikkan" "huitor" "humaniora" "humbil" "humor" "hyasinta" "hydrogen" "hyperbel" "hypoteahka" "hypotenus" "háigir" "hálddahus#ortnet" "háleštan#skihpár" "hállár" "hálvvát" "hámman" "hámsson" "hánar" "hánat" "hándal" "hápman" "hárddan" "hárddán" "hárpmon" "hárset" "hársit" "hárát" "hárččet" "háskil" "hássel" "hássel#sáhpán" "hávde#gámmár" "hávlár" "hávskár" "hávvar" "háŋgil" "identitehtta:identitehta" "idja#gozán" "ieš#eaiggát" "ieš-pronomen" "iežas#veksel" "ihkkon" "ihkon" "ihto#sinut" "iissot" "iissot" "imaš" "immunitehta" "inspektoráhta" "indefinihtta#pronomen" "infiltrašuvdna#rusttet" "intensitehta" "internehta" "internáhta" "interrogatiiva#pronomen" "intranehta" "invaliditehta" "ipmet" "ipmil" "ipmit" "irján" "isit" "iskkár" "ivssut" "ivssut" "iđat" "iđit" "iđit#rohkos" "jahke#duhát" "jahke#čoahkkin" "jalleš" "jaman" "jarin" "jaspis" "jeagge#biššan" "jeagge#guškil" "jeagge#máđir" "jeagge#sáhpal" "jeagge#vizar" "jeara" "jester" "jiehkko#heavval" "jiellat" "jienastan#seattal" "jieret" "jierit" "jietna#váiddon" "jievjun" "jipmil" "jobbár" "johka#hávvar" "johtin#láger" "johtin#sabet" "jonssot" "jorba#oaivvat#spihkár" "jorbbot" "jorin" "jorran#áksil" "jorre#doalan" "jorre#láger" "joule" "journal#nummar" "jovssus" "juhkan#láse#doalan" "julii" "jumeš" "junii" "junior" "juogut" "juohko#pássár" "juolge#doalan" "juolge#nohta" "juolut" "juomit" "juovla#eahket" "juovla#káktus" "jury" "jurán" "jus#leksikon" "juster#stempela" "justitiarius" "justitiárius" "juvlii" "juvllii" "juvnii" "juvnnii" "juŋgel" "juŋkor" "jálás" "jámet" "jámit" "jándor" "kajenne#bihppor" "kaleandar" "kalkulator" "kalkuláhtor" "kamela" "kamera" "kandidáhta" "kapihttal" "kapihttal" "karbo#hydráhta" "kardeaksa" "karoten" "kartoteahka" "kartotehka" "kasus" "katalysáhtor" "katehta" "katekehta" "keaisár" "keallár" "kefir" "keret" "kervel" "kievkkan" "kihtet" "kijkkár" "kijppár" "kilo#mehtar" "kilo#mehter" "kilo#mettar" "kinemateahka" "kinoteahka" "kirkolaš#čoahkkin" "klassihkkár" "kliissár" "klimáhta" "klyxa" "koordináhta" "koordináhtor" "koaffar" "koartil" "kodicilla" "kodisilla" "koffar" "komehta" "kommeanta#partihkkal" "kondensáhtor" "konfoluhtta#njolon" "konserváhtor" "konsuláhta" "kontor" "konttor" "korinta" "koriánder" "korpus" "korreláhta" "kosttet" "kreativitehta" "kreatsa" "kredihtta#rehket" "kriminalitehta" "kritera" "krysoprasa" "krysolihtta" "kráhter" "kubbel" "kullár" "kuntepillar" "kuráhtor" "kuššát" "kvalitehtta:kvalitehta" "kvantifikáhtor" "kvantitehtta:kvantitehta" "kviteret" "kvohta" "káfe#silon" "káffal" "káhkal" "káhtekismmos" "kámmir" "kámmár" "kánel" "kántor" "kápihttal" "kárven" "kásus" "kávrret" "kávval" "labirinta" "labyrinta" "ladni" "lakta#partihkkal" "laktin" "lappe#kodisilla" "laser" "lassi#árvo#divat" "lasttat" "lađan" "lađas#juolggat" "lađas#juolggat" "leaggán" "leana#dáiddár" "ledjon" "legitimitehta" "leibor" "leksikon" "lektor" "lektoráhta" "liegga#áibmo#rusttet" "liekkan" "liekkas#mihttár" "lievde#njaman" "lievla#njaman" "lieđđe#širan" "lihpar" "lihtar" "lihtor" "lihttar" "lihtter" "lihtto#stáhta" "liidna#kláva" "liige#befála" "liige#modula" "likten#heavval" "likviditehta" "linddor" "linjen" "linjjá" "linjár" "linnjen" "linnjár" "lisensiáhta" "litnet" "littar" "livat" "livnnet" "livttes#risen" "liántta#bužan" "loabát" "loahppa#kommunikeá" "lodde#virus" "lohkan#ortnet" "lohkir" "lohkke#raban" "lohkkir" "lojan" "lojat" "lokkon" "lokál#kásus" "lotte#gazza#sáhpal" "lottáš" "lovppár" "lovpár" "lovttan" "lovtton" "luffel" "luhkkár" "luhtat" "luhtehas#čukkon" "luhtet" "luksus" "luleš" "lummor" "luohkká#skihpár" "luohpet" "luoimman" "luojan" "luosat" "luossa#veajet" "luoste#sáhpán" "luosttat" "luotne#gáffal" "luottut" "luovat" "luovddat" "luovvar" "luovččan" "luovččan#šluppot" "luođđa#doalan" "luođđa#láger" "lutton" "luvddon" "lyra" "ládjo#gahpir" "láfol" "láfon" "láger" "lágut" "láhken" "láhkán" "láhten" "láiddon" "láiddot" "láidon" "láidun" "láikkan" "láitta#heavval" "láittan" "láittán" "láittár" "láktor" "lámbor" "lánnot" "lántor" "lánuorat" "láse#raban" "láser#čálán" "láskut#kartotehka" "láttat" "láttat" "láttán" "lávet" "lávgo#lovtton" "lávkkon" "lávllon" "lávot" "lávár" "láđđe#muzet" "láđđe#čáhpot" "lášmmut" "magnehta" "majestehta" "majora" "majoritehta" "mammon" "manager" "mandel" "mandáhta" "manin" "manin" "manna" "mano#mehtar" "maori#álbmot" "maraton" "marmor" "martihkkal" "maskulinitehta" "master" "matemátihkka" "matrikkel" "matrikkel#nummar" "maŋŋe#bismmar" "maŋŋe#stávval" "maŋŋe#áksil" "maŋŋe#ánnuitehta" "maŋŋe#šelggon" "maŋŋeš" "maššir" "meahcce#luovvar" "meahcce#lávet" "meahcce#váran" "meahcet" "meahcit" "meaisil" "meargŋun" "mearra#goaskin" "mearra#háskil" "mearra#álbmot" "meassár" "meađđen" "meaštár" "mediateahka" "mediatehka" "medieteahka" "megon" "mehtar" "mehter" "meinnet" "meisset" "mekanihkkár" "meren" "meres" "merkkon" "messet" "mettar" "miehte#gieđa#geavat" "miel#duopmár" "miel#eaiggát" "miella#vádjet" "miese#gahpir" "miestta#vizar" "mieđut" "mihttár" "mikro#botkkán" "mikro#doaimmár" "miktam" "milli#mehtar" "milli#mehter" "minikumma" "minister" "minoritehta" "moarrun" "modalitehta" "modernitehta" "modifikáhtor" "modula" "modus" "mohtor" "mohtor#crossa" "mohtor#liekkan" "moivvár" "moksin" "molon" "molssa#gákkan" "molsson" "molybden" "monin" "monin" "morddár" "mordár" "mosat" "moto#crossa" "moxa" "muffa" "muhke#biđggon" "muhkke#bulkor" "muhtor" "muhtter" "muito#nummir" "mulggon" "muohta#skohter" "muohta#skuhter" "muolddat" "muorra#faskkon" "muorra#goarvvet" "muorra#guppol" "muorra#ipmil" "muovje#jieret" "muoŧáš" "muoŧŧal" "murdin#molsson" "museum" "musihkkár" "muslim" "muzet" "muzet#čuoivvat" "muđon" "myhta" "myrra" "myrrá" "mádjit" "máhkan" "máhta" "máiddon" "májor" "máles#risen" "málgor" "málljar" "málár" "mámmon" "mándel" "mánus" "máran" "márbmor" "márffon" "márgár" "márkan" "márkan" "máron" "másomman" "máttil" "máđir" "naleš" "namma#gilkor" "namma#láhpan" "namma#láhpat" "nannán" "našunal#teáhter" "neahpát" "nealggár" "neavttár" "neillet" "nelet" "nelet" "nelot" "nenetsa" "netto#rehket" "nieida#gearret" "nieida#lihtor" "nieida#sahpal" "nieiddas#nisson" "nihkkel" "nihppel" "nii0kun" "niito#sáhpal" "niito#sáhpán" "nirbbet" "nisson" "nissun" "nivssat" "njalddán" "njaman" "njamat" "njeahcet" "njealje#juvllat" "njealje#čiegat" "njealjit" "njeccon" "njelot" "njiezat" "njigan" "njigon" "njiman" "njoalve#láger" "njoammil" "njoazan" "njolon" "njomon" "njozon" "njuikkon" "njulggot" "njunne#duhpát" "njuolggo#golmma#čiegat" "njuolggo#sátne#ortnet" "njuolggut" "njuorat" "njuorddut" "njuorjjo#čázet" "njuorjjot" "njuovan" "njuovvan#rusttet" "njuovčča#lieđat" "njuovčča#lieđat" "njuozat" "njurggan" "njurggon" "njuvddán" "njuvvat" "njágan" "njáhco#sabet" "njáhco#suohpan" "njáhka#veajet" "njálbme#fidjol" "noahtal" "nohta" "nomen" "nomináhta" "notáhta" "novrrán" "nuhtat" "nuhtot" "nulpo#gahpir" "nummar" "nummir" "nummár" "nuorta#gáiskkit" "nuorttut" "nuvan" "nuvttat" "nuvttot" "nábor" "nábot" "nábár" "náhpol" "náhpul" "náhpur" "nállo#láger" "náskál" "náste#finddár" "návet" "oaffar" "oagan" "oaive#gávpot" "oaive#ipmil" "oaive#isit" "oaive#joran" "oaive#skuvrin" "oaivil" "oales#heavval" "oalge#doalan" "oapman" "oarjel#sámegiel#arena" "oarján" "oassá#lasti" "oaván" "obba#bihppor" "obba#gárdin" "obba#rehket" "obersta" "observatora" "ogon" "ohter" "oiddot" "oidno#vihtan" "okkon" "oksygen" "oktaedar" "okto#ealit" "okto#orut" "olles#čoahkkin" "ollut" "ollut" "olmmoš#joren" "olmmoš#joren" "olmmoš#jorán" "olssot" "olus" "olut" "olut" "omen" "omman" "opera" "operáhtor" "opmir" "oppa#tráilar" "oppal" "oppas#rievttat" "orakel" "orbbeš" "orbbeš#goartil" "ordel" "ordil" "orgel" "orgil" "orrit" "ortnet" "orut" "oskál" "otnáš" "ovda#gáffal" "ovda#gávpot" "ovda#ánnuitehta" "ovda#šelggon" "ovddas#rohkos" "ovsttán" "ovtta#iđot" "ovtta#iđot" "ovttas#doaibman#arena" "ovttas#eaiggát" "ovttet" "ođat" "ođut" "paradiijás" "paradiás" "paradoksa" "parlamentarihkkár" "parlamentihkkár" "partihkkal" "parábel" "passivitehta" "person#nummir" "philosophicum" "pinakoteahka" "plakáhta" "plenum" "pohtehtos" "poles" "poles#kámmir" "politi#avokáhta" "politihkkar" "politihkkár" "pollen" "preassa" "predikáhta" "predikáhtor" "prioritehta" "priváhta" "proavás" "proavás" "produktivitehta" "profehta" "professor" "professoráhtta" "projektor" "promilla" "pronomen" "prorektor" "proseassa" "proselyhtta" "prosessor" "purpur" "pyramida" "pássár" "raban" "rabas#čoahkkin" "rabárber" "radiáhtor" "radiál#láger" "ragat" "rahpot" "ravddat" "ravggon" "realitehta" "refleaksa#veahčir" "regressáhta" "reahka#jálás" "reahpen" "reaidnár" "reainnár" "reantár" "refereanssat" "referáhta" "regeanta#nummar" "regel" "reguláhtor" "rehket" "rekambio#rehket" "rekel" "rektor" "rektáŋgel" "relatiiva#pronomen" "relativitehta" "reportár" "resiprohka#pronomen" "rettet" "revolver" "revolvár" "revy" "reáktor" "ribat" "ribon" "ridda#sáhpal" "riddo#máđir" "riddár" "rieban" "riehkan" "riehtet" "riejan" "riekte#regel" "rievssat" "rievtti#geavat" "rievvár" "riffal" "riffel" "rigearár" "rihcit" "rihkko#heavval" "rihkon" "riika#ráđđe#adel" "riika#čoahkkin" "riikka#beaive#ortnet" "rikkearár" "ris#váhnen" "ris#váhnen" "risen" "rist#váhnen" "rist#vánhen" "rista#rieban" "rivot" "rivttár" "rivut" "roabát" "roahkan" "roahnit" "roamše#jierit" "roatnet" "rockár" "rogon" "rohkkil" "rohkos" "rohkot" "rohpe#geaigán" "rohto#gaccet" "rohto#gaccet" "rohto#máđir" "rohto#vizar" "rohtor" "roibbon" "rokkan" "roman" "romani#álbmot" "romat" "ronnet" "rosgeret" "rosttot" "rottet" "rubel" "ruhkos" "ruhmut" "ruivvan" "rukses#feber" "rukses#guškil" "rukses#luovvar" "rukses#rieban" "rukses#ruoivil" "rukses#sinut" "rukses#sáhpán" "rulla#láger" "rullit" "ruohkan" "ruohtta#eahket" "ruoinnan" "ruoinnat" "ruoivil" "ruossa#riddár" "ruossa#rieban" "ruovde#gulmmet" "ruovde#gákkan" "ruovde#máđe:ruovde#máđe" "ruovssut" "ruovtto#goatnil" "ruovttot" "ruovttu#divššár" "ruođat" "ruoške#mándel" "ruošša#gearvil" "ruošša#guovssat" "ruošša#njoammil" "ruošša#állat" "ruošša#állat" "ruppal" "rusen" "rusttet" "rutnot" "ruŋkul" "ruškkut" "rábbe#goaskin" "rábit" "rábon" "rábát" "rádje#soalddát" "rádár" "ráhnit" "ráhto#goaskin" "ráiggan" "rájan" "rájus#kartotehka" "rámmát" "ránan" "ránat" "rándat" "ránes#beštor" "ránes#háigir" "ránes#livkkár" "ránes#rieban" "ránes#sáhpán" "ránsel" "ránsos" "rántet" "ráppár" "rásse#lohkko#vizar" "rásttes#vizar" "rátno#gođđin#stuolet" "rátno#stuolet" "rávdnje#aggregáhta" "rávnnut" "rávrran" "rávál" "ráđđe#dagat" "ráđđe#čoahkkin" "sabet" "sabit" "safira" "sahen#rusttet" "sahán#rusttet" "sal#biehtár" "sal#pehtter" "sallit" "sallit" "saláhta" "sampel" "sardonyksa" "sarrit" "sarrit" "sasttet" "savvon" "seagan" "seahkka#njurggan" "seahkot" "seaibbat" "seakka#gorddet" "seakka#sáhpal" "seakŋáš" "searvo#goazan" "seasan" "seattal" "seavdnjat" "seavnnját" "seaŋga#nisson" "seder" "sehpet" "seivvon" "sekel" "sekkor" "sektor" "seller" "selota" "semi-tráilar" "senet" "senet" "senior" "sennet" "sensitivitehta" "senti#mehtar" "senti#mehter" "senáhta" "sepmon" "septeamber" "septeambár" "serrat" "sesam#siepman" "seđel" "siepman" "sierat" "sierra#oaivil" "sierrán" "sierát" "siessal" "siffar" "sihkkel" "sihkon" "sihkor" "siidagas#vuoján" "siidda#isit" "siido#šelggon" "siiggát" "silan" "silar" "silon" "sirkil" "sirkus" "sirot" "sirot" "sirát" "sis#bandy" "sis-bandy" "siste#suorpmat" "sisttet" "sitara" "sitron" "sitáhta" "sitáhtta" "sivii0la:sivij'la" "sivjjot" "sivjjot" "sivnnát" "skabilken" "skabus#risen" "skarlagen" "skeaikkan" "skeaŋkár" "skeijár" "skerttet" "skievttár" "skiffar" "skihkár" "skihpár" "skilan" "skillet" "skirttet" "skoaran" "skoarkil" "skoavit" "skohtal" "skohter" "skohttal" "skoret" "skovlot" "skreaddár" "skruvva#bonjan" "skruvva#doalan" "skruvva#čárvvon" "skuhter" "skuhttár" "skuibor" "skumot" "skuoggir" "skuoggun" "skuoggun" "skuohtal" "skupmot" "skurbban" "skurbbet" "skurvvát" "skuter" "skuvla#skihpár" "skádja#seasan" "skáhčir" "skájan" "skárbban" "skárben" "skávval" "skávžžet" "smaragda" "smarágda" "smeargil" "smávva#boargár" "snelkkon" "snielut" "snihkkár" "snihppar" "sniiggár" "snijkkár" "snuiba#gahpir" "snuohtar" "snuolggár" "snuvssa" "snuđđor" "snáhpot" "snáhput" "snárket" "snávggut" "soabat" "soadje#sáhpán" "soahke#sáhpán" "soahte#soalddát" "soalddát" "soande#geasehan#bulkor" "soavvil" "soađan" "sohken" "soidnár" "solidaritehta" "solon" "sosial#demokráhta" "sotkkon" "sovkken" "spanut" "spažut" "speadjal" "speadjár" "speallan" "speisse#dálar" "speisset" "spektra" "spenáhta" "spetakkel" "spierta#čáhput" "spihkár" "spijkkár" "spináhta" "spivkkár" "spoavár" "sponsor" "sponta#heavval" "spáillit" "sruva#bonjan" "sráŋga#spihkár" "sráŋga#spiikkár" "stabilitehta" "stadi" "stadion" "stahtor" "stahtus" "stajár" "stalin#orgel" "standárden" "steaika#oapman" "stealle#nihppel" "steampal" "steampil" "steiken#termo#mihttár" "stellet" "stempel" "stetoskohpa" "stevdne#vihtan" "stevnnet" "stevvel" "stihka#gálssot" "stihka#gálssot" "stihká#gálssot" "stihká#gálssot" "stipendiáhta" "stivrra#čoahkkin" "stivrran" "stivrrár" "stoagan" "stoahčat" "stoahčet" "stoahčet" "stoavkkan" "stobe#raban" "stohpo#ortnet" "stokk#áŋkor" "stuora#háskil" "stuorat" "stuorra#geavjjat" "stuorra#gávpot" "stuorra#háskil" "stuorra#máđir" "stuorra#neillet" "stáhkkár" "stáhta" "stáhta#advokáhta" "stáhta#borgár" "stáhtus" "stáidnár" "stáinnat" "stáinnir" "stálle#doalan" "stálo#bárggeš" "stámbbut" "stárre#rutnot" "stávval" "stáđut" "subordináhtor" "substituhta" "substráhta" "suhkkol" "suhkol" "sui0te" "suittet" "suittet" "sukkol" "sukseassa" "suksessa" "sulfáhta" "sulin" "suodje#gahpir" "suodjár" "suohkan" "suohkat" "suohkat#risen" "suohpal" "suohpan" "suoidne#spábba#čárvvon" "suoidne#vahát" "suoidne#vahát" "suoivvan" "suojan" "suoksa#čurot" "suoksa#čurrot" "suonat" "suonjan" "suonjar#láger" "suonjir" "suopman" "suorat" "suorit" "suorpmat" "suovva#rieban" "suoŋir" "suvddár" "suvdár" "suverenitehta" "suvvol" "suđi#láttán" "sveisen#boalddán" "svisker" "syhkkel" "sykkel" "synkron#mohtor" "sábbát" "sábel" "sábát" "sádel" "sáfrán" "ságel" "ságge#gahpir" "ságát" "sáhco#dagan" "sáhkel" "sáhkát" "sáhpal" "sáhpel" "sáhpit" "sáhppes#jieret" "sáhpán" "sáhtán" "sákkil" "sákkin" "sálkor" "sálon" "sáme#digge#politihkkár" "sámmál" "sánddat" "sándil" "sáppat" "sáppát" "sárggon" "sátne#ortnet" "sáttat" "sávdnje#heavval" "sávzza#sinut" "sáđgin" "sáŋgár" "sŋiran" "tabernakel" "tableahtta" "tabuláhtor" "taffel" "taiga:tajga" "taksi" "teknihkkár" "telefon#nummar" "telefon#nummir" "telefon#čoahkkin" "tempel" "tempus" "teret" "termostáhta" "terror" "terrárium" "tetraedar" "tevnnet" "teáhter" "tiggár" "tihttel" "timotemos" "tingeltangel" "tomáhta" "topasa" "totem" "trailer" "traktor" "transformáhtor" "transistor" "trasáhta" "trinitrotoluen" "trippel" "triáŋgel" "trompehta" "trumpehta" "tráilar" "tráktor" "tráktáhta" "tulján" "tuljár" "tulját" "tuohpis" "tuollár" "turbo#mohtor" "turnus" "táhka" "táhkkal" "távval" "uksa#bearpmet" "ulbmil" "unduláhta" "universitehta" "unnit#álbmot" "upmol" "urbil" "urbán" "urmáhkár" "ustit" "uvssot" "uđámat" "vadnin" "vahát" "vajahas#gorddán" "valat" "valljivuođa#ortnet" "valmue#siepman" "valuhta" "varit" "varit" "variábel" "variáhtor" "varra#deaddo#mihttár" "varra#heahpat" "veabel" "veahkke#divššár" "veahkke#professor" "veahkkár" "veahčir" "veaike#boson" "veajet" "veaksel" "veaksil" "veallan" "veambil" "vearret" "vearro#livnnet" "vearro#martihkkal" "vearál" "veattat" "veattán" "vehikkel" "veivár" "veksel" "vektor" "velositehta" "ventiila#lovttan" "venzzon" "verrát" "vevvel" "veđor" "videoteahka" "viehkat" "viehta" "vielggut" "vieljaš" "viellja#álbmot" "viesso#hoaiddár" "viesso#hoiddár" "viesso#sáhpán" "vihtan" "viissár" "viisár" "viivvut" "vilges#bihppor" "vilges#bohppel" "vilges#háigir" "vilges#loppan" "vilges#luovvar" "vilges#máđir" "vilges#sennet" "vilges#sennet" "vilgon" "vintán" "viskositehta" "vispil" "visppon" "vissir" "visttan" "vizar" "viđa#čiegat" "viŋkil" "volta#mihttár" "vuhpon" "vujon" "vuobirs" "vuobis" "vuohes" "vuohtan" "vuohtan" "vuoidan#nihppel" "vuoiddas#božán" "vuoigatvuođa#kánsler" "vuoigŋan" "vuojahan#rusttet" "vuoján" "vuokta#boršta#doalan" "vuokta#gaccet" "vuokta#gaccet" "vuokta#láfol" "vuolahas#doaján" "vuolan" "vuolle#nohta" "vuopman" "vuorjjan" "vuorjjat" "vuoste#gieđa#geavat" "vuosttut" "vuostá#heavval" "vuovddit" "vuovde#gaccet" "vuovde#gaccet" "vuovde#godde#sáhpán" "vuovde#gáiskkit" "vuovde#sáhpal" "vuovde#sáhpán" "vuovde#veasen" "vuovde#vizar" "vuođđo#isit" "vuskkon" "vádjit" "vádjit" "váhkar" "váhkol" "váhnen" "váhttar" "váhtter" "váibbat" "váibmo#uvssot" "váiddon" "váivván" "válbor" "váldo#guottet" "váldo#ragat" "váldo#suopman" "vánhen" "váran" "várrehus#golmma#čiegat" "várvvár" "váskkir" "vátne#mielat" "vátnen" "váđas" "váđir" "váđot" "web#logan" "áddjot" "áddjut" "ádel" "áhkil" "áhkkut" "áhkkut" "áhkkár" "áhpe#rievssat" "áhttán" "áhččemin" "áibme#sákkan" "áibmo#bismmar" "áiccan" "áide#sáhpal" "áige#dávval" "áige#távval" "áige#čáján" "áinnut" "áiro#hopmir" "ákkil" "áksil" "áktor" "álbmot" "álgo#álbmot" "álin" "állan" "állat" "állat" "álon" "álsset#luovvar" "áltár" "ámmat" "ámmát" "ánnuitehta" "ápmil" "ápmir" "áppes" "áppet" "ápril" "áren" "árggan" "árka#cokkan" "árra#iđit" "árran" "ártnot" "árvo#dávvir" "árvo#ortnet" "ásat" "ásen" "ásii" "ásii" "ássi" "ávggos" "ávgos" "ávil" "ávjjur" "ávskár" "ávvir" "áŋkor" "østrogen" "č.ortnet" "čalbme#alán" "čalbme#oaidno#vihtan" "čalmme#oaidno#vihtan" "čalán" "čanan" "časkin#váiddon" "časkkan" "čavil" "čavččat" "čađan" "čeabet" "čeabet#njárččet" "čeabát" "čeahcet" "čeahcit" "čearpmat" "čearret" "čearret" "čearrit" "čeaskkut" "čeavllit" "čelččen" "čerggon" "čeŋkkeš#goartil" "čiegus#oaivil" "čieh#gahpir" "čiehka#dagan" "čiekčan#sabet" "čiekčár" "čielge#doalan" "čitna#hydráhta" "čiŋkkon" "čoahkkin" "čoahkkin#givssár" "čoahkkin#ortnet" "čoahkkár" "čoakkán" "čoakkán" "čoarve#heavval" "čoaskin" "čoavdda#bonjan" "čoavjjet" "čogat" "čohkkun#doalan" "čolon" "čorggut" "čuget" "čukkon" "čulggon" "čunu#goallár" "čuohpan" "čuoigan#sabet" "čuoiggan" "čuoih#gahpir" "čuoivvat" "čuojan" "čuolan" "čuonan" "čuorbban" "čuorggat" "čuorggut" "čuotnan" "čuovga#dávval" "čuovga#gilkor" "čurot" "čurrot" "čuvde#goartil" "čužot" "čábbon" "čáhce#bulkor" "čáhce#crossa" "čáhce#fápmo#rusttet" "čáhce#njaman" "čáhce#sabet" "čáhce#sáhpán" "čáhce#viehkat" "čáhce#ásat" "čáhpot" "čáhppes#bihppor" "čáhppes#dávval" "čáhppes#gaccet" "čáhppes#gaccet" "čáhppes#háigir" "čáhppes#jierit" "čáhppes#rieban" "čáhppes#vizar" "čáhppes#váđot" "čáhput" "čáihne#gaccet" "čálat" "čálggat" "čálgo#ortnet" "čálán" "čám#bohkal" "čám#bokkal" "čáppon" "čárvvon" "čázet" "čázit" "šealggut" "šeaman" "šelges#čuoivvat" "šelggon" "šibit" "šiiva#goazan" "šilljo#čuččot" "šilljo#čuččot" "širan" "šleaŋga#čuoivvat" "šlembbon" "šleđggon" "šleŋggon" "šliip0pár" "šlimir" "šlipsa" "šloahpar" "šlohtor" "šlubbo#oaivvat" "šluppot" "šlámbor" "šláŋŋa#čárvvon" "šoaran" "šoavkkut" "šolár" "šombbár" "šođbbut" "šuokŋa#gáffal" "šuvon" "šáhčir" "šávan" ;
     # in singular, Nom=Gen=Acc. The C-final ones are poetic short genitives
     # for the GAHPIR nouns, the V-final ones are either STAHTA ones or they
     # are III-II GOAHTI ones (the last group has not been systematically added)


# Sets for lexeme homonymy
# ------------------------

# The LAHKA set family
# - - - - - - - - - - 

# This is a family of sets made to account for the homony between "láhka" and
# "lága" in oblique cases.

LIST LAHKA = "aborta#láhka" "almennet#láhka" "almmolašvuođa#láhka" "apotehka#láhka" "arkiiva#láhka" "áhpenuohtástan#láhka" "álbmotoadjo#láhka" "bargiid#láhka" "bargobiras#láhka" "báikenamma#láhka" "bákte#láhka" "bátnedearvvašvuođabálvalus#láhka" "bivdo#láhka" "boazodoallo#láhka" "čázádat#láhka" "dálkkas#láhka" "dásseárvo#láhka" "deanu#láhka" "dearvvašvuođabargiid#láhka" "dearvvašvuođabargii#láhka" "dearvvašvuođa#láhka" "dearvvašvuođaregisttar#láhka" "divššohas#láhka" "divššohasdearvvašvuođa#láhka" "divššohasvuoigatvuođa#láhka" "duopmostuollo#láhka" "eana#láhka" "eanan#láhka" "eanadoallo#láhka" "eanandoallo#láhka" "eanavuovdin#láhka" "eananvuovdin#láhka" "eanansuodjalan#láhka" "eanasuodjalan#láhka" "eanavuovdin#láhka" "eatnan#láhka" "eatnandoallo#láhka" "eatnansuodjalan#láhka" "eatnanvuovdin#láhka" "elliidsuodjalan#láhka" "energi#láhka" "fápmudusa#láhka" "fápmudus#láhka" "Finnmárko#láhka" "friddjasuohkan#láhka" "fuođđo#láhka" "gávpot#láhka" "geahččo#láhka" "geaidno#láhka" "gearggusvuođa#láhka" "gielddadearvvašvuođabálvalus#láhka" "gielda#láhka" "giella#láhka" "gilioppalašeana#láhka" "girko#láhka" "golle#láhka" "guladigge#láhka" "guollebiebman#láhka" "háhkankonsešuvdna#láhka" "hálddahusláhka" "hálddašan#láhka" "heaggadieđuid#láhka" "huksen#láhka"  "industrikonsešuvdna#láhka" "jávrebivdo#láhka" "joatkkaoahpahus#láhka" "johtolat#láhka" "konsešuvdna#láhka" "kriminal#láhka" "kulturmuito#láhka" "lavdnjeloggun#láhka" "láhka" "láhka#namahus" "luodda#láhka" "luonddusuodjalus#láhka" "luopmo#láhka" "luossa#láhka" "láhka#ásahus" "mánáidgárde#láhka" "mánáid#láhka" "mánáidsuodjalusbálvalus#láhka" "mánáidsuodjalus#láhka" "mearraguolástan#láhka" "mearraresursa#láhka" "minerála#láhka" "mohtorfievrojohtolat#láhka" "mohtorjohtolat#láhka" "mudden#láhka" "namma#láhka" "nággo#láhka" "oahpahalli#láhka" "oahpahus#láhka" "oahpo#láhka" "olgunastin#láhka" "olmmošvuoigatvuođa#láhka" "oktasašsáme#láhka"  "pasientavuoigatvuođa#láhka" "persovdnabajásčuvgehus#láhka" "persovdnabajásčuvgen#láhka" "priváhtaskuvla#láhka" "ráŋggáštan#láhka" "reastaluvvan#láhka" "riektedikšo#láhka" "riekteveahkke#láhka" "sáivabivdo#láhka" "sáme#láhka" "sámi#láhka" "searvan#láhka" "sierra#láhka" "skuvla#láhka" "sosiála#láhka" "spesialistadearvvašvuođabálvalus#láhka" "stáhtaboargár#láhka" "stáhtaborgárrievtti#láhka" "stáhtafitnodat#láhka" "truvdnoárbe#láhka" "tuollu#láhka" "varasguolle#láhka" "várre#láhka" "vearrodahko#láhka" "vearro#láhka" "vuođđo#láhka" "vuođđoskuvla#láhka" "vuovdedoallo#láhka" "vuovdegáhtten#láhka" "vuovde#láhka" "1842-#láhka" "1863-#láhka" "1888-#láhka" "1899-bivdo#láhka" "1899-#láhka" "1902-#láhka"  "1920-várre#láhka" "1933-#láhka" "1965-#láhka" "1975-#láhka" "1975-várre#láhka" "1978-#láhka" ;

LIST LAHKA-MOD = "almennet" "almmolašvuođa" "apotehka" "arkiiva" "áhpenuohtástan" "álbmotoadjo" "bargi" "bargo#biras" "báike#namma" "bákte" "bátnedearvvašvuođa#bálvalus" "bivdo" "boazodoallo" "čázádat" "dálkkas" "dásseárvo" "deatnu" "dearvvašvuođa#bargi" "dearvvašvuohta" "dearvvašvuođa#fitnodat" "dearvvašvuođa#registtar" "divššohas#vuoigatvuohta" "eana" "eanasuodjalan" "eanavuovdin" "elliid#suodjalan" "energi" "fápmudus" "fápmudus" "Finnmárko" "friddja#suohkan" "fuođđo" "gávpot" "geaidno" "gearggusvuohta" "gielddadearvvašvuođa#bálvalus" "gielda" "gilioppalaš#eana" "irko" "golle" "guladigge" "guolle#biebman" "háhkan#konsešuvdna" "hálddašan" "heaggadieđut" "huksen"  "industri#konsešuvdna" "jávre#bivdo" "joatkkaoahpahus" "johtolat" "konsešuvdna" "kultur#muito" "lavdnje#loggun" "luodda" "luonddusuodjalus" "luopmo" "luossa" "mánáid#gárdi" "mánáidsuodjalus#bálvalus" "mearraguolástan" "mearra#resursa" "minerála" "mohtorjohtolat" "mudden" "namma" "oahpahalli" "oahpahus" "oahpo" "olgunastin" "olmmoš#vuoigatvuohta" "oktasašsáme"  "pasienta#vuoigatvuohta" "ráŋggáštan" "riekte#veahkke" "sáiva#bivdo" "sámi" "searvan" "sierra" "spesialistadearvvašvuođa#bálvalus" "stáhtaborgár#rievtti" "stáhta#fitnodat" "tuollu" "varas#guolle" "várre" "vearro#dahko" "vearro" "vuođđo" "vuođđo#skuvla" "vuovde#doallo" "vuovde#gáhtten" "vuovde" "1842-" "1863-" "1888-" "1899-#bivdu" "1899-" "1920-várre" "1933-" "1965-" "1975-" "1975-várre" "1978-" ;
# This list is for lahka compounds written with two single words

LIST LAHKA-VERB = "áimmahuššat" "árvalit" "ásahit" "čállit" "čuovvut" "gáibidit" "geavahit" "gieđahallat" "jorgalit" "mearridit" "ovddidit" "viiddidit" ;

LIST LAHKA-VERB-PASS = "atnit" "ásahit" "čállit" "čuoldit" "dadjat" "dahkat" "duššadit" "jorgalit" "jurddahit" "jurddašit" "mearridit" "oaivvildit" "rievdadit" ;

# This one works in combination with others, f.in. LAHKA-POST.
LIST LAHKA-NOUN = "§" "almmolaš#vuohta" "eana#girji" "eanan#girji" "eatnan#girji" "čađaheapmi" "fápmui#boahtin" "gáibádus" "geatnegas#vuohta" "geavaheapmi" "juksan#mearri" "kapihtal" "kapihttal" "láhka#ásahus" "mearrádus" "njuolggadus" "nr" "paragráfa" "riekte#subjeakta" ;

# This one does it more likely to be "láhka" in combination with f.i. LAHKA-VERB:
LIST LAHKA-NOUN-SUBJ = "eise#váldi" "guovddášeise#váldi" "stáhta" ;

LIST LAHKAGEN-NOUN = "ráhkkanan#bargu" ;

LIST LAHKA-ASAHUS = "alimus#riekti" "Stuoradiggi" "Stuorradiggi" "stuora#diggi" "stuorra#diggi" ;

LIST LAHKA-POST = "olis" "mielde" "vuođul" ;

LIST LAGA = "bargo#lága" "lága" "spábbačiekčan#lága" ;
# This is for lága compounds

SET NOT-LAHKA = N - LAHKA ;


#  Other compounds
# - - - - - - - - -
             
LIST XDEPARTEMEANTA = "bearaš#departemeanta" "eanandoallo#departemeanta" "dearvvašvuođa#departemeanta" "kultur#departemeanta" "birasgáhtten#departemeanta" "dutkan#departemeanta" "olgoriikka#departemeanta" ;

LIST XBEALLE = "duon#bealle" "suoma#bealle" "Suoma#bealle" ;
    # X side

LIST XBEALLI = "duon#bealli" "suoma#bealli" "Suoma#bealle" ;
    # half of X
     
     
# The girje/girji sets     
LIST GIRJI = "čoahkkin#girji" "girji" "oahppo#girji" ;
LIST GIRJE = "čoahkkin#girje" "girje" "oahppo#girje" ; 
# Words with a vowel-lowering variant.     
     


LIST GIELDA = "fylkka#gielda" "riddo#gielda" "sáme#gielda" "Sáme#gielda" "vuotna#gielda" ; 
    # We need this set because there is ambiguity between "gielda"+Gen/Acc and "gieldda"+Nom.

LIST GIELDDA = "gieldda" ;
     
     

# Nominal sets defined according to their semantical properties
# - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -


# Here we will write what sets we have and how we divide them.

# 


# Time sets
# - - - - -

LIST MANNU = "ođđa#jage#mánnu" "ođđa#jagi#mánnu"  "guovva#mánnu" "njukča#mánnu" "njuvčča#mánnu" "márs#mánnu"  "cuoŋo#mánnu" "cuoŋos#mánnu" "gáranas#mánnu"  "miesse#mánnu" "mái#mánnu"  "geasse#mánnu"  "suoidne#mánnu"  "borge#mánnu" "šnjilča#mánnu" "šnjilža#mánnu"  "čakča#mánnu"  "golggot#mánnu"  "skábma#mánnu" "november#mánnu" "ritne#mánnu"  "juovla#mánnu" ;

LIST AIGODAT = "ahtanuššan#áigodat" "áigi" "áigodat" "geassin#áigodat" "stuoradigge#áigodat" "sámedigge#áigodat" "guođohan#áigodat" "doaibma#áigodat" ;             

LIST VAHKKU = "beaivi" "b" "jándor" "bodda" "čakča" "čakča#geassi" "čakča#seavdnjat" "čuohte#jahki" "dálvi"	"diibmu" "dbm" "doaibma#jahki" "geardi" "geassi" "giđđa" "hávvi" "idja" "jahki" "jahke#čuohti" "mánnu" "mihcamárra" "riegádan#beaivi" "tiibmu" "vahkku" jahki "stuorradigge#áigodat" "1700-#lohku" ;
 # The quoteless jahki here is since the preprocessor is not fine-tuned
 # yet, it still gives '"1870-" # jahki N Sg Com'. # lets see if "dbm" works
 
LIST AIGI = "áigi" "bivdo#áigi" "Beassaš-#áigi" "čakča#áigi" "dálve#áigi" "geasse#áigi" "juovla#áigi";
    # Maybe it could be added to VAHKKU?

 
LIST BEAIVI = "mánnodat" "vuoss#árga" "disdat" "maŋŋeb#árga" "gaska#vahkku" "duorastat" "bearjadat" "lávvardat" "lávvordat" "sotna#beaivi" "duopmo#beaivi" "sábbát";

SET TIME = MANNU | VAHKKU | BEAIVI | AIGI ;
    # Do not add AIGODAT, it has some other characteristics in some cases.

SET NOTIME = N - TIME ;

LIST GEASSET = "dálvet" "dálvit" "giđđat" "geasset" ("geassit" Adv) "čakčat" ; # What is special here?


# Amount sets
# - - - - - -

LIST MEASURE = "miila" "kilo#mehtar" "kilo#mehter" "mehtar" "mehter" "centi#mehtar" "centi#mehter" "sente" "cm" "milli#mehtar" "milli#mehter" "tumá" "állan" "salla" "lihtter" "gilo" "čuolda#badji" "fierbme#badji" ;
LIST CURRENCY = "kruvdnu" "kr" "ruvdnu" "¢" "€" "$";
LIST CURRENCY-GEN = ("kruvdnu" Gen) ("kr" Gen) ("ruvdnu" Gen) ("¢" Gen) ("€" Gen) ("$" Gen);
SET NOT-CURRENCY-GEN = N - CURRENCY-GEN ; 	

LIST NUMUNIT = "čuohti" "duhát" "miljon" "millijovdna" "milliovdna" "mill" "milj" "miljo" "miljarda" "mrd";

LIST KIND = "šládja" "válga-#šládja" ;

LIST COUNTABLE = "oassi" "kapihttal" "paragráfa" ;

LIST NUMMAR = "nr" "nummar" "nummir" "tabealla#nummar" ;

LIST KLASS = "ášši" "s" "siidu" "§" "§§" "pragráfa" "oassi" "kap" "kapihttal" "kapihtal";


# Case-specific sets
# ------------------

LIST PL-NOUN = "beassaš" "čalbme#láse" "gálssot" "headja" "sabet" "sisttet" 
	"skárri"; 
 # Nominals that appear with plural numerals.
 
LIST SG-WORD = "ipmárdus" "doaivu" "dáro#giella" "gelbbolašvuohta" ("guovtte#gielalaš" Der/vuohta) "kultur#duogáš" "kultur#gelbbolašvuohta" "ovttas#bargu" "sáme#giella" ;
# Words that are used in singular unless their plurality is stressed by words such as "goappeš", "goappašat", "earálágan", "máŋgalágan" or Num or Ord.



# Nouns with syntactic preferences
# --------------------------------

LIST LOC-NOUN = "dáhpáhus" "dilálašvuohta" "ipmárdus" "oktavuohta" ;
    # Those nouns appear in locative unless a head asks for comitative.
    
LIST N-NOPX = "lohku";
 # Nominals that probably do not have a Px ('1600-logus').

LIST N-PX = "áhčči" "bárdni" "biigá" "boadnji" "eadni" "eammi" "gáibmi" "guoibmi" "nieida"  "oabbá" "olmmái" "ráhkis" "reaŋga" "skibir" "skihpár" "ustit" "viellja" ;
 # Nominals that well may have a Px. 

LIST ALU = ("allu" Gen)("assu" Gen)("čiekŋu" Gen)("gassu" Gen)("govdu" Gen)
	   ("guhkku" Gen)("lossu" Gen)("stuorru" Gen)("viidu" Gen);
	   # Those are words that only appear in one form, which Nickel calls A/G.
       # They are only in a predicative position.

LIST TITLE = "ámta#mánni" "direktevra" "eanan#hearrá" ("gásttašit" eaddji) "gonagas" "justitiarius" "korefereanta" "ossodat#direktevra" "profehta" "professor" "prof" "ráđđe#addi" "stipendiáhta" "sundi" "universitehtalektor" "vuosttas#konsuleanta" "cand.mag" "cand.oecon" "cand.philol" "cand.real" "cand.scient" "cand.theol" "cand" "dr.art" "eksp.hoavd" "eksp.sj" "fenr" "fig" "fil.tri" "gen.lt" "genr" "genr.lt" "h.r.adv" "hr" "kapt" "korp" "lekt" "lic" "ltn" "mag.art" "mag" "máilmme#meašttir" "merc" "odont" "oecon" "ordf" "ossod.dir" "stud.phil." "rektor" "res.kap" "stud.philol" "vit.ass" "dr" "dr.med" "dr.philos" "dr.theol" ; 

    
# Nouns as predicates
# -------------------

# subcategorizing for a particular case

LIST LOC-COMPL-NOUN = "ávki" "árvalus" "dovdu" "ipmárdus" "ovda#mearka" ;
    # Nouns that take a locative
    # There is also an idiomatic use of "ovda#mearka" in the constructions "ovdamearkka dihte" and "ovdamearka" (Ess).
    
LIST COM-COMPL-NOUN = "bargu" "lihkku" "ovtta#oaivil" "ovttas#bargu" "šiehtadus" ;

SET LOC-HEAD = LOC-VERB | LOC-COMPL-NOUN ;
SET COM-HEAD = COM-VERB | TOGETHER-VERB | COM-COMPL-NOUN ;

    
LIST PREGEN = ("álgogeahčen")("bealli" N Sg Loc)("gaskavuohta" N Sg Loc)("guoskevaš")
	("loahpageahčen")("miella" N Sg Loc)("oktavuohta" N Loc);
 # Nouns and adjectives that take a preceding genitive.
 
 




            
    
# Set hierarchy
# -------------

# this part is aiming at an overall semantic categorization of nouns
# the sets before might be aiming at a solution of particular syntactic or lexical problems (even those sets of course also aim at the solution of ambiguities :) )

# the 1. level distinguishes between abstract an concrete
# at the 2. level the feature concrete splits up into animate and not-animate

# SET ABSTRACT = EVENT | STATE | CONCEPT | ABSTRACT-ENTITY | ACTIVITY ;
# SET CONCRETE = ANIMATE | PLACE | TOOL ;

# We start with CONCRETE, anyway we have to start at the bottom, so that the individual lists (containing items from the lexicon) are defined before we use them in sets (containing list names)


# First-order enti
# Concrete entitites

# Living beings
# Humans

LIST HUMAN-INDIVIDUAL = "áhčči" "áddjá" "áhkká" "áhkku" "bártni" "boadnji" "boaris" "čeahci" "eadni" "eahki" "eamit" "eammi" "eanu" "gánda" "goaski" "guoibmi" "isit" "lagamuš" "muoŧŧa" "nieida" "nisson" "nisson#olmmoš" "nisu" "nuorra" "oabbá" "oambealli" "oarpmealli" "olbmo" "olmmái" "rávis#olmmoš" "siessá" "skibir" "skihpár" "ustit" "váhnen" "viellja" "vilbealli" "viissis";



LIST HUMAN-GROUP = "álgo#álbmot" "boanda#álbmot" "boazoguohton#kommišuvdna" "dáčča#álbmot" "Dale#álbmot" "delegašuvdna" "digge#álbmot" "eami#álbmot" "giliolbmo#joavku" "guolástus#álbmot" "kommišuvdna" "luonddu#álbmot" "media" "muosát#álbmot" "muosát-#álbmot" "prošeakta#joavku" "sáme#álbmot" "sámi#álbmot" "stivren#joavku" "unnit#álbmot" "unnitlohko#álbmot" "váldo#álbmot" "Vuotna#álbmot"  ;
    ## group of humans that connot be a building at the same time

LIST PROFESSION = "advokahta" "arkeologa" "artista" "assisteanta" "astoáigge#jođiheaddji" "áittardeaddji" "ákšo#álmmái" "ámmáhis#bargi" "ámmát#olmmái" "ásahus#hoavda" "ášše#jođiheaddji" "ášše#meannudeaddji" "badjelii#geahčči" "bagadalli" "bagadeaddji" "bajimusbearrái#geahčči" "bargiid#hoavda" "bargi" "bargiid#konsuleanta" "bargo#veahka" "bassaleaddji" "bassi" "báhkkár" "báhppa" "bálvaleaddji" "báŋko#hoavda" "bátne#dikšu" "bátne#doavttir" "beaivválaš#jođiheaddji" "bearašmánáidgárde#jođiheaddji" "bearrái#geahčči" "beavdegirje#čálli" ("bearre#mánnat" Actor) "bibliotekára" "biigá" "biila#divvu" "bileahta#vuovdi" "biras#bargi" "birasgáhtten#jođiheaddji" "birasgáhttenráđđe#addi" "biras#terapevta" "bisma" "bismá" "bispa" "bivdi" "bivttas#hábmejeaddji" "boanda" "boazo#doalli" "boazo#sápmi" "buohcce#dikšu" "buohcce#divššár" "busse#vuoddji" "buvttadeaddji" "čalbme#áhkku" "čalbme#doavttir" "čalbme#eadni" "čálli" "čorgejeaddji" "čuojaheaddji" "dáidár" "dáiddár" "dálon" "dálu#eamit" "dálu#isit" "dárkkisteaddji" "dearvvašvuođabuohcce#dikšu" "dearvvašvuođabuohcce#divššár" "dikšu" "dikta#čálli" "dirigeanta" "divššár" "doaimmahat#hoavda" "doaimmaheaddji" "doavttir" "dulka" "duodje#bagadalli" "duodje#konsuleanta" "duojár" "duopmar" "dutkan#assisteanta" "dutki" "ealáhus#konsuleanta" "ealáhus#bagadeaddji" "ealáhus#doalli" "ealáhus#hoavda" "ealáhus#jođiheaddji" "ealga#bivdi" "eallo#doalli" "eanan#doalli" "eanandoalli#sadjásaš" "eatnan#doalli" "eatnandoalli#sadjásaš" "eanandoallo#hoavda" "eatnandoallo#hoavda" "eise#váldi" "festivála#jođiheaddji" "fitnodat#bargi" "fitnodat#doaibmi" "fitnodat#jođiheaddji" "fitnodat#oamasteaddji" "fylkka#mánni" "fysio#terapeuta" "fysio#terapevta" "gávpe#assisteanta" "gávpe#hoavda" "gávpejas" "geahččoeise#váldi" "geaidno#bargi" "gieh#mánni" "giella#bargi" "giella#konsuleanta" "giellamiel#bargi" "gievkkan#assisteanta" "gievkkan#veahkki" "gievkkan#hoavda" "girdi" "girje#čálli" "girje#vuovdi" "girko#bálvaleaddji" "girko#dulka" "golle#rávdi" "govva#dáidár" "govva#dáiddár" "govvedeaddji" "govvejeaddji" "guolásteaddji" "guolle#bivdi" "hálddahus#bargi" "hábmejeaddji" "hárjeheaddji" "hoavda" "jietna#teknihkar" "jođiheaddji" "journalista" "juolge#dikšu" "kántor#bargi" "kántorhoavda" "kántur#bargi" "kántur#hoavda" "koahkka" "koara#dirigeanta" "konsuleanta" "kultur#bargi" "kultur#journalista"  "kultur#konsuleanta" "kursa#oahppi" "lágideaddji" "láibu" "lávlu" "leans#mánni" "leanut" "logopeda" "lotnolasealáhus#bargi" "luhkkár" "mánáidgárde#assisteanta" "mánáidgárde#jođiheaddji" "mánna#biigá" "mátke#jođiheaddji" "mearra#almmái" "mearra#olmmái" "miel#bargi" "museá#bargi" "musihkka#dahkki" "musihkkár" "návet#vázzi" "neavttár" "nuoraid#bargi" "oahpahalli" "oahpaheaddji" "oahppi" "oahppo#bagadalli" "oaivadeaddji" "oaivámuš" "ođas#hoavda" "ođas#lohkki" "ossodat#direktøra" "ossodat#jođiheaddji" "ovdaskuvla#oahpaheaddji" "parkeren#fákta" "pedagoga" "plánejeaddji" "poliisa" "politiija" "prentejeaddji" "professor" "prográmma#jođiheaddji" "prošeakta#bargi" "prošeaktamiel#bargi" "rádje#fákta" "rádje#soalddat" "ráđđe#addi" "ráđđe#olmmái" "rávdi" "reaŋga" "redaktevra" "rehketdoallo#dárkkisteaddji" "rehketdoallomiel#bargi" "rehket#doalli" "rektor" "rievssat#bivdi" "ruhttejeaddji" "ruvke#bargi" "ruvvejeaddji" "sadjásaš" "sárdnideaddji" "sávza#doalli" "sealge#eadni" "silba#rávdi" "skuvla#assisteanta" "skuvla#veahkki" "snihkkár" "soalddat" "sosial#bargi" "sosionoma" "spábbačiekčan#hárjeheaddji" "spábba#čiekči" "stáhta#čálli" "stáhta#ministtar" "studeanta" "suohkan#báhppa" "suohkan#doavttir" "suohkan#plánejeaddji" "suohkanfysio#terapeuta" "šibit#doalli" "šibit#doavttir" "tákse#vuoddji" "teáhter#bagadalli" "teknihkar" "telefon#vástideaddji" "turista#ofelaš" "uksa#fákta" "váldo#doaimmaheaddji" "várresátne#jođiheaddji" "veahkke#bargi" "veahkke#dikšu" "viessobearrái#geahčči" "viesso#hoiddár" "vuokta#čuohppi" "vuorro#hoavda" "vuovde#bargi" "vuovdi"; 

LIST OFFICE = "áirras" "bearaš#minister"  "dekana" "eanadoallo#minister" "fylkka#ráđđi" "gieldda#minister"  "guolástus#minister" "kultur#minister" "ministtar" "nisson#áirras" "politihkar" "politihkkár" "sága#doalli" "Sámedigge#presideanta" "sátne#jođiheaddji" "stivra#lahttu" "suodjalus#minister" "ovda#olmmoš" "presideanta" "stáhta#ráđđi" "várre#presideanta" ;

LIST ROLE =  "almmolašvuohta" "ássi" "čearda" "geavaheaddji" "stáhta#boargár" "virge#olmmoš" "vuoste#belle" "vuoste#bealli" ; #XXX bad name


SET HUMAN = HUMAN-INDIVIDUAL | HUMAN-GROUP | PROFESSION | ROLE | OFFICE ;


# Part-whole for human

LIST BODY = "ámadadju" "bahta" "bahta#ráigi" "bállo#suotna" "beađbe#lađas" "bealgi" "beallji" "bierggas" "bieža#dákti" "billu" "buđđá" "čalbme#deahkki" "čalbmi" "čatnan#gođđu" "čeabet" "čelččen" "čeŋkkeš" "čibbe#skálžu" "čibbi" "čielge#ađa" "čielge#dákti" "čielgi" "čoalli" "čoamohas" "čoarbbeal#deahkki" "čoarbbeal#lađas" "čoarb#bealli" "čoavji" "čorbma#oassi" "čorbma#váimmus" "čorbma#vuođđu" "čotta" "čuožžamas#deahkki" "čurti" "čuvdi" "dábba" "dáđvi" "dákte#gođus" "dákti" "dávdi" "deahkke#gođus" "deahkki" "erttet" "erttet#deahkki" "gahpárus" "gahperus" "gardnjil" "gaska#suorbma" "gassa#čoalli"  "gassa#váhkká" "gáddu" "gáibi" "gállu" "gánis" "gátnis" "geađđi" "geahpis" "giegir" "giehta" "giehta#deahkki" "giehta#gávva" "giehta#láhpi" "giehta#ramas" "giehta#ruohtta" "giehta#sealgi" "giehta#váibmu" "giehta#váimmus" "gilga" "goapmir#váimmus" "goaŧŧo#čotta" "goaŧŧu" "goike#dákti" "gullu" "guoggomas" "guoggomas#ráksá" "gurot#giehta" "hárdu" "heagga#roasmmoheapmi" "jarri" "juolge#bealgi" "juolge#čelččen" "juolge#gávva" "juolge#lađas" "juolge#láhpi" "juolge#mátta" "juolge#suorbma" "juolge#vuođđu" "juolgi" "lađas" "láhpe#lađas" "manin" "manne#jođas" "manne#ráksá" "maŋŋe#buoidi" "mánágoahte#čotta" "máná#goahti" "mielga#njunni" "monimuš" "monin" "monne#jođas" "monne#ráksá" "morče#suotna" "morči" "muohtu" "náhpi" "niehkki" "nierra" "niske#gohpi" "niski" "njálbmi" "njiehcehas" "njunni" "njuovčča" "oaive#assi" "oaive#čohkka" "oaivi" "oalge#deahkki" "oalgenoras#deahkki" "oalgenoras#váhkká" "oalgi" "oalul" "obba#lággá" "orbbeš" "raddi" "rápma" "rumaš" "rumaš#golggus" "rumaš#lahttu" "rumaš#njalbi" "sáhppe#suvri" "rumaš#golggus" "rumaš#lahttu" "rumaš#njalbi" "sáhppe#suvri" "siidu" "siskkit#beallji"  "sotken#oalli" "spiral" "suolbmudan#oalli" "suorbma" "šušmi" "váhkká#deahkki" "váibmu" "vuohčče#cuozza" "vuoivvas" "vuokta" "vuolit" "oassi" "gieđas" "vuorggu";



# Animals

LIST ANIMAL = "ábe#gáhttu" "áldu" "bálddis" "beana" "boazu" "bohkká" "ciiku" "čivga" "čuska" "dámmá" "dorski" "ealit" "gáhttu" "gáica" "gálbi" "gihcci" "gilot"  "guigu" "gussa" "hávga" "heasta" "juksu" "láppis" "loddi" "máhtu" "miessi"  "muortu" "njiŋŋálas" "ore" "sallit" "sarvvis" "sávza" spiinne#čivga "spiidni" "stábbasáidi" "stáinnir" "stuorasáidi" "rutnot" "vársi" "vielppis" "vierca" "vuoksá" "vuoncá#varis" "vuonccis" "vuonjal" ;

#LIST BIRD = "ákta" "báiski" "bovttáš" "cagan" "čearret" "gáiru" "skávli" "šuoršu" ;

LIST PLANT = "ávža" "beahci" "bastilis#látnjá" "boallo#rássi" "cizaš#vuolpu" "duopma" "fiskes#rássi" "gáma#suoidni" "gáiski" "gáskálas" "golle#rássi" "guossa" "horbmá" "juopmu" "lageš" "leaibi" "reatká" "sállja" "sieđga" "skáhpi" "skierri" "soahki" "suhpi";


SET ANIMATE = HUMAN | ANIMAL; 





# Things

LIST TOOLS = "áibmi" "ákšu" "baste" "bierggas" "bissu" "buođđu" "čuoibmi" "fierbmi" "goallun#stoahku" "golgadat" "HV-#bissu" "joddu" "joddu-#buođđu" "luovos#fierbmi" "mohtor#sahá" "muorra" "nálu" "nibi" "skárbban" "skierra" "soabbi" "vuogga";
    # Those could be further subdivided into: kitchen tools, working tools, hunting tools, fishing tools, writing tools...
    
#LIST FISHING-TOOLS = "buođđu" "fierbmi" "giehta#váđđu" "goallun#stoahku" "joddu" "joddu-#buođđu" "liidna" "luovos#fierbmi" "vuogga";
#LIST HUNTING-TOOLS = "bissu" "HV-#bissu" "njuolla";
#LIST KITCHEN-TOOLS = "baste" "gealva";
#LIST WORKING-TOOLS = "ákšu" "goaivu" "mohtor#sahá" "nibi";
LIST WRITING-TOOLS = "bliánta" "bleahkka" "dihtor" "krihtta" "liánta" "peanna";

#LIST HANDICRAFT-TOOLS = "áibmi" "nálu" "suorbma#goahti" "vikšamuorra" ;

LIST ABSTRACT-TOOLS = "bearrái#geahčču" "fápmu" "geahčču" ;
#Those are typically comitative (instead of locative) if they go together with verbs from the LIST ABSTRACT-ACTIVITY-VERB and probably also from INTELLECTUAL-ACTIVITY-VERB. The counterpart for locative is until now SITUATION, we might have to consider making a new list ABSTRACT-TOOL comprising SITUATION and other elements.

    
LIST CONTAINTERS = "bohtal" "eappir" "gohppu" "gore" "lihtti";
    #Those always function as units too, such as in: A bottle of water.

LIST VEHICLE = "biila" "boasta#biila" "borjjas#fanas" "borjjas#vanca" "bussa" "busse" "dámpa" "fanas" "fievru" "gálvo#biila" "geaksu" "girdi" "guorbme#biila" "heargi" "heasta" "helikopter" "johka#fanas" "láivi" "mini#bussa" "mini#busse" "mohtor#fanas" "mohtor#fievru" "mohtor#sihkkel" "mohtor#vanca" "mopeda" "njealjit" "njeallje#juvllat" "ruvtto#biila" "sihkkel" "skohter" "skuter" "skuvla#bussa" "skuvla#busse" "suhkki#vanca" "tráktor" "vanas" "vanca";


LIST TEXT = "aviisa" "ášše#logahallan" "biras#logahalla" "bláđđi" "boađus#logahallan" "čujuhus#logahallan" "dásádat#logahallan" "dieđáhus" "digge#logahallan" "divššohas#journála" "eana#girji" "eanaopmodat#logahallan" "GAB-#logahallan" "giddodat#logahallan" "girji" "huksen#logahallan" "jahke#dieđáhus" "jahke#logahallan" "johtti#čálus" "johtu#čálus" "journála" "journála#čállin" "journála#diehtu" "lasta" "láđđi" "listu" "logahallan" "mannohadde#logahallan" "málbma#logahallan" "mearka#logahallan" "miellahtto#logahallan" "mollengeađge#logahallan" "mielddus" "opmodat#logahalla" "reive" "ruovtto#siidu" "stuorradigge#dieđáhus" "teaksta" "vahátealli#logahalla" "vealge#logahallan" "vearro#logahallan";
    # A potential place (locative?) for sth. written.
    # Maybe one should differentiate between written and spoken texts

LIST ABSTRACT-TEXT = "dievasčoahkkin#mearrádus" "láhka#mearrádus" "máinnas" "mearrádus" "muitalus" ;
    # A potential place (locative?) for sth. written or abstract activity 




# Places

LIST BUILDING = "áiti" "barta" "buvda" "buvri" "dállu" "fiervá#buvri" "gearde#viessu" "geasse#buvri" "goahti" "gávpi" "girku" "kram#buvda" "láđđu" "láhtu" "mánáid#skuvla" "mearra#buvri" "návet" "návet#goahti" "návsti" "návstu" "nuoraid#skuvla" "orrun#viessu" "ráido#viessu" "rásse#láđđu" "skuvla" "stálle" "stohpu" "universiteahta" "viessu" "visti";

LIST GENERAL-PLACE = "ássan#báiki" "ássan#guovlu" "báike#goddi" "báiki" "biras" "boazodoallo#guovlu" "buođđo#sadji" "čakča#báiki" "čakča#orohat" "čoahkke#báiki" "dálve#báiki" "dolla#gáddi" "duottar#guovlu" "gávpe#báiki" "gávpot" "geasse#báiki" "gilli" "giđđa#báiki" "guovlu" "goahte#sadji" "guohton#eana" "guohton#eanan" "johtin#guovlu" "mátta#guovlu" "meahcce#gilli" "ravda#guovlu" "riika" "ruoktu" "sadji" "sámi#guovlu" "siseatnan#guovlu" "sohka#guovlu" "sallju" "siida" "šillju" "viesso#sadji" "viessu" "vuotna#guovlu" "vuovde#guovlu" ;

LIST GEOGRAPHICAL-PLACE = "bákti" "beahce#vuovdi" "borri" "coagis" "čolpi" "dearbmi" "duottar" "eana" "eanan" "eatnan" "eatnu" "fielbmá" "fiervá" "gáddi" "gáisá" "gáisi" "gárggu" "gárgu" "gieddi" "gohpi" "gohppi" "gorži" "guolbban" "jávri" "jávre#gáddi" "johka" "johka#gáddi" "johka#njálbmi" "láddu" "láttu" "latnja" "lokta" "luohkká" "luokta" "luoppal" "meahcci" "mearra#gáddi" "mielli" "mohkki" "muotki" "njárga" "njunni" "oaivi" "oalge#johka" "oalgi" "orda" "orrit" "reašši" "riddu" "riidi" "soahke#vuovdi" "stealli" "suolu" "vággi" "várre#vielti" "várri" "vielti" "vuohppi" "vuotna" "vuotna#gáddi" "vuovdi" ;


LIST POLITICAL-PLACE = "riika" "sáme#eana" "Sáme#eana" "stáhta" ;
    ## can also function as an actor
    
LIST FYLKA = "Finnmárku" "Finnmarku" "Romsa" "Nordlánda" "Lulli-Trøndelag" "Davvi-Trøndelag"
             "Hedmárku" "Akershus" "Buskerud" "Vestfold" "Østfold" "Aust-Agder" "Vest-Agder"
             "Rogalánda" "Hordalánda" "Opplánda" "Fjordane" "Romsdal" "Telemark" ;
    

LIST ROUTE = "bálggis" "fávli" "gaska" "geaidnu" "guorra" "johttin#geaidnu" "láhttu" "luodda" "luonddu#bálggis" "máđidja" "máđii" "miila" "ráidu" "spáiddar#ráidu"; # and others!

#LIST GOAHTI-PLACE = "árran" "boaššu" "loaidu"; 

LIST ROOM = "bártniid#hivsset" "feaskkir" "gievkkan" "hivsset" "kantuvra" "klássa#latnja" "lášmmohallan#sále"  "lášmmohallan#sálla" "lokta" "luohkká#latnja" "molssodan#latnja" "nieiddaid#hivsset" "nohkkan#latnja" "oađđen#latnja" "oađđin#latnja" "oahpaheaddji#latnja" "sallju" "stohpu" "šillju" ;

#LIST FURNITURE = "hildu" "ildu" "seaŋga" "skáhppe" "soffá" "stuollu" ;

SET PLACE = GEOGRAPHICAL-PLACE | POLITICAL-PLACE | GENERAL-PLACE | ROUTE ;




# Abstract


             
LIST ORGANIZATION = "álbmot#allaskuvla" "alla#skuvla" "ámmát#skuvla" "ásahus" "bargo#kantuvra" "báhpa#skuvla" "bellodat" "birasgáhtten#departemeanta" "buohcce#stohpu" "buohcce#viessu" "dearvvašvuođa#ásahus" "dearvvašvuođa#fitnodat" "departemeanta" "departementa" "doaibma#guovddáš" "dutkan#departemeanta" "eanandoallo#departemeanta" "eanandoallo#departementa" "eise#váldi" "fitnodat" "geahččo#doaibma" "geahččoeise#váldi" "gelbbolašvuođa#guovddáš" "giddagas" "gielda#departementa" "giella#guovdaš" "giella#ráđđi" "girku" "guovllu#stivra" "guovllubuohcce#viessu" "hálddašan#orgána" "dearvvašvuođa#guovddáš" "institušuvdna" "joatkka#skuvla" "johttisámi#searvi" "klinihkka" "kulturmuito#ráđđi" "láhka#ásahus" "Luondduhálddašandirektoráhta" "luondduhálddašan#direktoráhta" "mánáid#gárdi" "mánáid#skuvla" "meieriija" "musea" "našunál#diggi" "nuoraid#skuvla" "oahpahus#ráđđi" "olgoriikka#departemeanta" "orgána" "parlameantta" "psykiatriijabuohcce#viessu" "ráđđehus" "ráđđi" "reguleren#ráđđi" "riika#searvi" "Sámedigge#ráđđi" "Sámediggeráđđi" "sáme#siida" "servodat" "skuvla" "skuvla#kantuvra" "skuvlla" "stáhta" "suohkan" "suorgi" "synagoga" "universiteahtta" "virgáibidjan#ráđđi" "vuođđo#skuvla" 
"dearvvašvuođa#stašuvdna" "gielda" "riddo#gielda" "sáme#gielda" "Sáme#gielda" "vuotna#gielda" ; 
# those can both be buildings and represent humans

LIST INSTITUTION = "audiopedagoga#bálvalus" "álgo#bálvalus" "álgodearvvašvuođa#bálvalus" "bagadan#bálvalus" "báikenamma#bálvalus" "bálvalus" "bátnedearvvašvuođa#bálvalus" "bearráigeahččan#bálvalus" "bibliotehka#bálvalus" "birrajándor#bálvalus" "boles#bálvalus" "čállin#bálvalus" "data#bálvalus" "dábálašdoavtter#bálvalus" "dálveluodda#bálvalus" "dearvvašvuođa#bálvalus" "dieđihan#bálvalus" "diehtojuohkin#bálvalus" "divvun#bálvalus" "doarjja#bálvalus" "dulkon#bálvalus" "duollo#bálvalus" "fierpmádat#bálvalus" "fysioterapiija#bálvalus" "fitnodatdearvvašvuođa#bálvalus" "fuolahus#bálvalus" "fuolla#bálvalus" "gádjun#bálvalus" "gielddadearvvašvuođa#bálvalus" "girjerádjo#bálvalus" "girjerájus#bálvalus" "gohcci#bálvalus" "govdafierpmádat#bálvalus" "gulahallan#bálvalus" "huksehan#bálvalus" "inšenevra#bálvalus" "jordamor#bálvalus" "jorgalan#bálvalus" "konsuleanta#bálvalus" "maŋŋáhárjehallan#bálvalus" "mánáidsuodjalus#bálvalus" "militear#bálvalus" "neahtta#bálvalus" "nuoraid#bálvalus" "oaivadan#bálvalus" "oahpahus#bálvalus" "oasse#bálvalus" "oskku#bálvalus" "PP-#bálvalus" "poasta#bálvalus" "ráfi#bálvalus" "ruovtto#bálvalus" "ruovttu#bálvalus" "sámegiel#bálvalus" "servodat#bálvalus" "sihkarvuođa#bálvalus" "skuvladearvvašvuođa#bálvalus" "soalddát#bálvalus" "sosiála#bálvalus" "spesialista#bálvalus" "spesialistadearvvašvuođa#bálvalus" "spesialistadearvvašvuođa-#bálvalus" "suodjalan#bálvalus" "tele#bálvalus" "telematihkka#bálvalus" "vákšun#bálvalus" "Várre#bálvalus" "vuođđodearvvašvuođa#bálvalus" "vuoste#bálvalus" "Vuovde#bálvalus" ;
# the previous items can both represent abstract processes and humans

#SYMB-INSTITUTION = "njuolggadus" "vuoigatvuohta" "láhka" "ortnet" "čoahkkin#ortnet" ;

# Two aspects: Place and institution:
LIST EDUCATION = "alla#skuvla" "alla#skuvla#oahppu" "álbmot#alla#skuvla" "álbmot#alla#skuvla#oahppu" "álgo#oahppu" "ámmát#skuvla" "artium#kursa" "badje#dássi" "báhpa#skuvla" "boazodoallo#fága" "boazodoallo#suorgi" "boazodoallofága#oahppu" "eanandoalloalla#skuvla" "fága" "fidno#skuvla" "gaska#skuvla" "gávpe#oahppo#skuvla" "gierdo#skuvla" "gohttehat#skuvla" "gursa" "joatkka#oahppu" "joatkka#skuvla" "kursa" "luohkká" "mánáid#skuvla" "musihkka#skuvla" "musihkka#suorgi" "musihkkaalla#skuvla" "nuoraid#skuvla" "oahpaheaddjialla#skuvla" "oahpaheaddje#oahppu" "oahpaheai#skuvla" "oahpaheaialla#skuvla" "oahppu" "ovda#skuvla" "páhpa#skuvla" "ráves#olbmuid#skuvla" "rávis#olbmuid#skuvla" "rekrutt#skuvla" "rihppa#skuvla" "skuvla" "skuvlla" "smávva#skuvla" "sotna#beai#skuvla" "stuorra#skuvla" "suorgi" "teateralla#skuvla" "teáhteralla#skuvla" "universitehta" "veahkke#skuvla" "verdde#skuvla" "vuođđo#skuvla" "vuolle#dássi" ;

LIST INDUSTRY = "bálvalus#ealáhus" "bivdu" "boazo#ealáhus" "boazo#doallu" "ealáhus" "eanan#doallu" "guolle#bivdu" "luossa#bivdu" "lotnolas#ealáhus" "meahcce#ealáhus" "sáme#ealáhus" "tertieara#ealáhus" "vuoddo#ealáhus" "vuođđo#ealáhus" ;






SET ACTOR = ANIMATE | INSTITUTION | ORGANIZATION ;
    # Should EVENT be part of this set?


LIST STATE = ("dearvvas" Der/vuohta) "váivi";
    
    
LIST ABSTRACT-ENTITY = "fádda" "šiehtadallan#fáddá" ;   
    
LIST SITUATION = "areála#dilli" "bargo#dilli" "boazodoallo#dilli "dilli" doaibma#dilli" "duopmoválde#dilli" "eaiggát#dilli" "eallin##dilli" "eallin-#dilli" "eanadoallo#dilli" "eanaeaiggát#dilli" "fápmo#dilli" "gaska#dilli" "geavahan##dilli" "giella#dilli" "háldo#dilli" ""heahte#dilli"" "lonohallan#dilli" "miella#dilli" "monopol#dilli" "niestedábuhanheahte#dilli" "náittos##dilli" "oahpponeavvo#dilli" "riekte##dilli" "riido#dilli" "ruhta#dilli" "seaktedábuhan#dilli" "sierra#dilli" "skuvla#dilli" "valljivuohta#dilli" "vuoigatvuođa##dilli" "šaddadan#dilli" ;
    # Likely to be locative in most contexts


SET ACTOR-ROLE = HUMAN | ORGANIZATION | POLITICAL-PLACE ; # Intiution: Restricting possible actor subjects.



LIST EVENT =  "avádat#čoahkkin" "álbmot#čoahkkin" "ámtadigge#čoahkkin" "bargiid#čoahkkin" "bajimus#čoahkkin" "bismma#čoahkkin" "buohkaid#čoahkkin" "boazodoallo#šiehtadallan" "budjeahtta#čoahkkin" "čakča#márkan" "čoahkkin" "dievas#čoahkkin" "digaštallan#čoahkkin" "digge#čoahkkin" "dutkan#čoahkkin" "erenoamáš#čoahkkin" "fáddá#čoahkkin" "festivála" "fitnodat#čoahkkin" "gieldda#válga" "gitta#čoahkkin" "gozihan#čoahkkin" "gulaskuddan#čoahkkin" "guosse#čoahkkin" "gulaskuddan#čoahkkin" "jahke#čoahkkin" "joavko#čoahkkin" "joavko-#čoahkkin" "juristta#čoahkkin" "kursa" "loahppa#čoahkkin" "luodda#čoahkkin" "lávdegodde#čoahkkin" "mearridan#proseassa" "miellahtto#čoahkkin" "ministtar#čoahkkin" "miellačájehan#čoahkkin" "njunus#čoahkkin" "nominašuvdna#čoahkkin" "nášunal#čoahkkin" "ođđaorganiseren#čoahkkin" "oktavuohta#čoahkkin" "oktasaš#čoahkkin" "ovttasbargo#čoahkkin" "olgoriikaminister#čoahkkin" "olles#čoahkkin" "proseassa" "ráđđádallan" "ráđđe#čoahkkin" "ráđđehus#čoahkkin" "ráhkkanan#čoahkkin" "ráhkkananstivra#čoahkkin" "riekte#čoahkkin" "riikka#čoahkkin" "ságaškuššan#čoahkkin" "sáme#čoahkkin" "sámedigge#válga" "šiehtadallan" "siida#čoahkkin" "stivra#čoahkkin" "suokkardan#čoahkkin" "telefon#čoahkkin" "váldo#čoahkkin" "válga" "vuosttaldan#čoahkkin" "vuođđudan#čoahkkin" "váhnen#čoahkkin" "válga#čoahkkin" "vuosttaš#čájálmas" ; #XXX bad name, too general

LIST ILLNESS = "albmasuoigi" "allergiija" "anemiija" "astan#dávda" "árbe#dávda" "báhti" "bátne#várka" "bátne#vearka" "bárku" "bárko#dávda" "bárku#dávda" "bearri" "boazo#dávda" "boahkko#dávda" "boahkku" "boras#dávda" "buohcu" "buohcuvuohta" "buollán" "buozalmas" "čoavje#bávččas" "čoavje#bonjahat" "čotta#dávda" "dávda" "dohppehallan#dávda" "eksema" "gárren#ávnnas" "gárrenávnnas#váttisvuohta" ""gárren#dilli" "geahppes#dávda" "geahppes#vihki" "golgo#dávda" "golgo#vihki" "influensa" "juhkan#vuohta" "leasmi" "logahas#dávda" "luhča#dávda" "njoammu#dávda" "nuorvu" "nuoskkes#dávda" "oaivves#vuohta" "ráŋka" "rohttu#dávda "ruodnu" "skihpa" "časkásan#váddu" "searga#dávda" "sis#váddu" "soga#dávda" "soga#váddu" "suohtta" "varra#sonahat" "váddu" "vátne#dávda" "vihki" "vuolši" ;




# Sets denoting relations

#LIST OASSI = "oassi" "váldo#oassi"; 
	#Might be useful for partitive constructions.	
	
LIST HEADOFPARTS =  "nisson#oassi" "oassi" "váldo#oassi" "bealli" "guhtemuš" "goappašat" "ollu" "ollut" "proseanta" "%" ;
    # This may be a better partitive set.	



# Miscellaneous sets
# ------------------

LIST Clt = Qst Foc ; 

LIST Px = PxSg1 PxSg2 PxSg3 PxDu1 PxDu2 PxDu3 PxPl1 PxPl2 PxPl3 ;
LIST PX1 = PxSg1 PxDu1 PxPl1 ;
LIST PX2 = PxSg2 PxDu2 PxPl2 ;
LIST PX3 = PxSg3 PxDu3 PxPl3 ;

SET PXNOTSG1 = Px - PxSg1 ; SET PXNOTSG2 = Px - PxSg2 ; SET PXNOTSG3 = Px - PxSg3 ;
SET PXNOTDU1 = Px - PxDu1 ; SET PXNOTDU2 = Px - PxDu2 ; SET PXNOTDU3 = Px - PxDu3 ;
SET PXNOTPL1 = Px - PxPl1 ; SET PXNOTPL2 = Px - PxPl2 ; SET PXNOTPL3 = Px - PxPl3 ;



#SET PPOS = Po | Pr ;

LIST POST-A = "rájes" ;   
LIST POST-B = "rádjái" ;

#LIST WACKERNAGEL = "go" "ba" "bat" "be" "bahan" "son" ;

LIST GASKAL = "gaskal" "gaskkal" "gaskii" "gaskka" "gaskkas" ; 
 # p-positions that like coordination
 
LIST TIME-PREP = "badjel" "čađa" "gaskkal" "guovddáš" "maŋŋel" "maŋŋil" "miehtá" "ovdal" ; 

SET NUMERALS = Num - OKTA ;
# this set does not contain ordinals, I am not sure if that is necessary.

SET NOT-NUMERALS = WORD - Num ;

LIST MANGA = "máŋga" "galle" ; # Not referred to by any rule.

SET CARDINALS = Num - Ord - MANGA ;

SET NOT-CC = WORD - CC ;

SET NOT-PCLE = WORD - Pcle ;

LIST COMPAR = "dávjjit" "eanet" "earalágan" "eará" "earret" "seammás" 
		"seammalágan" "seammalágán" "seammaláhkai"; 
 # These combine with "go" Pcle, but COMPAR stands for 'comparison' rather than
 # 'comparative'.

LIST CONTRA = "muhto" ; # In lean muitalan, muhto dál muitalan.

LIST PROSEANTA = "proseanta" "%" ;

SET REALCLB = CLB - COMMA ;

SET NOTCOMMA = WORD - COMMA ;


# Border sets and their complements
# ---------------------------------

SET CP = (Pron Interr) | (Pron Rel) | MO ;

SET NP-BORDER = BOS | EOS | REALCLB | VFIN | Inf | (Actio Ess) | ConNeg | VGen | 
	Sup | PPRON-NOT-GEN | Recipr | Po | Pr | Pcle | ("jed") | Interj | CS | CP ; #XXX Why not Adv = old v.
#	Sup | PPRON-NOT-GEN | Recipr | Po | Pr | Pcle | ("jed") | Interj | CS | CP | Adv ; # We try Adv
# We have examples where Adv should not be part of NP-BORDER, such as:
## Gávcci 12 áirasiin  leat leamaš nissonolbmot

 # CLB removed from set in order to allow for NP-internal commas.
 # Refl is removed because of "sin iežaset vieruid" etc.



# Syntactic sets
# --------------


LIST ALLSYNTAG = (@+FAUXV) (@+FMAINV) (@-FAUXV) (@-FMAINV) (@-FSUBJ) (@ADV-A) (@ADV-ADV) (@ADVL) (@AN>) (@ActioN>) (@APP) (@CC) (@CMPND) (@CS) (@DN>) (@GA>) (@GN>) (@GP<) (@GP>) (@HNOUN) (@INTERJ) (@NPron<) (@NQ<)  (@NUM-PRON) (@OBJ) (@OPRED) (@PCLE)  (@CS-COMPL) (@PROP>) (@PrcN>) (@PronN<) (@PronN>)  (@QN<) (@QN>) (@SPRED) (@NumQ<) (@SUBJ) (@SUBJ-QH) (@TITLE) (@VOC) (@X); 

SET NON-APP = ALLSYNTAG - (@APP);

# SET ASSIGNEDSYN = ALLSYNTAG - @X ;
# funzt nicht.





# ============= #
# ============= #
# Mapping rules #
# ============= #
# ============= #
    MAPPINGS    #
# ============= #

# Lexically determined mappings
# -----------------------------

MAP (@ADV-ADV) TARGET ("seamma") IF (1 ("ollu"));
MAP (@ADV-ADV) TARGET ("seammá") IF (1 ("ollu"));
#MAP (@ADVL @PronN>) TARGET ("ollu") IF (-1 ("seamma") OR ("seammá"));
MAP (@ADVL) TARGET ("ollu") IF (-1 ("seamma") OR ("seammá"))(0 Adv);
MAP (@PronN>) TARGET ("ollu") IF (-1 ("seamma") OR ("seammá"))(0 Pron);
    ## Soabadanráđis galget leat golbma miellahttu ja seamma ollu várrelahttut.

# Mappings of minor categories
# ----------------------------

MAP (@CC) TARGET CC ;
	## Na muhto don it leat dasa nama čállán!

MAP (@CS) TARGET CS ;
	## Leago guhkes áigi dassá go Máreha oidnet?

MAP (@INTERJ) TARGET Interj ;
	## Maid, iigo leat boahtán?

MAP (@INTERJ) TARGET ("surgat") IF (-1 ("vuoi") OR ("hei"));
	## Vuoi surgat dan gándda!

MAP (@INTERJ) TARGET ("beargalat") IF (*-1 Nom BARRIER CS OR CP LINK NOT 0 (Dem Nom));
	## Gosa don leat beargalat mu reaga dahkan?

MAP (@PCLE) TARGET Pcle ;
	## Amma mii eat leat máksán?
	
MAP (@NNum<) TARGET ABBR IF (-1 Num)(0 INITIAL LINK NOT 1 NP-MEMBER);
    ## 98 B.


# Determiner mappings
# -------------------

MAP (@DN>) TARGET (Dem Sg Nom) IF (*1C (N Sg Nom) BARRIER NP-BORDER);
	## Váidalusas galgá leat namahuvvon dat dilálašvuohta.

MAP (@DN>) TARGET (Dem Nom) IF (*1 Num OR Pron OR A OR N BARRIER NP-BORDER LINK 0 Nom)
	(NOT 1 Interr OR COMMA OR Dem);
	## Jogo gikse dat guolit?

MAP (@DN>) TARGET (Dem Acc) IF (*1 Num OR Pron OR A BARRIER NP-BORDER OR (N Acc) 
	LINK 0 Acc)(NOT *1 CC BARRIER WORD);
	## Mun lean oastán ja klipsen dáid njiŋŋelasaid.

MAP (@DN>) TARGET (Dem Acc) IF (*1 (N Acc) BARRIER Ill OR Com OR Loc OR Ess OR VFIN);
	## Naba daid stuora dorskiid, gosa daid bidjet?

MAP (@DN>) TARGET (Dem Gen) IF (*1 Num OR Pron OR A OR N BARRIER NP-BORDER 
	LINK 0 Gen OR ADVLCASE)(NOT 1 Po);
	## Mun bessen dan bargui.

MAP (@DN>) TARGET (Dem Attr) IF (*1 N BARRIER NP-BORDER);
	## Makkár vaikkuhusat leat jus veahkki ii addojuvvo?

MAP (@DN>) TARGET (Dem Sg Com) IF (*1 (N Sg Com) BARRIER NP-BORDER);
	## Dat ásahuvvojedje aiddo dainna ulbmiliin, ahte...

MAP (@DN>) TARGET (Dem Pl Ill) IF (*1 (N Pl Ill) BARRIER NP-BORDER);
	## De lassánii maid sisafárren dáidda guovlluide.

MAP (@DN>) TARGET (Dem Pl Loc) IF (*1 (N Pl Loc) BARRIER NP-BORDER);
	## ...ahte eamiálbmotriekti sáhttá leat okta dain speadjaliin,...

MAP (@DN>) TARGET (Dem Pl Com) IF (*1 (N Pl Com) BARRIER NP-BORDER);
	## ...de offiseara bijai muhtun ustibiid Jesusa lusa dáiguin sániiguin...

MAP (@DN>) TARGET (Dem Pl Com) IF (*1 (N Pl Com) BARRIER NOT-AA);
	## Ain ferte joatkit daiguin sierra doaibmabijuiguin.


# Attribute mappings
# ------------------

MAP (@ADV-A) TARGET ("beare") OR ("eanet" Adv) OR ("erenoamáš") OR ("earenoamáš") OR 
	("hirbmat") OR ("hui") OR ("menddo") OR ("mii" Sg Gen) OR ("nu") OR ("oba") 
	OR ("oppa" Adv) OR ("veaháš" Adv) IF (1 A)
	(NOT 0 ("eanet") LINK 1 ("uhcit") OR ("unnit"));
	## Soai leaba goappašagat nu jallat.
	## In dieđe man buorit dat leat.

MAP (@ADV-A) TARGET ("mihá") OR ("ollu" Adv) IF (1 Comp OR COMPAR);
	## Dakkár almmuhemiid leat mihá divraseabbo ráhkadit.

MAP (@ADV-A) TARGET ("buot") IF (1 Superl);
	## Guhtemuš báhkkon lea buot mávssolaččamus?

MAP (@ADV-A) TARGET ("mii" Sg Gen) IF (1 (Pron Pers Nom))
	(*1 (A Sg Nom) BARRIER V-NOT-AUX);
	## Leatgo don fuomášan dan máilmmi, man dat lea váralaš?

MAP (@ADV-ADV) TARGET ("eanet" Adv) OR ("erenoamáš") OR ("earenoamáš") OR 
	("hirbmat") OR ("hui") OR ("menddo") OR ("mii" Sg Gen) OR ("nu") OR ("oba") 
	OR ("oppa") IF (1 Adv)(NOT 1 ("fal") OR ("maiddái") OR ("mo"))
	(NOT 0 ("mii" Sg Gen) LINK 1 Po)(NOT 0 ("eanet") LINK 1 ("uhcit") OR ("unnit"));
	## Man dávjá mun livččen dáhtton čohkket du mánáid.

MAP (@ADV-ADV) TARGET Adv IF (0 Indef)(1 VGen)(NOT 1 Sg3);
	## Dát ađa lea uhccán vuošša.

MAP (@ADV-ADV) TARGET Date IF (-1 BEAIVI);

MAP (@ADV-ADV) TARGET ("mearri" Gen) IF (-1 (Pron Dem Sg Gen))(1 Adv);
	## Lihkkus fuomášuvvui dát dan meare árrat ja ahte dálkkoduvvo árrat.
	# A controversial tag. Mearri is not an adverb, but functions as one in this case.
	# On the other hand, should "mearri" in front of an adjective get the tag @ADV-A? Right now it gets @GA>

MAP (@ADVL) TARGET ("ihkinassii") IF (-1 ("gosa"));
	## Mun áiigun čuovvut du gosa ihkinassii manažat.

MAP (@PronN>) TARGET ("juohke");
	## Galgá gávdnot divššohasbearráigeahčči juohke fylkkas. 

MAP (@PronN> @GP<) TARGET (Pron Attr) ;
	## Leago guhkki eará dáluide?
	## Doppe leaba earet eará Sondre Lerche ja Madrugada.
	# It has to be checked if the @GP< needs to be removed in some cases.
	
MAP (@PronN<) TARGET ("eatnat") IF (-1 ("giittos"));
	## Giittos eatnat luoikamis.

MAP (@PronN<) TARGET (Indef Pl Nom) IF (*-1 PPRON-DU-PL OR (N Pl) LINK 0 Nom
	BARRIER CS)(NOT -1 CC OR CLB)(NOT 1 Rel);
	## Soai leaba goappašagat nu jallat.

MAP (@PronN<) TARGET (Refl Sg Nom) IF (*-1 MUN OR DON OR (Pron Sg3 Nom) OR 
	(Pron Sg Nom) OR (N Sg Nom) BARRIER CS OR CP OR REALCLB);
	## Dasgo mun lean ieš gohčohallama vuloš. 

MAP (@PronN>) TARGET (Refl Sg Nom) IF (*1 MUN OR DON OR (Pron Sg3 Nom) OR 
	(Pron Sg Nom) OR (N Sg Nom) BARRIER NOT-ADV-PCLE);
	## Ieš han don dadjet dan. 

MAP (@PronN<) TARGET (Refl Pl Nom) IF (*-1 PPRON-DU-PL OR (N Pl) BARRIER CS OR CP
	OR REALCLB LINK 0 Nom);
	## Mii han leat ieža gullan dan su iežas njálmmis.

MAP (@PronN>) TARGET (Refl Pl Nom) IF (*1 PPRON-DU-PL OR (N Pl) 
	BARRIER NOT-ADV-PCLE LINK 0 Nom)(NOT *1 VFIN OR Inf BARRIER NOT-ADV-PCLE);
	## ...muhto ieža dii ehpet guoskkat daid suorpmain ge.

MAP (@PronN>) TARGET Indef IF (*1 N BARRIER NPNH)(NOT 0 Nom LINK 1 Acc)
	(NOT 0 ADVLCASE)
	(NOT 1 (N Sg Gen) LINK NOT 0 Px LINK *1 N BARRIER NPNH OR CC)
    (NOT 1 (N Sg Gen PxSg2) LINK *-1 (V-SG2) LINK *1 N BARRIER NPNH OR CC)
    (NOT 1 VFIN OR Ess OR ("veara"));
    ## Son galgá beassat buktit oaivilis buot gažaldagaide.
    ## Eai leat šat nu ollu ohcamat priváhta ealáhusain.

ADD (@PronN>) TARGET Indef IF (1 ("eará") OR Dem)(NOT 0 Nom LINK 1 Acc)
	(NOT 0 ADVLCASE); 
    ## Ollu earáidege go ovttagielagiidda lea váttis gulahallat....
    ## ..muhto ii fal nu ollu earáin.
    ## Ruoŧas bođii loahpas vástadusa Sámi Oahpahusguovddážis, Johkamohkis, muhto ii fal nu ollu earáin.
    # The partitive construction would have been máŋggain earáin and ollu daid earáin
    
    
MAP (@ADVL) TARGET Indef IF (0 ("ollu"))(1 ("eará" Pl Loc) OR (Dem Pl Loc))(*2 V-SG3 BARRIER NOT-ADJ-PCLE)
    (NOT 0 Nom LINK 1 Acc)(NOT 0 ADVLCASE);
MAP (@ADVL) TARGET Indef IF (*-1 V-SG3 BARRIER VFIN OR CP OR CS)(0 ("ollu"))(1 ("eará" Pl Loc) OR (Dem Pl Loc))
    (NOT 0 Nom LINK 1 Acc)(NOT 0 ADVLCASE);
	## Ollu earáin lea hávski.
	#  for habeo-constructions where ollu modifies eará+Loc

MAP (@PronN>) TARGET Indef IF (NOT *-1 V-SG3 BARRIER VFIN OR CP OR CS)(0 ("ollu"))(1 ("eará" Pl Loc) OR (Dem Pl Loc))
    (NOT *2 V-SG3 BARRIER NOT-ADJ-PCLE)(NOT 0 Nom LINK 1 Acc)(NOT 0 ADVLCASE);
	## Ollu earáin leat hávski.
# Mange andre har det triveleg. Mange av dei andre er trivelege.
# Ollu earáin leat hávskit
# Mange andre kjøper mjølk. Mange av dei andre kjøper mjølk.
# Ollu earát ostet mielkki. Ollusat earáin ostet mielkki.

MAP (@PronN>) TARGET Indef IF (0 Nom)(*1 (N Nom) BARRIER NPNH)
	(NOT 0 Pl LINK 1 Inf);
	## Goabbáge báddi galgá leahkit 60 alen guhku.

MAP (@PronN>) TARGET Indef IF (1 A-CASE)(NOT 1 ("veara"));
	## Sáhttáge ollu buriid dadjat bohtosiid birra.

MAP (@PronN>) TARGET Indef IF (NOT 0 ("iešguhtet") OR ADVLCASE)(1 Num);
	## Buot oktanuppelohkái máhttájeaddji vulge Galileai.

MAP (@PronN>) TARGET ("buot") IF (1 Dem OR Indef);
	## Buot dáid mun attán dutnje.
	## Sii ledje eanet sivalaččat go buot earát Jerusalemis.

MAP (@PronN>) TARGET ("buot" Pron) IF (*1 N BARRIER NPNH);
	## Dát máksá ahte dat lea lávdan buot servodaga surggiide.

MAP (@PronN>) TARGET Indef IF (*1 N BARRIER NP-BORDER OR REALCLB)
	(NOT *-1 ("leat") BARRIER NOT-ADV-PCLE LINK *1 Indef LINK *1 PrfPrc
	BARRIER VERB)
	(NOT *-1 INF-VERB BARRIER NOT-ADV-PCLE LINK *1 Indef LINK *1 Inf
	BARRIER VERB)(NOT 0 ADVLCASE)(NOT 1 (Pers Gen) LINK *1 N BARRIER NOT-NP OR CLB 
	LINK NOT 0 Loc)(NOT 1 Adv LINK 1 N)
	(NOT 1 (N Sg Gen) LINK *1 N BARRIER NPNH)(NOT 1 VFIN OR Ess OR ("veara"));
	## Ollu dáin doaibmabijuin sáhttá álggahit dálá rámmaid siskkobealde.

MAP (@PronN>) TARGET ("mii" Interr Pl Nom) IF (*1C (N Pl Nom) BARRIER NP-BORDER);
MAP (@PronN>) TARGET ("mii" Rel    Pl Nom) IF (*1C (N Pl Nom) BARRIER NP-BORDER);
	## ...dan hárrái, mat riektegáldut leat, mo daid berre dulkot, ja ...

MAP (@PronN>) TARGET Interr IF (NOT 0 ("gii"))(1 N)
	(NOT 0 ("mii") LINK NOT 1 (Pl Gen))(NOT 0 (Sg Nom) LINK 1 V-SG3)
	(NOT 1 Gen LINK *1 N BARRIER NPNH);
MAP (@PronN>) TARGET Rel    IF (NOT 0 ("gii"))(1 N)
	(NOT 0 ("mii") LINK NOT 1 (Pl Gen))(NOT 0 (Sg Nom) LINK 1 V-SG3)
	(NOT 1 Gen LINK *1 N BARRIER NPNH);
	## Guđe sihkarvuođanjuolggadusat ledje biddjon...

MAP (@PronN>) TARGET Interr IF (0 ("guhtemuš"))(*1 (Pers Loc) BARRIER NOT-PCLE);
MAP (@PronN>) TARGET Rel    IF (0 ("guhtemuš"))(*1 (Pers Loc) BARRIER NOT-PCLE);
	## Guhtemuš ba dis sáhttá...

MAP (@PronN>) TARGET ("buot") IF (*1 (N Pl Nom) OR (N Pl Acc) BARRIER NP-BORDER);
	## Ámtamánnii gulai "bearráigeahččat buot min gullevaš eatnamiid, gittiid 
	## ja opmodagaid".

MAP (@AN>) TARGET (A Attr) ;
	## In fuobmán dadjat ahte Márehis lea ođđa irgi.
	## Dalle gusto vuosttas, nubbi ja goalmmát teakstaoassi seammaláhkái.

# buorre
MAP (@AN>) TARGET Nom IF (0 ("buorre"))(*1 (N Nom) BARRIER NPNH);
	## Leatgo buorit guolit?

MAP (@AN>) TARGET Acc IF (0 ("buorre"))(*1 (N Acc) BARRIER NPNH);
	## Doaivvutgo oažžut buriid guliid?

MAP (@AN>) TARGET (Sg Gen) IF (0 ("buorre"))
	(*1 (N Sg Gen) OR (N Sg Ill) OR (N Sg Loc) BARRIER NPNH);
	## Dearvvašvuođabargiidlága § 40 vuosttas lađđasa mielde galgá 
	## journála čállojuvvot buori fidnodábi mielde.

MAP (@AN>) TARGET (Pl Gen) IF (0 ("buorre"))
	(*1 (N Pl Gen) OR (N Pl Ill) OR (N Pl Com) BARRIER NPNH);
	## Giittán maiddái universitehtalektora Fredrik Harhoff 
	## buriid ságastallamiid ovddas.

MAP (@AN>) TARGET (Sg Com) IF (0 ("buorre"))(*1 (N Sg Com) BARRIER NPNH);
	## Jos mii addet buriin dáhtuin...

MAP (@AN>) TARGET (Pl Ill) IF (0 ("buorre"))(*1 (N Pl Ill) BARRIER NPNH);
	## Son lea sivdnidan min Kristus Jesusis buriide daguide.

MAP (@AN>) TARGET (Pl Loc) IF (0 ("buorre"))(*1 (N Pl Loc) BARRIER NPNH);
	## Son ferte leat dovddus buriin daguin. 

MAP (@AN>) TARGET (Pl Com) IF (0 ("buorre"))(*1 (N Pl Com) BARRIER NPNH);
	##

MAP (@AN>) TARGET Ess IF (0 ("buorre"))(1 (N Ess));
	## Maŋemus son šattai buorrin oahpaheaddjin.


# Prenominal participles

MAP (@PrcN>) TARGET PrfPrc IF (-1 Dem OR Num OR Attr OR Indef)(NOT -1 Pers)
	(*1 N BARRIER NOT-A);
	## ..maid almmolaš dohkkehuvvon dearvvašvuođabargit addet,..
	## Galgá leat okta čohkkejuvvon divššohasjournála. 

MAP (@PrcN>) TARGET PrfPrc IF (*-1 PrfPrc BARRIER NPNHA LINK 0 V-NOT-AUX 
	LINK NOT 0 Actio)(*1C N BARRIER NOT-ADJ);
	## Ii leat boahtán mannan váhkus.

MAP (@PrcN>) TARGET PrfPrc IF (NOT *-1 ("leat") BARRIER PrfPrc OR (Actio Ess))
	(NOT *-1 PrfPrc LINK 0 (Actio Nom) LINK NOT *-1 Nom BARRIER CS LINK 
	NOT 0 ("leat"))(0 Pass)(*1C N BARRIER NPNH); 
	## ...dan mii lei dáhpáhuvvan bosttahuvvon olbmuide.

MAP (@PrcN>) TARGET PrfPrc (*-1 CLB OR go BARRIER NOT-PRE-A-N)(1 TIME);
	## Dalle šattašii liikka buorre dálki go mannan sotnabeaivve.

MAP (@PrcN>) TARGET PrfPrc IF (*-1 BOS OR REALCLB BARRIER NOT-PRE-A-N)(NOT 0 ("leat")) 
	(*1 N BARRIER NPNH LINK NOT 0 TIME LINK NOT 1 TIME);
	## Ovdamearkan oaivvilduvvon fálaldahkii lea sámi kulturdieđu lohkanbaji.
	
MAP (@PrcN>) TARGET PrfPrc IF (*-2 BOS OR REALCLB)(-1 Com OR Loc)(NOT 0 ("leat"))
	(*1 N BARRIER NPNH LINK NOT 0 TIME LINK NOT 1 TIME);
    ## Sieiddi bálvalemiin sturron eallu gohčoduvvui bálvvoseallun, mi sáhtii jávkat, go oamasteaddji jámii dahje go sieidi ii bálvaluvvon.

MAP (@PrcN>) TARGET PrfPrc IF (*-1 Po BARRIER NOT-NP LINK *-1 BOS OR REALCLB 
	BARRIER NOT-NP)(NOT 0 ("leat"))(*1 N BARRIER NPNH LINK NOT 0 TIME LINK
	NOT 1 TIME);
	## Sámiid várás oaivvilduvvon doaimmaid hárrái galggašii...
 # For those cases where the Po is homonymous with verb, and therefore a barrier
 # in the preceding rule.

MAP (@PrcN>) TARGET PrfPrc IF (*-1 Pr BARRIER NOT-NP LINK -1 NP-BORDER LINK 
	NOT 0 ("leat"))(*1 (N Gen) BARRIER NOT-NP);
	## 1978 láhka balansere gaskal sámi vuoigatvuođaid boarásmuvvan oainnu,...

#MAP (@PrcN>) TARGET PrfPrc IF (*-1 V-NOT-AUX BARRIER CS OR CP OR AUX 
#	LINK NOT 0 A OR N)(0 Pass)(1 N)(NOT 1 TIME);
	## Vuoigatvuohta dearvvašvuođaveahkkái gusto riikka almmolaš ruhtaduvvon 
	## spesialistadearvvašvuođabálvalussii.
	
MAP (@PrcN>) TARGET PrfPrc IF (*-1 V-NOT-AUX LINK NOT 0 PrfPrc BARRIER CS OR CP OR AUX 
	LINK NOT 0 A OR N)(0 Pass)(1 N)(NOT 1 TIME);
	
MAP (@PrcN>) TARGET PrfPrc IF (*-1 CS OR CP BARRIER NPNHA LINK NOT 0 A OR N)(0 Pass)(*1 N LINK NOT 0 TIME BARRIER NPNHA);
    ## Ráđđehus háliida lágidit dearvvašvuođa- ja sosiálabálvalusaid oktagaslaš dárbbuid mielde , earret eará pasientavuoigatvuođaid lágain ja dearvvašvuođabargiid lágain.

MAP (@PrcN>) TARGET PrfPrc IF (-1 Adv LINK *-1 COPULAS BARRIER NOT-ADV-PCLE LINK 
	*-1 Nom BARRIER V-NOT-AUX OR CC OR CLB OR CS OR CP)
	(*1 Nom BARRIER NPNHA LINK 1 NP-BORDER);
	## Ruonáeana lea čielgasit ráddjejuvvon geográfalaš guovlu.

MAP (@PrcN>) TARGET PrfPrc IF (NOT *-1 Neg BARRIER VERB)(NOT 0 AUX)(1C (N Nom));
	## Son lea oahppan olmmoš.

MAP (@PrcN>) TARGET PrfPrc IF (0 ("mannat"))(1 TIME);
	## Ii leat boahtán mannan váhkus.

MAP (@PrcN>) TARGET PrfPrc IF (-1 ("nu"))(0 ("gohčodit" Pass));
	## Dáin áššiin sáhttet Sámedikkit várra nu gohčoduvvon friija 
	## árvvoštallama mielde mearridit.

MAP (@PrcN>) TARGET PrfPrc IF (*-1 CC BARRIER NPNH LINK -1 Acc LINK NOT 0 PrfPrc)
	(1 (N Acc));
	## Divššohasas, gii dárbbaša guhkit áiggi ja oktiiheivehuvvon dearvvašvuođa-
	## bálvalusaid,...

MAP (@PrcN>) TARGET PrsPrc IF (*1 N BARRIER NOT-ADJ);
	## Boađe boahtti vahku.

MAP (@PrcN>) TARGET PrsPrc IF (1 CC)(2 Attr)(3 N);
	## Guoski dahje lagas eatnamat.

MAP (@ActioN>) TARGET (Actio Nom) IF (-1 Gen)(1 N)(NOT 1 PrfPrc);
	## Sii orro sin iežaset dahkan darfegođiid siste.
	## Sámegiela geavaheapmi journálain lea lassánan.
	## Bálvalusaid ovdánahttin riikkarájáid rastá.

 # Since we cannot at present distinguish between long and short Actios,
 # this rule hits not only the short Actio in the first example, which is 
 # correct, but also the long Actio in the second example, which is wrong.

 # Problem: At present (051106), the rule hits neither, which also makes it hard
 # to document the tag. It hits the third sentence, though, together with @GN>.

ADD (@TITLE) TARGET TITLE IF (1 Prop LINK 0 Fem OR Mal OR Sur);
ADD (@TITLE) TARGET PROFESSION IF (1 Prop LINK 0 Fem OR Mal OR Sur);
ADD (@TITLE) TARGET OFFICE IF (1 Prop LINK 0 Fem OR Mal OR Sur);
	## Stuorra giitu maiddái professor Birger Stuevold Lassenii.
 # ADD so that argument mappings can apply later.

#MAP (@QN<) TARGET MEASURE IF (-1 Num)(0 Gen);
    ## 5 kr.

MAP (@QN<) TARGET MEASURE IF (-1 Num)(1 ALU OR A);
	## Min viessu lea vihtta mehtara alu.
	## Sus lea njellje mehtara guhkkosaš biila.

MAP (@QN<) TARGET MEASURE IF (-1 Num)(1 Adv OR Pr);
	## Ádjá viessu lea guokte miilla bajábeal márkana.

MAP (@QN<) TARGET TIME IF (-1 Num)(1 ("boaris") OR ("nuorra"));
	## Mánná lea golbma jagi boaris.

MAP (@QN<) TARGET TIME IF (-1 Num)(1 ("maŋŋil"));
	## Dat dáhpáhuvai golbma jagi maŋŋil.

MAP (@QN<) TARGET N IF (-1 Num)(1 ("velggolaš"));
	## Ánde lea minnje vihttalot ruvnno velggolaš.

MAP (@QN<) TARGET (N Sg Acc) IF (-1 (Num Acc))(0 MEASURE)(1 (N Acc));
	##


# Genitive mappings
# -----------------

MAP (@GN>) TARGET (Pron Sg1 Gen) IF (1 (Refl PxSg1));
	## Orru lossat geardduhit mu iežan ávžžuhusa.

MAP (@GN>) TARGET (Pron Sg2 Gen) IF (1 (Refl PxSg2));
	## Diet lea du iežat ášši.

MAP (@GN>) TARGET (Pron Sg3 Gen) IF (1 (Refl PxSg3));
	## Mii han leat gullan dan su iežas njálmmis. 

MAP (@GN>) TARGET (N Sg Gen) IF (1 (Refl PxSg3));
	## Dát lea Hearrá iežas dahku.

MAP (@GN>) TARGET (Pron Du1 Gen) IF (1 (Refl PxDu1));
	##

MAP (@GN>) TARGET (Pron Du2 Gen) IF (1 (Refl PxDu2));
	##

MAP (@GN>) TARGET (Pron Du3 Gen) IF (1 (Refl PxDu3));
	##

MAP (@GN>) TARGET (Pron Pl1 Gen) IF (1 (Refl PxPl1));
	## Min iežamet posišuvdna lea nu ahte...

MAP (@GN>) TARGET (Pron Pl2 Gen) IF (1 (Refl PxPl2));
	## Bestojupmi ii leat din iežadet dahku.

MAP (@GN>) TARGET (Pron Pl3 Gen) IF (1 (Refl PxPl3));
	## Sii orro sin iežaset dahkan darfegođiid siste.
 
MAP (@GP>) TARGET Gen IF (NOT 0 Num LINK 1 Ill)
	(NOT 0 (V Actio) LINK 1 ("dihte") OR ("dihtii"))(1 Po);
	## Na mun gal hupmen dan birra.

MAP (@GP>) TARGET Gen IF (NOT 0 Cmpnd)(1 CC)(*2 Gen BARRIER NPNHA LINK 1 Po)
	(NOT 0 (V Actio) LINK 1 CC LINK 1 (V Actio) LINK 1 ("dihte") OR ("dihtii"));
	## Divššohas galgá maiddái oažžut dieđuid vejolaš riskkaid ja 
	## lassiváikkuhusaid birra.
	
MAP (@GP>) TARGET Gen IF (NOT 0 Cmpnd)(1 COMMA)
    (*2 Gen BARRIER NPNHA LINK 1 CC LINK *1 Gen BARRIER NPNHA LINK 1 Po)
	(NOT 0 (V Actio) LINK 1 CC LINK 1 (V Actio) LINK 1 ("dihte") OR ("dihtii"));
	## Dat lohká stuorradiggedieđáhusas Sámedikki birra ja stuorradiggedieđáhusain ođđa 
	## mielladearvvašvuođafuolahusa, ođđa fástadoavtterortnega ja ođđa veajuiduhttima birra.

MAP (@GP>) TARGET Gen IF (1 (Neg Foc))(2 Gen)(3 Po);
	## Ii leat váldojuvvon ovdan Ruoŧa iige Suoma bealde.

MAP (@GP>) TARGET Gen IF (0 TIME OR AIGODAT)(1 Num)(2 Po);
	## Finnmárkku fylkkagielda lea jagi 1988 rájes ovddidan doaimmaid... 

MAP (@GP<) TARGET Gen IF (*-1 Pr BARRIER NOT-NP OR CLB)(NOT 1 (N Gen))
	(NOT 0 TIME LINK *-1 Po BARRIER NP-BORDER LINK -1 Gen)
	#(NOT 0 Num LINK *1 NOT-CURRENCY-GEN BARRIER NP-BORDER)
	(NOT -1 Gen OR CLB LINK 2 Po)
	(NOT 0 CURRENCY LINK 0 Gen LINK -1 Num);
    ## Lága mearrádusat galget váikkuhit luohttevašvuođa gaskal 
	## divššohasa ja dearvvašvuođabálvalusa.
	#(NOT 0 Num LINK *1 NOT-CURRENCY-GEN OR BARRIER NP-BORDER)

MAP (@GP<) TARGET Gen IF (*-1 CC BARRIER CS LINK *-1 GASKAL BARRIER CS)
	(1 NP-BORDER);
	## Dábálaččat earuha váldolaččat gaskal dábálaš (totala) geavahan-
	## vuoigatvuođaid ja sierranas (partiála) geavahanvuoigatvuođaid.

MAP (@GP<) TARGET (Pl Gen) IF (*-1 GASKAL BARRIER NOT-NP LINK NOT -1 (Pl Gen))
	(1 NP-BORDER);
	## Finnmárkkus ii goassige leat leamaš ságastallan gaskal muhtun muddui 
	## seammadássásaš oasálaččaid.
 
MAP (@NQ<) TARGET Gen IF (*-1 NUMERALS BARRIER NP-BORDER OR N OR REALCLB OR PUNCT
	LINK 0 Nom LINK NOT 0 Ord)(NOT 0 Num)(NOT 1 A OR N)(NOT -1 COMMA);
	## Mun máksen 100 ruvnno duvle.
#MAP (@NQ< @GN>) TARGET Gen IF (*-1 NUMERALS BARRIER N OR REALCLB OR PUNCT
#	LINK 0 Nom LINK NOT 0 Ord)(NOT 0 Num)(NOT -1 Ord LINK -1 Num);
		
MAP (@NQ< @GN>) TARGET Gen IF (*-1 NUMERALS BARRIER N OR REALCLB OR PUNCT
	LINK 0 Nom LINK NOT 0 Ord)(NOT 0 Num);
	## Skuvlajagis 2001/2002 leat oktiibuot 469 oahppi suohkana vuođđoskuvllain.

MAP (@NQ<) TARGET (N Gen) IF (-1 NUMERALS LINK 0 Nom LINK NOT 0 Ord 
	LINK *-1 go BARRIER NOT-ADV-PCLE LINK -1 V-TRANS-ACT)
	(1 Acc LINK 1 CC OR CLB);
	##  ...addet go máŋga dearvvašvuođabargiidjoavkku veahki ja fállojuvvo go...

MAP (@NQ<) TARGET Gen IF (-1 CC LINK -1 (N Gen) LINK *-1 NUMERALS BARRIER NP-BORDER 
	OR N OR REALCLB OR PUNCT LINK 0 Nom LINK NOT 0 Ord);
	## Mus leat čieža vielja ja oappá.

MAP (@NQ<) TARGET (N Gen) IF (*-1 NUMERALS BARRIER NOT-ADJ LINK 0 Nom)(1 Prop);
	## Joavkkus leat vihtta oahppi Kárášjogas.

MAP (@GN>) TARGET Gen IF (0 MANNU)(1 Ord);
	## ...ođđajagemánu 1. b. 2002.

MAP (@GN>) TARGET Gen IF (0 TIME OR AIGODAT OR ROUTE)(NOT 1 CC)(NOT 0 VAHKKU LINK 1 CARDINALS)
	(*1 N BARRIER NP-BORDER OR REALCLB OR Po OR Pr);
	## Min áiggi boaresolbmot muitaladdet ahte...
	
MAP (@ADVL @GN>) TARGET Gen IF (-2 Gen LINK 0 MANNU)(-1 Ord)(0 ("b") OR ("beaivi"));
    ## Geassemánu 8.b. 2001 láhkaásahus nr 676 oktagaslaš plánaid birra dearvvašvuođalágaid vuođul lea addojuvvon láhkamearrádusaid láhkavuođuin.
    
#MAP (@ADVL @GN>) TARGET Gen IF (NOT -1 Num)(0 MANNU)(1 CARDINALS);
    ## Guovdageaidnu / Kautokeino, skábmamánnu 2002.

MAP (@ADVL) TARGET Gen IF (0 TIME OR ROUTE);
	## Maid Biret-Elle áigu dán dálvve dahkat?
	## ...ja geasset vel moatte háve dađi miel go dárbu lea.

MAP (@-FSUBJ) TARGET Gen IF (1 (Actio Gen))(NOT 1 N)(2 N) ;
	## Gean ráhkadan fanas dat lea?

MAP (@-FSUBJ) TARGET Gen IF (1 Ger);
	## Máhte lihkai Máreha boađedettiin.

MAP (@-FSUBJ) TARGET Gen IF (1 VAbess);
	## Diet dáhpáhuvai mu dieđikeahttá.

MAP (@-FSUBJ) TARGET Gen IF (*-1 ("leat") BARRIER NPNHA)(1 (V Actio Loc));
	## Dat ii leat geafes olbmo nagodeames. 

MAP (@GA>) TARGET Gen IF (*-1 COPULAS BARRIER VERB OR CS OR CP)(1 (A Comp))
	(NOT 1 N);
	## Máret lea vieljaidis boarráset.

MAP (@GA>) TARGET Gen IF (1 (A Comp))(*2 COPULAS BARRIER VERB OR CS OR CC OR CP 
	LINK NOT *1 V-NOT-AUX BARRIER CS);
	## Duon guovtti girjji buoret dat gal lea.

MAP (@GA>) TARGET Gen IF (1 (A Comp))(NOT 1 VERB OR Adv)(NOT *2 N BARRIER NP-BORDER);
	## Mun jáhkán du iežan falibun.

MAP (@GA>) TARGET Gen IF (0 MEASURE)(1 A);
	## Suorpmas lea guokte millimehtara govdat.

MAP (@GA>) TARGET ("mearri" Gen) IF (-1 (Pron Dem Sg Gen))(1 A);
	## Dan meare stuora viessu.
	## Suolu lea dan meare unni ahte guođohit ii sáhte dárbbašan.

MAP (@GA>) TARGET Gen IF (1 ("veara"));
	## Dasgo bargi lea bálkkás veara.

MAP (@GA>) TARGET Gen IF (1 CC)(2 Gen)(3 ("veara"));
	## ...buot dan mii leat ráhkistan ja gudnejahttin veara.

MAP (@GN>) TARGET Gen IF
    (NOT -1 NUMERALS LINK 0 Nom)
	(NOT 0C ALU OR Cmpnd OR Num)(*1 N OR Actio LINK NOT 0 (Actio Ess) BARRIER NOT-NP OR REALCLB)
	(NOT 1 PUNCT LINK 1 CC)(NOT 1 Inf)
	(NOT 1 CC LINK *1 Gen BARRIER NOT-NP LINK 1 Po)
	(NOT 1 COMMA LINK 1 Gen LINK 1 CC LINK 1 Gen LINK 1 Po)
	(NOT 0 Actio LINK 1 ("dihte") OR ("dihtii"));
	### Leatgo oaidnán Máreha misiid?
	## Sámediggái juolluduvvo ovddasvástádus dearvvašvuođa- ja sosiálasuorggi almmolaš sámi diehtobálvalanguovddáža jođiheames.

MAP (@GN>) TARGET Gen IF
    (NOT -1 NUMERALS LINK 0 Nom) 
	(NOT 0 ALU OR Cmpnd OR Num)
	(1 COMMA)(2 Rel LINK *1 COMMA LINK *1 N BARRIER NOT-NP OR REALCLB)
	(NOT 1 PUNCT LINK 1 CC)
	(NOT 1 CC LINK *1 Gen BARRIER NOT-NP LINK 1 Po)
	(NOT 1 COMMA LINK 1 Gen LINK 1 CC LINK 1 Gen LINK 1 Po)
	(NOT 0 Actio LINK 1 ("dihte") OR ("dihtii"));
    ## Buoremus sámepolitihkka ožžojuvvo áigálii sámi veahkadaga, man ovddasta Sámediggi, ja ráđđehusa gaskaneas ipmárdusain.
	
MAP (@GN>) TARGET Gen IF (-2 ("s") OR ("siidu") OR ("paragráfa") OR 
    ("kapihttal") OR ("kapihtal") OR HEADOFPARTS OR ("jahki") OR GEASSET LINK 1 
    CARDINALS)(*1 N BARRIER NOT-A);
    ## 8 ođđa bargosaji ásahuvvojedje jagis 2001 ealáhusfoandda ruhtadoarjagiin.

MAP (@GN>) TARGET Gen IF (NOT -1 NUMERALS LINK 0 Num)
	(NOT 0 ALU OR Num OR Cmpnd)(*1 N BARRIER NOT-AA OR CLB)(NOT 1 Inf);
	## Vuosttaš vuojahaga fávllimus čuolda lea goaratčuoldan.

MAP (@GA>) TARGET Gen IF (1C A)(1 (Sg Loc))(2C NP-BORDER); #  (NOT 0 Interr OR ALU)
	## ...dan čuldii mii lea joddočuoldda lagamusas.

MAP (@NPron<) TARGET (N Pl Gen) IF (-1 ("mii" Interr) OR ("mihkkege") OR ("gii"));
	## Maid ipmašiid doppe dagat?
	## Ii šaddan mihkkege gávnnademiid.
	
# Appositions
# -----------

#MAP (@APP) TARGET (Num Nom) IF (-2 BOS OR (PUNCT LEFT))(-1 (KLASS Nom))(1 Prop);
 MAP (@APP) TARGET (Num Nom) IF (-2 BOS OR (PUNCT LEFT))(-1 (KLASS Nom));
	## Ášši 07/00 Sámedikki 1999 jahkedieđáhus.

MAP (@APP) TARGET (A Ord) IF (-1 Prop LINK NOT 0 Plc);
	## Heinrich IV. lea boaris.
	## Heinrich njealját lea boaris.
	
MAP (@APP) TARGET Num IF (-3 MANNU)(-2 Ord)(-1 ("beaivi") OR ("b" ABBR));
    ## Geassemánu 8.b. 2001 láhkaásahus nr 676 oktagaslaš plánaid birra dearvvašvuođalágaid vuođul lea addojuvvon láhkamearrádusaid láhkavuođuin.
    
MAP (@APP) TARGET (Num Nom) IF (-1C Nom LINK 0 MANNU)(0 CARDINALS);
MAP (@APP) TARGET (Num Nom) IF (-1 Loc LINK 0 MANNU)(0 CARDINALS);
    ## Guovdageaidnu / Kautokeino, skábmamánnu 2002.
    ## Guovdageaidnu / Kautokeino, skábmamánus 2002.
    
#MAP (@APP) TARGET (Num Nom) IF (-1 Gen LINK 0 MANNU)(0 CARDINALS LINK 0 Nom)(*1 Po OR N BARRIER NOT-NP);
    ## Guovdageaidnu / Kautokeino, skábmamánu 2002 rádjai lei buorre.  


# Numeral mappings
# ----------------

MAP (@APP @SUBJ @SPRED @HNOUN) TARGET (Num Nom) IF (-1 MANNU LINK NOT 0 Nom)(0 CARDINALS);
    ## Guovdageaidnu / Kautokeino, skábmamánu 2002 viesut ledje buorre.
MAP (@ADVL @GP> @GN>) TARGET (Num Gen) IF (-1 MANNU LINK NOT 0 Nom)(0 CARDINALS);
    ## Guovdageaidnu / Kautokeino, skábmamánu 2002.	
MAP (@OBJ @OPRED) TARGET (Num Acc) IF (-1 MANNU LINK NOT 0 Nom)(0 CARDINALS);
	
# Numeral adjective mappings
# --------------------------

MAP (@AN>) TARGET (A Ord) IF (*1 N BARRIER NOT-A);
	## III kapihtal priváhta arkiivvaid birra máinnaša vuosttažettiin gáhttenárvosaš priváhta arkiivvaid.

MAP (@AN>) TARGET (A Ord) IF (1 ("b"));
	## Guovdageaidnu, skábmamánu 3. b. 2002.
	
# Coordination
# - - - - - - -
	
MAP (@AN>) TARGET (A Ord) IF (1 CC LINK *1 N BARRIER NOT-A);
    ##  Dát lea ođđa orndet , ja jienasteapmi dáhpáhuvvá dušše čakčamánu 28. beaivve, iige 28. ja 29. beaivvi nie go mii čáliimet maŋimuš áviisas.

MAP (@AN>) TARGET (A Ord) IF (1 COMMA OR CC LINK 1 (@AN>) LINK 1 CC LINK *1 N BARRIER NOT-A);
 
# Quantifier mappings
# -------------------

MAP (@NumQ<) TARGET (Num) IF (-1 Num)(0 NUMUNIT);
    ## Dat lea 4 miljovnna deajakohpa.
    
MAP (@AN>) TARGET (A Ord)(*1 N BARRIER NOT-A);
        ## Dat lei 33. teakstaoassi.
        ## Arkiivaláhka III máinnaša vuosttažettiin gáhttenárvosaš priváhta arkiivvaid.


MAP (@CS-COMPL) TARGET (Num Nom) IF (*-2 V-TRANS-ACT BARRIER VERB LINK 
	NOT 0 ("eanet"))(-1 ("go"))(NOT 1 N LINK 0 ADVLCASE)
	(NOT *1 VFIN BARRIER CS);
	## Ii heargi gal nagot guoddit eambbo go guokte diekkára. 

MAP (@CS-COMPL) TARGET (Num Nom) IF (-1 ("dego"))(NOT *1 VFIN BARRIER CS);
	## Ráhkkásiiddán, allet vajáldahte ahte Hearrái lea okta beaivi 
	## dego duhát jagi ja duhát jagi dego okta beaivi.

# MAP (@QN>) TARGET (Num Acc) IF (*1 (N Acc) BARRIER NP-BORDER OR N)(NOT 1 ("dbm") OR ("diibmu"))(NOT 2 (Num));
	## Mun máksen 100 ruvnno duvle.

MAP (@ADVL) TARGET (Num Nom) IF (-1 ("b" ABBR) OR ("beaivi"))(-2 Ord);
	## Artihkkal lea šaddan fas áigeguovdilin maŋŋá go heivehuvvui 
	## norgalaš riektái miessemánu 21. b. 1999 nr 30 lága bokte.
	
MAP (@QN>) TARGET (Num Acc) IF (1 MEASURE LINK 1 ALU);
	## Guokte golbma mehtera guhku, ja guhkitge, sáhttá vuojahat leat.
	
MAP (@QN>) TARGET (Num Acc) IF (0 Num BARRIER NOTCOMMA)(*1 (Num Acc) LINK 1 MEASURE LINK 1 ALU);
	## Guokte golbma mehtera guhku, ja guhkitge, sáhttá vuojahat leat.
	
MAP (@QN>) TARGET (Num Acc) IF (1 (Num Acc) LINK 1 (Num Acc) LINK 1 MEASURE LINK 1 ALU);
	## Guokte golbma njeallje mehtera guhku, ja guhkitge, sáhttá vuojahat leat.

MAP (@QN>) TARGET (Num Acc) IF (1 COMMA LINK 1 (Num Acc) LINK 1 COMMA LINK 1(Num Acc) LINK 1 MEASURE LINK 1 ALU);
    ## Čuollun sáhttá leat dušše okta fierbmi gurgaluvvon, muhto dábálaččat lea čuollu guhkit, golbma, njeallja fierbmebaji guhku.

#MAP (@QN>) TARGET (Num Gen) IF (NOT 0 (Num Nom))(NOT -1 ("nr"))(*1 (N Gen) BARRIER NP-BORDER OR N);
MAP (@QN>) TARGET (Num Gen) IF (NOT -1 ("nr"))(*1 (N Gen) OR (N Acc) OR (N Ill) OR (N Loc) BARRIER NP-BORDER OR N); #XXXTEST
	## Galgá čájehit vuollegašvuođa juohke ovtta divššohasa eallimii.
	## Diibmá lei Davvi Nuorra sturron 268 mielláhttui.

MAP (@QN>) TARGET (Num Com) IF (*1 (N Com) BARRIER NP-BORDER OR N);
	## Das ráhkaduvvo vuojahat gátti guvlui nu galliin čuolddain 
	## (dahje oar-julggiin) go lea vuogas.

MAP (@QN>) TARGET (Num Acc) IF (*1 ALU BARRIER NP-BORDER OR N);
	## Noađđespagáin leat dat geažit guokte golbma tumá guhku.

MAP (@QN>) TARGET (Num Acc) IF (1 MEASURE OR TIME)(2 A OR Adv OR Po);
	## Suorpmas lea guokte millimehtara govdat.

MAP (@QN>) TARGET (Num Acc) IF (1 (N Sg Gen))(2 (A Comp));
	## Son vuvddii daid biergguid vihtta ruvnno hálbbibui.

MAP (@QN>) TARGET (Num Gen) IF (1 TIME)(2 ("boaris"));
	## Son lea viđa jagi boaris.

MAP (@QN>) TARGET Num IF (1 ("jahkásaš"));
	## Golmma-njealje jahkásaš mánná biddjojuvvo giissá sisa čohkkut.

MAP (@QN>) TARGET (Num Gen) IF (1 MEASURE)(2 ALU OR A);
	## Min viessu lea viđa mehtara alu.

MAP (@NUM-PRON) TARGET (N Num Nom) IF (-1 (Pers Nom))(NOT -1 Dem)
	(-1 Pl1 OR Pl2 OR Pl3);
	## Mii golmmas oktan du vieljain finaimet Niillas-čeazi geahčen.

MAP (@SUBJ @SPRED) TARGET (N Num Nom);
	## Go sotnabeaivi šattai, de čuoigaledje máŋggas várrái.
	
MAP (@NumN<) TARGET Nom IF (-1 ("nr"))(0 CARDINALS OR INITIAL)(-2 N OR A OR ABBR OR ACR)(NOT -2 Gen);
#MAP (@NumN<) TARGET (Num Nom) IF (-1 ("nr" Nom))(-2 N OR A OR ABBR OR ACR);
    ## Mun lean ilus go beasan ovdanbuktit St.dieđ. nr. 33.
    #Maybe it has to be (N @ADVL).

MAP (@SUBJ-QH @SPRED) TARGET (Num Nom) IF (0 NUMERALS)
	(*1 Gen BARRIER NP-BORDER LINK NOT 0 ROUTE OR TIME OR Loc OR Num OR Adv)
	(NOT 1 CC)(NOT *1 ALU BARRIER NP-BORDER)(NOT -1 (@NNum>) LINK -2 (@OBJ))
	(NOT -1 ("s") OR ("siidu") OR ("paragráfa") OR 
    ("kapihttal") OR ("kapihtal") OR HEADOFPARTS OR ("jahki") OR GEASSET);
	## Dá dat leat, 350 ruvnno.
	# Maybe "OR (N @ADVL)" has to be added.

MAP (@SUBJ-QH @SPRED) TARGET (Num Nom) IF (1 (Dem Pl Loc));
	## Okta dain doaimmain lea redigeret ja čorget journála.
	## ...ahte eamiálbmotriekti sáhttá leat okta dain speadjaliin...

MAP (@SUBJ-QH) TARGET (Num Nom) IF (1 TIME)(*2 ("vássit") BARRIER V-NOT-AUX);
	## ...ovdalgo 10 jagi leat vássán...

MAP (@SUBJ-QH) TARGET (Num Nom) IF (*-1 ("vássit") BARRIER VERB OR CLB)(1 TIME);
	## Muhto dál leat juo vássán golbma beaivvi dan rájes go dát dáhpáhuvai.

MAP (@SUBJ-QH) TARGET (Num Nom) IF (-1 CC)(1 Prop);
	##

MAP (@OBJ) TARGET (Num Acc) IF (*-1 ("dahkat") OR ("váldit") BARRIER NOT-ADV)(0 CARDINALS)(1 TIME);
    ## Mun válddán 2 diimmu.
    
MAP (@NumN<) TARGET Num IF (-1 N OR ACR LINK NOT 0 (@NQ<))(0 CARDINALS)(1 EOS)
    (NOT -1 NUMMAR OR MANNU);
    ## Dat lea dieđáhus 4.
    ## Son dagai 1000 diimmu 2005:s.
	## Viidváseappot mearrida dearvvašvuođabargiidlága § 10 vuosttas lađđasa...
 # Should hit numerals that only have to do with numbering, not with amount. 
 
MAP (@NumN<) TARGET Num IF (-2 N)(-1 NUMMAR OR MANNU)(0 CARDINALS)(1 EOS);
    ## Dat lea dieđáhus nr. 4.
 
MAP (@NumN<) TARGET Num IF (-1 N OR A OR ACR LINK NOT 0 (@NQ<))(0 CARDINALS)(NOT 0 ("oktii"))
    (NOT 1 NPROP OR A OR ACR)(NOT -1 NUMMAR OR MANNU);
    ## Bearjadaga 21.03.1980.
    #This one is still too strict. It's getting better..
    
MAP (@NumN<) TARGET Num IF (-1 N OR A OR ACR LINK NOT 0 (@NQ<))(0 CARDINALS)(NOT 0 ("oktii") OR ("okta") OR ("1"))
    (1 Nom)(NOT -1 NUMMAR OR MANNU);
    
MAP (@NumN<) TARGET Num IF (0 CARDINALS) (-1 ("s") OR ("siidu") OR ("§") OR ("paragráfa") OR 
    ("kapihttal") OR ("kapihtal") OR HEADOFPARTS OR ("jahki") OR GEASSET LINK NOT 0 (@NQ<));
	## Dat lea s. 240.
	# evtl. LAHKA 
	
MAP (@NumN<) TARGET Num IF (-3 ("s") OR ("siidu") OR ("§") OR ("paragráfa") OR 
    ("kapihttal") OR HEADOFPARTS OR GEASSET LINK NOT 0 (@NQ<))
	(-2 Num)(-1 CC)(0 CARDINALS);
	## ...gč. dearvvašvuođabargiidlága kapihttal 6 ja 7.
	
MAP (@NNum>) TARGET ("nr") IF (1 WORD OR Num);
    ##Mun lean ilus go beasan ovdanbuktit St.dieđ. nr. 33.
    
MAP (@NNum>) TARGET N IF (0 ("nummar") OR ("nummir"))(1 WORD OR Num);

#MAP (@QN>) TARGET Num IF (NOT -1 Num OR ("nr"))(NOT 0 ADVLCASE)(*1 Gen OR Ill OR Loc OR Com
 # 	BARRIER NPNH OR REALCLB LINK NOT 0 Interr)(NOT 0 (N Num) LINK 1 (Sg Com))
#	(NOT 1 Pers LINK 0 Sg1 OR Sg2 OR Sg3)
#	(NOT 1 CC LINK 1 Num LINK 1 NP-BORDER)
 #   (NOT 1 ("dbm") OR ("diibmu") LINK 1 (Num));
	## Jus divššohas lea gaskal 12 ja 16 jagi boaris,...

MAP (@QN>) TARGET Num IF (*1 Ess BARRIER NPNH LINK NOT 0 (V Actio));
	##

MAP (@QN>) TARGET ("okta" Nom) IF (*1 (N Nom) BARRIER NPNH)(NOT 1 Prop OR Dem);
	## Sámegiela doaibmaplánas lei okta doaibmabidju sámegillii jorgalit
	## dieđalaš artihkkaliid.

MAP (@QN>) TARGET ("okta" Gen) IF (*1 (N Gen) OR (N Acc) OR (N Ill) OR (N Loc) 
    BARRIER NPNH)(NOT 1 Prop OR Dem);

MAP (@QN>) TARGET ("okta" Com) IF (*1 (N Com) BARRIER NPNH)(NOT 1 Prop OR Dem);
    ## Dát lea faskkon maid geavaha dušše ovttain gieđain.
    
MAP (@QN>) TARGET ("okta" Ess) IF (*1 (N Ess) BARRIER NPNH)(NOT 1 Prop OR Dem);
    # # No example yet.

# To be evaluated against the previous 4 rules.
#MAP (@QN>) TARGET ("okta") IF (*1 N BARRIER NPNH)(NOT 1 Prop OR Dem);
#	## Sámegiela doaibmaplánas lei okta doaibmabidju sámegillii jorgalit
#	## dieđalaš artihkkaliid.

MAP (@QN<) TARGET Num IF (-1 ("diibmu" Nom) OR ("dbm" Nom));
	## Mun boađán diibmu vihtta/viđas.
	
#MAP (@QN<) TARGET Ord IF (-1 N)(NOT 1 N OR A OR ACR);
    ## Olav V.
	
#MAP (@ADVL) TARGET Num IF (-1 MANNU)(1 NP-BORDER);
	## Dat hávdáduvvojedje Álahedjui skábmamánus 1997.
 
MAP (@ADVL) TARGET Num IF (0 Acc)(*1 TIME OR ROUTE BARRIER NOT-A); 
    ## Dat orui doppe vihtta guhkes jagi.
    
    
    
# Fragment mappings
# -----------------

MAP (@HNOUN) TARGET (N Nom) IF (NOT *-1 VFIN OR ("leat" PrfPrc) OR CP)
    (NOT 0 ("nummar") OR ("nummir"))(1 CARDINALS)
	(NOT *1 (N Nom) BARRIER CS OR CC)
	(NOT *1 VFIN BARRIER CS)(NOT 1 go OR COMMA);
	## Luossa-bidin jođđu-buođuin Deanus.

MAP (@HNOUN) TARGET (N Nom) IF (NOT *-1 VFIN)(NOT *-1 ("leapma" Dimin))
	(1 Inf)(NOT *2 VFIN)(NOT 0 Pl LINK 1 V-PL3)
	(NOT *-1 CC BARRIER NOT-NP LINK -1 Nom);
	## Stipeanda čállit prošeaktaohcamiid.

#MAP (@HNOUN) TARGET (N Nom) IF (NOT *-1 VFIN OR ("leat" PrfPrc))(1 EOS);
	## Vuosttaš boddu.
	
MAP (@HNOUN) TARGET (N Nom) IF (NOT *-1 VFIN OR ("leat" PrfPrc) OR (N Nom) OR (ACR Nom))(NOT 0 ("nummar") OR ("nummir") LINK 1 CARDINALS)(NOT *1 VFIN);
	## Vuosttaš boddu.
	# Should numerals be included? Until we are sure, look at the next rule.
	
MAP (@HNOUN) TARGET (Num Nom) IF (-1 BOS)(1 CLB);
    ## Okta.
    ## 1.
    # Unless "." is the ordinal marker.
	
MAP (@HNOUN) TARGET (N Sg Nom) IF (*-1 BOS BARRIER NOT-NP)
	(NOT *-1 CC BARRIER NOT-NP LINK -1 Nom)
	(NOT 1 CC LINK *1 Nom BARRIER NOT-NP)
	(*1 Inf BARRIER VERB LINK 1 EOS OR PUNCT);
	## Áigemearri váidalusa buktit.

MAP (@HNOUN) TARGET (N Sg Nom) IF (*-1 BOS BARRIER VFIN)(1 CC)
	(*1 (N Nom) BARRIER NOT-NP LINK *1 EOS BARRIER VFIN);
	## Váidalusa hápmi ja sisdoallu § 7-4.

MAP (@HNOUN) TARGET (N Sg Nom) IF (*-1 CC BARRIER NOT-NP LINK -1 Nom 
	LINK *-1 BOS BARRIER NOT-NP)(1 EOS OR PUNCT)
    (NOT *1 PUNCT-LEFT BARRIER NOT-ADV-PCLE LINK *1 PUNCT-RIGHT LINK *1 V-PL3 BARRIER NOT-ADV-PCLE);
	## Suhttu ja riidu.
	## Molssaevttolaš dálkkasdiehtaga našunála dutkanguovddáš ja Sámi dearvvašvuođadutkanguovddáš (goappašat fágabirrasat gullet Romssa universitehta vuollái) berrejit sáhttit oassálastit dán bargui.

MAP (@VOC) TARGET Nom IF (-1 BOS OR REALCLB)(1 COMMA)(2 Nom)(NOT 2 Interr)
	(NOT 3 COMMA OR CC LINK *1 Nom BARRIER NPNH);
	## Hearrá, mun lean gullan olusiid muitaleamen dán. 

MAP (@VOC) TARGET (N Sg Nom) IF (-1 BOS OR REALCLB)(1 COMMA)
	(*2 (Pron Sg2))(NOT 2 Interr);
	## Hearrá, du ráhkis ustit lea buohcci.
	## Ustiban, in mun daga dutnje vearrut.

MAP (@VOC) TARGET Nom IF (-1 BOS OR REALCLB)(1 COMMA)(2 VFIN);
	## Ustiban, in mun daga dutnje vearrut.

MAP (@VOC) TARGET Nom IF (*-1 COMMA BARRIER CC OR NOT-NP OR CONTRA 
	LINK *-1 (V Sg2) OR (V Du2) OR (V Pl2) OR (Pron Sg2) OR (Pron Du2) OR 
	(Pron Pl2) LINK NOT 0 ("leat"))(1 EOS)
	(NOT 0 Sg LINK *-1 V-SG3 BARRIER VERB);
	## Vuoi du, Betsaida!

MAP (@VOC) TARGET Nom IF (-1 COMMA)(0 Pers)(0 Sg2 OR Du2 Or Pl2)
	(*1 Nom BARRIER NPNH LINK 1 EOS);
	## Gáidet eret mu luhtte, dii verrošeaddjit! 

MAP (@VOC) TARGET (N Nom) IF (-2 BOS)(-1 (Imprt Sg2))(NOT -1 Adv OR Qst)(1 EOS);
	## Gula eadni!

MAP (@VOC) TARGET (N Nom) IF (-3 BOS)(-2 (Imprt Sg2) OR (Sg2 Qst))(NOT -2 Nom)
	(-1 COMMA)(1 COMMA);
	## Buvttes, Máhtte, goikemuoraid!

MAP (@VOC) TARGET (N Nom) IF (-4 BOS)(-3 (V Sg2))(-2 go)(-1 COMMA)(1 COMMA);
	## It go, bártnážan, boađáše fargga ruoktot?

MAP (@CMPND) TARGET Cmpnd;
	## Dát ii guoskka dikšui mii boahtá mánáid- ja nuoraidpsykiatriija 
	## vuollái.

MAP (@PROP>) TARGET (Prop Attr);
	## Rektor Tove Bull álgaga mielde...


# Verb mappings
# -------------
MAP (@+FAUXV @+FMAINV) TARGET VFIN IF (NOT *-1 ("leat") OR INF-VERB OR Neg 
	BARRIER VERB OR CS OR CP)(0 ("orrut"));
	## Sii orro darfegođiid siste.

MAP (@-FAUXV @-FMAINV) TARGET ("orrut");
	## Olbmot eai orron čadnon dušše iežaset mearragáddái bivdit.

MAP (@-FAUXV @-FMAINV) TARGET Inf IF (*-1 INF-VERB BARRIER VFIN)
	(0 AUX-OR-MAIN);
	## Son galgá beassat buktit oaivilis.

MAP (@+FAUXV @+FMAINV) TARGET VFIN IF (NOT *-1 VFIN BARRIER CS OR CP)
	(0 AUX-OR-MAIN);
	## Maid Máret áiggui goarrumis?

MAP (@-FAUXV) TARGET ConNeg IF (*-1 Neg BARRIER VERB)(0 AUX-OR-MAIN)
	(*1 Inf BARRIER VERB OR CS OR CP);
	## Eai guovttis ge nagot loktet dan geađggi. 

MAP (@-FAUXV @-FMAINV) TARGET AUX-OR-MAIN IF (NOT 0 VFIN);
	## In beassan dal deatnojohtima oaidnit.

MAP (@-FMAINV) TARGET AUX-OR-MAIN IF (*-1 ("leat") BARRIER VERB)
	(NOT *1 VERB BARRIER CS);
	## Sámegiela lea ávkkálaš máhttit.

MAP (@-FAUXV) TARGET AUX IF (*-1 Neg BARRIER CS OR CP)(0 ConNeg OR PrfPrc);
	## Eat mii sáhte vuolgit.

MAP (@-FAUXV) TARGET PrfPrc IF (0 ("leapma" Dimin));
	## Máŋgga oktavuođas lea leamaš stuora jearru ja dárbu
	## eamiálbmotdutkamiid fágaidgaskasaš semináraide.

MAP (@-FAUXV) TARGET Inf IF (*-1 INF-VERB BARRIER VFIN OR CLB OR CS OR CP)(0 AUX);
	## Dat áigu leat ruovttus.

MAP (@+FAUXV) TARGET VFIN IF (0 AUX);
	## Don šattat vel joksat Sirpmás gohcciid.

MAP (@-FAUXV) TARGET AUX ;
	## Mun in šaddan vuolgit.

MAP (@+FMAINV) TARGET VFIN-NOT-AUX ;
	## Boađe boahtte vahku.

MAP (@-FMAINV) TARGET PrfPrc IF (*-1 ("leat") BARRIER PrfPrc OR (Actio Ess))
	(1 NP-BORDER);
	## Ja Biret-Elle lea easka skuvllas geargan.

MAP (@-FMAINV) TARGET PrfPrc IF (*-1 ("leat") BARRIER PrfPrc OR (Actio Ess))
	(1 Num);
	## Lean mun gal oaidnán čakčaseavdnjadiin dola rievdamiin čázi alde Deanus.

MAP (@-FMAINV) TARGET PrfPrc IF (*-1 VFIN BARRIER REALCLB OR go LINK NOT 0 (V N))
    (NOT *-1 PrfPrc BARRIER VERB OR N); 
	## Ii dal láhppán gal leat.
	## Ii son liikon dohko.
	## Ii okta ge geahččalan eará sivaid ohcat.

MAP (@-FMAINV) TARGET PrfPrc IF (*-1 (Actio Nom) BARRIER NOT-ADV-PCLE LINK
	*-1 ("leat" Sg3) BARRIER NPNHA LINK NOT *-1 Nom BARRIER CS);
	## Leago miehtan vižžojuvvon divššohasas...

MAP (@-FMAINV) TARGET PrfPrc IF (*-1 ("leat") BARRIER CS)(NOT 0 Pass);
	## Dalle livčče njuolggadusaid giehtaguššan issorasat geahpidan 
	## guolástanvuoigaduvvomiid logu.

MAP (@-FMAINV) TARGET PrfPrc IF (-2 PrfPrc)(-1 CC);
	## Artihkal lea sámegillii jorgaluvvon ja almmuhuvvon Romssa universitehta 
	## Juridihka fakultehta čálaráiddus.

MAP (@-FMAINV) TARGET PrfPrc IF (*-1 ("dušše") BARRIER VERB LINK 
	*-1 ("leat") BARRIER VERB);
	## Čuollun sáhttá leat dušše okta fierbmi gurgaluvvon.

 # Since "dušše" is also VFIN, we need a special rule to find "leat" across it.

MAP (@-FMAINV) TARGET PrfPrc IF (*-1 ("go" Pcle) BARRIER VERB OR CS OR CP LINK
	-1 ("leat") LINK 0 VFIN-NOT-IMP);
	## Leat go don máistán dien bierggus?

MAP (@-FMAINV) TARGET PrfPrc IF (*-1 ("leat") BARRIER NOT-ADV);
	## Leatgo dahkkojuvvon gustojeaddji vuoigatvuođat nugo...

MAP (@-FMAINV) TARGET Inf;
	## Manaigo Biret-Elle viežžat boastta?

MAP (@-FMAINV) TARGET ConNeg ;
	## Iigo dutnje šat boađe áviisa?

MAP (@-FMAINV) TARGET (V Actio Ess);
	## Oađidettiin gullen olbmo boahtimin.

MAP (@-FMAINV) TARGET (V Actio Gen) IF (1 ("dihte") OR ("dihtii"));
	## Boađán du lusa čállin dihte dan reivve.

MAP (@-FMAINV) TARGET (V Actio Gen) IF (1 CC)(2 (V Actio Gen))
	(3 ("dihte") OR ("dihtii"));
	## ...seailluhan, nannen ja ovddidan dihte sámi servodaga.

MAP (@-FMAINV) TARGET (V Actio Gen) IF (1 COMMA)(2 (V Actio Gen))(3 CC)
	(4 (V Actio Gen))(5 ("dihte") OR ("dihtii"));
	## ..seailluhan, nannen ja ovddidan dihte sámi servodaga.

MAP (@-FMAINV) TARGET (V Actio Loc) IF (*-1 V-NOT-AUX BARRIER NOT-ADV-PCLE);
	## Juhán heittii suhkamis.

MAP (@-FMAINV) TARGET (V Actio Loc) IF (*-1 NP-HEAD-ACC BARRIER VERB 
	LINK *-1 V-NOT-AUX OR ("jaska") BARRIER NPNHA);
	## Dat galgá hehttet guoli jorggiheames vulos.

MAP (@-FMAINV) TARGET Sup;
	## Áddjá bázii mállása vuoššažit. 


# Complements of particles in comparison
# --------------------------------------


ADD (@CS-COMPL) TARGET Nom IF (*-2 ("nu") BARRIER NOT-NP LINK 1 A OR Adv)(-1 ("go"));
	## Nu guhkás go vejolaš galgá dat čuovvoluvvot.
   
MAP (@CS-COMPL) TARGET Nom IF (*-1 ("go") BARRIER NOT-NP LINK NOT -1 VFIN)
	(1 PUNCT)(2 EOS);
	## Ovdabarggut állanit seamma beallái go "láhkateavstta lunddolaš lohkan".

MAP (@CS-COMPL) TARGET Nom IF (*-1 ("go") BARRIER NOT-NP LINK NOT -1 VFIN)
	(1 CC)(NOT *1 VFIN-NOT-IMP);
	## Sin mearridanorgánain berre mearridanváldi leat nu viiddis go vejolaš 
	## ja nu čadni go vejolaš.

MAP (@CS-COMPL) TARGET Nom IF (*-1 ("go") BARRIER NOT-NP OR Nom 
	LINK NOT -1 VFIN OR BOS)(1 CLB LINK NOT *1 VFIN BARRIER CC);
	##  Muhto buođđogeažit ja giđđabuođut ráhkaduvvojit muđui seammá 
	## vugiid mielde go geassebuođut, ja danne heive buođđun birra 
	## obbalohkái juo muitaluvvot.

MAP (@CS-COMPL) TARGET Nom IF (*-1 ("go") BARRIER NOT-NP OR Nom 
	LINK *-1 Nom BARRIER CS OR CONTRA)(NOT *-1 go BARRIER NOT-NP 
	LINK -1 VFIN LINK *-1 BOS OR CS BARRIER NOT-CC)(1 EOS OR CLB)
	(NOT 1 CLB LINK *1 VFIN BARRIER CC)
	(NOT 1 CLB LINK *1 NP-HEAD-NOM BARRIER NPNH)
	(NOT 2 Interr LINK *1 VFIN LINK *1 VFIN BARRIER CS); 
	## ...muhto galjebut dat lea go guv-čalmmit.
	

MAP (@CS-COMPL) TARGET Nom IF (-1 ("dego"))
	(NOT *1 VFIN-NOT-IMP BARRIER CS OR CLB);
	## Sii leat dego eŋgelat almmis.

MAP (@CS-COMPL) TARGET Nom IF (*-1 ("dego") BARRIER NOT-NP)(1 EOS OR CLB);
	## Son oahppaladdá min allagasas dego beaivváža badjáneapmi.

MAP (@CS-COMPL) TARGET (N Nom) IF (*-1 ("go") BARRIER NPNH LINK -1 A)
	(NOT *1 VFIN BARRIER NOT-ADV-PCLE);
	## Máhttájeaddji ii leat stuorit go oahpaheaddjis.

MAP (@CS-COMPL) TARGET (Pron Nom) IF (*-1 ("go") BARRIER NPNH LINK -1 A)
	(NOT *1 VFIN BARRIER NOT-ADV-PCLE);
	## Dalle dat viežžá čieža eará vuoiŋŋa vel, bahábuid go son ieš. 

MAP (@CS-COMPL) TARGET Nom IF (*-1 ("dego") BARRIER NOT-NP)(1 (Pron Interr))
	(*2 VFIN LINK NOT *1 VFIN);
	## Dat lea dego olmmái gii manai olgoriikii.

MAP (@CS-COMPL) TARGET Nom IF (*-1 ("dego") BARRIER NOT-NP)
	(NOT 0 Attr LINK *1 Nom BARRIER NPNH)(NOT *1 VFIN-NOT-IMP BARRIER CS OR CLB)
	(NOT 1 ("go") OR ("dego"));
	## Dego njamaheaddji eadni dikšumin mánáidis...

MAP (@CS-COMPL) TARGET Nom IF (*-1 ("dego") BARRIER NPNH)(0 Sg)(1 CC)
	(*2 (Sg Nom) OR (Sg3 Nom) BARRIER NPNH LINK *1 V-SG3 BARRIER NOT-ADV-PCLE);
	## Dat guhte ii biso mu siste, bálkestuvvo olggos dego oaksi ja son goldná.

MAP (@CS-COMPL) TARGET Nom IF (*-1 COMMA BARRIER NPNHAI LINK -1 (@CS-COMPL))(1 COMMA OR CC)
	(*2 Nom BARRIER NPNH);
	## Seamma gusto uhcit fuđđožiidda nu go neahti, čeavrris, mievri ja rieban.
	## ledje beakkán ja dovddus olbmot nu go Diaz , Maria Hætta , Anne Gaup , Erik ja vel muhtin eará dovddus olbmot

MAP (@CS-COMPL) TARGET Nom IF (*-1 CC BARRIER NPNHAI LINK -1 (@CS-COMPL))
	(NOT *1 VFIN OR go OR ("dego") BARRIER CS);
	## Jesus lea gievrrat go Sáhtán ja bahá vuoiŋŋat. 

MAP (@CS-COMPL) TARGET Acc IF (-2 Acc)(-1 ("dego"));
	## Mun vuolggahan din dego sávzzaid gumppiid gaskii.

ADD (@CS-COMPL) TARGET Nom IF (*-1 ("go") BARRIER NOT-NP LINK NOT -1 VFIN)
	(1 EOS);
	## Dat galgá čuovvoluvvot nu guhkás go vejolaš.


# Other special nominative mappings
# ---------------------------------

ADD (@ADVL) TARGET Nom IF (0 ("diibmu") OR ("dbm"))(1 Num);
	## Mun boađán diibmu vihtta.

MAP (@ADVL) TARGET Nom IF (-2 Num)(-1 ("ruvdno"))(NOT 0 ("velggolaš"));
	## Sallidat mákset 10 ruvnno bihttá. 


# Arguments and secondary predicates
# ----------------------------------

MAP (@OPRED) TARGET ("veara") IF (*-1 NP-HEAD-ACC BARRIER NOT-NP 
	LINK *-1 OPRED-VERB BARRIER NOT-NP);
	## Muhto mun in ane ealliman mange vearan.

MAP (@SPRED @OPRED) TARGET ("veara" Ess);
	## Dan birra in gávnnat vearan cealkit.

MAP (@SPRED @ADVL) TARGET ("veara"); 
	## Dat ledje vihttalogiduhát silbaruđa veara.			# @SPRED
	## Ohcamat filbmaulbmiliidda leat lassánan mearkkašahtti veara. # @ADVL

ADD (@ADVL) TARGET ("mearkkašan#veara");
	## Buhttenrievttálaččat ii nuppástuvvon mearkkašanveara...

ADD (@ADVL) TARGET ("namuhan#veara");
	##

MAP (@SUBJ @SPRED) TARGET Nom IF (NOT 0 ("diibmu") OR ("dbm") OR Adv
	LINK *1 Num BARRIER NP-BORDER OR COMMA LINK 1 Num LINK *1 MEASURE LINK 1 ALU)
	(NOT 0 Num LINK *-1 ("diibmu") OR ("dbm") BARRIER NP-BORDER)
	(NOT 0 (Prop Nom) LINK 1 ORGANIZATION);
	## Manne ii leat áviisa boahtán?

MAP (@PROP>) TARGET (Prop Plc Nom) IF (1 N); 
    ## Beazedievvá mánáidgárdi.
    
MAP (@SPRED) TARGET PrfPrc IF (*-1 ("orrut") BARRIER NOT-ADV-PCLE)(1 NP-BORDER);
	## Olbmot eai orron čadnon dušše iežaset mearragáddái.

MAP (@SPRED @OPRED) TARGET PrsPrc IF (-1 ("nu"))(1 ("go" Pcle));
	## Mearridanváldi berre leat nu čadni go vejolaš.

MAP (@SUBJ) TARGET Indef IF (NOT *-1 Nom BARRIER NOT-ADV-PCLE)
	(NOT -1 ("mii" Interr Sg Gen))(NOT 0 Acc OR ADVLCASE)(1 V-SG3)
	(NOT *2 Nom BARRIER CS LINK NOT 0 PrfPrc)
	(NOT *-1 V-TRANS-ACT BARRIER CS)(NOT *1 V-TRANS-ACT BARRIER CS);
	## ...ovdal go buot lea dáhpáhuvvan.

MAP (@SUBJ) TARGET ("buot") IF (*1 V-SG3 BARRIER NOT-ADV-PCLE)
	(NOT *1 V-TRANS-ACT BARRIER CS OR CP)(NOT 1 ("buohkanassii"));
	## Buot lei varran.

MAP (@SUBJ) TARGET ("buot") IF (-1 COPULAS);
	## Danin lea buot nu olu álkit inuihtaide.

MAP (@OBJ @OPRED) TARGET Acc IF (0 NUMERALS)
    (*1 Gen BARRIER NP-BORDER LINK NOT 0 ROUTE OR TIME OR Loc)
    (NOT 1 CC)(NOT *1 ALU BARRIER NP-BORDER)(NOT -1 (@NNum>) LINK -2 (@OBJ));
    ## Mun oidnen 4 nieidda.

MAP (@OBJ @OPRED) TARGET Acc IF (NOT 0 TIME OR ROUTE OR Adv)
    (NOT 0 Num LINK *1 TIME OR ROUTE BARRIER NOT-A);
    ## Dat orui doppe vihtta guhkes jagi.
	## Manaigo Biret-Elle viežžat boastta? 

ADD (@OBJ) TARGET Acc IF (*-1 V-TRANS-ACT BARRIER VERB OR CC OR CLB OR CS OR CP)
	(0 TIME OR ROUTE);
	## Stuorábu ráđđet beaivvi ja uhcibu ráđđet ija.

ADD (@OBJ) TARGET Acc IF (*-2 V-TRANS-ACT BARRIER VERB OR CC OR CLB OR CS OR CP)
	(-1 Indef)(0 TIME OR ROUTE);
	## Dat gáibida ollu áiggi.

ADD (@OBJ) TARGET Acc IF (0 TIME OR ROUTE)
	(*1 V-TRANS-ACT BARRIER NOT-ADV-PCLE LINK 0 VFIN OR Inf OR PrfPrc);
	## ...maŋŋá go lea 16 jagi deavdán.

MAP (@OBJ) TARGET Indef IF (*-1 V-TRANS-ACT BARRIER N OR CLB)
	(NOT 0 Gen OR ADVLCASE OR ("vaikko"))
	(NOT *-1 V-SG3 BARRIER VFIN)(NOT *1 NP-HEAD BARRIER NOT-NP); 
	## Go bohte gávpogii, de muitaledje buot.

MAP (@OBJ) TARGET ("ollu" Indef) IF (*-1 V-TRANS-ACT BARRIER NPNH 
	LINK *-1 NP-HEAD-NOM BARRIER CS OR CP);
	## Meardebuođđobivdu gal ii mearkkaš ollu álbmoga ealáhussii.

MAP (@OBJ) TARGET Indef IF (*-1 V-TRANS-ACT BARRIER CS LINK NOT *1 Inf)(NOT 0 ADVLCASE)
	(NOT *1 (N Acc) BARRIER NOT-ADJ)(NOT *1 Inf);
	## Mun anán buot vahágin.
	
MAP (@OBJ) TARGET Indef IF (*1 V-TRANS-ACT BARRIER CS LINK NOT 0 (Imprt Prs Pl1))
    (NOT 0 ADVLCASE)(NOT *1 (N Acc) BARRIER NOT-ADJ);
	## Buot maid dii čatnabehtet eatnama alde, lea čadnojuvvon almmis.

MAP (@OBJ) TARGET ("buot" Pron) IF (*-1 (@SUBJ) BARRIER CC OR CS OR CP)
	(*1 V-TRANS-ACT BARRIER NOT-ADV-PCLE);
	## It don galgga buot čuohpadit.
	
MAP (@OBJ) TARGET (Pron Rel) IF (0 Acc);
    ## 	
	
MAP (@SPRED) TARGET (N Ess) IF (*-1 BOS OR CS OR (":" CLB) OR CP BARRIER NOT-NP)
	(*1 COPULAS BARRIER NOT-ADV-PCLE LINK 1 Inf);
	## ...man ulbmilin lei lohkat ja juogustit bohccuid. 

MAP (@SPRED) TARGET (N Ess) IF (*-1 BOS OR CS OR (":" CLB) OR CP BARRIER NOT-NP)
	(*1 COPULAS BARRIER NOT-ADV-PCLE LINK *1 (N Nom) BARRIER NOT-NP);
	## Riektemetoda vuođđun lea našunálastáhtalaš riekteárbevieruid áddejupmi.

MAP (@SPRED) TARGET ALU ;
	## Dábálaččat leat joddut sullii ovtta guhku.

MAP (@SPRED) TARGET Gen IF (*-1 COPULAS BARRIER NOT-ADV-PCLE)(1 EOS OR CS);
	## Dat lea mu.

MAP (@SPRED) TARGET Gen IF (*-1 CC BARRIER NPNH LINK *-1 (@SPRED) BARRIER WORD)
	(1 EOS OR CS);
	## Áhká rumaš ii leat su iežas oapmi, muhto boatnjá.

MAP (@SPRED) TARGET (Interr Gen) IF (NOT 0 ("mii"))(NOT *1 A OR N BARRIER NPNH)
	(NOT *1 V-NOT-AUX BARRIER CS OR CP);
	## Gean dat lea?

MAP (@SPRED) TARGET Gen IF (NOT 0 TIME)(1 Pers)(2 COPULAS)
	(NOT *3 V-NOT-AUX BARRIER CS OR CP);
	## Dan guovttos dat leat.

MAP (@SPRED) TARGET (Pass PrfPrc) IF (-1 BOS OR REALCLB)(1 ("leat"))
	(*2 NP-HEAD-NOM BARRIER NOT-NP);
	## Buressivdniduvvon lehkos son guhte boahtá Hearránammii.

MAP (@SPRED) TARGET Indef IF (*-1 COPULAS BARRIER VERB LINK *-1 (@SUBJ) BARRIER 
	CS OR CP)(NOT 0 ADVLCASE)(NOT *1 VERB BARRIER CS);
	## Dakkár dálolaččat leat nu ollu dál Deanus, ahte...

MAP (@-FMAINV) TARGET PrfPrc IF (-1 COMMA)(NOT *-2 VFIN)(NOT *1 VFIN);
	## Pearmišuvnnat, gohččojuvvon gohččuma ja áššelisttu dohkkeheapmi.



# Adverbial mappings
# ------------------

MAP (@ADVL) TARGET Adv ;
	## Mun máksen 100 ruvnno duvle.

MAP (@ADVL) TARGET Po ;
	## Daid ovddas gal in máksán maidege.

MAP (@ADVL) TARGET Pr ;
	## Jus divššohas lea badjel 16 jagi boaris...

MAP (@ADVL) TARGET (VAbess) ;
	## Mus báhce moadde girjji logakeahttá.

MAP (@ADVL) TARGET VGen ;
	## Máhtte manai čuoigga skuvlii.

MAP (@ADVL) TARGET Ger ;
	## Biigá lei bassaladdame báhpa boađedettiin.

MAP (@ADVL) TARGET Ill ;
	## Cokka láibbiid sehkkii.
	
MAP (@ADVL) TARGET Loc ;
	## Maid dat Kárášjogas bargá?

MAP (@ADVL) TARGET Com ;
	## Dieđut galget meannuduvvot várrugasvuođain.

MAP (@ADVL) TARGET Ess IF (0 ("bievla") OR ("buolaš") OR ("čuoika") OR 
	("dálki") OR ("dulvi") OR ("seavdnjat"));
 # and more nouns
	## Ii leat bievlan vejolaš fievrridit gálvvu gerresiiguin. 

MAP (@ADVL) TARGET (N Ess) IF (0 ("bassi"));
	## Bassin it galggaše bargat.

MAP (@SPRED @OPRED) TARGET (A Ess) IF (0 ("bassi"));
	## Son gohčoduvvo bassin ja Ipmila Bárdnin.
 # "Bassin" can be predicative or adverbial. Therefore it is treated separately.

MAP (@SPRED @OPRED) TARGET Ess ;
	## Dat lea oahpaheaddjin doppe.

MAP (@ADVL) TARGET Acc IF (0 TIME OR ROUTE);
	## Beaivvi mun čohkkájin doppe.

MAP (@ADVL) TARGET Gen IF (1 NP-BORDER OR Num)(NOT 1C Adv);
	## Áddjá lei soggái čalmmiid.
	## Leago dudno mielas suohtas go sáhttá rabas julggiid vázzit?
    ## Mun čálán dieđáhusa 4. # I don´t see a genitive here?
    # This is about an absolute genitive
    
MAP (@SUBJ @OBJ @ADVL @SPRED @OPRED @GP> @GP<) TARGET Date ;
    ## Odne lea 27.01.2005.
    ## 21.03. lea mu riegádanbeaivvi.
    
# Default mapping
# ---------------

MAP (@X) TARGET WORD ;



# ==================== #
# ==================== #
# Disambiguation rules #
# ==================== #
# ==================== #



 
# ============================== #
# Testing out a new section type #
# ============================== #
            CORRECTIONS          #
# ============================== #

#SUBSTITUTE (Plc) (Sur) TARGET (Prop) IF (-1C (Mal Attr) OR (Fem Attr));
SUBSTITUTE (Plc) (Sur) TARGET Prop IF (-1C (Mal Attr) OR (Fem Attr));
    ## Linda England.
    # This rule does not work, it may be that the CORRECTIONS mechanism is not implemented.
    # Todo: Discuss this with Eckhard.
    
#SUBSTITUTE (@OBJ) (@APP) TARGET N IF (-1 Ord LINK -1 Num LINK -1 KLASS);
	## Geahča § 12 vuosttas lađđasa mávssu birra máŋgosa/nuppádasa ovddas.

SUBSTITUTE (@OBJ) (@APP)  (N) IF (-1 Ord); #(-2 Num)(-3 N LINK 0 KLASS);
	## Geahča § 12 vuosttas lađđasa mávssu birra máŋgosa/nuppádasa ovddas.
# This is a stupid version of the previous rule, just to see whether it works.
# Neither this nor the following one work. Sigh.
SUBSTITUTE (@QN>) (@APP) TARGET Num IF (-1 KLASS)(0 Num);

# =================================== #
# One-cohort disambiguation - cycle 0 #
# =================================== #
             CONSTRAINTS #1           #
# =================================== #

    # ----------------- #
    # Table of Contents #
    # ----------------- #

    # Abbreviations
    # Lexicalised names
    # Lexicalised derivations
    # Lexicalised adjectives
    # Adjectives or nouns, not adverbs
    # Lexicalised adverbs
    # Adverbs, not nouns or adjectives
    # Lexical N versus Actio
    # Nouns, not adverbs or adjectives 
    # Nouns, not verbs
    # Nouns, verbs or numerals
    # Not noun
    # Temporal expressions with locative case
    # Adposition + Px
    # Some verb rules
    # Compounds
    # Proper nouns

# Abbreviations
# -------------

SELECT ("s") IF (0 ("S"));
	## s. 24.

# Acronyms
# --------

REMOVE Num IF (0 ACR);
	## TV2

# Lexicalised names
# -----------------

REMOVE ACR IF (0 Prop) ;
    ## NRK lea čujuhan iežas áigut leat dehálaš seailluhit sámi servodaga.

# Lexicalised derivations
# -----------------------
# SELECT instead of REMOVE if one wants to investigate word structure.

# m4 starts (derivations)
REMOVE N-DER IF (0 LEX-N LINK NOT 0 Ess);
       ## Dat lea oahpaheaddjin doppe.
 # Since Actio was included in the N-DER set, this rule now removes many Actios.
 # This may be unfortunate. If so, the remedy is to remove Actio from the N-DER set.

REMOVE N-DER IF (0 N-DER LINK 0 Ess)(0 LEX-N LINK 0 Ess)(NOT 0 Actio);
	## Boarrásis leai dat vel eanaš ealáhussan ge.
 # Removes N-DER if O is also a lexical noun, and both nouns are essive.

REMOVE A-DER IF (0 LEX-A);
	## Dábálaš mearrádusat.

REMOVE V-DER IF (0 LEX-V)(NOT 0 (eaddji)); 
	## Maid don doaivvut?

# m4 ends

 # The negative condition is to avoid ending up with only a PrfPrc reading 
 # e.g. for "registrerejeaddji" in 
 # "… dan láhkai ahte dieđuid registrerejeaddji, journálaovddasvástideaddji ja
 # bearráigeahččoeiseválddit sáhttet viežžat dieđuid dárbbu mield".
 

# Lexicalised adjectives
# ----------------------
REMOVE VGen IF (0 A);
	## Na varas guolli dal lea buorre.

REMOVE PrsPrc IF (0 A);
	## Áššis biddjui mearrideaddji deaddu dasa ahte...  

REMOVE (V* A) IF (0 LEX-A);
	## Boradeapmi basakeahtes gieđaiguin ii daga olbmo buhtismeahttumin.

SELECT A IF (0 ("sivaheapmi"));
	## Ehpet livčče dubmen sivahemiid.
 # This rule removes the reading sivahit+V+eapmi+N+Actio.
 # A more general rule (SELECT A IF (0 (heapmi))(0 Actio) went wrong.

# Particular adjectives
# ---------------------

REMOVE ("čáppis") IF (0 ("čáppat" Attr));
	## čáppat nieidda.
	
# Adjectives or nouns, not adverbs
# --------------------------------
 # Words that probably are not adverbs when they appear in the -aččat form.
 # Words that are most likely to be nouns are picked out by a later rule. 
 # This rule deals with those -aččat words that could be nouns or adjectives 
 # but not adverbs. 

REMOVE Adv IF (0 ("ášše#oassi" laš) OR ("bosnialaš") OR ("čuovvovaš") OR 
	("dánskalaš") OR ("Davvinorga" laš) OR ("davvi#norga" laš) OR 
	("davviriikkalaš") OR ("dehálaš") OR ("duiskkalaš") OR ("eahpeduhtavaš") OR 
	("egyptalaš") OR ("gávpi" laš) OR ("gávppašan#bealle" laš) OR 
	("geavahan#riekti" laš) OR ("greikalaš") OR ("gullevaš") OR ("hollándalaš") OR 
	("julev#sápmelaš") OR ("juvddálaš") OR ("kretalaš") OR ("lulli-#norga" laš) OR 
	("máksu" laš) OR ("norgalaš") OR ("nuortalaš") OR ("oarje#sápmelaš") OR 
	("ohcejohkalaš") OR ("palestinalaš") OR ("romalaš") OR ("sadjásaš") OR 
	("sápmelaš") OR ("sogalaš") OR ("sorjavaš") OR  ("sullasaš") OR ("suddu" laš) 
	OR ("vepselaš"));
	

# Lexicalised adverbs
# -------------------
REMOVE (A* Adv) IF (0 LEX-ADV) ;
	## Son dábálaččat ráhkkana dasa dálvvi mielde. 

REMOVE Foc IF (0 ("dieđusge"));
	## Romssa universitehta áigu dieđusge dán geahččalit áimmahuššat.

SELECT Adv IF (0 ("eahpitkeahttá"));


# Adverbs, not nouns or adjectives
# --------------------------------
 # Words that probably are adverbs when they appear in the -aččat form.
 # Since only the -aččat form gets the Adv tag, the rule will hit this form.

SELECT Adv IF (0 ("absoluhtalaš") OR ("agálaččat") OR ("aitosaččat") OR 
	("aktiiva" laš) OR ("aktiivva" laš) OR ("almmolaš") OR ("analogalaš") OR 
	("automáhtalaš") OR (álgoálgosaš) OR ("álgovuolggalaš") OR ("árbevirolaš") OR 
	("beaivválaččat") OR ("biologalaš") OR ("buhtten#riekti" laš) OR 
	("bustávva" laš) OR 
	("čálalaččat") OR ("čearddalaš") OR ("dábálaččat") OR ("duođalaččat") OR 
	("dutkan#politihkka" laš) OR ("eahpedábálaš") OR ("eahpeformálalaš") OR 
	("eahpekritihkalaš") OR ("eahpevirggálaš") OR ("eaktodáhtolaččat") OR 
	("ekologalaš") OR ("ekologálaš") OR ("ekonomálaš") OR ("eksplisihtalaš") OR 
	("elektrovdna" laš) OR ("etihkka" laš) OR ("etymologalaš") OR ("fágalaš") OR
	 ("fonehtalaš") OR ("fonologalaš") OR ("fonotávttalaš") OR ("formálalaš") OR
	 ("fysiologa" laš) OR ("gaskamearálaš") OR ("geavatlaččat") OR 
	("gealbu" laš) OR ("geografa" laš) OR ("geográfa" laš) OR ("giella" laš) OR 
	("girko#riekti" laš) OR ("globálalaččat") OR ("gorri" laš) OR 
	("govttolaš") OR ("hálddahuslaš") OR ("historjjálaš") OR ("ideologalaš") 
	OR ("iešheanálaš") OR ("implisihtalaččat") OR ("jahkásaččat") OR 
	("jeavddalaččat") OR ("jietnadat#oahppa" laš) OR ("juridihkka" laš) OR 
	("konkrehtalaš") OR ("kritihkka" laš) OR ("kronologalaš") OR 
	("kultur#historjá" laš) OR ("kultur#politihkka" laš) OR ("kultuvra" laš) OR 
	("logihkalaš") OR ("luohtu" laš) OR ("máŋgga#fága" laš) OR ("mekánalaš") OR 
	("metoda" laš) OR ("moralalaš") OR ("morálalaš") OR ("morfologalaš") OR 
	("našuvnnalaš") OR ("negatiivalaš") OR ("negatiivvalaš") OR 
	("njálmmálaččat") OR ("njuolgguslaččat") OR ("oaive#ášši" laš) OR 
	("obbalaš") OR ("oktasaččat") OR ("oktilaš") OR ("ollislaččat") OR 
	("oppalaččat") OR ("optimalalaččat") OR ("ovttabealálaš") OR 
	("ovttajienalaš") OR ("ovttamielalaš") OR ("pedagogalaš") OR 
	("politihkka" laš) OR ("praktihkka" laš) OR ("prinsihppa" laš) OR 
	("priváhta#riekti" laš) OR ("profehtalaš") OR ("psykologalaš") OR 
	("ráfálaččat") OR ("reála#politihkka" laš) OR ("riekte#historjá" laš) OR 
	("riekti" laš) OR ("riikkaidgaskasaš") OR ("sámegielalaš") OR 
	("riikka#goddi" laš) OR ("ruhta" laš) OR ("rumašlaš") OR 
	("sámepolitihkalaš") OR ("sekundáralaččat") OR ("semantihkalaš") OR 
	("servodat#ávki" laš) OR ("siviila#riekti" laš) OR ("sosiálalaš") OR 
	("stáhta#politihkka" laš) OR ("strategalaš") OR ("suorge#politihkka" laš) OR 
	("teknihkka" laš) OR ("teorehtalaš") OR ("topografa" laš) OR 
	("váldo#ášši" laš) OR ("virgi" laš) OR ("vierru" laš));

	## Oppalaččat daid gažaldagaid birra mat gieđáhallojuvvojit NAČ 1993:34,...


REMOVE (N Dimin) IF (0 Adv OR A);
	## Olu mearrádusat gártet iešguđet doaibmasurggiid gaskasaččat.

REMOVE (N* Dimin) IF (0 Adv OR A);
	## Mun vuolggán ihttážis mánnu.
	
REMOVE ("vuolli") IF (-1 ("čállit"))(0 ("vuollái"));
    ## Sii áigot bissehit rievssatbivdu čakčaorohagain, ja dasa lea Saari čállán vuollái.



# Lexical N versus Actio
# ----------------------

REMOVE (N Actio) IF (0 ("eallin") OR ("váldo#giella"));
	## Dat geaidnu lea seaggi mii doallu eallimii.
	## Ruovttugiella mearkkaša giela, mii lea leamaš váldogiellan ruovttus.
 # other nouns?


# Nouns, not adverbs or adjectives 
# --------------------------------
 # Words of which both the singular (-laš) and the plural (-laččat)
 # are probably nouns:

SELECT N IF (0 ("Álttá#sápmelaš") OR ("Anár#sápmelaš") OR ("badje#sápmelaš") 
	OR ("boazo#sápmelaš") OR ("dálolaš") OR ("Deanu#sápmelaš") OR 
	("duottar#sápmelaš") OR ("eana#láigolaš") OR ("Guovdageaidnu" laš) OR 
	("johtti#sápmelaš") OR ("láddelaš") OR ("mearra#sápmelaš") OR 
	("Ohcejoga-#sápmelaš") OR ("Porsáŋggu#sápmelaš") OR ("riddo#sápmelaš") OR 
	("Ruošša#sápmelaš") OR ("samarialaš") OR ("skoalta#sápmelaš") OR 
	("Várjjat-#sápmelaš") OR ("Várjjat#sápmelaš") OR ("várre#sápmelaš") 
	OR ("vuotna#sápmelaš"));

	## ...gos boazosápmelaččat sáhttet jođihit boazodoaluset.


 # Words which are probably nouns when they appear in the plural form (-laččat),
 # but could be adjectives when they appear in the singular form. The rule is 
 # designed to hit the (Pl Nom) forms.

SELECT (N Pl Nom) IF (0 ("egyptalaš") OR ("greikalaš") OR ("julev#sápmelaš") OR 
	("juvddálaš") OR ("kretalaš") OR ("nuortalaš") OR ("oarje#sápmelaš") OR 
	("romalaš") OR ("sápmelaš") OR ("sogalaš"));

	## Sápmelaččat orro maid skandinávalaš njárgga siskkimus guovlluin.
	## ...ja doppe ásse dušše nuortalaččat. 


SELECT (N Pl Nom) IF (0 (eaddji));
	## Dat geađgi man huksejeaddjit hilgo,...


# Nouns, not verbs
# ----------------

REMOVE VAbess IF (0 ("orohat"));
	## Guovlu lea orohaga olggobealde.

REMOVE PrsPrc IF (0 (N Sg));
	## geavaheaddji


# Nouns, verbs, or numerals
# -------------------

REMOVE (Num Ill) IF (0 ("mun")) ;
	## Mii válljiimet dan.

REMOVE (Num Ill) IF (0 (N Ill));
	## Vi.

SELECT (V Neg Ind Sg3) IF (*-1 BOS BARRIER REALWORD);
	## Ii.

SELECT (V Neg Ind Sg2) IF (*-1 BOS BARRIER REALWORD);
	## It.

SELECT (V Neg Ind Sg1) IF (*-1 BOS BARRIER REALWORD);
	## In.

SELECT Inf IF (*-1 (Neg Sup) BARRIER CLB OR CS);




# Particular nouns
# -------------------------------

# identitehta
# - - - - - -
REMOVE ("identitehtta") IF (0 ("identitehta"));

# eana
# - - - 
REMOVE Sg IF (*-1 ("stáhta") OR ("priváhta") OR ("matrikuleret"))(0 ("eana" Pl Loc));
	## Stáhta eatnamiin.

# bealle
# - - - -

REMOVE XBEALLI IF (0 XBEALLE);
    ## Sin jahkásaš johtolagat sáhtte leat čuđiidge kilomehteriid, dat olle dála Suomabeale vuovdeguovlluid dálveorohagain Jiekŋameara rittu geasseorohagaide.
    
# alla#skuvla not allas#kuvla
# - - - - - - - - - - - - 

SELECT EDUCATION IF (0 EDUCATION);
    ## 1. Čakčamárkaniid lágidemiin mii bargat ovttas Sámi allaskuvllain.
    

# Noun classes
# ------------

REMOVE XMENTA IF (0 XMEANTA);
	## dokumentii.

# Not noun
# --------
REMOVE ("duđđa" N Pl);
	## Duđat go mu čállimii?


# Temporal expressions with locative case
# ---------------------------------------
SELECT (N Sg Loc) IF (0 MANNU) ; # To avoid "skábma#mánus" N Sg Nom.
	## Dat hávdáduvvojedje Álahedjui skábmamánus 1997.



# Southern Locative vs. Essive
# ----------------------------

REMOVE (Sg Loc) IF (0 Ess)(NOT 0 Actio);
	## girkun
	# This is a rule that prevents southern dialectal forms to destroy the rest of hte
	# parser. It should be commented out whenever analysing texts with Locative -n only.
	# All words with Cg will avoid this rule, since e.g. várin is southern Loc only.
	## Luossa-bivdin joddu-buođuin deanus. 
	
	

# Dates vs. numerals
# ------------------

REMOVE Num IF (NOT -1 ("§") OR ("paragráfa") OR ("kapihttal") OR ("kap"))(0 Date);
    ## Odne lea 27.01.2005.
    ## Dat lea § 12.2.12.
    
	
# Adposition + Px
# ---------------
SELECT Adp IF (0 ("gaska" N Ess)); ## Sii árvališgohte gaskaneaset.


# Some verb rules
# ---------------

REMOVE Imprt IF (0 Qst);
REMOVE Imprt IF (0C Pass);
	## Bijatgo don sohkkara dedjii?
	## Jos fávleoalggis lea guolli, loktestuvvo dat dušše čázeoaivái.

REMOVE Imprt IF (0  ("berret") OR ("dáidit") OR ("fertet") OR ("galgat") OR 
	("lávet") OR ("sáhttit"));
	## Báldáhan ferte diŋgot.
	## Lága mearrádusat galget váikkuhit luohttevašvuođa.
 # And more verbs, but not the whole AUX set (not "leat", for example).

REMOVE Inf IF (0 Qst);
	## Leatgo addojuvvon neavvagat ja dieđut divššohassii,...

REMOVE Foc IF (0 Actio OR PrfPrc OR VGen);
	## Studeanttat geat leat čađahan váldofágaeksámena...
	
	
# Particular verbs
# ----------------

#REMOVE ("leahkit") IF (0 ("leat" V IV Imprt Prs Sg3) OR ("leat" Inf));	
	## lehkos!

# Vowel-lowering
# --------------

REMOVE GIRJE IF (0 GIRJI);
    ## Uccitlogu evttohusat oidnojit Sámedikki čoahkkingirjjiin.
    
# Diphtong or not
# ----------------

REMOVE EHTTA IF (0 EAHTTA);

#REMOVE AHTA IF (0 AHTTA);

# Compounds
# ---------

REMOVE (SgNomCmp Cmpnd) IF (0 (SgNomCmp Cmpnd)); #SELECT Cmpnd IF (0 Cmpnd); works as well
SELECT SgGenCmp IF (0 SgNomCmp);  
    ## Sosiála- ja dearvvašvuođadepartemeanta.
# This is a mystery. The second rule is the one we want to work, but it only works
# if 
# The background here is that we don't know which one is used if 
# morphology cannot tell, so we may as well remove one of them.

# Compound verbs
# - - - - - - - -

REMOVE ("olggo#saddit") IF (0 ("olggos#addit"));


# Compound nouns
# - - - - - - -

# čuođi
# - - -

REMOVE ("čuhti") IF (0 ("čuođi" Num))(1 MEASURE);
REMOVE ("čuđđi") IF (0 ("čuođi" Num))(1 MEASURE);
    ## Sin jahkásaš johtolagat sáhtte leat čuđiidge kilomehteriid, dat olle dála Suomabeale vuovdeguovlluid dálveorohagain Jiekŋameara rittu geasseorohagaide.
    
# friddjagoartta
# - - - - - - - -

REMOVE ("friddj#agoarta") IF (0 ("friddja#goarta"));

# jienastuslohku
# - - - - - - - -
SELECT ("jienastus#lohku") IF  (0 ("jienastus#loahku"));
    ## Go galgá beassat fárrui sámi jienastuslohkui, de ferte...
    
# máksomearri
# - - - - - -    
REMOVE ("máksom#earri") IF (0 ("mákso#mearri"));
    ## Ii galgga leat stuorát sullasaš máksomearri.
    
# mihttomearri
# - - - - - - -
REMOVE ("mihttom#earri") IF (0 ("mihtto#mearri"));

# nisson
# - - - -

SELECT ("nisson") IF (0 ("nisu"));
    ## Gielddat sáhttet eambbo ulbmillaččat stivret iežaset servodaga ealáhusovdáneami , nu ahte sin gielda šáddá buore gieldan maiddái nissoniidda orrut.

# ovttaoaivilis
# - - - - - - -
SELECT Loc IF (0 ("ovtta#oaivil"));
    ## Finnmárkku Ovddádusbellodaga jođiheaddji Knut Erik Mauseth ii leat ovttaoaivilis iežas bellodagain dien áššis.


# ruvdnu
# - - - -

SELECT ("ruvdnu") IF (0 ("ruvdno"));
SELECT ("kruvdnu") IF (0 ("kruvdno"));

# suorgi vs. suorggis
# - - - - - - - - - -

SELECT ("suorgi") IF (-1 ("priváhta"))(0 ("suorggis"));
    ## Mot lea sámi nissoniid posišuvdna obbalaččat bargoeallimis (vuođđoealáhusain, almmolaš- ja priváhta suorggis , smávva fitnodagain je.) ja erenoamážit go leat bargin sámi ásahusain ja organisašuvnnain ?
    # A coordination rule might work in this case too.

# unnitlohku
# - - - - - -    
SELECT ("unnit#lohku") IF (0 ("unnit#logo"));
    ## Go vuos dovdá ovtta unnitlohkoálbmoga, lea álki fuobmát oahpes minstariid maiddái eará unnitloguid diliin.
    
# válgamálle
# - - - - - -

SELECT ("válga#málle") IF (0 ("válgam#álli"));

         
# vuollebealli
# - - - - - - -
REMOVE ("Vuolle#bealli") IF (0 ("vuolle#bealli"));

SELECT LAHKA IF (NOT 0 LAGA);
    ## Čujuhuvvo maiddái sierramotiivvaide dearvvašvuođalága 45. paragráfii čuolbmačilgehusaid birra jávohisvuođageatnegasvuođa ja geigenlobi ektui epikriissa oktavuođas (oasi dearvvašvuođabargiidlágas oktan mearkkašumiiguin).
    
SELECT GIELDA IF (NOT 0 GIELDDA);
    ## Sosiála ja dearvvašvuođadepartemeanta lea dorjon Spesialistadoavtterguovddáža ja fylkkagieldda psykiatriija dearvvašvuođafuolahusa Kárášjogas.
    

# Proper nouns
# ------------

REMOVE (Prop @NQ<) IF (0 Prop);
	## 3.2 Sámi mánáid ja nuoraid bajásšaddaneavttut.

# This is a Lule Sámi form.
REMOVE ("Sámedigge") IF (0 ("Sámediggi"));
REMOVE ("sáme#digge") IF (0 ("sáme#diggi"));

# The form "norga" is not in the dictionary...
REMOVE ("norga") IF (0 ("Norga"));

REMOVE ("Sáme#giella") IF (0 ("sáme#giella"));
    ## Sámegiela hállet áidna giellan mánát, geat eai leat vel á´lgán skuvlii, bázáhallan olbmot ja boarrásat.

REMOVE ("Sáme#politihkka") IF (0 ("sáme#politihkka"));
    ## Sámepolitihka birra.

REMOVE Prop IF (0 CNOUN)(0 (Prop Pl));
    ## Sápmelaččaide lea hui mávssolaš ahte sin dárbbut váldojit vuhtii. 
    
REMOVE Prop IF (NOT -1 (Prop Attr))(0 ("jávri") OR ("guhkki") OR ("kap") OR ("klubbu") OR ("arena") OR ("arvat") OR ("bassi")); #XXXTEST
    # Philosophy is that these are nouns rather than names.
    ## Jávri lea stuoris.

    
SELECT Prop IF (-1 ("Min" Prop))(0 ("Áigi" Prop));
SELECT Prop IF (0 ("Min" Prop))(1 ("Áigi" Prop));
   ## Mun lean dan lohkan Min Áiggis.
   
REMOVE Prop IF (0 ("sieidi")) IF (1 ("bálvaleapmi"));
SELECT (Gen @GN>) IF (0 ("sieidi") LINK 1 ("bálvaleapmi"));
    ## Sieiddi bálvalemiin sturron eallu gohčoduvvui bálvvoseallun, mi sáhtii jávkat, go oamasteaddji jámii dahje go sieidi ii bálvaluvvon.

# ===================================== #
# Local disambiguation - cycles 1 and 2 #
# ===================================== #

# --------------------------------------------------------- #
# Cycle 1a: cases that will be needed in careful mode later #
# --------------------------------------------------------- #
                             CONSTRAINTS #2                 #
# --------------------------------------------------------- #
# The idea with cycle 1a is to disambiguate cases that are judged as easier and
# more secure than later cases, and that at the same may be used as context
# for other rules in later cycles.

    # ----------------- #
    # Table of Contents #
    # ----------------- #
    
    # Personal pronouns
    # An early rule for "eanaš"/"eanas"
    # Px constraints
    # Certain verb readings
    # A rule for "dušše"
    # ahte
    
    
    


# Personal pronouns
# -----------------

# Mon ( = Mo N Prop Ess)
# - - - - - - - - - - -

# Moai ( = Moa N Prop Ill)
# - - - - - - - - - - - - 
SELECT MOAI IF ( *1 V-DU1);     ## Moai čilge dan ášši.
SELECT MOAI IF (*-1 V-DU1);     ## Čilgego moai dan?


# mii ( = Interr)
# - - - - - - - -

# Left conditions
SELECT Pers IF (-3 BOS OR CS OR (":" CLB))(-2 V-PL1)(-1 go)(0 MII-PERS); 
SELECT Pers IF (-1 (V Ind Prs Pl1 Clt))(0 MII-PERS);
	## Eat go mii leat profehtastallan du nammii?
	##

SELECT Pers IF (-1 (V Neg Pl1))(0 MII-PERS);     
SELECT Pers IF (*-1 V-PL1 BARRIER NOT-ADV OR ("seamma"))(0 MII-PERS)(NOT 1 V-SG3);
	## Eat mii sáhte vuolgit.
	## Goas oinniimet mii du nelgon dahje goikan?

 # The last condition added because of the example below. Attempts to formulate 
 # a negative condition on V-PL1 instead (e.g. LINK NOT 0 A) did not work.
	## Dat geaidnu lea govdat mii doalvu gáđohussii. 

# Right conditions
SELECT Pers IF (0 MII-PERS)(1C Interr);
SELECT Pers IF (0 MII-PERS)(1 COMMA)(2C Interr);
SELECT Pers IF (0 MII-PERS)(*1 V-PL1 BARRIER VFIN OR CP OR CS LINK NOT 0 Adv);
	##
	## .. de mii, guđet leat eatnagat, leat okta rumaš.
	## Dasgo mii oinniimet su násti badjáneamen.

SELECT Pers IF (0 MII-PERS)(1 CC)(2 (N Nom));
	## Mii ja farisealaččat atnit borakeahttáivuođa.


# mis and sis
# - - - - - -
  # mun Pron Pers Pl1 Loc = mis Adv, corr. for son Pron Pers Pl3 Nom = sis Adv
  # This one is hard to resolve, as they have the same distribution.


# don ( = Pron Dem Sg Acc/Gen)
# - - - - - - - - - - - - - -
SELECT Pers IF (*-1 (V Sg2) BARRIER CS LINK NOT 0 Adv)(0 DON); 
SELECT Pers IF (0 DON)(*1 V-SG2 BARRIER PUNCT);  
	## Liikotgo don Abbai?
	## Don it leat nama čállán.


# son ( = Pcle) 
# - - - - - - -
SELECT Pers IF (*-1 V-SG3 BARRIER CLB OR CS OR CP)(NOT -1 Qst or ("go"))
	(0 ("son" Pron Pers Sg3 Nom)); 
	## Ii son liikon dohko.

SELECT Pers IF (0 ("son" Pron Pers Sg3 Nom))(1 (Pron Interr Sg));
	## Son gii addá veahki galgá mearridit.

SELECT Pcle IF (NOT -2 CLB)(-1 Qst or ("go"))(0 ("son"))(1 Nom)
	(NOT 1 Refl OR Inf OR Cmpnd);
	## Lea go son Máret boahtán?

SELECT (Pron Pers Sg3 Nom) IF (0 ("son"))(*1 V-SG3 BARRIER N);  
	## Son fertii vuolgit. 
	
  
# sutnje ( = forms of the verb "suotnjat")
# - - - - - - - - - - - - - - - - - - - -
SELECT (Pron Ill) IF (0 ("son"))(1 (Pron Interr Sg)); 
SELECT (Pron Ill) IF (*-1 (V Ind)) (0 ("son"));  # Barrier?
	## Dearvvašvuođabálvalus galgá addit sutnje gii ohca...
	## Son galgá addit sutnje dárbbašlaš dieđuid.


# danin (Pron Ess OR Adv)
# - - - - - - - - - - - -

SELECT (Pron Ess) IF (*-1 COPULAS BARRIER NOT-ADV-PCLE)(0 ("danin"))(1 EOS OR CLB);
	## ...jos oapmi fierpmit leat danin,...
  

# dat ( Pron Pers, Pron Dem, Pcle)
# - - - - - - - - - - - - - - - -
SELECT Dem IF (*1 (Pl Loc) BARRIER NPNHA)(0 (Pl Loc));
SELECT Dem IF (*1 (Sg Loc) BARRIER NPNHA LINK 1 CS LINK 1 Loc)(0 (Pl Loc));
    ## Dain hirbmat váriin ja mehciin sin guovllus lei earenoamáš vallji sihke elliid ja eará dáfus.

SELECT Dem IF (*1 (Pl Com) BARRIER NPNHA)(0 (Pl Com));
SELECT Dem IF (*1 (Sg Com) BARRIER NPNHA LINK 1 CS LINK 1 Com)(0 (Pl Com));

#SELECT Dem IF (*1 (Pl Nom) BARRIER NPNHA)(0 (Pl Nom));
#SELECT Dem IF (*1 (Sg Nom) BARRIER NPNHA LINK 1 CS LINK 1 Nom)(0 (Pl Nom));
#
#SELECT Dem IF (*1 (Pl Gen) BARRIER NPNHA)(0 (Pl Gen));
#SELECT Dem IF (*1 (Sg Gen) BARRIER NPNHA LINK 1 CS LINK 1 Gen)(0 (Pl Gen));
#
#SELECT Dem IF (*1 (Pl Acc) BARRIER NPNHA)(0 (Pl Acc));
#SELECT Dem IF (*1 (Sg Acc) BARRIER NPNHA LINK 1 CS LINK 1 Acc)(0 (Pl Acc));
#
#SELECT Dem IF (*1 (Pl Ill) BARRIER NPNHA)(0 (Pl Ill));
#SELECT Dem IF (*1 (Sg Ill) BARRIER NPNHA LINK 1 CS LINK 1 Ill)(0 (Pl Ill));
#
#SELECT Dem IF (*1 (Pl Ess) BARRIER NPNHA)(0 (Pl Ess));
#SELECT Dem IF (*1 (Sg Ess) BARRIER NPNHA LINK 1 CS LINK 1 Ess)(0 (Pl Ess));

#SELECT Dem IF (*1 (Sg Loc) BARRIER NPNHA)(0 (Sg Loc));
#SELECT Dem IF (*1 (Sg Com) BARRIER NPNHA)(0 (Sg Com));
#SELECT Dem IF (*1 (Sg Nom) BARRIER NPNHA)(0 (Sg Nom));
#SELECT Dem IF (*1 (Sg Gen) BARRIER NPNHA)(0 (Sg Gen));
#SELECT Dem IF (*1 (Sg Acc) BARRIER NPNHA)(0 (Sg Acc));
#SELECT Dem IF (*1 (Sg Ill) BARRIER NPNHA)(0 (Sg Ill));
#SELECT Dem IF (*1 (Sg Ess) BARRIER NPNHA)(0 (Sg Ess));

SELECT Pers IF (-1 Nom)(NOT -1 Pers)(0 ("dat" Nom))(*1 ("leat") BARRIER NOT-ADV-PCLE 
	LINK 1 CLB OR EOS);
	## Hei, soiddas dat lea, máistte don dan gáfe!
	## Gean mearkka bohccot dat ledje?

SELECT Pcle IF (-1 Nom)(NOT -1 A OR Interr OR PrfPrc)(0 ("dat" Nom))
	(NOT *1 N BARRIER NP-BORDER);
	## Máret dat boahtá.
	## Mun dat lean.

SELECT Pcle IF (NOT *-1 (V Sg3) OR (V Du3) OR (V Pl3) BARRIER CC OR CS OR CP)
	(NOT -1 BOS OR CS OR (":" CLB))(0 ("dat" Nom))(NOT *1 N BARRIER NOT-NP)
	(NOT *1 (V Sg3) OR (V Du3) OR (V Pl3) BARRIER CC OR CS OR CP);
	## Na vuolggán dat gal mun ge.

REMOVE Dem IF (0 ("dat"))(NOT 0 ("dasgo") OR ("danin") OR ("dainna"))
	(NOT 1 A OR Num OR N OR Indef OR Attr)(NOT 1 PrfPrc LINK *1 N BARRIER NPNH)
	(NOT 0 Nom LINK 1 ("gal") OR Pcle LINK 1 (N Nom))
	(NOT 1 NP-HEAD-GEN LINK 1 (Actio Gen) LINK 1 N);
	## Dá dat leat.
	## Moai soabaime dasa.
 
REMOVE Dem IF (0 (Pron Pers))(NOT *1 N BARRIER NPNHAII)(NOT 1 ("nubbi" Gen));
	## Aiddo sii movttidahttojuvvojit daid buot smiehttat sámegillii.
	## Nubbi noađđeheargi goallostuvvo giehtahearggi lávžái ja goalmmát fas dan nuppi lávžái. # It shouldn´t take this one.

SELECT Dem IF (-2 (N Pl Nom))(-1 CC)(0 (Pron Pers Pl3 Gen))(*1 N BARRIER NPNH);
	## Skovit ja daid rávačállagat galget leat sámegillii.


# dasa ( = forms of the verb "dassat")
# - - - - - - - - - - - - - - - - - - 
REMOVE VGen IF (0 Pron);
	## Na muhto don it leat dasa nama čállán! 
	## Dat gal lei beare unnán.

# Earlier specific rules, to be reactivated if the previous one is too strong.

#SELECT Dem IF (0 ("dat" Pron Dem Sg Ill))(1 VFIN); ## Dasa lea guhkes historjá.
#SELECT Dem IF (0 ("dat" Pron Dem Sg Ill))(1 ("lassi" N Ess)); ## Dasa lassin.
#SELECT Ill IF (-1 ("sivva"))(0 ("dat")); ## Sivva dasa lea, ahte ...
#SELECT Ill IF (-1 PrfPrc)(0 ("dat")); ## Sii leat čujuhan dasa vuosttas jagi.


SELECT Ill IF (0 ("dat"))(1 ("lassi" Ess));
	## Dasa lassin buvttán dás lasseargumenttaid.  


# dáidda (N Sg Nom or Pron Dem Pl Ill)
# - - - - - - - - - - - - - - - - - - -
SELECT Ill IF (0 ("dáidda"))(*1 (N Pl Ill) BARRIER NPNH);
	## ...ja dáidda gielaide sáhttá čállit journálaid. 

SELECT Ill IF (*-1 ILL-VERB BARRIER NOT-ADV-PCLE)(0 ("dáidda"));
	## ...mat gullet dáidda 1972-konvenšuvnna mielde.

REMOVE Nom IF (-1 NP-HEAD-NOM)(0 ("dáidda"));
	## Sus lea álgovuorus dušše guohtunvuoigatvuohta dáidda. 

REMOVE Nom IF (*-1 V-TRANS-ACT BARRIER CS OR CP OR VFIN)(0 ("dáidda"));
	## ...lea čađahan iskamiid muhtun orohagain ja ávžžuhan 
	## boazologu dáidda.

SELECT Ill IF (-1 ("lassi"))(0 ("dáidda"));
	## Lassin dáidda lea Sámi vuoigatvuođalávdegotti ovdaolmmoš...

SELECT Ill IF (0 ("dáidda"))(*1 ("bidjat") OR ("lasihit") BARRIER V-NOT-AUX); 
	## Ferte mearridit makkár deattu dáidda galgá bidjat.


# dii ("D" Num Ill) or (Pron Pers Pl2 Nom)
# - - - - - - - - - - - - - - - - - - - - -

SELECT Pers IF (0 ("don" Pron Pers Pl2 Nom));
	## Dii.
	# Careful would have been to look for Pl2 verb or excl mark.

# Other homonymies linked to personal pronouns, with non-obvious answers.
  # Munin = Pron Pers Sg1 Ess = Ess of N Prop Muni (!)
  # dudnon = Pron Per Du2 Ess = dudnu N Sg ... PxSg1 (covered by Px rule)
  # Sunin = Pron Pers Sg3 Ess = Ess of N Prop Sun  
  
# Personal pronoun, not numeral
# - - - - - - - - - - - - - - -

REMOVE Num IF (-1 BOS)(0 (Pron Pers));
    ## Min oainnu mielde lea ealga ressursa.


# An early rule for "eanaš"/"eanas"
# ----------------------------------
SELECT Pron IF (0 ("eanaš") OR ("eanas"))(1 (Dem Loc) OR (Pers Loc));
	## Eanaš dáin gálduin leat hui unnán dieđuid.


# Px constraints
# --------------

# First select some clear Px cases, and remove some equally clear non-cases
# Let these come directly after local disambiguation.

#REMOVE PxSg3 IF (*-1 (PRON-12 Gen @GN>));
    ## Dilli lea min mielas čuožžilan. 


# PxSg1
# - - -
SELECT (Nom PxSg1) IF (-1 BOS)(NOT 0 VFIN)(1 EOS);
	## Ustibažžan!


SELECT (PxSg1) IF (-1 V-SG1)(NOT 0 Dem)(NOT -1 ("leat"))(NOT *1 Acc BARRIER VFIN LINK NOT 1 Po)(NOT *1 ("ahte") BARRIER WORD);
SELECT (PxSg1) IF (*-1 V-SG1 LINK NOT 0 ("leat"))(NOT 0 Dem)(1 CC OR COMMA LINK 1 PxSg1) ;
SELECT (PxSg1) IF (*-3 V-SG1 LINK NOT 0 ("leat"))(-1 CC LINK -1 PxSg1)(NOT 0 Dem);
	## Mun attán heaggan sávzzaid ovddas.
	## Mun attán heaggan, áhkkán ja ruhtan sávzzaid ovddas.
    ## Mun válddán ovdamearkan Buolbmága. # It shouldn`t take this example.

SELECT (Nom PxSg1) IF (-1 PUNCT OR CLB)(0 N-PX)(1 COMMA);
	## Bárdnán, leage dorvvolaš.


# PxSg2
# - - -
SELECT (PxSg2) IF (-1 V-SG2)(NOT -1 ("leat"))(-2 DON);
	## Don attát heakkat mu ovddas?

SELECT (PxSg2) IF (*-1C (Imprt Sg2) BARRIER NPNHA LINK 0 V-TRANS-ACT LINK
	NOT 0 ("dassat") OR Pass LINK -1 CC OR CLB OR CS OR BOS)(NOT 0 Adv)
	(NOT 0 (N Pl Nom) LINK *-1 CC OR CLB BARRIER NPNH LINK -1 Nom)
	(NOT 0 (N Pl Nom) LINK -1 Attr LINK 0 Imprt)
	(NOT 0 (N Pl Nom) LINK *1 V-PL3 BARRIER NOT-ADV-PCLE);
	## Buvtte dasto oaffarskeaŋkkat!
	## Lihka bajás, váldde seaŋggat ja vázzil!

#	(NOT 0 Prop OR (V Sg2))
#	(NOT 0 Inf LINK *-1 INF-VERB BARRIER VERB)


SELECT (PxSg2) IF (0 (N Pl Nom))(*1C (V Sg2) BARRIER NOT-ADV-PCLE OR CLB);
	## Hearrá Ipmilat galggat gudnejahttit.

REMOVE (Gen PxSg2) IF (*-1 ("leat") BARRIER CS OR CP) (NOT *-1 (Pron Sg2 Loc)BARRIER CS OR CP)(0 (N Pl Nom))(*1 ("mielde"));

SELECT (Gen PxSg2) IF (*-1 Sg2 BARRIER CS OR CP)(0 (N Pl Nom))(1 Po)(NOT 1 N)
	(NOT *2 NP-HEAD-GEN BARRIER NPNH);
	## Go don rohkadalat, de mana gámmárat sisa.

SELECT PxSg2 IF (-1 ("hearrá" Sg))(0 ("ipmil"));
	## It galgga geahččalit Hearrá Ipmilat.


REMOVE PxSg2 IF (*-1 BOS OR (":" CLB) BARRIER NPNH)(0 (N Pl Nom))(1 (V Pl3) OR (Pron Rel Pl Nom) OR KLASS);
	## Searvi divžžohasat ožžot maid dieđuid bohtasiid birra.

REMOVE PxSg2 IF (*-1 BOS OR (":" CLB) BARRIER Sg2)(NOT 0 N-PX);
	## Nystø mielas leaba soai Lene Hansenin rievttes olbmot ovddastit sámi álbmoga dáhtu Tromssa ohcamušas.
	#  No antecedent, no reflexiven.


# PxSg3
# - - -

# Special nouns

REMOVE PxSg3 IF (0 KLASS)(1 Num);
    ## Suohkanstivra mearridii áššis 62/99 sirdit administrašuvnna Levdnjii.
    
    
REMOVE PxSg3 IF (-2 MANNU LINK 0 Gen)(-1 (A Ord))(0 ("beaivi" Loc));
    ## Konvenšuvdna lea fámus miessemánu 1. beaivvis 2002 ja gusto 30 jagi dán beaivvi rájis.            
REMOVE PxSg3 IF (-1 CC LINK -1 Loc)(0 (Sg Loc))(NOT 1 NP-MEMBER);
    # Remove possessive suffixes in locative coordination.
    ## Hukset orrun- ja doaibmaguovddáža Mázii ja fuollaásodagaid dakko lahkosii, lea vuoruhuvvon doaibma suohkana ekonomiplánas ja doaibmaprográmmas, nu maiddái suohkana boarrásiidplánas oktiibuot 6 mill kr jagi 2001 rádjái.    


 #SELECT (PxSg3) IF (-1 (V TV Sg3) LINK NOT 0 Pass LINK *-1 ("son" Sg3 Nom) OR (N Sg Nom) BARRIER NOT-ADV-PCLE)
	#(NOT 0 N-NOPX)(NOT 0 Attr LINK *1 N BARRIER NPNH);
	## Son beastá álbmogis sin suttuin.
	## Jietna čuorvu meahcis.
	# The last example is errounuously analysed as Px, the rule must be changed. XXX.
	## Jesus leai sullii golbmalotjahkasaš go álggahii doaimmas.

SELECT (PxSg3) IF (*-2 (V Sg3) BARRIER V-NOT-AUX)
	(-1 PrfPrc LINK 0 V-TRANS-ACT LINK NOT 0 LOC-VERB)(0 N-PX)(NOT 0 Nom);
	## ...ovdal go Márjá lei riegádahttán bártnis.


SELECT (PxSg3) IF (*-3 (N Sg Nom) OR (Pron Pers Sg3 Nom))(-2C A)(-1 go)
	(NOT 0 Attr LINK *1 N BARRIER NPNH);
	## Máhttájeaddji ii leat stuorit go oahpaheaddjis, ii ge bálvaleaddji
	## stuorit go hearrás.
	## Mávssut leat adnon eambbo go bušeahtas.

#REMOVE (PxSg3) IF (NOT *1 (V Sg3) BARRIER VFIN)(NOT *-1 (V Sg3) BARRIER VFIN);
    # This rule is removed to  "Residual cases" because then is the infinitiv picked out.
    # This rule is very careful. It just presupposes any potential singular subject 
    # in the sentence in order to allow for a possessive suffix.
    ## Vuođđun lea biddjon ahte dát lanjadagat adnojit "boarrásiid-dikšosadjin", 
    ## muhto maŋŋil teavsttas čilget njealját ásodaga birra.

SELECT (PxSg3) IF (*-1 (Sg Nom) OR (Sg3 Nom) BARRIER Pers OR CS OR CP OR LOC-VERB
    LINK NOT 0 Attr)(NOT *-1 (Imprt Sg2) BARRIER VERB OR CS)(NOT -1 (N Nom) OR (N Acc))(0 N-PX)(NOT 0 Loc LINK 1 Ill);

	## Dat guhte ráhkista bártnis dahje nieiddas eanebut go mu...
    ## Son galgá boahtit álo beroškeahttá makkár duogáš eatnis lea.
    # It might be problematic using Acc as a constraint, since Acc=Gen. But we cannot think of a Gen+NounPx construction that works.

SELECT PxSg3 IF (*-1 (V Sg3) BARRIER CS OR REALCLB)(0 Gen)(1 Po)
	(NOT 1 Pr LINK *1 Gen BARRIER NPNH);
	## Dahje maid sáhttá olmmoš addit máksun heakkas ovddas?

SELECT (Gen PxSg3) IF (*-1 BOS OR CS OR (":" CLB) OR CLB BARRIER NPNH)(1 Po)
	(*2 V-SG3 BARRIER NOT-ADV-PCLE LINK NOT 0 A LINK *1 N BARRIER NPNH);
	## Raporttas bokte dáhttu bargojoavku čalmmustahttit Sámedikki.

SELECT (PxSg3) IF (-2 N-PX)(-1 CC);
	## Mun lean boahtán bidjat soahpameahttunvuođa bártni ja áhčis gaskii.

REMOVE PxSg3 IF (0 ("ođđasis"));
	## Byrokratiijat hábmemušaideaset geažil dušše ráhkadit ođđasis
	## daid čuolmmaid.
 # We could perhaps have selected Adv instead.
 
REMOVE Px IF (0 ("oktavuohta" Loc));
	## Dán doaibmabiju eat váldde mielde dán oktavuođas.

REMOVE (Gen PxSg3) IF (NOT *-1 Pr BARRIER NPNH)(1C Prop);
	## erret eará stuorradiggedieđáhusas Sámedikki doaimmaid birra.

REMOVE PxSg3 IF (NOT *-1 (Sg Nom) OR (Sg Loc) BARRIER CLB OR Rel)(NOT 0 N-PX);
	## Dássážii ii leat čađahuvvon dutkan mii čájeha mo sámi divššohasat vásihit dikšofálaldaga. # ??

# All Px


REMOVE Px IF (*-1 (Pron Pers Gen @GN>) BARRIER NOT-ADJ);
    ## Dilli lea min mielas čuožžilan.
    ## Mu mielas dat lea hui dehálaš njuolggadus.
    #  Note: This rule is too general to accept a hypothetical "Mu áhččán"



# ------------------- #
     CONSTRAINTS #3   #
# ------------------- #

# Certain verb readings
# ---------------------

 # Here we pick out finite verbs that follow personal pronouns. 
 # This selection is quite safe at this point, and we get rid of several 
 # ambiguities that would otherwise be disturbing in the following cycles. 
 # Note that all the following rules, except the rule for Sg2, also select 
 # imperatives.   

# Sg1 - early cycle, safe rules
# - -

SELECT (V Sg1) IF (*-1 MUN BARRIER Nom OR V-SG1 OR CS LINK NOT *-1 V-SG1 
	BARRIER NOT-ADV-PCLE)(NOT 0 PrfPrc LINK *-1 ("leat") BARRIER CS OR CP OR 
	CLB)(NOT 0 PrfPrc LINK *-1 V-SG1 BARRIER NOT-ADV-PCLE);
	## Dego mun dás vuollelis mottiin ovdamearkkain čájehan,...

SELECT (V Sg1) IF (*1 MUN BARRIER NOT-ADV-PCLE OR CLB);
	## Man guhká ferten mun gierdat din?


# Sg2 - early cycle, safe rules
# - - 

SELECT V-SG2 IF (*-1 DON BARRIER V-SG2 OR ConNeg OR CS LINK NOT *-1 V-SG2 
	BARRIER CS OR CP OR CLB);
	## ...jos don fal luoitádat ja gudnejahtát mu.
	## Rohkadallet dasto ná: Áhččámet, don guhte leat almmis.

SELECT V-SG2 IF (*1 DON BARRIER CS OR CP OR CLB);
	## Leat go don dat guhte galgá boahtit? 

REMOVE Inf IF (*-1 BOS BARRIER VFIN)(*1 ("ahte" CS) BARRIER NOT-ADV);
	## Dainna lágiin fuobmát jođánit ahte sámi gielladilis lea árvu maid eará riikkain.


# Sg3 - early cycle, safe rules
# - - 

SELECT (V Sg3) IF (*-1 SON BARRIER Nom OR V-SG3 OR CS OR PUNCT LINK NOT *-1 V-SG3 
	BARRIER NOT-ADV-PCLE)(NOT 0 Adv LINK *1 V-SG3);
	## Geas ležžet bealjit, son gullos!

SELECT (V Sg3) IF (*1 SON BARRIER NOT-ADV-PCLE OR go OR CLB)
	(NOT 0 CS)(NOT 0 Adv LINK *1 (V Sg3) BARRIER CC OR CS OR CP);
	## ...oažžu son atnit dušše dan čázis mii sutnje lea mearriduvvon.

REMOVE (V Sg3) IF (-1 (Pron Nom))(NOT -1 Sg OR Sg3);
	## Sii manne vissui.
	## Dii uhccán oskkolaččat. 

REMOVE (V Pl3) IF (-1 (Pron Rel Sg))(0 (V Sg3));
	## Kahihttalis mas čilge orrunlanjadagaid latnjačovdosa nannejuvvo ahte 
	## lanjadat leat plánejuvvon orruide geat dárbbašit divššu guhkit.


# Negative verb, not abbreviation or roman numeral Ii.

SELECT (V IV Neg Ind Sg3) IF (0 ("ii"))(*1 Foc OR ConNeg BARRIER PUNCT OR COMMA);
	## Ii oktage lea boahtán.

# Du1 - early cycle, safe rules
# - - 
SELECT (V Du1) IF (*-1 MOAI BARRIER V-DU1 OR CS OR PUNCT LINK NOT *-1 V-DU1 
	BARRIER NOT-ADV-PCLE);
	## Moai dihte ahte dát lea munno bárdni.

SELECT (V Du1) IF (NOT 0 Adv LINK *-1 BOS OR PUNCT BARRIER NOT-CC)
	(NOT 0 Adv LINK *-1 (V Du1))(*1 MOAI BARRIER CLB OR CS);
	##


# Du2 - early cycle, safe rules
# - - 
SELECT (V Du2) IF (*-1 DOAI BARRIER V-DU2 OR CC OR CS OR PUNCT LINK NOT *-1 V-DU2 
	BARRIER NOT-ADV-PCLE);
	## Manne doai ozaide mu?

SELECT (V Du2) IF (*1 DOAI BARRIER NOT-ADV-PCLE OR CLB);
	##


# Du3 - early cycle, safe rules
# - -
 # The competition is -ba Foc.

SELECT (V Du3) IF (*-1 SOAI BARRIER V-DU3 OR CS OR PUNCT LINK NOT *-1 V-DU3 
	BARRIER NOT-ADV-PCLE);
	## Soai leaba dahkan čielga oavdudagu.
	## Soai ferteba mannat. 

SELECT (V Du3) IF (*1 SOAI BARRIER NOT-ADV-PCLE OR CLB);
	## Fertebago soai mannat? 

SELECT (V Du3) IF (*-1 ("guokte" Nom) BARRIER CS OR CP) ; 
	## Guokte nissona ferdneba milluin.

REMOVE (V Sg3) IF (0 Du3)(*1 ("guokte" Nom) BARRIER NPNHA);
	## Mus leaba dušše dát guokte oappá geaid don dás oainnát.

SELECT (V Du3) IF (-1 (N Nom))(*-2 CC BARRIER NPNH LINK -1 (N Nom));
	## Per Berg ja Rolf Olsen leaba doppe.
	## Deanu Searat ja Sámi Siida leaba ovttas ordnen dáid gilvvohallamiid.
        ## Stuorra nieida ja unna gánda ferteba leat doppe.
 # Note that 'Searat' is Pl, hence no number requirement in this rule.

SELECT (V Du3) IF (*1 (Sg Nom) BARRIER NPNHA LINK 1 CC LINK *1 (Sg Nom)
	BARRIER NPNH);
	## Dá leaba Brita ja Ole Kåven.

REMOVE (V Sg3) IF (*-1 (N Num Nom) BARRIER VERB OR CLB OR CC OR CS OR CP)
	(0 (V Du3));
	## Biera-guovttos Márehiin leaba daid mánáid váhnemat.


# Pl1 - early cycle, safe rules
# - - 
SELECT (V Pl1) IF (*-1 MII-PERS BARRIER VFIN OR PUNCT LINK NOT *-1 V-PL1 
	BARRIER NOT-ADV-PCLE OR CLB);
	## Mii sápmelaččat gal leat vásihan dán...

SELECT (V Pl1) IF (*1 MII-PERS BARRIER NOT-ADV-PCLE OR CLB OR ("seamma") 
	LINK NOT *1 V-SG3 BARRIER NOT-AA OR CLB);
	## Galgat go mii vuordit muhtun eará? 

REMOVE Imprt IF (-1 MII-PERS OR (N Pl Nom))(0 (V Prs Pl1));
	## Mii áigut maiddái leat aktiivvalaččat mielde juohke dásis.


# Pl2 - early cycle, safe rules
# - -
SELECT (V Pl2) IF (*-1 DII BARRIER NOT-ADV-PCLE OR CLB);
	##

SELECT (V Pl2) IF (*1 DII BARRIER NOT-ADV-PCLE OR MO OR CLB);
	## Addet dii sidjiide borramuša!


# Pl3 - early cycle, safe rules
# - -
SELECT (V Pl3) IF (*-1 SII BARRIER V-PL3 OR CS OR PUNCT LINK NOT *-1 V-PL3 
	BARRIER NOT-ADV-PCLE)(NOT 0 Adv LINK *1 V-PL3 BARRIER CC OR CS OR CP);
	## ...vai sii áiggi mielde oččoše doarvái gelbbolašvuođa.

SELECT (V Pl3) IF (*1 SII BARRIER NOT-ADV-PCLE OR CLB)
	(NOT 0 Adv LINK *1 V-PL3 BARRIER CC OR CS);
	## Máhttego sii čilget áššiset dárogillii?

SELECT (Prt Pl3) IF (*-2C (Prt Pl3) BARRIER VFIN)(-1 CC);
	## Ja buot olbmot imaštalle sakka ja jerre. 

REMOVE (V Ind Pl3) IF (1C VFIN)(NOT 1 Pl3) ;
	## Manne ii leat áviisa boahtán?
	## Máhtte manai čuoigga skuvlii.
	## Vihtta bivdovuogi mun gal dušše namahin.  

REMOVE (Prt Sg2) IF (NOT *-1 (Pron Pers Sg2))(0 (Prs Pl3))(*1 Pass BARRIER NOT-ADV);
	## Reivelonuhallan sámi institušuvnnaiguin galget dahkkot sáme- ja dárogillii.
	#  Probably not prodrop if passivized Sg2.

SELECT (V Pl3) IF (-1 (Pron Rel Pl) OR ("mo") OR ("mov") OR ("movt"))          (-2 (N Pl));
SELECT (V Pl3) IF (-1 (Pron Rel Pl) OR ("mo") OR ("mov") OR ("movt"))(-2 COMMA)(-3 (N Pl));
	## Njuolggadusat movt meannudit oanehisáigge vealgáibideami, beaivádan 25.04.00.
	## Vai bálvalusas lea buohccedikšofágalaš dássi eaktuduvvo ahte leat fágabargit geat virgáividdjojit.

SELECT (V Pl3) IF (-1 CS)(*-2 (V Pl3) BARRIER VFIN LINK -1 (N Pl Nom) OR (Pron Pers Pl3 Nom));
	## Sii ožžot smiehttanáiggi jos háliidit muitalit dan birra.

# PrsPrc
# - - - -
SELECT PrsPrc IF (NOT 0 LEX-N)(1 CC OR COMMA LINK 1 A);
	## Soahpamušas váilu njuolggadusat smávit vahágahtti ja geavatlaččat
	## bággolonistemiide.
 # Early rule since many PrsPrc readings are removed later.

 
# dušše A rule for "dušše"
# - - - - - - - - - -
REMOVE V IF (-1 CS)(0 ("dušše"))(*1 Acc BARRIER NPNHA); 
	## Suhpodit daddjojuvvo dat go dušše nođiid váldá olmmoš eret.

 # This rule has to precede particle rules, so that the verb reading of 
 # "dušše" will not force the CS reading of a preceding particle.


# ahte
# ----
SELECT CC IF (-1 COMPAR)(0 ("ahte"))(1 COMPAR);
SELECT CC IF (-1 Comp)(0 ("ahte"))(1 Comp);
	## Dárogiel sánit bohtet eanet ahte eanet sámegiela sisa.
	## Dat šattai heajut ahte heajut.

SELECT CS IF (0 ("ahte")); # All other occurrences of "ahte" are CSs.

 # These rules have to precede all rules referring to CC, so I put 
 # them here, although they logically belong with the subjunction rules 
 # in a later block.



# ---------------------------------------------------------------- #
# Cycle 1b: removing the readings that were left from the 1a rules #
# ---------------------------------------------------------------- #
                             CONSTRAINTS #4                        #
# ---------------------------------------------------------------- #
  

# Removing (nearly) all remaining Px readings
# -------------------------------------------
 # The remaining Px readings will be taken care of later.

REMOVE (PxSg1) ; # IF (0 Ess); # yes, kill'em all
	## Ale bala váldimis Márjjá ruktosat áhkkán.  


REMOVE (Nom PxSg2);
	## Su lusa bohte olbmot Jerusalemis.
	## Na dát ledje buorit mállásat. 

REMOVE (PxSg2) IF (NOT *-1 (Imprt Sg2) OR MOD-ASP BARRIER CS OR CP LINK 0 Sg2)
	(NOT *1 (V Sg2) BARRIER VFIN OR CS);
	## De bohte ruđat. 
	## Ja gillii fas dohko mannat. 
	## ...gii eanemus ja bistevaččat doallá oktavuođa divššohasain.

REMOVE (PxSg2) IF (*-1 (Sg Gen) OR (Sg3 Gen) BARRIER NPNH LINK -1 CC);
	## ...eará čuovvoleapmi mii johtui biddjo ja dan bohtosat.


REMOVE (PxSg3) IF (-1 Indef)(0 Loc);  # Possessed nouns do not go with Indef?
        ## Olmmoš ii eale dušše láibbis, muhto juohke sánis mii boahtá
        ## Ipmila njálmmis.
        ## Alimusriekti lea dattege muhtun duomus cealkán ahte...

REMOVE (PxSg3) IF (NOT *-1 (V Sg3) BARRIER CS)(NOT -1 go)
	(NOT *1 (V Sg3) BARRIER CS) ;
	## Maid don áiggut márkanis?
	## Divššohasas galget leat dat dieđut, ...
	## Nu guhkás go Gonagas mearrida...

REMOVE (PxSg3) IF (*-1 (@SUBJ) BARRIER CS OR CP OR (@SUBJ) OR Inf 
	LINK NOT 0 Sg OR (TV Sg3))
	(NOT -1 Inf LINK 0 V-TRANS-ACT LINK *-1 V-SG3 BARRIER VERB OR CC OR CLB 
	OR CS OR CP);
	## Láhka gusto olbmuide norgga skiippaid alde mat johtet olggoriikkas.
	## Son oastá dáid váldolinjáid hárrái Stuorradikkis.
	# The last sentence is problematic, telles us that (Sg3 TV) 
	
#REMOVE (PxSg3) IF (*-1 VFIN BARRIER CC OR CLB OR CS OR CP LINK NOT 0 Sg3 OR Inf)
#	(NOT *-1 ConNeg BARRIER CC OR CLB OR CP LINK *-1 (Neg Sg3) BARRIER VERB)
#	(NOT *1 VFIN);
#	## ...geat eai oro bissovaččat riikkas.
	# Removed this rule. It removed the Px in
	## Máhtte ii váidalan geafivuođas."


REMOVE (PxSg3) IF (*-1 Interr BARRIER VFIN)(*1 VFIN LINK NOT 0 Sg3);
	## ...man oaivilis sii leat dakkár áššiid hárrái.



# -------------------------------------------- #
# Cycle 2: Other cases of local disambiguation #
# -------------------------------------------- #
                   CONSTRAINTS #5              #
# -------------------------------------------- #
# Note: Is it clearer to introduce a "CONSTRAINTS" boundary in front of Cycle 2?


# This is the main part of the local disambiguation.
# Here we go through the grammar in a systematic way.
# Cases that should be handled before or after the main disambiguation, 
# will be taken care of in cycles before or after this one.

    # ----------------- #
    # Table of Contents #
    # ----------------- #

    # Noun or not?
        # Based on left context
        # Based on right context
        # Based on circumscribed context
        # Some particular nouns 
    # Early Px rules       
    # Adjectives and adverbs
    # Disambiguationg abbreviations
    # Disambiguating clitics
    # Disambiguating numerals
    # Disambiguating adpositions
        # Rules relating to particular adpositions
        # Adpositions that take modifiers
        # The case of GASKAL
        # Po or Pr?   
    # Disambiguating subjunctions
        # Some particular subjunctions
        # CS selection for specific CS candidates
        # CS removal for specific CS candidates
        # CS removal for specific CS candidates
    # Disambiguating adverbs
        # General adverb rules
        # Adverbs, not interjections
        # Adjectives, not adverbs
        # Adverbs, not comparative or plural adjectives
        # Adverbs, not verbs
        # Verbs, not adverbs
        # Adverbs, not pronouns
        # Adverbs, not nouns
        # Nouns, not adverbs
        # Rules related to specific adverbs (quite a lot)
    # Disambiguating pronouns
        # Interrogative pronouns
        # Reflexive pronouns
        # Reciprocal pronouns
        # Numerals
            # General rules
            # Rules related to specific numerals
        # Indefinite pronouns
            # General rules
            # Particular indefinite pronouns
        # Demonstrative pronouns
            # DP-internal number concord
            # DP-internal case concord
        # Personal pronouns
            # Some rules for personal pronouns again (will hit "dat", except the last one)
            # Disambiguating against other POS
    # Disambiguating adjectives
       # Rules related to specific adjectives
       # Comparative
       # Attribute disambiguation
            # Rules for Attr between Dem and N
            # Other attribute rules
            # Coordinated attributes
            # Special rules for 'buorre' (the only adjective showing case agreement)
            # Comparatives
       # Adjectives as predicates
            # Coordination
       # And now some rules for adverbs that modify adjectives
    # Disambiguating verbs - part 1
        # ConNeg forms
        # Imperative
        # Infinitive
        # Verbgenitive
        # Perfect Participle
        # Actio
        # Present participle
        # Rules for "orrut"
        # Rules for "addit" (which is an adjective, but more often a verb)
    # Disambiguating nouns
        # Proper nouns
        # Disambiguating the semantic tags
        # Nouns versus adjectives
        # Numerus disambiguation
        # Case disambiguation
        # Special rules
        # Genitive
        # Nominative and accusative
        # Nominative
        # Accusative
        # Accusative Px or locative 
        # Illative
        # Locative
        # Comitative
        # Locative and comitative - Disambiguation based upon coordination
        # Essive
        # Late case rules (after other case rules have worked)
    # Disambiguating verbs - part 2 
        # Finite forms
        # Indicative or imperative
        # Verbs according to person and number
        # Rules for a special infinitive construction
        # More finite verbs
        # Passive
        # Non-finite verb forms
            # Infinitive
            # Present Participle
            # Actio/Perfect Participle
            # Imperative II (empty)
            # Verbgenitive
        # Selecting some more finite verbs
        # Lexical disambiguation of verbs
    # Residual cases
        # Case rules
        # Other rules for nouns and pronouns
        # Px rules
        # Determiners
        # Adverbs and adjectives
    
    
    
    


# ============ #
# Noun or not? #
# ============ #


# Based on left context
# ---------------------

SELECT N IF (-1 (A Attr))(NOT 0 Inf OR VFIN OR ("veara") OR ("mearkkašan#veara") OR
	("namuhan#veara") OR ("leat" PrfPrc))(NOT -1 Adv)(1 NP-BORDER);   
	## Dat lea čuovvovaš sajis.
	## Jáhkán livččii váddáseabbu leamaš jus ii livčče juogalágan bearašoktavuohta, lohká Budeaju nieida gii lea bajásšaddan gávpogis.
	# It should not take the latter one.

SELECT (N Nom) IF (-2C (N Nom))(-1 CC)(NOT 0 VERB OR Pron)(NOT 0 (A Ess) LINK -2 Ess)
	(NOT 1 N OR Pron OR Num OR A OR Actio);  
	## Mánát ja nuorat leat dál.

 # Write rules for other case forms.

SELECT N IF (-2 BOS)(-1C Gen)(1 EOS);        ## Son lea Ruoŧa čálli.
                                              # (titles)

REMOVE N IF (-1 (N Gen) OR (Pron Pers Gen))(NOT -1 Nom)(0 Po)(NOT 0 ("guovddáš")); 
	## Daid ovddas gal in máksán maidege.

SELECT N IF (*-1 (Dem Acc) BARRIER NPNH LINK *-1C VFIN BARRIER NOT-ADV-PCLE)
	(0 Acc)(0 VFIN)(NOT 0 PrfPrc LINK *-1 ("leat"));
	## Áhčči dagai dan fatnasa nu oanehačča.

REMOVE PrsPrc IF (-1 A)(NOT -1 Adv)(NOT 1 Ess); 
	## Dainnna ii leat vissis mearri galljodagas.

SELECT A IF (-1 ("eambbo"))(NOT 0 Comp);
	## Muhto eambbo eahpidahtti lea čielggadusa s. 17 cealkámuš.

REMOVE VFIN IF (*-2 VFIN BARRIER CS OR CP)(-1 PrsPrc)(NOT -1 LEX-N) ;
	## Johanas lei buolli ja báiti čuovggas.


# Based on right context
# ----------------------

SELECT N IF (NOT 0 Pers OR VFIN OR (A Ess))(1 CC)(2C N); 
	## Sápmelaččat ja dážat leat ovdal eallán ovttas.

SELECT N IF (NOT 0 PrfPrc LINK *-1 ("leat") BARRIER NOT-ADV-PCLE)(1 COMMA)
	(*2 HNOUN BARRIER NPNH OR CC LINK 1 CC);
	## ...ležžet sii dál sápmelaččat, dážat dahje kveanat.

SELECT N IF (NOT 0 Adv OR Inf OR PrfPrc OR ("hárrái") OR ("orrut") OR ("veara"))
	(1 COMMA)(2 CP);   
	## Doaimmat, maiguin mun barggan.
	## Gažaldat lea dušše, man ollu ... # NOT 0 Adv!
	## Lea miellagiddevaš lohkat, maid ...# NOT 0 Inf!

SELECT N IF (NOT 0 Po OR Pron OR Inf OR PrfPrc)(1C (Pron Interr)); 
	## Dát lea mu bálvaleaddji gean mun lean válljen.

SELECT N IF (-1 BOS OR CS OR (":" CLB))(1 (PUNCT LEFT))(*2 (PUNCT RIGHT) LINK 1 (Pron Interr));
	## Riehkit (riehkit ja fierváhirssat) mat bohtet beaŋkká siskkobeallai 
	## olggobeal priváhta eatnamiid gullet eanaeaiggádii.

REMOVE N IF (0 Pr)(NOT 0 ("guovddáš"))
	(*1 (N Gen) OR (Pron Pers Gen) BARRIER CC OR CS OR CP OR go 
	LINK NOT 0 Cmpnd BARRIER NPNH);
	## Jus divššohas sivas fysihkalaš vigiid,..   

REMOVE N IF (0 VFIN)(0 (V-TRANS-ACT Qst))(NOT *1 VFIN BARRIER VERB OR CS OR CP)
	(*1 Acc BARRIER NPNH);
	## Áiggutgo gáfe?

REMOVE N IF (0 VFIN)(0 V-TRANS-ACT)(1 ("go" Pcle))(*2 Acc BARRIER NPNH)
	(NOT *2 VFIN BARRIER VERB OR CS OR CP);
	## Áiggut go gáfe?

REMOVE A IF (0 N)(NOT 0 (@ADVL))(1 Po)(NOT 1 Pr LINK *1 NP-HEAD-GEN BARRIER NP-BORDER);
        ## Čohkkán dárolaččaid gaskkas.

REMOVE A IF (NOT -1 ("mii" Sg Gen))(0 N)(NOT 0 Comp OR Ess OR ("veara") OR 
	("mearkkašan#veara") OR ("namuhan#veara"))(1C VFIN)
	(NOT 1 ("leat") LINK *1 ("ahte") BARRIER NOT-ADV-PCLE)
	(NOT 1 go LINK *1 COPULAS BARRIER V-NOT-AUX  LINK *1 (@SUBJ) BARRIER NOT-NP)
	(NOT 0 Qst LINK *1 COPULAS BARRIER V-NOT-AUX  LINK *1 (@SUBJ) BARRIER NOT-NP)(NOT 0 (A Ord) LINK -1 Prop LINK NOT 0 Plc); 
	## Dál go sápmelaččain leat bohccot.
	## Man guhkki lea du biila? # NOT -1 "mii" Sg Gen!	 

SELECT VFIN IF (1 go)(*2 ("ahte") BARRIER NOT-ADV);
	## Dáhtut go ahte mii gáikut dat eret?

REMOVE VFIN IF (-1 BOS OR CS OR (":" CLB))(0 NP-HEAD-SG-NOM)(1C V-SG3); 
	## Lásse lea mus reaŋgan.
  # Here I am careful, and require the V-SG3 to be a secure reading.

REMOVE VFIN IF (-1 BOS OR CS OR (":" CLB))(0 (Prop Nom))(NOT 0 ("Lea"))(1 V-SG3);	 
	## Máhtte ferte leat vuolgime.
  # Here I am less careful, and accept also ambiguous V-SG3, since I take it
  # that when the left context is a name, it is a more secure subject-candidate.
  # Alternatively, I might drop the 1C in the previous rule, or include the
  # pronouns.

REMOVE PrsPrc IF (NOT *1 N BARRIER NOT-ADJ-CC)(NOT -1 ("nu"))
	(NOT *-1 COPULAS BARRIER NOT-ADV-PCLE)(NOT 1 COMMA OR CC LINK 1 A) ;
	## Bearráigeahčči galgá doaimmahit bargguidis iešheanálaččat.
	## ...ja ahte dat dáhttu čielgasit berre doahttaluvvot.
 # This rule hits many nouns as well, like "dáhttu" in the last example. OK! 

REMOVE Adv IF (1C Po)(NOT 1 Px OR Adv OR ("mielde"));   
 # The last part of this rule, plus some modifications in Po rules, were necessary
 # to get the adverb analysis of "mielde" in
	## Mun vulgen dušše mielde.
 	## ... šattai Unjárga maid mielde... 

REMOVE A-CASE IF (*-1 BOS OR CS OR (":" CLB) BARRIER NOT-DEM)(0 N)(NOT 0 ("buorre"))(1 N)
(NOT 0 Ord LINK *1 N BARRIER NOT-A);
	## Sápmelaččaid historjá lea boaris.
 # No adjective, except buorre, agrees with a head noun in case. The correct
 # reading for a case-inflected prenominal word must thus be N.
 # (Problem: we have not considered a string predicative adj + other NP)

REMOVE (A Gen) IF (0 N)(NOT 0 Ord)(*1 N BARRIER NOT-ADJ);
	## Sámi dutkamiid guovddáža ásaheapmi.


# Preverbal subject
SELECT (Pl Nom) IF (NOT 0 Inf LINK -1 INF-VERB)(1C (V Pl3))(NOT 0 Adv);
	## Olu nuorat bohte naitalanahkái.
 # This is a brave rule, probably too strong. It comes this early in order to
 # precede other rules.
 # New: Modified because of
	## Prošeavtta áigut čađahit. # Added C to avoid áigut as subject.
	## ... geainna váimmut buoremusat lávggadit. # Buoremusat = Adv!


# Based on circumscribed context
# ------------------------------

REMOVE A IF (-1 Dem)(1 (Pron Interr));    
	## Son lea dan sápmelaš gean dovddan.

REMOVE (A Sg Nom) IF (NOT *-1 VFIN OR ("mii" Sg Gen))(0 (N Sg Nom))
	(*1 V-SG3 BARRIER NOT-ADV-PCLE LINK NOT 0 ("leat") LINK NOT *1 ("ahte")
	BARRIER NOT-ADV-PCLE);
	## Sápmelaš dál soittii dávjá dadjalit: gal han doppe ledje olbmot.

REMOVE (A Pl Nom) IF (NOT *-1 VFIN)(0 (N Pl Nom))(*1 V-PL3 BARRIER NOT-ADV-PCLE);
	## Dálá guovlluin sápmelaččat eai leat ássan nu guhká.

REMOVE Adv IF (NOT *-1 (Pl Nom) BARRIER CS)(0 (N Pl Nom))
	(*1C V-PL3 BARRIER NOT-ADV-PCLE OR CP);
	##  ...ahte nuorat eai nagodan identifiseret iežaset kulturduogážiinniset.

REMOVE V IF (-1C Dem)(1 V-IND-FIN);	
	## Dán báhkis šaddá čáppa gohppu.

REMOVE V IF (-1 BOS OR CS OR (":" CLB))(0 N)(1 ("go"))(2 VFIN);	
	## Ádjás go lea ođđa fanas?

REMOVE (V Ind) IF (-1 Dem)(0 N)(1C (V Ind));
	## Dakkár registreren ii leat goassege leamaš oassin árbevierus. 

REMOVE (V Ind) IF (-1 CS)(0 N)(NOT 0 (V Pl1)) (1 (Pron Interr) LINK *1 (V Ind)); 
	## Dasgo mánná mii lea su siste... 

REMOVE Adv IF (-1 BOS OR CS OR (":" CLB) OR PUNCT)(0 (N Sg Nom))
	(NOT 0 Prop OR ("duo") OR ("várra"))(1 V-SG3);
	## Áigi lea dievvan.

REMOVE (N Ess) IF (NOT *-1 OPRED-VERB BARRIER VERB)(0 (V Actio Ess));
	## Son oinnii Ipmila Vuoiŋŋa boahtimin vuolás.
	## Luosahat adnojuvvojit dalle goas stuorát guolli lea vuodjamin.


# Some particular nouns
# ---------------------

# álbmot
# - - - -
SELECT N IF (-1 ("sápmi" Sg Gen))(0 ("álbmot"));
	## Dárbbašago sámi álbmot bálvalusfálaldagaid lasiheami?

# bealistis
# - - - - -
SELECT N IF (*-1 NP-HEAD-NOM BARRIER NOT-ADV-PCLE)(0 ("bealis"))(0 Px);
	## Stáhta bealistis vuođusta rievttis háldduiguin.

# fuolla (idiomatic)
# - - - - - - - - - 
SELECT (N Acc) IF (*-1 ("atnit") BARRIER VFIN OR CS OR CP)(0 ("fuolla")); 
	## Son ii ane fuola sávzzain.
	
# gielda
# - - - -
SELECT ("gielda") IF (-1 FYLKA LINK 0 (@GN>));

# giella
# - - - -
SELECT ("giella") IF (-1 Attr);
	## Sámegiella hállet áidna giellan mánát, geat eai leat vel álgán skuvlii.

# guovddáš
# - - - - -
SELECT Attr IF (0 ("guovddáš"))(1 N); 
	## Guovddáš doaibmabidju lea vuođđudit sámegiela álgooahpahusa.

# guovttes
# - - - - 
REMOVE ("guovttes") IF (1 (Sg Gen));
REMOVE ("guovttes") IF (*1 (N Sg Gen) BARRIER NPNH);
	## Viesus leat guokte olbmo.
	## Sáhtášii juohkit guokte 5 mánnosaš stipeandda čállit ohcamiid. 

# hálidit, háliidit vs. hálihit
# - - - - - - - - - - - - - - -

REMOVE ("hálihit") IF (0 ("hálidit") OR ("háliidit"))(*1 (@-FMAINV) BARRIER CS OR CLB);
    ## Son ii hálit dan dahkat.

# imaš
# - - - 
SELECT N IF (-1 VFIN OR ConNeg OR A OR Dem)(0 ("imaš"));
	## Lei imaš, go buvddas eai vuovdde albma varramárffiid.
	## dakkár imaš ja ártegis dilli ahte ...

# jahke#čuohti
# - - - - - - -
SELECT ("jahke#čuohti");
	## Finnmárkolaččat ledje atnán vuvddiid jahkečuđiid.

# lága vs láhka
# - - - -
    
SELECT ("lága") IF (0 ("lága"))(-1 ("eará") OR ("iežá"))(*-1 BOS LINK *1C ("lága")) ;
    ## Lága gáibida menddo olu eará lágaid ektui.
    ## Sii bidjet menddo stuora gáibádusaid min lágii eará lágaid ektui.

# láhka
# - - -
  # The point here is to disambiguate "láhka" from "lága".
	
SELECT ("láhka") IF (0 ("láhka"))(*1 ("fápmu") LINK 1 ("boahtin") BARRIER V-NOT-COP);

SELECT ("láhka") IF (0 ("láhka") LINK *1 LAHKA-NOUN BARRIER V-NOT-COP);
	## Norgga stáda lea iešguđet lágaid ja láhkaásahusaid bokte geatnegahtton.
	## Sosiálabálvalusaid lága § 4-3.
    	
SELECT ("láhka") IF (0 ("láhka"))(*-1 LAHKA-NOUN OR ("gč") BARRIER V-NOT-COP) ;
	## Gč. lága suoidnemánu b. 1992 nr. 97.	
    ## Fápmuiboahtin ja eará lágaid rievdadusat.
    ## Gonagas addá láhkaásahusa lága geavaheami birra.

SELECT ("láhka") IF (0 ("láhka"))(-1 ("rievdadit") OR ("jorgalit") LINK 0 Pass);
SELECT ("láhka") IF (0 ("láhka"))(*1 ("rievdadit") OR ("jorgalit") BARRIER V-NOT-AUX LINK 0 Pass);
	## Rievdaduvvon láhka....
	## Ođđa lágat galget jorgaluvvot sámegillii.

SELECT ("láhka") IF (*-1 MANNU LINK 1 Ord LINK 1 ABBR LINK 1 Num)(0 ("láhka"));
	## Dat lea dahkkon skábmamánu 28. b. 1898 lága mielde.
		
SELECT ("láhka") IF (0 ("láhka" Loc))(*-1 BOS LINK *1 LAHKA-VERB-PASS LINK 0 Pass) ;
	## Dat lea mearriduvvon lágas 33.
    ## Ii adnon norgga lágas daid attáldagaid birra maid dáččat mákse iežaset gonagassii.

SELECT ("láhka") IF (*1 ("mearridit") OR ("čuoldit") BARRIER VFIN)
	(0 ("láhka" Sg Gen))(*1 Po BARRIER V);
	## Dat lea mearriduvvon lága 3 mielde.
	
SELECT ("láhka") IF (-1 (@GN>) LINK 0 LAHKA-MOD)(0 ("lága"));
    ## Ráđđehus háliida lágidit dearvvašvuođa- ja sosiálabálvalusaid oktagaslaš dárbbuid mielde, earret eará pasientavuoigatvuođaid lágain ja dearvvašvuođabargiid lágain.
    
SELECT ("láhka") IF (0 ("láhka"))(1 LAHKA-POST)(*-1 BOS LINK *1 LAHKA-NOUN) ;
    # In addition to a LAHKA-POST, there is a noun which make it more likely to be "láhka".
    ## Sis lea leamaš geatnegasvuohta čállit journálaid maiddái ovddeš lágaid mielde.
    ## Lága mielde lei vejolaš oažžut albma eanangirjji mii dáhkidii   oamastanvuoigatvuođa gávpe- ja árbeoktavuođain ge.

SELECT ("láhka") IF (0 ("láhka"))(*-1 BOS LINK *1 LAHKA-NOUN-SUBJ LINK 0 (@SUBJ) LINK *1 LAHKA-VERB) ;
    ## Guovddášeiseválddit árvaledje lága vai sidjiide livčče vejolaš váikkuhit Finnmárkku čearddalašvuođadiliide.
    
SELECT ("láhka") IF (0 ("láhka"))(*-1 BOS LINK *1 LAHKA-VERB LINK *1 LAHKA-ASAHUS LINK 0 Ill OR Loc) ;
    ## Dáidá šaddat váttis ovddidit lága Stuoradiggái dál.    
    
SELECT ("láhka") IF (0 ("láhka"))(-1 ("čuovvut")) ;
    # If it´s "lága", then one should expect a Gen., like "sin lága, gávpoga lága".     
    ## Olbmot eai čuovo lága.    

SELECT ("láhka") IF (0 ("láhka" Gen))(1 LAHKAGEN-NOUN) ; 
    ## Dan oaidná lága ráhkkananbarggus.

SELECT ("láhka") IF (0 ("láhka"))(1 LAHKA-POST)(2 ("lohpi") OR LAHKA-NOUN) ; 
SELECT ("láhka") IF (-1 ("lohpi") OR LAHKA-NOUN)(0 ("láhka"))(1 LAHKA-POST) ;       
    ## Sis lei lága mielde lohpi vuosttaš vuovdimis prioriteret dážaid sámiid ja láddelaččaid ovddabeallái. 
    ## Sis lei lohpi lága mielde.       
    
SELECT ("láhka") IF (0 ("láhka"))(1 ("olis")) ;
    ## Ii mihkkege hehtte nammadeames ovddasvástideaddji maiddái dakkár doaimmain mat eai adno  
    ## dearvvašvuođaásahussan lága olis.   
    
SELECT ("láhka") IF (0 ("láhka" Acc))(*-1 BOS LINK *1 LAHKA-VERB) ;    
    ## Ja de bohciida jearaldahkan leat go sii geat dán lága čálle, bidjan sápmelaččaid sisriikalažžan.
    ## Stáhta galgá jorgalit lága sámegillii.
	
	
# láhka in coordination

SELECT ("láhka") IF (0 ("láhka"))(*1 LAHKA LINK 1 LAHKA-POST) ; 
    ## Mo lágaid gáibádusaid ja gáibádusaid daid lágaid olis sáhttá áimmahuššat.

SELECT ("láhka") IF (0 ("láhka"))(-1 ("eará") OR ("iežá"))(*-1 BOS LINK *1C LAHKA) ;
    ## Dan áiggi rájes go láhka fápmui biddjo, dahkkojit čuovvovaš njuolggadusat eará lágain.
    
SELECT ("láhka") IF (0 ("láhka"))(*-1C LAHKA) ; 
SELECT ("láhka") IF (0 ("láhka"))(*1C LAHKA) ; 
    ## Go teavsttas lea guhkes láhkanamahus, de sáhttá lága namahusa bidjat aisttonmearkkaid sisa.
    # This one might make trouble for "lága".   

SELECT ("láhka") IF (0 ("lága"))(1 CC OR COMMA LINK 1 ("konvenšuvdna") OR ("láhka#ásahit"));
    ## Gihpa gieđahallá maid vuoigatvuođaid, mat vuođđuduvvet lágaide ja konvenšuvnnaide.
    ## Sámi perspektiiva lea mielde dehálaš dearvvašvuođa- ja sosiálpolitihkalaš guorahallamiin 
    ## ja mearrádusdokumeanttain, nugo lágain, láhkaásahusain ja bagadusmateriálain.
    
SELECT ("láhka") IF (*-1 CC BARRIER NPNH LINK -1C LAHKA)(0 ("lága"));
    ## Máŋggabealatvuohta ja ovttadássásašvuohta čuovvula maiddái ođđa pasientavuoigatvuođalágaid ja lága dearvvašvuođabargiid birra.
           

# máŋga
# - - - -
REMOVE N IF (0 ("máŋga"))(NOT 0 Loc)(*1 N BARRIER NPNH);
	## Bargu ollašuhttit máŋga dáin doaibmabijuin lea juo álggahuvvon.
	
# mearri (idiomatic)
# - - - - - - - - - -

SELECT ("mearri" Gen) IF (-1 (Pron Dem Sg Gen))(1 A OR Adv);
    ## Lihkkus fuomášuvvui dát dan meare árrat ja ahte dálkkoduvvo árrat.

SELECT ("mearri" Gen) IF (-1 ("seammá") OR ("okta"))(1 A OR N);
    ## Sámi sisafárren álggi ovdalaš 1750. 1770:s ledje dan dihte gártan measta seammá meare sápmelaččat go dáččat dohko - 21 "vearrosápmelačča"154 ja 24 dáčča.

# oppalaš
# - - - -
SELECT Attr IF (0 ("oppalaš"))(*1 N BARRIER NPNH);
	## Sámi álbmoga bálvalusfálaldagaid dási oppalaš lokten eaktuda, ahte...

# vašálaš
# - - - - 
SELECT N IF (0 ("vašálaš"))(NOT *1 HNOUN BARRIER NPNH);
	## Mun bijan du vašálaččaid du julggiid vuložin.
	
# Early possessive rule
# ---------------------

REMOVE Px IF (0 TIME LINK 1 Num);
    ## Prošeakta lea ruhtaduvvon ja álggahuvvo jagis 2002.



# ====================== #
# Adjectives and adverbs #
# ====================== #

SELECT A IF (-2 BOS OR CS OR (":" CLB))(-1 ("mii" Interr Sg Gen));
	## Man guhkki lea girkui?

REMOVE VERB IF (-1 ("mii" Interr Sg Gen)); 
	## In dieđe man buorit dat leat.

REMOVE VERB IF (-1 ("mihá")); 
REMOVE VERB IF (-1 ("seamma"))(*1 go BARRIER NOT-NA LINK NOT 1 VFIN); 
REMOVE V IF (1 ("ihkinassii"));
	## Muhtun riggodagaid geavahit mihá unnit dál.
	## Bargiid guottut mearkkašit seamma ollu go fáktadieđut.
	## Mun áiggun čuovvut du ihkinassii manažat.

REMOVE N IF (-1 ("nu") OR ("mihá") OR ("hirbmat") OR ("hui"))(0 Adv OR A OR PrfPrc);
	## Mun lean nu suhttan. 
	## Lea hui easkka čuožžilan gažaldat.

SELECT Adv IF (-1 ("leat"))(1 PrfPrc);
SELECT Adv IF (-1 VERB OR Com)(0 ("ovttas"))(NOT 0 Foc); 
	## Divššohas lea duođalaččat roasmmohuvvan.
	## Bátnedoavttir galgá ovttas eará bargiiguin mearridit.

REMOVE Adv IF (0 ("nuorra"));
	## Nuorat dajai sutnje:...

SELECT (A Nom) IF (-1 ("vuoi"));
	## Vuoi surgat dan gándda!
	
REMOVE (A Comp) IF (-1 BOS OR CLB)(0 Adv)(1 EOS OR CLB);
    ## Ovttajienalaččat.
    
REMOVE (A Comp) IF (-1 V)(0 Adv)(1 (N Ess));
    ## Son lea válljejuvvon ovttajienalaččat ovddideaddjin.

# ======================================= #
# Disambiguating abbreviations           #
# ======================================= #

# Case disambiguation of abbreviations
# ------------------------------------

# Abbreviations are also disambiguated via normal case disambiguation rules, 
# but some contexts are special to abbreviations, and therefore dealt with
# separately here.
# Keep in mind: perhaps the placement of these rules is to early.

SELECT Nom IF (*-1 EOS LINK NOT *1 V)(0 (ABBR N))(1 Num);
    ## Sd.prp.nr. 63:s.
    
SELECT Nom IF (-1 Num OR NUMMAR)(0 INITIAL LINK NOT 1 NP-MEMBER);
    ## 98 b.
    
#SELECT Acc

#SELECT Gen


# ========================== #
# Disambiguating clitics     #
# ========================== #

REMOVE Qst IF (0 ("dego"));
	## Su ámadadju báittii dego beaivváš. 

REMOVE Qst IF (0 Adv)(NOT *1 ("?")); 
	## Nugo dieđát, de lean dál.


# ============================================ #
# Disambiguating symbols and punctuation marks #
# ============================================ #

SELECT (@ADVL) IF (-1 LEFT)(0 ("§") OR ("§§"))(*1 RIGHT BARRIER REALWORD);
	## (§§ 1521)


# ========================== #
# Disambiguating numerals    #
# ========================== #

SELECT Ord IF (NOT -1 N)(0 ACR)(*1 N BARRIER NOT-A);
    ## III kapihtal priváhta arkiivvaid birra máinnaša vuosttažettiin gáhttenárvosaš priváhta arkiivvaid.
    
SELECT Ord IF (-1 N LINK 0 Gen)(0 ACR)(*1 N BARRIER NOT-A);
    ## Arkiivalága III kapihtal priváhta arkiivvaid birra máinnaša vuosttažettiin gáhttenárvosaš priváhta arkiivvaid.
    ##  Arkiivalága III. kapihtal priváhta arkiivvaid birra máinnaša vuosttažettiin gáhttenárvosaš priváhta arkiivvaid.

    ## III. kapihtal priváhta arkiivvaid birra máinnaša vuosttažettiin gáhttenárvosaš priváhta arkiivvaid.
    
SELECT (A Ord) IF (-1 Prop LINK NOT 0 Plc);
	## Heinrich IV. lea boaris.
	## Heinrich njealját lea boaris.
    
SELECT Num IF (-1 COUNTABLE OR ("nr"))(0 ABBR OR ACR) ;
	## Dieđáhusa oasis I ovdanbuktojuvvo Bb:a sámepolitihkalaš vuođđolávdi.
	
SELECT Num IF (-1 (@HNOUN))(0 (Num Nom) OR (Num Attr));

REMOVE Range IF (0C Num)(NOT 1 VFIN)(-1 NUMMAR OR KLASS OR LAHKA);
  
SELECT Nom IF (-1 Prop) (0 ("guovttos"));
    ## Biera guovttos Márehiin leaba daid mánáid váhnemat.
    
REMOVE Px IF (-1 Prop) (0 ("guovttos"));
    ## Biera guovttos Márehiin leaba daid mánáid váhnemat. 

SELECT Num IF (0 (N ACR));
    ## Plána- ja huksenlaga II mielde. 

SELECT Nom IF (-1C Gen OR Loc LINK 0 MANNU)(0 CARDINALS);

SELECT Nom IF (-1C Gen LINK 0 MANNU)(0 CARDINALS)(*1 Po OR N BARRIER NOT-NP);
    ## Guovdageaidnu / Kautokeino, skábmamánu 2002 rádjai lei buorre.  


# ========================== #
# Disambiguating adpositions #
# ========================== #
        CONSTRAINTS #6       #
# ========================== #

# Rules relating to particular adpositions
# ----------------------------------------

# gaskan
# - - - -
SELECT Pr IF (0 ("gaskan"))(1 VGen);
	## Gaskan bora viehkalin olggos.

# miehtá
# - - - -
 # These rules are here because "miehtá" is also VFIN, and many of the Pr/Po
 # rules below include the condition (NOT VFIN). So "miehtá" goes wrong there.
	 
SELECT Pr IF (*-1 VFIN BARRIER CC OR CS OR CP OR CLB)(NOT -1 Gen)(0 ("miehtá"))
	(*1 Gen BARRIER NPNH);
	## Dalle bisttii luossaguolli miehtá dálvvi.

SELECT Pr IF (-1 Acc LINK *-1 V-TRANS-ACT BARRIER NPNHA)(0 ("miehtá"))
	(*1 Gen BARRIER NPNH);
	## Sii dieđihedje dan miehtá guovllu, ja sii bukte su lusa buot
	## buhcciid.

SELECT Pr IF (-1 Nom LINK *-1 COPULAS BARRIER NPNHA)(0 ("miehtá"))
	(*1 Gen BARRIER NPNH);
	## Searvegottiin lei dál ráfi miehtá Judea ja Galilea ja Samaria.

SELECT Pr IF (*-2 VFIN BARRIER CS)(-1 COMMA OR CC)(0 ("miehtá"))
	(*1 NP-HEAD-GEN BARRIER NP-BORDER LINK NOT 1 Po);
	##

SELECT VFIN IF (NOT *-1 VFIN BARRIER CS)(NOT -1 Gen LINK NOT 0 Nom)(0 ("miehtá"));
	## Jus divššohas dasa miehtá, dahje dilálašvuohta lea nu,...
	## Áldu mii dálvvi miehtá njamaha miesi gohčoduvvo čearpmateadni.
 
# vuostá
# - - - -

SELECT Adv IF (-1 ("váldit"))(0 ("vuostá"));
	## Sámi servodagas váilu árbevierru váldit vuostá almmolaš diehto-
	## juohkima dakkár hámis.


# Adpositions that take modifiers
# -------------------------------

SELECT Pr IF (-1 Gen)(0 ("bealde") OR ("beale") OR ("beallái") OR ("geahčai")
	OR ("geahčen") OR ("geaže"))(1 Gen)(NOT 1 TIME);
	## Daid sáhttá heaŋggastit dan heargái mii daid eará goahtemuoraid lea 
	## guoddimin, guokte bealji goappáge beallái hearggi. 


# The case of GASKAL
# ------------------

	
SELECT Pr IF (0 GASKAL)(1 (Pron Gen))(2 CC)(3 (Pron Gen));
	## Dat lei gaskal min ja din.

SELECT Po IF (-3 (Pron Gen))(-2 CC)(-1 (Pron Gen))(0 GASKAL);
	## Dat lei min ja din gaskkas.

SELECT Pr IF (0 GASKAL)(1 Num)(2 CC)(3 Num);
	## Gaskal 12 ja 16 jahkásaš mánáin lea muhtun muddui iešheanalis

SELECT Po IF (-3 Num)(-2 CC)(-1 Num)(0 GASKAL);
	## Alimusriekti veadjá leat jagiid 1854 ja 1897 gaskka doallan
	## siskkáldas čoahkkima.

SELECT Pr IF (0 GASKAL)(*1 NP-HEAD-GEN BARRIER VERB OR CS LINK 1 CC LINK 
	*1 NP-HEAD-GEN BARRIER VERB OR CS);
	## Ferte navdit ahte dáža duopmostuolut ja eará riektegeavaheaddjit 
	## dábálaččat dulkojit dáža siskkáldas rievtti nu ahte garvá vejolaš 
	## vuostálasvuođa gaskal dákkár álbmotriektenorpma ja siskkáldas 
	## norgga njuolggadusa.

SELECT Pr IF (0 GASKAL)(*1 (N Gen) BARRIER NPNH LINK *1 CC BARRIER NOT-NP 
	LINK *1 (N Gen) BARRIER NPNH LINK 0 HNOUN);
	## Lága mearrádusat galget váikkuhit luohttevašvuođa gaskal divššohasa
	## ja dearvvašvuođabálvalusa.
	## Dán láhkai lea leamaš muhtun muddui lotnolas doahttaluvvon dássádat-
	## dilálašvuohta gaskal stáhta almenneteaiggádin ja geavahanvuoigaduvvon 
	## giliássiid.

SELECT Pr IF (0 GASKAL)(1 Dem)(2 NUMERALS);
	## Ferte oaidnit oktavuođa gaskal dan guokte soahpamuša.

SELECT Pr IF (0 GASKAL)(1 A)(2 CC)(3 A LINK *1 (N Gen) LINK 0 HNOUN); # Barrier?
	## Galgá válljejuvvot gaskal vejolaš ja dohkálaš iskkadan- ja
	## dikšunvugiid.

SELECT Po IF (-1 (N Gen) LINK *-1 CC BARRIER NPNH LINK -1 (N Gen))(0 GASKAL);
	## Riiddut boazodolliid ja eará vuoigatvuođaoamásteddjiid gaskkas 
	## livčče lassánan. 

SELECT Po IF (-3 Inf)(-2 ("ja"))(-1 Inf)(0 GASKAL);
	## Sániid oamastit ja eaiggáduššat gaskkas geavahuvvo "ja".



# =============== #
  CONSTRAINTS #7  #
# =============== #

# Po or Pr?
# ---------

# Across parenthesis

SELECT Po IF (-1 (PUNCT RIGHT))(*-2 (PUNCT LEFT) LINK -1 Gen);
	## ...sáhttá dulkot telestudio dahje govvatelefovnna (ruoná
	## nummira) bokte.
	


# Requiring Genitive

REMOVE Po IF (NOT -1 (N Gen) OR (A Gen) OR (Num Gen) OR (Pron Gen) OR (Actio Gen) OR (Num Nom))(NOT -2 MANNU LINK 1 Ord);
REMOVE Po IF (-1 N)(NOT -1 Gen); 
#REMOVE Po IF (0 ("mielde"))(-1 Acc)(-2 TV);
REMOVE Pr IF (NOT *1 N OR A OR Num OR Pron OR Date BARRIER NPNH);
	## Jus divššohas lea badjel 16 jagi boaris,... 
	## Leansmánni lea kommišuvnnas fárus.
	## Na mun gal hupmen dan birra.
	## Kapihttala 11 mielde.

SELECT Pr IF (NOT -1 Gen OR (Pron Interr Nom))(NOT 0 A OR VFIN OR Adv)
	(*1 (N Gen) BARRIER NPNH LINK 0 HNOUN);
	## Jus divššohas lea vuollil 16 jagi boaris,...

SELECT Pr IF (-1 ("dušše"))(*1 NP-HEAD-GEN BARRIER NPNH);
	## Dat lea dušše badjelaš állanbeali guhku.

SELECT Pr IF (NOT -1 Gen)(1 (Pron Gen));
	## ...muhto ovdal dan áiggi ledje "sápmelaččat boahtán dán guvlui". 

REMOVE Pr IF (NOT 1 (Num Gen) OR Date)(NOT *1 (N Gen) BARRIER NPNH);
	## Olbmuin bisttii luossaguolli miehtá dálvvi. 
	## Ovdal dan aiggi ledje sápmelaččat boahtán dán guvlui.
	## Gonagas addá láhkaásahusa lága geavaheami birra Svalbarddas.

SELECT Po IF (*-1 BOS OR CS OR (":" CLB) OR CLB BARRIER NPNH)(-1 Gen)(NOT 0 ("guovddáš") OR ("mielde"));
SELECT Po IF (-1 Gen)(1 CC)(NOT -1 Acc LINK *-1 CC BARRIER NOT-NP LINK -1 Acc LINK
	-1 V-TRANS-ACT BARRIER NPNHA);
SELECT Po IF (-1 Gen)(NOT -1 VERB OR Adv)(1 TIME); 
	## Dán vuostá sáhttá dadjat, ahte ...
	## Jos sáddo deavdá buođu vuollái dahje jos sáddo hávddarda buođu vuolde... 
	## Sámi guovlluin ožžo Sámeparlameantta Suoma beallái jagi 1976.

SELECT Po IF (-1 Gen)(NOT -1 ("leapma" Dimin))(1 Gen LINK *1 Po BARRIER NP-BORDER);
	## Dattetge ii leat muitaluvvon makkárge namahan veara geavahanriiddu birra 
	## dálolaččaid ja boazodoalu gaskkas.

SELECT Po IF (-1 (Interr Gen));
	## Vuoi dan olbmo gean bokte fillehusat bohtet.

SELECT Pr IF (NOT -1 Gen LINK *-1 BOS BARRIER (V V-TRANS-ACT) OR Pr) (NOT -1 TIME)
        (NOT 0 Attr OR VFIN)(NOT 1 VFIN)(*1 (N Gen) OR (Pron Gen) BARRIER PUNCT 
	LINK NOT 0 Cmpnd LINK NOT *1 N OR Po BARRIER CC OR CP OR CS OR CLB)(NOT -1C (@GP>));
	## Sáhttá dahttege vurket journála olggobealde doaimma. 

#SELECT Po IF (-1 (@GP>)); # more careful with -1C than with -1.
SELECT Po IF (-1C (@GP>));

SELECT Pr IF (1 (Num Gen));
	## Vuollel 12 jahkásaš mánáin ii leat iešheanalis geahččanvuoigatvuohta.

REMOVE Pr IF (NOT 1 Gen OR Date);
	## Geavatlaš sivaid geažil čatnasa bajimus submi namuhuvvon máksomearrái.
	
SELECT Pr IF (0 TIME-PREP)(1 Date OR TIME)(NOT 2 Po);
    ## DBE dáhttu oažžut dieđu suohkanis ovdal 01.11.99. 

SELECT Adv IF (-2 (Num Acc))(-1 (N Acc))(0 ("maŋŋil"));
	## Dat dáhpáhuvai golbma jagi maŋŋil.

SELECT Po IF (-1 Gen)(NOT -1 VFIN OR ("leat" PrfPrc) OR Adv)(NOT -1 Acc LINK *-1 CC 
	BARRIER NOT-NP LINK -1 Acc LINK *-1 V-TRANS-ACT BARRIER NPNHA)
	(NOT 0 Attr OR ("mielde") OR ("guovddáš") OR CS)(NOT 1C Po)(NOT 1 Gen);
	## Daid ovddas gal in máksán maidege.

SELECT Po IF (-1 Num)(0 ("rádjai")); # Make general rule!
REMOVE Po IF (NOT -1 Gen OR Num);
	## ... paragráfaide 3-18 rájes 3-21 rádjái.
	## Jus divššohas dasa miehtá,... 


# Removing some readings that compete with Po
# - - - - - - - - - - - - - - - - - - - - - -
REMOVE N IF (-1 Gen)(0 Po)(NOT 0 ("guovddáš"))(NOT 1C Po);
REMOVE V IF (-1 Gen)(0 Po)(NOT 1C Po);
	## ...maid ektui sáhtii leat buori oskkus.
	## Gávpot mii lea vári alde, ii sáhte leat čihkosis.


 
# =========================== #
# Disambiguating subjunctions #
# =========================== #
        CONSTRAINTS #8        #
# =========================== #

 # Subjunctions are disambiguated in two subcycles. This is the first.
 
# amas
# - - -
# A, or Neg Sup, or CS (if followed by (nominative) subject and (infinitival) verb).

SELECT A IF (0 ("amas"))(1 CC)(2 A);
SELECT A IF (*-1 ("leat") BARRIER NOT-ADV OR CLB)(0 ("amas"));
	## Máŋgasat leat sakka fuolastuvvan amas ja dovddus vahágiin.
	## Ollu sápmelaččaide lea dál ain amas jurdda.

SELECT A IF (0 ("amas"))(1 N)(NOT *1 Nom BARRIER NOT-NP);
	## Eai dovdda amas olbmo jiena.

SELECT (Neg Sup) IF (0 ("amas"))(1 Inf);
	## Bávlos lei nappo mearidan gárvit Efesosa, amas ájihuvvot Asia eanan-
	## gottis. 

SELECT CS IF (0 ("amas"))(1 Neg);
	## Prinsihpa maid lean čuovvon lea juohkehačča ektui fuolahan, amas ii
	## vuovdi vahágahtto.

SELECT CS IF (0 ("amas"))(*1 Nom BARRIER NPNHA LINK *1 Inf);
	## Mánás leat juolggit biddjojuvvon man nu vuostái nođiid ala, amas juolggit
	## čerggiidit. 

# amat
# - - -
SELECT (Neg Sup) IF (0 ("amat"))(1 Inf); # Also Pcle?
	## Sii guddet du gieđaideaset alde, amat norddastit juolggát geađgái.

# daningo
# - - - -
# dasságo
# - - - -
 # These two are taken care of by the rule in the cycle 'Disambiguating clitics'. 

# dasgo
# - - -
SELECT CS IF (0 ("dasgo"))(NOT *1 ("?"));
SELECT CS IF (0 ("dasgo"))(1 Interr);
	## Dasgo Herodes lea ohcamin máná goddin dihtii dan.
	## Dasgo goabbá lea stuorit,...

# de
# - -
SELECT CS IF (*-1 ("jos") OR ("go") LINK -1 BOS OR CS OR (":" CLB))(-1 COMMA)(0 ("de"));
	## Jos dieđášin, de dajašin.
	## Go olbmot jaskkodit, de sáhpánat álget jursit.
	## Son diehtá, ahte jos galgá leat mielde, de ferte máksit.

# go
# - -

# Yes/no ( = fi. -ko/-kö, no. V1))

SELECT Pcle IF (-3 BOS OR CLB)(-2 CC)(-1 V)(0 go);
SELECT Pcle IF (-2 BOS OR REALCLB OR PUNCT)(0 go)(*1 ("?"));
	## Muhto boađát go?                                                  
	## Eai go báhkinat ge daga aiddo nu?

SELECT Pcle IF (-2 ("vai"))(0 go);
	## ...vai geahččaluvvo go gávdnat čovdosiid bissovaš rievttis,... 

SELECT Pcle IF (-2 VERB)(-1C VFIN)(0 go);
SELECT Pcle IF (*-2 VFIN BARRIER CC OR CP OR CS LINK NOT 0 Inf)(-1C VFIN)(0 go)
	(NOT 1 VFIN); 
	## Árvvoštaladettiin galgá go addit lobi lea dattege gažaldat das,...  
	## De oaidná olmmoš dárbbaša go oalgebáddi luoitiluvvot. 

SELECT Pcle IF (-1 (V Neg) OR MOD-ASP)(0 go);                
	## Son jerre it go boađe?
	## Sámedikki eará orgánat mearridit ieža galget go čoahkkimat leat
	## rabas vai gitta.

SELECT Pcle IF (*-3 ("go" Pcle) BARRIER VFIN OR CS OR CP LINK -1 VFIN)
	(-2 CC)(-1 VFIN)(0 go);
	## Addet go máŋga dearvvašvuođabargiidjoavkku veahki ja 
	## fállojuvvo go divššohasaide birrajándororrun.

SELECT Pcle IF (-2 Inf)(-1 VFIN)(NOT -1 PrfPrc)(0 go);
	## Farisealaččat atne Jesusa čalmmis oaidnit áigu go buoridit sábbáhin.

SELECT Pcle IF (-3 N OR Pron)(-2 ("ahte"))(-1 VFIN)(0 ("go"));
	## Politihkas lei 1866 rájes sáhka ahte galggai go stivrregoahtit 
        ## gii oažžu eatnama láigohit stáhtas.

SELECT Pcle IF (-1 N LINK *-1 BOS BARRIER NOT-NP)(NOT -1 ("hávvi"))(0 go)
	(*1 VFIN BARRIER CS OR CP)(*1 ("?"));
	## Eatni goarrun gákti go dus lea die?

SELECT Pcle IF (-2 (N Gen) LINK *-1 BOS BARRIER NOT-NP)(-1 Po)(0 go)
	(*1 VFIN BARRIER CS OR CP);
	## Girdi mielde go don manat Helssegii?
	

# Something else ( = fi. kuin, no. (så) som) (are all these CS?)
# Testing: 
SELECT CS IF (*-1 A OR Adv LINK *-1 ("nu") OR ("mihá") BARRIER CS)(0 go);
SELECT CS IF (*-1 ("seammá"))(NOT -1 VFIN LINK NOT *-1 VFIN BARRIER CS)(0 go);  
	## Nu guhkás go Gonagas mearrida... 
	## Giđđabuođut ráhkaduvvojit seammá vuogi mielde go geassebuođut. 
	## Seammá gusto go... # NOT -1 VFIN!

SELECT CS IF (*-1 COMPAR BARRIER NOT-ADJ OR CLB)(0 go); 
SELECT CS IF (*-1 Comp BARRIER CS OR CP OR CONTRA OR PUNCT LINK NOT 0 VERB)(0 go)
	(NOT *1 VFIN BARRIER CS OR CP);
	## Dát dáhpáhuvva dávjjit go dat, maid ovdal namuhin.
	## Láhkaásahus addá máŋgga áššis dárkilit njuolggadusaid go láhka.

SELECT CS IF (-1 Comp LINK *-1 COPULAS BARRIER V-NOT-AUX OR CS)
	(NOT *-1 V-PL1 OR V-PL2 OR V-PL3 BARRIER V-NOT-AUX)(0 go);
	## Dasgo mun in leat álggage heajut go dat mahkáš alla apoastalat.

SELECT CS IF (*-2 Comp BARRIER NPNH LINK NOT 0 Inf)(-1 A OR N)(0 go);
	## Dii han lehpet eanet veara go ollu cizážat.

SELECT CS IF (-1 A OR Adv)(NOT -1 Po)(0 go)(*1 Interr BARRIER VFIN);
	## Joavku ii válddahala dát gažaldagaid dobbelii go maid Sámi
	## vuoigatvuođalávdegoddi lea dahkan.

SELECT CS IF (0 go)(*1 CONTRA OR EOS BARRIER VFIN); 
SELECT CS IF (0 go)(1 ("ahte"));
	## Mus ii leat go okta beana.
	## Lea váttis áddet earáláhkai, go ahte maiddái duopmostuollu rahčá
	## čađat dainna váttisvuođain. 


# vai
# - -
# This one may be CS (vai = 'in order to') or CC (vai = 'or').
SELECT CC IF (0 ("vai"))(1 Neg)(2 CLB);
SELECT CC IF (0 ("vai"))(1 Qst);
	## Leago sámeálbmogis vuoigatvuođat vai eai, dahje...
	## ... vai leago ..

SELECT CC IF (0 ("vai"))(2 ("go"));
	## Dáhpáhuvvá go dat ođđa lágaid bokte, vai geahččaluvvo go gávdnat
	## čovdosiid bissovaš rievttis? 

SELECT CC IF (0 ("vai"))(NOT *1 VFIN BARRIER CS); 
	## ...čállogo journála elektrovnnalaččat vai báhpárii.

SELECT CC IF (*-1 Inf BARRIER VERB OR CLB)(0 ("vai"))(*1 Inf BARRIER VERB);
	## Buriid dahkat vai baháid dahkat.

# vaikko
# - - - -
SELECT Pron IF (0 ("vaikko"))
               (1 ("mii") OR ("gii") OR ("guhte") OR ("goabbá") OR
                  ("guhtemuš") OR ("makkár"));
  # "vaikko mii", etc. is a pronoun. Other vaikko cases are CS.
  # This should probably rather be taken care of in the preprocessor.

#SELECT CS IF (-1 BOS OR CS OR (":" CLB))(0 ("vaikko"))(*1 ("de"));
 # See rule in the next cycle.



 # Subjunctions are disambiguated in two subcycles. This is the second.

# ============== #
  CONSTRAINTS #9 # 
# ============== #

# CS selection for specific CS candidates
# ---------------------------------------
SELECT CS IF (0 go);       # All other go's are CS. (differ from Pcle sect 2?)
SELECT CS IF (0 ("vaikko"));
SELECT CS IF (0 ("vai"));
	## Na geasset go bođii diehtiht ahte ii beasa skuvlii.
	## Ja go beasai fas seammá bargui.
	## Ásahusa journálaovddasvástideaddji sáhttá liikká rievdadit journála
	## vaikko gustojit vuolláičállojuvvon journálamerkestemiide. 
	## Vai miehtan galgá leat lágalaš, ferte divššohas leat ožžon...

# CS removal for specific CS candidates
# -------------------------------------
REMOVE CS IF (0 ("de"));
	## De leat máksán.

# Some particular subjunctions
# ----------------------------

SELECT CS IF (-1 COMMA)(0 ("de") OR ("vaikko"))(NOT 1 CS);
        ## Jos geahččá dearvvašvuođarievtti geahččanguovllus, de ii gáibádus
        ## áddehantti dieđuid oažžumis čatnas giellalága ráddjehusaide.

REMOVE CS IF (NOT *1 VFIN OR ("leapma" Dimin)); # The latter is "leamaš".
	## Sii manne meahccái, vaikko lei issoras buolaš.
	## Eai lean váldán go niestebohcco.



# ====================== #
# Disambiguating adverbs #
# ====================== #
        CONSTRAINTS #10  #
# ====================== #


# General adverb rules
# --------------------

SELECT Adv IF (-2 VFIN)(-1 ("nu"))(1 COMMA)(2 ("ahte"));
	## Lohku lassánii nu johtilit, ahte ...

SELECT Adv IF (*-1C VFIN-NOT-AUX BARRIER NOT-ADV OR PUNCT)(0 A-ATTR)
	(NOT 0 Inf OR N)(NOT 0 ("eanaš") OR ("eanas"))(NOT 1 N)
	(NOT 1 CC LINK 1 (A Attr) LINK *1 N BARRIER NOT-ADJ);     
	## Mannet ja guorahallet dárkilit dán máná hárrái!
 # Adv if postverbally

SELECT Adv IF (-2 VERB)(NOT -2 COPULAS)(-1 ("hui") OR ("nu"))
	(NOT *1 N BARRIER NP-BORDER);
	## Son čállá hui čábbát.

SELECT Adv IF (*-1 (V Sg1) OR (V Sg3) BARRIER COPULAS OR Nom OR CS OR CP OR PUNCT 
	OR REALCLB OR CONTRA)(0 (A Pl Nom))(NOT 0 Inf)(NOT 1 N)
	(NOT *-1 COPULAS BARRIER NOT-ADV-PCLE LINK *-1 Nom BARRIER CS);
	## Dál ii leat oktage Romssa universitehta guovddášhálddahusas gii
	## hálddaša sámegiela njálmmálaččat ja čálálaččat.
	## Jus lea geavatlaččat vejolaš, galget divššohasat...
 # The last condition covers cases where 0 is a comparative adjective.

SELECT Adv IF (*-1 COPULAS BARRIER NOT-ADV-PCLE)(NOT *-1 V-PL3 BARRIER CS)
	(NOT *-1 ("leapma" Dimin) LINK NOT *-1 VFIN BARRIER CS)	
	(0 (A Pl Nom))(NOT 0 Inf)(1 A);
	## Dát mearrádus lei formálalaččat gustojeaddjin gitta dassáigo...

REMOVE Nom IF (-1 (Pron Interr Nom))(0 Adv);
	## Iige leat duopmu mii čielgasit ja njuolga mearrida...

REMOVE (Pl Nom) IF (*-1 V-SG3 BARRIER VFIN OR CP OR CS OR CONTRA OR REALCLB)
	(0 Adv); 
REMOVE (Pl Nom) IF (0 Adv)(*1C V-SG3 BARRIER NOT-ADV-PCLE OR CLB);
	## ...gos mánná ássá dahje gaskaboddasaččat orru. 
	## Son dábálaččat ráhkkana dasa dálvvi mielde. 

REMOVE Gen IF (0 Adv)(NOT 0 TIME)(NOT *-1 Pr BARRIER NPNH)
	(NOT *1 Po BARRIER NP-BORDER)(NOT *1 N BARRIER NP-BORDER);  
	## Dát dagut leat rasistalaččat.
  
#REMOVE Adv IF (-1 (N Gen) OR (Pron Gen))(NOT -1 Adv OR VFIN OR ("leat" PrfPrc))
#	(0 Po);
 # Po if after Gen
 # but no hits in corpus?

#SELECT Adv IF (0 ("mielde"))(*-1 ("leat") OR ("dáidit") BARRIER VFIN OR CS OR CP LINK -1 (Pron Loc))(NOT 1 (N Nom));
SELECT Adv IF (0 ("mielde"))(*-1 ("leat") OR ("dáidit") BARRIER VFIN OR CS OR CP LINK -1 (Pron Loc))(NOT -2 (N Nom))(NOT 1 (N Nom));
    ## Mus lea dorski mielde.
    ## Sudnos ledje evttohusat mat sudno gáttu mielde geahpidivčče riidduid.
    ## Sis lei lága mielde lohpi.
    ## Sis lei lohpi lága mielde.

SELECT Adv IF (0 ("mielde"))(*-1 ("go" Pcle) LINK -1 ("leat") BARRIER VFIN);
SELECT Adv IF (0 ("mielde"))(*-1 ("leat" Qst) BARRIER VFIN);
    ## Leago dorski mielde?
    ## Lea go dorski mielde?
    
REMOVE Adv IF (-1 (N Gen) OR (Pron Gen))(NOT -1 Adv OR VFIN OR ("leat" PrfPrc))
	(0 Po)(NOT 0 ("mielde") LINK *-1 MIELDE-VERB BARRIER VFIN);
    ## Válddán girjji mielde.
    ## Válddán dorski mielde.
    
REMOVE Adv IF (-1 (Num Nom @NumN<))(-2 (N Gen) OR (Pron Gen))(NOT -2 Adv OR VFIN OR ("leat" PrfPrc))
	(0 Po)(NOT 0 ("mielde") LINK *-1 MIELDE-VERB BARRIER VFIN); 
	## Sii sáhttet buktit hálddahuslaš cealkámuša lága kapihttala 11 mielde.    
 
REMOVE Adv IF (-1 ("leat"))(0 (A Attr))(NOT 0 ("eanaš") OR ("eanas"))
	(NOT 0 (@ADV-A))(1 N)(NOT 2 VERB);  
	## Bussás leat alit čalmmit.

SELECT Adv IF (0 (Pron Indef))(1 CS);
	## Dat mearridit seammás ahte stáhtaeiseválddit galget ovddidit 
	## doaibmabijuid. 

#REMOVE Indef IF (-1 BOS OR CC OR ("go"))(0 (Pron Indef));
#REMOVE Indef IF (-1 BOS OR CC OR ("go"))(0 (Pron Indef Loc));
#SELECT Adv IF (-1 BOS OR CC OR ("go") OR (PUNCT RIGHT))(0 (Pron Indef));
SELECT Adv IF (-1 BOS OR CC OR ("go") OR (PUNCT RIGHT))(0 ("seammás") OR ("seammas"));
	## Dat mearridit seammás ahte stáhtaeiseválddit galget ovddidit 
	## doaibmabijuid. 
	# # There could be problems with PUNCT RIGHT.
	
SELECT (Indef Loc) IF (*-1 LOC-VERB OR ("orrut") BARRIER NPNH)(0 (Pron Indef)) ;	
    ## Moai orro seammás.

SELECT Adv IF (NOT -1 CC LINK -1 Indef)(0 (Pron Indef))(1 COMMA)(2 CS);
	## Muhto geavadis ráddjejuvvui vuoigatvuohta veaháš, go ráfáiduhttima
	## spiehkastat mearralottiid ektui gáržžui.

REMOVE Po IF (-1C NOT-NP)(0 Adv)(1 NOT-NP OR CLB);  
	## Mun in leat mielde.

REMOVE (N Gen) IF (-1 VFIN)(0 Adv)(NOT 1 N or Po); 
	## Diet lea beare unnán.

REMOVE (Pron Interr) IF (NOT *-1 Neg BARRIER VFIN)(0 ("maid"))(1 CLB);
	## Dá lea reivve maid.


# Adverbs, not interjections
# --------------------------

REMOVE Interj IF (NOT -1 Interj OR BOS)(0 Adv);      ## Dat dinii bures doppe.


# Adjectives, not adverbs
# -----------------------

REMOVE Adv IF (NOT *-2 V-NOT-AUX BARRIER NOT-ADV-PCLE)(-1 (Pron Indef))
	(0 (A Pl Nom));
	## Ehpet go dii leat ollu mávssolaččabut go dat?

SELECT (A Pl Nom) IF (*-1 BOS OR REALCLB)(1 COPULAS LINK *1 (Pers Nom) BARRIER
	NOT-ADV-PCLE LINK NOT *1 Nom BARRIER CS);
	## Ávdugasat lehpet dii go sii bilkidit ja doarrádallet din.


# Adverbs, not comparative or plural adjectives
# ---------------------------------------------

SELECT Adv IF (-1 BOS OR CS OR (":" CLB))(0 (A Nom))(NOT *1 (N Loc) BARRIER NPNH)
	(*1 VFIN BARRIER CS OR CP LINK *1C NP-HEAD-NOM BARRIER CLB OR CS OR CP)
	(*1 V-NOT-COP BARRIER CS OR CP LINK *1C NP-HEAD-NOM 
	BARRIER CLB OR CS OR CP OR go);
	## Geavatlaččat ja ruđalaččat sáhttá dát garrasit čuohcat.
  # The non-copula verb does not have to be finite, but there must be a finite verb.

REMOVE Comp IF (-1 ("hui") OR ("nu") OR ("seammá"))(0 Adv);
	## Dat lea hui čielgasit.

REMOVE Comp IF (-1 VFIN)(0 Adv)(NOT 0 PxPl3)(1 Inf);
	## Ráđđehus áigu erenomázit giddet fuopmášumi sámegillii.

REMOVE (A Comp) IF (-1 N OR Pron)(1 VFIN)(NOT 1 N);
	## ...ja ahte dat dáhttu čielgasit berre doahttaluvvot.
 
SELECT Adv IF (*-1 NP-HEAD-NOM OR (A Nom) BARRIER NOT-ADV-PCLE)
	(NOT 0 (Pron Interr))(1 PrfPrc)(NOT 1 Actio LINK 1 ("veara"));
	## ...leatgo sámit historjjálaččat adnon eamiálbmogin...

SELECT Adv IF (-2C Adv)(-1 CC)(1 VERB);
	## ...son gii eanemus ja bistevaččat doallá oktavuođa divššohasain.

SELECT Adv IF (0 (A Pl Nom))(*1C V-1-2 BARRIER NOT-ADV-PCLE);
	## Persovnnalaččat in rávveše geange risikeret vel nuppes kr. 255.307. 

SELECT Adv IF (*-1 ("leat") BARRIER NOT-ADV-PCLE)(0 (A Pl Nom))
	(*1 PrfPrc OR (Actio Ess) BARRIER VERB OR CC OR CLB OR CS OR CP);
	## ...dallego sii ledje lobálaččat bohccuiguin orrumin boazogoahtunorohagas 
	## gosa gulle.

REMOVE Attr IF (1 CC)(NOT 2 Adv OR PRE-NP-HEAD);
	## ...čállogo journála elektrovnnalaččat vai báhpárii.

REMOVE (A Nom) IF (0 Adv)(1 (A Nom) LINK NOT *1 N BARRIER NPNH);
	## Geainnut ledje maid guhkit rabas.

SELECT Adv IF (*-1 VFIN BARRIER CP OR CS)(-1 (@SPRED))(0 A)(1 EOS);
    ## Seammás oinniimet ahte dát njeallje biirre šadde doarvái bieđggus geográfalaččat.


# Adverbs, not verbs
# ------------------

REMOVE (V Ind) IF (-1 Nom LINK *-1 (V Ind) BARRIER NPNHA)(0 ADV-NOT-VERB);
	## Oar-julggiid atnet olbmot dušše dalle go bodni lea nu garas ahte...

SELECT Adv IF (-1 (Pron Interr))(1 CC)(2C Adv)(NOT *3 A BARRIER NOT-ADV);
	## ... dan dearvvašvuođabálvalussii, mii ollásit dahje osohahkii... 

SELECT Adv IF (-1 VERB)(1 CC)(2 ("seammá"))(3C Adv)(NOT *4 A BARRIER NOT-ADV);
	## Njuolggadusat fertejit gustot ollásit ja seammá láhkai...

REMOVE VERB IF (*-1 INF-VERB BARRIER CS OR CP)(0 Adv)(1 Inf);
	## ...go duohta dilálašvuođaid ii sáhte ollásit čilget.

REMOVE VGen IF (*-1 ("leat") BARRIER NOT-ADV-PCLE)(0 Adv)(*1 N BARRIER VERB);
	## Lea čađat dárbu ođasmahttit jorgalusaid.


# Verbs, not adverbs
# ------------------
REMOVE Adv IF (*-1 ILL-VERB)(0 Inf)(NOT *-1 BOS LINK *1 Ill)(NOT 1 CS);
    ## Sii liikojit čuožžut synagogain ja gáhtačiegain rohkadallamin,
    ## vai olbmot fuomášit sin.
    ## Sii liikojit fuobmat, ahte ganddat ...

REMOVE Adv IF (NOT *-1 Acc) (-1 INF-VERB)(0 Inf);
    ## Gonagas sáhttá addit láhkaásahusaid dan birra.
    
#REMOVE Adv IF (0 ("addit"))(1 N);
	## Gonagas sáhttá addit láhkaásahusaid dan birra.

REMOVE Adv IF (-1C N)(0 ("addit"))(NOT *1 VERB BARRIER NOT-ADV-PCLE); 
	## Divššohasbearráigehčči galgá govttolašvuođain addit 
	## sutnje dieđuid gii jearrá.

SELECT V IF (0 ("addit"))(NOT 0 Actio)(*1 Acc OR Ill BARRIER NPNHA);
	## Son gohčui addit niidii dan.
 

# Adverbs, not pronouns
# ---------------------
SELECT Adv IF (0 (Pron Indef Nom))(NOT 0 Attr LINK *1 N BARRIER NPNH)(NOT 0 Num)
	(*1 (N Nom) BARRIER CC OR CS OR CP OR CLB LINK NOT 0 Inf);
	## ...go Sámedikki áššemeannudeamis unnán buvttaduvvojit čállosat 
	## sámegillii.
	## Muhto veajan gal eanaš biddjojuvvojit dakkár tiŋggat mat... 

SELECT Adv IF (0 (Pron Dem Sg Loc))(NOT *1 N BARRIER NPNH);
	## Bođus das lea dávjá ahte dat ii iskojuvvo dárkileappot.


# Adverbs, not nouns
# ------------------

SELECT Adv IF (*-1 VFIN)(0 GEASSET)(NOT 1 Acc);  ## Son lei doppe geasset.
SELECT Adv IF (0 GEASSET)(*1 VFIN);              ## Dálvet son vulggii meahccái.

SELECT Adv IF (*-1 VFIN BARRIER NOT-ADV)(0 ADV-NOT-OTHER-POS)(NOT 0 Num);
	## Lea dušše okta Ipmil.
        ## Ja Biret-Elle lea easka skuvllas geargan.
       # Move to cycle 1a?

SELECT Adv IF (-1 CS)(0 ADV-NOT-OTHER-POS)(1 Nom LINK 1 VFIN OR Interr);
	## Lahka mearridii, ahte dušše sii guđet máhte...
 # A bit half-hearted the previous LINK).

REMOVE Interj IF (-1 BOS)(0 ADV-NOT-NAMES);             ## De fidnejin.
	## No hits in corpus.

SELECT Adv IF (-1 (N Nom))(0 ADV-NOT-NOUN);
SELECT Adv IF (-1 CC)(0 ADV-NOT-NOUN)(NOT *1 VFIN); 
	## Dáin áššiin sáhttet Sámedikkit várra nu gohčoduvvon friija
	## árvvoštallama mielde mearridit. 
	## Dás lea unnimus 150 jagis sáhka ja várra 200 jagis.

SELECT Adv IF (NOT 0 ("okta" Ess))(0 Ess)(NOT 0 ("mii" Foc));
	## Eatnagat bohtet nuortan ja oarjin.
	## ...amadet gaikut nisuid bajás oktan árvvohis rásiiguin.
 # This rule also hits "ihttin", "guhkkin", "muhtumin", "iehčanassii",
 # It is possible that some conditions will have to be added.  


# Nouns, not adverbs
# ------------------

Remove Adv IF (-1C Gen)(NOT -1 Interr)(0 (N Loc)) ;   
	## Son bođii njukčamánu loahpas.
   

# Rules related to specific adverbs
# ---------------------------------

# álggage
# - - - -
SELECT Adv IF (*-1 Nom BARRIER CS)(0 ("álggage"));
SELECT Adv IF (0 ("álggage"))(*1 Nom BARRIER CS OR CP);
	## In mun leat álggage heajut go dát.
	## Sis, geain eai lean álggage njuovvanbohccot,...

# álgu
# - - -
SELECT N IF (-1 Gen OR (A Attr))(0 ("álgu"));  
	## Prošeavtta álgu ii lean álki.

# beare
# - - - 
SELECT Adv IF (0 ("beare"))(1 ("ollu"));  
	## Orohagas leat beare olu bohccot.

# beallái
# - - - -
 # The Po/Pr seems to be taken care of by the general rules in an earlier block.
 # But rules are needed that distinguish between N and Adv readings.

# čađat
# - - -
SELECT Adv IF (-1 VFIN)(NOT -1 Pl3 OR Neg)(0 ("čađat"));
SELECT Adv IF (0 ("čađat"))(1 VFIN)(NOT 1 Pl3); 
	## Lea čađat dárbu ođasmahttit jorgalusaid.
	## Son muitala ahte dii čađat jurddašehpet min iluin.

# dadjat
# - - - -
SELECT Adv IF (-1 COMMA)(0 ("dadjat"));
	## Muhto joddobuođuiguin bivdojuvvo luossa miehtá deanu, dadjat
	## njálmmi rájes gitta gierragii.

SELECT Adv IF (0 ("dadjat"))(1 ("juo"));
	## Háhkan lea šaddan dadjat juo cieggan riektedilli.

# dainna
# - - - -
SELECT Adv IF (0 ("dainna"))(1 go);
	## ...muhto sáhttá vuordit dainna go ferte vuoruhit dikšuma. 
 
SELECT Adv IF (0 ("dainna"))(*1 CS BARRIER NOTCOMMA);
	## Kvaliteahtta sihkkarasto dainna, ahte bargit dovdet kultuvrra.

# danin
# - - - 


SELECT Adv IF (0 ("danin"))(NOT *1 (N Ess) BARRIER NPNH)
		(NOT *-1 COPULAS BARRIER NOT-ADV)(NOT 1 CLB)
		(NOT *-1 OPRED-VERB BARRIER NOT-ADV)
		(NOT *1 OPRED-VERB BARRIER NOT-ADV);
        ## Danin badjánii jurdda lága ráhkadit.
	## Návetruitu lávejit danin atnit. # Essive - hence the restrictions.

SELECT Ess IF (*-1 COPULAS BARRIER NOT-ADV)(0 ("danin"))(1 CLB); 
	## ...jos fierpmit leat danin,...

SELECT Ess IF (0 ("danin"))(*1 OPRED-VERB BARRIER NOT-ADV-PCLE);
SELECT Ess IF (0 ("danin"))(*-1 OPRED-VERB BARRIER NOT-ADV-PCLE);   
	## Návetruitu lávejit danin atnit. 
	##

SELECT Adv IF (0 ("danin")); 
	## Sierralágan diehtojuohkingeahččaleamit leage danin dehálaččat.

# If there are more essives, give rules for them.

# dassá
# - - -
REMOVE VFIN IF (0 ("dassá"))(1 go)(*2 VFIN BARRIER CS);
	## Dassá go son jámii, lea guhkes áigi.

REMOVE VFIN IF (-1 TIME)(0 ("dassá"));
	## Vuosttažettiin adnui sámi kultuvra árvvoheabbon go dáža kultuvra 
	## 100 jagi dassá.

# dákko
# - - - 
REMOVE (V Imprt Prs Sg2) IF (0 ("dákko"))(*1 VFIN);
REMOVE (V Imprt Prs Sg2) IF (*-1 VFIN)(0 ("dákko"));
	## ... oaiviliid ja ákkaid bokte mat dákko ovddiduvvojit,...
	## Hammerfeastta suohkanbáhppá Harboe lea dákko maid, nu go 
	## eará oktavuođain, buorre gáldu.
  # Alternatively, on could remove all imperatives. Here, I remove the
  # imperative reading of "dággut" if there is a finite verb somewhere
  # else.
 
# duo
# - -
SELECT Adv IF (NOT -1 Dem or (A Attr))(0 ("duo"));
	## Áddjá go duo lea boahtime?

# duođaid
# - - - -
SELECT Adv IF (-1 BOS OR PUNCT OR CLB)(0 ("duođaid"))(1 COMMA);
	## Duođaid, mun cealkkán didjiide:...

# dušše
# - - - -
 # There is a rule "Lea dušše okta Ipmil" for the typical dušše cases.

REMOVE N IF (0 ("dušše")); 
REMOVE A IF (0 ("dušše"));
	## Vihtta bivdovuogi mun gal dušše namahin,...
	## Oar-julggiid atnet olbmot dušše dalle go bodni lea nu garas...  
 # Brutal, but the noun and the adjective are almost always "dušši".

REMOVE VFIN IF (*-1 VFIN BARRIER CP OR CS)(0 ("dušše"));
REMOVE VFIN IF (0 ("dušše"))(*1 VFIN BARRIER CP OR CS);
	## Nu eai dieđus dárbbaš skáhppot birra buot gal, dušše lasi 
	## dan sadjái mii mieská.
	## ...go dušše áibbas moatti sajis heive meardebuođđu adnojuvvot.

REMOVE VFIN IF (-1 ("leat"))(0 ("dušše"));
	## Leamaš dušše vihtta dorski nuhtiin.

REMOVE Actor IF (0 ("duššat")); 	# Make a set for non-agentive verbs?
	## Oktagaslaš olbmo lonuheami dohkálašvuohta lea stáhtii geavadis
	## duššin.

SELECT Adv IF (-1 BOS OR CS OR (":" CLB))(0 ADV-NOT-VERB);
SELECT Adv IF (-2 BOS OR CS OR (":" CLB))(-1 Pcle)(0 ADV-NOT-VERB);
	## Dušše daid dieđuid, mat gusket konkrehta várraárvvoštallamii,
	## sáhttá čuoldit geahččanlobis.
	## No hits for the last rule?

# eambbo
# - - - - 
SELECT A IF (0 ("eambbo"))(1 N);
	## Dingojitgo eambbo biergu?

SELECT Adv IF (0 ("eambbo"));
	## Gittabivdosiin ii leat lohpi bivdit deanu eambbo go beallái.

# eanaš/eanas
# - - - - - - -
SELECT Adv IF (*-1 NP-HEAD-NOM BARRIER CLB OR CS OR CP)
	(0 ("eanas") OR ("eanaš"))(NOT *1 N BARRIER NPNH);
	## Muhto báinnan dat gal eanaš lea.

SELECT Adv IF (*-1 COPULAS BARRIER NOT-ADV-PCLE LINK *-1 NP-HEAD-NOM BARRIER 
	NOT-ADV-PCLE)(0 ("eanaš") OR ("eanas"))(NOT 1 N LINK 1 N);
	## Áššemeannudeapmi lea eanaš dárogillii.
	## Sámegiella lea eanaš oktavuođain unnitlogugiella. (NOT 1 N LINK 1 N)!

SELECT Adv IF (*-1 NP-HEAD-NOM BARRIER NOT-ADV-PCLE LINK NOT 0 PrfPrc 
	LINK *-1 COPULAS BARRIER NOT-ADV-PCLE)(0 ("eanaš") OR ("eanas"));
	## Oahpahusossodagas lea áššemeannudeapmi eanaš dárogillii.

SELECT Adv IF (-1 COPULAS)(0 ("eanas") OR ("eanaš"))(1 N)(NOT 1 TIME)
	(*2 CS OR EOS BARRIER N);  
	## Dál lea eanaš ovttamielalasvuohta, ahte...
	## Ovdal ledje eanas vuonaid bivdán, muhto go skoavdefatnasat bohte... 
	## Ealus lea eanaš álddut.

SELECT Adv IF (0 ("eanaš") OR ("eanas"))(1 PrfPrc);
	## Nuppádassii leat čielga gáibadusat eaktodáhtolaš oskui, mii 
	## guoská giddodahkii, eanaš vuođđoduvvon registrerema oktavuođas. 

# eanet
# - - -
SELECT Adv IF (0 ("eanet"))(1 CC)(2 ("eanet"))(3 A);
SELECT Adv IF (-2 ("eanet"))(-1 CC)(0 ("eanet"))(1 A); 
	## Dutkangaskkusteapmi ja jearru dutkanbohtosiidda šaddá eanet ja 
	## eanet guovddáš oassi dutkama árgabeaivvis.

SELECT Adv IF (0 ("eanet"))(1 ("uhcit") OR ("unnit"));
SELECT Adv IF (-1 ("uhcit") OR ("unnit"))(0 ("eanet"));
	## ...muhto dál lea eanet uhcit jávkan.  
	## Go vierroopmodaga geavaha ja eaiggát unnit eanet čielgasit
	## dasa leat miehtán,...

SELECT Adv IF (0 ("eanet"))(NOT 0 Pron)(NOT 1 Dem OR N OR A);
	## Guovlu atnigoahtá olu eanet.
	## Dat lea eanet go 10.
 # Ambiguous: ## ...hálida universitehta almmuhit eanet dieđalaš artihkkaliid
	      ## sámegillii.
 # - hence the condition (NOT 1 A).

SELECT Adv IF (0 ("eanet"))(1 A-CASE)(NOT *2 N BARRIER NPNH);
	## Báhkkodeamis ferte leat sáhka geavaheamis mii lea oktoráđđejeaddji
	## dahje ainjuo čielgasit eanemus čalbmáičuohcci.
 # But the A-CASE is often a wrong analysis.

SELECT Attr IF (0 ("eanet"))(1 Dem OR N);
	## Jos galgá eanet dutkiid ožžodit dán suorgái,... 
	## Noađđebealli oažžu deaddit eanemus viegu.
	## ...okta dahje eanet dain eará namuhuvvon doaimmain.

# erenoamáš
# - - - - - -
SELECT Adv IF (0 ("erenoamáš"))(1 A);
	## Doaibmabidju lihkostuvvui bures ja lei erenoamáš hutkkálaš.

# earenoamážit/erenoamážit
# - - - - - - - - - - - - - -
SELECT Adv IF (0 ("earenoamážit") OR ("erenoamážit"))
	(*1 Loc OR Ill OR Com BARRIER NPNHA);
	## ...ja earenoamážit dán gaskavuođas servodatlaš beliid.

SELECT Adv IF (0 ("earenoamážit") OR ("erenoamážit"))
	(*1 NP-HEAD-GEN BARRIER NPNHA LINK 1 Po);
	## ...earenoamážit gáhtten dihte Goahtečoruvuona gođđobáikkiid...
   
# easkka
# - - - -
SELECT Adv IF (0 ("easkka"))(*1 TIME BARRIER NPNHA);
	## ...mii easkka maŋimuš áiggiid lea jávkágoahtán. 
	## Dat lei boahtán ruoŧagielain easkka j. 1863.
 # Ambiguous: ## Dat čielgá dálá oasálaččaide easkka proseassas. 

SELECT Adv IF (0 ("easkka"))(1 Num OR ("maŋŋel"));
	## Muhto easkka 1898:s dovdagohte Láhppiolbmot dan mii maŋŋelis bođii.
	## ...easkka maŋŋel 1529 ásaiduvve olbmot duođas báikkiide.

# goas
# - - -
SELECT Adv IF (0 ("goas"))(NOT 1 go)(*1 ("<?>"));         
SELECT Adv IF (0 ("goas"))(1 CC)(2 CP);
	## Goasbat Máret fuobmái fas dohko vuolgit?
	## Goas ja mo dearvvašvuođaveahki lea addojuvvon,... 

# goassige
# - - - - -
SELECT Adv IF (*-1 Neg BARRIER VFIN)(0 ("goassige"));
	## Ii leat stáhtas várra goassige leamaš ruđalaš ávki dán ámmahis. 

# gosa
# - - -
SELECT Adv IF (0 ("gosa"))(*1 VFIN BARRIER CLB OR CS);
       ## Gosa don čihket dan?
       ## Son oinnii báikkiid gosa juo ledje ásaiduvvan. 
 # Here, the rule giving the verbal reading of "gosa" is missing. It must
 # be added.

# gosage
# - - - -
SELECT ("gosa" Foc) IF (*-1 Neg BARRIER VFIN-NOT-IMP);
	## Mii eat sáhttán vuolgit gosage.

SELECT ("gosa" Foc) IF (*-1 Qst BARRIER CS OR CP);
	## Leatgo don vuolgime gosage?

SELECT ("gosa" Foc) IF (*-1 ("go" Pcle) BARRIER CS OR CP);
	## Leat go don vuolgime gosage?

# gusto
# - - -
SELECT Adv IF (0 ("gusto"))(1 VFIN);
	## Don gal gusto beroštat čuovvut.

SELECT Adv IF (-1 VFIN)(NOT -1 Neg)(0 ("gusto"));           
	## Diet lea gusto ávkkálaš.

REMOVE VGen IF (0 ("gusto"));
	## Jeahkálat nohket gal dálvvis gusto.

# hárve
# - - -
SELECT Adv IF (*-1 VFIN BARRIER CS OR CP LINK NOT 0 Neg)(0 ("hárve"));
	## Prinsihpalaš gažaldagat leat leamaš baicce hárve Norgga duopmostuoluin.

# ikte
# - - -
SELECT Adv IF (0 ("ikte"))(*1 (V Prt));         ## Ikte lei hui buolaš.
SELECT Adv IF (*-1 (V Prt))(0 ("ikte"));        ## Ikte lei hui buolaš.

# jogo
# - - -
SELECT Qst IF (0 ("jogo"))(NOT *1 ("dahje"));
	## Jogo gikse dat guolit?

# juo
# - -
SELECT Interj IF (-1 BOS)(0 ("juo"))(1 COMMA);
	## Juo, dat gal heive.

# láhkai
# - - - -
SELECT N IF (-1 Dem OR Attr OR (Interr Gen))(0 ("láhkai"));
	## Seammá láhkai go váldojournála ja lassijournála geavaheami oktavuođas...
	## Dán barggus lea erenoamáš deaŧalaš bures gulahallat Sámedikkiin guđe
	## láhkai galgá návccaid atnit.

# maid
# - - -
SELECT Adv IF (-1 (Pron Rel))(NOT -1 (Pron Rel Pl Acc));
    ## ... mat maid leat politihkalaš bellodagat.

SELECT Adv IF (-1 INF-VERB)(0 ("maid"))(*1 Inf BARRIER CS OR CP);
	## Mun háliidan maid mannat.

SELECT Adv IF (-1 ("leat"))(0 ("maid"))(*1 PrfPrc BARRIER ("leat") OR CS OR CP);
	## Lea maid lassin gávnnahan metodalaččat váttisin mearridit...

SELECT Adv IF (-1 ("leat"))(0 ("maid"))(*1 (A Nom) BARRIER NOT-ADV);
	## Geainnut ledje maid guhkit rabas.

SELECT Adv IF (-1 N LINK *-1 BOS OR CS OR (":" CLB) BARRIER NPNHA)(0 ("maid"))
	(NOT *1 VFIN LINK *1C VFIN BARRIER CS OR CP);
	## Suomas maid lei dilli váttis.
	## Nubbi maid fuobmáimet lei ahte... (Interr, hence the last condition.) 

SELECT Adv IF (*-1 V-PL3 BARRIER NOT-ADV-PCLE)(0 ("maid"))(*1 NP-HEAD-PL-NOM 
	BARRIER NPNHA LINK NOT *1 V-PL3 BARRIER NOT-ADV);
	## Riggodagaide gullet maid fuođđot ja lottit.

SELECT (Rel Sg) IF (-1 ("buot"))(0 ("maid"));
    ## Buot maid dii dahkabehtet, lea buorre.

REMOVE Adv  IF (0 ("maid"))(*1 VFIN BARRIER CS OR CP LINK *1 COMMA 
	BARRIER CS OR CP LINK 1 VFIN)(NOT -1 (N Pl) LINK *2 V-PL3 BARRIER NOT-ADV);
REMOVE Interj  IF (0 ("maid"))(*1 VFIN BARRIER CS OR CP LINK *1 COMMA 
	BARRIER CS OR CP LINK 1 VFIN)(NOT -1 (N Pl) LINK *2 V-PL3 BARRIER NOT-ADV);
	## Dan maid dalle dárbbaša fievrridit, ferte olmmoš noađuhit.

SELECT Rel IF (*-1 VFIN BARRIER CS OR CP LINK 0C VERB)(0 ("maid"))
	(*1 VFIN BARRIER CLB OR CS OR CP)
	(NOT *1 Inf BARRIER VERB OR Nom LINK *-1 INF-VERB BARRIER VERB)
	(NOT -1 ("leat" Pl3) LINK *2 (N Pl Nom) BARRIER NPNHA);
	## Don galggat doallat dan maid leat vurdnon Herrái.

SELECT Adv IF (-2 BOS OR CS OR (":" CLB))(-1 (Pron Pers) OR N)(0 ("maid"))
	(NOT *1 VFIN LINK *1C VFIN BARRIER CS OR CP);
	## Sidjide maid lei Ruija.

SELECT Adv IF (-1 COPULAS LINK 0 Pl3)(0 ("maid"))(*1 (N Pl Nom) BARRIER NPNHA);
SELECT Adv IF (-1 Loc LINK *-1 COPULAS BARRIER NPNHA OR VERB)(0 ("maid"))
	(*1 (N Nom) BARRIER NPNHA); 
	## Romssa universitehtas leat maid eará plánat álggahit fálaldagaid.
	## Leago stáhtas maid geatnegasvuohta doarjut doalahit dán kultuvrra.

SELECT Adv IF (*-1 CS BARRIER VFIN OR CLB)(NOT -1 ("go"))(0 ("maid"))
	(*1 VFIN LINK NOT *1 VFIN);
	##... nu ahte dat maid siskkilda ávnnalaš kultureavttuid.    

SELECT (Pron Interr) IF (-1 BOS OR PUNCT)(1 Nom)(*1 ("?"));

REMOVE Interj IF (1 CC)(2 (Pron Interr));
REMOVE Adv IF (1 CC)(2 (Pron Interr));
 	## Maid mii dál vihtaniiguin?
	## Lea registreren maid ja man ollu lea borran. 

SELECT Adv IF (0 ("maid"))(NOT *1 VFIN);
SELECT Adv IF (0 ("maid"))(NOT *1 VFIN BARRIER CC OR CS OR CP);
	## Dán oktavuođas čujuhuvvo maid oanáduslistui ja gáldolistui.
	## Sápmelaččat oasálaste maid dasa, muhto lassin ledje sis vel eará 
	## resursat.

SELECT Adv IF (*-1 INF-VERB BARRIER VERB)(0 ("maid"))(*1 Inf BARRIER VERB);
	## Sápmelačcat galge maid vearu máksit Danmárku-Norgii. 
 
SELECT Adv IF (*-1 (V Sg3) BARRIER NOT-ADV-PCLE LINK NOT *-1 Nom BARRIER CS)
	(0 ("maid"))(*1 NP-HEAD-SG-NOM BARRIER NPNHA);
	## Dattege bohciida maid sierra gažaldat, 

SELECT Interj IF (-1 BOS)(0 ("maid"))(1 COMMA);
	## Maid, iigo leat boahtán?

# manne
# - - -
SELECT Adv IF (-2 BOS OR CS OR CLB OR PUNCT)(-1 CC)(0 ("manne"))(*1 VFIN);     
SELECT Adv IF (*-1 BOS OR CS OR CLB OR PUNCT BARRIER NOT-ADV-PCLE)(0 ("manne"))
	(*1 VFIN BARRIER CC OR CS OR CP LINK NOT 0 Inf); 
	## Ja manne moraštehpet biktasiid dihtii? 
	## Naba dii, manne dii ieža rihkkubehtet Ipmila báhkkomiid?
	## Sii manne isideaset lusa ja muitaledje buot. # BARRIER CC!

SELECT Adv IF (*-1 BOS OR CS OR CLB OR PUNCT BARRIER NOT-ADV-PCLE)(0 ("manne"))
        (*1 LEAT BARRIER CC OR CS OR CP LINK *1 (N Pl Nom) BARRIER CC OR CS OR CP OR CLB OR PUNCT);
	## Manne dis leat diekkár bahás jurdagat din váimmuin?

SELECT Adv IF (*-1 VFIN BARRIER CS OR CP OR CC OR CLB LINK NOT 0 Neg)(0 ("manne"));
	## Jos oktage jearaš dudnos manne doai dahkabeahtti dan,...

# mielas
# - - - -
REMOVE Adv IF (-1 Gen)(0 ("mielas"));   
	## Mu mielas don sáhtát mielas boahtit.

# muhtumin
# - - - - -
SELECT Adv IF (0 ("muhtumin"));
	## Muhtumin lea deaŧalaš čállit journála dalán.

# naba
# - - -
SELECT Adv IF (0 ("naba"))(NOT 1 V-SG3);
       ## Naba don, man ollu don leat dahkan.
  # "Naba" is a proper noun also, but hardly relevant in a Sami context.
  # Thus, it could perhaps be deleted in all contexts. The intended context
  # here is one of the subject, but it is somewhat halfheartedly written.

# ođđasit
# - - - -
SELECT Adv IF (-1 ("fas"))(0 ("ođđasit"));
	## Elia gal boahtá dahkat buot fas ođđasit.

SELECT Adv IF (*-1 Nom BARRIER CS OR CP)(0 ("ođđasit"))
	(*1 V-NOT-AUX BARRIER CS OR CP);
	## Eat mii sáhte ođđasit ásahit vuođđu.

# oktan/ovttas
# - - - - - - -
SELECT Adv IF (0 ("oktan") OR ("ovttas"))(*1 Com BARRIER NP-BORDER);
	## Divššohas lea vuoigatvuohta iežas journála geahččat oktan
	## mildosiiguin. 

SELECT Adv IF (-1 (Pl Nom))(0 ("ovttas"))(1 V-PL3);
	## Ferte oaidnit ollislaš gova maid soahpamušat ovttas vuođđudit.
 
SELECT Adv IF (*-1 NUMERALS BARRIER NPNH LINK 0 Nom)(0 ("ovttas"))
	(*1 V-PL3 BARRIER NOT-ADV-PCLE);
	## Ferte oaidnit ollislaš gova maid dát guokte soahpamuša ovttas vuođđudit.

# oktii
# - - -
REMOVE V-SG3 IF (*-1 VFIN BARRIER CS OR CP)(-1 ("dušše"))(0 ("oktii"));
	## Vuoigatvuohta gusto dušše oktii.

REMOVE V-SG3 IF (-1 ("dušše") LINK *-1 BOS BARRIER VFIN)(0 ("oktii"))
    (*1 VFIN BARRIER CS OR CP);
    ## Dušše oktii lean oaidnán čájáhusa mii čájehii álgoálbmoga rávdebarggu.
    ## Ja dušše oktii lean oaidnán čájáhusa mii čájehii álgoálbmoga rávdebarggu.

SELECT Adv IF (-1 ("dušše") LINK *-1 BOS BARRIER VFIN)(0 ("oktii"))
    (*1 VFIN BARRIER CS OR CP)(NOT *1 ILL-VERB);
    
SELECT Adv IF (-1 ("dušše") LINK *-1 VFIN BARRIER CS OR CC)(0 ("oktii"))
    (*1 Inf OR PrfPrc);
    ## Lulli-sámiguovlu biirret heajumusat go sis lea dušše oktii leamaš listu
    ## mas nisu lea njunnožis.

SELECT Adv IF (-1 VFIN LINK -1 N LINK -1 CC LINK -1 N)
    (0 ("oktii"));


#SELECT Adv IF (*-1 VFIN BARRIER NOT-ADV LINK -1 N LINK *-1 CC BARRIER NPNH LINK -1 N)
#    (0 ("oktii"))(1 NP-BORDER);

SELECT Num IF (-1 ("dušše") LINK *-1 BOS BARRIER VFIN)(0 ("oktii"))
    (*1 VFIN BARRIER CS OR CP)(*1 ILL-VERB);

REMOVE V IF (*-1 VFIN BARRIER CC OR CS OR CP)(0 ("oktii"));
	## Vuoigatvuohta gusto dušše oktii seammá dilálašvuođa ovddas.

SELECT Adv IF (-1 BOS OR CS OR (":" CLB))(0 ("oktii"))(1 go OR (N Pl));
	## Oktii go leimmet mannamin rohkadallanbáikái, bođii min ovddal 
	## muhtun šlávvanieida. 
	## Oktii cuohpaid galgá biddjot luossa go sáltejuvvo.

SELECT Adv IF (0 ("oktii")) (1 ("njunni" N Pl Gen) OR ("juolgi"  N Pl Gen) OR 
       ("sealgi"  N Pl Gen) OR ("geahči" N Pl Gen));
    ## Dat čohkkáiga oktii njuniid.
    
SELECT Adv IF (*-1 TOGETHER-VERB OR COM-VERB) (0 ("oktii"));
    ## Porsáŋggu loguin leat buot klássaid submi biddjon oktii.
    #  Here we definitely need some barriers.


SELECT Adv IF (*1 TIME BARRIER N OR V OR Pron)(*1 Ill OR Loc BARRIER NPNH);    
    ## Oktii  juohke  stuorradiggeáigodagas  ovddiduvvo  dieđáhus daid
    ## doaibmabijuid  birra  mat  galget sihkkarastit ja ovddidit sámi giela

    
SELECT Adv IF (-1 ("vel"))(0 ("oktii")); 
    ## Lea go Sámedikki mielas Microsoft duodaid anssašan vel oktii
    ## nuvttá máidnosa, mii ii leat duohta ge?

SELECT Adv IF (0 ("oktii"))(1 ("vel"));
    
REMOVE Num IF (-1 (@SUBJ))(0 Adv)(1 VFIN);


    
# okto
# - - -
SELECT Adv IF (*-1 VFIN BARRIER CS OR CP OR CC)(0 ("okto"));
SELECT Adv IF (0 ("okto"))(*1 VFIN BARRIER CS OR CP OR CC); 
	## ...ahte fertejit leat okto geavahan dahje čielgasit eanemusat
	## geavahan,
	## ...maid badjel sii ovdal okto leat ráđđen.

# ollásit
# - - - -
SELECT Adv IF (-1 VERB)(NOT -1 INF-VERB)(0 ("ollásit" Adv));
	## ...ahte historjjálaš ovdáneapmi čielggaduvvo ollásit.  

SELECT Adv IF (0 ("ollásit" Adv))(*1 Loc OR Ill BARRIER NPNHA);
	## Dát eatnamat eai gula datte ollásit dábálaš njuolggadusaide
	## stáhta eatnamiid namas. 

SELECT Adv IF (0 ("ollásit" Adv))(*1 NP-HEAD-GEN BARRIER NPNHA LINK 1 Po);
	##
	
	
# ollu
# - - 

SELECT (Pron Indef @PronN>) IF (-1 Rel)(1 N);
	## maid ollu dearvvašvuođaásahusat.

SELECT (Pron Indef @PronN>) IF (*-1 V BARRIER NOT-ADV)(*1 (N Acc) OR (N Nom) BARRIER NOT-A);
	## Gielddat, main orrot ollu sápmelaččat.

SELECT Adv IF (*-1 V-TRANS-ACT LINK NOT 0 ILL-VERB OR COM-VERB OR LOC-VERB LINK *1 ADVLCASE)(NOT *1 CS)
              (-1 ("seammá") OR ("seamma"))(0 ("ollu"));
    ## Bargiid guottut mearkkašit seammá ollu bálvaluskvalitehttii go fáktadieđut geavaheaddjiid giela ja kultuvrra birra.
              
SELECT Adv IF (-1 ("seamma") OR ("seammá"))(0 ("ollu"))(NOT *1 N OR A BARRIER NPNH);	
    ## Soabadanráđis galget leat golbma miellahttu ja seamma ollu várrelahttut.

#SELECT Pron IF (-1 ("seamma") OR ("seammá"))(0 ("ollu"))(*1 N BARRIER NPNH LINK 1 ("go"));	
    ## Bargiid guottut mearkkašit seammá ollu bálvaluskvalitehttii go fáktadieđut geavaheaddjiid giela ja kultuvrra birra.

REMOVE Adv IF (NOT -1 ("seamma") OR ("seammá") OR ("mii" Pron))(0 ("ollu"))(NOT 1 V);
    # This is a kill-all-rule...



# oppa
# - - -
SELECT Adv IF (0 (@ADV-A))(0 ("oppa"));
	## Dat lea oppa juhkkis olmmoš.

# rádjái/rádjai
# - - - - - - -
SELECT Po IF (-1 Num)(0 ("rádjai") OR ("rádjái"));
	## Njuolggadusat nággolága § 204:s § 209 rádjái ožžot seammá geavahusa.
 # This rule should be extended to all postpositions likely to occur after
 # number expressions. Todo: Make a set.
 
 
# seamma / seammá 
# - - - - - - - - 
SELECT Adv IF  (0 ("seamma") OR ("seammá"))(1 ("ollu"));
    ## Soabadanráđis galget leat golbma miellahttu ja seamma ollu várrelahttut.

# siivot
# - - - -
SELECT Adv IF (-1 VFIN)(0 ("siivot"));
	## Vácce siivot amat boktit máná.

# sullii
# - - - -
SELECT Adv IF (0 ("sullii"))(1 Num);    
	## Bušeahtas lea sullii 650.000 ruvnno leamaš várrejuvvon.

 # This rule must be generalised on both conditions: "sullii" must be extended
 # to a set of adverbs denoting quantification, and Num must be extended to
 # more measures (or parallel rules must be written).

SELECT Adv IF (-1 ("leat"))(0 ("sullii"));
	## Spahká lea sullii dušše suorbmabeali asu gežiid bokte.
 # The idea is that "leat" doesn't subcategorise for illatives.

# unnán
# - - - 
SELECT Adv IF (0 ("unnán"))(1 VFIN OR PrfPrc)(NOT 1 N);
	## Eanaš duopmárat unnán dovdet eamiálbmogiid historjjá.
	## Dat orru oppanassii unnán čilgejuvvon 1800-logu gaskamuttu.

# uhcit/unnit
# - - - - - - 
SELECT Adv IF (-1 ("eanet"))(0 ("uhcit") OR ("unnit"));
SELECT Adv IF (0 ("uhcit") OR ("unnit"))(1 ("eanet") OR ("eambbo"));
	## Dál lea eanet uhcit jávkan.
	## Go eaiggát eanet unnit čielgasit dasa lea miehtán,... 

# várra
# - - -
SELECT Adv IF (0 ("várra"))(*1 (N Sg Nom) BARRIER CC OR CLB OR CS OR CP);
	## Mearkkašahttimus Ruonáeatnama ruovttustivrii dál várra lea váttisvuohta 
	## ruhtadit buot áššesurggiid hálddašeami.

SELECT Adv IF (*-2 A BARRIER WORD)(-1 CC)(0 (várra))
	(*1 PrfPrc BARRIER NOT-ADV-PCLE); 
	## 

SELECT Adv IF (-1 CLB)(0 ("várra"))(NOT *1 V-SG3);
	## Sápmelaččat geat ledje birgegoahtán dušše boazudoaluin, várra
	## ledje buot friddjamusat.

# vehážiid
# - - - - -
SELECT Adv IF (0 ("vehážiid"))(1 ("vehážiid"));
SELECT Adv IF (-1 ("vehážiid"))(0 ("vehážiid"));
	## ...mii easkka maŋimuš áiggiid lea vehážiid vehážiid jávkkagoahtán.

# visot
# - - -
SELECT Adv IF (0 ("viso"))(NOT 1 (N Pl));
	## Ii leat vel visot čohkkejuvvon.
	## Visot dan maid NRK sádde digitála fierpmis sáhttá maid gullat Nettradios.
 # This is not a particularly good rule for 'visot'.
 
# viššal vs. viššalaš
# - - - - - - - - - -
REMOVE ("viššalaš") IF (*-1 HUMAN LINK 0 (@SUBJ) BARRIER CS OR CP);
    ## Sihke Mázes ja Guovdageainnus leat boarrásat viššalit finadan doaibmaguovddážiin.

# vuhtii
# - - - - 
SELECT Adv IF (-1 ("váldit"))(0 ("vuhtii"));
	## ...muhto ferte váldit vuhtii máná agi ja láttasvuođa.



# ======================= #
# Disambiguating pronouns #
# ======================= #
       CONSTRAINTS #11    # 
# ======================= #


# Interrogative and relative pronouns
# ===================================

# Relative or interrogative

SELECT Interr IF (-1 BOS OR (":"))(*1 ("?"));
SELECT Interr IF (-2 BOS)(-1 Pcle)(*1 ("?"));
SELECT Interr IF (-2 BOS)(-1 PUNCT);
	## Maid don doaivvut, mun gielistan?
	## Na maid don diŋgojit?
	## Man uhccán osku dus lea!
 # The last example is an exclamation. Hit because no ? required.

REMOVE Interr IF (-1 Interr); 
	## Dat lea čuonžža, mii maid gorgŋu detnui čakčageasi.

REMOVE (Pron Pers) IF (NOT -1 V-PL1)(0 ("mii"))
        (*1 V-SG3 BARRIER V-PL1 LINK NOT 0 (Pl1));
	## Dát ii guoskka dikšui mii boahtá psykiatriija vuollái.

SELECT Rel IF (*-1 V-NOT-AUX BARRIER CS OR CP LINK *-1 NP-HEAD-NOM 
	BARRIER CS OR CP)(*1C NP-HEAD-NOM BARRIER CS OR CP);
	## ...ahte olbmot besset oaidnit maid sii sáhttet vuordit.

SELECT (Pron Rel Sg) IF (-1C NP-HEAD-SG OR (Card Sg))
	(NOT *-2 NUMERALS BARRIER NPNH);
SELECT (Pron Rel Pl) IF (-1 NP-HEAD-PL)(NOT -1 NP-HEAD-SG);
	## ...go lei sáimmaiguin bivdán jávrris maid stáhta lei láigohan. 
	## Doaibma gullá doaimmaide maid ulbmil lea movttiidahttit sámi nuoraid... 

# Rel or Interr
REMOVE Interr IF (NOT -1 BOS)(0 Rel);
REMOVE Interr IF (NOT -2 BOS)(-1 CC OR COMMA)(0 Rel);
REMOVE Interr IF (NOT -3 BOS)(-2 COMMA)(-1 CC)(0 Rel);

REMOVE Rel IF (*-1 BOS BARRIER N OR Pron OR Num OR A);

# Coordination for Rel and Interr

#REMOVE Interr IF (-2 COMMA)(-1 CC)(0 Rel);
	## , muhto geat ieža eai šat meroštala alcceseaset sáme identitehta.


# Number detection
  # Plural number

# Referring back to QP
SELECT Pl IF (*-1 NUMERALS BARRIER NPNH LINK NOT 0 Ord)(0 (Pron Rel));
	## Davviriikkain leat guokte álbmoga maid atnit álgoálbmogin.

SELECT Pl IF (-2C NP-HEAD-PL)(-1 COMMA)(0 (Pron Rel));
	## Deaŧalaš lea ahte čállojuvvojit dakkár gillii, maid buot bargit
	## áddejit. 
        ## Dat divššohasat, geaid leat geavadis vejolaš joksat,...

# Referring back to plural PoP

SELECT Pl IF (-2 Pl)(-1 Po)(0 (Pron Rel));
    ## Jos in livčče dahkan dakkár daguid sin gaskkas maid ii oktage eará leat dahkan, de ...

SELECT Pl IF (-3 Pl)(-2 Po)(-1 COMMA)(0 (Pron Rel));
	## ...ahte resurssaid ektui maid livččii dárbbašan atnit... 
	## Sivaid geazil, maid berre čuovvolit,... 

SELECT Pl IF (-1 Po LINK *-1 NUMERALS BARRIER NPNH)(0 (Pron Rel));
SELECT Pl IF (-2 Po LINK *-1 NUMERALS BARRIER NPNH)(-1 COMMA)(0 (Pron Rel)); 
	## No hits? 
	## No hits?

# Referring across Plural PP

REMOVE Sg IF (*-1 (N Pl) BARRIER (N Sg))(0 (Pron Rel));
    ## Jos in livčče dahkan dakkár daguid sin gaskkas maid ii oktage eará leat dahkan, de ...


REMOVE Sg IF (-2 (N Pl))(-1 Inf OR PrsPrc)(0 ("mii" Pron Rel));
	## Ovddasteaddjis lea vuoigatvuohta dieđuid geahččat maid divššohas
	## biehttaluvvo oaidnimis.

 # Singular number

SELECT Sg IF (NOT -3 CC)(-2C NP-HEAD-SG)(-1 COMMA)(0 (Pron Rel));

SELECT Sg IF (-2 Sg LINK NOT *-1 NUMERALS BARRIER NPNH)(-1 Po)(0 (Pron Rel));
SELECT Sg IF (-3 Sg LINK NOT *-1 NUMERALS BARRIER NPNH)(-2 Po)(-1 COMMA)
	(0 (Pron Rel));
	## Mandáhta mielde maid Sámi vuoigatvuođalávdegoddi lea addán...
	## ...ja dan dearvvašvuođaveahki birra, maid son oažžu,... 

REMOVE Pl IF (*-1 BOS OR CS OR (":" CLB) BARRIER NOT-ADV-PCLE)(0 ("mii" Pron Interr))
	(NOT *1 (N Pl Acc) OR (N Pl Gen) BARRIER NPNH);
	## Maid don doaivvut, mon gielistan?

REMOVE Pl IF (*-1 VERB BARRIER NOT-ADV-PCLE LINK NOT -1 (N Pl))
	(0 ("mii" Pron Rel))(NOT *1 (N Pl Acc) OR (N Pl Gen) BARRIER NPNH);
	## Ii nagot áddet maid miehtán sisdoallá.

REMOVE Pl IF (NOT *-2 ("mii" Pron Interr) BARRIER CS)(-1 CC)(0 ("mii" Pron Interr))
	(NOT *1 (N Pl Acc) OR (N Pl Gen) BARRIER NPNH);
	## Daidda heŋgejuvvojit gurpasat, ja maid dál de guottehit lea áigumin.
# Look at this rule!


# Case detection

SELECT (Sg Gen) IF (0 ("mii" Interr) OR ("mii" Rel))(1 A)(NOT 1 Comp OR Superl);
SELECT (Sg Gen) IF (0 ("mii" Interr) OR ("mii" Rel))(1 Adv)(NOT 1 Superl);
	## Man guhkki lea skuvlii?
	## It go don gula man garrasit sii sivahallet du?
	
SELECT Gen IF (0 (Pron Interr) OR (Pron Rel))(1 Po); 
	## Son gean birra dieđut leat,...  

REMOVE Acc IF (0 (Pron Interr) OR (Pron Rel))(*1 NP-HEAD-NOM BARRIER NPNH LINK *1 COPULAS BARRIER
	NOT-ADV-PCLE LINK NOT *1 PrfPrc BARRIER CS OR CP);
	## Doaibma gullá doaimmaide maid ulbmil lea movttiidahttit sámi nuoraid...





# Reflexive pronouns
# ==================
  # iežas is Gen or Acc
  # ieža is Pl or Du

# Reflexive iežas
# ----------------
SELECT Gen IF (-1 Acc)(NOT -1 PrfPrc)(0 ("ieš" Gen))(1 HNOUN);
	## ... muhto it fuomáš hirssa iežat čalmmis?

SELECT Acc IF (*-1 VFIN BARRIER NOT-ADV-PCLE)(0 Refl)
	(1 (Actio Ess) OR PrfPrc) ;
	## Danne lohká Næss dál iežaset plánemin akšuneregoahtit dávjjibut.
  # Acc ieža- is also captured by later object rules.

# Emphatic ieš
# -------------
  # Sg ieš is unique, there is homonymy Du/Pl for ieža.
 # Problem: Prs Du1 = Prt Pl3.

SELECT Pl IF (*-1 V-PL OR (V Imprt Pl2) OR MII-PERS OR DII OR SII OR 
	(Pron Interr Pl Nom) BARRIER CS OR CP OR PUNCT)(0 ("ieš" Pron Refl Pl Nom))
	(NOT *1 V-DU BARRIER NOT-ADV-PCLE);
	## Allet dubme, amadet ieža dubmejuvvot!
	## ...muhto leat maiddái muhtumat geat ieža leat dahkan iežaset 
	## dohkketmeahttumin...

SELECT Pl IF (0 ("ieš" Pron Refl Pl Nom))(*1 V-PL BARRIER CS OR CP);
	## ...ja didjiide mihtiduvvo seammá mihtuin mainna ieža ge mihtidehpet.

SELECT Du IF (*-1 V-DU OR MOAI OR DOAI OR SOAI BARRIER CS OR PUNCT)
	(0 ("ieš" Pron Refl Du Nom));
	## Doai galgabeahtti ieža mannat.

SELECT Du IF (0 ("ieš" Pron Refl Du Nom))
	(*1 V-DU OR MOAI OR DOAI OR SOAI BARRIER CS OR CP);
	## Ieža soai muitaleigga mii lei dáhpáhuvvan mátkkis.

          

# Reciprocal pronouns
# ===================

SELECT Recipr IF  (1 Recipr); 
SELECT Recipr IF (-1 Recipr); 
	## ...de dadje guhtet guoibmáseaset:...  
	## Gielddat berrejit ásahit fásta dulkaveaga, kánske ovttasbarggus nubbi
	## nuppiin.




# ================ #
  CONSTRAINTS #12  #
# ================ #

REMOVE Recipr ;
	## Jos dajan nubbái: Mana! de son manná.



# Numerals
# ========
 # These rules have to precede the rules for indefinite pronouns, or the Ord 
 # reading of "nubbi" will be removed where it should remain.

# Numerals or not
# ---------------

REMOVE N IF (0 Num)(*1 (N Sg Gen) BARRIER NOT-A);


# General rules
# -------------

#REMOVE Nom IF (-1 ABBR OR ACR OR ("§") OR ("paragráfa") OR ("kapihttal") OR 
#	("kapihtal") OR ("jahki") OR GEASSET)(NOT -1 ("nr"))(0 Num);
#	## Dat lea nr. 4.
	
REMOVE Acc IF (-1 ABBR OR ACR OR ("§") OR ("paragráfa") OR ("kapihttal") OR 
	("kapihtal") OR HEADOFPARTS OR ("jahki") OR GEASSET)(NOT -1 ("nr"))(0 Num);
	## 
	## Mun logan nr. 4. Mun logan nr. ovtta.
	
REMOVE Gen IF (-1 ABBR OR ACR OR ("§") OR ("paragráfa") OR ("kapihttal") OR 
	("kapihtal") OR HEADOFPARTS OR ("jahki") OR GEASSET)(NOT -1 ("b" ABBR))(NOT -1 ("nr"))(0 Num);
	## Rievdaduvvon lágain geassemánu 15. b. 2001 nr 93.
	## ...maid KITH gárvvistii ja almmuhii geasset 2001.
 # These rules leave only the caseless Num reading. # Well, now we have case everywhere
 # SELECT Nom is much stronger than

SELECT (Num Acc) IF (-1 (Num Acc))(0 ("duhát"))(1 (N Acc));
	## Dat máksá guokte duhát ruvnno.

SELECT Num IF (1 ("ruvdnu"));
	## Dainna čuđiin ruvnnuin áiggun bierggu oastit.


# Rules related to specific numerals
# ----------------------------------

# galle
# - - - 
SELECT Indef IF (*-1 Neg BARRIER CS OR CP)(0 ("galle"));
	## Eaige sámiid ássanguovllus leat galle gieldda, mat...
 
REMOVE Indef IF (0 ("galle"));
SELECT Num IF (NOT -1 Neg)(0 ("galle"))(*1 N BARRIER NPNH);
	## ...ja govve galle joga leat Deanučázádaga njuoskkadagas.


# nubbi
# - - -
SELECT Ord IF (*-2 ("vuosttas") OR ("vuosttaš"))(-1 CC OR COMMA)(0 ("nubbi"));
SELECT Ord IF (0 ("nubbi"))(*1 CC OR COMMA LINK 1 ("goalmmát")); 
	## Vuosttaš vuojahaga fávllimus čuolda lea goaratčuoldan ja
	## nubbi čuolda fas gáddeoal-čuoldan.
	## Nubbi noađđeheargi goallustuvvo giehtahearggi lávžái ja goalmmát
	## fas dan nuppi lávžái.

SELECT Gen IF (0 ("nubbi"))(1 N);
	## Son oaččui čáda ášši fas nuppi Stuorradikkis.


SELECT Indef IF (0 ("nubbi"))(*1 CC OR COMMA LINK 1 ("nubbi"));
SELECT Indef IF (0 ("nubbi"))(*1 COMMA LINK 1 CC LINK 1 ("nubbi"));
        ## Nubbi lei dáža, muhto nuppi bearrašis lei isit sápmelaš.
	## Nubbi váldojuvvo, ja nubbi báhcá.

SELECT Indef IF (*-2 ("nubbi"))(-1 CC OR COMMA)(0 ("nubbi"));
	## Čielggadeapmi lea guovtteoasát - nubbi ieš vuoigatvuođa-
	## gažaldagaid birra ja nubbi dán suorggi siskkabeale hálddahus-
	## ortnegiid birra.

SELECT Indef IF (*-2 ("okta" Num))(-1 CC OR COMMA)(0 ("nubbi"));
	## Okta ášši lea mii lágas mearriduvvui, nubbi lea man kommišuvnnat 
	## čuvvo mearrádusaid.
	## Riektegeavadis sáhtášii danin geavahit guokte váldolinjjá,
	## okta ovdal ja nubbi fas maŋŋá 1970.

SELECT Indef IF (-1 ("unnimustá") OR ("unnimusat"))(0 ("nubbi"));
	## Mánáin lea ovttastallanvuoigatvuohta unnimustá nuppiin váhnemiin.

REMOVE Attr IF (0 ("nubbi"))(1 Dem OR Adv);
	## ... ja nubbi dán suorggi siskkabeale hálddahusortnegiid birra.
	## ...nubbi fas rávesolbmuid psykiatriijas.


# Remove the nominal reading of "nubbi"
# - - - - - - - - - - - - - - - - - -  -

 # The idea is that "nubbi" is a noun only when it is not followed by
 # a noun with which it agrees in case. Some of the case combinations may
 # still hide a noun "nubbi", but no errouneous hits were found in the corpus.

REMOVE N IF (0 ("nubbi" Nom))(*1 (N Nom) BARRIER NOT-ADJ);
REMOVE N IF (0 ("nubbi" Gen))(*1 (N Gen) BARRIER NOT-ADJ);
        ## 22. b. 1902 lága nuppi lađđasa a bustávas. 

REMOVE N IF (0 ("nubbi" Acc))(*1 (N Acc) BARRIER NOT-ADJ);
REMOVE N IF (0 ("nubbi" Gen))(*1 (N Ill) BARRIER NOT-ADJ);
        ## Vuosttas ja nuppi lađđasii:

REMOVE N IF (0 ("nubbi" Gen))(*1 (N Loc) BARRIER NOT-ADJ);
        ## ... ja nuppi teakstaoasis lea válddálas

REMOVE N IF (0 ("nubbi" Com))(*1 (N Com) BARRIER NOT-ADJ);
        ## Nuppiin sániiguin daddjon lea sáhka oasis gáddevuoigatvuođas.

REMOVE N IF (0 ("nubbi" Ess))(*1 (N Ess) BARRIER NOT-ADJ);
        ## Rávdná lea leamaš SÁNSa jođiheaddjin ja NSRa nubbin jođiheaddjin.

#REMOVE N IF (-1 Gen OR Dem)(0 ("nubbi" Ess))(*1 N BARRIER NOT-ADJ);
        ## Rávdná lea leamaš SÁNSa jođiheaddjin ja NSRa nubbin jođiheaddjin.
 # I tried CASE instead of Ess, but then the rule did not fire.

# okta
# - - -
SELECT Gen IF (-1 BOS OR CS OR (":" CLB) OR REALCLB)(0 ("okta"))(*1 Loc BARRIER NOT-ADJ);
	## Ovtta ovddasteaddjis divššohasa ovddas lea vuoigatvuohta...


# Indefinite pronouns
# ===================

# General rules
# -------------
SELECT (Pron Attr) IF (0C Pron)(*1C N BARRIER NOT-ADJ-PCLE OR go);
        ## Dan muitalii muhtin mátkkošteaddji gean namma lei Keilhau.
        ## Njuolggadusain ledje eará ge mearrádusat.

SELECT Attr IF (0 Pron)(1 (A Attr))(*2 HNOUN BARRIER NP-BORDER);
	## ...je eará almmolaš dahje priváhta doaibma.

SELECT Attr IF (1 ("láhkai"));
	## Seammá láhkái go váldojournála ja lassijournála geavaheami oktavuođas...
REMOVE Attr IF (0 Pron)(1 (Pron Interr));
	## Lea unnán maid sáhttit dahkat.
	
REMOVE (Indef Attr) IF (1 (Pron Pers Loc));
    ## Ja muhtun sis viegai dakkaviđe su lusa.        

SELECT Indef IF (*-1 Neg)(0 (Interr Foc));  
SELECT Indef IF (-1 Indef)(0 (Interr Foc));
	## Ale divtte geange badjelgehččat du.
	## Várut dadjamis maidege geasage dan birra! 

SELECT Indef IF (*-1 ("jus") OR ("jos") BARRIER CS)(0 Interr);  
SELECT Indef IF (-1 ("menddo"))(0 N);
	## Jus guhtege dadjá áhččásis dahje eadnásis:...
	## ...ahte doaimmat eai juogaduvvo menddo ollugiidda.

SELECT Indef IF (0 (Interr Foc))(NOT *1 VFIN);
	## Dat oste guđege sabehiid.

SELECT Indef IF (*-1 BOS OR CS OR (":" CLB) BARRIER NOT-ADV)(*1 N BARRIER NPNH);
	## Ollu amerihkálaččat doivot Oslo Ruoŧa oaivegávpogin.

REMOVE Adv IF (*-1 ("leat") BARRIER VERB OR CC OR CLB OR CS OR CP)(0 Indef)
	(NOT 1 A)(NOT *1 VERB BARRIER CC OR CLB OR CS OR CP);
	## Doppe ledje ollu olbmot.
	## Diet gal lei beare unnán.
 # Indefinite pronouns, not adverbs, if there is no verb other than 'leat'.

REMOVE Adv IF (*-1 CC OR CLB BARRIER NOT-ADV)(0 Indef)(NOT 0 Loc)
	(*1 N BARRIER NPNH);
	## Dán áššis, nugo eanaš eará áššiin,...

REMOVE Adv IF (-1 ("sihke"))(0 Indef)(*1 N BARRIER NP-BORDER);
	## Dat gáibida almmatge sihke ollu áiggi ja resursaid.
	
REMOVE Adv IF (1 Interr);
    ## Attášii sutnje buot maid dárbbašit.
    ## Danne lea váttis čohkket barggu buot surggiide oktanaga.
    # Possibly sentences such as: 
    ## Son lea njuoskan buot, mii lea fuones ášši.
    # could be counterexamples, but until now a comma is supposed to be inbetween.



# Particular indefinite pronouns
# ------------------------------

# This is an important section. It should first be expanded
# and then probably generalised according to classes of 
# indefinite pronouns.

# buot
# - - -

SELECT Adv IF (0 ("buot"))(1 (A Superl)) ;
	##

SELECT (Pron Indef) IF (0 ("buot"))(*1 N BARRIER NPNH);
	## Mii addet bálvalusfálaldaga buot álbmogii.
 
SELECT (Pron Indef) IF (0 ("buot"))(1 Num)(*2 N BARRIER NPNH);
	## De fertejit buot vihtta miellahtu oassálastit.

SELECT (Pron Indef) IF (0 ("buot"))(1 PrfPrc)(*2 N BARRIER NOT-A);
	## ahte buot náitalan olbmuin galggai leat guolástanvuoigatvuohta.

SELECT (Pron Indef) IF (0 ("buot"))(1 V-PL3);
	## Vuollásaš ráđit buot heaittihuvvojedje oktasaš hálddahuslaš jođiheapmái.
	
# eará
# - - -

SELECT (Indef Attr) IF (0 ("eará"))(1 PrfPrc)(2 N);
	##  ...ja eará vuostáiváldojuvvon dokumeanttaid...

# earri
# - - -

REMOVE ("earri" N Sg Acc PxSg2) IF (-2 (V Pl3))(-1 COMMA)(0 ("eará"))(1 (V Pl3));
	## Muhtamat čállet, earát ožžot jorgalan ja bagadallanveahki dán bargui.

REMOVE ("earri" N Sg Acc PxSg2) IF (-1 ("buot") OR ("máŋga"));
	##  Buot earát.

REMOVE ("earri" N Sg Acc PxSg2) IF (NOT *-1 Sg2)(*1 (V Pl3));
	## ... go dat maid earát vásihit.

REMOVE ("earri" N Sg Acc PxSg2) IF (1 (Pron Rel Pl));
	## ... ja earát geat háliidit ...

# juoga
# - - -
SELECT (Indef Nom) IF (-1 CLB)(0 ("juoga"))(1 Interr);
	## ..., juoga mii boahtá ovdan Romssa universitehta plánain.

# nubbi
# - - -
# Pron Indef or A Ord

SELECT (Pron Indef Gen) IF (0 ("nubbi"))
	(1 ("dáfus" Po) OR ("beali" Po) OR ("hávvi") OR ("beallái" Po)
	OR ("bealli") OR ("sohka#bealli"));
	## Nuppe dáfus deattuha suohkan, ahte...

SELECT (Pron) IF (*-1 ("nubbi"))(0 ("nubbi"));
SELECT (Pron) IF (0 ("nubbi"))(*1 ("nubbi"));


# ollu
# - - -
SELECT Pron IF (-2 Pron)(-1 CC)(0 ("ollu"));
	## Váilos unnán dahje ollu.

SELECT Pron IF (-1 ("seamma"))(0 ("ollu"))(*1 N BARRIER NPNH);
	## Jus eanebut leat ožžon seamma ollu jienaid, de mearriduvvo boađus vuorbádemiin.

# seamma/seammá
# - - - - - - - 

#SELECT Attr IF (-1 Num)(0 ("seamma") OR ("seammá"))(1 A OR N ); # Old version.
SELECT Attr IF (0 ("seamma") OR ("seammá"))(*1 A OR N BARRIER NOT-NUMERALS );
	## Dasto smiehttatge fas seamma sullasaš jearaldagaid. 
	## Sáhttá leat ulbmillaš, ahte seamma olmmoš áimmahuššá máŋga
	## dain doaimmain.

SELECT Attr IF (0 ("seamma") OR ("seammá"))(1 Adv OR ("ollu" Pron))(NOT 1 VERB OR ("gusto")) ;
	## Mu oainnu mielde sáhttá seammá bures oaidnit dan láhkai, ahte... 
    #  The "gusto" here should be removed by a set of sentence adeverbials

# Then we kill the rest of the attributes...
# But did we catch enough attributes?    

#REMOVE Attr IF (0 ("seamma") OR ("seammá")) ;
	## Seammá gusto poliklinihkalaš divššus. 
    ## Seamma gusto poliklinihkalaš divššus. 

# unnán
# - - -
SELECT Indef IF (*-1 COPULAS BARRIER V-NOT-AUX)(0 ("unnán"))
	(NOT 1 PrfPrc LINK *1 NP-HEAD-ACC BARRIER NPNH);
	## Dat gal lei beare unnán.
	## Sii leat hui unnán dahkan guorahallat "sámi vieruid ja riekteáddejumiid".
	## Eanetlohku lea menddo unnán bidjan Finnmárkku mihtilmas diliid vuođđun.

 # In the last example "unnán" is an Adv. Hence the last condition. 
 # Alternative: (NOT 1 PrfPrc LINK 1 Inf)?


# Demonstrative pronouns
# ======================

REMOVE Pcle IF (-1 Interr OR CC)(0 ("dat"));
	## Maid dat Kárášjogas bargá?
	## Dahje dat gávccenuppelogis geat sorbmašuvve...

SELECT Dem IF (NOT -1 VERB)(1 (Indef Attr));
	## Nuppi geahči dan seammá lávžžis fas biddjojuvvo bađa birra.

REMOVE (Dem Sg) IF (0 ("dat" Sg))(NOT *1 (N Sg) BARRIER NOT-NP LINK 0 HNOUN)
	(NOT 1 PrfPrc LINK *1 (N Sg) BARRIER NP-BORDER)(NOT 1 (Indef Sg));  
	## .. ja dan golut leat govttolaččat veahki ektui.

REMOVE (Dem Pl) IF (0 ("dat" Sg))(NOT *1 (N Pl) OR (Indef Pl) OR NUMERALS 
	BARRIER NOT-NP)(NOT 1 NP-HEAD-GEN LINK 1 (Actio Gen) LINK 1 (N Pl));
	## ...ja ahte dat dáhttu čielgasit berre doahttaluvvot. 

REMOVE Dem IF (0 Pers)(1 Dem);
	## ...beroškeahttá das makkár geavahus leamaš dolin.


# makkár (Negative polarity demonstratives)
# - - - 

SELECT Attr IF (*-1 Neg BARRIER NOT-VERB)(0 (Pron Dem Attr Foc))(1 N);


# DP-internal number concord
# --------------------------
SELECT Sg IF (0C (Pron Dem))(*1C (N Sg) BARRIER NPNH);
	## Dát olmmái bilkida Ipmila.

SELECT (Dem Sg) IF (*-1 BOS OR CS OR (":" CLB) BARRIER NOT-ADV-PCLE)
	(*1 (N Sg) BARRIER NPNH LINK NOT 0 (N Pl))(NOT 1 VFIN)
	(NOT 0 Pers LINK 1 CC);
	## Dát ohcan lea juhkkojuvvon golmma departementii.

SELECT Pl IF (0C (Pron Dem))(*1C (N Pl) BARRIER NOT-ADJ LINK 0 HNOUN);
	## Naba daid stuora dorskiid, gosa daid bidjet? 

SELECT (Dem Sg Nom) IF (0 ("dat"))(1 ("gal") OR Pcle)(2 (N Sg Nom))(NOT 2 PrfPrc);
	## Láve dat gal njiŋŋelas boazu ge muhtomin adnojuvvot noađi guoddit.
	## Ii lean gal eambbo go 11 liekkasceahki, muhto ii dat gal orron hehttemin su vuojadeamis.
	# This rule should not take the latter one.


# DP-internal case concord
# ------------------------

# More rules for Dem in front of NUMERALS must be added.


# Nominative

SELECT (Dem Sg Nom) IF (0 ("dat"))(*1 (N Sg Nom) BARRIER NOT-ADJ 
	LINK 0 HNOUN LINK *1 V-SG3 BARRIER CC OR CS OR PUNCT);

SELECT (Dem Sg Nom) IF (0 ("dat"))(*1C HNOUN BARRIER NOT-ADJ LINK 0 (Sg Nom)
	LINK *-1 V-SG3 BARRIER CS OR CP);
	## Dalle dat geahppaset gurppas daddjojuvvo veajan. 
	## Ii han ovttasge du sogas leat dat namma. 

SELECT (Dem Pl Nom) IF (*1 (N Pl Nom) BARRIER NOT-ADJ LINK 0 HNOUN 
	LINK *1 V-PL3 BARRIER CS);
SELECT (Dem Pl Nom) IF (*1 (N Pl Nom) BARRIER NOT-ADJ LINK 0 HNOUN 
	LINK *-1 V-PL3 BARRIER CS OR CP);
	## Dát vejolašvuođat leat dehálaš eallindiliid eavttut álbmogii.
	## Noađđespagáin leat dat geažit guokte golbma tumá guhku.

SELECT (Dem Pl Nom) IF (*1 (N Pl Nom) BARRIER NOT-ADJ LINK 0 HNOUN
	LINK *1 V-DU3 BARRIER CS);
SELECT (Dem Pl Nom) IF (*1 (N Pl Nom) BARRIER NOT-ADJ LINK 0 HNOUN 
	LINK *-1 V-DU3 BARRIER CS OR CP);
	## Go dat geažit mat geaigut leaba bajás, gohčoduvvoba spahká-oaivin.
	## No hits in corpus for the last rule, but it looks OK!

SELECT (Dem Pl Nom) IF (0 ("dat"))(1 NUMERALS LINK 0 Nom);
	## ...ožžo dat 50 vuotnaguolásteaddji Gáivuonas buhtadasa.

SELECT (Dem Pl Nom) IF (1 Nom)(2 (Pron Interr Pl));
	## No examples in corpus? 
	
REMOVE (Nom) IF (*-1C (Gen @DN>) OR (Acc @DN>) BARRIER NOT-ADJ);
    ## SDD áigu suokkardit Sámedikkiin, sáhttágo dán plána doaimmaid čađaheami delegeret Helse Nordii.
    # This is a rule disambiguating between nominative and genitive/accusative
    # Only a couple of words in Northern Sámi need to be disambiguated with respect to that.

# Accusative

SELECT (Dem Acc) IF (0 ("dat"))(1C (Num Acc) OR (Indef Acc));
	## Tabealla vuosttaš ceakkočuolddas oaidnit dan golbma bajitdási
	## váldofáddajoavkku.

# Genitive

SELECT (Dem Gen) IF (0 ("dat"))(1C (Num Gen))(NOT 0 (Dem Pl) LINK 1 ("okta"));
SELECT (Dem Gen) IF (NOT *-1 VERB OR CC BARRIER NOT-ADV-PCLE)(0 ("dat"))
	(*1C (N Gen) BARRIER NOT-ADJ LINK 0 HNOUN);
	## Davimus guovllut ledje dálá Ruošša ja daid guovtti davvi-
	## ovttastumiid oktasaš vearroeanan.
	## ...de lei stáhta dan eatnama oamasteaddji maid. 

SELECT (Dem Sg Gen) IF (NOT *-1 VERB BARRIER NOT-ADV-PCLE)(0 ("dat"))
	(*1C (N Sg Ill) BARRIER NPNH);
	## Nubbi noađđeheargi goallostuvvo giehtahearggi lávžái ja goalmmát
	## fas dan nuppi lávžái.
	
SELECT (Dem Sg Gen) IF (1 ("láhki" Sg Ill));	
	## Lean smiehttan dan láhkái.
	
SELECT (Dem Pl Ill) IF (0 ("dat"))(*1C (N Pl Ill) BARRIER NOT-NP);
	## Láhka gusto daidda dearvvašvuođahálddahusa dearvvašvuođa-
	## dieđuid gieđahallamiidda.

 # Disambiguating (Pl Ill) is only a question of distinguishing between 
 # Pers and Dem. Accordingly, the barrier can be less strict.

SELECT (Dem Sg Gen) IF (NOT *-1 V-TRANS-ACT BARRIER (@OBJ) LINK NOT *-1 (@OBJ) 
	BARRIER CS OR CP)(0 ("dat"))(*1C (N Sg Loc) BARRIER NPNH);
	## Ja dan báhkka cizas lea fierpmit dassá go dat čoasku.

SELECT Dem IF (0 ("dat"))(0C Loc)(*1 (N Pl Loc) BARRIER NPNH); 
	## Seamma olmmoš áimmahuššá máŋga dain doaimmain dahje rollain. 

SELECT Dem IF (0 ("dainna"))(*1 (N Sg Com) OR (N Pl Loc) BARRIER NPNH);
	## ...suollemas dainna doaivvuin ahte... 
	## Guhtemuš dainna bivdovugiin guhkimus leaš geavahuvvon...

 # No disambiguation between Sg Com and Pl Loc here. All we do is 
 # distinguish between Dem and Pers.
 # As for Dem Pl Com, it is taken care of by the first rule for DP-internal
 # number concord above. 

REMOVE Dem IF (0 ("dainna"))(NOT *1 (Sg Com) OR (Pl Loc) BARRIER NPNH);
	## Dainna Máret lávii čiŋadit.

SELECT (Dem Pl Gen) IF (0 ("dat"))(*1C (N Pl Com) BARRIER NPNH);
	## ...ja daid lunddolaš rájáiguin ássanguovlluid siiddaid gaska. 

SELECT (Dem Pl Com) IF (*1C (N Pl Com) BARRIER NPNH);
	## Sámediggi čujuhii ahte ain ferte joatkit daiguin sierra doaibmabijuiguin.

REMOVE Acc IF (*-1 BOS OR CS OR (":" CLB) BARRIER NOT-ADV-PCLE)(0 Dem)
	(*1 (N Loc) OR (N Com) BARRIER NPNH);
	## Dán mearrádusas ii leat lagabuidda definerejuvvon...

REMOVE (Dem Acc) IF (0 ("dat"))(1 ADVLCASE);
	## De našunálastáhtat válde daid háldoseaset.

REMOVE (Dem Acc) IF (NOT *-1 V-TRANS-ACT BARRIER CS OR CP)(1 N)(1 ADVLCASE);
	## Mun vuolggán vel dán vahkus.
	
SELECT (Dem Sg Gen) IF (*1 VAHKKU BARRIER NOT-ADJ LINK 0 Gen OR Loc); 
	## Olbmot juige dan galbma ijas. 	

REMOVE (Dem Acc) IF (*-1 V-TRANS-ACT BARRIER NOT-ADV-PCLE)(1 N)(1 ADVLCASE)
	(*1 NP-HEAD-ACC BARRIER CS OR CP OR CLB OR CC);
	## ...geas lea ovddasvástádus ollašuhttit dán suorggis giellalága 
	## gáibadusaid.

REMOVE (Dem Nom) IF (0 ("dat"))(NOT *1 (N Nom) BARRIER NPNH);
REMOVE (Dem Sg Loc) IF (0 ("dat"));
REMOVE (Dem Sg Ill) IF (0 ("dat"));
	## Maid dat Kárášjogas bargá?
	## ...ja lea vuoigatvuohta oažžut das máŋgosa sierranas jearrama
	## vuođul.
	## Olggosaddin sáhttá liikká dáhpáhuvvat jos gávdnojit dasa
	## beaktilis sivat.

REMOVE (Pron Pers Sg3 Gen) IF (NOT -1 Pr)(0 (Pron Dem Sg Gen))
	(*1 (N Sg Gen) BARRIER NOT-ADJ) ;
	## Dieđuid galgá heivehit dan sámi guvlui, gos mánát orrot.


# Essive

#REMOVE Dem IF (0 Ess)(NOT *1 (N Ess) BARRIER NPNH); # duon
# Removed because 


# Personal pronouns
# =================

SELECT Sg3 IF (0 (Pron Pers Sg3 Nom))(1 COMMA)(2 (Pron Rel Sg));
	## Dat, mii oktii leamaš riekta, ii dárbbaš leat šat riekta dál.


# Some rules for personal pronouns again (will hit "dat", except the last one)
# ---------------------------------------------------------------------------- 

SELECT (Sg3 Nom) IF (*-1 (V Sg3) BARRIER CC OR CS OR CLB OR CP OR PUNCT)
	(NOT *1 (N Sg Nom) BARRIER NPNH); 
	## Iihan dat liikon Kárášjohkii?

SELECT (Sg3 Nom) IF (*1 (V Sg3) BARRIER VFIN OR CS OR CP LINK NOT 0 (N Pl))
	(NOT *-1 V-SG3 BARRIER NOT-ADV OR CLB)
	(NOT 0 Pl3 LINK *1 V-DU3 BARRIER VERB OR CS OR CP);
	## Ja dan báhkka cizas leat fierpmit dassá go dat čoasku.

SELECT (Pl3 Nom) IF (*-1 (V Pl3) BARRIER CC OR CS OR CLB OR CP OR HNOUN-NOM);
SELECT (Pl3 Nom) IF (*1 (V Pl3) BARRIER VFIN OR CS OR CP);
	## Eai dat leat vel giksan.
	## Dathan gal leat buorit guolit.

SELECT (Pl3 Nom) IF (*1 V-DU3 BARRIER VERB OR CS OR CP);
	## Duo duon rukses biillas dat čohkkába.

SELECT (Pron Sg3) IF (1 (Pron Interr Sg));
SELECT (Pron Sg3) IF (1 COMMA)(2 (Pron Interr Sg));
	## ...go dušše dat mii guoská reguleremii.  
	## Dat, gii ákkastallá...
 
SELECT (Pron Pl3) IF (1 (Pron Interr Pl));
SELECT (Pron Pl3) IF (1 COMMA)(2 (Pron Interr Pl));
	## Dat mat sáhttet háhkkojuvvot, leat ovdalis juo namuhuvvon.
	## ...go dat, mat leat namahuvvon vuosttas lađđasis, galget...

SELECT (Pron Sg3 Nom) IF (*1 ("ahte") BARRIER WORD);
	## Dat, ahte Nielsenii eai lean čájehan mákkarge hávdesajiid, 
	## sáhttá čuovvut máŋgga áššis.

REMOVE (Pers Acc) IF (*1C (N Acc) BARRIER (Actio Loc) OR CS OR CP OR Po OR ("dego")
	LINK NOT 0 TIME);
	## De lea sihke divššohas ja su lagamus oapmahaččas vuoigatvuohta 
	## oažžut dieđuid.
	## Min áiggi lea dat gildojuvvon.
	## Go ii oktage vuosttildan dan vuoigatvuođadili...


# Dem or Pers
# -----------

SELECT Dem IF (0 ("dan"))(*1 N BARRIER NOT-ADJ LINK 1 CS OR Interr);
SELECT Dem IF (0 ("dan"))(*1 N BARRIER NOT-ADJ LINK 1 COMMA LINK 1 CS OR Interr);
	## dan teoriija maid
	## dan teoriija, go
	
# Number
# ------

# refering forward to the subjectpredicate

SELECT Sg3 IF (0 (Pron Pl3) LINK 0 Nom)(*1 Sg LINK 0 (@SPRED) BARRIER (@SUBJ) OR CS OR CP OR CC);
    ## Lean mun gal oaidnán čakčašeavdnjadiin dola rievdamiin čázi alde Deanus, muhto dat leamaš vissa gobmi.

SELECT Pl3 IF (0 (Pron Pl3) LINK 0 Nom)(*1 Pl LINK 0 (@SPRED) BARRIER (@SUBJ) OR CS OR CP);

# Disambiguating against other POS
# --------------------------------
 # Getting rid of problematic names.
 # Problematic here are the names Dan, Dien, and the essive of the noun duo.

REMOVE ("Dan" Prop Sg Nom) IF (-1 BOS)(0 Dem)(1 N); #(NOT 1 Prop); 
REMOVE ("Dien" Prop Sg Nom) IF (-1 BOS)(0 Dem)(1 N);
	## Dan nieidda mun dovddan. 

REMOVE (Dem Sg Nom) IF (NOT *-1 V-SG3 BARRIER CS)(NOT *1 (N Sg Nom) BARRIER NPNH)
	(NOT *1 V-SG3 BARRIER CS);
	## Dasto leat dát iešguđetge doaibmabijut juhkkojuvvon...

REMOVE (Dem Pl Nom) IF (NOT *-1 V-PL3 BARRIER CS)(NOT *1 (N Pl Nom) OR NUMERALS
	BARRIER NPNH)(NOT *1 V-PL3 BARRIER CS);  
	## Amma duot lei Niillasa guollebiila?

 # The verb is there to capture intransitive Dem.

REMOVE ("Lea" Prop Sg Nom) IF (-1 BOS)(NOT *1 Sg3 BARRIER CLB OR CS);
	## Lea dehálaš deattuhit, ahte IKT ii doaimma.


# ========================= #
# Disambiguating adjectives #
# ========================= #
         CONSTRAINTS #13    #
# ========================= #

# Rules related to specific adjectives
# ====================================


# alit
# - - 

SELECT ("allat" A Comp Attr) IF (*1 ("oahpu") OR ("dutkan") OR ("skuvlla") BARRIER NPNH);
	## Oahppo- ja dutkanministtar dat lea ráhkadan dieđáhusa alit sámi oahpu ja dutkama birra.

# dološ
# - - - - 
SELECT A IF (0 ("dološ"))(1 N);
	## ...go lea gažaldat háhkamis dološ áiggi rájes geavaheami...

# garra
# - - -
 # These rules are here to remove "garra" N Sg Nom.
SELECT Attr IF (0 ("garas"))(1 (N Nom)); 
SELECT Attr IF (NOT *-1 V-SG3)(0 ("garas" A Attr))(1 A OR N)(NOT *1 V-SG3);
	## Norggas lea leamaš gal garra digaštallan sámi oahppoplána hárrái.
	## Beatnagat eai biva viššat garra buollašiid.


# nanus
# - - -

SELECT A IF (-1 ("hui"))(0 ("nanus"));
	## hui nanus.
	
# sierra
# - - - -
SELECT (A Attr) IF (0 ("sierra"))(*1 N BARRIER NPNH);
	## Sámelága vuođul lea ásahuvvon sierra ovddasteaddji orgána.

# stuoris
# - - - -
SELECT Attr IF (-1C N)(0 ("stuorát"))(1C (N Pl))(1 HNOUN); 
	## ...leat viiddis njuolggadusat áššemeannudeapmái stuorát 
	## čázádatmuddemiid oktavuođas.

# surgat
# - - - -
SELECT (A Sg Nom) IF (0 ("surgat"))(0 (@INTERJ));
	## Hei surgat, lea divrras!

# veara
# - - -

REMOVE N IF (-1 (Actio Gen))(0 ("veara"));
	## ...buot dan mii leat ráhkistan ja gudnejahttin veara.

REMOVE N IF (*-1 (@OBJ) BARRIER NOT-NP)(0 ("veara"));
	## De bođii geafes leaska ge gii bijai guokte smávvaruđaža, 
	## moatte šlántte veara.

REMOVE N IF (0 ("mearkkašan#veara") OR ("namuhan#veara"));
	## Buhttenrievttálaččat ii nuppástuvvon mearkkašanveara...

 # We would like to have a more general rule that could hit all compounds 
 # with "-veara", but since that is not possible yet we list some relevant 
 # words instead.

# vejolaš
# - - - -

SELECT (@CS-COMPL) IF (*-2 A OR Adv LINK -1 ("nu"))(-1 ("go"));
    ## Sámi boarrásat sápmelaččat galget nu guhkás go vejolaš beassat dovdat oktiigullevašvuođa.
    ## Danne dárbbašuvvo nu buorre gulahallan go vejolaš Sámedikkiin.
    # BARRIER?
    
# vuolit
# - - - -
SELECT Attr IF (0 ("vuolit"))(1 ("gráda") OR ("dássi"));
	## Skuvla dušše addá oahpu vuolit dásis. 


# Comparative
# ===========

SELECT Comp IF (NOT -1 ("nu") OR ("seamma") OR CC)
	(NOT 0 (Pl Nom) LINK -1 Comp OR COMPAR LINK *-1 COPULAS BARRIER 
	NOT-ADV-PCLE)(*1 ("go" Pcle) BARRIER NOT-N);
        ## Dážat navde Finnmárkku buoret guovlun go iežaset ruovttubáiki.

SELECT Comp IF (-1 ("mihá"));
SELECT Comp IF (-1 ("olu"))(NOT 0 (Pl Nom) LINK *1 V-PL3 BARRIER NOT-ADV-PCLE);
	## Nuppe dáfus ledje mihá eanebut geat bivde lagabus gátti.
	## ...ja danin lea buot nu olu álkit inuihtaide. 

REMOVE Adv IF (*-1 ("go" Pcle) BARRIER NPNH OR CC)(*1 N BARRIER NOT-ADJ-CC);
	## Alimusriekti ii badjelgeahččange sápmelaččaid nu sakka go vuolit
	## instánssa duopmu.


# Attribute disambiguation
# ========================

# Only Attr if something to lean on
# ---------------------------------

REMOVE Attr IF (NOT *1 N BARRIER NPNHCCC)
    (NOT 1 PUNCT-LEFT LINK *1 PUNCT-RIGHT LINK *1 N BARRIER NPNHCCC);
    ## Dalle sáhttá beaktilit háhkat guovtte ja maiddái golmmagielat (maiddái suomagielat) gelbbolašvuođa .
    



# Rules for Attr between Dem and N
# --------------------------------
SELECT (A Attr) IF (*-1 (Dem Sg Nom) BARRIER NPNH)(*1 (N Sg Nom) BARRIER NPNH);
SELECT (A Attr) IF (*-1 (Dem Pl Nom) BARRIER NPNH)(*1 (N Pl Nom) BARRIER NPNH);
	## Dalle dat geahppaset gurppas daddjojuvvo veajan.
	## ...jus dát máinnašuvvon árbevirolaš dilálašvuođat eai
	## geahččaluvvo čuvgejuvvot.

SELECT (A Attr) IF (*-1 (Dem Sg Acc) BARRIER NPNH)(*1 (N Sg Acc) BARRIER NPNH);
SELECT (A Attr) IF (*-1 (Dem Pl Acc) BARRIER NPNH)(*1 (N Pl Acc) BARRIER NPNH);
	## ...ja son doaimmaha dán dábálaš dahje eanaš geavaheami.
	## Nuppe beales geahččalit dahkat dáid siskkáldas áddejumiid
	## duođaštanfáddán.

SELECT (A Attr) IF (*-1 (Dem Sg Gen) BARRIER NPNH)(*1 (N Sg Gen) BARRIER NPNH);
SELECT (A Attr) IF (*-1 (Dem Pl Gen) BARRIER NPNH)(*1 (N Pl Gen) BARRIER NPNH);
	## Dán konkrehta háve gustojedje gáržžideamit nappo earáide go
	## boazosapmelaččaide.  
	## No cases left after the Dem Pl Acc-Attr-N Pl Acc rule? 

SELECT (A Attr) IF (*-1 (Dem Sg Gen) BARRIER NPNH)(*1 (N Sg Ill) BARRIER NPNH);
SELECT (A Attr) IF (*-1 (Dem Pl Ill) BARRIER NPNH)(*1 (N Pl Ill) BARRIER NPNH);
	## Dás lei maiddái gažaldat geavahanvuoigatvuođas vuovdái ja dan
	## vejolaš viidodahkii. 
	## ...mat gullet dáidda ja eará lunddolaš sullasaš doaimmaide. 

SELECT (A Attr) IF (*-1 (Dem Sg Gen) BARRIER NPNH)(*1 (N Sg Loc) BARRIER NPNH);
SELECT (A Attr) IF (*-1 (Dem Pl Loc) BARRIER NPNH)(*1 (N Pl Loc) BARRIER NPNH);
	## Ja dan báhkka cizas leat firpmit dassá go dat čoasku.
	## Sápmelaččat ledje vuosttasin dahje okta dain vuosttaš álbmot-
	## joavkkuin,... 
	## ...dainna váilevaš vejolašvuođain...

SELECT (A Attr) IF (*-1 (Dem Pl Com) BARRIER NPNH)(*1 (N Pl Com) BARRIER NPNH);
	## Geainnuin livččii eambbo turistajohtolat suohkana čoahkke-
	## ássanguovddášiid čađa daiguin ekonomalaš oalgeváikkuhusaiguin.

SELECT (A Attr) IF (*-1C Dem BARRIER NPNH)(*1 N BARRIER NOT-ADJ-CC)
	(NOT 0 N LINK *1 VFIN BARRIER NOT-ADV);
	## Mo Alimusriekti meannudii vaikkoba dakkár vuolit dási olbmuid
	## vihtančilgehusaigun?


# Other attribute rules
# ---------------------

SELECT Attr IF (0 Prop LINK 1 ("guovttos")); 
    ## Gaskaija áiggi Bávlos guovttos Silasiin rohkadalaiga ja lávlluiga.
    # a. also without the LINK to Com? b. larger set than only "guovttos"?
    
REMOVE VFIN IF (-1C VFIN)(NOT 0 ConNeg);
	## Mun áiggun oastit biilla.
	## Gos dáppe lea máddi?
	## Ánne-Sofe buvttii varas biergguid.
	## Máhtte manai čuoigga skuvlii.
	## Dan dihte go lea čađat dárbu ođasmahttit jorgalusaid.
 # As we see, this rule can lead to a verbal, a nominal, an adjectival or an
 # adverbial reading. It comes here because this is where it is first needed.

REMOVE (A Attr) IF (1 CC)(2 N)(NOT 2 (A Attr))
	(NOT 2 PrfPrc LINK *1 N BARRIER NPNH);
	## ...jus doaimma ovttadagat sihke fágalaččat ja lágideami bokte leat...

SELECT (N Attr) IF (NOT 0 Prop)(1 N);
	## Máttimus sápmelaččat eahpidedje dáža dutkiid eanet go davit sápme-
	## laččat.

REMOVE Adv IF (NOT *-1 VERB BARRIER CS OR CP)(NOT 0 ("gosa") OR ("mielde"))
	(NOT *1 VERB BARRIER CS OR CP);
	## Diehtojuohkin divššohasa lagamuš oapmahažžii.
	## Stuorát guolit mat eai bisán čalmmiide, mannet čađa.
 
SELECT (A Attr) IF (*-1 BOS OR CS OR (":" CLB) BARRIER NPNHA)(NOT 0 VERB)(NOT 1 Dem OR VERB)
	(NOT 0 Prop LINK -1 (Prop Attr) LINK 2 CC)
	(NOT 0 Prop LINK 1 CC LINK 1 Prop LINK NOT 1 Prop)
	(*1 N BARRIER NPNH LINK NOT 1 Po)(NOT 1 COMMA LINK 1 N);
	## Golbma čáppa, doaimmalaš nieidda livžo áhčiset riegádanbeaivve.

SELECT (A Attr) IF (-1 COPULAS)(1 A OR N LINK NOT *1 Po OR Loc OR Inf BARRIER NP-BORDER)
	(NOT 1 Adv OR VERB)(*2 go BARRIER NOT-NP);
	## Sis-Finnmárkkus lea vuolit gaskamearálaš bruttoboahtu go fylkkas...

SELECT (A Attr) IF (*-1 COPULAS BARRIER NOT-ADV-PCLE LINK *-1C (@SPRED) BARRIER 
	NOT-ADV-PCLE)(NOT 1 CLB LINK 1 N)(*1 N BARRIER NPNH LINK NOT 1 Po OR Loc);
	## Dieđuid diehtogieđahallama ovddasvástideaddjin leat guovlluguovdasaš 
	## dearvvašvuođadoaimmat.

SELECT (A Attr) IF (*-1 Num BARRIER NPNH)(*1 N BARRIER NPNH)(NOT 0 Prop LINK 1 CC)(NOT 0 ("kr") LINK -1 CARDINALS);
	## Dan dihte ohcat guokte lassi stipendiáhttavirggi sámi/eamiálbmot-
	## guoskevaš dutkamiidda.

SELECT (A Attr) IF (-1 Adv)(NOT 0 Inf)(*1C N BARRIER NPNH)(NOT 1 CLB LINK 1 N)
	(NOT 1 Prop);
	## Mánná viehkalii olggos álás julggiid.
	## Universitehtas lea maiddái čielga nášunála ovddasvástádus...

SELECT (A Attr) IF (NOT *-3 COPULAS BARRIER NOT-ADV-PCLE)(-2 Attr OR PrfPrc OR PrsPrc)
	(-1 CC)(1 N LINK NOT 0 Nom)(NOT 0 (Prop Gen) LINK NOT 1 Prop);
	## Soahpamušas váilot njuolggadusat smávit vahágahtti ja geavatlaččat 
	## bággolonistemiide.
 
REMOVE (A Nom) IF (0 (A Attr))(NOT 0 (Pl Nom) LINK *-1 (V Pl3) BARRIER NOT-ADV-PCLE
	LINK NOT *-1 (Pl Nom) BARRIER CS OR CP OR CC)
	(NOT 0 (Pl Nom) LINK *-1 COPULAS BARRIER NOT-ADV-PCLE LINK 0 Pl3)
	(*1C N OR Cmpnd BARRIER NOT-ADJ OR (PUNCT RIGHT) LINK NOT 1 Po OR PREGEN)
	(NOT 1 Prop OR Ill)(NOT 1 COMMA LINK 1 N)
	(NOT 1 Gen LINK *1 Po OR PREGEN BARRIER NP-BORDER);
	## Dat lea čuovvovaš sajis.

REMOVE (A Gen) IF (0 (A Attr))(*1 N BARRIER NOT-ADJ LINK NOT 1 Po);
REMOVE (A Acc) IF (0 (A Attr))(*1 N BARRIER NOT-ADJ LINK NOT 1 Po);
        ## Sámegiella ja dárogiella leat ovttaárvosaš gielat.
	## Lea deaŧalaš movttiidahttit studenttaide gazzat doavttergrádaoahpu. 
 # The careful mode (*1C N) was removed because of the second example.
 # But reintroduced because of the third example.

 
REMOVE V IF (NOT -1 (N Nom))(0 (A Attr))(*1 N BARRIER NOT-ADJ LINK NOT 1 Po)
	(NOT 0 Inf)(*2 VFIN BARRIER CC OR CLB OR CP OR CS);
        ## Olles doaibmaviessu lea sullii 2300 njealjehasmetera stuoru. 

REMOVE N IF (0 (A Attr))(NOT 1 VFIN OR PrfPrc OR CLB)(NOT 1 PREGEN) 
	(NOT 0 (Sg Nom) LINK *-1 (V Sg3) BARRIER NOT-ADV-PCLE LINK 
	NOT *-1 Nom BARRIER CC OR CP OR CS OR REALCLB)
	(NOT 0 (Pl Nom) LINK *-1 (V Pl3) BARRIER NOT-ADV-PCLE LINK 
	NOT *-1 Nom BARRIER CC OR CP OR CS OR REALCLB)
	(*1 N BARRIER NOT-ADJ LINK NOT 1 Po);
	## ...muhto dakkár radikála rievdadus eahpitkeahttá buktá garra
	## reakšuvnnaid,...

SELECT (A Attr) IF (NOT *-1 COPULAS BARRIER NP-HEAD-NOM OR V-NOT-AUX)
	(NOT 0 Num OR Inf OR Comp OR ("nubbi"))(NOT 0 Adv LINK 1 Gen LINK 1 N)
	(1 N)(NOT 1 VFIN OR Inf OR PREGEN)
	(NOT 0 (N Sg Nom) LINK *-1 V-SG3 BARRIER NPNHA LINK NOT *-1 (@SUBJ)
	BARRIER CS OR CP)
	(NOT *-1 ("go") BARRIER NOT-NP LINK NOT -1 VFIN LINK NOT 1 EOS);
	## Gonagas sáhttá mearridit sierranaš njuolggadusaid báikkalaš
	## dilálašvuođaid ektui.

SELECT (A Attr) IF (NOT *-1 LEAT-FIN-NOT-IMP BARRIER NOT-ADV LINK *-1 BOS BARRIER NOT-ADV)
	(NOT 0 Adv OR Inf)(1 A-CASE LINK 1 CLB  LINK 1 NP-BORDER); 
	## ...main leat mielde dušše priváhta oasálaččat, ii leat...
	## Lea dehálaš deattuhit, ahte IKT ii doaimmá.
	## Lea erenoamáš dehálaš movttiidahttit sámegielat doaktáriid bargat gielddain.

REMOVE Adv IF (-2 A OR N)(-1 ("go" Pcle))(1 N);
	## No longer needed after the introduction of the previous rule?

REMOVE PrsPrc IF (0 (A Attr))(1 N);
        ## Sidjiide maid lei guollerikkis Ruija geasuheaddji guovlu.
 
REMOVE (A Loc) IF (0 Attr)(1 N);
	## Olggosaddin sáhttá liikká dáhpáhuvvat jos gávdnojit dasa beaktilis sivat.


SELECT (A Attr) IF (*-1 BOS OR CS OR (":" CLB) BARRIER NPNHA)(NOT 0 VFIN OR Inf)
	(*1 HNOUN BARRIER NPNH)(NOT 1 VFIN)(NOT 1 COMMA LINK 1 N);
	## Sámedikki vuosttamuš dábálaš dievasčoahkkima maŋŋa...  

REMOVE N IF (-1 Gen OR Dem OR Indef)(0 Attr)(1 N)(NOT 1 VFIN);
	## Oktasaš vuoigatvuođat ledje šaddan dološ áiggi rájes...

REMOVE VFIN IF (-1 Gen OR Dem OR Indef)(0 Attr)(1 N);
	## Lávži goallostuvvo dan ovddit noađđehearggi čeabehii.

REMOVE Adv IF (NOT -2 V-NOT-AUX)(-1 Gen OR Dem OR Indef)(NOT -1 PrfPrc)
	(0 Attr)(1 N);
	## ...galget divššohasa lagamus oapmahaččat oažžut dieđu...

SELECT (A Attr) IF (1 (PUNCT LEFT) LINK *1 (PUNCT RIGHT) LINK 1 N);
	## Dábálaččat earuha váldolaččat gaskal dábálaš (totala) geavahan-
	## vuoigatvuođaid ja sierranas (partiála) geavahanvuoigatvuođaid.

SELECT (A Attr) IF (-1 COMPAR)(1 ("go" Pcle))(2 (A Attr))(*3 N BARRIER NOT-ADJ);
	## Sámedikki ásaheapmi lea eanet ideologalaš go čielga mearrádus.

SELECT (A Attr) IF (NOT 0 Adv OR Num)(1 TIME);
	## Váilui guhkit áiggi lotnolaš oaivila. 

SELECT (A Attr) IF (*-1 ("lassi" Ess) BARRIER NPNH)(*1 Ill BARRIER NPNH);
	## Vuoigatvuohta leat Sámedikkiid jienastusloguin lea lassin dábálaš 
	## jienastanvuoigatvuođa eavttuide.

 
# Coordinated attributes
# ----------------------
SELECT (A Attr) IF (NOT 0 Prop)(1 COMMA)(2 Attr)(3 CC LINK *1 HNOUN BARRIER NPNH);
	## ...go viidáset gaskkusta oppalaččat álbmoga historjjálaš,
	## kultuvrralaš ja politihkalaš dieđuid.

SELECT (A Attr) IF (NOT 0 Prop)(1 COMMA)(2 Attr OR PrsPrc)(NOT 3 N LINK 1 COMMA) 
	(*3 HNOUN BARRIER NPNH);
	## Ferten maid imaštallat go dehálaš, sámepolitihkalaš birrasat
	## barget nu áŋgirit dán ovdii.

SELECT (A Attr) IF (-2 Attr)(NOT -2 Prop)(-1 CC)(1 N OR Actio);
	## Divššohasain lea vuoigatvuohta oažžut čilgejuvvot álkit ja
	## oanehaččat journálas geavahuvvon fágadoahpagiid.

SELECT PrfPrc IF (*-1 CC BARRIER NOT-ADV LINK -1 A);
	## Dilálašvuođaid álgu sáhttá leat dovdameahttun dahje jávkan historjjá
	## seavdnjatvuhtii.

 # The last rule does not hit only prenominal PrfPrc. It is nevertheless useful,
 # since PrfPrc but not Actio coordinates with adjectives in predicate position. 

SELECT (A Attr) IF (1 CC)(2C (A Attr));
SELECT (A Attr) IF (1 CC)(2 (A Attr))(NOT 2 VERB)(3 N OR Cmpnd)(NOT 4 Po); 
	## ...go galgá válljejuvvot gaskal vejolaš ja dohkkalaš iskkadan-
	## ja dikšunvugiid. 
	## Olbmot geat ellet náitoslágan dahje guimmešvuođalágan gaskavuođain.

SELECT (A Attr) IF (NOT -1 ("leat"))(1 CC)(*2 PrfPrc BARRIER NOT-ADV)
	(*3 N BARRIER NPNH);
	## Johtalus rabas dahje galbmon mearas lea muddejuvvon.

SELECT (A Attr) IF (*-1 Neg BARRIER VFIN OR CS OR CP)
	(*1 CONTRA BARRIER WORD LINK 1C Attr);
	## ILO-konvenšuvdna nr 169 ii vuođđuduva formála, muhto funktionála
	## oamastanvuoigatvuođa doahpagii.


# Special rules for 'buorre' (the only adjective showing case agreement)
# ----------------------------------------------------------------------
SELECT (Pl Nom) IF (0 ("buorre"))(*1 (N Pl Nom) BARRIER NOT-ADJ-CC);
	## Leatgo buorit guolit?

SELECT Acc IF (0 ("buorre"))(NOT 0 Comp OR Superl)
	(*1C (N Acc) BARRIER NOT-ADJ-CC);
	## Doaibmaplána lea bidjan buori vuođu.
	## Doaivvutgo oažžut buriid guliid?

SELECT Gen IF (0 ("buorre"))(NOT 0 Comp OR Superl)
              (*1C (N Gen) BARRIER NOT-ADJ-CC LINK 0 HNOUN);
	## ...galgá journála čállojuvvot buore fidnodábi mielde.


# Comparatives
# ------------
 # This block of rules is there to ensure case agreement for attributive
 # comparatives.

REMOVE Gen IF (0 Comp)(*1 HNOUN BARRIER NPNH LINK 0 NOT-GEN); 
REMOVE Acc IF (0 Comp)(*1 HNOUN BARRIER NPNH LINK 0 NOT-ACC);
	## Oppalaččat daid gažaldagaid birra mat gieđahallojuvvojit... 
	## ...oktiibuot 3.000.000 ruvnno jahkásaččat 5 jagi badjel.

REMOVE Nom IF (0 Comp)(*1 HNOUN BARRIER NPNH OR PUNCT LINK 0 NOT-NOM LINK 
	NOT 0 Inf)(NOT 1 COMMA OR CC)(NOT 0 (Pl Nom) LINK *-1 (V Pl3) 
	BARRIER NPNHA)(NOT 0 (Sg Nom) LINK *-1 V-SG3 BARRIER NPNHA)
	(NOT 1 Gen LINK *1 Po OR PREGEN BARRIER NP-BORDER);
	## Álggahit eará doaibmabijuid gáibida baicca lassi árjja...
   

# Adjectives as predicates
# ========================

SELECT (A Pl Nom) IF (*-1 COPULAS BARRIER NOT-ADV-PCLE LINK *-1 (N Pl Nom) OR
	(Pron Pl Nom) OR (Pl1 Nom) OR (Pl2 Nom) OR (Pl3 Nom) BARRIER CS 
	OR (Sg Nom))(NOT 0 N)(NOT 1 A)(NOT 1 Adv LINK 1 A);
	## ...ja dan golut leat govttolaččat veahki ektui.

SELECT (A Pl Nom) IF (*-1 COPULAS BARRIER NOT-ADV-PCLE LINK *-1 (Du1 Nom) OR 
	(Du2 Nom) OR (Du3 Nom) BARRIER CS)(NOT 0 N)(NOT 1 VERB OR A);
	## I have found no examples, but the rule should work! 

SELECT (A Pl Nom) IF (*-1 COPULAS BARRIER NOT-ADV-PCLE LINK *-1 Nom BARRIER CS 
	LINK *-1 CC BARRIER NPNH LINK -1 Nom)(NOT 0 N)(NOT 1 VERB OR A);
	## Sámegiella ja dárogiella leat ovttaárvvosaččat.  

SELECT (A Sg Nom) IF (*-1 COPULAS BARRIER NOT-ADV-PCLE LINK *-1 (Sg Nom)
	BARRIER NOT-ADV-PCLE LINK *-1 BOS OR CS OR (":" CLB) OR CP BARRIER NOT-ADV-PCLE)
	(1 CLB OR EOS);
	## Jos viessu lea dohkálaš, de ráfi orru dan badjel.
	## Vuoigŋa gal lea geargat, muhto oažži lea headju.

SELECT (A Sg Nom) IF (*-1 COPULAS BARRIER NOT-ADV-PCLE LINK *-1 (Sg Nom)
	BARRIER NOT-ADV-PCLE LINK *-1 BOS OR CS OR (":" CLB) OR CP BARRIER NOT-ADV-PCLE)
	(*1 COPULAS BARRIER NOT-ADV-PCLE LINK *1 CLB OR EOS BARRIER NOT-ADV-PCLE);
	## ...dan dihte go geassegollebivdu lei áibbas heittot leamaš,...

SELECT (A Sg Nom) IF (*-1 COPULAS BARRIER NOT-ADV-PCLE LINK *-1 (N Sg Nom) OR
	(Pron Sg Nom) OR (Sg1 Nom) OR (Sg2 Nom) OR (Sg3 Nom) BARRIER CC OR CS LINK
	NOT 0 Ess)(NOT 0 N OR Inf OR Adv)(NOT 1 VERB)(NOT 1 CC LINK 1 Adv)
	(NOT *1 N BARRIER NPNH);
	## Mun in leat dohkálaš ahte don boađášit mu vissui.

SELECT (A Sg Nom) IF (*-1 COPULAS BARRIER Nom OR PrfPrc OR CS OR MO 
	LINK 0 Sg1 OR Sg3)(NOT 0 Adv OR N OR Inf)(NOT 1 VERB OR Num OR A)
	(NOT 1 ("nu") OR ("hui") LINK 1 A)(NOT 1 CC LINK 1 Adv)
	(NOT *1 N BARRIER NPNH); 
	## Lea ártet go it gallet fulkkiin.

SELECT (A Sg Nom) IF (*-1 COPULAS BARRIER VERB OR CS OR MO LINK *-1 (Neg Sg1) OR 
	(Neg Sg2) OR (Neg Sg3) BARRIER NOT-ADV-PCLE)(NOT 0 N)(NOT 1 N OR A)
	(NOT 1 ("nu") OR ("hui") LINK 1 A);
	## Ii leat vuogas geavahit hálduiváldima dálá fásta opmodagaide.

SELECT (A Sg Nom) IF (*-1 COPULAS BARRIER NOT-AA LINK 0 Sg3)(NOT 0 Inf)
	(0 (N Sg Loc) OR (Actor) OR (N* A))(*1 EOS OR CLB BARRIER NOT-ADV-PCLE); 
	## Lea veahá eahpidahtti, ahte eanaš oassi ođđa govvadáidagis,...  
	## Dávjá lea lunddolaš, ahte doaktáris lea vástádusrolla.

SELECT (A Sg Nom) IF (-1 NP-HEAD-SG-NOM)(*-2 COPULAS BARRIER NOT-ADV-PCLE)
	(NOT 0 Attr LINK *1 (N Sg Nom) BARRIER NPNH OR CLB);
	## Dábálaččat lea čuollu guhkit, golbma, njellja firbmebaji guhku.

SELECT (Sg Nom) IF (*-1 Ill BARRIER NOT-ADV-PCLE LINK *-1 COPULAS 
	BARRIER NOT-ADV-PCLE)(*-1 V-SG3 BARRIER V-NOT-AUX)(NOT 0 PrfPrc)
	(NOT 0 NUMERALS LINK *1 ALU OR ("velggolaš") BARRIER NP-BORDER);
	## ...muhtun gii lei sutnje velggolaš logi duhát taleantta.
	## Mus lea dutnje juoga dadjamuš.

SELECT (A Pl Nom) IF (*-1 (N Pl Nom) BARRIER NOT-ADV-PCLE OR CLB LINK *-1 COPULAS
	BARRIER V-NOT-AUX OR CC OR CS OR CP);
	## Mánnáguoddi-spagáin leat spahká-oaivvit sakka guhkibut.
	## Dán oktavuođas leat dološ geavahusa njuolggadusat miellagiddevaččat.

SELECT (A Pl Nom) IF (*-1 COPULAS BARRIER VERB OR CS OR MO LINK *-1 (V Neg) 
	BARRIER NOT-ADV-PCLE LINK 0 V-DU-PL)(NOT 0 N)(NOT 1 VERB OR A); #(NOT 1 N);
	## Sis-Finnmárkku dikkit maŋŋilgo Norga badjelasas válddii sorjjas-
	## meahttunvuođa dán guovllus, eai šat lean seamma dehálaččat 
	## sápmelaččaid gaskkas go ovdal.

SELECT (A Sg Nom) IF (*-1 BOS OR REALCLB BARRIER NOT-CC)
	(1 (Dem Sg Nom) OR MUN OR DON OR SON); 
	## Ja ávdugas son guhte oskkui.
 
SELECT (A Pl Nom) IF (*-1 BOS OR REALCLB BARRIER NOT-CC)
	(*1 NP-HEAD-PL-NOM BARRIER NPNH);
	## Ávdugasat sii geat moraštit.

REMOVE VERB IF (-1 COPULAS)(0 Nom)(NOT 0 PrfPrc)(1C (Pron Interr));
REMOVE (A Attr) IF (-1 COPULAS)(0 Nom)(1C (Pron Interr)); 
	## Dat geaidnu lea govdat mii doalvu gáđohussii.
	## Dát dáidá leahkit duohta máid boazodoalli lohká.

REMOVE VFIN IF (*-1 COPULAS BARRIER NOT-ADV-INDEF)(0 Attr)(1 N OR EOS);
	## Joddu suhppejuvvo go doaris lea gárvvis.	

SELECT A IF (*-1 COPULAS BARRIER NOT-ADV-INDEF)(NOT 0 VERB OR Ord OR N)
	(1 N OR EOS);
	## ...de dilli orru leamen earalágan.

SELECT A IF (-2 (N Sg Nom))(-1 ("leat" Sg3))(NOT 0 N OR PrfPrc OR Inf)
	(NOT 0 Ill LINK 1 PrfPrc)(NOT 0 Adv LINK 1 A); 
	## Sin váilevaš oahppu ja giellamáhttu lea guhkit áiggi leamaš
	## heađušteaddjin. 	

REMOVE (A Attr) IF (0 A)(1 COMMA)(NOT 2 A OR PrsPrc)(NOT 2 CC LINK 1 Attr);
	## Jos dat ii leat vejolaš, de mii fertet mannat.

REMOVE (A Attr) IF (0 A)(NOT 1 A OR N OR Num OR Actio OR PrfPrc OR PrsPrc OR 
	CC OR COMMA); 
	## Dat galgá čuovvoluvvot nu guhkás go vejolaš.

REMOVE (A Attr) IF (0 A-CASE OR Adv)(1 CC)(NOT *2 N BARRIER NP-BORDER);
	## ...berre mearridanváldi leat nu viiddis go vejolaš ja nu
	## čadni go vejolaš.

REMOVE (A Gen) IF (*-1 COPULAS BARRIER NOT-ADV-PCLE)(1 Inf); 
	## Lea hávski oažžut reivve.

SELECT (A Sg Nom) IF (*-1 COPULAS BARRIER NOT-ADV LINK 0 V-SG3 LINK NOT *-1 Loc
	BARRIER N)(NOT -1 Gen)(NOT 0 Inf OR Attr)
	(NOT 0 Adv LINK *1 VERB BARRIER NOT-ADV)(NOT 1 PrfPrc)
	(NOT 1 CC LINK 1 Adv);
	## Buođđu lea dál gárvvis.
	## Dalle lea lunddolaš, ahte...

SELECT (A Pl Nom) IF (*-1 COPULAS BARRIER NOT-ADV-PCLE)
	(*-1 V-DU-PL BARRIER V-NOT-AUX LINK NOT *-1 Neg BARRIER NOT-ADV-PCLE);
	## Dán máilmmi mánát leat čeahpibut soabadit gaskaneaset.

SELECT (A Pl Nom) IF (1 (Pron Pl2))(*2 COPULAS BARRIER VERB)
	(*2 V-PL2 BARRIER NOT-ADV-PCLE);
	## Man čeahpit dii lehpet hilgut Ipmila báhkkomiid! 

SELECT (A Pl Nom) IF (-1 (N Pl Nom))(*-2 COPULAS BARRIER NOT-ADV-PCLE LINK 0 Pl3)
	(NOT 1 PrfPrc);
	## ...vai leago eanavuovdinorgánat geatnegasat luobahit eatnamiid...

SELECT (A Pl Nom) IF (*-1 ("orrut") BARRIER NOT-ADV-PCLE)
	(*-1 V-PL3 BARRIER CS OR CP);
	## Riekteháhkama eavttut geavahusa njuolggadusaid mielde eai oro absoluhtat.

SELECT (A Pl Nom) IF (-2 (A Pl Nom) LINK *-1 COPULAS BARRIER NOT-ADV-PCLE)(-1 CC);
	## ...jus dat leat čuožžovaččat ja dárbbašlaččat.

REMOVE (A Pl Nom) IF (*-1 COPULAS BARRIER VERB)(*-1 V-SG3 BARRIER V-NOT-AUX)
	(*1 (A Sg NOM) BARRIER Nom OR VERB OR CS OR CP OR CC);
	## No hits?


# Coordination
# ------------
SELECT (A Sg Nom) IF (-2 (A Sg Nom))(-1 CC OR COMMA)(1 CC OR COMMA)(2 (A Sg Nom));
SELECT (A Sg Nom) IF (-2 (A Sg Nom))(-1 CC)(1 CS OR EOS)(NOT 0 Inf LINK -2 Inf);
	## e. e. man bissovaš, dovddus ja bindil dat lea.
	## Vare livččet jogo galmmas dahje liekkas!

REMOVE (A Attr) IF (1 COMMA OR CC)(2 A)(NOT *1 N BARRIER NP-BORDER);
	## ...man bissovaš, dovddus ja bindil dat lea.

SELECT (A Pl Nom) IF (-2 (A Pl Nom))(-1 CC OR COMMA)(1 CC OR COMMA)(2 (A Pl Nom));
	## Lehket álo ustitlaččat, vuollegaččat ja guhkesmielalaččat.

SELECT (A Pl Nom) IF (-2 (Pl Nom))(NOT -2 Adv)(-1 CC)(1C NP-BORDER);
	## Ollu profehtat ja vanhurskásat leat áibbašan oaidnit dan.

SELECT (Pl Nom) IF (-2 (Pl Nom))(NOT -2 Adv OR Inf)(-1 CC)(1 CLB OR CC)
	(2 NP-BORDER);
	## De bohte ollu tuollárat ja suttolaččat ja čohkkájedje beavddis. 

SELECT (Pl Nom) IF (1 CC)(2C (Pl Nom))(NOT 2 Interr);
	## Dušše inuihtat ja sápmelaččat sáhttet adnojuvvot Davviriikkaid
	## eamiálbmogin.

 
# And now some rules for adverbs that modify adjectives
# =====================================================

SELECT Adv IF (0 A)(NOT 0 Inf)(1C A)(NOT *1 N BARRIER NPNH);
	## ...jus ii leat čielgasit dárbbašmeahttun dahje geavatlaččat
	## veadjemeahttun.
	## ...ahte váikkuhusat divššohasa lagamuččaide leat mearkka-
	## šahtti stuorrát.  

SELECT Adv IF (*-1 COPULAS BARRIER NOT-ADV-PCLE)(1 A);
	## Sámi riekteáddejupmi ii lean doarvái miellagiddevaš 
	## suokkarduvvot ovttas dainna gažaldagain.

SELECT Adv IF (-2C Adv)(-1 CC)(0 A);
	## Maŋŋá soađi lea sámi álbmot rahčan ollu vai dat adnojuvvošii
	## seammá dássásažžan, sihke duođalaččat ja rievttálaččat.

SELECT Adv IF (0 A)(1 CC)(2C Adv);
	## ...dan muddui go dat lea geavatlaččat ja ruđalaččat vejolaš.




# ============================= #
# Disambiguating verbs - part 1 #
# ============================= #
             CONSTRAINTS #14    #
# ============================= #

# In this cycle we apply some safe verb rules.


# ConNeg forms
# ------------

SELECT (Imprt ConNeg) IF (*-1 (Neg Imprt) BARRIER VFIN OR ConNeg OR CS OR CP);
	## Ale ge jorggit selggiid sutnje gii bivdá dus loanas.

REMOVE (Ind ConNeg) IF (-1 ConNeg)(*-2 (Neg Ind) 
    BARRIER VFIN OR ConNeg OR PrfPrc OR CS OR CP)(0 A)(1 go);
    ## Máhttájeaddji ii leat stuorit go oahpaheaddjis, ii ge bálvaleaddji
    ## stuorit go hearrás.
    ## Máhttájeaddji ii leat stuorit go oahpaheaddjis, ii ge bálvaleaddji
    ## stuorit johtileabbot go hearrás.

SELECT (Ind ConNeg) IF (*-1 (Neg Ind) BARRIER VFIN OR ConNeg OR PrfPrc OR CS OR CP)
	(NOT 0 N LINK *1 ConNeg BARRIER NOT-ADV-PCLE)
	(NOT 0 Loc LINK -1 (Neg Foc) LINK -1 Loc);
	## Eai dat dohkke šat maninge.

SELECT (Cond ConNeg) IF  (*-1 (Neg Ind) BARRIER VFIN OR ConNeg OR CS OR CP);
	## Ii go son ollu eanet gárvvohivčče din?

REMOVE ConNeg IF (NOT *-1 Neg BARRIER CS OR CONTRA OR ("go"));
REMOVE (ConNegII) IF (NOT *-1 (Neg Imprt) BARRIER VFIN OR CS);
	## Dasgo mii oinniimet su nástti badjáneamen ja leat boahtán...
	## Stáhta galgá fuolahit ahte gávdno divššohasbearráigeahčči 
	## juohke fylkkas.

SELECT (Imprt ConNeg) IF (*-1 CC BARRIER NOT-ADV LINK *-1 (Imprt ConNeg) 
	BARRIER VERB);
SELECT (Ind ConNeg) IF (*-1 CC BARRIER NOT-ADV LINK *-1 (Ind ConNeg) BARRIER VERB);
	## Allet nappo moraš ja cealkke...
	##

SELECT ConNeg IF (*-1 PrfPrc BARRIER NOT-ADV-PCLE LINK *-1 Neg BARRIER VERB OR CS)
	(0 ("leat"))(NOT *1 PrfPrc BARRIER NOT-ADV-PCLE);
	## Ii dál láhppán gal leat.
# Need more advanced rule to select ConNeg when there is more
# material between Neg and ConNeg.


REMOVE ConNeg IF (NOT *-1 (Neg) BARRIER CLB OR V-IND-FIN)
	(NOT *-1 Nom LINK -1 Neg); 
	## Juohke muorra mii ii šaddat buriid šattuid, čullojuvvo ja 
	## bálkestuvvo dollii.
	## Gávpot mii lea vári alde, ii sáhte leat čihkosis.


# Imperative
# ----------

REMOVE Imprt IF (1 ("go" Pcle));
REMOVE Imprt IF (*-1 (Neg Ind) BARRIER CLB OR V-IND-FIN);
REMOVE Imprt IF (*-1 ConNeg BARRIER VFIN OR COMMA OR CS OR CP);
	## Go bohciida soahpameahttunvuohta das ahte galget go journála... 
	## Mánát eai gillen dálu čorget.
	## Na muhto don it leat dasa nama čállán!

REMOVE Imprt IF (-1 N OR A OR Po);
	## Čuorvvas gullo Ramas. 
	## Boarrásis leai dat vel eanaš ealáhussan ge.
 # This will work fine when headings no longer interfere.

REMOVE Imprt IF (*1C VFIN BARRIER NOT-ADV-PCLE OR CLB);
	## Lágas ii duššaduvvo oktage bustávaš dahje čuoggáš, ovdal go...

REMOVE Imprt IF (*-1 ("\(") BARRIER ("\)"))(0 N);
	## Láigolihtus eai čuovo makkárge sierravuoigatvuođat gáddái (buo.
	## riehkit, stárat).

SELECT Imprt IF (-1 BOS OR PUNCT OR REALCLB)(0 ("leat"))(1 A);
	## Leage buorre!


# Infinitive
# ----------

SELECT Inf IF (*-1 (Neg Sup) BARRIER CLB OR CS); 
SELECT Inf IF (*-1 ("amas" CS) BARRIER VERB);
	## ...amaset sin čalmmit oaidnit,... 
	## ...amas lojes hearggit livvut. 

SELECT Inf IF (*-1 NP-HEAD-SG-NOM BARRIER NOT-ADV-PCLE LINK *-1 CC BARRIER NPNHA
	LINK *-1 Inf BARRIER NOT-ADV);
	## Dearvvašvuođaveahkkedárbu galgá árvvoštallojuvvot ja diehto addo-
	## juvvot.
 # This rule has to come before the ones below.
 
SELECT Inf IF (*-1 INF-VERB BARRIER CS OR CLB OR CP OR Neg)(NOT *1 Inf BARRIER VFIN OR CS OR CP);
    ## Bissuin galgá heahkkahuhttit, ii nie váigadit goddit; sáhttá leat váiga.
 
SELECT Inf IF (-1 BOS)(*1 ("ii" V IV Neg Ind Sg3) BARRIER V);
	## Viežžat viervván rivgo čiektageregin ii leat ásti, iige sáváháhttin.


# Rules that prevent later selection of Inf for a finite verb in the frame 
# INF-VERB...CC... ____							   
# - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - 
 
REMOVE Inf IF (*-1 NP-HEAD-PL-NOM BARRIER NPNHA LINK -1 CC)
	(NOT *-1 A BARRIER NOT-ADV-PCLE)(NOT *-1 INF-VERB BARRIER VERB);
REMOVE Inf IF (*-1 (Num Nom) BARRIER NOT-NA LINK -1 CC LINK NOT *-1 Inf 
	OR INF-VERB BARRIER VFIN);
	## ...ja ollugat biehttalit iežaset sámevuođa.
	## ...ja máŋga Davviálbmogiid fakultehta studeantta leat oasi oahpus
	## gazzan Romssa universitehtas.

REMOVE Inf IF (-1 CC LINK NOT *-1 Inf BARRIER CS);
REMOVE Inf IF (-3 (A Attr))(-2 CC)(-1 PrfPrc)(0 N);
	## Áirasat geat vuostálastet eavttuhusa jienastit nu ahte deaddilit
	## boalu "vuostá" ja čuožžilit.
	## Mearrasápmelaččat ledje áŋgiris ja oahppan bivdit.


# Now we select Inf 
# - - - - - - - - -

 # The first rule seems much better without the NOT 0 Adv.

SELECT Inf IF (*-1 INF-VERB BARRIER CS OR CLB OR CP OR Neg);#(NOT 0 Adv);
	## Skuvla galggai oahpahit sámemánáide dárogiela.
        ## Prošeavtta áigut čađahit.

SELECT Inf IF (*-1 INF-VERB BARRIER VERB OR NP-HEAD-PL-NOM OR CONTRA OR CS OR CP
	LINK 0 VFIN OR PrfPrc OR Inf);
	## Son oažžu vuokkain ja luovosfirpmiin, namalassii golgadagain,
	## bivdit miehtá deanu.

SELECT Inf IF (-2 Inf)(-1 COMMA)(1 COMMA OR CC);
	## Luondduresursaealáhusat sáhttet álggahuvvot, bisuhuvvot ja ovddiduvvot.


# This rule looks backwards across a relative clause:
SELECT Inf IF (*-1 VFIN BARRIER CS OR CP OR CONTRA LINK *-1 (Pron Interr) 
	BARRIER CS LINK *-1 INF-VERB BARRIER VFIN OR Inf OR CS OR CP);
	## ...galgá sihke divššohas ja váhnemat dahje earát geain lea váhnen-
	## ovddasvástádus oažžut dieđu.

SELECT Inf IF (*-1 ACC-INF-VERB BARRIER Inf OR CS OR CP OR Nom OR Neg);
	## Lea deaŧalaš movttiidahttit studeanttaid gazzagoahtit doavttergráda-
	## oahpu.

SELECT Inf IF (-1 (A Nom))(*-2 COPULAS BARRIER NOT-ADV-PCLE);
	## Dál lea váttis dadjat juste beaivvi goas váttisvuođat čoavdašuvvet.

SELECT Inf IF (*-1 ("leat") BARRIER NOT-ADV-PCLE OR CLB	LINK *-1 Loc 
	BARRIER NOT-ADV-PCLE)(NOT 0 Adv);
	## ...vaikkovel sis livččege ovdal leat leamaš geavahanvuoigatvuohta.

SELECT Inf IF (*-1 ("leat" Sg3) BARRIER NOT-ADV)(*1 ("ahte") BARRIER WORD);
	## Doaibmaplána ulbmil lea sihkkarastit, ahte...

SELECT Inf IF (-3 ("dat" Sg3 Gen))(-2 ("ala"))(-1 ("ahte"));
	## Sámediggi áigu bargat dan ala ahte oččodit ruovttoluotta...



# Verbgenitive
# ------------
SELECT VGen IF (-1 ("gaskan"));
	## Gaskan viessobasa bohte guossit.

SELECT (VGen) IF (*-2 VFIN BARRIER CS)(NOT -1 CC)(-1 ("nu") OR ("hui"))(NOT 0 Adv); 
 # Probably also after other modifiers.
	## Dat hálai nu huikki.

REMOVE VGen IF (0 N)(NOT 0 V-TRANS-ACT LINK *1 Acc BARRIER NOT-NP);
	## Dakko ii leat menddo fávlái buođu bissehit.

REMOVE VGen IF (0 Inf);
	## Ja dalle lea dušše rohttet spahkagežiid vuolil.

REMOVE VGen IF (-1 BOS)(0 Imprt);
	## Várut beatnagis!

REMOVE VGen IF (NOT *-1 V-IND-FIN BARRIER CLB OR CS)
	(NOT *1 V-IND-FIN BARRIER CLB OR CS);
	## Vuoigatvuohta beassat válljet buohcceviesu.

REMOVE (Pass VGen);
	## Didjiide mihtiduvvo seammá mihtuin.

REMOVE VGen IF (0 ("ovttas"));
	## Lea heivvolaš gieđahallat daid ovttas.

REMOVE VGen IF (0 ("hárve"));
	## Seammás jođálmahttá passivitehta dementiija ovdáneami, eandalii 
	## juos hárve gullá eatnigielas.

REMOVE VGen IF (0 ("njuolgga"));
	## Nubbi gažaldat lea, sáhttágo konvenšuvnna mearrádusaid iskat 
	## njuolgga Norgga duopmostuolus.


# Perfect Participle
# -------------------

SELECT PrfPrc IF (0 Pass);
	## ...nu go namahuvvon vuosttas ja nuppi teakstaoasis.
 # Exceptions might turn up.

REMOVE PrfPrc IF (*-1 CC BARRIER NPNH LINK -1 N LINK NOT *-1 ("leat") BARRIER NPNHA)
	(0 N)(NOT 0 ("leapma" Dimin))(NOT -1 CC LINK *-1 PrfPrc BARRIER VERB);
  	## Journála sirdin ja luoikan.
	## ...de lassánii maid sisafárren ja koloniseren dáidda guovlluide. 

REMOVE PrfPrc IF (*-1 Gen BARRIER NPNH LINK *-1 CC BARRIER NPNH LINK -1 N LINK 
	NOT *-1 ("leat") BARRIER NPNHA)(0 N)(NOT 0 ("leapma" Dimin));
	## Iige njuovvan ja bierggu rádjan leat mihkkege dearvvašvuođa-
	## váttisvuođaid.
 # The reason for the specific mention of Gen in this rule is that at this stage, 
 # the nominals in question are both Acc and Gen. Since Acc is NPNH, it acts as a 
 # barrier for the previous rule.  

SELECT PrfPrc IF (-1 Neg);
	## Buorre lea go it fuobmán.

SELECT PrfPrc IF (*-1 Neg BARRIER PrfPrc OR CONTRA OR VFIN OR CS)
	(NOT *-1 ConNeg BARRIER Neg LINK NOT 0 ("leat"))(NOT 0 Ess)
	(NOT *1 ConNeg BARRIER CC OR CS OR CP OR CLB); 
	## Iihan dat liikon Kárášjohkii.

SELECT PrfPrc IF (*-1 Neg BARRIER NOT-ADV-PCLE)(0 INF-VERB)
	(*1 Inf BARRIER VERB OR CS);
	## No hits any more?

REMOVE (N Ess) IF (*-1 Neg BARRIER NOT-ADV-PCLE)(0 PrfPrc)
	(NOT *1 ConNeg OR PrfPrc BARRIER CS);
	## Manne son ii ohcan dan ođđa virggi?

SELECT PrfPrc IF (*-1 Neg BARRIER VERB OR CS OR CP)
	(NOT *1 ConNeg OR PrfPrc BARRIER VERB OR CS OR CP OR ("nugo"));
	## In leat gal gullan inge lohkan.

REMOVE PrfPrc IF (*-1 ("leat") BARRIER NPNHA OR Nom LINK *-1C (N Ess) 
	BARRIER NOT-ADV LINK *-1 BOS OR CS OR (":" CLB) OR CP BARRIER NPNH)(0 N)
	(NOT 0 ("leapma" Dimin));
	## Dasa lassin álggahuvvojit movttiidahttin ja nannendoaimmat, maid 
	## vuođđun lea dutkan.

SELECT PrfPrc IF (*-1 ("leat") BARRIER NOT-ADV-PCLE)
	(NOT 0 Ess LINK 1 (A Nom) OR PrfPrc)
	(NOT 0 Actio LINK 1 PrfPrc LINK 0 (@-FMAINV))(NOT 1 ("veara"))
	(NOT 1 CC LINK 1 Actio LINK NOT 0 PrfPrc)
	(NOT 0 Actio OR N LINK *1 ("leat" PrfPrc) BARRIER COPULAS);
	## Norgga láhkaaddit leat áigá jo suokkardan vuđolaččat kvenaid 
	## eamiálbmotstáhtusa gažaldaga.
	
SELECT PrfPrc IF (*-1C Rel LINK -1 Pron OR N LINK *-1 ("leat") BARRIER NOT-ADV-PCLE)
	(NOT 0 Ess LINK 1 (A Nom) OR PrfPrc)
	(NOT 0 Actio LINK 1 PrfPrc LINK 0 (@-FMAINV))(NOT 1 ("veara"))
	(NOT 1 CC LINK 1 Actio LINK NOT 0 PrfPrc)
	(NOT 0 Actio OR N LINK *1 ("leat" PrfPrc) BARRIER COPULAS);
	## Muhtin biirres leat gávcci 12 áirasiin leamaš nissonolbmot, ja eará biirres leat buohkat geat leat válljejuvvon leamaš dievddut .
	# The relative sentence version. 
	
# Topicalized version
# - - - - - - - - - -

#easy topicalized version:
SELECT PrfPrc IF (*-1 Nom BARRIER VFIN OR CP OR CS LINK *-1 ("leat") BARRIER NOT-ADV-PCLE LINK NOT *-1 Nom BARRIER CP OR CS)
	(NOT 0 Ess LINK 1 (A Nom) OR PrfPrc)
	(NOT 0 Actio LINK 1 PrfPrc LINK 0 (@-FMAINV))(NOT 1 ("veara"))
	(NOT 1 CC LINK 1 Actio LINK NOT 0 PrfPrc)
	(NOT 0 Actio OR N LINK *1 ("leat" PrfPrc) BARRIER COPULAS);
	## Muhtin biirres leat gávcci áirasa leamaš nissonolbmot, ja eará biirres leat buohkat geat leat válljejuvvon leamaš dievddut.

#version where the subject is extended by partitive construction:		
SELECT PrfPrc IF (-1 Loc LINK -1C (@PrcN>) OR A LINK -1 Num LINK *-1 ("leat") BARRIER CP OR CS)
	(NOT 0 Ess LINK 1 (A Nom) OR PrfPrc)
	(NOT 0 Actio LINK 1 PrfPrc LINK 0 (@-FMAINV))(NOT 1 ("veara"))
	(NOT 1 CC LINK 1 Actio LINK NOT 0 PrfPrc)
	(NOT 0 Actio OR N LINK *1 ("leat" PrfPrc) BARRIER COPULAS);
    # leat+(@SUBJ+(Adv)+(quantor+PrcN>+Loc))+-FAUX/-FMAIN+@SPRED
    ## Muhtin biirres leat gávcci obbalaččat 12 válljejuvvon áirasiin leamaš nissonolbmot, ja eará biirres leat buohkat geat leat válljejuvvon leamaš dievddut.

SELECT PrfPrc IF (*-1 (@OBJ) BARRIER NOT-ADV LINK *-1 ("leat") BARRIER NPNHA)
	(NOT 0 Actio LINK -1 PrsPrc)(NOT 1 COMMA LINK *1 Actio BARRIER NPNH)
	(NOT 0 Actio LINK *1 PrfPrc BARRIER NOT-ADV-PCLE);
	## Leago barggu ohcan? 

SELECT PrfPrc IF (-1 (@SUBJ) LINK *-1 ("leat") BARRIER NOT-NP)
	(NOT 0 Actio LINK -1 PrsPrc)(NOT 0 Actio LINK 1 CC LINK 1 Actio LINK 
	*1 N BARRIER NOT-NP);
	## Leatgo sii njuovadan juo?

SELECT PrfPrc IF (*-1 ("leat") BARRIER VERB OR CS OR CP OR CC LINK NOT 0 PrfPrc)
	(NOT 0 Actio LINK *1 PrfPrc BARRIER NOT-ADV-PCLE LINK NOT 0 (@PrcN>))
	(NOT 0 Ess LINK 1 (A Nom) OR PrfPrc)(NOT 1 ("dihte") OR ("dihtii"))
	(NOT 1 COMMA OR CC LINK *1 Actio BARRIER NPNH)(NOT 1 ("veara"));
	## Biret-Elle lea easka skuvllas geargan.

SELECT PrfPrc IF (0 ("leat" PrfPrc))(NOT *1 V-SG3 BARRIER NPNHA);
    ## Eará guovlluin gos leat vel unnit sámit go Finnmárkkus, leamaš dákkár ákkain vel stuorát deaddu almmolaš ságastallamis.
    ## Leamaš illá sáhttá leat lovttas.
    # It should not take the latter one (demo).

SELECT PrfPrc IF (*-1 VFIN BARRIER CLB OR CS)(NOT 0 N OR Actio OR Ess)
        (NOT *-1 CC BARRIER VFIN);
	## Sámediggi sáhttá mearridit meannudit ášši giddejuvvon uvssaid duohken. 
        
  # PrfPrc if there is not a Sg1 to the left, but if there is a CC to
  # the right of a possible Sg1, then we forget about the Sg1.

SELECT PrfPrc IF (-1 NP-HEAD-GEN LINK *-1 ("dadjat") BARRIER NPNHA)(1 N)
	(2 NP-BORDER);
	## Dat dajai iežas gávdnan golli.
 # More verbs should be listed here.

SELECT PrfPrc IF (0 ("mannat"))(1 TIME);
	## Ii leat boahtán mannan vahkus.
	## Mannan čuohtejagi gaskkamuttos lei stuorra gilvu.

REMOVE (V Pass Sg1) IF (*1 (N Nom) BARRIER CS OR CP);
	## Juolluduvvon permišuvnnat.

REMOVE Actio IF (-1 Dem OR (Pron Indef))(0 PrfPrc)(1 N);
#REMOVE Actio IF (0 PrfPrc)(1 N);
	## ahte buot náitalan olbmuin galggai leat guolástanvuoigatvuohta.
	## Luossa-bivdin joddu-buođuin deanus. 
    ## Gumppe goddin boazu. Du čoaggin muorjjit gopmánedje láhttái.

# "Leamaš" without finite verb
# - - - - - - - - - - - - - -
SELECT PrfPrc IF (-1 NP-HEAD)(NOT -1 Gen)(0 ("leapma" Dimin)); 
SELECT PrfPrc IF (0 ("leapma" Dimin))(*1 A-CASE BARRIER NOT-ADV-PCLE);
	## Muhto dat leamaš vissa gobmi.
	## Árbevirolaččat leamaš várrugasat addit ovttaskas olbmuide...

SELECT PrfPrc IF (0 ("leapma" Dimin))(*1 Nom BARRIER NOT-ADV-PCLE);
SELECT PrfPrc IF (NOT *-1 VFIN BARRIER CS)(0 ("leapma" Dimin))
	(NOT *1 VFIN BARRIER CP OR CS);
	## Muhto leamaš gal ovdal okta bivdovuohki vel.
	## Sápmelaččat je eiseválddiid gaskkas leamaš kulturboatkka, mii...

SELECT PrfPrc IF (0 ("leapma" Dimin))(*1 (V Actio Ess) BARRIER NOT-ADV-PCLE);
	## Dáidet leamaš buohcame?

SELECT PrfPrc IF (*-2 PrfPrc BARRIER VERB OR CC OR CP OR CS)(-1 CC)
	(0 ("leapma" Dimin));
	## Doaibmaplána lea bidjan buori vuođu ja leamaš mielde rievdadeame...


# Actio
# -----
#REMOVE PrfPrc IF (-1 NP-HEAD-GEN)(0 Actio)(1 N)(2 NP-BORDER OR Dem);
SELECT Actio IF (-1 NP-HEAD-GEN)(0 Actio)(1 N)(2 NP-BORDER OR Dem);
	## Háliidit go sii konvenšuvnna mearridan vuoigatvuođaid sirdo-
	## juvvon earáide?
	# XXX This is a strong rule -- do a follow up and check whether it hits too often.


# Present participle
# ------------------
SELECT PrsPrc IF (-1 ("hui"))(*1 N BARRIER NPNH);
	## Mudden ferte dagahit janza ja hui vahágahtti meassamiid.

SELECT PrsPrc IF (-1 ("nu"))(1 ("go" Pcle));
	## Mearridanváldi berre leat nu čadni go vejolaš.

REMOVE PrsPrc IF (0 N)(1C NP-BORDER);
REMOVE PrsPrc IF (0 N)(1 CLB)(2 NP-BORDER);
	## Ii dain leat áiti ii ge láhtu.
	## Sii geain lea váldi, gohčoduvvojit buorredahkkin.


# Rules for "orrut" (there are som more rules at # orrut vs. orrot) 
# -----------------#
#SELECT V-SG3 IF (0 ("orrut"))(*1 PrfPrc OR (Actio Ess) BARRIER VFIN OR CS OR CP);
	## Elásttalaš hálddašanvuoigatvuohta čuoččuhus orru 
	## mielddisbuktimin ahte...
    # This rule doesn´t allow plural, so I remove it. The example works although. And it´s 
    # important that the following example works: 	
	## Olbmot orro liikomin bures dáid márkaniidda ja mii ieža leat duhtavaččat.

SELECT VERB IF (0 ("orrut"))(*1 A BARRIER NOT-ADV-PCLE);
	## Sus lea vuoigatvuohta orrut anonyman. 

SELECT V-SG3 IF (0 ("orrut"))(*1 ("dego") OR CS BARRIER NOT-ADV-PCLE);
  	## Dás orru datte ahte...
	## Orru dego boazodoallu ain geahččaluvvosii hálddašuvvot ja stivrejuvvot...
 
SELECT Ind IF (0 ("orrut"))(*1 ("leat" Inf) BARRIER VERB OR CS OR CP);
	## Min oktavuođas orru omd. boazodoallu leat leamen...
 

# Rules for "addit" (which is an adjective, but more often a verb)
# ----------------------------------------------------------------

SELECT Inf IF (*-1 COPULAS BARRIER NOT-ADV)(0 ("addit"))
	(NOT *1 Nom BARRIER CS OR CP OR REALCLB);
	## ...man ulbmil lea addit oppalaš ipmárdusa sámi kultuvrras.


# Actio Loc = Actio Ess
# ---------------------
# This is due to the southern Locative -n

REMOVE Loc IF (-1 ("leat"))(0 (Actio Ess)) ;
	## Ulbmilat leat rievdamin.
	
REMOVE Loc IF (-1 ("orrut"))(0 (Actio Ess)) ;
    ## Ii dat gal orron hehttemin su vuojadeamis.



# ==================== #
# Disambiguating nouns #
# ==================== #
      CONSTRAINTS #15  #
# ==================== #


# Proper nouns
# ============

# Prop or not
# ----------- 

SELECT Prop IF (NOT *-1 BOS OR (":") OR (PUNCT LEFT) OR (Num Nom) BARRIER REALWORD); 
	## Áirras Egil Olli nammaduvvui Sámedikki ovddasteaddjin.
	## 1.1. Sámi doaibmaplána darbu ja ulbmilat.
 # Capitalised nouns in the middle of a sentence are names.

REMOVE (Prop Pl) IF (0 (Prop Sg))(NOT 0 ("Sámediggi") OR (laš));    
	## Marit lea dál.
  # Marit N Sg, not Mari+t Pl Nom

REMOVE (Prop Pl) IF (-1 BOS)(0 CNOUN-PL-NOM);        
	## Sámit leat álgoálbmot.

REMOVE (Prop Pl Nom) IF (-1 BOS)(NOT *1 V-PL3 BARRIER CS);	     
	## Unnit nuppástusaiguin.	

REMOVE (Prop Pl Acc) IF (-1 BOS)(0 CNOUN);
	## Gunaid galgá leat várrogas atnit.

SELECT Prop IF (-1 BOS)(0 Loc)(1 COMMA)(2 MANNU)(3 Num) ;
SELECT Prop IF (-1 BOS)(0 Loc)(1 COMMA)(2 Num) ;
	## Deanus, borgemánu 31. b. 2005.
	
SELECT Prop IF (-1 BOS)(0 ("Sápmi" Prop))(1 ORGANIZATION);
    ## Sámi allaskuvla láhčá diliid dulkoma joatkkaoahppofáldahkii, mii oaivvilduvvo olbmuide, geat dovdet dearvvašvuođa- ja sosiálasuorggi, ja maiddái MBD ovddasvástádussuorggi.

# Removing proper nouns that are lookalikes
# - - - - - - - - - - - - - - - - - - - - -

REMOVE ("Dan" Attr) IF (NOT 1 Prop);
REMOVE ("Dan" Prop) IF (1 CNOUN); # Dan is Nom or Attr.

REMOVE ("Hu" Ill) IF (1 A or Adv);

REMOVE Prop IF (-1 BOS)(0 ("Lassi")); 
#REMOVE Prop IF (0 ("Lassi"))(1 Pron);
#REMOVE Prop IF (0 ("Lassi"))(*1 N BARRIER NPNH);
	## Lassi dutkandoarjagat sáhttet leat mielde... 
	## Lassin dasa ferte váldit vuhtii sierralágan dilálašvuođaid.
	## Lassin jorgalandoarjagii, de lea dárbu sihke...
  	## Lassin leat oalle viiddis boazuguohtunguovllut.

SELECT Prop IF (0 ("Lásse"))(*1 VFIN BARRIER CS);
	## Lásse dat leigga sihtan mu váldit lusan.
 
REMOVE ("Lea") IF (-1 BOS OR PUNCT)(NOT 1 V-SG3); 
REMOVE ("Lea") IF (-1 BOS OR PUNCT OR (":"))(1 A); 
	## Lea dušše okta Ipmil. Lea idja.
	 ## Lea buolaš. Son dajai: Leage gierdávaš.
 
REMOVE ("Man") IF (-1 BOS OR PUNCT OR (":"))(0 (V Imprt));
	## De son celkkii sutnje: "Mana basadit Siloaláddos."
  # Cannot think of sensible conditions.

REMOVE Prop IF (-1 BOS)(0 ("Mo"))(NOT 1 go)(*1 ("?"));
REMOVE Prop IF (-2 CLB)(-1 PUNCT)(0 ("Mo"));
	## Mo sáhttá su riika dalle bissut?

REMOVE Prop IF (0 HEADOFPARTS);
	## Osiid ektui, dárkojuvvo Finnmárku muđui Almennehin,...


# Selecting proper nouns
# - - - - - - - - - - - -
SELECT (Prop Nom) IF (0 ("Rabbi"))(1 COMMA);
	## Rabbi, lea buorre go mii leat dáppe.

SELECT Prop IF (0 ("Ruoŧŧa"))(1 CC)(2 Prop);
	## Ruoŧa ja Suoma beale riektedilli lea measta ovttalágan.


# Particular proper nouns
# -----------------------
# These should probably be unified later.
# Sámi + these words = common noun: (this is the open group, this should  be default).
REMOVE Prop IF (0 ("sápmi" Gen))(1 ("plána") OR ("doaibma#plána") OR ("perspektiiva") OR ("mánná") OR ("álbmot") OR ("bargi") OR ("geavaheaddji") OR ("kultuvra") OR ("servodat"));
	## Sámi perspektiiva lea dehálaš.

# Sámi + these words = proper noun: (this is the closed set, the set of institutions)
SELECT Prop IF (0 ("Sápmi"))(*1 ("alla#skuvla") OR ("guovddáš") OR ("giella#ráđđi") OR ("giella#láhka") OR ("ossodat") BARRIER NPNH);
	## Sámi dearvvašvuođadutkama guovddáš.


REMOVE Prop IF (0 (N Gen))(1 CC)(2 (N Gen))(NOT 2 Prop);
	## Sámi ja dáža kultuvra.


# Prop as Attr or not
# -------------------

SELECT (Prop Attr) IF (-1 (V Sg3))( 1 Prop)(2 Prop)(3 CLB);
SELECT (Prop Attr) IF (-2 (V Sg3))(-1 Prop)(1 Prop)(2 CLB);
	## Dat lea dehálaš, lohká Svein Ottar Helander.

SELECT (Prop Attr) IF (NOT -1 Prop)(1 CC)(2 (Prop Attr))(3 INITIAL OR (N Prop));
        ## Dá leaba Brita ja Ove Kåven.

REMOVE (Prop Attr) IF (NOT 1 (Prop Mal) OR (Prop Fem) OR (Prop Sur) OR INITIAL); 
	## Trond Trosterud lea dál.
        ## Olles doaibmaviessu lea sullii 2300 njealjehasmetera. 

SELECT Attr IF (0 Prop)(1 INITIAL)(2 Prop);

SELECT Attr IF (0 (Prop Mal) OR (Prop Fem))(NOT 0 Loc)
        (1C (N Prop Sur) OR (N Prop Fem) OR (N Prop Mal))
	(NOT 0 (@OBJ) LINK *1 (@OPRED) BARRIER NOT-NP); 
SELECT Attr IF (0 (Prop Sur))(NOT 0 Loc)(1C (N Prop Sur))
	(NOT 0 (@OBJ) LINK *1 (@OPRED) BARRIER NOT-NP);
	## Áirras nr. 17 Randi S. Pedersen oaččui permišuvnna.
	## Áirras nr. 10 Ole Henrik Magga oaččui permišuvnna.
	#  Two rules in order to avoid Sur Attr to Mal or Fem.

SELECT (@PROP>) IF (0C Nom)(1 ORGANIZATION);
    ## Ođđajagimánus 2001 lei Beazedievvá mánáidgárdi doaimmas golmmain ossodagain.

SELECT (@GN>) IF (0 (Prop Gen))(1 ORGANIZATION);
    ## Oslo universiteahta namas Oslo namma lea genitiivvas.
    
    
# Choosing between different nouns
# --------------------------------

SELECT ("virgi") IF (*-1 PROFESSION LINK 0 (@GN>) BARRIER NOT-ADJ)(0 ("viergi"));
    ## Eai leat lahkage doarvái logopedat, geat dikšot geavaheaddjiid klinihkain, eaige sámiid ássanguovllus leat galle gieldda, mat leat ásahan logopeda virggiid.
    
SELECT ("virgi") IF (-1 ("luohpat"))(0 ("viergi" Loc));
     ## Virgeloguid lea suohkan dássážii geahpedan dainna lágiin go olbmot luhpet virggiin.

# Choosing between different proper nouns
# ---------------------------------------

# Jáhko vs. Jáhkob
# - - - - - - - - -
REMOVE ("Jáhko") IF (0 ("Jáhkob"));
	## De oinnii Jáhkoba ja su vielja Johanasa.

# Johan/Johana/Johanas
# - - - - - - - - - - -
SELECT ("Johanas" Nom) IF (1 V-SG3)(*1 VFIN-NOT-AUX);
	## Daid beivviid gásttašeaddji Johanas bođii ja sárdnidišgođii. 

SELECT ("Johanas" Nom) IF (*-1 COPULAS BARRIER NOT-NP)
	(NOT *1 V-TRANS-ACT BARRIER CS OR REALCLB);
	## Su namma galgá leat Johanas.

# Nilse vs. Nilsen
# - - - - - - - - -
REMOVE ("Nilse") IF (0 ("Nilsen" N Prop Sg Nom));
	## Tor Nilsen lea boahtán.

# Norga vs. Norge
# - - - - - - - -
REMOVE ("Norge") IF (0 ("Norga"));
	## Mun vuolggán Norgii.
	
	
# Disambiguating the semantic tags
# ================================

REMOVE Plc (-1 (Prop Mal Attr) OR (Prop Fem Attr))(0 Sur);
    ## Trond Trosterud lea dál.	
	
REMOVE Fem IF (-1 (Prop Attr))(0 Sur);
	## Johan Mattis Eira ja Karen Marie Eira Buljo.

REMOVE Sur IF (-1 (Prop Attr))(NOT -1 Mal OR Fem);
	## Helse Nord.
	
	
# Nouns versus adjectives
# =======================
 # Note that although the following rules are single cohort rules, they could 
 # not come very early, since correct readings might then be removed. Also note 
 # that we do not remove (A Sg Nom) or (A Pl Nom).

REMOVE (A Sg Acc) IF (NOT *-1 COPULAS BARRIER NOT-ADV-PCLE)(0 (N Sg Acc));
REMOVE (A Sg Gen) IF (0 (N Sg Gen))(NOT 0 Ord);
	## Beaivváš addá eatnamii lieggasa. 
	## Muhtomin ferte guokte gurpasa bidjat oktan noađđebeallin.

REMOVE (A Sg Loc) IF (0 (N Sg Loc));
REMOVE (A Sg Ill) IF (0 (N Sg Ill));
REMOVE (A Sg Com) IF (0 (N Sg Com));
	## Ovtta ovddasteaddjis divššohasa ovttas lea vuoigatvuohta...
	## Juohke gurpasii biddjojuvvo guottahat.
	## Lean mun gal oaidnán čakčaseavdjadiin dola rievdamiin čázi alde.

REMOVE (A Pl Acc) IF (0 (N Pl Acc));
REMOVE (A Pl Gen) IF (0 (N Pl Gen));
	## Ulbmil lea movttiidahttit sámi nuoraid gazzagoahtit universitehtaoahpu.
	## ...de lea inuihtaid dilli áibbas eará go sápmelaččaid. 

REMOVE (A Pl Loc) IF (0 (N Pl Loc));
REMOVE (A Pl Ill) IF (0 (N Pl Ill));
REMOVE (A Pl Com) IF (0 (N Pl Com));
	## Leigo sápmelaččain maid vuoigatvuohta bivdimii.
	## Sámi nuoraide láhččojuvvo vejolašvuohta oassálastit gieddebargguide.
	## Sii ledje gávpejasat oktavuoigatvuođain gávppašit sápmelaččaiguin.

SELECT (N Nom) IF (0 (A Nom))(*1 COPULAS BARRIER (N Nom) OR (Pron Nom) OR CC 
	OR CS OR CP OR CLB LINK NOT *1 V-NOT-AUX BARRIER CS OR CP) 
	(*1 (Pron Nom) OR (A Nom) BARRIER CS OR CP LINK NOT 0 Interr); 
	#LINK NOT *1 Nom BARRIER CS OR CP);
	## Divššohasa ovddasteaddji vuosttas teakstaoasi mielde lea son gii...

SELECT N IF (NOT 0 Po OR Adv OR VFIN OR Inf OR PrfPrc OR PrsPrc OR (Num Nom))
	(NOT 0 Attr LINK *1 N BARRIER NPNH)(1 PrfPrc LINK 0 V-NOT-AUX);
	## Go joddu lea čáhcái boahtán,..
	
# Nouns vs. verbs
# ======================

REMOVE VFIN IF (-1C (Attr @AN>));
    ## Eambbo doaimmat ruovttuin váikkuhit maiddái veahkkebálvalusossodaga.

# Numerus disambiguation
# ======================

# Early plural rule
# -----------------

SELECT Pl IF (0 SG-WORD)(-1 (Pron Gen) LINK -1 Num OR Ord BARRIER NPNH);
     ## Dát lea okta min ovttasbargguin.
     # This rule is necessary before the next one as it chooses plural even if the genitive pronoun modifier after the numeral and in front of the singular noun is not disambiguated yet (Acc-Gen disambiguation comes later).

# Singular not plural
# ------------------- 

SELECT Sg IF (0 SG-WORD)(NOT *-1 Num OR Ord BARRIER NPNH)(*-1 BOS LINK NOT *1 ("goappeš") OR ("goappašat") OR ("earálágan") OR ("máŋgalágan"));
    ## Go funet lea guolli goddomin, de fitná olmmoš buođu luhtte dušše
    ## oktii jándoris, savdnjileamen joddu amas dat dierrut, suollemasat
    ## dainna doaivvuin ahte nago de lea boađistan.
    ## Buoremus sámepolitihkka ožžojuvvo áigálii sámi veahkadaga ja ráđđehusa gaskaneas ipmárdusain.
   
    
# lahka-compound
# - - - - - - - -

SELECT Sg IF (-1 LAHKA-MOD)(0 LAHKA);
    ## Ráđđehus háliida lágidit dearvvašvuođa- ja sosiálabálvalusaid oktagaslaš dárbbuid mielde, earret eará pasientavuoigatvuođaid lágain ja dearvvašvuođabargiid lágain.

# Depending on the scope
# - - - - - - - - - - - -
# the time dimension can cause problems here...

SELECT Sg IF (-1 FYLKA LINK 0 (@GN>))(0 ("fylkka#diggi" Com) OR ("fylkka#gielda" Com) OR ("fylkka#mánni" Com) OR ("fylkkas#gielda" Com) OR ("fylkkarehket#dárkun" Com))
(NOT -3 FYLKA LINK -2 CC);
    ## Son deattuha sakka gulahallama sihke Sámedikkiin ja Finnmárkku fylkkadikkiin ášši gieđahaladettiin.
    
SELECT Sg IF (-1 ("stáhta" Sg) LINK 0 (@GN>))(0 XDEPARTEMEANTA LINK 0 Com)
(NOT -3 ("stáhta") LINK -2 CC);
    ## Dán váldošiehtadusa sohpe Stáhta Eanandoallodepartemeanttain nuppi bealis, ja dsto nubbin dásseárvosaš áššebeallin Norgga Boazosápmelaččaid riikasearvi (NBR).

    
# Plural not singular
# ------------------- 

# Depending on the scope
# - - - - - - - - - - - -
    
SELECT Pl IF (-3 (FYLKA @GN>))(-2 CC)(-1 (FYLKA @GN>))(0 ("fylkka#diggi" Loc));
    ## Son lea leamaš sihke Romssa ja Finnmárku fylkkadikkiin.

SELECT (Pl Loc) IF (-1 ("Norga" @GN>))(0 N)(NOT 0 Prop OR ("oaivegávpot") OR ("stáhtaministtar") OR ("sámipolitihkka") OR ("dutkanráđi") OR ("riika#rádji"));
    ## Vuostálasvuođat dán guovtti oahpaheaddjijoavku gaskkas lea dovdos
    ## fenomena Norgga joatkkaskuvllain.
    # This rule might be to open. One has to make a set of unique words and exclude them from this rule.
    
SELECT (Pl Loc) IF (-1 ("suohkan" @GN>))(0 ("vuođđo#skuvlla") OR ("mánáid#gárdi"));
    ## Skuvlajagis 2001/2002 leat oktiibuot 469 oahppi suohkana vuođđoskuvllain.


# Case disambiguation
# ===================

# ================ #
  CONSTRAINTS #16  #
# ================ #

# Special rules
# =============
 
# Secure Accusative rules
# =======================

# Here we put rules that we want to work before the main Genitive chapter.
# Many of them use the strict transitive verb sets (they really want acc objects, so we give them that).


SELECT Acc IF (*-1 MOVEMENT-VERB BARRIER CS)(0 PLACE)(NOT 1 Po)(NOT -1 CC LINK *1 VFIN) ;
SELECT Acc IF (0 PLACE)(NOT 1 Po)(*1 MOVEMENT-VERB BARRIER CS);
SELECT Acc IF (*-1 ("vázzit") OR ("viehkat") BARRIER CS)(0 EDUCATION)(NOT 1 Po) (NOT 1 ROOM LINK 0 Loc)(NOT -1 CC LINK *1 VFIN) ;

SELECT Acc IF (0 EDUCATION)(NOT 1 Po)(*1 ("vázzit") OR ("viehkat") BARRIER CS) ;
	## Mun váccán vákkiid, vuvddiid ja váriid.
	## Sii vázzet skuvlla.
	## Mun váccán skuvlla gávpogis.
	## Mun váccán skuvlla šiljus. # This is not Acc, but Gen.


# Accusative or genitive in front of ALU and in front of adjectives
# -----------------------------------------------------------------

# Exceptional accusative attributes in front of ALU nouns.

SELECT Acc IF (0 Num LINK *1 MEASURE BARRIER NOT-A LINK 1 ALU);
SELECT Acc IF (0 Num LINK 1 ALU);
SELECT Acc IF (0 Num LINK 1 Num LINK 1 ALU);


# Not-so-exceptional?

SELECT Acc IF (0 NUMERALS)(1 MEASURE)(2 ALU OR A);
	## Visti lei vihtta mehter alu.

SELECT Gen IF (-1 (Num Acc))(0 MEASURE)(1 ALU OR A);
	## Min viessu lea vihtta mehtara alu.
	## Sus lea njeallje mehtara guhkkosaš biila.
	
SELECT Acc IF (0 NUMERALS BARRIER NOTCOMMA)(*1 (Num Acc) LINK 1 MEASURE LINK 1 ALU OR A);
	## Guokte golbma mehtera guhku, ja guhkitge, sáhttá vuojahat leat.
	
SELECT Acc IF (1 (Num Acc) LINK 1 (Num Acc) LINK 1 MEASURE LINK 1 ALU OR A);

SELECT Acc IF (1 COMMA LINK 1 (Num Acc) LINK 1 COMMA LINK 1 (Num Acc) LINK 1 MEASURE LINK 1 ALU OR A);
    ## Čuollun sáhttá leat dušše okta fierbmi gurgaluvvon, muhto dábálaččat lea čuollu guhkit, golbma, njeallja fierbmebaji guhku.

SELECT Acc IF (-1 (Num Acc))(0 MEASURE)(1 Pr);
	## Ádjá viessu lea guokte miilla bajábeal márkana.

SELECT Acc IF (-1 (Num Acc))(0 TIME)(1 ("boaris"));
	## Mánná lea golbma jagi boaris.

SELECT Gen IF (-1 (Num Gen))(0 MEASURE)(1 A OR ALU);
	## Visti lei viđa mehter alu.
	## Dohko manai viđa mehter govdosaš geaidnu.

SELECT Gen IF (-1 (Num Gen))(0 TIME)(1 ("boaris"));
	## Son lea viđa jagi boaris.

SELECT Acc IF (-1 (Num Acc))(0 TIME)(1 ("maŋŋil"));
	## Dat dáhpáhuvai golbma jagi maŋŋil.

SELECT Acc IF (0 Num)(1 (N Acc))(2 ("velggolaš"));
SELECT Acc IF (-1 Num)(1 ("velggolaš"));
	## Ánde lea munnje vihttalot ruvnno velggolaš.

SELECT Gen IF (0 ("okta"))(1 ALU);
	## Dábálaččat leat joddut sullii ovtta guhku.

SELECT Acc IF (0 Num)(1 ("gitta"))(2 (Num Acc));
	## Dát ledje gáddenuohtit mat ledje 180 gitta 200 sala guhku, ja 15 gitta 
	## 20 sala čieŋu.


# Locative, not PxSg3
# -------------------

SELECT Loc IF (NOT *-1 Sg3)(0 PxSg3);
	## Mun borren áhkus luossaláibbi.


# Genitive
# ========
 # Since disambiguation between accusative and genitive is very difficult, 
 # let us pick out as many genitives as possible here.

# Various genitive rules
# ----------------------

# Genitive after quantifier
# - - - - - - - - - - - - - 
#REMOVE Gen IF (*-1C (@QN>) BARRIER NPNH)(NOT *1 N BARRIER NOT-NP);
SELECT Gen IF (*-1C Num BARRIER NPNH OR Gen OR CC OR PUNCT OR REALCLB OR Ord
	LINK NOT 0 ("okta") OR Ord)(NOT 0 VERB OR ("nr") OR ("nummar") OR ("s"));
#(NOT -1C (@QN>) LINK NOT *1 N BARRIER NOT-NP);
	## Golbma čáppa, doaimmalaš nieidda livžo áhčiset riegádanbeaivve.
	## 
	
REMOVE Gen IF (*-1C Ord BARRIER NPNH OR Gen OR CC OR PUNCT OR REALCLB OR Ord
	LINK NOT 0 ("okta"))(NOT 0 VERB OR ("nr") OR ("nummar") OR ("s"))(NOT 1C Po)(NOT *1 N BARRIER NPNH);
	## Vuođđun lea biddjon ahte dát lanjadagat adnojit "boarrásiid-dikšosadjin", muhto maŋŋil teavsttas čilget njealját ásodaga birra.
    
SELECT Gen IF (*-1 Num BARRIER NPNH OR Gen OR PUNCT OR REALCLB LINK 
	NOT 0 ("okta") OR Ord LINK *-1 ("leat") BARRIER NOT-ADV-PCLE)
	(NOT *1 V-TRANS-ACT BARRIER CLB OR CC OR CS OR CP);
	## Joavkkus leat vihtta oahppi Kárášjogas.

SELECT Gen IF (*-1 Num BARRIER NPNH OR Gen OR PUNCT OR REALCLB LINK 
	NOT 0 ("okta") OR Ord LINK -1 CC OR CLB LINK *-1 (Num Nom) BARRIER NOT-NP)
	(NOT 0 Num LINK -1 CC OR CLB);
	## Joavkkus leat vihtta oahppi Kárášjogas, vihtta oahppi Muosáhis ja 
	## vihtta Bearalvágis.
 
SELECT Gen IF (-1 CC)(-2 Cmpnd)(*-3 (NUMERALS @SUBJ-QH) BARRIER NPNH OR PUNCT LINK 0 Nom);
	## Máŋga sámi dearvvašvuođa- ja sosiálabargi vásihit dan. 

SELECT Gen IF (*-1 (Num Nom) BARRIER NOT-NP OR CLB)(NOT -1 CC)
	(*1 V-PL3 BARRIER NOT-ADV-PCLE);
	## Máŋga Romssa universitehta dutkanbirrasa leat ásahan ovttasbargguid...

SELECT Gen IF (*-1 NUMERALS BARRIER NPNH OR CC OR CLB OR PUNCT OR ("ruvdnu") OR HEADOFPARTS LINK
	0 Nom LINK NOT -1 ABBR OR CLB)
	(NOT *-1 (Num Acc) BARRIER NPNH LINK *-1 Nom BARRIER CC OR CLB OR CS OR CP)
	(NOT *-1 V-TRANS-ACT BARRIER VERB OR CS OR CP OR CC OR CLB)
	(*1 Gen BARRIER NPNH);
	## Máŋga Romssa universitehta dutkanbirrasa leat ásahan ovttasbargguid...


# Genitive in front of postposition
# - - - - - - - - - - - - - - - - - 
SELECT Gen IF (NOT 0 Adv OR PrfPrc)(1C Po)(NOT 1 Px);  
SELECT Gen IF (NOT 0 Adv OR PrfPrc)(1C @NumN<)(2C Po)(NOT 2 Px);    
SELECT Gen IF (NOT -1 ("§") OR ("nr"))(0 Num)(*1 (N Gen) BARRIER NPNH LINK 1 Po);
	## Lávdegottiid čoahkkimat dollojit giddejuvvon uvssaid duohken.
	## Divššohasas lea vuoigatvuohta oažžut dearvvašvuođadilis árvvo-
	## štallojuvvot 30 bargobeaivvi sisa.
	## Sii sáhttet buktit hálddahuslaš cealkámuša lága kapihttala 11 mielde.

SELECT Gen IF (*-1 BOS BARRIER NOT-CC)(1 (Po Qst));
	## Girdi mieldego don manat?

SELECT Gen IF (*-1 BOS BARRIER NOT-CC)(1 Po)(2 go);
	## Girdi mielde go don manat?

SELECT Gen IF (1 CC)(*2 Gen BARRIER NPNH LINK 1 GASKAL);
	## Juos fágabargi ja geavaheaddji gaskka leat dasa lassin gielalaš
	## dahje kultuvrralaš gulahallanváttisvuođat,...

SELECT (V Actio Gen) IF (1 ("dihte" Po) OR ("dihtii"))(*1 (N Acc) BARRIER NOT-NP);
	## Vuovdon earit máhcahovvojit sámi guovlluide nannen dihte báhcán doaluid.

SELECT (V Actio Gen) IF (1 CC)(2 (V Actio Gen))(3 ("dihte" Po) OR ("dihtii"));
	## ...seailluhan, nannen ja ovddidan dihte sámi servodaga.

SELECT Gen IF (0 Actio)(1C Po);
	## Válddán dan ášši guorahallan vuollái.

#SELECT Gen IF (*-1 Po OR PREGEN BARRIER NOT-NP OR PUNCT OR REALCLB)
#	(NOT 1 COMMA LINK 1 Gen LINK NOT *1 CC BARRIER Po)(NOT 1 Pers)
#	(*1 Po OR PREGEN BARRIER NOT-NP OR REALCLB);
SELECT Gen IF (*-1 Po OR PREGEN BARRIER NOT-NP OR PUNCT OR REALCLB)(NOT 0 Ind)
	(NOT 1 COMMA LINK 1 Gen LINK NOT *1 CC BARRIER Po)(NOT 1 Pers)
	(*1 Po OR PREGEN BARRIER NOT-NP OR REALCLB);	
	## ...dan vuolde sisdoalu birra divššohasjournálain, čállima, divodeami,  sihkkuma, vurkema, sirdima, journála beassanvejolašvuođa ja duššadeami birra.
	## Mun vánddardan sin mielde ja muitalan sámiid birra olbmuide. 
	# It shouldn´t take the latter one.

SELECT Gen IF (1 (PUNCT LEFT))(*2 (PUNCT RIGHT) LINK 1 Po);
	## Dearvvašvuođaregisttarláhka lea sierraláhka heaggadieđuidlága (cuoŋo-
	## mánu 14. b. 2000 láhka nr 31 heaggadieđuid gieđahallama birra) ektui.

SELECT Gen IF (1 CC)(*2 NP-HEAD-GEN BARRIER NPNHA LINK 1 Po);
	## Seammás áiggun maiddái ovdanbuktit dihto reflekšuvnnaid sihke eaŋkil 
	## rievttálaš geavatlaš gažaldagaid ja eambbo prinsihpalaš gažaldagaid hárrái.


# Genitive after preposition
# - - - - - - - - - - - - - -
SELECT Gen IF (*-1C Pr BARRIER NPNH OR REALCLB OR CONTRA)(NOT 0 VFIN)
	(1 NP-BORDER OR CC OR COMMA OR ("ieš")); 
	## Earret mu ii giige lea boahtán.
	## Sáhka su birra viidánii miehtá Syria, ja ...
	## ...gaskal vejolaš ja dohkálaš iskkadan- ja dikšunvugiid.
	## ...ja vulggii rastá jávrri iežas gávpogii. 

SELECT Gen IF (*-1C Pr BARRIER NOT-NP OR Po)(*1 Interr BARRIER WORD);
	## Sáhttá dadjat, ahte jagi 1978 láhka balansere gaskkal sámi 
	## vuoigatvuođaid boarásmuvvan oainnu, mii lei ráđđejeaddjin 
	## "vigihis ávkkástallanrievtti áigodagas", ja eanet ođđaáigásat 
	## vuoigatvuođaid dovddasteaddji oainnu.

SELECT Gen IF (-1C Pr)(0 Num);
	## Jus divššohas lea badjel 16 jagi boaris... 

SELECT Gen IF (-3 GASKAL)(-2 (Num Gen))(-1 CC)(0 Num);
	## Jus divššohas lea gaskal 16 ja 18 jagi boaris,...


# Genitive inside a two-part Po
# - - - - - - - - - - - - - - -
SELECT Gen IF (-1 POST-A)(1 N)(2 POST-B);
	## Nu leat 40 buolvva Dávveda rájes Babylonia fáŋgavuođa rádjái.


# Genitive attribute of adposition.
# - - - - - - - - - - - - - - - - -
SELECT Gen IF (1 ("bealde" Pr) OR ("beale" Pr) OR ("beallái" Pr) OR ("geahčai" Pr)
	OR ("geahčen" Pr) OR ("geaže" Pr));
	## Ánde boahtá don bealde joga.


# Genitive in front of "guvlui"
# - - - - - - - - - - - - - - -
SELECT Gen IF (NOT 0 Dem OR Loc)(1 ("guovlu" N Sg Ill));
	## Áddjá lei nohkkan, láse guvlui oaivi.


# Genitive in front of "láhkái"
# - - - - - - - - - - - - - - - 
SELECT (Actio Gen) IF (1 ("láhki" N Sg Ill));
	## ...go dearvvašvuođabargit iežaset árvvoštallama mielde gávnnahit...
	## Dat ráhkaduvvo gáttis gárvvisin ja gessojuvvo vatnasii suohppun
	## láhkái.

SELECT (N Gen) IF (1 ("láhki" N Sg Ill));
	## Dat lea áhčis láhkai.


# Genitive with "dan mađe N"
# - - - - - - - - - - - - -
SELECT (Gen Sg) IF (0 ("dat"))(2 ("mađe"));

# Genitive with "oktavuhtii"
# - - - - - - - - - - - - - -
SELECT Gen IF (0 N)(1 ("oktavuohta") LINK 0 Ill);
    ## Váldosisdoallu sápmelaččaid ektui lea, ahte Kárášjoga MNP oktavuhtii ásahuvvo bearašossodat ja Leavnnja DPS oktavuhtii fáhkkaveahkkejoavku ja oanehis áiggi dikšuma seaŋgasajit.



# Genitive with "veara"
# - - - - - - - - - - -
SELECT Gen IF (NOT 0 Nom)(*1 COPULAS BARRIER V-NOT-AUX OR CS 
	LINK *1 ("veara") BARRIER VERB OR CS);
	## Min ii leat veara dohko vuolgit.
	## Dasgo bargi lea bálkkás veara. # NOT 0 Nom!


# Genitive in comparison
# ----------------------
SELECT Gen IF (NOT *-1 V-TRANS-ACT BARRIER NOT-ADV-PCLE)(NOT 0 VERB)
    (1 (A Comp Ess))(NOT 1 VERB OR Adv)(NOT 2 go);
	## Mun jáhkán du iežan falibun.
    ## ..sámi servodat dárbbaša dárbbašlaš veahki hukset min iežamet oahpahusvuogádaga guhkit áiggi boahteáiggis."
	
# Genitive relative pronouns
# - - - - - - - - - - - - - -
SELECT (Pron Interr Gen) IF (NOT 0 Indef OR ("mii"))(1 N)
	(*1 COPULAS BARRIER CLB)(NOT *1 V-TRANS-ACT BARRIER CLB);
        ## ...ja gii galgá árvvoštallat guđe dieđut galget leat journálas.
	## ...ja guđe árvvoštallamiid vuođul doaibmabidju lea álggahuvvon.

SELECT (Pron Interr Gen) IF (0 ("mii"))(1 (N Nom))
	(*1 COPULAS BARRIER CLB)(NOT *1 V-TRANS-ACT BARRIER CLB);
 	## Jerusalemis lea láttu man namma hebreagillii les Betesda.


# Genitive indefinite pronouns
# - - - - - - - - - - - - - - -
SELECT Gen IF (0 Indef)(1 (N Ill) OR (N Loc) OR (N Com));
	## De galget ollu dearvvašvuođabargit iešguđet fidnojoavkkuin dan
	## geavahit. 


# Genitive in proper name sequences
# - - - - - - - - - - - - - - - - - 
REMOVE Acc IF (NOT *-1 V-TRANS-ACT BARRIER NOT-ADV-PCLE)(0 Prop)(1 Prop)
	(NOT 1 ADVLCASE);          
	## Dat lea Norgga Röros ja Máhte Piera.

# The idea behind the following rule pair is to that in a (Spec + N) 
# sequence, the Prop is the possessor rather than the other way around.
# XXX these two rules should be generalised to star notation.
REMOVE Acc IF (*-1 V-TRANS-ACT BARRIER NOT-ADV-PCLE)(0 Prop)(*1 (N Acc) BARRIER NPNH);#(NOT *1 Po BARRIER N);
	## Mun oainnán Máhte beatnaga Máreha biilla duohken.
	## Mun oainnán kánske Máhte beatnaga Máreha biilla duohken.
	## Son oaidná min Máhte biilla duohken.
	#  0 = Máhte

#REMOVE Acc IF (*-1 V-TRANS-ACT BARRIER NOT-ADV-PCLE)(0 Prop)(1 Gen LINK 1 Po BARRIER N);
	## Mun oainnán Máhte beatnaga Máreha biilla duohken.

# Genitive after "mii" and "mihkkege"
# - - - - - - - - - - - - - - - - - -
SELECT (N Pl Gen) IF (-1 ("mii") OR ("gii"))(1 NP-BORDER)(*1 ("?")); 
	## Maid ipmašiid doppe dagat?
 # Embedded questions are also possible, but harder to formulate a rule. 

SELECT (N Pl Gen) IF (-1 ("mihkkege"));
	## Iige njuovvan ja bierggu rádjan leat mihkkege dearvvašvuođa-
	## váttisvuođaid.


# Genitive subjects of non-finite verbs
# - - - - - - - - - - - - - - - - - - -
REMOVE Gen IF (*-1 CP OR CS)(0 TV)(1 Actio)(NOT 1 Ess OR Com)(NOT *1 VFIN BARRIER CP OR CS);
    ## Ii leat šiitimis ahte Stuorradiggi dattetge dán stuorradiggeáigodagas
    ## oažžu gieđahallamii máŋga ášši main lea stuorra mearkkašupmi sámiide.
    # Has to stand in front of the next rule (SELECT Gen)

SELECT Gen IF (NOT *-1 ("leat") BARRIER CS)(NOT 0 Interr OR Dem)(1C Actio)
	(NOT 1 Ess OR Com)(NOT 1 (V Actio Loc))
	(NOT 1 PrfPrc LINK *1 NP-HEAD-ACC BARRIER NPNH)(NOT 2 Po);
	## Eatni dahkan láibi lei buorre.

SELECT Gen IF (0 Interr)(1 (Actio Gen))(2 N);
	## Gean ráhkadan fanas dat lea?

SELECT Gen IF (*-1 ("leat") BARRIER NPNHA)(1 (V Actio Loc));
	## Dat ii leat geafes olbmo nagodeames. 

SELECT Gen IF (*-1 (@OBJ) BARRIER VERB LINK *-1 V-TRANS-ACT BARRIER NPNHA)
	(1 Ger);
	## Son muitalii dan buot olbmuid guladettiin.


# Accusative after actio (exceptions to next rule)
# - - - - - - - - - - - - - - - - - - - - - - - - 
SELECT Acc IF (*-1 ("dihte") OR ("dihtii") BARRIER NPNH LINK -1 Actio)
	(1 NP-BORDER);
	## ... diehtin dihtii iežas dili ja veahki sisdoalu.

SELECT Acc IF (*-1C Actio BARRIER HNOUN-NOM OR (Pron Nom) OR VERB OR CC OR CS OR CP 
	LINK NOT 0 Cmpnd OR Gen OR IV)(NOT -1 CC LINK -1 Actio LINK NOT 0 Cmpnd)
	(NOT 0 TIME OR ROUTE OR Loc)(NOT 0 Num LINK -1 ("§"))(1 NP-BORDER)(NOT 1 N); 
	## ...dahje biehttalit botkemis nealgudeami mii lea jođus. 


# Accusative after "vuoi" (exceptions to REMOVE Acc-rule below)
# - - - - - - - - - - - - - - - - - - - - - - - - -
SELECT Acc IF (*-1 ("vuoi") BARRIER NPNH)
	(NOT 0 TIME LINK *-1 (N Acc) BARRIER ("vuoi"))
	(NOT *1 N BARRIER NPNH OR CC OR CLB);
	## Vuoi du, Betsaida!

SELECT Acc IF (*-1 ("vuoi") BARRIER NOT-NP OR CLB OR CP)
	(NOT 0 TIME LINK *-1 (N Acc) BARRIER ("vuoi"))(1 NP-BORDER);
	## Vuoi dán máilmmi ja dan fillehusaid!
 
SELECT Acc IF (*-1 ("vuoi") BARRIER NOT-NP)(0 Dem);
	## Vuoi surgat dan gándda!

SELECT Acc IF (*-1 CC BARRIER NPNH LINK -1 Acc LINK *-1 ("vuoi") BARRIER NOT-NP)
	(NOT 0 Dem OR Pers);
	## Vuoi áhpehemiid ja njamaheddjiid daid beivviid! 

SELECT Acc IF (*-1 Pers BARRIER NPNH OR CLB LINK *-1 ("vuoi") BARRIER NOT-ADV);
	##  Muhto vuoi din riggáid, dasgo dii lehpet juo ožžon iludet.


# Accusative after adjectives and nouns
# - - - - - - - - - - - - - - - - - - -

SELECT Acc IF (*-1 A BARRIER NPNH OR CC OR CLB OR PUNCT LINK 0 Nom LINK 
	NOT 0 Attr OR ("buorre") OR Adv OR N)(NOT 0 Px OR PrfPrc OR Adv)
	(1 EOS OR CLB); #(NOT 0 TIME);
	## Mánát leat oažžulat guliid.

SELECT Acc IF (-1 ("ahkit"));
	## Mánás lea ahkit eatni.

#Accusative in coordination... to be explained.
REMOVE (Gen @GN>) IF (*-1 BOS LINK *1 Acc)(0 Acc LINK 0 LOC-COMPL-NOUN)(*1 Loc BARRIER NPNH);       
    ## Gihpa gieđahallá maid vuoigatvuođaid, mat vuođđuduvvet lágaide ja konvenšuvnnaide, ovdamearkkaid gielddaid plánain sámi mánáid ja nuoraid ovdii ovttas singuin, ja dasto ovdamearkkaid fálaldagain, mat addojit dearvvašvuođastašuvnnain ja mánáidgárddiin juo dan rájes go mánát leat uhcit, skuvlaja asttuáigefálaldagain, gitta nuoraide, geat leat oahpus, ja ovdamearkkaid nuoraiddiehtojuohkimis ja vejolašvuođain davviriikkalaš ja riikkaidgaskasaš ovttasbargui.
# This rule targets the third "ovdamearkkaid". The idea is
# a. that 
# Genitive (or nominative) in clause without full verb
# - - - - - - - - - - - - - - - - - - - - - - - - - - -
REMOVE Acc IF (NOT *-1 V-TRANS-ACT BARRIER PUNCT)(NOT *-1 CS LINK *1 V-TRANS-ACT BARRIER Acc)(NOT *-1 CS BARRIER V-TRANS-ACT LINK *1 CS BARRIER VFIN)(NOT *-1 CS BARRIER V LINK 1 @CS-COMPL)
	(NOT *-1 (Pass PrfPrc) BARRIER NPNH LINK *-1 V-TRANS-ACT BARRIER NPNHA)
	(NOT -1 CC LINK -1 Acc LINK -1 ("ollu"))(NOT 0 TIME)
	(NOT 0 NUMERALS LINK *1 TIME OR MEASURE BARRIER NOT-A)
	(NOT *1 V-TRANS-ACT BARRIER CS LINK 0 VFIN OR (@-FMAINV))
	(NOT *1 CLB BARRIER NOT-NP); # LINK *1 (Pron Acc) BARRIER NOT-ADV-PCLE);
	## Leago doppe kafea?
	## Amma duot lei Niillasa guollebiila?
	## Láhka divššohasvuoigatvuođaid birra.
	## ...jus divššohas oažžu ávkki dearvvašvuođaveahkis. 
	## Sámedikki čoahkkimat dollojuvvojit rabas uvssaid.

 # This rule also (correctly) removes Acc from many elements that will 
 # eventually be analysed as verbs.

REMOVE Acc IF (*-1 CP BARRIER VERB)(NOT 0 TIME)
	(*1 COPULAS BARRIER V-NOT-AUX LINK NOT *1 VERB);
	## ...ja makkár váldi dain nuppádassii berre leat.
	## ...main sámi ja sámi guoskevaš fáttát leat guovddážis.
 # This rule removes Acc in subordinate clauses that has no full verb.

REMOVE Acc IF (*-1 COPULAS BARRIER NOT-AA LINK *-1 (@SUBJ) BARRIER V-NOT-AUX 
	LINK *-1 BOS OR CLB BARRIER NPNH)(1 CLB OR EOS);
	## Ale váldde, dat lea mu!


# Genitive in front of "oktavuođas" etc.
# - - - - - - - - - - - - - - - - - - -
SELECT Gen IF (1 PREGEN);
	## ...go sii bargguset oktavuođas Sámedikkis ožžot dakkár dieđuid.
	## ...geat barget erenoamážit sámi guoskevaš čuolbmačilgehusaiguin.


# Genitive between Dem and N
# --------------------------
 # Here I have left out all rules that would involve an Acc/Gen demonstrative,
 # since these demonstratives could be objects not belonging with the N.
 
REMOVE Acc IF (*-1 (Dem Nom) BARRIER NPNH)(*1C (N Nom) BARRIER NPNH OR Dem);
	## Dát eaiggáduššanvuoigatvuođa doaba de geahččaluvvo...

REMOVE Acc IF (*-1 (Dem Sg Com) BARRIER NPNH)(*1 (N Sg Com) BARRIER NP-BORDER OR Dem);
 	## No examples in corpus, but the rule should work. 

REMOVE Acc IF (*-1 (Dem Pl Ill) BARRIER NPNH)(*1 (N Pl Ill) BARRIER NP-BORDER OR Dem);
	## Dearvvašvuođaregisttarláhka gusto daidda dearvvašvuođa-
	## hálddahusa dearvvašvuođadieđuid gieđahallamiidda.

REMOVE Acc IF (*-1 (Dem Pl Loc) BARRIER NPNH)(*1 (N Pl Loc) BARRIER NP-BORDER OR Dem);
	## No examples in corpus.

REMOVE Acc IF (*-1 (Dem Pl Com) BARRIER NPNH)(*1 (N Pl Com) BARRIER NP-BORDER OR Dem);
	##

REMOVE Acc IF (*-1 ("oktan") OR ("ovttas") OR ("ovttasráđiid") BARRIER NPNH)
	(*1 (N Com) BARRIER NP-BORDER);
	## Eamiálbmotguovddáža ásaheami sáhttá geahččat ovttas universitehta
	## Áivan dutkamiid guovddáža ohcamiin.


# Genitive in temporal adverbials
# -------------------------------
 # A 'measure' time adverbial is Acc, others are Gen. 

SELECT (@GN>) IF (0 MANNU)(1 (A Ord));    
	##  Dál lea miessemánu 1. beaivvi.

SELECT Gen IF (-1 Ord)(0 ("beaivi") OR ("vahkku"));
	## Evaluerenraporta addojuvvui GOD:ii geassemánu 20. beaivvi 2000.

SELECT Nom IF (-3 MANNU)(-2 Ord)(-1 ("beaivi") OR ("b" ABBR))(0 Num);
	## Sámedikki bargiidčoahkkimis Guovdageainnus juovlamánu 7. beaivvi 2001
	## mearridii...

SELECT Gen IF (-1 ("boahtte") OR ("boahtit" PrsPrc) OR ("ovddit" Attr))(0 TIME);
	## Boađe boahtte vahku.

SELECT Gen IF (-1 COMMA)(0 TIME)(1 EOS);
	## Mun vuolggán ihttin, bearjadaga.

SELECT Gen IF (-1 BOS OR CS OR (":" CLB))(0 BEAIVI);
	## Bearjadaga mii vuolgit oaggut.

SELECT Gen IF (-1 NP-BORDER)(0 BEAIVI);
	## Mun boađán bearjadaga.
 # The idea here is that the BEAIVI should not be modified by e.g. 'olles'.

SELECT Gen IF (-2 Gen LINK 0 MANNU)(-1 Ord)(0 ("b") OR ("beaivi"));
    ## Geassemánu 8.b. 2001 láhkaásahus nr 676 oktagaslaš plánaid birra dearvvašvuođalágaid vuođul lea addojuvvon láhkamearrádusaid láhkavuođuin.

SELECT Gen IF (0 Card)(1 (N Gen));
	## Dat lea duháhiid jagiid boaris.

SELECT Gen IF (0 Num)(1 TIME)(1 ADVLCASE);
	## Journálat sáhttet lágiduvvot fylkkamánne rádjui 10 jahkái.

SELECT Gen IF (-1 (@ADVL))(NOT -1 Pr OR Attr)(0 TIME);
	## Diddi vuodjala detnui mihcamárain maŋás ja guvžá fas čavččabeallái geasi.
	## Čuonžžá goargŋu detnui čakčageasi.

SELECT Gen IF (-1 ("juohke") OR ("seammá" @PronN>) OR ("seamma" @PronN>))(0 TIME); 
	## ...álbmotjoavkku mat ovdal dahje seammá áiggi...
 # Other attributes similar?

SELECT Gen IF (0 ("jahki"))(1 Num); 
	## Jagi 2002 bušeahttačoahkkima,...   

SELECT Gen IF (NOT 0 VERB)(1 ("áigi"))(NOT 1 (Sg Loc) LINK 1 ("áigi" Sg Ill));
	## Čoahkkimis lea lávdegoddi árvalan konfereanssa áiggi, báikki ja fáttáid.
	## ...oppa dan áiggi go dáláš konvenšuvdna lea doaibman.

SELECT Gen IF (-1 Gen)(0 ("áigi"));
	## ...gonagasa Herodesa áiggi,..

SELECT Gen IF (0 ("hávvi"))(*1C Acc BARRIER CS OR CP);
	## Váilevaš vejolašvuohta doaimmahit kultuvrra ávnnaslaš beliid sáhttá
	## muhtun háviid mearkkašit álbmotriekterihkkuma.

SELECT Gen IF (0 ("hávvi"))(NOT *-1 V-TRANS-ACT BARRIER CS)
	(NOT *1 V-TRANS-ACT BARRIER CS OR CP);
	## Fágalaš sudjadeapmi daid háviid go doavttir lea dahkan biehttaleami.

SELECT Gen IF (*-1 V-PASS BARRIER VFIN OR CS)(0 ("hávvi"))
	(NOT *1 V-NOT-AUX BARRIER CS OR CP);
	## Muhtun háviid buhtaduvvo meassamiid ruđalaš vahát.

 # The time expression "hávvi" is not Acc, but the problem is the homonymous noun. 

SELECT Gen IF (*-1 BOS OR CS OR (":" CLB) BARRIER NPNHA)
	(0 ("áigi") OR ("bodda") OR ("geardi") OR ("hávvi"));
	## Eará háviid ii leat hoahpu journálačállimiin.

SELECT Gen IF (1 ("riegádan#beaivi"));
	## Golbma čáppa, doaimmalaš nieidda livžo áhčiset riegádanbeaivve.

SELECT Gen IF (-1 (Num Gen))(0 ("geardi"));
	## Galggat golmma geardde beaivvis borrat ovtta tableahta.

	
# Genitive in place adverbials
# ----------------------------
SELECT Gen IF (*-1 MOVEMENT-VERB BARRIER NPNHA)(0 ROUTE);
	## ...ja sii manne eará luotta ruoktot eatnamasaset.


# Genitive specifiers
# -------------------

REMOVE (N Acc) IF (*-1C NP-BORDER BARRIER WORD)(NOT -1 V)(NOT 1 VFIN)
	(*1 N BARRIER NPNH OR CC LINK *1 VFIN BARRIER CC OR CS OR CP)
	(NOT *1 (@SUBJ) BARRIER NOT-NP OR CC LINK *1 V-TRANS-ACT BARRIER 
	CC OR CS OR CP LINK NOT *1 (OBJ) BARRIER CLB OR CC OR CS OR CP)
	(NOT 0 ("hearrá" Acc) LINK 1 ("ipmil" Acc))
	(NOT 1 Ill LINK *1 VFIN BARRIER NOT-ADV-PCLE);
	## Ášši meannudeapmi loahpahuvvui guovvamánu 20. b. dii. 09.15. 

REMOVE (N Nom) IF (*-1 NP-BORDER BARRIER WORD)(NOT -1 COPULAS)(0 Gen)
	(NOT 0 TITLE)(NOT 1 COPULAS OR Inf)(*1 (N Nom) BARRIER NOT-ADJ OR 
	CLB OR ("/") LINK *1 VFIN BARRIER CC OR CS OR CP OR CONTRA OR PUNCT)
	(NOT 1 VFIN BARRIER NOT-ADV-PCLE);
	## Ášši meannudeapmi loahpahuvvui guovvamánu 20. b. dii. 09.15. 
	## Maiddái álgoálbmotfestivála Riddu Riđđu doarjja unnui.
 
REMOVE (Pron Pers Acc) IF (-1 NP-BORDER)(*1 N BARRIER NOT-Attr LINK *1 VFIN
	BARRIER CC OR CS OR CP);
	## ...lea go sin doaibma vuoigaduvvon oažžut doarjaga.

SELECT Gen IF (*-1 BOS OR CS OR (":" CLB) BARRIER NPNHA)(NOT 0 Adv OR ("mii" Interr) OR 
	Dem OR Attr OR PrsPrc OR PrfPrc OR VFIN OR Actio)(NOT 0 TITLE LINK 1 Prop)(NOT 0 PxSg2)
	(1C (N Nom) OR (N Acc));
	## Sámi parlamentáralaš ráđi doaibmaáigi čuovvu Sámedikki
	## válgaáigodaga.

#SELECT Gen IF (*-2 BOS OR CS OR (":" CLB) BARRIER NOT-ADV-PCLE)(*-1 Num BARRIER NPNH)
SELECT Gen IF (*-1 Num BARRIER NPNHA LINK *-1 BOS OR CS OR (":" CLB) BARRIER NOT-ADV-PCLE)
	(*1 Loc BARRIER NPNH OR Dem LINK NOT 0 Interr);
	## Okta Sámedikki stuora hástalusain lea ain bargat buoridit... 
	## Okta ášši mas lea stuora mearkkašupmi, lea ahte... #NOT 0 Interr!

SELECT Gen IF (*-1 VFIN BARRIER N OR Pron OR CLB)(NOT 0 Num OR Dem OR VGen OR PrfPrc OR PrsPrc OR Indef OR Interr)
	(NOT 1 CLB)(*1C (N Nom) OR (N Acc) BARRIER NPNH OR PrfPrc OR CC OR PUNCT LINK 
	NOT 0 Prop OR TIME OR ROUTE);	
	## Lávdegottiid doaibmaáigi čuovvu Sámedikki válgaáigodaga.
	## Lea deaŧalaš movttiidahttit vuolit gráda studeanttaid válljet sámi guoskevaš čuolbmačilgehusaid ja eamiálbmotáššiid váldofágafáddán.

SELECT Gen IF (-1 ("nugo"))(1 N);
	## Ráđđehus áigu ruhtadit dehálaš oktasašdoaimmaid, nugo suorggi dulkaoahpu ja diehtojuohkinbálvalusa.	

SELECT Gen IF (NOT 0 Dem OR Num OR Attr OR Indef OR VERB OR ("ahkit" Nom))
	(NOT 0 (N Acc) LINK 1 (Prop Acc))(NOT 0 ("hearrá") LINK 1 ("ipmil"))
	(*1C (Acc N) BARRIER NPNH OR PrfPrc OR CLB OR CC OR PUNCT LINK 1 EOS);
	## Divššohas galgá ieš beassat mearridit dearvvašvuođaveahki
	## miehtama.
	## Bistevažžan dahkat sámi kulturdieđu lohkanbajioahpu.

SELECT Gen IF (NOT 0 A OR Dem OR Num)(*1 (N Acc) BARRIER NPNH OR CC LINK 1 PrfPrc);
	## Tromssa fylkagieldda joatkkaskuvladási skuvlastruktuvrra árvvoštallan.
	## Dearvvašvuođaregisttarlága mielde leat divššohasjournálat
	## ásahuvvon dikšuma várás.
 # Many of the PrfPrcs referred to will turn out to be Actios. That is OK.

SELECT Gen IF (0 Interr)(1 (N Ess))(NOT 1 V-SG1 LINK NOT 1 VFIN);
	## ...man ulbmilin lea lohkat ja juogustit bohccuid.

SELECT Gen IF (*-1 (@OBJ) BARRIER CS OR CP)(0 (Pers Sg2))(1 (Refl Gen PxSg2));
	## Hearrá, mun lean gullan olusiid muitaleamen dán olbmá ja buot baháid 
	## birra maid son lea dahkan du iežat olbmuide Jerusalemis.

SELECT Gen IF (*-1 COPULAS BARRIER NOT-ADV-PCLE LINK *-1C Nom BARRIER CS OR CP OR
	CC OR CLB)(NOT 0 Loc)(*1C ADVLCASE BARRIER NPNH LINK NOT 0 Ess)
	(NOT 1 Inf OR PrfPrc LINK 0 TV)(NOT 1 CC)(NOT 0 Num LINK 1 (N Gen));
	## Arkelaus lei šaddan Judea gonagassan.
	## Don it leat guhkkin eret Ipmila riikkas.

SELECT Gen IF (*-1 ACC-ILL-VERB BARRIER NOT-ADV-PCLE)
	(*1 Ill BARRIER NPNH LINK *1 Acc BARRIER NPNH OR CC LINK NOT 0 Interr)
	(NOT *1 Ill BARRIER NPNH LINK *1 Acc BARRIER NPNH LINK 1 Po)
	(NOT 0 (Dem Pl) LINK *1 (Sg Ill) BARRIER NPNH);
	## Láhkaásahus ii bija doaimma guđege oassái ovddasvástádusa ásahit
	## journálavuogádaga.

SELECT Gen IF (*-1 ("dahkat") BARRIER VERB OR CS OR CP)(*1 Com BARRIER NPNH);
	## Ii go mus leat lohpi dahkat iežan opmodagain mo ieš dáhtun?

SELECT Gen IF (0 ("okta"))(1 (N Gen) LINK *1 N BARRIER NOT-ADJ);
	## Dan eatnamis oažžu ovtta gusa fuođđara.

SELECT Gen IF (-1 Acc OR Gen)(0 ("sápmi" Sg))(1 N); 
	## Čálán girjji sámi áššiid birra.
	
SELECT Gen IF (0 ("sápmi" Sg))(*1C Acc BARRIER NOT-ADJ);
	##  Dalle čujuhuvvui ahte lea dárbu nannet doaimmaid mat ovddidit 
	## sámi dutkama, fágaidgaskasaš ovttasbarggu ja sámi áššiid oahpahusa.
	## Ráđđehus deattuha sámegiela ja sámi kulturáddejumi gelbbolašvuođa loktema.	
	
SELECT Gen IF (0 ("sápmi" Sg))(1C Gen)(2C Acc);
	## Ráđđehus deattuha sámegiela ja sámi kulturáddejumi gelbbolašvuođa loktema, ja sámi bargiid bálkáheami.	
	
#SELECT Gen IF (0 ("sápmi" Sg))(1 CC)(2 (A Attr));
	## ...váfistan dihte sámi ja eamiálbmotguskevaš dutkama.
	# This rule doesn´t allow "sápmi" Acc + CC + A + Acc, which could be true.	

REMOVE (Pers Acc) IF (-1 Acc)(NOT *1 V-TRANS-ACT BARRIER CS);
	## Mun oidnen ovtta du vieljain.

REMOVE Acc IF (*-1 CLB BARRIER WORD)(NOT 0 Interr)(NOT 1 CC OR CLB)
	(*1 Nom BARRIER NPNH OR CC LINK NOT 0 Interr);
	## Sámi dáidda- ja kulturpolitihkka.
	## Sámiid válgalihttu evttoha ahte...

REMOVE Acc IF (*-1 ILL-VERB BARRIER NPNHA)(*1 Ill BARRIER NPNH);
	## Ehpet eisege beasa almmi riikii.

REMOVE Acc IF (*-1 Ill BARRIER VERB OR CC OR CLB OR CS OR CP)
	(*1 ILL-VERB BARRIER V-NOT-AUX OR CC OR CLB OR CS OR CP);
	## Dan válddi, masa Ruonáeatnama ruovttustivra lea ollen,...

REMOVE Acc IF (*-1 Nom BARRIER NOT-ADV-PCLE LINK *-1 ILL-VERB 
	BARRIER NOT-ADV-PCLE)(*1 Ill BARRIER NPNH);
	## ...mat gullet sámilága giellanjuolggadusaid hálddašanguvlui.

REMOVE Acc IF (*-1 LOC-VERB BARRIER NPNHA OR Loc OR CLB)
	(*1 Loc BARRIER NOT-NP LINK NOT 0 (V-TRANS-ACT Actio) OR Prop);
	## Divššohasas lea vuoigatvuohta biehttalit journála olggosaddimis.

REMOVE Acc IF (*-1 Nom BARRIER NOT-ADV-PCLE LINK *-1 LOC-VERB 
	BARRIER NOT-ADV-PCLE)(*1 Loc BARRIER NPNH);
	## 

REMOVE Acc IF (*-1 LOC-VERB BARRIER NOT-ADV-PCLE OR go LINK NOT 0 ACC-LOC-VERB
	LINK *-1 Loc BARRIER V-NOT-AUX OR (@SUBJ) OR CC OR CLB OR CS OR CP);
	## Das čuovvu mu oainnu mielde dat, ahte...

REMOVE Acc IF (*-1 ILL-VERB BARRIER NOT-ADV-PCLE OR go LINK NOT 0 ACC-ILL-VERB
	LINK *-1 Ill BARRIER V-NOT-AUX OR (@SUBJ) OR CC OR CLB OR CS OR CP);
	## ...masa gullet Kárášjoga, Guovdageainnu, ...ja Gáivuona gielddat.
	
SELECT (Attr) IF (0 ("nr"));

CONSTRAINTS # 17

#SELECT Gen IF (*-1 Ill BARRIER NPNH LINK NOT 0 Px)
	#(*1 Po BARRIER NPNH LINK NOT 0 Px);
	## Son bijai su alimussii tempela geađgemuvrra ala.
	#Might have to be checked.

SELECT Gen IF (*-1 Loc BARRIER NPNH LINK NOT 0 Actio)(NOT 0 VFIN)
	(NOT 1 Pers)(*1 Po BARRIER NPNH);
	## ...mii lea leamaš čihkosis máilmmi sivdnideami rájes.

SELECT Gen IF (*-1 CC OR COMMA BARRIER NPNH LINK -1 Loc)(NOT 0 VERB)
	(*1 Loc BARRIER NOT-NP OR REALCLB LINK NOT -1 CC);
	## Su lusa bohte olbmot Jerusalemis ja oppa Judeas ja Jordana guovlluin.

SELECT Gen IF (0 ("láhka") OR ("mánáidsuodjalus#láhka") OR ("ávžžuhus"))
	(1 ("§") OR ("mearrádus")); 
	## ...gč. dearvvašvuođabargiid lága§ 45.

SELECT Gen IF (-1 CC)(0 (N Prop))(*1 (N Nom) BARRIER NPNH OR COMMA OR CC);
	## ...ja Romssa universitehta lea doaibmaplána bokte...

SELECT Gen IF (1 ("/"))(2 Attr)(NOT 2 Prop);
	## Lea dárbbašlaš erenoamážit vuoruhit sámi/eamiálbmotguskevaš dutkamiid.

SELECT Gen IF (-1 Indef)(0C Pers)(*1 (N Pl Loc) BARRIER NPNH);
	## Muhtun kretalaš, muhtun sin iežaset profehtain, lea dadjan...
 
SELECT Gen IF (*-1 ("dego") BARRIER NOT-NP)(*1 (N Nom) BARRIER NOT-NP OR CLB);
	## Máret livžu dego logi máná eadni.

REMOVE Acc IF (-2 ADVLCASE)(-1 ("go" Pcle))(*1 ADVLCASE BARRIER NPNH);
	## Dat gullá lunddolaččat dakkár áššiide go Sámedikkiid válganjuolggadusaide.

REMOVE Acc IF (-1 ("sárdnut") BARRIER NOT-ADV-PCLE)(0 Gen)
	(*1 ("birra") BARRIER NPNH);
	## Mii gullat sin sárdnumin Ipmila stuora daguid birra min iežamet gillii.

REMOVE Acc IF (*-1 ("birra") BARRIER NPNHA LINK *-1 ("sárdnut") BARRIER VERB
	OR CC OR CLB OR CS OR CP);
	## Mii gullat sin sárdnumin Ipmila stuora daguid birra min iežamet gillii.
	
SELECT Gen IF (0 MANNU LINK 1 Ord);
    ## Ráđđehus áigu gulaskuddancealkámušaid vuođul árvvoštallat nuppástuhttit lága levgema birra gielddaid almmolaš visttiin, ja evttohusa dahkat guovvamánu 6. birra virggálaš levgenbeaivin.

SELECT Gen IF (*-1 ("vrd") BARRIER NOT-AA)(1 Com); 
	## ... vrd. earret eará pasientavuoigatvuođaid lágain ja dearvvašvuođabargiid lágain.

    
# Acronyms and other nouns where Nom=Gen/Acc
# - - - - - - - - - - - - - - - - - - - - - -
    # vahkku
    # plana

REMOVE Nom IF (*-1 (@SUBJ) LINK *1 VFIN LINK NOT 0 Pass BARRIER VFIN LINK NOT 0 Pass)
	(NOT -1 (Prop Attr))
	(0 (Sg Gen) LINK *1 N BARRIER NOT-ADJ)
	(NOT *-1 CC BARRIER NPNH LINK -1 (@SUBJ))
	(0 (@SUBJ) LINK NOT *1 (@SUBJ) BARRIER CP OR CS);                                                        
    ## Buollinveahka lea maiddái leamaš skuvllain ja mánáidgárddiin muitaleame buollingáhttema, ja oassálastán DBE jahkásaš buollingáhttenvahkus.
    ## Oasttán LSD gávpogis.
    ## Čalmmeheamit ja bealje- ja čalmmeheamit ja olbmot váilevaš dárogiel máhtolašvuođain dárbbašit veahki ávkašuvvat geahččanvuoigatvuođain.


# Genitive in coordination
# ------------------------

SELECT Gen IF (*-1 BOS OR CS OR (":" CLB) BARRIER NOT-ADV-PCLE)(NOT 0 (Pers Nom))
	(NOT 0 (Prop Nom) LINK 1 CC LINK 1 (Pers Sg3 Gen))(1 CC)
	(2 NP-HEAD-GEN LINK *1 (N Nom) BARRIER NPNH LINK NOT 1 Loc);
	## Kanáda ja Austrália riekteapparáhtat leat fuomášan...  
	## Jekonja ja su vieljat leat dál. # (NOT 0 (Prop Nom)...

SELECT Gen IF (*-1 V-TRANS-ACT BARRIER NOT-ADV-PCLE)(0 HUMAN-INDIVIDUAL)(1 CC)(2 HUMAN-INDIVIDUAL LINK 0 Gen LINK 1 Gen OR Acc);	
	## Mun vuovddán eatni ja áhči beatnaga.
	
SELECT Gen IF (*-1 V-TRANS-ACT)(-2 Acc)(-1 CC)(0 HUMAN-INDIVIDUAL)(1 Acc OR Gen);
	## Mun vuovddán gusa ja áhči beatnaga sidjiide.	

SELECT Gen IF (NOT 0 Nom LINK *-1 VFIN BARRIER NPNHA)(1 CC)
	(*2 Po OR PREGEN BARRIER NPNH OR Nom LINK NOT *1 Interr OR VFIN OR Inf 
	BARRIER WORD);
	## Divššohas galgá maiddái oažžut dieđuid vejolaš riskkaid ja
	## lassiváikkuhusaid birra.

SELECT Gen IF (1 COMMA)(*2 NP-HEAD-GEN BARRIER NPNH LINK 1 CC 
	LINK *1 Po BARRIER NPNH);
	## Dat čuožžila vuođđolága ásaheaddji, láhkaásaheaddji, ráđđe-
	## husa ja majestehta vuostá.

SELECT Gen IF (1 COMMA)(*2C NP-HEAD-GEN BARRIER NPNH LINK 1 COMMA
	LINK *1 NP-HEAD-GEN BARRIER NPNH LINK 1 COMMA OR CC OR NP-BORDER);
	## ...nannet máhtu sámi historjjá, kultuvrra, árbevieruid ja
	## bajásgeassinnorpmaid ja -minstariid birra.

SELECT Gen IF (1 COMMA)(2 CC)(*3 (N Gen) BARRIER NPNH LINK 1 Po);
	## ...dán vuolde mearrádusat rájadeami, hilguma ja buktima birra.

SELECT Gen IF (-2C NP-HEAD-GEN LINK *-1 NP-BORDER BARRIER NPNH)(-1 CC);
	## ...ahte kultuvrra ja eallinvuogi eaktivuohta ii galgga leat...  

SELECT Gen IF (-2C NP-HEAD-GEN LINK NOT 0 @NQ<)(-1 CC)(*1 NP-HEAD-GEN BARRIER NPNH);
	## ...šiehtadus Hálkavári ja Iŋggásguolbana báhčinguovlluid dáfus. 

SELECT Gen IF (-2C Gen)(-1 COMMA)(1 COMMA)(2 Gen);
	## ...masa gullet Kárášjoga, Guovdageainnu, Unjárgga, Porsáŋggu, Deanu
	## ja Gáivuona gielddat.

SELECT Gen IF (-3C NP-HEAD-GEN LINK *-1 NP-BORDER BARRIER NPNH)(-2 CC)(-1 PRE-NP-HEAD);
	## Guovlluid dearvvašvuođafitnodagaid ja eará stáhta doaimmaid hálddahusstivremis SDD:s lea vejolaš váikkuhit njuolggabut.

SELECT Gen IF (NOT *-1 V-NOT-AUX BARRIER NPNHA OR CLB)(NOT -1 Gen)
	(NOT 0 Nom OR PrsPrc)(1 CC)(2 Gen)(*3 Loc BARRIER NPNH OR CLB);
	## Tyrusa ja Sidona guovlluin bohte olbmot su lusa.

SELECT Gen IF (-2C Gen)(NOT -2 Cmpnd)(-1 CC)(*1 Loc BARRIER NPNH OR CLB);
	## ...muhto farisealaččaid ja saddukealaččaid oahpus.

REMOVE Acc IF (NOT -2 Acc LINK *-1 Inf BARRIER NPNHA)(-1 CC)(NOT 0 Interr)
	(*1C (N Nom) BARRIER NPNH);
	## Sámedikki čoahkkinjođihangotti ja lávdegottiid čoahkkimat dollojit... 

SELECT Gen IF (*-1 COMMA BARRIER NPNH LINK -1C NP-HEAD-GEN)(1 CC)
	(*2 NP-HEAD-GEN BARRIER NPNH);
	## Gielddaid, fylkkagielddaid ja guovlluid dearvvašfitnodagaid
	## bargoaddit galget...

SELECT Gen IF (-2 Ill)(-1 CC)(NOT 0 Cmpnd OR (@TITLE))(*1 Ill BARRIER NPNH);
SELECT Gen IF (-2 Com)(-1 CC)(NOT 0 Cmpnd)(*1 Com BARRIER NPNH);
	## Eahpitkeahttá lea ávkin boazodollui ja sámi beroštumiide dat.
	## Gáhttenplánain ja gáhttenplána evttohusain ii addojuvvo doarvái... 

SELECT Gen IF (-2 Loc)(-1 CC)(NOT 0 Cmpnd)(*1 Loc BARRIER NPNH);
SELECT Gen IF (-2 Ess)(-1 CC)(NOT 1 CC)(*1 Ess BARRIER NPNH LINK NOT 0 Actio);
	## Dál lea dárbu oažžut ollislaš geahčastaga ovdánanminstaris ja
	## otnáš areáladilis.
	## Sámit leat aktiivvalaš ovttasbargoguoibmin ja eavttuid biddjin. 

#SELECT Gen IF (NOT *-1 V-TRANS-ACT BARRIER REALCLB OR PUNCT OR CS)
#	(NOT 0 Nom LINK *1 Gen BARRIER NPNH)(NOT 0 VFIN LINK 1 Adv)
#	(NOT 0 PrfPrc LINK 1 N)(*1 Po BARRIER NPNH OR REALCLB OR PUNCT OR (Pron Pers));
SELECT Gen IF (NOT *-1 V-TRANS-ACT BARRIER REALCLB OR PUNCT OR CS)
	(NOT 0 Nom LINK *1 Gen BARRIER NPNH)(NOT 0 VFIN LINK 1 Adv)
	(NOT 0 PrfPrc LINK 1 N)(*1 Po BARRIER NPNH OR REALCLB OR PUNCT OR (Pron Pers) LINK NOT 0 ("mielde"));	
	## ...jus eai leačča dárbbašlaččat dearvvašvuođa vahátváikkuhusaid
	## eastadeami dihtii.

SELECT Gen IF (-1 (Pl3 Gen) LINK *-1 (@OBJ) BARRIER NOT-ADV-PCLE)(1 CC)(2 Gen)
	(NOT 2 Cmpnd)(*3 N BARRIER NPNH LINK NOT 0 Gen);
	## Boazosápmelaččain lea vuoigatvuohta geavahit eatnamiid ja gáttiid 
	## sin elliid ja sin iežaset ealáhussii.

SELECT Gen IF (1 ("/"))(2C Gen);
	## ...mii ii čuoza nu garrasit viidodaga/čikŋodaga ja guhkkodaga dáfus.

SELECT (Gen @GN>) IF (*-1 BOS LINK *1 (Gen @GN>) LINK 1 (N Nom) LINK *1 TV LINK *1 (N Gen @GN>) LINK 1 Nom LINK *1 TV LINK 0 VFIN);
            ## Dearvvašvuođabargiilága § 10, mii gieđahallá dieđuid addima pasienttaide, ja pasientavuoigatvuođalága § 1-3, mii gieđahallá vuoigatvuođa informerejuvvon miehtamii, leat ovdamearkkat dakkár vuoigatvuođain.
            #Parallell constructions.

# Nominative and accusative
# =========================

REMOVE Gen IF (-1 BOS OR CS OR CLB)(NOT 0 TIME)(1 (Pron Pers))(*1 VFIN); 
	## Daid mun sáltejin. 
	## ...man mii dan áigge dovdat gonagasriika Norgan.

REMOVE Gen IF (-1 BOS OR CS)(1 Adv)(NOT 1 N OR A); 
        ## Dieđuid maiddái ii dárbbaš addit go...

REMOVE Gen IF (NOT 0 TIME)(1 CLB)(2 CC)(3 NPNH);#(NOT 3 Gen LINK *1 Po BARRIER NPNH);
	## Eará veahki sáhttá addojuvvot jus divššohas čájeha beroštumi, ja lea...

REMOVE Gen IF (*-1 V-TRANS-ACT BARRIER CS OR CC OR VERB)(NOT *-1 Pr OR NUMERALS BARRIER NP-BORDER)(NOT -1 ("mii"))
	(NOT *-1 CC BARRIER NOT-NP LINK *-1 GASKAL BARRIER CS)(NOT 0 TIME OR ROUTE)(1 Ill OR Loc OR Com)(NOT *-1 HEADOFPARTS LINK 1 Gen LINK 1 Loc);
REMOVE Gen IF (-2 V-TRANS-ACT)(-1 ("go"))(0 Acc)(1 REALCLB);	
	## Eai lean váldán go niestebohcco.
	## Mun vuovddán gusa ja áhči beatnaga oahpaheaddjái.
	

REMOVE Gen IF (NOT *-1 Pr OR NUMERALS BARRIER NPNH)(0 N)(1 Dem);
	## Jahkásaš bušeahttarámma dán prošeavtta doaimmaide gártá oktiibuot
	## 70.000 ruvnno. 

REMOVE Gen IF (NOT *-1 Pr OR NUMERALS BARRIER NPNH)(1 BEAIVI);
	## Sii bivdet luosa mánnodaga.

REMOVE Gen IF (*-1 OPRED-VERB BARRIER NPNH LINK NOT *-1 NP-HEAD-ACC
	BARRIER V-NOT-AUX OR CC OR CLB OR CS OR CP)(1 Ess);
	## Don galggat gohčodit su Jesusin.

REMOVE Gen IF (*-1 V-NOT-AUX BARRIER CS OR CP)(1 ("§"));
	## Vuoigatvuohta oažžut dearvvašvuođaveahki § 2-2.
 # This rule would work fine if we could make an earlier rule select Gen 
 # for all occurrences of compounds in -lága in front of§. 

REMOVE Gen IF (NOT *-1 Pr BARRIER NP-BORDER)(0 (Prop Nom))(1 CLB)(NOT 2 (Prop Gen));
	## Gažaldat áirasis Egil Olli, Bargiidbellodaga sámediggejoavku.

REMOVE Gen IF (*-1 VFIN BARRIER NOT-ADV-PCLE)(0 N)(1 CC)(2 (Pron Sg3 Gen));
	## Váldde máná ja su eatni fárrui.

REMOVE (Interr Gen) IF (1 Dem);
	## Viiddodatrámmaid geažil, maid dát ovdanbuktit ferte doallat,...

REMOVE Gen IF (NOT 0 Dem)(*1 ("hávvi" N Sg Gen) BARRIER NPNH);
	## ..geasa dearvvaávuođabálvalus addá dahje fallá veahki juohke
	## áidna háve...

REMOVE Gen IF (NOT *-1 Pr OR NUMERALS BARRIER NPNH)(NOT 0 TIME)(1 CLB)(2 CC);
	## Dat váikkuha ollu dasa, mo olbmot vásihit bálválusaid, ja oažžu sin
	## dovdat stuorát oadjebasvuođa.

REMOVE Gen IF (NOT *-1 Pr OR NUMERALS BARRIER NPNH)(NOT 0 TIME)(1 Inf LINK 0 V-TRANS-ACT)(1C VERB);
	## Vuoigatvuohta journála geahččat.

REMOVE Gen IF (0 ("stáhtus"))(1 Ess)(*2 V-SG3 BARRIER NOT-ADV-PCLE);
	## Inuihtaid stáhtus eamiálbmogin ii leat vuođđolágas nannejuvvon.

REMOVE Gen IF (1 ("áigi" Sg Loc))(2 ("áigi" Sg Ill));
	## Lea stuorradiggi mii ferte mearrádusa áiggis áigái dulkot ja atnit.

REMOVE Nom IF (*-1 Acc BARRIER CS OR CP OR CONTRA OR VFIN LINK *-1 ACC-INF-VERB
	BARRIER VFIN OR CLB OR CS OR CP OR PUNCT OR REALCLB)
	(NOT *-1 COPULAS BARRIER NOT-ADV-PCLE)(NOT *-1 (Interr Acc) BARRIER Acc);
	## Hearrá, suova mu vuos mannat ruoktot hávdádit áhččán.
 
REMOVE Acc IF (NOT *-1 V-TRANS-ACT-NOT-ACTIO BARRIER CS LINK 0 (@+FMAINV) OR (@-FMAINV))(NOT *-1 CS BARRIER V LINK 1 @CS-COMPL)(NOT 0 TIME OR Dem)
	(NOT 0 NUMERALS LINK *1 TIME OR MEASURE BARRIER NOT-A)
	(NOT *1 V-TRANS-ACT BARRIER CS);
	## Sámedikkis lea leamaš čoahkkin 14.03.01.
	## Leago dudno mielas suohtas go sáhttá rabas julggiid vázzit?
  


# Predicates of accusative subjects
# ---------------------------------
SELECT (Sg Acc) IF (*-1 COPULAS BARRIER NOT-ADV-PCLE LINK 0 Inf 
	LINK *-1 (Sg1 Acc) OR (Sg2 Acc) OR (Sg3 Acc) BARRIER NOT-ADV-PCLE)
	(NOT 0 PrfPrc)(NOT 0 Attr LINK *1 N BARRIER NPNH);
	## ... de diktet mu leat jalla vai mun ge beasašin veahá rábmot! 
 # and more rules!

 # Rules for object predicatives are found in the accusative section, but 
 # this rule needs to come earlier since copulas are involved.



# ================ #
  CONSTRAINTS #18  #
# ================ #
# Nominative
# ==========

# Miscellaneous  rules
# --------------------

REMOVE (Nom) IF (*-1 Nom BARRIER NPNHA OR CC OR CLB LINK NOT 0 Dem)(0 (A Attr))(1 N);
	## Sápmelaččat leat oadjebasat olggut birasáitagiid ektui.

REMOVE (Nom) IF (*-1 Nom BARRIER NPNHA OR CC OR CLB LINK NOT 0 Dem)(0 (Prop Sur Attr))(1 (N Sur));
	## Tor Berg Hansen

REMOVE (Nom) IF (*-1 Nom BARRIER NPNHA OR CC OR CLB LINK NOT 0 Dem)(0 (Prop Sur Attr) OR (Prop Sur Attr) OR (Prop Sur Attr))
	(1 (N Sur) OR (N Mal) OR (N Fem));
	## Tor Arne Berg Hansen
	## Tor Hansen Per Olsen

REMOVE (N Nom) IF (*-1 (Pers Nom) BARRIER NOT-ADJ)(NOT 0 Num)(NOT 1 EOS);
	## Muhto son várra áinnas beroštii dakkár dilálašvuođaide. 
	## Don ipmilmeahttun jalla! # NOT 1 EOS!

REMOVE (Sg Nom) IF (NOT *-1 CC BARRIER NPNHA LINK *-1 Nom)(NOT 0 (N Num))
	(*1C V-DU-PL BARRIER NOT-ADV-PCLE OR CLB); 
	## Historjjálaš dilálašvuođat uhccán mearkkašit dálá dilálašvuhtii. 

REMOVE Nom IF (0 ("hearrá"))(1 ("ipmil"))(NOT 1 Nom);
	## Hearrá Ipmilat galggat gudnejahttit.

SELECT Loc IF (1 ("eret"));
	## Dáiddát don ge leat Galileas eret.

SELECT Nom IF (-1 Gen)(NOT -1 (Pron Dem Gen))(0 N-SG-NOM)(NOT 0 PrfPrc OR Ill)
	(1 V-SG3);
	## Du ášši lea dehalaš.
	## Dan ášši ferte dutkat.
 # HMM, brave.. - but it seems to work!
 # Note that the tag is selected, the set is in the condition.

SELECT Nom IF (NOT *-1 V-TRANS-ACT BARRIER CC OR CS OR CP)(-1C Gen)
	(0 ("dáidda"));
	## Deike lea máŋggaid jagiid čohkkejuvvon sámi dáidda.

SELECT Nom IF (-2 Num)(-1 ("ruvdno"));
	## Láđđi máksá 70 ruvnno mehtar.

SELECT (N Num Nom) IF (-1 (Pers Nom))(-1 Pl1 OR Pl2 OR Pl3);
	## De mii golmmas leansmánniin manaimet geahččat.

SELECT (N Nom) IF (*-1 ("okta" Nom) BARRIER NPNH LINK NOT 0 Foc);
	## Málli vuššojuvvui okta lihtter.

SELECT Nom IF (0 ("veara"))(1 VERB);
	## Eanetlohku ii oro namuhan veara berošteamen gielddaid dárbbuid.

# Nominative if two names in a row
SELECT Nom IF (-1 PROP)(0 PROP)(*1 V-SG3 BARRIER VFIN); 
	## Birasgáhttenminister Siri Bjerke dajai čielgasit ahte...

 
# Vocatives, subjects of sentence fragments
# - - - - - - - - - - - - - - - - - - - - - 
SELECT Nom IF (*-1 BOS OR (":") BARRIER NOT-ADJ)(0 Prop)(1 COMMA); 
	## Biera, du sabehiid!
	## Jovsset, Dávveda Bárdni!

SELECT (Sg Nom) IF (*-1 MUN OR DON OR (Pron Sg3) BARRIER NOT-AA 
	LINK *-1 BOS OR (":") BARRIER NOT-ADJ-CC)(1 EOS);
	## Son celkkii: Don jalla!

SELECT (Pl Nom) IF (*-1 MII-PERS OR DII OR SII BARRIER NPNHA
	LINK *-1 BOS OR CLB BARRIER NOT-ADJ-CC)(1 EOS);
	## Dii guoktilaččat!

SELECT (A Pl Nom) IF (-2 MII-PERS OR DII OR SII LINK *-1 BOS OR CLB 
	BARRIER NOT-ADJ-CC)(-1 Adv OR Indef)(1 EOS);
	## Dii uhccán oskkolaččat!

SELECT Nom IF (*-1 BOS OR PUNCT BARRIER NPNH)(1 COMMA)(2 Imprt);
	## Sáhtán, gáidda eret!

SELECT Nom IF (-1 BOS OR PUNCT OR REALCLB)(1 COMMA);
	## Oahpaheaddji, mun áiggun čuovvut du.

SELECT Nom IF (*-1 (Pron Sg2) BARRIER NPNH LINK -1 COMMA)(0 N)(1 EOS);
	## Mun dubmen du iežat sániid mielde, don heajos bálvaleaddji!


# Nominative in titles and sentence fragments
# - - - - - - - - - - - - - - - - - - - - - -

# A single word is nominative
SELECT Nom IF (-1 BOS OR (":"))(NOT 0 VFIN)(0 N OR A OR Pron)(1 EOS); 
	## Katja.
	## Bálvaleaddjit manne dalle isideaset lusa ja jerre: Hearrá! 

# A single word with a numeral in front of it is nominative
SELECT Nom IF (-2 BOS)(-1 Num)(1 EOS);
	##

# An NP head with a genitive modifier is nominative
SELECT Nom IF (-2 BOS)(-1 Gen)(0C N)(1 EOS);
	##

# A title is nominative if it has a Nom reading at all.
SELECT Nom IF (0 (@TITLE));
	## Stuorra giitu maiddái professor Birger Stuevold Lassenii.
	# in this case the title of a person is meant, not a sentence fragment
 
# An NP head with an Attr modifier is nominative.
SELECT Nom IF (-2 BOS)(-1 Attr)(1 EOS);
	##

# A clause-final numeral preceded by § is nominative.
SELECT Nom IF (-1 ("§") OR ("kapihtal") OR ("kapihttal"))(0 Num)(1 EOS); 
	## Vuoigatvuohta oažžut dárbbašlaš dearvvašvuođaveahki § 2-2.

# A Gen series is broken by an intermediate Prop
REMOVE Gen IF (0 Nom)(1 (Prop Gen))(*2 N OR Po BARRIER NPNH);
	## Ráđđehusa dearvvašvuođa- ja sosiálabálvalusaid doaibmaplána Norgga
	## sápmelaččaid várás  .


# Nominative after "go", "dego" and "nugo"
# - - - - - - - - - - - - - - - - - - - - -
SELECT Nom IF (*-1 ("go" Pcle) BARRIER NPNH LINK NOT -1 VFIN)
	(*-1 COPULAS BARRIER V-NOT-AUX OR CS)(NOT 1 N OR A);
	## Jesus lea gievrrat go Sáhtán ja bahá vuoiŋŋat.

SELECT Nom IF (*-1 ("go" Pcle) BARRIER NOT-NP LINK NOT -1 VFIN)
	(NOT 0 Acc OR VFIN)(1 CLB OR CS OR EOS);
	## Ii leat almmustuvvan oktage stuorit go gásttašeaddji Johanas.

SELECT Nom IF (*-1 ("dego") OR ("nugo") BARRIER NPNH)
	(NOT 0 Attr LINK *1 N BARRIER NPNH)(*1 NP-BORDER OR CLB BARRIER WORD);
	## Su ámadadju báittii dego beaivváš.

SELECT Nom IF (-2 ("dego") OR ("nugo"))(-1 Gen OR PrsPrc)(1 NP-BORDER);
	## ...johtá dego gillju ledjon ozadettiin muhtuma...

REMOVE Imprt IF (*-1 ("dego") BARRIER VFIN OR CS OR CP);
	## ...dego vuonccis čohkke čivggaidis soajáidis vuollái.

SELECT Nom IF (-1 ("go" Pcle))(0 Num);
	## ...go heaittihanáigemearri lea oanehat go vihtta jagi.
	
# Nominative of "nummar" and "nummir" preceding a cardinal
# - - - - - - - - - - - - - - - - - - - - - - - - - - - - - 
REMOVE (N Gen) IF (0 ("nummar") OR ("nummir"))(1 CARDINALS OR WORD);

# Nominative subjects
# -------------------

# Preverbal subjects
# - - - - - - - - - -
SELECT (N Nom) IF (*-1 (Dem Nom) BARRIER NOT-ADJ)(NOT 0 ("leapma" Dimin));
	## Noađđespagáin leat dat geažit guokte golbma tumá guhku.
	## ...ja ahte dat dáhttu čielgasit berre doahttaluvvot. 

SELECT (Num Nom) IF (-1 (Dem Sg Nom));
	## Dát guokte ášši Siviilaáittardeaddji geavadis leat váldon...

SELECT Nom IF (*-1 CS OR BOS OR CC OR CP OR PUNCT OR (":") BARRIER NPNHA OR (Dem Acc))(0 Sg)
	(NOT 0 Adv OR Loc)(NOT 0 Ess LINK 0 (@ADVL))
	(NOT 0 Attr LINK *1 N BARRIER NPNH)(*1C V-SG3 BARRIER NOT-ADV-PCLE);
	## ...ahte doaimma jođihangoddi vejolaččat mearrida...

SELECT (N Sg Nom) IF (NOT 0 Acc)(1 V-SG3)(NOT 1 N)(NOT *2C Nom BARRIER CS);
	## Doaibmaplána árvvoštallan čajeha maiddái ahte...
	## Dan ášši ferte čilget nállevealaheami ektui sierra. # Topicalised object.

SELECT (N Sg Nom) IF (NOT *-1 (N Sg Nom) BARRIER CS OR CP OR CC OR CLB)
	(NOT 0 Loc)(1 (Neg Sg3));
	## Sámi dutkan ii leat dan dihte soaittáhagas válljejuvvon dutkansuorgi.
 	
SELECT Nom IF (*-1 CS OR BOS OR CC OR CP OR PUNCT OR (":") BARRIER NPNHA)(0 Pl)
	(NOT 0 Adv OR Ill)(*1 V-PL3 BARRIER NOT-ADV-PCLE); 
	## ...go beaktilis sivat bealuštit dan.

SELECT Nom IF (0 Actio)(*1 ILL-VERB BARRIER V-NOT-AUX OR Nom OR CC OR CS OR CP);
	## Ruovttus fárren sáhttá čuohcit olbmui sakka.
 # The idea here is that 0 is not accusative, since there is an ILL-VERB.
 # So we could  alternatively REMOVE Acc. But the present rule seems to work.

SELECT Nom IF (*-1 CS OR BOS OR CC OR CP BARRIER NP-HEAD OR VFIN)(0 NUMERALS)
	(*1 V-PL3 BARRIER CS)(NOT *1 Nom BARRIER CS);
	## Guokte jahkásaš doavttergrádakurssa leat lágiduvvon.
	## Go máŋggas galget čállit seamma journálii, de...  
 # In the last example, the reading N Sg Nom is chosen because of the reading 
 # Num Sg Loc.

SELECT Nom IF (*-1 CS OR BOS OR CC OR CP BARRIER NP-HEAD OR VFIN)(0 NUMERALS)
	(*1 COPULAS BARRIER VERB OR CS LINK 0 (Pl3) LINK NOT *1 PrfPrc OR Inf
	BARRIER VFIN OR CS OR CP);
	## Vihtta bivdovuogi leat Deanus luosa bivdidettiin. 

SELECT (Sg Nom) IF (*-1 CS OR BOS BARRIER NOT-ADV-PCLE)(NOT 0 Adv OR PrfPrc)
	(NOT 0 Qst)(*1 V-SG3 BARRIER NOT-NP OR Nom OR Ess OR CS OR CP LINK 
	NOT *1 NP-HEAD-SG-NOM BARRIER NPNHA)(NOT 0 Gen LINK *1 N BARRIER NPNH)
	(NOT 0 Ess);
	## Mii vuordit ahte stáhta sámi vuoigatvuođaid barggu oktavuođas čuovvu...

SELECT Nom IF (0 (N Num Sg Loc))(*1 V-DU3 BARRIER NOT-ADV-PCLE);
	## Máná-guovttos leigga sávrrit viehkat.
 # The formulation is designed to hit "guovttos", also in compounds.

SELECT Nom IF (0 ("galle"))(*1 V-PL3 BARRIER Nom OR VFIN);
	## ... ja albmada galle "mearrasámi" ledje vearrováldit.

SELECT Nom IF (0 (N Num))(*1 V-PL3 BARRIER NOT-ADV-PCLE);
	## Dakkár doaimmat, main máŋggas čállet dieđuid journálaide,...

SELECT Nom IF (0 NOTIME OR Pron)(NOT 0 ("leat") OR PrfPrc)
	(NOT 0 A LINK *1 N BARRIER NPNH)(NOT 0 Adv OR Attr OR VFIN)
	(NOT 1 CC LINK 1 VFIN)
	(*1C V-PASS BARRIER LEFT OR V-NOT-AUX OR NP-HEAD OR Po OR CS OR CLB LINK NOT 0 Inf);
	## Váfistan dihte ođđa bargovugiid lea komitea viiddiduvvon.
	## Bušeahtas lea sullii 650.000 ruvnno leamaš várrejuvvon dáidda.
	## Dat guhte osku ja gásttašuvvo, bestojuvvo.

SELECT Nom IF (0 Num)(1 TIME)(*2 ("vássit") BARRIER V-NOT-AUX OR CC OR CLB OR CS OR CP);
	## Go logi jagi lea vássán maŋimus journálamerkesteami rájes,...

REMOVE (Prs Sg3) IF (NOT *-1 (Sg Nom) BARRIER CS OR CP)(0 A OR Indef)
	(*1C (Prs Sg3) BARRIER CS OR CP OR CC OR CLB OR ("dego") LINK NOT 0 Qst);
	## Man uhccán osku dus lea!

SELECT Nom IF (0 Sg)(1 CC)(*2 (Sg Nom) BARRIER NPNH LINK *1 V-DU3 
	BARRIER NOT-ADV-PCLE);
	## Silas ja Timoteus báziiga Bereai.

SELECT NP-HEAD-NOM IF (*-1 CC BARRIER NPNH LINK -1 NP-HEAD-NOM)(1 V-PL3);
	## Árbevierru ja geavat sáhtášedje leat dulkonmomeanttat.
  
REMOVE Nom IF (*-1 BOS OR CS OR (":" CLB) BARRIER NOT-ADV-PCLE)(NOT 0 (Pron Pers))(1C V-1-2);
	## No examples in corpus?

SELECT Adv IF (*-1 (N Pl Nom) BARRIER CS OR CP OR CC OR COPULAS OR ("orrut"))
	(0 (A Pl Nom));
	## Eamiálbmotlaš áššit jogo systemáhtalaččat dubmejuvvojit menddo siivut...

SELECT Adv IF (0 (A Pl Nom))(*1 V-NOT-AUX BARRIER CC OR CS OR CP)
	(*1 (N Pl Nom) BARRIER CS OR CP)
	(NOT 1 CC OR COMMA LINK *1 N OR A BARRIER NPNH);
	## Nappo leat gazaldagas historjjálaččat háhkkojuvvon vuoigatvuođat.

SELECT Adv IF (-1 BOS OR CS OR (":" CLB))(0 (A Pl Nom))(NOT 1 CLB)
	(NOT *1 V-DU3 OR V-PL3 BARRIER VFIN OR CS OR CP);
	## ...go cálálaččat lea registreren maid ja man ollu lea borran.
	## Reálapolitihkalaččat gehččojuvvon, de lea mihtilmas...

SELECT (A Pl Nom) IF (NOT *-1 (Pl Nom) OR (Pl3 Nom) BARRIER CC OR CS OR CP
	OR CLB)(0 NOMINAL-ADJ)(NOT 0 (N Pl Nom) OR (Pron Nom))
	(NOT 0 (A Comp Pl Nom))(NOT *1 (N Pl Nom) BARRIER CS OR CP OR CLB)
	(*1 V-PL3 BARRIER NOT-ADV-PCLE);
	## Go Herodes árvidii ahte viisát ledje beahttán su,...

SELECT (A Pl Nom) IF (*-1 BOS OR CS OR (":" CLB) BARRIER NOT-ADV-PCLE)(*1 Neg OR COPULAS 
	BARRIER NOT-ADV-PCLE LINK 0 Pl3)(NOT *1 V-NOT-AUX BARRIER CS OR CP);
	## Nu maŋimuččat šaddet vuosttamužžan.

SELECT (A Pl Nom) IF (*-1 BOS OR CS OR (":" CLB) BARRIER NOT-ADV-PCLE)(*1 Neg OR COPULAS 
	BARRIER NOT-ADV-PCLE LINK 0 Du3)(NOT *1 V-NOT-AUX BARRIER CS OR CP);
	## ...ahte oasálaččat eaba leat dássásaččat.

SELECT (A Pl Nom) IF (*-1 BOS OR CS OR (":" CLB) BARRIER NOT-ADV-PCLE)(*1 Neg OR COPULAS 
	BARRIER NOT-ADV-PCLE LINK 0 Pl3 LINK *1 CC BARRIER V-NOT-AUX OR CS OR CP 
	LINK *1 VFIN BARRIER CS OR CP);
	## Geafit gal leat álo din luhtte ja sidjiide sáhttibehtet dahkat buriid.
 

 
# Postverbal subjects
# - - - - - - - - - -
SELECT (N Sg Nom) IF (*-1 V-SG3 BARRIER NPNHAIIP OR CP LINK NOT 0 (Imprt Sg2) 
	LINK *-1 BOS OR CS OR CLB BARRIER Nom)(NOT 0 VFIN OR PrfPrc OR Indef OR 
	Gen OR Ill OR Loc)(NOT 1 ("ahte"))(NOT 0 Loc LINK *1 NP-HEAD-NOM 
	BARRIER NPNH)(NOT 0 PrsPrc LINK 1 (N Sg Nom));
	## Guovddášis dán bárggus lea Sámi dutkamiid guovddáš.
        ## Riikkaid galgá Piera oaidnit.

SELECT (N Sg Nom) IF (*-1 V-SG3 BARRIER NPNHAIIP OR CP LINK *-1 BOS OR CS OR CLB
	BARRIER Nom)(-1 (A Attr));
	## Dasgo dalle han gaikkoduvvo ođđa bivttas, ja...
 
SELECT (Pron Sg Nom) IF (*-1 V-SG3 BARRIER Nom OR CC OR CS OR CP 
	LINK NOT 0 (Imprt Sg2) LINK *-1 BOS OR CS OR CLB BARRIER Nom)
	(NOT 0 Attr LINK *1 A OR N BARRIER NPNH);
	## Erenoamášit guoská dát dakkár áššiide, mat...
	## Ovdabargguin ii boađe ovdan eará, go ahte... 

SELECT (Sg3 Nom) IF (*-1 V-SG3 BARRIER CS OR PUNCT)(NOT *1 V-SG3 BARRIER CS OR CP);
	## Eahpitkeahttá lea ávkin sámi beroštumiide dat go guovllut...

SELECT (Sg Nom) IF (*-1 (Imprt Sg3) BARRIER NOT-NP)(NOT 0 Acc OR Ess);
	## Nu báitos din čuovggas ge olbmuide.

SELECT (Pl Nom) IF (*-1 (Imprt Pl3) BARRIER NOT-NP)(NOT 0 Acc);
	##

SELECT Nom IF (*-1 Pass BARRIER NPNHA OR CC)(NOT *-1 Nom BARRIER CS OR REALCLB)
	(NOT -1 (Pass PrfPrc) LINK NOT *-1 ("leat") BARRIER CS OR CP OR REALCLB 
	OR PUNCT)(NOT 0 Attr LINK *1 N BARRIER NPNH);
	## ...ahte lea ráhkaduvvon diehtojuohkinmateriála.

SELECT Nom IF (NOT *-3 Nom BARRIER CS OR REALCLB)(-2 Pass)(-1 (Pass PrfPrc));
	## Váldonjuolggadussan galgá geavahuvvot čohkkejuvvon journála...

SELECT (N Pl Nom) IF (*-1 V-PL3 BARRIER NPNHAIIP LINK *-1 BOS OR CS OR CLB 
	BARRIER Nom)(NOT 0 PrfPrc);
	## 1995:s gárvánedje rádiobuvttadeapmái lanjat.

SELECT (Pl3 Nom) IF (*-1 V-PL3 BARRIER NP-HEAD-NOM LINK *-1 BOS OR CS OR
	CLB BARRIER NP-HEAD-NOM);
	## No hits?

SELECT (Num Nom) IF (*-1 ILL-VERB BARRIER NPNHA LINK 0 (Pl3) LINK *-1 Ill 
	BARRIER NOT-ADV-PCLE);
	## Eanetlohku, masa gulle Riektejoavkku 6 eará lahtu,...

SELECT (Num Nom) IF (*-1 V-PL3 BARRIER VFIN OR CS OR CP 
    LINK NOT *-1 (@SUBJ) OR (@SPRED) OR (@SUBJ-QH) BARRIER CS OR CP)
	(NOT *-1 Inf BARRIER VFIN OR CS OR CP LINK *-1 VFIN BARRIER CLB OR CS OR CP 
	OR CS);
	## .. ja ahte sin bálvalit moadde olbmo.
	## Skuvlajagis 2001/2002 leat oktiibuot 469 oahppi suohkana vuođđoskuvllain.
SELECT (Pron Pl Nom) IF (*-1 V-PL3 BARRIER NP-HEAD-NOM OR CC OR CS OR CP
	LINK *-1 BOS OR CS OR CLB BARRIER NP-HEAD-NOM);
	## Eará osiid giddodagaid dáfus, leat dát dávjá dilit maid...
	## Sáhttetgo earát gáibidt buhtadasa boahtá das makkár beroštumiin lea... 

SELECT Nom IF (*-1C V-PL3 BARRIER NOT-ADV-PCLE OR MO LINK *-1 BOS OR CS OR CLB
	BARRIER Nom)(0 (N Num) OR NUMERALS);
	## Mus báhce moadde girjji logakeahttá.
	## Go sotnabeaivi šattai, de čuoigaledje máŋggas várrái.

SELECT (Pl Nom) IF (*-1 V-PL3 BARRIER NPNHAI LINK *-1 BOS OR CS OR (":" CLB) BARRIER 
	NOT-ADV-PCLE)(0 NOMINAL-ADJ);
	## ...de bohte muhtun viisát nuortan Jerusalemii.

SELECT Nom IF (*-1 COMMA BARRIER NPNH LINK NOT *-1 V-MAIN BARRIER CS OR CP)(NOT *1 VFIN BARRIER CS OR CP)(NOT 0 Adv OR ("leat" PrfPrc))(NOT 0 Actio LINK 1 ("veara"));
	## Dá dat leat, 350 ruvnno.
	## Eará guovlluin gos leat vel unnit sámit go Finnmárkkus, leamaš dákkár ákkain vel stuorát deaddu almmolaš ságastallamis.  
	# It should not take the latter one.

#SELECT Nom IF (*-1 VERBAL-ACTIVITY-VERB BARRIER NPNH LINK -1 COMMA LINK *-1 VFIN);
SELECT Nom IF (*-1 VERBAL-ACTIVITY-VERB LINK -1 COMMA LINK *-1 VFIN)(NOT 1 Nom);
	## Don leat jallu, dadja Tomi.
	## Don leat jallu, dadja Tomi skihpar.

 
# Nominative predicatives
# -----------------------

REMOVE A IF (NOT *-1 COPULAS OR OPRED-VERB BARRIER V-NOT-AUX)(0 N)(NOT 0 ("veara"))
	(1 CLB)(NOT 2 A OR PrsPrc);
	## Ulbmil lea ovddidit guovddáža eamiálbmotdutkamiid guovddázin.
 # This rule has to follow identification of subject.

SELECT (Sg Nom) IF (*-1 COPULAS BARRIER VERB OR CLB OR CP OR CS OR PUNCT OR go OR 
	NP-HEAD-NOM LINK 0 Sg3)(0 NP-HEAD)(NOT 0 PrfPrc OR Actor OR (Prop Loc) OR Ess)
	(NOT 0 Loc LINK *-1 Nom BARRIER CS OR CP OR CLB OR PUNCT)
	(NOT 0 A LINK 0 Ill)(NOT 0 Adv LINK 1 Pr OR VGen)(1C NP-BORDER);
	## Dasgo dus boahtá oaivámuš gii lea mu álbmoga Israela báimman.
	## Dat gal lei beare unnán. 

SELECT (Sg Nom) IF (*-1 COPULAS BARRIER NPNHA OR CC OR (Sg Nom))
	(*-1 V-SG3 BARRIER V-NOT-AUX)(NOT 0 Loc OR Ess OR Adv)
	(*1C NP-BORDER BARRIER WORD);
	## Go šattai eahket, de...

SELECT (Pl Nom) IF (*-1 COPULAS BARRIER VERB OR CLB OR CP OR CS OR PUNCT OR go OR 
	NP-HEAD-NOM OR (@SPRED) LINK 0 Pl3)(0 A OR NP-HEAD)(NOT 0 Inf)(1C NP-BORDER);
	## Vuollel 7% Finnmárkku eatnamiin leat vuovddit. 
	## Olmmošlassáneami geažil šadde eanebut geat galge juogadit buriid.

SELECT (Pl Nom) IF (*-1 COPULAS BARRIER VERB OR CLB OR CP OR CS OR PUNCT OR go OR 
	NP-HEAD-NOM LINK 0 Pl3)(0 A OR NP-HEAD)(NOT 0 Inf)(1 CLB)(2 NOT-NP);
	## ...main leat mielde dušše priváhta oasálaččat, ii leat...

SELECT (Pl Nom) IF (-1 (Indef Attr) LINK *-1 COPULAS BARRIER NOT-ADV-PCLE 
	LINK 0 Pl3)(1 CLB)(2 NOT-NP OR CC);
	## ...seammás go nuppe dáfus leat veháš várrugasat, ja dadjet ahte...

SELECT Nom IF (*-1 COPULAS BARRIER NPNHA OR PrfPrc LINK *-1 MUN 
	BARRIER NOT-ADV-PCLE)(*-1 V-SG1 BARRIER V-NOT-AUX)(NOT 0 PrfPrc)
	(NOT 0 Loc LINK *-1 Nom BARRIER CS OR CP OR CLB OR PUNCT)
	(NOT 1 N OR A)(NOT *1 PrfPrc BARRIER VFIN OR CS);
	## Vaikko mun lean friddja in ge lean geange vuollásaš,...

SELECT Nom IF (*-1 COPULAS BARRIER NPNHA OR PrfPrc LINK *-1 DON 
	BARRIER NOT-ADV-PCLE)(*-1 V-SG2 BARRIER V-NOT-AUX)(NOT 0 PrfPrc)
	(NOT 0 Loc LINK *-1 Nom BARRIER CS OR CP OR CLB OR PUNCT)(NOT 1 N)
	(NOT *1 PrfPrc BARRIER VFIN OR CS);
	## Mii diehtit ahte don leat Israela oahpaheaddji.

SELECT Nom IF (*-1 COPULAS BARRIER V-NOT-AUX OR CS OR CC)
	(NOT *-1 Nom BARRIER NPNHA)(NOT 0 VERB OR Adv OR Loc OR Ill OR Ess)
	(*1 CLB OR CS BARRIER NP-HEAD OR (A Nom));
	## ...maidda lágas eai leat sierra njuolggadusat, muhto...

SELECT Nom IF (-2 Nom)(-1 COPULAS)(0 N OR A)(NOT 0 PrfPrc OR Ess OR (Prop Loc))
	(1 EOS OR ("go"));
	## ...ahte dat leat earalágan go eará joavkkut.
 # Hits here because "earalágan" does not agree?

SELECT Nom IF (*-1 COPULAS BARRIER NOT-ADV-PCLE)(0 (N Num));
	## Stobus leigga guovttis.
	## Mii leimmet golmmas.

REMOVE Loc IF (NOT -1 ("diibmu") OR ("dbm"))(0 (N Num Nom))(NOT 1 Num LINK 1 ("eret"));
	## Mii bođiimet golmmas.
	## Viđas golbma eret lea guokte. # Hence the last condition.


# Nominative as objects in existential clauses
# --------------------------------------------
SELECT Nom IF (-1 COPULAS LINK *-1 Loc BARRIER V-NOT-AUX OR N OR CS OR CP)
	(NOT 0 Adv OR Ess)(NOT *1 (N Nom) BARRIER NPNH);
	## Ii dus leat gávdni masage.

SELECT Nom IF (*-1 COPULAS BARRIER NPNHA OR Nom LINK *-1 Loc BARRIER N OR A OR 
	V-NOT-AUX OR CLB OR CC OR CS OR CP)(NOT 0 PrfPrc)(1 NP-BORDER)
	(NOT 0 Attr OR PrsPrc LINK *1 N BARRIER NOT-ADJ-CC)
	(NOT 0 Adv LINK *1 Nom BARRIER CC OR CLB OR CS OR CP);
	## Dat bulle ja goldne go dain ii lean ruohtas.

SELECT Nom IF (*-1 COPULAS BARRIER VFIN OR Inf OR PrfPrc OR CS OR CP OR Nom)
	(*-1 V-PL3 BARRIER VFIN OR CS LINK *-1 Loc BARRIER NOT-ADV-PCLE)
	(0 NUMERALS);
	## Davviriikkain leat eahpitkeahttá guokte álbmoga maid atnit álgoálbmogin.
 
SELECT Nom IF (*-1 Loc BARRIER VERB OR CLB OR CC OR CP OR CS OR Nom 
	LINK *-1 COPULAS BARRIER VERB OR Nom OR CLB OR CC OR CP OR CS LINK
	NOT *-1 Nom BARRIER VERB OR CC OR CLB OR CS OR CP)(NOT 0 PrfPrc)
	(NOT 0 Adv LINK *1 Nom BARRIER CC OR CLB OR CS OR CP)
	(NOT 0 Attr LINK *1 N BARRIER NPNH)(NOT 0 VFIN LINK -1 CLB)
	(1 NP-BORDER); 
	## Ovdal go kristtalašvuohta bođii Sápmái lei noaiddis erenoamáš
	## rolla oaidnilin ja dálkkodeaddjin.

REMOVE Acc IF (*-1 COPULAS BARRIER PrfPrc OR Inf OR (Actio Ess) OR VFIN 
	OR CC OR CLB OR CS OR CP)(NOT 0 TIME)(1 CS OR EOS);
	## Dain áššiin lea Ruonáeatnama ruovttustivrras ráđđeaddi váldi. 

REMOVE Nom IF (NOT 0 Dem)(1C Nom)(NOT 1 PUNCT OR Num OR Interr)
	(NOT *-1 COPULAS BARRIER V-NOT-AUX OR CS OR CP)(NOT *1 COPULAS BARRIER
	V-NOT-AUX OR CS OR CP LINK NOT *1 V-NOT-AUX BARRIER CS OR CP)
	(NOT -1 Gen LINK 1 Actio LINK 1 N);
	## Divššohasjournála láhkaásahus.
	## ...maid buot journála gevaheaddji dearvvašvuođabargit áddejit.

SELECT (Pl Nom) IF (*-1 COPULAS BARRIER VERB OR CS OR CP OR go LINK 0 Pl3)(1 EOS);
	## Váikkuhusat divššohasa lagamuččaide leat mearkkašahtti stuorrát.
	 
SELECT (Pl Nom) IF  (*-1 COPULAS BARRIER NPNHAI OR PrfPrc)
	(*-1 V-DU-PL BARRIER V-NOT-AUX OR CS)(*-1 PPRON-DU-PL BARRIER CS OR CP 
	LINK NOT *-1 (Pl Nom) BARRIER NOT-ADV-PCLE);
	## Ehpet go dii leat ollu mávssolaččabut go dat?

SELECT (Pl Nom) IF  (*-1 COPULAS BARRIER NPNHAI OR PrfPrc)
	(*-1 V-DU-PL BARRIER V-NOT-AUX OR CS)(NOT 0 Inf)
	(*-1 (N Sg Nom) BARRIER CS LINK *-1 CC BARRIER NPNH LINK -1 (N Sg Nom));
	## Anne ja Biret leaba goappašagat nu jallat.

SELECT Nom IF (*-1 COPULAS OR (IV) BARRIER NOT-ADV-PCLE)(0 NUMERALS)(1 ("ruvdno"));
	## Doaibmabiju jahkásaš bušeahttarámma lea: 380.000 ruvnno.


# Nominative in coordination and apposition
# -----------------------------------------

REMOVE Nom IF (*-1 VFIN LINK NOT 0 Pl)(0 Loc LINK 1 CC LINK *1 Nom LINK NOT *1 VFIN);
    ## Plána lei ahte galggai čađahit johtaluslohkamiid diimmá jagi skuhtervuodjimis ja bievlavuodjimis.
    
SELECT Nom IF (*-1 CC BARRIER NPNHAI LINK -1 Nom LINK NOT 0 Acc 
	LINK NOT *-1 V-NOT-AUX BARRIER NPNHA)(NOT 0 VFIN)(1C NP-BORDER);  
	## Divššohas dahje su ovddasteaddji gii oaivvilda ahte...

SELECT Nom IF (-2C HNOUN-NOM)(-1 CC)(NOT 0 Cmpnd OR Actor OR Adv)
	(NOT 0 Attr OR Gen LINK 1 (N Nom))
	(NOT 0 PrfPrc LINK -1 CC LINK *-1 PrfPrc BARRIER VERB);
	## Eamiálbmogat ja riekti - muhtun prinsihpalaš vuolggasajit.

SELECT Nom IF (*-1 CC BARRIER NPNHA LINK -1 Nom LINK NOT 0 Cmpnd)
	(1 CLB OR CS OR EOS)(NOT 0 Inf LINK -1 CC LINK -1 Inf);
	## Doai ja vel okta guossi, dii galgabehtet oažžut dán lanja.

SELECT Nom IF (-2 (Pron Nom))(-1 CC);
	## Jesus Kristus ieš ja Ipmil.

SELECT Nom IF (-1 CC LINK *-1C (Num Nom) BARRIER NOT-NA)(0 Num);
	## Journála divodeapmi ja sihkkun.
	## Joavkkus leat guokte Riikaoasselávdegotti ovddasteaddji ja 2 Sámedikkis.

SELECT Nom IF (-2C Nom)(-1 CC)(1 Gen LINK *1 Po BARRIER NPNH)
	(NOT *1 Nom BARRIER Po);
	## Dearvvašvuođaásahusas mearriduvvo divvun dahje sihkkun dearvvaš-
	## vuođabargiidlága § 44 mielde.

SELECT Nom IF (*-1 CC BARRIER NPNH LINK *-1 Nom BARRIER WORD LINK NOT 0 PrfPrc)
	(1 CS OR EOS OR (PUNCT RIGHT))(NOT 0 Inf LINK -1 CC LINK -1 Inf);
	##  ...Jáhkoba ja Jovssesa eadni, ja Salome. 

SELECT Nom IF (1 CC)(*2C Nom BARRIER NPNHAI LINK 1 NP-BORDER OR CLB OR PUNCT-LEFT OR PUNCT-RIGHT);
	## 3) Oččodeapmi, diehtojuohkin ja ásahusovttasbargu.
	## Molssaevttolaš dálkkasdiehtaga našunála dutkanguovddáš ja Sámi dearvvašvuođadutkanguovddáš (goappašat fágabirrasat gullet Romssa universitehta vuollái) berrejit sáhttit oassálastit dán bargui.

SELECT Nom IF (1 COMMA LINK *1 Nom BARRIER NOT-NP LINK 1 CC LINK *1 Nom BARRIER
	NOT-NP LINK *1 V-PL3 BARRIER NOT-ADV-PCLE);
	## Sámedikki hálddahusa nannen, dearvvašvuođa- ja eallindiliiskkadeami 
	## ruhtadeapmi ja Sámi dearvvašvuođadutkanguovddáža doaimmaid viiddideapmi 
	## váldojit ovdan.

SELECT Nom IF (NOT 0 Loc)(1 COMMA)(*2 NP-HEAD-NOM BARRIER NPNH LINK 1 CC LINK
	*1 NP-HEAD-NOM BARRIER NPNH LINK 1 CC OR COMMA OR NP-BORDER);
	## ...dikšu, dearvvašvuođabisuheaddji, veajuidahtti dahje dikšun-
	## ja fuolahanulbmilat.

SELECT Nom IF (1 COMMA)(*2C NP-HEAD-NOM BARRIER NPNH LINK 1 COMMA LINK
	*1 NP-HEAD-NOM BARRIER NPNH);
	## Romssa universitehtas lea sámi dutkan, gelbbolašvuođahuksen, 
	## oahpahus ja eará gaskkustepmi leamaš...

SELECT Nom IF (1 ("/"))(*2C Nom BARRIER NPNH);
SELECT Nom IF (-2C Nom)(-1 ("/"));
	## Vuoroheapmi lea: isit/eamit,...
	## Lea ráhkaduvvon diehtojuohkinmateriála/oččodanmateriála. 

SELECT Nom IF (*-1 COMMA BARRIER VFIN LINK -1C NP-HEAD-NOM)(1 COMMA)
	(*2 NP-HEAD-NOM BARRIER NPNH);
	## ..persovdnarievtti dábálaš láhkavuogádagat, bearašriekti, árberiekti,... 

SELECT Nom IF (NOT 0 VERB)(1 COMMA)(*2 NP-HEAD-NOM BARRIER NPNH OR CC OR COMMA 
	LINK 1 EOS OR CP OR CS);
	## Jáhkobii riegádii Jovsset, Márjjá boadnja.

SELECT Nom IF (-2C Nom)(-1 COMMA)(1 CLB OR CS OR CP OR EOS)(NOT 0 Adv LINK 1 CS);
	## Olusat dadjet munnje dan beaivvi: Hearrá, Hearra!

SELECT Nom IF (-2C Nom)(-1 COMMA)(0 N)(NOT 0 Loc)(1 Inf);
	## Muhto ruovttustivra ii leat ožžon mearridanválddi dakkár áššiid 
	## hárrái go stáhtavuođđoásahus, stáhtaborgárdoaba, pássa- ja visumváldi, 
	## váldi mearridit stáhtasymbolaid,...

SELECT Nom IF (*-1 (V Pl3) BARRIER NPNH LINK NOT *-1 (Pl Nom) OR (Pl3 Nom) 
	BARRIER CS OR CP)(1 CC)(*2 Nom BARRIER NPNH);
	## ...maid liikká fertejedje leansmánni ja guokte albmá čujuhit.

SELECT Nom IF (*-1 COMMA BARRIER NPNHAI LINK -1C Nom)(1 CC);
	## ...geat leat divššohasa lagašolbmot, fuolaheaddji dahje veahke-
	## fuolaheddji.  

SELECT Nom IF (1 CC)(2C (Pron Nom))(3 (Pron Nom));
	## Gonagas Agrippa ja dii buohkat geat lehpet dás minguin!

SELECT Nom IF (1 CC)(2C N-NOM)(2 HNOUN);
	## Jos dát leažža dušše olbmuid dáhttu ja dahku,...

SELECT (Indef Nom) IF (-2 NP-HEAD-NOM)(-1 COMMA)(1 Pers);
	## Muhtun kretalaš, muhtun sin iežaset profehtain, lea dadjan... 

SELECT Nom IF (-2 Nom)(-1 (Neg Foc));
	## Eai ovdabarggut, teoria iige geavat doarjjo klausula áddejumi. 

SELECT Nom IF (-1 PUNCT-LEFT)(0 KLASS LINK NOT 0 ADVLCASE)(*1 PUNCT-RIGHT BARRIER Po OR TV OR N);
    ## Sámediggi sáhttá maid buktit cealkámušaid guovlluid dearvvašvuođafitnodagaid jahkedieđáhusain ja plánaárvalusain (kap. 3.4).
    
# Nominative in parallell constructions
# -------------------------------------

SELECT (Nom @SUBJ) IF (*-1 BOS LINK *1 (@GN>) LINK 1C (Nom @SUBJ) LINK *1 TV)(-1 (@GN>))(0 N LINK *1 TV);
            ## Dearvvašvuođabargiilága § 10, mii gieđahallá dieđuid addima pasienttaide, ja pasientavuoigatvuođalága § 1-3, mii gieđahallá vuoigatvuođa informerejuvvon miehtamii, leat ovdamearkkat dakkár vuoigatvuođain.


# Not nominative
# --------------
REMOVE Nom IF (*-1 (Pron Nom) BARRIER COPULAS OR CLB OR CC OR CS OR CP
	LINK NOT 0 Interr)(NOT *-1 (Pron Attr) BARRIER NPNH)(0 N);
	## Ávdugasat sii geat duddjojit ráfi!

REMOVE Nom IF (*-1 V-NOT-AUX BARRIER CLB OR CC OR CS OR CP OR PUNCT 
	LINK *-1 (Pron Nom) BARRIER CLB OR CC OR CS OR CP OR PUNCT)
	(NOT *-1 PrsPrc BARRIER NPNH);
	## ...vai ollašuvašii dat mii lea celkojuvvon profehta Jesaja bokte.

REMOVE Nom IF (*-1 VFIN BARRIER NOT-ADV-PCLE OR REALCLB LINK *-1C Nom 
	BARRIER NOT-ADV-PCLE)(*1 V-NOT-AUX BARRIER CLB OR CS OR CP OR go);
	## ...jus sierranas sivat eai eará cealkke.
	## Dii galgabehtet vuđolaččat bargat.

REMOVE Nom IF (*-1 Inf BARRIER VFIN OR PrfPrc OR CS OR CP OR REALCLB OR go 
	OR ("dego") LINK 0 V-TRANS-ACT LINK -1 N)(NOT *1 VFIN BARRIER CS);
	## Universitehtii lea ain hastalussan jorgalit dárogielmateriála.

REMOVE Nom IF (*-1 BOS OR CS OR REALCLB BARRIER NOT-NP)(NOT 1 Prop)
	(*1C Nom BARRIER NPNH OR CC OR CLB OR PUNCT LINK *1 VFIN BARRIER NOT-NP)
	(NOT 1 (Interr Nom))(NOT -1 @GN> LINK 1 ABBR);
	## Romssa universitehta vuosttaš čielgadeapmi sámi dutkama birra dahkkui...

REMOVE Nom IF (*-1 COPULAS BARRIER NPNHA LINK *-1 Loc BARRIER NOT-ADV-PCLE)
	(*1 Nom BARRIER NPNH OR CC OR PUNCT OR CLB);
	## Sámedikkiin lea ráđđeaddi váldi buot sámi beroštumiide guoskevaš
	## áššiin.

REMOVE (Sg Nom) IF (*-1 VFIN BARRIER CC OR CS OR CP OR CONTRA OR REALCLB OR go OR ("dego") 
	LINK NOT 0 Sg3)(NOT -1 COMMA LINK *-1 (Imprt Sg2) BARRIER CS)
	(NOT 0 NUMERALS OR (Cmpnd) OR ("veara")); 
	## Dan mađe čuovggai mánnu, ahte earuhedje albmáid reagas.
	## Odne leat Per, Kåre ja Petter boahtán.

SELECT Adv IF (NOT 0 Attr OR VFIN OR Inf OR VGen OR Indef OR N OR Loc OR Ill)
     (NOT 0 (Num Ess) LINK *1 Com OR Ess BARRIER NPNH)
	(*1 A OR N BARRIER NPNH)(NOT 1 CLB OR CC);
	## Farisealaččat ja erenoamážit sin čáláoahppavaččat nimmoredje.
	
# One early locative rule / partitive construction
# - - - - - - - - - - - - - - - - - - - - - - - - -
SELECT Loc IF (*-1 HEADOFPARTS BARRIER NPNH OR COMMA);
	## Guhtemuš dainna bivdovugiin guhkimus leaš gevahuvvon...
	## Dat sáhttet leat oassin árvvoštallamiin.
	#If this does not work: maybe: REMOVE Acc?
SELECT Loc IF (*-1 HEADOFPARTS LINK 1 Gen BARRIER NPNH OR COMMA);
    # Just in case there is a genitive which still is an accusative as well and therefore NPNH and ruled out by the rule before.

SELECT Loc IF (-3 HEADOFPARTS LINK 1 Ill LINK 1 (PrfPrc @PrcN>));
SELECT Loc IF (-3 HEADOFPARTS LINK 1 Com LINK 1 (PrfPrc @PrcN>));
SELECT Loc IF (-3 HEADOFPARTS LINK 1 Loc LINK 1 (PrfPrc @PrcN>));

# Accusative
# =============== #
  CONSTRAINTS #19 #
# =============== #

        # Miscellaneous rules related to accusative
        # Topicalised object
        # Accusative between Aux and main verb
            # After negation
            # In front of an infinitive
            # In front of a participle
        # Accusative postverbally
        # Accusative sentence-finally
        # Accusative numeral
        # Accusative after accusative numerals
        # Objects with object predicatives
        # Accusative subjects of infinite verbs
        # Accusative in temporal adverbials
        # Accusative in measure expressions
        # Accusative in apposition
        # Accusative indefinite pronouns
        # Accusative in coordination
        # Accusative in apposition
        # Not accusative

# Miscellaneous rules related to accusative
# -----------------------------------------
 # The REMOVE Acc rules in this section are rules that do not make reference
 # to an unambiguous Acc in the environment. Rules that do so come at the end 
 # of the accusative chapter.
 # Also note that at present, causatives with two accusatives go wrong.

SELECT (N Sg Acc) IF (-1 ("čáhkket"))(0 ("sadji"));
	## Njálmmálaš duođaštusat fertejit čáhkket saji čálalaš kultuvrra
	## duođaštusaide.

REMOVE Acc IF (-1 Attr)(NOT -1 VERB)(*1 (N Nom) OR (N Acc) 
	BARRIER NPNH OR COMMA OR CC LINK 1 CC LINK NOT *1 Po BARRIER NPNH);
	## ...muhto dálá máilmmi váivvit ja riggodaga fillehus hávkada sáni.

REMOVE Acc IF (*-1 COPULAS BARRIER NOT-ADV-PCLE)(*1 NP-HEAD-NOM BARRIER NPNH);
	## ...ahte dat lea maiddái divššohasa dáhttu ja ahte...

REMOVE Acc IF (*-1 Nom BARRIER NOT-ADV-PCLE LINK NOT 0 Actor LINK *-1 COPULAS BARRIER NOT-ADV-PCLE)
	(*1 CS OR EOS BARRIER V-NOT-AUX);
	## Leat go don juvddalaččaid gonagas?


REMOVE Acc IF (*-1 COPULAS BARRIER NOT-ADV-PCLE)
	(*1 Po BARRIER NPNH LINK *1 Nom BARRIER NPNH);
	## ...šattai dan oainnu mielde lunddolaš, ahte...

REMOVE Acc IF (*-1C Pass BARRIER VFIN OR Inf OR CS OR CP OR CC OR CLB LINK 0 VERB)
	(NOT -1 (Pass PrfPrc) LINK -1 CC LINK -1 Acc)
	(NOT *-1 (Pass PrfPrc) BARRIER NPNH LINK *-1 V-TRANS-ACT BARRIER NPNHA);
	## ...mii lea celkojuvvon profehta Jesaja bokte.

REMOVE Nom IF (*-1C Nom BARRIER CS OR CP OR CC OR CLB OR go OR ("dego") OR ("/"))
	(*-1 V-NOT-AUX BARRIER CS OR CP OR CC OR CLB LINK NOT 0 PrsPrc)
	(NOT 0 Num LINK -1 N OR ("§"))(NOT 1 CC LINK 1 Nom)
	(NOT *-1 VFIN BARRIER CS OR CP LINK *-1 VFIN BARRIER CS OR CP)(NOT -1 @AN>);
	## Na mun váccán joatkkaskuvlla doppe.
 # Two finite verbs, two Nom: "Nu guhkás go Gonagas mearrida gusto láhka..." 

REMOVE Nom IF (-1 (N Nom))(*1 V-NOT-AUX BARRIER NOT-ADV-PCLE);
	## Lagamus oapmahaččas lea vuoigatvuohta journála geahččat.

REMOVE Gen IF (NOT *-1 Pr OR Num BARRIER NPNH)(1 CC)(2 NP-BORDER);
	## Čuovvovaččat ovdanbuktit váldofáddájoavkkuid ja plánejuvvon 
	## doaibmabijuid.

REMOVE (N Gen) IF (NOT *-1 Pr OR Num BARRIER NPNH)(1 (Pron Pers Gen));
	## Gonagas sáhttá addit láhkaásahusaid dán birra.
	
REMOVE Gen IF (-1 (@GN>))(0 ("§"))(1 Num);
    ## Dearvvašvuođabargiidlága § 10, mii gieđahallá dieđuid addima pasienttaide, ja pasientavuoigatvuođalága § 3-1,...

#REMOVE (N Gen) IF (1C Gen LINK *1 Po OR N BARRIER V); 
REMOVE (N Gen) IF (-1 V-TRANS-ACT OR TIME)(NOT -1 PrfPrc LINK -1 (@OBJ))(1 Gen LINK *1 Po OR N BARRIER V OR CC OR COMMA); 
	##  Ráđđehus láhčá dilálašvuođaid sámi kultuvrra máŋggadáfotvuođa seailluheapmái.
	## Bargiidbellodaga sámediggejoavku divvu kritihkalaš gažaldagaid ráđi ruhtavuoruhemiid hárrái.
	## Sii leat sivaid ožžon eatni alkoholageavaheami geažil.
 # The barrier is too weak. The logic is that when we know sámi is Gen, then the
 # constituent before it cannot modify it, hence must be Acc or Nom.
 # The left context also is bad, since we can have a GGPo structure. VXXXPo, where X = A, G
 # can have different solutions.
 # XXX This is the core rule (set) for VX*P strings
 # Return to this point.


# Topicalised object
# ------------------

SELECT Acc IF (-1 BOS OR CS)(0 ("mii"))(1 (N Pl Gen))(2 NP-BORDER)(*1 V-TRANS-ACT); 
	## Maid ipmašiid doppe dagat?

REMOVE Nom IF (NOT *-1 (V Pl3) BARRIER CS)(NOT -1 ("§") OR ("b") OR ("nr"))(0 NUMERALS)
	(NOT 1 TIME)(NOT 1 CC)(NOT *1 (V Du3) OR (V Pl3) BARRIER CS)(*1 V-TRANS-ACT);
	## Bargu ollašuhttit máŋga dáin doaibmabijuin lea juo álggahuvvon.

SELECT Acc IF (*-1 BOS OR CS OR (":" CLB) BARRIER NPNH)(NOT 0 Nom)
	(1 COMMA LINK *1 COMMA LINK *1 V-TRANS-ACT BARRIER NP-HEAD)
	(NOT 1 COMMA LINK *1 NP-HEAD-GEN BARRIER NPNH LINK 1 COMMA OR CC);
	## Dáid doaibmabijuid viidáset ovdáneami, earret eará ođasmahttit 
	## sámegieloahppomateriála, sáhttá áimmahuššat universitehta bušeahtta-
	## rámmaid  siskkobealde.

SELECT Acc IF (-1 BOS)(0 (Pron Dem))(*1C Acc BARRIER NPNH) ;
	## Dán máŋggadáfotvuođa háliidit mii bisuhit.

SELECT Acc IF (*-1 BOS BARRIER NPNHA OR Nom)(*1 V-SG3 BARRIER NOT-NOM)
	(*2 TV BARRIER Acc LINK *1 EOS OR CS OR CLB BARRIER Acc) ;
	## Dan ášši ferte čilget nállevealaheami ektui sierra.
	# This rule is relevant for nouns where Nom=Acc=Gen.
 
# Accusative between Aux and main verb
# ------------------------------------

# After negation
# - - - - - - - -
SELECT Acc IF (*-1 (V Neg) BARRIER CS OR CP)(NOT *-1 ConNeg BARRIER Neg 
	LINK NOT 0 ("leat"))(NOT 0 Nom)(*1 (TV ConNeg) OR (TV PrfPrc) 
	BARRIER NOT-ADV-PCLE LINK NOT *1 Acc);
	## Na muhto don it leat dasa nama čállán!


# In front of an infinitive
# - - - - - - - - - - - - - 
SELECT Acc IF (*-1 VFIN BARRIER NPNHAI)(NOT 0 VERB)(NOT 0 Nom OR Loc OR Adv OR TIME)
	(*1 Inf BARRIER NOT-ADV-PCLE OR CLB LINK 0 V-TRANS-ACT);
	## Dasgo Herodes lei bidjan olbmuid váldit Johanasa gitta.

SELECT Acc IF (*-1 VFIN BARRIER CS OR CP OR PUNCT LINK *-1 Nom BARRIER CS OR CP)
	(NOT 0 PrfPrc OR Nom OR Adv OR TIME)(1 Inf);
	## Almmolaš buohcceviessu dahje guovlopsykiatralaš guovddáš galgá
	## divššu čađahit. #This rule doesn`t take this example anymore.
	## Divššohasas lea vuoigatvuohta oažžut dearvvašvuođadilis árvvoštallojuvvot.

SELECT Acc IF (*-1 VFIN BARRIER NPNH)(1 CC)(*2 NP-HEAD BARRIER NPNH LINK 1 Inf);
	## ...ahte lea ferten niesteseahka dahje lávkka guoddit.


# In front of a participle
# - - - - - - - - - - - - -
SELECT Acc IF (*-1 LEAT-FIN-NOT-IMP BARRIER NPNHAI)(NOT 0 Adv OR Nom OR TIME)
	(*1 PrfPrc LINK 0 V-TRANS-ACT BARRIER NOT-ADV LINK NOT *1 ("leat" PrfPrc) 
	BARRIER CS OR CP);
	## Leago barggu ohcan?

SELECT Acc IF (*-1 ("leat") BARRIER NOT-ADV-PCLE)(1 Ess)(2 PrfPrc);
	## Mun lean dan veahkkin ráhkadan.

SELECT Acc IF (1 ("jahki"))(2 ("deavdit"));
	## ...maŋŋá go lea 16 jagi deavdán.




# Accusative postverbally
# -----------------------
	
SELECT Acc IF (*-1 V-TRANS-ACT BARRIER VERB OR CC OR CLB OR CS OR (@OBJ) 
	OR PUNCT OR ("dego") LINK NOT 0 AUX OR (@ADVL) OR PrsPrc)(0 ("duot"))(NOT 0 TIME)
	(NOT 0 (Acc PxSg3) LINK 0 Loc)
	(NOT 0 Nom LINK NOT *-1 Nom BARRIER CC OR CLB OR CS OR CP);
	## Mun válddán duon girjji.
    ## Ovttasbargu Ruošša dutkanásahusaiguin gáibida almmatge sihke ollu áiggi 
	## ja resurssaid.
	

SELECT Acc IF (*-1C V-TRANS-ACT BARRIER NPNHA OR CC OR CLB OR ("nugo") OR ("dego")
	LINK 0 (@+FMAINV) OR (@-FMAINV) LINK NOT 0 Gen)(NOT 0 (Sg Nom) LINK *-1 (V Sg3) BARRIER VFIN OR CS OR CP)
	(NOT -1 PrfPrc LINK NOT *-1 ("leat") BARRIER CS OR CP)
	(NOT 0 PrfPrc OR Adv OR VFIN OR Loc OR TIME)(1C NP-BORDER);
	## Sámegillii galbet universitehta lea šaddan realitehta.

SELECT Acc IF (*-1C V-TRANS-ACT-NOT-ACTIO BARRIER NPNHAIIP OR COMMA LINK 0 (@+FMAINV) OR (@-FMAINV))
	(NOT -1 PrfPrc OR PrsPrc LINK -1 BOS OR CS OR (":" CLB))(0 NP-HEAD)
 	(NOT 0 PrfPrc or Adv or Dem or Nom OR Loc OR TIME) 
	(NOT 0 V-PL3 LINK -1 CC LINK *-1 V-PL3 BARRIER NPNHAIIP OR COMMA)	
	(*1C NP-BORDER OR (Num Nom) OR PUNCT BARRIER WORD)(NOT 1 Po) ;
	## Divššohasbearráigeahčči sáhttá váldit meannudeapmái áššiid, mat... 

 # The (Num Nom) is not optimal, we need to consider the Num Ord and Num Card
 # tagging. Now, 22. is given Num Ord and 22.10. is given Num Nom, this gives
 # the correct result, but is not satisfying, linguistically.
 # we just have A Ord now, instead of Num Ord

SELECT Acc IF (*-1C Po BARRIER NPNHAIIP LINK -1 N OR Pron LINK *-1C V-TRANS-ACT 
	BARRIER NPNHAIIP)(0 NP-HEAD)(NOT 0 Interr OR Refl OR VFIN OR PrfPrc or Adv 
	OR TIME)(1 NP-BORDER OR ADVLCASE OR Adv)(NOT 1 Po);
	## Boazosápmelaččat eai sáhte atnit dološ áiggi rájes geavaheami
	## riektevuođđun bohccuid guođoheapmái Aursunddas.        

 # Extended copy of the previous rule.
 # This rule is to tolerate a PP between the verb and the object.

SELECT Acc IF (*-1 V-TRANS-ACT BARRIER NOT-ADV-PCLE LINK NOT *-1 (@OBJ) BARRIER CS)
	(NOT -1 Gen)(0C N)(1 (Prop Gen));
	## ...go čielggadii oamastanvuoigatvuođa Finnmárkku eatnamiid hárrái.

REMOVE Nom IF (*-1 V-TRANS-ACT BARRIER NPNHA OR PUNCT OR CLB OR CS OR CP 
	LINK 0 VFIN LINK *-1 NP-HEAD-SG-NOM OR NP-HEAD-PL-NOM BARRIER NOT-ADV 
	OR CLB)(NOT *-1 CC BARRIER NPNH LINK -1 Nom); 
	## Seammá gusto jus mánná molsu skuvlla.

SELECT Acc IF (*-1C V-TRANS-ACT BARRIER NPNHAIIP OR COMMA)(0 NP-HEAD)
	(NOT 0 PrfPrc OR Adv OR Dem OR Inf )(1 CC)(*2 VFIN BARRIER NOT-ADV-PCLE);  
	## Bearašsuodjaluskantuvrrat fállet divššu ja rávvejit bearrašiid.
 # Could this rule be modified so that it accepts a subject between 0 and verb? 

REMOVE Gen IF (*-1 V-TRANS-ACT BARRIER NOT-ADV-PCLE)(1 Pers);
	## Sii bilkidit ja doarrádallet din mu dihtii.

REMOVE Gen IF (*-1 V-TRANS-ACT BARRIER NOT-ADV-PCLE)(1 Dem);
	## De gohčui doalvut iežas don beallái jávrri.

SELECT Acc IF (NOT *-1 Acc BARRIER CS OR CP)(NOT *-1 CC BARRIER NPNH LINK -1 Acc)
	(0 NP-HEAD)(NOT 0 VFIN)(1 (A Ess))(NOT *1 Actio BARRIER NOT-ADJ-CC);
	## ...go beaktilis almmolaš beroštumit dagahit dan govttolažžan.

SELECT Acc IF (*-1 ACC-LOC-VERB BARRIER NOT-ADV-PCLE LINK NOT 0 Pass)
	(*1 Loc BARRIER NOT-NP);
	## Sáhttá viežžat dieđuid divššohasa oapmahaččain.

SELECT Acc IF (*-1 ACC-ILL-VERB BARRIER NOT-ADV-PCLE LINK NOT 0 Pass)
	(*1 Ill BARRIER NPNH);
	## Ii oktage bija duohpakeahtes duogŋasa boares biktasii.

REMOVE Gen IF (*-1 V-TRANS-ACT BARRIER NPNHA)(0 N)(1 (Pron Indef))
	(NOT *1 Ill BARRIER NPNH LINK 1 Acc);
	## ...čájehit vuollegašvuođa juohke ovtta divššohasa eallimii.

REMOVE Gen IF (*-1 V-TRANS-ACT BARRIER NPNHA OR Acc LINK NOT 0 ILL-VERB 
	OR ACC-ILL-VERB OR VGen LINK NOT *-1 Acc BARRIER CS OR CP OR CC OR REALCLB)
	(NOT 1 Acc)(*1 Ill BARRIER NPNH LINK NOT 0 ("láhki"));
	## Dasto son vuolggahii sin Betlehemii ja dajai:...
  
REMOVE Gen IF (*-1 Nom BARRIER NOT-ADV-PCLE LINK *-1 V-TRANS-ACT BARRIER 
	NOT-ADV-PCLE LINK NOT 0 ILL-VERB LINK NOT 0 PrsPrc)(*1 Ill BARRIER NPNH);
	## Bijatgo don sohkkara dedjii?

REMOVE Gen IF (*-1 V-TRANS-ACT BARRIER NPNHA OR (@OBJ) OR CLB LINK NOT 0 LOC-VERB OR N)
	(NOT *-1 V-TRANS-ACT BARRIER NPNHA LINK *-1 (@OBJ) BARRIER NOT-ADV-PCLE)
	(NOT 0 Dem)(NOT *1 Acc OR Nom BARRIER NOT-NP OR CC)(*1 Loc BARRIER NPNH);
	## Sii manne vissui ja oidne máná etniinis. 
	## Skuvla galggai oahpahit dárogiela vissis guovlluin.  

SELECT Acc IF (*-1 Loc BARRIER NPNH LINK -1 LOC-VERB)(1 NP-BORDER OR CC OR CLB);
	## Divššohasas lea vuoigatvuohta biehttalit vuostáváldimis vara dahje
	## varrabuktagiid.

SELECT Acc IF (*-1 ("dihte") OR ("dihtii") BARRIER NOT-NP OR PUNCT LINK -1 Actio)
	(NOT 0 Inf)(1 NP-BORDER OR PUNCT);
	## ...ja ovdanbuktin dihte ođđa čoavddusevttohusaid.

SELECT Acc IF (*-1 V-TRANS-ACT BARRIER NPNHA)(1 (VAbess));
	## In mun dáhto vuolggahit sin borakeahttá ruoktut.

SELECT Acc IF (*-1 V-TRANS-ACT BARRIER NPNHA)(NOT 0 Adv)(1 Sup)(NOT 1 Neg);
	## Máhtte guđii bohccuid livadeažžat.

REMOVE Gen IF (*-1 V-TRANS-ACT BARRIER NPNHA)(NOT 0 TIME)(*1 CC BARRIER WORD 
	LINK *1 Nom BARRIER NPNH LINK *1 VFIN BARRIER NOT-ADV-PCLE);
	## Mun lohken girjji ja Niillas guldalii ođđasiid.

 

# Accusative sentence-finally
# ---------------------------

# Removed this one because it made trouble. The example function although.  
#REMOVE Gen IF (NOT *-1 Pr OR Num OR CC BARRIER NPNH)(0 N OR Pron)(NOT 0 TIME)
 #	(*1 CS OR CP OR EOS BARRIER NOT-ADV-PCLE) ;
	## Ferte čájehit várrugasvuođa go lea sáhka sihkkumis dakkár dieđuid.

#REMOVE Gen IF (NOT *-1 Pr OR Num OR CC BARRIER NPNH)(0 N OR Pron)(NOT 0 TIME)
# 	(*1 COMMA BARRIER NOT-ADV-PCLE LINK 1 CC) ;

 # CC in the preceding rule because of coordination after Pr. The following rule 
 # compensates for it.

SELECT Acc IF (NOT *-1 Pr OR Num BARRIER NPNH)(0 NOTIME OR Pron)
	(NOT 0 Nom OR Loc OR Adv OR VERB)
	(*1 CLB BARRIER NOT-ADV-PCLE LINK NOT *1 Gen BARRIER NPNH);
	## Jus divššohas roasmmohuvvá dahje oažžu duođalaš váttisvuođaid,...
	## Liggen dihtii viesuideaset, ferte ollusat muoraid boaldit.

REMOVE Gen IF (NOT *-1 Pr OR Num BARRIER NPNH)(0 NOTIME OR Pron)
 	(*1 PUNCT BARRIER NOT-ADV-PCLE LINK 1 NP-BORDER) ;
	## Dohkkehit persovnnalaš sávaldagaid geavahit álbmotdálkkodeami
	## ja guvhlláriid.

REMOVE Gen IF (NOT *-1 Pr OR CC OR (Num Nom) BARRIER NPNH)(-1 Gen)(0 NOTIME OR Pron)
	(NOT 0 ALU)
	(*1 CLB BARRIER NOT-ADV-PCLE)(NOT 1 COMMA LINK 1 Gen LINK 1 CC LINK 1 Gen);
	## ...ahte vuhtiiváldit divššohasa dárbbuid, beroštumiid ja riekte-
	## sihkarvuođa dearvvašvuođabálvalusa ektui.


# Accusative numeral
# ------------------

SELECT Acc IF (*-1 V-TRANS-ACT BARRIER NOT-ADV-PCLE LINK NOT *-1 Acc BARRIER CS OR CP)
	(0 Num)(1 (N Acc));
	## Galgá geavahit ovtta divššohasjournála juohke divššohasa nammii.
	## Juohke kursa lea árvvoštallojuvvon máksit 50.000 ruvnno.

SELECT Acc IF (0 Num)(*1C (N Acc) BARRIER NPNH OR Gen LINK NOT 0 (@QN<));
	## Láve dat gal olmmoš ovtta suorrerissi maiddái atnit.


# Accusative after accusative numerals
# ------------------------------------
SELECT Acc IF (*-1 (Num Acc) BARRIER NPNH OR N LINK NOT 0 Gen);
	## Go mánná lea deavdán 12 jagi, son galgá beassat buktit oaivilis.

SELECT (N Acc) IF (*-1C (Num Acc) BARRIER NPNH);
	##  Mihttomearrin lea buvttadit 2 sámegiel almmuheami jahkái.


# Objects with object predicatives
# --------------------------------
 # Note that a rule for accusative predicates following accusative subjects and 
 # copulas is found right before the nominative rule block.

SELECT Acc IF (*-1 OPRED-VERB BARRIER NPNHA LINK NOT 0 Pass OR Actio LINK 
	NOT *-1 NP-HEAD-ACC BARRIER V-NOT-AUX OR CS OR CP)(NOT 0 TIME)
	(NOT *1 Acc BARRIER NPNH OR PUNCT OR CC OR ("/"));
	## Dážat eai atnán sámiid buoret go beanan.

#SELECT Acc IF (*-1 OPRED-VERB BARRIER NOT-NP OR PUNCT LINK NOT 0 Pass OR Actio)
#	(NOT *-1C Acc BARRIER CC OR CS)(NOT *-1 Num BARRIER NPNH LINK -1 Acc)
#	(1 Ess);
	## Lea deaŧalaš movttiidahttit vuolit gráda studeanttaid válljet 
	## sámi guoskevaš čuolbmačilgehusaid ja eamiálbmotáššiid váldofágafáddán.

SELECT Acc IF (*-1 OPRED-VERB BARRIER NOT-NP OR PUNCT LINK NOT 0 Pass OR Actio)
	(NOT *-1C Acc BARRIER CC OR CS)(1 PUNCT OR CLB OR EOS);
	## ...ja muđui atná son doavggusin oađđadagaid (duolji, ránu) ja 
	## biktasiid.

SELECT Acc IF (*-1 CC BARRIER NPNH LINK -1 Ess LINK -1 Acc)(NOT 0 PrsPrc)
	(1 A OR Ess);
	## ...dahkan doaimmaid vuoiggaleabbun ja sosiálabálvalusaid álkibut.

SELECT Acc IF (*-1 V-TRANS-ACT BARRIER NOT-ADV-PCLE OR CLB LINK NOT *-1 NP-HEAD-ACC
	BARRIER V-NOT-AUX OR CP OR CS)(1 Ess)(NOT 2 Inf);
	## Mun oidnen Máreha mánnán.
	## Mun oidnen Máreha boahtime.
 # The rule also hits objects in front of Actio Ess, which seems OK.

REMOVE Acc IF (*-1 Acc BARRIER NOT-NP OR Inf OR CLB LINK *-1 OPRED-VERB 
	BARRIER NPNH)(NOT -1 ("/") OR CC LINK -1 Acc);
	## ...go doavttir lea dahkan biehttaleami apotehka vuoigatvuođa vuostá.

REMOVE Acc IF (*-1 Acc BARRIER NOT-NP LINK *-1 OPRED-VERB BARRIER NOT-NP)
	(NOT *-1 COMMA OR ("/") OR (PUNCT LEFT) OR CC BARRIER NPNH LINK -1 Acc)
	(NOT 0 Interr);
	## Dát sáhttá dáhpáhuvvat go atná konkrehtalaš daguid/hilgumiid ovtta
	## dahje nuppi riekteáddejumi boađusin.
 

# Accusative subjects of infinite verbs
# -------------------------------------

SELECT Acc IF (*-1 V-TRANS-ACT BARRIER NOT-NP)(NOT 0 Gen LINK *1 (N Acc) OR (N Gen) 
	BARRIER NPNH OR CC)(NOT 0 ("leat"))
	(*1 (Actio Ess) BARRIER NOT-NP LINK NOT -1 CC);
	## De oinnii olbmá čohkkámin tuolloviesus.

SELECT Acc IF (*-1 V-TRANS-ACT BARRIER NOT-NP)(NOT 0 ("leat" PrfPrc))(NOT 0 Gen LINK *1 (N Acc) 
	BARRIER NPNH OR CC)(*1 PrfPrc BARRIER NOT-ADV-PCLE);
	## Niillas logai Máhte geahččan Máreha ráfálažžan.
	## Stuorradiggi sihkui dasa lassin vejolašvuođa mii dassážii leamaš lean geavahit suomagiela veahkkegiellan.
    # It should not take the latter one.

# Accusative in temporal adverbials
# ---------------------------------

REMOVE Nom IF (*-1 (@SUBJ) BARRIER CS OR CP)(NOT *-1 ("leat") BARRIER VERB)
	(0 NUMERALS)(1 TIME);
	## Fierbmi bistá guokte- golbma jagi.

SELECT (Acc @ADVL) IF (NOT *-1 ("leat") BARRIER VERB)(0 Num)(1 TIME)(NOT 2 N OR Po);
	## Go fylkkadoavttir lea seailluhan journála 10 jagi,...

#SELECT (Acc @ADVL) IF (-1C NP-BORDER OR A)(NOT -1 Interr OR Indef OR Pr)(NOT 0 Loc)
#	(0 TIME)(NOT *1 TIME BARRIER NPNH)(NOT 1 ("loahppa") OR ("álgu"))
#	(NOT -1 V-TRANS-ACT LINK NOT *-2C (@OBJ) BARRIER CS OR CP LINK NOT *1C (@OBJ) BARRIER V-TRANS-ACT OR CP OR CS)
#	(NOT 0 ("b") OR ("beaivi") LINK -1 Ord);
	## Máhtte duddjostalai beaivvi.
	## Helse Nord sáhttá dađi mielde ovdánit bargoneavvun guhkes áiggi áŋgiruššamis sápmelaččaid várás.

SELECT (Acc @ADVL) IF (-1C NP-BORDER OR A)(NOT -1 Interr OR Indef OR Pr)(NOT 0 Loc)
	(0 TIME)(NOT *1 TIME BARRIER NPNH)(NOT 1 ("loahppa") OR ("álgu"))
	(NOT -1 ST-VERB LINK NOT *-2C (@OBJ) BARRIER CS OR CP LINK NOT *1C (@OBJ) BARRIER V-TRANS-ACT OR CP OR CS)
	(NOT 0 ("b") OR ("beaivi") LINK -1 Ord);
	
SELECT (Acc @ADVL) IF (0 Num)(*1 TIME BARRIER NOT-A);
    ## Dat orui doppe vihtta guhkes jagi.

SELECT Acc IF (0 ("bodda"));
	## Mun finan gávppašeame dan botta.


# Accusative in measure expressions
# ---------------------------------

SELECT Acc IF (0 Num)(1 TIME OR MEASURE OR ("ruvnno"))(2 A);
	## Mánná lea golbma jagi boaris.


# Accusative in apposition
# ------------------------
SELECT (N Acc) IF (-1 (Pers Gen))(1C (Prop Acc));
	## Jesus válddii fárrosis Biehtára, Jáhkoba, ja su vielja Johanasa. 

SELECT Acc IF (*-1 V-TRANS-ACT BARRIER VERB OR CS OR CP OR CC)(1 COMMA)
	(*1C NP-HEAD-ACC BARRIER NPNH OR CC OR ("gč") LINK NOT 0 Interr);
	## Oidnet go don Mihkkala, min nuoramus bártni?

SELECT Acc IF (*-1 (PUNCT LEFT) BARRIER NOT-NP OR ABBR LINK -1C Acc)
	(1 (PUNCT RIGHT));
SELECT Acc IF (-2C Acc)(-1 (PUNCT LEFT))(NOT 1 Acc)
	(*1 (PUNCT RIGHT) BARRIER NOT-NP);
	## ...ja muđui atná son doavggusin oađđadagaid (duolji, ránu) ja 
	## biktasiid.


# Accusative indefinite pronouns
# ------------------------------
SELECT Acc IF (0 (Pron Indef))(*1C (N Acc) BARRIER NPNH OR N);
	## Galgetgo sii guldalit iešguđet geavaheaddjijoavkkuid. 


# Accusative in coordination
# --------------------------

# First part
# - - - - - -
SELECT Acc IF (0 N)(1 CC)(*2C NP-HEAD-ACC BARRIER NPNHA OR Gen LINK 1 NP-BORDER);
	## Dalle skáhppo biergasiid nugo firpmiid ja buođđomuoraid.
	## Mun oainnán Mihkkala oappá ja Máreha.
	## Mun oainnán Mihkkala ja Máreha viesu.
  # The philosopy is that coordination is balanced.

SELECT Acc IF (*-1 V-TRANS-ACT BARRIER NOT-ADV-PCLE)(NOT 0 HUMAN-INDIVIDUAL)(1 CC)(2 HUMAN-INDIVIDUAL LINK 0 Gen LINK 1 Gen OR Acc);	
	## Mun vuovddán gusa ja áhči beatnaga.

SELECT Acc IF (-1 Gen)(0 N)(1 CC)(*2C NP-HEAD-ACC BARRIER NPNHA LINK 1 NP-BORDER);
	## Noaidi ipmirda olbmo jierpmi ja olbmo sielu.
	## Mun oainnán Mihkkala goađi ja Máreha viesu.
  # The philosopy is that if both NPs have a Gen, then we have coordinated
  # objects

SELECT Acc IF (1 CC)(2C Acc)(3C NP-BORDER);
	## Bovde váivvážiid ja rámbbiid, lápmásiid ja čalmmehemiid.

SELECT Acc IF (*-1 V-TRANS-ACT BARRIER NPNHA OR CONTRA)(1 CC OR CLB)
	(*2C Acc BARRIER NOT-ADJ LINK 1 CC OR CLB OR NP-BORDER);
	## Dát guoská bargguide ásahit mastera ja bachelora,..

SELECT Acc IF (*-1 V-TRANS-ACT BARRIER NPNHA)(1 CC)(2 Acc)(3 CLB);
	## ...váldi mearridit mihtuid ja deattuid,...

SELECT Acc IF (*-1C V-TRANS-ACT BARRIER NPNHAII)(1 CLB)(2 Acc)(3 CC OR CLB)
	(NOT 4 Gen LINK *1 N BARRIER NPNH OR CC)
	(NOT 4 Indef LINK 1 Gen LINK *1 N BARRIER NPNH OR CC);
	## Jesus válddii fárrosis Biehtára, Jáhkoba ja Johanasa.

SELECT Acc IF (*-1C V-TRANS-ACT BARRIER NPNHAII)(0 N)(1 CC)
	(*2C NP-HEAD-ACC BARRIER NPNH OR Gen);
	## Mii guoská váldofáddájovkui mii gieđahallá dutkama ja oahpahusa, de...

SELECT Acc IF (1 ("/") LINK *1 NP-HEAD-ACC BARRIER NPNH LINK 1 NP-BORDER);
	## Lea dárbu ásahit sámegiela vierisgiellaoahpahusa/álgooahpahusa.
 
SELECT Acc IF (*-1 V-TRANS-ACT BARRIER NPNHAII)(0 N)(1 COMMA)
	(*2 NP-HEAD-ACC BARRIER NPNHA LINK 1 CC LINK 1 NP-HEAD-ACC BARRIER NPNHA 
	LINK 1 NP-BORDER);
         ## Jos juohká dážaid, sámiid ja láddelaččaid iešguđet sadjái, ...

SELECT Acc IF (1 COMMA)(*2 Acc BARRIER NPNH LINK 1 CC LINK 1 Acc LINK 1 NP-BORDER)
(NOT *-1 ("nugo" Adv) BARRIER NPNH LINK *-1 Loc BARRIER NOTCOMMA)(0 (Pl Loc));
	## ...váfistan dihte Romssa universitehta sámi dutkama, oahpahusa ja
	## gaskkusteami.
	
SELECT Acc IF (*-1 COMMA BARRIER NPNH LINK -1C NP-HEAD-ACC)(1 COMMA OR CC)
	(2C Gen)(3C Acc);
	## Gielalaš hehttehusat váttásmahttet sihke guorahallama, diagnostiserema, gieđahallama, 
	## divššu, fuolaheami ja dieđuid addima geavaheaddjái.	


# Second part
# - - - - - -
SELECT Acc IF (*-1 CC BARRIER NPNH LINK -1C Acc)(NOT 0 Inf)(1 NP-BORDER)
	(NOT 0 Dem LINK 1 PrfPrc LINK *1 N BARRIER NPNH);
	## ...ja atnigohte meahci ja vuovddi.
	
#SELECT Acc IF (*-1 V-TRANS-ACT BARRIER )(	
 
SELECT Acc IF (-1 CC OR COMMA BARRIER NPNH LINK *-1 VFIN BARRIER CS OR CP LINK 
	*-1 (Pron Interr) BARRIER VFIN OR CC OR CS OR CP LINK -1 NP-HEAD-ACC)
	(1 NP-BORDER OR CC OR CLB);
	## Divššohasbearráigeahčči galgá addit sutnje dieđuid gii jearrá, ráđiid 
	## ja bagadallama. 

SELECT Acc IF (*-1 CC BARRIER NPNH LINK -1C Acc)(1 COMMA);
	## Álgoálbmogii galgá sihkkarastit "rivttiid ja vejolašvuođaid, maid
	## našunála lágat sihkkarastet eará ássiide".

SELECT Acc IF (*-1 CC OR COMMA BARRIER NPNH LINK -1C NP-HEAD-ACC)(1 COMMA OR CC)
	(*2 NP-HEAD-ACC BARRIER NPNH LINK 1 CLB OR CC OR NP-BORDER);
 	## ...váfistan dihte Romssa universitehta sámi ja eamiálbmot-
	## guoskevaš dutkama, oahpahusa ja gaskkusteami.

SELECT Acc IF (*-1 COMMA OR CC BARRIER NPNH LINK -1C Acc LINK *-1 V-TRANS-ACT 
	BARRIER NPNHAII)(1 CC OR COMMA);
        ## Jos juohká dážaid, sámiid ja láddelaččaid ieš guđet sadjái, ...

SELECT Acc IF (-2C Acc)(-1 ("/"));
	## Dát sáhttá dáhpáhuvvat go atná konkrehtalaš daguid/hilgumiid 
	## okta dahje nuppi riekteáddejumi boađusin.

SELECT Acc IF (-1 COMMA LINK *-1 Rel BARRIER COMMA LINK -1 COMMA LINK -1C Acc)(NOT 1C Acc)(NOT *1 VFIN BARRIER CP OR CS);
    ## Gihpa gieđahallá maid vuoigatvuođaid, mat vuođđuduvvet lágaide ja konvenšuvnnaide, ovdamearkkaid gielddaid plánain sámi mánáid ja nuoraid ovdii ovttas singuin, ja dasto ovdamearkkaid fálaldagain, mat addojit dearvvašvuođastašuvnnain ja mánáidgárddiin juo dan rájes go mánát leat uhcit, skuvlaja asttuáigefálaldagain, gitta nuoraide, geat leat oahpus, ja ovdamearkkaid nuoraiddiehtojuohkimis ja vejolašvuođain davviriikkalaš ja riikkaidgaskasaš ovttasbargui.
# Acc , Rel ... , 0=Acc ... , Rel..., CP....,...

# Third part
# - - - - - -
SELECT Acc IF (*-1 CC BARRIER NPNH LINK -1C Acc LINK -1 COMMA BARRIER NPNH 
	LINK -1C Acc)(1 NP-BORDER OR Gen);
	## Ferte vuhtiiváldit divššohasa dárbbuid, beroštumiid ja riektesihkar-
	## vuođa dearvvašvuođabálvalusa ektui. 

SELECT Acc IF (*-1 CC BARRIER NOT-ADV LINK -1 COMMA LINK *-1 Acc LINK -1 COMMA LINK *-1 Rel BARRIER COMMA LINK -1 COMMA LINK -1C Acc)(NOT *1 VFIN BARRIER CP OR CS);

#REMOVE (Gen @GN>) IF (0 (LOC-COMPL-NOUN))(*1 Loc BARRIER NPNH);       


# Accusative in apposition
# ------------------------

SELECT Acc IF (*-1 COMMA BARRIER NPNHA LINK -1 Acc)(NOT 0 Gen LINK 1 N)
	(NOT 1 COMMA OR CC LINK 1 Gen)(*1 COMMA BARRIER VFIN);
	## Romssa universitehta ohcá dan dihte lassiruhtajuolludeami, oktiibuot
	## 3.000.000 ruvnno jahkásaččat 5 jagi badjel, váfistan dihte Romssa
	## universitehta sámi ja eamiálbmotguoskevaš dutkama, oahpahusa ja
	## gaskkusteami.

SELECT Acc IF (0 ("hearrá"))(1C ("ipmil" Acc));
	## It galgga geahččalit Herrá Ipmilat.

 # Special rule to avoid genitive reading of "Hearrá". Needed because "Hearrá Ipmilat"
 # is different from "Title Name" and "Firstname Lastname" combinations.
 
SELECT Acc IF (-1 ("hearrá" Acc))(0 ("ipmil"));
	## Don galggat ráhkistit Hearrá Ipmilat oppa váimmustat..


# Not accusative
# --------------

REMOVE Acc IF (-1 ("juohke"))(0 ("okta"))(*1 N BARRIER NOT-ADJ LINK NOT 0 Acc);
	## ...čájehit vuollegašvuođa juohke ovtta divššohasa eallimii.

REMOVE (N Acc) IF (*-1C (N Acc) BARRIER Inf OR Actio OR CS OR CP OR CC OR CLB 
	OR ("/") LINK NOT *-1 Interr BARRIER CS)(NOT 0 Prop LINK -1 (N Acc));
	## Mánáin lea vuoigatvuohta oažžut dárbbášlaš dearvvašvuođaveahki
	## maiddái dearvvašvuođaiskkadeami hámis...

#REMOVE Acc IF (NOT 0 Dem OR Num)(*1C (N Acc) BARRIER NPNH OR PrfPrc OR COMMA OR CC 
#	OR ("/") LINK NOT 0 TIME OR ROUTE)(NOT 0 (N Acc) LINK 1 (Prop Acc));
	## Divššohasas lea vuoigatvuohta iežas journála geahččat.
	# Removed this rule because it made troble when there is a Acc + Gen + Ess, and it isn´t 
	# anymore necessary for the example.
	
REMOVE Acc IF (NOT 0 Dem OR Pers OR Interr)(*1C (Pron Acc) BARRIER CC OR CP 
	OR CS OR CLB OR Neg OR Actio OR go LINK NOT 0 Interr OR Refl)(NOT 1 Indef);
	## Golbma olbmá jearahit du.

#REMOVE Acc IF (NOT *-2 Acc BARRIER NOT-NP)(-1 CC)(NOT 0 Interr)
#	(*1 N BARRIER NPNH OR CC OR COMMA);
	## Buot borramuš ja siidda gálvvut, ja... 

REMOVE Acc IF (NOT *-1 V-TRANS-ACT BARRIER VFIN-NOT-AUX OR Inf OR CS)(NOT *-1 CS BARRIER V LINK 1 @CS-COMPL)(NOT 0 TIME)
	(NOT 0 Dem LINK *1 (N Acc) BARRIER NPNH)
	(NOT 0 NUMERALS LINK *1 TIME BARRIER NOT-A)  
	(NOT *1 V-TRANS-ACT BARRIER CS OR CP);
        ## Sápmelaččaid historjá álbmogin lea duháhiid jagiid boaris, muhto
        ## dálá ássanguovlluin sápmelaččat eai leat ássan nu guhká.

REMOVE Acc IF (NOT *-1 V-TRANS-ACT BARRIER V-NOT-AUX OR CS OR CP)(NOT 1 Ger)
	(*1 V-TRANS-ACT BARRIER CS OR CP LINK *1C Acc BARRIER CC OR CS OR CP);
	## Bivdi sáhttá vuordit guoli boahtit buđđui.

REMOVE Acc IF (*-1C Acc BARRIER CS OR CP OR REALCLB)(NOT *-1 CC BARRIER NPNH 
	LINK -1 Acc)(*1C A BARRIER NPNH LINK 1 NP-BORDER)(NOT 1 CC);
	## ...maid sáhttá geavahit eamiálbmotjoavkkuid miellahtuid buorrin.

REMOVE Acc IF (NOT *-1 V-TRANS-ACT BARRIER CS)(NOT 0 TIME)
	(*1 V-PASS BARRIER V-NOT-AUX OR CS OR CP);
	## ...ahte sámi nuoraide láhččojuvvo vejolašvuohta oassálastit... 

REMOVE Acc IF (*-1C V-PASS BARRIER VFIN OR Inf)(NOT 0 TIME)
	(NOT *1 V-TRANS-ACT BARRIER CS OR CP);
	## Dál nannejuvvui girku sámeguovlluin.

REMOVE Acc IF (NOT 0 TIME)(*1 V-TRANS-ACT BARRIER CLB OR CS OR CP OR CC 
	LINK *1 ("ahte") BARRIER NOT-ADV-PCLE OR ("nu"));
	## Mu referáhta namma dadja, ahte...
 # Removes accusative object if there is a clausal object.

REMOVE Acc IF (*-1 V-TRANS-ACT BARRIER NOT-ADV-PCLE)(0 Loc)(*1 ("ahte") BARRIER WORD);
	## Olmmoš galgá almmuhit skovis, ahte objektiivvalaš eaktu lea dievvan.
 # Same here.

REMOVE Acc IF (0 (N Sg Nom))(1 V-SG3)(NOT *1C Nom BARRIER CS)(NOT *1 TV LINK *1 EOS OR CS OR CLB BARRIER Acc);
	## ...mii lea mearriduvvon lágas dahje maid láhka doarju.

REMOVE Acc IF (NOT 0 TIME OR Dem)(*1C N BARRIER NPNH LINK 1C Inf);
	## Oainnán iežan bargun guossohastit veahá biepmu buohkaide. 

REMOVE Acc IF (0C HNOUN OR (Pron Pers))(NOT 1 CC OR PUNCT OR CLB)
	(*1C (N Acc) BARRIER NPNH LINK NOT 0 TIME LINK 1 NP-BORDER OR ADVLCASE);
	## Lea dárbu nannet hálddahuslaš bargiid vejolašvuođa oahppat sámegiela.
	## Dál lea stuorát dáhttu go ovdal atnit sápmelaččaid dološ 
	## geavahanávkkástallama dakkarin. 

REMOVE Acc IF (0C HNOUN OR (Pron Pers))(NOT 1 CLB)
	(*1 Acc BARRIER NPNH OR CC LINK 0C N LINK NOT 0 Loc OR TIME LINK 1 CLB);
	## Válljedettiin journálaovddasvástideaddji berre árvvoštallat su 
	## fágagealbbu.
	## ...de rahpá čoahkkima ovddit áigodaga Sámedikki čoahkkinjođiheaddji.

SELECT Gen IF (*-1 ("dihte") OR ("dihtii") BARRIER NPNH LINK -1 Actio)
	(NOT 0 Dem)(NOT 0 Inf LINK -1 CC)(NOT 1 CC)(*1 (N Acc) BARRIER NPNH);
	## ...váfistan dihte Romssa universitehta sámi dutkama.
	
REMOVE Acc IF (-1 ("mannat" @PrcN>) OR ("boahtte" Attr) OR (Dem @DN>))(0 Gen LINK 0 TIME);
    ## Mannan dálvvi hárjánin čuoigat.
    ## Dán jagi ii lean áivevátnu juo árradit.
    	
# Accusative Px or locative
# =========================

SELECT Acc IF (*-1 ST-VERB BARRIER VERB)(NOT *-1 (@OBJ) BARRIER VFIN)(0 Loc LINK NOT 0 Actio OR BUILDING OR Prop)(NOT *1 (@OBJ) BARRIER V);
	## Sámediggi ovddida rollas eavttuid buktin...

SELECT Acc IF (-1 ("hupmat") OR ("hállat") OR ("hoallat") OR ("sárdnut"))(0 ("eatni#giella") LINK 0 Loc);
	## Máŋgga sápmelažžii lea dehálaš beassat hállat eatnigielas.


SELECT Acc IF (-1 ("addit"))(0 ("vuoigŋa"));
    ## Muhto Jesus čurvii fas alla jienain ja attii vuoiŋŋas.


# Illative
# ========
 # Sg Ill = Prt Sg3 (for some verbs), Pl Ill = Prt Du2 (for some verbs)
 
SELECT Ill IF (*-1 VFIN OR Inf BARRIER CC OR CLB OR CS OR CP)(0 (Prt Sg3))
	(NOT 0 Adv);
	## Dat lea sivalaš helveha dollii.  
 # The last condition because of "oktii" etc.

REMOVE (Prt Du2) IF (*-1 VFIN OR Inf BARRIER CS OR CP);
	## Fágabirrasat main lea gieddebárgu, bovdejuvvojit fállat giedde-
	## bárgguid sámi nuoraide. 

REMOVE (Du2) IF (*1 VFIN BARRIER CC OR CS OR CP OR REALCLB);
	## Jahkásaš bušeahttarámma dán prošeavtta doaimmaide gártá 70.000 ruvnno.

SELECT Ill IF (-2C Ill)(-1 COMMA OR CC);
	## ...vuoigatvuohta guohtumii, bivdui, guollebivdui, jna.

SELECT Ill IF (1 CC OR COMMA)(*2C Ill BARRIER NPNH);
	## Gohccái ja čujuheapmái nammaduvvui gonagaslaš vuovdefáldi.



# =================== #
      CONSTRAINTS #20
# =================== #

# An early comitative rule
# ========================

SELECT (Sg Com) IF (-1 ("vrd"))(0 (Pl Loc));
    ## Sámediggi lea váldán badjelasas váldoovddasvástádusa sihkkarastimis doaibmadoarjaga Nuortasámi museai (vrd. čoahkkimiin Njávdámis 05.06.01).



# Locative
# ========

# ToC for this section

    # NP-internal disambiguation of Loc
    # Disambiguation based upon type of NP
    # Disambiguation based upon verb valency
    # Disambiguation based upon idiosyncratic verb NP combinations
    # Other locative rules

# NP-internal disambiguation of Loc
# ---------------------------------
SELECT Loc IF (*-1 ("buot") BARRIER NPNH)(0 (Pl Loc));
	## Nu lea buot servodaga dásiin.
	
SELECT (Pl Loc) IF (*-1 ("dievva"))(0 (Sg Com) BARRIER NPNH);
    ## Dušše diibmabeali ovdalaš lei birasviessu dievva Sámi nuorain.

SELECT Loc IF (*-1C (Dem Pl Loc) BARRIER NPNH);
SELECT Loc IF (-2C (Dem Pl Loc))(-1 Adv);
	## Ollu dáin doaibmabijuin sáhttá álggahit...
	## ??
 # "Dain" is unambiguously Loc. "Dainna" is not.

REMOVE (Dem Pl Loc) IF (1 (Num Sg));
	## Dainna čuđiin ruvnnuin áiggun bierggu oastit.

SELECT Loc IF (*-1 HEADOFPARTS BARRIER NPNH OR COMMA);
	## Guhtemuš dainna bivdovugiin guhkimus leaš gevahuvvon...
	## Dat sáhttet leat oassin árvvoštallamiin.

SELECT (Pl Loc) IF (-2 Num)(-1 PROSEANTA); 
	## Dat lea 50 % ruđain.


SELECT (Pl Loc) IF (*-1 ("ieš#guhtet" Pron Indef Gen) OR ("eanet" A Comp Attr) BARRIER NPNH)(0 (Sg Com));
	## Dat čájeha, man sierralágan sámi identitehtta sáhttá leat iešguđet birrasiin.
	## Válgaoassálastin iešguđet válgamálliin Norggas.
	## Galle áirasa leat válljejuvvon iešguđet joavkkuin 1989 -- 2001 49 Tab.4.

SELECT (Pl Loc) IF (1 COMMA)(2 (Pron Interr Pl));
	## Livččii buorre, juos hálddašanguovllu olggobeale doaimmain, mat bálvalit sámi
	## geavaheddjiid, biddjošii deaddu.


# Disambiguation based upon type of NP
# ------------------------------------

SELECT (Pl Loc) IF (0 (laš) OR ("sápmelaš"))(NOT 0 (A*));
	## Sápmelaččain sáhttet leat erenoamáš vuoigatvuođat.

SELECT Loc IF (NOT -1 Num)(0 VAHKKU OR AIGODAT);
	## 1870-jagiin lea čielggas ahte láddelaččaide guske ortnegat.
	## ...gal guhkui ruoššaid gávppašanriekti guvttiin vahkuin. NOT -1 Num!

SELECT (Pl Loc) IF (NOT *-1 COM-HEAD BARRIER NPNH)(0 PLACE);
	## Sápmelaččat leat unnitlogus earret muhtun unnit guovlluin.

SELECT Loc IF (-1 ("máilbmi" @GN>))(0 ("nuortta#oassi" Com))(NOT *-1 COM-HEAD)(NOT 1 COM-HEAD);
    ## Sápmelaččat leat maid geavahan seammálágan álbmotdálkkodeami , go máilmmi nuorttaosiinge , guhppen ja boaldán báhkkoliid (moxa).
    # this noun combination is also place in most cases

SELECT Loc IF (0 Num)(*1 PL-NOUN BARRIER NOT-ADJ);
SELECT Loc IF (0 PL-NOUN);
	## Mun ledjen golmmain heajain.

SELECT (Pl Loc) IF (0 ("beassáš"));
	## Dat bistá beassážiin hellodagaide.
	
REMOVE Com IF (0 ("lagus"));
    ## Guovlluin, mat leat Ruoŧa ráji lahkosiin, ledje stuora erohusat dáin sisaboađuin jagis 1998 ja ledje gaskkal 81 000 kr ( Romssas ) ja 400 000 kr ( Davvi-Trøndelágas ).
    # Hopefully, we can make a set for location substantives that cannot be comitative.


# Disambiguation based upon verb valency
# --------------------------------------

SELECT Loc IF (*-1 LOC-VERB BARRIER CS OR CP OR VFIN OR COM-HEAD LINK NOT 0 Adv)
	(NOT *-1 CC BARRIER CS OR CP LINK -1 LOC-VERB)
	(NOT 0 Num OR Pron LINK *1C (N Com) BARRIER NPNH)(0 Com);
	## Juohke dáhpáhusas ferte áibbas boastut leat beroškeahttá sámiid
	## riekteáddejumiin.

SELECT Loc IF (NOT 0 ("dat") OR Actio)(NOT *1C Com BARRIER NPNH)
	(NOT 0 (N Num Nom) LINK 1 (Sg Com))(NOT 0 (Sg Com) LINK -1 (N Num Nom))
	(*1 LOC-VERB BARRIER CS OR CP OR V-NOT-AUX);
	## Maiddái sámi fágabirrasiin ja ásahusain sáhttá jearrat ráđiid.

SELECT Loc IF (*-1 ACC-LOC-VERB BARRIER NOT-NP);
	## Sáhttá viežžat dieđuid divššohasa oapmahaččain.
	
SELECT Loc IF (*-1 ("fitnat") BARRIER NPNH)(0 N);
    ## Mii leat ášši ovddidan sihke go leat fitnan departemeanttain.
    
    
SELECT Loc IF (*-1 PLACE-VERB BARRIER CP OR CS)(0 PLACE OR ORGANIZATION);
    ## ..gohčodedje maid earáid go sámesogat olbmuid, geat orro sámesiiddain.
    
SELECT Loc IF (*-1 INDUSTRY BARRIER NPNH)(0 GEOGRAPHICAL-PLACE);
    ## Guollebivdu jogain ja jávrriin.
    
SELECT Loc IF (0 HUMAN OR ORGANIZATION)(*1 SOURCE-VERB BARRIER CS OR CP OR VFIN LINK NOT 0 Adv)
	(NOT *-1 CC BARRIER CS OR CP LINK -1 LOC-VERB);
	## Guovlluid dearvvašvuođafitnodagain gáibiduvvojit dakkár rutiinnat.

SELECT Loc IF (*-1 SOURCE-VERB BARRIER CS OR CP OR VFIN LINK NOT 0 Adv)
	(NOT *-1 CC BARRIER CS OR CP LINK -1 LOC-VERB)
	(0 HUMAN OR ORGANIZATION);
	
SELECT (Pl Loc) IF (*-1 (@SUBJ) LINK 0 HUMAN OR ORGANIZATION OR (Pron Rel))(0 ORGANIZATION OR HUMAN OR EVENT LINK 0 (Sg Com) LINK NOT -1 PRON-DU)(*1 ABSTRACT-ACTIVITY-VERB LINK NOT 0 Actio LINK NOT 1 N BARRIER CP OR CS OR COMMA);
    ## Gielddat berrejit dearvvašvuođastašuvnnain deattuhit mánáid ja nuoraid váttisvuođaid ovddalgihtii eastadeami ja psykososiálabarggu mánáid ja nuoraid váras.
SELECT (Pl Loc) IF (*-1 ABSTRACT-ACTIVITY-VERB LINK NOT 0 Actio LINK NOT 1 N)(0 ORGANIZATION OR HUMAN OR EVENT LINK 0 (Sg Com) BARRIER CP OR CS OR COMMA LINK NOT -1 PRON-DU);
    ## Departemeanta áigu ovttasbargat Sámedikkiin ja movttiidahttit gielddaid ovddidit doaimmaid, mat nannejit sámi mánáid ja nuoraid identitehta ja oktiigullevašvuođa sámi servodagain.
    # This rules assumes that abstract activities are performed with a tool but not with the help of others in comitative case. The set of abstract-activity verbs might have to be restricted to those verbs that do have these features.
    
SELECT (Pl Loc) IF (0 ORGANIZATION OR HUMAN OR EVENT LINK 0 (Sg Com) BARRIER CP OR CS OR COMMA LINK NOT -1 PRON-DU)(*1 ABSTRACT-ACTIVITY-VERB LINK NOT 0 Actio LINK NOT 1 N);
    
    
    
SELECT (N Pl) IF (0 (Sg Com))(1C (Pron Rel Pl));
    ## Áigodatbajilgovva nissoniin geat leat válljejuvvon Sámediggái 1989-2001 54 Tab.4 .

# Locative in appositions
# -----------------------
    
SELECT Loc IF (*-1 PUNCT-LEFT BARRIER NPNH LINK -1 Loc)(0 Com LINK *1 PUNCT-RIGHT BARRIER VFIN OR CP OR CS);
    ## Mot lea sámi nissoniid posišuvdna obbalaččat bargoeallimis (vuođđoealáhusain, almmolaš- ja priváhta suorggis, smávva fitnodagain je.) ja erenoamážit go leat bargin sámi ásahusain ja organisašuvnnain?    

# Disambiguation based upon NP coordination
# -----------------------------------------
#both directions

SELECT Loc IF (*-1 ("nugo" Adv) BARRIER NPNH LINK *-1 Loc BARRIER NOTCOMMA)(0 (Acc PxSg3));
	## Departemeantta bealis atnet dehálažžan, ahte ovdánahttin dievasmahttá ja doarju sámi gelbbolašvuođa fylkkagielddaid sullasaš bálvalusain nugo gárrenávnnassuodjalusas, mánáidsuodjalusas ja bearašsuodjalusas .

SELECT Loc IF (0 (Sg Com))(*1 ("nugo" Adv) BARRIER NOTCOMMA LINK *1 Loc BARRIER NPNH);   
	## Departemeantta bealis atnet dehálažžan, ahte ovdánahttin dievasmahttá ja doarju sámi gelbbolašvuođa fylkkagielddaid sullasaš bálvalusain nugo gárrenávnnassuodjalusas, mánáidsuodjalusas ja bearašsuodjalusas. 

SELECT Loc IF (*-1 CC BARRIER NPNH LINK -2C (@GP>) LINK -1 Loc)(0 Loc LINK 1C (@GP>))(NOT *1 VFIN BARRIER CP OR CS);
SELECT Loc IF (*-1 CC BARRIER NPNH LINK -1C (@GP>) LINK -1 Loc)(0 Loc LINK 1C (@GP>))(NOT *1 VFIN BARRIER CP OR CS);
    ## Dat lohká stuorradiggedieđáhusas Sámedikki birra ja stuorradiggedieđáhusain veajuiduhttima birra.
    ## Dat lohká stuorradiggedieđáhusas Sámedikki ja stuorradiggedieđáhusain veajuiduhttima birra. 
    #Unfortunately, the second rule does not work yet.
    
SELECT Loc IF (*-1 CC OR COMMA BARRIER NPNH LINK -1C Loc)(0 Com)(NOT *1 VFIN BARRIER CP OR CS);
    ## Mot lea sámi nissoniid posišuvdna obbalaččat bargoeallimis (vuođđoealáhusain, almmolaš- ja priváhta suorggis, smávva fitnodagain je.) ja erenoamážit go leat bargin sámi ásahusain ja organisašuvnnain?
    
# Other
# -----
    
SELECT (Pl Loc) IF (*-1 VERBAL-ACTIVITY-VERB BARRIER CP OR CS)(0 Com LINK 0 EVENT);
    ## Dan geažil lea dehálaš ávžžuhit bargiid sámástit ráđđádallamiin.
    
SELECT (Pl Loc) IF (NOT *-1 COM-HEAD BARRIER NPNH)(0 TEXT OR ABSTRACT-TEXT)(*1 VERBAL-ACTIVITY-VERB LINK 0 Pass BARRIER NPNH);
    ## Dálá sámiid máidnasiin ja muitalusain muitaluvvo dološ oskui gullan hálddiid birra .

SELECT (Pl Loc) IF (NOT *-1 COM-HEAD BARRIER NPNH)(*-1 VERBAL-ACTIVITY-VERB LINK 0 Pass BARRIER NPNH)(0 TEXT OR ABSTRACT-TEXT);
    ## Sii máinnašuvvojedje vearrologahallamiin earáid maŋŋá .  
	
# Disambiguation based upon idiosyncratic verb NP combinations
# --------------------------------------------------------------

# áimmahuššat
# - - - - - -

SELECT Loc IF (*-1 ("áimmahuššat") BARRIER CP OR CS)(0 TEXT);
    ## Deaŧalaš lea ahte dieđut divššohashálddahuslaš vuogádagain gieđahallojuvvojit nu ahte divššohasaid persovdnasuodjalus áimmahuššojuvvo seamma láhkai go divššohasjournálainge.

# álggahit
# - - - - -

REMOVE (Sg Com) IF (*-2 ("álggahit") BARRIER CP OR CS)(*-1 @OBJ)(0 (Pl Loc));
    ## Giellakantuvrras ii leat formála váldi álggahit gielladoaimmaid etáhtain, muhto veahkehit.
    
# bearrat
# - - - -

REMOVE (Sg Com) IF (0 ("suohkan"))(*1 ("bearrat") BARRIER CP OR CS);
    ## Mii bivddiimet 11 vearroáššis eará suohkaniin veahki bearrat vearu.

# beastit
# - - - -

REMOVE Com IF (*-1 ("beastit") BARRIER CP OR CS)(0 ("suddu"));
    ## Son beastá álbmogis sin suttuin.

REMOVE Com IF (0 ("suddu"))(*1 ("beastit") BARRIER CP OR CS);
    ## Son riegádahttá bártni, ja don galggat gohčodit su Jesusin, dasgo
    ## Olbmot son lea sin suttuin beastán.
    ## Dieđán, ahte son lea álbmogis sin suttuin beastán.
    
#REMOVE Loc IF (-1 COM-VERB)(0 (Sg Com));
    ## Son barggai suttuin, vaikko Jesus beastá su.

# bivdit
# - - - -

#REMOVE Com IF (*-1 ("bivdit"))(0 PLACE BARRIER NPNH);
	## Eará bivdovugiiguin bivdojuvvo dušše muhtun sajiin.
	
SELECT Loc IF (*-2 ("bivdit") LINK *1 ("árvvoštallan" Acc) BARRIER NPNH)(0 (Sg Com));
    ## Guovlluid dearvvašvuođafitnodagaid plánat ovddiduvvojit SDD dohkkeheapmái, ja SDD bivdá árvvoštallamiid geahččoeiseválddiin.
    
# buktit
# - - - -

SELECT Loc IF (*-1 ("buktit") BARRIER CP OR CS)(0 TEXT);
    ## Sámediggi sáhttá maid buktit cealkámušaid guovlluid dearvvašvuođafitnodagaid jahkedieđáhusain ja plánaárvalusain (kap. 3.4).
    
# buoridit
# - - - - -

REMOVE Com IF (*-1 ("buoridit") BARRIER CP OR CS)(0 ("buozalvas") OR ("buozalmas") OR ("vihki"));
    ## son oahpahii sin synagogain, sárdnidii evangeliuma riikka birra ja
    ## buoridii olbmuid buot buozalvasain ja vigiin. 
    
# čoaggit
# - - - - -

REMOVE Com IF (*-1 ("čoaggit") BARRIER CP OR CS)(0 ("bastilis#látnjá") OR ("gáskálas"));
    ## Sáhttá ba viidnemurjjiid gal čoaggit bastilislánjáin dahje fiikkoniid
    ## gáskálasain ?
    

# čuožžut
# - - - - -

REMOVE Com IF (*-1 ("čuožžut") BARRIER CP OR CS)(0 ("synagoga") OR ("gáhta#čiehka"));

# čuovvulit
# - - - - -

REMOVE Com IF (*-1 ("čuovvulit" Pass) BARRIER CP OR CS)(0 ("stuorradigge#dieđáhus"));
    ## Politihkalaš sávaldat dáhkidit sámi geavaheaddjiid bálvalusaid dási lea maid čuovvuluvvon iešguđet stuorradiggedieđáhusain, earret eará stuorradiggedieđáhusas Sámedikki doaimmaid birra, mii geigejuvvo juohke jagi ja stuoradiggedieđáhusain mielladearvvašvuođafuolahusa, fástadoavtterortnega ja veajuiduhttima birra.

# deattuhit
# - - - - -

SELECT (Pl Loc) IF (*-1 ("deattuhit") BARRIER V OR CP OR CS)(0 (Sg Com));
    ## Departemeantta áigu deattuhit sámepolitihkalaš áššiid johtočállagiin ja raporterengáibádusain, ovdamearkka dihte mielladearvvašvuođa, boarrásiidfuolahusa, borasdávdafuolahusa ja bargiidrekruterendoaimmaid buoridanplánaid oktavuođas.
    # deattuhit is inclined to take a locative adjunct


    
# dovdat
# - - - -

REMOVE Com IF (0 ("šaddu"))(*1 ("dovdat") BARRIER CP OR CS);
    ## Šattuin dii dovdabehtet sin.
    
REMOVE Com IF (*-1 ("dovdat") BARRIER CP OR CS)(0 ("šaddu"));
    ## Muora dovdá dan šattuin.

# dikšut
# - - - -   

SELECT Loc IF (*-1 ("dikšut") BARRIER CP OR CS)(0 PLACE OR ORGANIZATION);
    ## Eai leat lahkage doarvái logopedat, geat dikšot geavahedjiid klinihkain, eaige sámiid ássanguovllus leat galle gieldda, mat leat ásahan logopeda virggiid.
    
    
# finadit
# - - - - -

REMOVE Com IF (*-1 ("finadit") OR ("fitnat") BARRIER NPNH)(0 ORGANIZATION);
   ## Sihke Mázes ja Guovdageainnus leat boarrásat viššalit finadan doaibmaguovddážiin.
   ## Mii leat ášši ovddidan sihke go leat fitnan departemeanttain ja go ministarat ja stádačállit leat finadan min suohkanis.
   
   # I am not sure about the BARRIER but if we got another noun again the next word could be complement either to finadit or to the noun.
   # REMOVE Com IF (*-1 ("finadit") BARRIER NPNH)(0 PLACE);
   
# fuobmát
# - - - -

SELECT Loc IF (*-1 ("fuobmát") BARRIER CP OR CS OR COM-HEAD)(0 ("dilli"));
    ## Go vuos dovdá ovtta unnitlohkoálbmoga, lea álki fuobmát oahpes minstariid maiddái eará unnitloguid diliin.
   
    
# gáibidit
# - - - - -

REMOVE Com IF (*-1 ("gáibidit") BARRIER CP OR CS)(0 ORGANIZATION OR HUMAN);
   ## Stáda gáibida eambbo ahte eambbo suohkaniin go regulere ja lágaid rievdada 
   ## ja suohkanat galget ovttatládje ođasmahttojuvvot.
   # maybe gáibidit does not stand with comitative at all
   
# geahpedit
# - - - - -

REMOVE Com IF (*-2 ("geahpedit") OR ("geahpidit") BARRIER CP OR CS)(*-1 @OBJ)(0 ("jávri"));
   ## Suohkan lea maiddái ráhkadan plána geahpedit guollehivvodaga muhtin jávrriin.
   
# geavahit
# - - - - -

SELECT Loc IF (*-1 ("geavahit") BARRIER CP OR CS)(0 ("doaibma") OR TEXT);
    ## Divššohashálddahuslaš vuogádagat, maid ollu dearvvašvuođaásahusat geavahit doaimmaineaset, eai gula "juornála" -doahpagii, mii máksá čohkkejuvvon dieđuid ovttaskas divššohasa birra.

   
# geiget
# - - - -

REMOVE Com IF (*-1 ("geiget") BARRIER CP OR CS)(0 ("stuoradigge#dieđáhus"));
    ## Politihkalaš sávaldat dáhkidit sámi geavaheaddjiid bálvalusaid dási lea maid čuovvuluvvon iešguđet stuorradiggedieđáhusain, earret eará stuorradiggedieđáhusas Sámedikki doaimmaid birra, mii geigejuvvo juohke jagi ja stuoradiggedieđáhusain mielladearvvašvuođafuolahusa, fástadoavtterortnega ja veajuiduhttima birra.


    
# gullat
# - - - -

REMOVE Com IF (*-1 ("gullat") BARRIER CP OR CS)(0 HUMAN);
    ## Go Herodes árvidii ahte viisát ledje beahttán su, de moaráhuvai.
    
# leat
# - - - 

SELECT Loc IF (*-1 ("leat") BARRIER CP OR CS OR (@SPRED) OR COM-HEAD LINK NOT *-1 Loc)(0 EVENT OR PLACE OR ORGANIZATION BARRIER N);
    ## Beassášmárkaniin fitne sullii 9000 guossi ja 3500 guossi ledje čakčamárkaniin.
    ## Skuvlajagis 2001/2002 leat oktiibuot 469 oahppi suohkana vuođđoskuvllain.
    
# luohpat
# - - - -

SELECT Loc IF (-1 ("luohpat"))(0 ("virgi"));
    ## Virgeloguid lea suohkan dássážii geahpedan dainna lágiin go olbmot luhpet virggiin.

# morihit
# - - - -

REMOVE Com IF (*-1 ("morihit") BARRIER CP OR CS)(0 ("nagir"));
   ## Go Jovsset morihii nahkáriinnis, de son dagai nugo Hearrá eŋgel lei
   ## gohččon, ja válddii Márjjá lusas áhkkán.
    
# oažžut
# - - - -

SELECT Loc IF (*-1 ("oažžut") LINK 0 V-TRANS-ACT LINK *1 Acc BARRIER CP OR CS)(0 HUMAN BARRIER NPNH LINK NOT *-1 ("ovttas") BARRIER NPNH);
    ## Go eará geavaheaddjit ožžot bálvalusaid guovttegielat bargiin , čalmmustahtto sámegiella ja sámi kultuvra ja vejolaš ovdagáttut sáhttet geahppánit .

# oahpahit
# - - - - -

REMOVE (Sg Com) IF (*-1 ("oahpahit") BARRIER CP OR CS)(0 ORGANIZATION OR PLACE);
    ## Son oahpahii sin synagogain.
    
# registreret
# - - - - - -

REMOVE (Sg Com) IF (*-1 ("registreret") BARRIER CP OR CS)(0 EVENT OR PLACE);
    ## Gallis registrerejuvvon sámi jienastuslogus sámediggeválggain.    
    
# sáhtostit
# - - - - -

REMOVE (Sg Com) IF (*-1 ("sáhtostit") BARRIER CP OR CS)(0 ("doaibma#guovddáš"));
    ## Suohkan sáhtosta ain boarrásiid ruoktot ovan doaibmaguovddážiin.

    
# unnidit
# - - - -

REMOVE (Sg Com) IF (*-1 ("unnidit") BARRIER CP OR CS)(0 ORGANIZATION OR PLACE);
    ## Virgáibidamiid bisseheapmi daguhii ahte mii leat unnidat sadjásašbargiid anu mánáidgárddiin ja skuvllain.
    
# váldit mielde
# - - - - - - -

REMOVE (Sg Com) IF (*-2 ("váldit") BARRIER CP OR CS)(-1 ("mielde"))(0 N OR Pron);
    ## Allet váldde ruđaid mielddádet boahkániin, allet golli, allet silbba
    ## dahje veaikki.
    # It might be possible to generalize further.
    
# váldit vuhtii
# - - - - - - -
    
SELECT (Pl Loc) IF (*-1 ("váldit") LINK NOT 0 Actio LINK NOT 1 N LINK 1 ("vuhtii"))(0 ORGANIZATION OR INSTITUTION LINK 0 (Sg Com) BARRIER CP OR CS OR COMMA LINK NOT -1 PRON-DU);

SELECT (Pl Loc) IF (NOT *-1 COM-HEAD BARRIER CP OR CS)(0 INSTITUTION OR ORGANIZATION LINK 0 (Sg Com) BARRIER CP OR CS OR COMMA LINK NOT -1 PRON-DU)(*1 ("váldit") LINK NOT 0 Actio LINK NOT 1 N LINK 1 ("vuhtii"));
    ## Geahččoeiseválddit galget bearráigeahčuin váikkuhit dasa ahte dearvvašvuođa- ja sosiálabálvalusain váldet vuhtii geavaheaddjiid vuoigatvuođa oažžut áddehahtti informašuvnna ja vuoigatvuođa informerejuvvon miehtamii, ja fágalaččat dohkálaš doaimmaid giellagáibádusaid.
    
# vástidit
# - - - - -

SELECT (Pl Loc) IF (*-1 ("vástidit") BARRIER NPNHA LINK *1 Ill BARRIER NPNHA)(0 (Sg Com));
SELECT (Pl Loc) IF (*-1 ("vástidit") BARRIER NPNHA)(0 (Sg Com) LINK *1 Ill BARRIER NPNHA);
    ## SDD lea nammadan guovlluid dearvvašvuođafitnodagaid stivrraid, mat vástidit doaimmaineaset dušše stáhtii, mii daid eaiggáduššá.
    
# vurket
# - - - -

SELECT Loc IF (*-1 ("vurket") BARRIER CP OR CS OR VFIN)(0 Com LINK 0 ORGANIZATION OR HUMAN);
    ## Muhtun dearvvašvuođaásahusaide sáhttet heivet ain guhkti sáddenáigemearit, go dat sáhttá leat dárbu vurket journálaid guhká muhtun ásahusain.
    
        	
# Atnit deastta
# - - - - - - - 
	
REMOVE Com IF (*-2 ("atnit") LINK 1 ("deastta") BARRIER NPNH);
    ## Nubbi lea ahte válbmasit hábmejuvvon gáhttenráđđenvugiin ferte atnit
    ## deastta sámi luondduárvvuin ja vieruiduvvamiin.
    # Should also allow for illative
    
# Demo example
REMOVE (Com) IF (@1 BOS LINK *1 ("orrot")) (0 (Pl Loc));
    ## Amsterdamas sii orrot fatnasiin.
    
# Idiosyncratic NP NP combinations
# --------------------------------

# dárbu
# - - - 

SELECT Loc IF (*-1 ("dárbu") BARRIER NOT-NP)(0 (Pl Com) LINK 0 ORGANIZATION);
    ## Sámi mánáid dárbbut mánáidgárddiin, sámi ohppiid dárbbut vuođđo- ja joatkaskuvllain ja sámi servodaga dárbbut rávisolbmuid oahpahusas ja alit oahpus ja dutkamis, fertejit lea Sámedikki beroštumi ja ráhčamušaid vuođđun dán suorggis.

# diehtu
# - - - -

SELECT Loc IF (*-1 ("diehtu") BARRIER CP OR CS)(0 N);
    ## Deaŧalaš lea ahte dieđut divššohashálddahuslaš vuogádagain gieđahallojuvvojit nu ahte divššohasaid persovdnasuodjalus áimmahuššojuvvo seamma láhkai go divššohasjournálainge.
    # In both cases: information "about" and "from" the complement is locative.

# dikšu
# - - -

SELECT Loc IF (-1 ("dikšu"))(0 ILLNESS OR BODY);
	## Nuoraidjoavku fállá poliklinihkalaš dikšunfálaldaga erenoamážit nuoraide ja nuorra rávisolbmuide, geain leat rádjašuvvan gárrenávnnasváttisvuođat, geat dárbbašit divššu gárrenávdnasiin ja miellaváttisvuođain ovttastuvvan váttisvuođaid geažil, dahje geain lea iešsoardinproblematihkka .
	
SELECT Loc IF (0 ILLNESS OR BODY)(*1 ("dikšu") BARRIER CP OR CS LINK *-1 TV);
SELECT Loc IF (0 ILLNESS OR BODY)(*1 ("dikšu") BARRIER CP OR CS LINK *1 TV);
    ## Gárrenávdnasiin son dárbbaša divššu.
    ## Gárrenávdnasiin son divššu dárbbaša.
    ## Julggiin son divššu dárbbaša.

REMOVE Com IF (*-1 ("guolle#hivvodat") BARRIER NPNH)(0 ("jávri"));
    ## Vestfjellprošeakta, mas kártet guollehivvodagaid jávrriin davábealde eanu.
    #in the future: SELECT Loc IF (*-1 STATE) BARRIER NPNH)(0 PLACE Sg Com);

# lassáneapmi
# - - - - - -

SELECT Loc IF (*-1 ("lassáneapmi") BARRIER NPNH)(0 HUMAN OR ORGANIZATION);
    ## Vuosttaš sámediggeválga čađahuvvui juste dan áiggis go lei biddjon guhkes-áiggi fuomášupmi ja lei oalle fáhkkes lassáneapmi nissonáirasiin Norgga eará álbmotválljejuvvon orgánain.
    
SELECT (Pl Loc) IF (*-1 ("lassáneapmi") LINK *1 Loc BARRIER NPNH)(0 INSTITUTION OR ORGANIZATION BARRIER COM-HEAD OR CP OR CS);
    ## Vuosttaš sámediggeválga čađahuvvui juste dan áiggis go lei biddjon guhkes-áiggi fuomášupmi ja lei oalle fáhkkes lassáneapmi nissonáirasiin Norgga eará álbmotválljejuvvon orgánain.

# nuppádas
# - - - - -

SELECT Loc IF (-1 ("nuppádas"))(0 TEXT);
    ## Elektrovnnalaš journála geavahettiin addo dalle dábálaččat čálalaš nuppádas journáladieđuin .
    
# ohcan
# - - -

REMOVE Com IF (*-1 ("ohcan") BARRIER NPNH)(0 ("ealáhus"));
    # Eai leat šat nu ollu ohcamat priváhta ealáhusain.
    
# ovddasvástádus
# - - - - - - - -
SELECT (Pl Loc) IF (*-1 ("ovddas#vástádus"))(0 (Sg Com) BARRIER NPNH);
    ## Viđa guovllu dearvvašvuođafitnodagas lea ovddasvástádus guovllu olbmuid spesialistadearvvašvuođabálvalusain.
    
SELECT (Pl Loc) IF (*-1 LOC-COMPL-NOUN LINK NOT 0 ("ovda#mearka" Ess))(0 (Sg Com) BARRIER NPNH);

# stáhta
# - - - -
SELECT Loc IF (*-1 BOS LINK NOT *1C V)(*-1 HUMAN LINK NOT -1 (@GN>))(0 ("stáhta" Com));
    ## ILO konvenšuvdna (nr 169) eamiálbmogiid ja čearddaid birra iešbirgejeaddji stáhtain (ILO-konvenšuvdna).
    
# ABSTRACT-ENTITY
# - - - - - - - -

SELECT (Pl Loc) IF (*-1 ABSTRACT-ENTITY BARRIER NPNH)(0 EVENT OR TEXT);
    ## Boazodoallu - ii namut maidege bággonjuovvamiid birra, vaikko dat lea ge okta dain guovddáš šiehtadallanfáttáin dán jagi boazodoallošiehtadallamiin.
    
# Other locative rules
# --------------------
SELECT Loc IF (*-1 ("sáhka") BARRIER NPNH);
        ## Ii leat dušše sáhka olbmuid oktavuođain juohkebeaivválaš eallimis.

SELECT Loc IF (*-1 ("fuolahus") BARRIER NPNH);
	## Jus mánáidsuodjalusbálvalus lea váldán fuolahusa mánáin...

SELECT Loc IF (1 ("gos"));
SELECT Loc IF (1 COMMA)(2 ("gos"));  
	## Riikkain gos lea čearddalaš, oskkolaš dahje gielalaš minoritehtat,...
	## Sajiin, gos ii leat oktage priváhta eanaeaiggát,...

SELECT Loc IF (*-1 BOS OR CS OR REALCLB OR Pr BARRIER NPNHAI)(NOT 0 (A Nom))
	(NOT 0 (N Num Nom) LINK *1 (V Du3) BARRIER NOT-ADV-PCLE)
	(*1 ("leat") BARRIER (@SUBJ) OR (@OBJ) OR (@SPRED) OR (@OPRED) );
#	(*1 ("leat") BARRIER NOT-ADV-PCLE-NEG ); # Check this rule with the positive barrier.
	## Ádjásgo lea ođđa fanas?
        ## Suomas maid lei dilli váttis.
        ## Lágas eai leat mearrádusat.
	## Daningo sámiin lea iežaset giella.
	## Vuollel 12 jahkásaš mánáin ii leat iešheanalis geahččanvuoigatvuohta.

SELECT Loc IF (1 COPULAS LINK 0 VFIN)(*2 NP-HEAD-NOM BARRIER NPNHA LINK NOT *1 V-NOT-AUX
	BARRIER NOT-ADV-PCLE);
	## ..gos čoavjevuliin leat geažit latnjalagaid. 
        ## 70 proseanttas guovllu dearvvašvuođaásahusain leat sámegielat bargit.
         # But we do not catch the "of them" construction:
        ## Vihta dainna leat dál.

SELECT Loc IF (*-1 BOS OR CS OR REALCLB BARRIER NOT-NP)(*1 Interr BARRIER WORD
	LINK *1 VFIN BARRIER CS OR CP LINK *1 VFIN BARRIER CS OR CP LINK 0 ("leat")
	LINK *1 NP-HEAD-NOM BARRIER NPNHA LINK NOT *1 PrfPrc BARRIER NOT-ADV-PCLE);
	## Váhnemiin dahje earáin, geain lea váhnenovddasvástádus, lea vuoigat-
	## vuohta miehtat dearvvašvuođaveahki divššohasaide.


SELECT Loc IF (-1 ("leat" Clt) LINK *-1 BOS OR CS OR REALCLB BARRIER NOT-CC); 
SELECT Loc IF (-1 ("go" Pcle))(-2 ("leat") LINK *-1 BOS OR CS OR REALCLB 
	BARRIER NOT-CC); 
	## Leago dis ruđaid?
	## Lea go dis ruđaid?

SELECT (Sg Loc) IF (*-1 NP-HEAD-NOM BARRIER NPNH LINK *-1 COPULAS BARRIER NPNHA);
	## Dat lei váldooaidnu boazodoallolága dulkomis. 

SELECT Loc IF (*-1 COPULAS BARRIER NPNHA LINK NOT *-1 Nom BARRIER CS)
	(*1 Nom BARRIER NPNHA);
	## Dattege lea lassebeavdegirjjis stuorra ideologalaš mearkkašupmi 
	## sámi vuoigatvuođagáibádusaid hárrái.

SELECT (Prop Loc) IF (*-1 COPULAS BARRIER NOT-ADV-PCLE LINK NOT *-1 ("namma")
	OR ("áhčči") BARRIER CS OR CP);
	## Go sii ledje ain čoahkis Galileas, de...
	## Su namma galgá leat Johanas. NOT *-1 "namma"!

SELECT (Pl Loc) IF (1 LEAT-FIN-NOT-IMP)(*2 NP-HEAD-NOM OR (Num Nom) 
	BARRIER V-NOT-AUX OR CS);
	## Badjel 16 jahkásaš mánáin lea iešheanalis vuoigatvuohta...

SELECT Loc IF (0 ("láhka"))(1 ("§")); 
	## ...mat leat namahuvvon Dearvvašvuođabargiid lágas § 3.

REMOVE (Sg Loc) IF (0 (V Actio Loc))(*1 Acc BARRIER NPNH);
	## Ii leat sáhka juohkimis iešguđet journálačálli olbmuid ektui.

SELECT Loc IF (0 (Acc PxSg3))(1 NP-BORDER)(*-1C NP-HEAD-ACC BARRIER CC OR CS OR CP);
	## Beassá dárogiela maid vehá oahppat dulkomis.

REMOVE (Sg Com) IF (0 Pl)(1C (Interr Pl));
	## Mearrádusa ferte čađahit láhkaaddimiin ja eará doaimmain mat almmolaš 
	## eiseválddit mearridit.
	
REMOVE Com IF (*-2 (@OBJ) BARRIER VFIN)(-1 (N Ill) OR (ACR Ill));
    # Disambiguation of locative and comitative.
    
REMOVE Com IF (*-4 (@OBJ) BARRIER VFIN)(*-3 (N Ill) OR (ACR Ill))(-1 CC OR COMMA)(-2 Com);
    ## Doarjjaruđaiguin Sámediggi sáhttá vuoruhemiidis mielde addit doarjaga 
    ## kvalitehtaloktendoaimmaide gielddain, fylkkagielddain ja oahppoásahusain.    



# Plural locative as complement of quantifier

SELECT Loc IF (*-1 HEADOFPARTS BARRIER NPNH OR COMMA)(0 Pl or Pl3)(NOT 0 Num OR Ord) ;
        ## Mun dovdan vihtta proseantta dainna.
	## Dat maid gohčodit normála giellaovdáneapmi dáhpáhuvvá mánáin geat
	## bajásšaddet eanet go ovttain gielain go seamma jahkásaš ovttagielat mánáin.
    # This rule should be tested. In: zcorp/dis/sme/all/1.429.txt (8272).

SELECT Loc IF (-1 NUMERALS)(0 Pl or Pl3)(0 Pron)(1 EOS) ;
        ## Mun dovdan vihtta dainna.
         # This rule should be tested In: zcorp/dis/sme/all/1.429.txt (8278)
        
        

# Comitative
# ==========

# ToC for this section

     # Various rules
     # NP-internal disambiguation of Com
     # Disambiguation based upon type of NP
     # Disambiguation based upon verb valency
     # Disambiguation based upon idiosyncratic combinations
     # Coordination


# Various rules
# -------------
SELECT Com IF (*-1 ("ovttas") OR ("ovttamielas") OR ("oktan") OR ("searválagaid") 
	BARRIER (N Com));
	## Ii lean dárbu dábálaš eanavuovdimii, oktan opmodatregistreremiin.
	
SELECT Com IF (*-1 ("guovttá")) (0 Loc LINK 0 ANIMATE BARRIER CS OR CP);
    ## Dat son muitalii, go guovttá leimme dainna nieiddain.

SELECT Com IF (*-1 ("oktavuohta") OR ("gaskavuohta") OR ("ovttas#ráđiid") BARRIER 
	NPNHA);
	## ...go sii váldet oktavuođa doaktáriin.
	## Mii leat juo váldán oktavuođa Sámi Álbmotbellodagain ja Sosialisttalaš Gurutbellodagain, muitala Smuk Rolstad.

SELECT Com IF (*-1 ("váldit") BARRIER NPNH LINK -1 ("oktavuohta"));
	## Vuoigatvuohta oktavuođa váldit divššohasbearráigeahččiin.

SELECT Com IF (*-1 ("malssidit") BARRIER VERB);
	## Máhtte malssidii lávkkas Máreha lávkkain.

	
SELECT (Sg Com) IF (-1 (Pers Du1))(*1 V-DU1 BARRIER VERB OR CC OR CS OR CP);
	## Moai áhčiin manaime dohko.

SELECT (Sg Com) IF (-1 (Pers Du3))(*1 V-DU2 BARRIER VERB OR CC OR CS OR CP);
	##

SELECT (Sg Com) IF (-1 (Pers Du3))(*1 V-DU3 BARRIER VERB OR CC OR CS OR CP);
	##

SELECT (Sg Com) IF (-1 (Pers Gen) LINK 0 Du1 OR Du2 OR Du3)(1 N);
	## Dat lea munno vieljain dállu.

SELECT (Sg Com) IF (1 (Pron Interr Sg));
SELECT (Sg Com) IF (1 COMMA)(2 (Pron Interr Sg));
	## Rávisolbmot hállet mánáin gii lea lahtigoahtán 2-3 sáni cealkkan.
	
SELECT (Sg Com) IF (*-1  (N Sg Loc) BARRIER NPNH OR PUNCT-LEFT OR PUNCT-RIGHT OR CLB OR COMMA LINK *-1 IV BARRIER TV)(0 N);
    ## Várrepresideanta lea maid 05.01.00 leamaš čoahkkimis Sosiála- ja dearvvasvuođadepartemeanttain čilgen dihte Sámedikki juolludemiid sámi dearvvasvuođa- ja sosiálaplána čuovvuleamis .
    ## Soai leigga čohkkámin fatnasis áhčiineaskka ja čiktimin firpmiid. 
    # This rule should be tested. In: zcorp/dis/sme/all/060227b.txt (8295).
        
SELECT Com IF (-1 Gen)(0 ("láhka#vuođđu" Loc));
    ## Geassemánu 8.b. 2001 láhkaásahus nr 676 oktagaslaš plánaid birra dearvvašvuođalágaid vuođul lea addojuvvon láhkamearrádusaid láhkavuođuin.
    
SELECT (Sg Com) IF (*-1 HUMAN OR ORGANIZATION OR INSTITUTION LINK 0 (@SUBJ) LINK NOT 0 LOC-HEAD BARRIER NPNH)(0 (Pl Loc) LINK 0 HUMAN)(*1 VFIN BARRIER CP OR CS);
    ## Norgga delegašuvdna, várrepresideanttain, doalai sártni dievasčoahkkimii čoahkkima loahpahettiin, mas doarjjui WHO boahtteáiggi barggu dearvvasvuođain.

    
# Idiomatic expressions
# ---------------------

# dáinna lágiin / dáinna vugiin
# - - - - - - - - - - - - - - - -
SELECT Com IF (-1 ("dát") OR ("dat") OR ("diet"))(-1 Com)(0 ("láhki"))(0 Com);

SELECT Com IF (-1C (@AN>) OR (@GN>) OR (@DN>) OR (@QN>))(0 ("vuohki" Pl Loc))(NOT *-1 ("leat") BARRIER CP OR CS)(NOT *1 ("leat") BARRIER CP OR CS);
    ## Eiseválddit galget doaibmevaš vugiin doarjut dán barggu. 

# vehkiin
# - - - -
SELECT (Sg Com) IF (-1 (@GN>))(0 ("veahkki" Loc));
    ## Sámi ássiin lea nana árbevierru veahkehit ieš iežas persovnnalaš fierpmádagaid vehkiin.

    
# NP-internal disambiguation of Com
# ---------------------------------
SELECT Com IF (*-1C (Dem Sg Com) BARRIER NPNH);
SELECT Com IF (-2C (Dem Sg Com))(-1 Adv);
	## Romssa guovllubuohcceviessu lea bargamin dáinna áššiin.
	## ??

SELECT Com IF (0 Num); # Cf. rules for PL-NOUN above.
SELECT Com IF (*-1C Num LINK 0 Com BARRIER NOT-ADJ OR ("jahkásaš"));
	## ...de son gal luoitá nođiid; ovttain sániin dadjat: luoitala.
	## Mo sáhttá mottiin sániin dadjat...

SELECT (Sg Com) IF (0 (Pl Loc) LINK 0 (@AN>))(1C (Sg Com));
    ## Áššebealit galget juohke jagi šiehtadallat ekonomalaš doaimmaid, fágalaš sosiála áššiid ja eará gažaldagaid ja doaimmaid mat buriin vugiin ovdánahttet boazodoalu.

# Disambiguation based upon type of NP
# ------------------------------------ 
SELECT Com IF (0 Prop)(NOT 0 ("Sápmi") OR ("Sámediggi")); 
	##
 # Person names are normally sg.
 
# Disambiguation based on valency of adverbs and particles
# --------------------------------------------------------

SELECT Com IF (*-1 ("searválagaid") BARRIER NPNH)(0 (Pl Loc));
	## Ráđđehus áigu searválagaid Sámedikkiin čuovvulit Sámedikki mánáid - ja nuoraidplána.
	
SELECT Com IF (0 (Pl Loc))(*1 Rel LINK 1 ("searválagaid"))(NOT *1 Com BARRIER NPNH);
	## Dán barggus sávvá Sámediggi oažžut ovttasbarggu eará oasálaččaiguin, vuosttažettin gielddain ja fylkkagielddain, mas searválagaid sáhttit ovdamearkka dihtii váikkuhit konsešuvdnaeavttuid hábmema...
	

# Disambiguation based upon verb valency
# --------------------------------------
SELECT Com IF (*-1 COM-VERB BARRIER VERB OR CS OR CP LINK 0 VERB)(NOT 0 PLACE);
	## Dieđut galget meannuduvvot várrugasvuođain.

SELECT Com IF (0 Sg)(*1 COM-VERB BARRIER CS OR CP LINK 0 VERB)
	(NOT *1 COPULAS BARRIER VERB LINK *1 COM-VERB BARRIER NOT-ADV-PCLE 
	LINK 0 Inf);
    ## ...go sápmelaččat duođas álge eanadoaluin bargat.


REMOVE (Pl Loc) IF (*-2 TOGETHER-VERB OR COM-VERB)(*-1 ("oktii"))(0 N BARRIER Loc);
    ## Mii heive bures oktii ránes datneláiggiin.
    # we have to think about a barrier, another Loc to the right for example
    
REMOVE Loc IF (*1 TOGETHER-VERB)(*2 ("oktii") BARRIER CS OR CP)(NOT *3 Com BARRIER CS OR CP);
    ## Dáinna háliidan čatnat oktii digáštallamiid fidnopedagogihka ja
    ## álgoálbmotoahpu birra, mat dán rádjái leat leamašan birrasiin man 
    ## gaskkas lea leamaš unnán oktavuohta.

SELECT (Sg Com) IF (*-1 ("ovttas#bargat") LINK 0 V-SG)(0 (Pl Loc))
(NOT *-1 Com);    
    ## SDD ovttasbargá Sámedikkiin , Helse Nordiin ja gelbbolaš dutkanbirrasiiguin , ja čađahit sámi pasientaiskkadeami.
    
    
SELECT (Sg Com) IF (*-1 ("ovttas#bargat" Inf) LINK *-1 V-SG)
(NOT *-1 Com);
    ## SDD áigu ovttasbargat Sámedikkiin , Helse Nordiin ja gelbbolaš dutkanbirrasiiguin , ja čađahit sámi pasientaiskkadeami.
    
REMOVE Loc IF (*-1 BOS LINK NOT *1 ILL-VERB OR ("leat"))
     (0 (Sg Com) LINK NOT 0 TIME LINK *-1 Ill BARRIER CP OR CS)
     (NOT *-1 (HEADOFPARTS Com) BARRIER CP OR CS);
REMOVE Loc IF (*-1 Ill)(*-1 BOS LINK *1 ("gullat") OR ("leat"))(0 (Sg Com) LINK NOT 0 TIME LINK *-1 Ill BARRIER CP OR CS OR COMMA)(NOT *-1 (HEADOFPARTS Com) BARRIER CP OR CS);
    ## Dasa gullá mánáid ja nuoraid problemaláhttema eastadanbargguin.
    # Those rules are supposed to hit those cases where both illative and locative exclude each other. 
    # This is probably NOT the case in illative verb constructions where the illative
    # does not denote a "location (goal)", with habeo-constructions, where the 
    # locative gets another function, and probably not with partitive constructions.
    # Unfortunately, those are not mentioned in the rules. Now, they are. 
    
# tools (concrete and abstract)
# - - - - - - - - - - - - - - -
    
REMOVE Loc IF (*-1 ACTIVITY-VERB OR MOVEMENT-VERB)(0 Com LINK 0 TOOLS);
    ## Dan moai ráidniime skárbbaniin.
    
REMOVE Loc IF (0 TOOLS)(*1 ACTIVITY-VERB OR MOVEMENT-VERB BARRIER NOT-ADV);
    ## Ja ovdal go háhppehin basttiin goivestit eret de Ánddar guvssiin bođii, vierrulii áicemaehtton: šattai ollái ipnun.
    
REMOVE Loc IF (0 TOOLS LINK 1 CC LINK *1 Loc BARRIER NPNHA)(*1 ACTIVITY-VERB OR MOVEMENT-VERB);
    ## Mon in leat dugo Sivvar - mii láve bissuin ja beatnagiin mannat dugo livččii dáčča.
    
REMOVE (Pl Loc) IF (*-1 (@SUBJ) LINK 0 HUMAN)(0 ABSTRACT-TOOLS BARRIER CS OR CC OR COMMA)(*1 ABSTRACT-ACTIVITY-VERB);
	## Geahččoeiseválddit galget bearráigeahčuin váikkuhit dasa ahte dearvvašvuođa- ja sosiálabálvalusain váldet vuhtii geavaheaddjiid vuoigatvuođa oažžut áddehahtti informašuvnna ja vuoigatvuođa informerejuvvon miehtamii, ja fágalaččat dohkálaš doaimmaid giellagáibádusaid.

# BODY as an instrument
# - - - - - - - - - - -

SELECT (Sg Com) IF (*-1 VERBAL-ACTIVITY-VERB BARRIER VFIN OR LOC-HEAD)(0 (Pl Loc) LINK 0 BODY);
    ## Lávdde luhtte juiget olbmot olles čoddagiin.
    # Idea: Body as an instrument to perform verbal activity.
    
SELECT (Sg Com) IF (*-1 VERBAL-ACTIVITY-VERB BARRIER VFIN OR LOC-HEAD)(0 (Pl Loc) LINK 0 HUMAN);
    ## Terje hárve hupmá mediain, muhto son áinnas hupmá sápmelašvuođas birra.
    
# Disambiguation based upon idiosyncratic V NP combinations
# ---------------------------------------------------------

# álggahit
# - - - - -

REMOVE Loc IF (*-1 ("álggahit") BARRIER CP OR CS)(0 ("beanagitta#váldin") OR Actio);
    ## Mii álggaheimmet maiddái beanagittaváldimiin...
    # REMOVE Loc IF (*-1 ("álggahit") BARRIER CP OR CS)(0 ACTIVITY OR ACTION);
    

# ásahit
# - - - -

REMOVE Loc IF (*-1 ("ásahit") BARRIER CP OR CS OR Com)(0 ("ruhta#doarjja"));
    ## 8 ođđa bargosaji ásahuvvojedje jagis 2001 ealáhusfoandda ruhtadoarjagiin.
    # It has to be the first comitative.

# bártidit
# - - - - -

REMOVE Loc IF (*-1 ("bártidit") BARRIER CP OR CS)(0 ("prográmma"));
    ## 
    
# čállit/ tevdnet/ devdnet/ sárgut
# - - - - - - - - - - - - - - - - -

REMOVE Loc IF (*-1 ("čállit") OR ("tevdnet") OR ("devdnet") OR ("sárgut") BARRIER CP OR CS)
    (0 WRITING-TOOLS);
    ## Mun čálán bleahkain.
    
# čielggadit
# - - - - - -

REMOVE Loc IF (0 ("bargobearrái#geahčču") OR ("bargodearvvašvuođa#bálvalus"))(*1 ("čielggadit") BARRIER CP OR CS);
    ## Bargobearráigeahčuin ja bargodearvvašvuođabálvalusain mii ovttasráđiid
    ## čielggadeimmet dárbbašlaččamusaid ja nu vealttaimet bággosáhkkoheamis.
    # Does not hit these example sentences!

REMOVE Loc IF (*-1 ("čielggadit") BARRIER CP OR CS)(0 ("bargobearrái#geahčču") OR ("bargodearvvašvuođa#bálvalus"));    

    
# čohkkát
# - - - - 

REMOVE Loc IF (*-1 PLACE-VERB BARRIER CP OR CS)(0 HUMAN);
    ## Soai leigga čohkkámin fatnasis áhčiineaskka ja čiktimin firpmiid.
    # áhčči can be replaced by a set of people, maybe even a wider set
    # Does not hit these example sentences!
    
# čuorvut
# - - - - -

SELECT Com IF (*-1 ("čuorvut") OR ("čurvet") BARRIER CP OR CS)(0 ("jietna"));
    ## Ovccát diimmus Jesus čuorvvui alla jienain: "Eli, Eli, lema sabaktani?

        
# dahkat
# - - - -

REMOVE Loc IF (*-1 ("dahkat") BARRIER CP OR CS)(0 ("rábmon"));
    ## Go don attát attáldagaid gefiide, de ale daga dan rábmomiin nugo
    ## guoktilaččat dahket synagogain ja šiljuin, vai olbmot 
    ## gudnejahtášedje sin.
    
# dearvvahit
# - - - - - -

REMOVE Loc IF (*-1 ("dearvvahit") BARRIER CP OR CS)(0 ("ráfi"));


# dikšut, hoitát
# - - - - - - - 

SELECT (Pl Loc) IF (*-1 ("dikšut") OR ("hoitát") OR ("hoaidit") BARRIER NPNH)(0 ILLNESS);
    # cf. "dikšu" rule.
        
# geahpedit
# - - - - -

SELECT Com IF (0 ("máhttu"))(*1 ("geahpedit") OR ("geahpidit") OR ("nannet") BARRIER CP OR CS);
   ## Dákkár máhtuin sáhttá geahpedit guovttekultuvralaš bearrašiid mánáid dili, ja nannet sámi mánáid identitehta.
   
   


# geahččat
# - - - - -

REMOVE Loc IF (*-1 ("geahččat") BARRIER CP OR CS)(0 ("anistupmi")); 
    ## Dat guhte geahččá nissonii anistumiin, lea juo rihkkon náittosdili váimmustis.
    
# gohččut
# - - - - -

REMOVE Loc IF (*-1 ("gohččut") BARRIER NPNHA)(0 ("šuokŋa"));
    ## ...gohččo váivves šuoŋain olbmo sirdit saji...
    
# guldalit
# - - - - -

SELECT Com IF (0 ("giehta" Loc))(*1 ("guldalit") OR ("njávkkasitit") BARRIER NOT-ADV);
    ## Iđđesbeallái vuohttá lieggasa gieđain guldala.
    # BODY, vielleicht Extremitäten und Sprechorgane, BODY-ACTIVITY?
SELECT Com IF (*1 ("guldalit") OR ("njávkkasitit") BARRIER NPNHA)(0 ("giehta" Loc));
        
# investeret
# - - - - - -

REMOVE Loc IF (0 ("bušeahtta"))(*1 ("investeret") BARRIER CP OR CS); 
    ## ..., muhto mii diehtit ahte fágagirjjit leat divrasat ja diimá jagi bušeahtain ii sáhttán investeret.
    

# juogadit
# - - - - -

REMOVE Loc IF (*-2 ("juogadit") BARRIER CP OR CS)(*-1 @OBJ)(0 HUMAN); 
    # REMOVE Loc IF (*-1 ("juogadit") BARRIER CP OR CS)(0 HUMAN);
    # Maybe it has to be the first comitative following the object

# lassánit
# - - - - -

SELECT (Sg Com) IF (*-1 ("lassánit") BARRIER CP OR CS)(0 ("kruvdnu") OR ("kruvdno") OR ("duhát"));
    ## Háldooamit lassánedje 11 mill kruvnnuin.
    
# láktadit
# - - - - -

SELECT Com IF (*-1 ("láktadit") BARRIER CP OR CS)(0 ("viidni"));
    ## Ja muhtun sis viegai dakkaviđe su lusa, válddii guobbarasa ja láktadii
    ## dan suvrra viinniin, bijai dan soabbegeahčái ja attii sutnje juhkat.
    
# leat
# - - -

SELECT Com IF (*-1 BOS LINK *1 ("leat" PrfPrc) LINK *-1 VFIN BARRIER CP OR CS)(0 (Pl Loc) LINK 0 ANIMATE LINK *-1 A BARRIER CP OR CS);
SELECT Com IF (*-1 ("leat" ConNeg) LINK NOT *1 PrfPrc BARRIER CP OR CS)(0 (Pl Loc) LINK 0 ANIMATE LINK *-1 A BARRIER CP OR CS);
SELECT Com IF (*-1C ("leat") LINK 0 VFIN LINK NOT *1 PrfPrc OR ConNeg BARRIER CP OR CS)(0 (Pl Loc) LINK 0 ANIMATE LINK *-1 A BARRIER CP OR CS);
    ## Go dii ehpet leat šiega leamaš ealibiin.
    ## Go dii lehpet šiega leamaš ealibiin.
    ## Go dii lehpet šiega ealibiin.

# lihkostuvvat/lihkustuvvat
# - - - - - - - - - - - - -

SELECT Com IF (*-1 ("lihkostuvvat") OR ("lihkustuvvat") BARRIER CP OR CS)(0 ("prošeakta"));
    ## Mii leat lihkostuvvan bures dáinna prošeavttain.

# luddet
# - - - -

REMOVE Loc IF (*-1 ("luddet") BARRIER CP OR CS)(0 ("ákšu"));
    ## Máret luddii galdda ákšuin.
    # Not hit!
    
    
# mihtidit
# - - - - -

REMOVE Loc IF (*-1 ("mihtidit") BARRIER CP OR CS)(0 ("mihttu"));

# namuhit
# - - - -

SELECT Com IF (*-1 ("namuhit") OR ("gohčodit") BARRIER CP OR CS)(0 ("sátni"));
    ## Sámediggeráđđi šalloša ja imaštalla go departementa ii namut sámi skuvlla sániin ge skuvlla IKT-áŋgiruššanplánas » .
    
SELECT Com IF (0 ("sátni"))(*1 ("namuhit") OR ("gohčodit") BARRIER CP OR CS);
    ## Sániin "lappalainen" gohčodedje maid earáid go sámesogat olbmuid, geat orro sámesiiddain.
    
# oahpahit
# - - - -

REMOVE (Pl Loc) IF (*-1 ("oahpahit") BARRIER CP OR CS)(0 ("isitvuohta"));
    ## Go Jesus lei heaitán sárdnideames, de olbmot hirpmástuvve su
    ## oahpaheamis, dasgo son oahpahii sin isitvuođain ii ge nugo sin
    ## čálaoahppavaččat.
    # Loc Sg might be possible though

   
# oastit
# - - - -

REMOVE Loc IF (*-1 ("oastit") BARRIER CP OR CS)(0 ("ruvdno") OR ("ruvdnu") OR ("ruhta"));
    ## Dainna čuđiin ruvnnuin áiggun bierggu oastit.
    # No hit!
REMOVE Loc IF (0 ("ruvdno") OR ("ruvdnu") OR ("ruhta"))(*1 ("oastit") BARRIER CP OR CS);
    ## Ruđain osttiimet liseanssaid Skrivebord 2000 ja web-rámmaide.
    
# ruohkkat
# - - - - -

SELECT Com IF (*-1 ("ruohkkat") BARRIER CP OR CS)(0 ("gurot#giehta") OR ("giehta") OR ("olgeš#giehta") LINK 0 (Pl Loc));
    ## Ruohkkalettiin gurotgieđain gitnaliid duohken čállilii.
    # Where giehta becomes the place/theme of ruohkkat, it gets accusative/not locative.
    
# ruhtadit
# - - - - -

REMOVE Loc IF (0 ("ruhta" Com) LINK NOT *-1 HEADOFPARTS BARRIER CP OR CS)(*1 ("ruhtadit"));
REMOVE Loc IF (0 HEADOFPARTS LINK 0 Com LINK *1 ("ruhta" Loc) BARRIER CP OR CS)(*1 ("ruhtadit"));
    ## Osiin gelbbolašvuođaguovddážiidda juolluduvvon ruđain sáhttá maid ruhtadit sadjásašgoluid hospiterema oktavuođas dahje lassigoluid, mat Kárášjogas / Leavnnjas šaddet joatkkaoahpus.
    
# soabadit
# - - - - -

REMOVE Loc IF (*-1 ("soabadit") BARRIER CP OR CS)(0 HUMAN);

# stuorrut
# - - - - -

SELECT Com IF (0 ("bálvaleapmi") LINK 0 (Pl Loc))(1 ("stuorrut") BARRIER CP OR CS);
    ## Sieiddi bálvalemiin sturron eallu gohčoduvvui bálvvoseallun.

# suokkardit
# - - - - - -

SELECT (Sg Com) IF (*-1 ("suokkardit") BARRIER NPNHA)(0 (Pl Loc) LINK 0 ORGANIZATION OR HUMAN OR Org);
    ## SDD áigu suokkardit Sámedikkiin, sáhttágo dán plána doaimmaid čađaheami delegeret Helse Nordii.
    
# veahkehit
# - - - - -

SELECT Com IF (*-1 ("veahkehit") BARRIER CP OR CS)(0 ("tráktor"));
    ## Soai eaba dušše veahkehan mu tráktoriin.
    
# viehkalit
# - - - - -

#SELECT Com IF (*-2 ("viehkalit") LINK 1 Ill BARRIER NPNH)(0 ("diehtu" Loc));
#SELECT Com IF (*-2 MOVEMENT-VERB LINK 1 Ill BARRIER NPNH)(0 ("diehtu" Loc));
SELECT (Sg Com) IF (0 N LINK *-1 Ill BARRIER NPNH LINK *-1 MOVEMENT-VERB BARRIER NPNH)
    (NOT 0 PLACE OR ORGANIZATION OR TIME OR ROUTE OR MEASURE);
    ## Sáhttá viehkalit siidii dieđuin jus juo olmmoš skibáiduvvá.
	#  Todo: Choose between the three.    
    
# vuodjit
# - - - -

REMOVE Loc IF (*-1 MOVEMENT-VERB BARRIER CP OR CS)(0 VEHICLE);
#REMOVE Loc IF (*-1 ("vuodjit") OR ("boahtit") OR ("vuolgit") BARRIER CP OR CS)(0 VEHICLE);
    ## Máhte vujii biillain.
    # There could be an accusative in this position too.


# Coordination
# ------------

SELECT Com IF (*-1 CC OR COMMA BARRIER NPNH LINK *-1C Com BARRIER WORD);
	## Das ráhkaduvvo vuojahat gátti guvlui nu galliin čuolddain 
	## (dahje oar-julggiin) go lea vuogas.
	## Bargiidbellodaga Per Nils Saari beasai suohkanstivrii ja válljii ovttasbargat olgešbellodagain, gurutbellodagain ja johttisámi listtuin vai ožžot eanetlogu suohkanstivrras.
	
# Disambiguation based upon idiosyncratic NP NP combinations
# ----------------------------------------------------------

# ulbmil
# - - - -
REMOVE Loc IF (0 ("ulbmil"))(1 ("ahte"));
    ## Viidáseappot álggahuvvui bargu man fylkkagielda ovttaidahttá ulbmiliin ahte ráhkadit raportta.

# It seems as if ulbmil merely can be locative in habeo-constructions, so mabybe this rule needs to be extended.

# ovttaoaivilis
# - - - - - - -
REMOVE Loc IF (*-3 (Sg @SUBJ))(*-2 ("leat") LINK *1 ("ovtta#oaivil"))(0 (Sg Com));
REMOVE Loc IF (*-3 (Sg @SUBJ))(*-2 ("leat") LINK *1 ("ovtta#oaivil"))(0 (Sg Com));
    ## Finnmárkku Ovddádusbellodaga jođiheaddji Knut Erik Mauseth ii leat ovttaoaivilis iežas bellodagain dien áššis .

# ovttasbargočoahkkin/ čoahkkin
# - - - - - - - - - - - - - - -
REMOVE Loc IF (*-1 EVENT BARRIER NOT-NP)(0 ("mánáid#suodjalus") OR INSTITUTION OR HUMAN OR ORGANIZATION OR Org);
    ## Mis leat ovttasbargočoahkkimat PP-bálvalusain, skuvllain, mánáidsuodjalusain ja skulvadearvvašvuođabálvalusain.
    ## Sámediggepresideanta lea čoahkkimis mánáid- ja bearašministeriin guovvamánu 17. b.
    ## Guovdageainnu johttisápmelaččaid listu lea behtohallan go gulai maid Sámedikki presideanta dajai čoahkkimis Eanandoallodepartementtain, Gieldadepartementtain ja Norgga boazosápmelaččaid riikaservviin.
    # ORGANIZATION might be a bit dangerous...
    
# Human
# - - -

SELECT Com IF (-1 HUMAN LINK 1 ("fásta#bálká") OR ("bálká"));
    ## Bálvalusas lea nubbi suohkanfysioterapeuta fástabálkkáin ja nubbi priváhta dásis, gii oažžu 100 % doarjaga.
    # Assuming that the noun directly following a noun from the set HUMAN will also be its complement.
    
SELECT Com IF (*-1 COM-COMPL-NOUN BARRIER NPNH)(0 HUMAN OR ORGANIZATION OR STATE);
    ## Dearvvašvuođabargiid doaibma adnojuvvo priváhtan, vaikke vel dearvvašvuođabargiin lea fástadoavtteršiehtadus dahje eará šiehtadus almmolašvuođain, muhto eai leat dattetge virggis almmolaš ásahusas.
   

# =========== #
  CONSTRAINTS #21
# =========== #

# Locative and comitative - Disambiguation based upon coordination
# ================================================================
 # The idea is to let the other rules work first, and then let the
 # coordination follow suit.


SELECT (Sg Loc) IF (1 CC OR ("/"))(*2C Loc BARRIER NPNH);
	## Dan čuolddas dahje oar-juolggis ráhkadišgoahtá olmmoš doarrása.

SELECT (Sg Loc) IF (*-1 CC  OR ("/") BARRIER NPNH LINK -1C Loc); 
	## Go 110a:s lea sáhka sápmelaččaid "ealáhusaid" sihkkarastimis
	## ja ovdánahttimis,...


SELECT (Pl Loc) IF (*-1 CC OR ("/") BARRIER NPNH LINK *-1 Loc LINK NOT 0 Com BARRIER NPNH);
	## Seamma olmmoš áimmuhuššá máŋga dáin doaimmain dahje rollain.
	## Sámi oahpahussuorgi stivrejuvvo daid rámmaid mielde maid lágat ja njuolggadusat bidjet, ja daid ulbmiliid ja višuvnnaid mielde mat ovdanbohtet Sámediggeplánas ja eará oahpahus- ja oahppoplánain ja strategiijain.
	## Mii maid muđui fuomášuhttit ahte ollu lohkoávdnasiin mat fátmmastit dahje čilgejit sámediggeválgga , dain lea sáhka smávva loguin / vuollegis lohkoárvvuin.


SELECT (Pl Loc) IF (1 CC OR (Neg Foc) OR ("/"))(*2 Loc BARRIER NPNH LINK NOT 0 Com);
	## Son lea ođđa mediain ja skuvllas.
	## Vuđolaš váttisvuohta lea dattege ahte sámi bustávat eai doaimma dohkálaččat ovttaskas dihtoriin eaige interneahtas.
		
SELECT Loc IF (*-1C Loc LINK 1 COMMA OR CC  OR ("/"))(0 (Sg Com) BARRIER NPNH)(1 COMMA OR CC OR (Neg Foc) LINK *1C Loc BARRIER NPNH);
	## Dearvvašvuođaásahusain, doaktáriin, bátnedoaktáriin ja sealgeetniin lea leamaš geatnegasvuohta čállit journálaid maiddái ovddeš lágaid mielde.
	
SELECT Loc IF (*-1C Loc LINK *1 Rel BARRIER Com OR CP OR CS)(0 Com LINK *-1 CC OR COMMA OR (Neg Foc)  OR ("/") BARRIER NPNH);
    ## 


SELECT (Sg Com) IF (*-1 CC OR (Neg Foc)  OR ("/") BARRIER NPNH LINK -1 Com LINK NOT 0 Loc);
	## Ii lean dárbu dábálaš eanavuovdimii, oktan opmodatregistreremiin
	## ja čuoldimiin.

SELECT (Sg Com) IF (1 CC OR (Neg Foc)  OR ("/"))(*2 Com BARRIER NPNH LINK NOT 0 Loc);
	## Fága galgá ovddiidit ovttasbarggus Sámedikkiin ja sámi dearvvašvuođa-
	## ja sosiálabargiiguin.
	
# Coordination with SEAMMAX OR XGO
# --------------------------------
# We always need rules for SEAMMAX LINK 1 ("go") OR ("dego") and for the compound coordinator XGO
# Coordination to the right as well as to the left are needed
# the comma to the left of XSEMMA/DEGO might be optional
# that is the reason why BARRIER NOT-COMMA is used
# the comma to the right of the coordinated element though is kept

# Coordination to the left
# - - - - - - - - - - - - -

SELECT Loc IF (NOT *-1 (@GN>) BARRIER CP OR CS)(0 (Sg Com))(*1 SEAMMAX LINK NOT 0 Qst BARRIER NOT-COMMA LINK 1 ("go") OR ("dego") LINK *1 Loc BARRIER NPNH LINK 1 COMMA) ;
    ## Sámediggeráđđi čujuha cealkámušastis áššái ahte sápmelaš rávisolbmuin seammaládje go sámi mánáin , ferte leat vejolašvuohta oažžut vuođđoskuvlaoahpahusaset O97 Sámi mielde.

SELECT Loc IF (NOT *-1 (@GN>) BARRIER CP OR CS)(0 (Sg Com))(1 XGO BARRIER NOT-COMMA LINK *1 Loc BARRIER NPNH LINK 1 COMMA);
# Those rules are locative specific. They choose a locative if no other coordination candidates are existent, which means no genitive modifiers, no other arguments or adjuncts. The (NOT *-1 (@GN>) constraints is supposed to exclude genitive modifier coordination.

SELECT Com IF (NOT *-1 (@GN>) BARRIER CP OR CS)(0 (Pl Loc))(1 SEAMMAX LINK NOT 0 Qst BARRIER NOT-COMMA LINK 1 ("go") OR ("dego") LINK *1 Com BARRIER NPNH LINK 1 COMMA) ;

SELECT Com IF (NOT *-1 (@GN>) BARRIER CP OR CS)(0 (Pl Loc))(1 XGO BARRIER NOT-COMMA LINK *1 Com BARRIER NPNH LINK 1 COMMA);


# Another round of locative rules
# ===============================
# those apply if no comitative rule hits

SELECT Loc IF (NOT *-1 BOS LINK *1 COM-HEAD BARRIER CP OR CS)(0 LOC-NOUN);
    ## Dakkár dilálašvuođain berre anonymiseret dahje krypteret dieđuid, mat eai leat nu hearkkit.
    
# Partitive constructions
# - - - - - - - - - - - - 

#The following rules are for two numerlas seperated by sth. that still makes sure we are talking about one NP:
SELECT Loc IF (NOT *-1 COM-HEAD BARRIER CP OR CS)                  (*-1 Num BARRIER NPNHA LINK -1 PRE-NP-HEAD OR Adv LINK -1 Num)(0 Com BARRIER NPNH)(NOT *-1 COM-HEAD BARRIER CP OR CS);
    ## Muhtin biirres leat gávcci obbalaččat 12 válljejuvvon áirasiin leamaš nissonolbmot, ja eará biirres leat buohkat geat leat válljejuvvon leamaš dievddut.
SELECT Loc IF (NOT *-1 COM-HEAD BARRIER CP OR CS)(-1C (@PrcN>) LINK *-1 Num BARRIER NPNHA LINK -1 PRE-NP-HEAD OR Adv LINK -1 Num)(0 Com BARRIER NPNH)(NOT *-1 COM-HEAD BARRIER CP OR CS);

#The following ones are for two numerals next to each other, here we have to make sure that those are not of the form "2-3 Nom:ain" written as "2 3 Nom:ain",:
SELECT Loc IF (NOT *-1 COM-HEAD BARRIER CP OR CS)(*-1 Num BARRIER NPNHA LINK     0 Sg OR Pl LINK -1 Num LINK NOT 0 Sg OR Pl)(0 Com)(NOT *-1 COM-HEAD BARRIER CP OR CS);
SELECT Loc IF (NOT *-1 COM-HEAD BARRIER CP OR CS)(*-1 Num BARRIER NPNHA LINK NOT 0 Sg OR Pl LINK -1 Num LINK     0 Sg OR Pl)(0 Com)(NOT *-1 COM-HEAD BARRIER CP OR CS);
    # The LINK Sg/Pl constraint has du be mentioned because it is the only thing that distinguishes arabic numbers from word numbers
    # this rule basically says that if we have 2 different numeral notations next to each other, they should denote a partitive construction
    # This rule has to be placed after other numeral rules since we do not want to describe constructions such as "book 1 second part (Loc/Com)"
    # The last two rules work except for the gávcci/ovcci case

    
REMOVE Com IF (NOT *-1 COM-HEAD BARRIER CP OR CS)(0 (Pl Loc) LINK *1 KLASS BARRIER NAPP LINK 1 Num)(NOT *1 COM-HEAD BARRIER CP OR CS);
    ## Sámediggi sáhttá maid buktit cealkámušaid guovlluid dearvvašvuođafitnodagaid jahkedieđáhusain ja plánaárvalusain (kap. 3.4).
    # The point is, that if further paragraph-, page- or chapter- information is given, usually the location is meant.
    
SELECT Loc IF (NOT *-1 COM-HEAD BARRIER CP OR CS)(0 ORGANIZATION LINK *-1 HUMAN OR ROLE BARRIER VFIN OR CP OR CS OR COMMA)(NOT *1 COM-HEAD BARRIER CP OR CS);
    ## Konvenšuvdna eamiálbmogiid ja čearddaid birra iešbirgejeaddji stáhtain dohkkehuvvui Norggas jagis 1990, muhto dakkár dohkkeheapmi ii leat velá dahkkon Ruoŧas.
    
SELECT (Pl Loc) IF (NOT *-1 BOS LINK *1 COM-HEAD BARRIER CP OR CS)(0 SITUATION)(NOT *1 COM-HEAD BARRIER CP OR CS) ;
    ## Álbmotriekti gierdá dákkár doaimmaid duššefal heahtediliin ja go daidda lea dievaslaš ipmárdus dákkár álbmogiid ovddasvástideaddji eiseválddiid bealis.
    
SELECT Loc IF (*-1 N BARRIER NPNHAII)(0 KIND)(NOT 1 NP-MEMBER);
    ## Dát lea mearkkašahtti vaikko geahččá dan olis ahte nissonoassi maiddái eará válga-šlájain vásihii muhtin " nuppástusaid " 90-logus .
    
# Passive constructions
# ---------------------

SELECT Loc IF (-1 ACTIVITY-VERB LINK 0 Pass LINK NOT 0 COM-VERB OR TOGETHER-VERB)(0 HUMAN OR ORGANIZATION LINK NOT *1 VFIN BARRIER CP OR CS);
    ## Ossodat galgá váikkuhit dasa, ahte dearvvašvuođapolitihkalaš mearrádusat ollašuhttojit ja politihkalaš bargoaddit, bargit, geavaheaddjit ja ássit besset oahpásmuvvat vásáhusaiguin, mat čoggojit geahččodoaimmain.

# Essive
# ======

SELECT Ess IF (-1C Nom)(0 Nom);
	## Gáibadussan lea ahte olbmos lea sámegiella ruovttogiellan.

SELECT Ess IF (*-1 Acc BARRIER NPNHA LINK *-1 OPRED-VERB BARRIER NPNHA);
	## Máret anii Máhte jallan.

SELECT Ess IF (1 CC)(*2C Ess BARRIER NOT-NP);
	## Ovdal go kristtalašvuohta bođii Sápmái lei noaiddis erenoamáš 
	## rolla oaidnilin ja dálkkodeaddjin.


# Late case rules (after other case rules have worked). 
# ===================================================== 
 # Case rules that make crucial reference to certain verb forms are placed after
 # the verb rule block.

SELECT (Num Nom) IF (*-1 CC BARRIER NOT-ADV-PCLE LINK *-1 (Num Nom) BARRIER NPNH);
	## Čieža, ja vel moatte uhca guoláža.

REMOVE (Dem Gen) IF (0 ("dat"))(*1C (N Acc) BARRIER NPNH);
	## Naba daid stuora dorskiid, gosa daid bidjet?

SELECT Nom IF (*1 Loc BARRIER NPNH LINK 1 COPULAS LINK *1 (A Nom) BARRIER 
	NOT-ADV-PCLE);
	## ...ahte boazosápmelaččaid luonddoriggodagaid ávkkástallan orohagain 
	## lei ovdal viidát go dat mii lea addon sidjiide.

SELECT Gen IF (NOT *-1 V-TRANS-ACT BARRIER NOT-ADV-PCLE)(0 Dem)
	(*1C (N Gen) OR (N Sg Ill) OR (N Sg Loc) OR (N Pl Com)
	BARRIER NOT-ADJ LINK NOT 0 Cmpnd);
	## Mii eat galgga dán digaštallamii searvat dán oktavuođas.

SELECT Acc IF (0 Indef)(1 (A Acc));
	## Dasgo mun in leat goassege borran maidege eahpebasiid.

REMOVE Gen IF (1 (Pers Gen));
	## Dii huksebehtet hávdemuittuid profehtaide geaid din máddarat godde! 

SELECT Gen IF (*-1 (Indef Acc) BARRIER NOT-ADV)(0 Pers);
	## Oktan suinna sii russiinávlejedje guokte rievvára, nuppi su olgeš ja 
	## nuppi fas su gurut beallái.
	
SELECT Nom IF (0C ("ulbmil" Nom) OR ("ulbmil" @GN>))(*1C Loc BARRIER NPNH);
    ## Ulbmil seammádássásaš bohtosiid juksamis galgá gustot álo beroškeahttá makkár giella- dahje kulturduogáš veahki dárbbašeaddjis lea.



# ============================= #
# Disambiguating verbs - part 2 #
# ============================= #

# Finite forms #
# ================ #
   CONSTRAINTS #22 #
# ================ #

# Finite or not
# =============

SELECT VFIN IF (*-1 BOS OR CS OR (":" CLB) BARRIER VERB)(0 INF-VERB)
	(NOT 0 ("manne") LINK *-1 BOS BARRIER NOT-CC)
	(*1 Inf BARRIER VFIN OR CS OR CP);
	## Áigugo fas skuvlii álgit? 
	## Sáhttit maid namahit ahte ...
 # Looks brave but works!

SELECT VFIN IF (NOT *-1C VFIN BARRIER CS OR CP)(-1 Loc)(NOT -1 (Interr Sg))
	(0 LOC-VERB); 
	## Nuppi lađđasis čuovvu, ahte...

REMOVE VFIN IF (NOT -2 BOS OR CLB)(-1 Inf)(NOT 0 Qst);
	## Lága juksanmearrin lea váikkuhit sihkkarastit álbmogii 
	## ovttadássásaš dearvvašvuođaveahki.
 # Neg conditions because of:
	## Suhpodit daddjojuvvo dat go dušše nođiid váldá olmmoš eret...
	## Mearrideaddjin dasa ferte danin leat addojuvvogo... 

REMOVE VFIN IF (*-1 Nom BARRIER NOT-ADV-PCLE OR CLB LINK *-1C VFIN 
	BARRIER NOT-ADV-PCLE OR CP OR CLB);
	## Lea dárbu nannet doaimmaid mat ovddidit sámi dutkama.

REMOVE VFIN IF (*-1 PrfPrc BARRIER HNOUN OR (Pron Nom) OR VERB OR CC OR CS OR CP 
	OR CLB LINK *-1 ("leat") BARRIER NOT-ADV-PCLE);
  	## Sin badjel geat orrot jápmima eatnamis ja suoivanis, lea ihtán čuovggas.
	## Gii lea cuigen din báhtarit boahttevaš moarreduomus?

REMOVE VFIN IF (*-1C VFIN BARRIER CC OR CS OR CLB OR PUNCT LINK *-1 CS BARRIER NOT-NP)
	(0 Inf)(*1 COMMA LINK 1 ("de" Adv));
	## Vaikko ii loga makkárge revolušuvnna vuordit dásseárvopolitihkas iežas gielddas maŋŋel dán kurssa, de deattuha ahte buot dáhpáhuvvet proseassa čađa.

REMOVE VFIN IF (*-1C VFIN BARRIER CC OR CS OR CP OR CLB OR PUNCT LINK NOT *-1 Interr
	BARRIER NOT-NP)
	(*1 CC OR CLB OR CS OR CP OR EOS BARRIER NP-HEAD-NOM OR NP-HEAD-ACC);
	## Muhto sáđuid son boaldá čáskatmeahttun dolas.

REMOVE VFIN IF (NOT *-1 Rel BARRIER VERB)
	(*1C VFIN BARRIER CLB OR CC OR CS OR CP OR PUNCT OR go OR ("dego") OR 
	("galle" Num) LINK NOT 0 Qst)(NOT *1 Neg BARRIER VFIN)
	(NOT *1 VFIN BARRIER CLB OR CC OR CS OR CP LINK 1 go);
	## Sámegillii gálbet universitehta lea šaddan realitehta.
	## Áldu mii dálvvi miehtá njamaha miesi gohčoduvvo čearpmateadnin.


REMOVE VFIN IF (0 Loc)(*1 Neg BARRIER NOT-ADV-PCLE OR CLB LINK *1 PrfPrc OR ConNeg 
	BARRIER VERB OR CLB OR CC OR CS OR CP);
	## Lágas ii duššaduvvo oktage bustávaš dahje čuoggáš, ovdal go...
	## Máhttájeaddjit orro jávohaga eai ge dalle muitalan geasage maid ledje oaidnán.
	
REMOVE VFIN IF (-1 ("nu"))(*1 ("go" Pcle) BARRIER NOT-NA); 
	## No longer needed?

REMOVE Inf IF (-1 ("nu"))(*1 ("go" Pcle) BARRIER NOT-NA); 
	## No longer needed?

REMOVE ConNeg IF (*-1 PrfPrc BARRIER NOT-ADV-PCLE);
	## Go geavahus ii duođaštuvvon leat álggahuvvon ovdal go... 


# Indicative Negative
# -------------------
  # Unique are Sg1, Sg3, Du1, Du2, Du3, Pl1, Pl2, Pl3
  # Sg2 "it" = ABBR.

# Select Neg Sg2 instead of ABBR
REMOVE ABBR IF (*-1 ConNeg OR PrfPrc)(0 Neg); ## Dáppe don leamaš it.
REMOVE ABBR IF (0 Neg)(*1 ConNeg OR PrfPrc);  ## Don it leat dál.


# Infinitive
# ----------
SELECT Inf IF (*-2 INF-VERB BARRIER VFIN OR NP-HEAD-NOM OR CS)(-1 Po)
	(NOT *-1 CP BARRIER CS OR VFIN);
	## Dearvvašvuođabargit galget divššohasa agi, silolaš dilálašvuođa,
	## láddama ja vásáhusduogáža ektui láhčit dilálašvuođaid buoremus
	## lági mielde.

SELECT Inf IF (NOT *-2 CC BARRIER NPNH LINK -1 Nom)(-1C (Sg Nom))
	(NOT 0 V-PL3 LINK *1 NP-HEAD-PL-NOM BARRIER NPNHA);
	## Mánáin lea vuoigatvuohta oažžut dearvvašvuođaiskkadeami.
	## Mus ii leat báljo dilli vuolgit.

SELECT Inf IF (-1 Acc)(NOT -1 (Pl Nom))(*1 Acc BARRIER NPNH);
SELECT Inf IF (-1 Acc)(NOT -1 Interr)(*1 Ill BARRIER NPNH);
	## Son vuolggahii bálvaleaddjiidis rávkat bovdejuvvon gussiid heajaide.
	## ...leago dieđiheamis doarvái vuođđu ášši váldit meannudeapmái.

SELECT Inf IF (-1 COMMA LINK *-1 (Pron Interr) LINK *-1 INF-VERB BARRIER VERB);
	## Dalle fertejit buotlágan ávnnaslaš eavttut maid dárbbaša vai
	## duođalaččat galgá sáhttit kultuvrras dikšut, gullat dása.

SELECT Inf IF (-1 COMMA LINK *-1 (Pron Interr) LINK *-1 (Pron Interr) 
	LINK *-1 INF-VERB BARRIER VERB);
	## Eai galgga sii geat gullet dakkáraš minoritehtaide ovttas eará 
	## láhttuiguin mat gullet sin jovkui, manahit vuoigatvuođaideaset dikšut 
	## iežaset kultuvra, dovddastit ja geavahit iežaset oskku, dahje geavahit 
	## iežaset giela.

SELECT Inf IF (NOT *-1 (Pl Nom) BARRIER NOT-ADV-PCLE)
	(1 LEAT-FIN-NOT-IMP)(NOT 1 Qst);
	## Dáidda heŋgejuvvojit gurpasat, ja maid dál de guottehit lea áigumin.

SELECT Inf IF (*-1 COPULAS BARRIER NOT-ADV-PCLE LINK *-1 (A Sg Nom) BARRIER
	NOT-ADV-PCLE);
	## Dárbbašlaš lea dikšut jávrriid ovdal go lea ekonomalaččat veara bivdit.

SELECT Inf IF (*-1 (@HNOUN) BARRIER VERB)(1 PUNCT OR EOS);
	## Áigemearri váidalusa buktit.

REMOVE Inf IF (NOT *-1 VFIN BARRIER CS)(0 ("leat"))(*1 PrfPrc BARRIER VERB);
	## De leat máksán.


# Coordinated infinitives
# - - - - - - - - - - - -
SELECT Inf (*-1 COMMA BARRIER VERB OR MO LINK -1C Inf)(NOT 0 (Neg Imprt))
	(NOT *-1 (Pl Nom) BARRIER CS)(NOT *1 (Pl Nom) BARRIER CS);
	## Mun in leat boahtán gomihit, muhto ollašuhttit.
	## Son galgá stuorrut, mun fas uhccut.

SELECT Inf IF (*-1 CC BARRIER VFIN OR CS LINK *-1C Inf BARRIER WORD); 
	## Hearrá Ipmilat galggat gudnejahttit, ja áivvestassii su bálvalit.

SELECT Inf IF (*-2C Inf BARRIER VERB)(-1 CC);
	## Duođalaš nana jáhku geažil lea divššohas vuoigatvuohta biehttalit
	## vuostáváldimis vara dahje varabuktagiid dahje biehttalit botkemis
	## nealgudeami mii lea jođus.
 
SELECT Inf IF (*-3 Interr LINK *-1 Inf BARRIER VERB)(-2 COMMA)(-1 CC)
	(NOT *1 Nom BARRIER NPNHA);
	## Divššohasbearráigeahčis lea vuoigatvuohta buktit oaiviliiddis
	## dilálašvuođaid birra mat gullet bearráigeahčči bargoviidodahkii,
	## ja evttohit konkrehtalaš buoridandoaimmaid.


# Infinitives in lists
# - - - - - - - - - - 

SELECT Inf IF (-2 VFIN)(-1 (":" CLB));
	## Ulbmilat leat: eastadit soađi, nannet ráfi.

SELECT Inf IF (*-2 Inf BARRIER VFIN LINK -1 (":" CLB) LINK -1 VFIN)
	(-1 ("\;" CLB));
	## Ulbmilat leat: eastadit soađi; nannet ráfi.

SELECT Inf IF (*-2 Inf BARRIER VFIN LINK -1 ("\;" CLB) OR ("," CLB) LINK *-1 Inf BARRIER VFIN 
	LINK -1 (":" CLB) LINK -1 VFIN)
	(-1 ("\;" CLB) OR ("," CLB));
	## Ulbmilat leat : eastadit soađi; nannet ráfi; lohkat girjiid.
	#           L-1 L-1 L*-1        L-1 *L-2      -1   0

SELECT Inf IF (*-2 Inf BARRIER VFIN LINK -1 ("\;" CLB) OR ("," CLB) LINK *-1 Inf BARRIER VFIN 
	LINK -1 ("\;" CLB) OR ("," CLB) LINK *-1 Inf BARRIER VFIN LINK -1 (":" CLB) LINK -1 VFIN)
	(-1 ("\;" CLB) OR ("," CLB));
	## Ulbmilat leat : eastadit soađi; nannet ráfi;   lohkat girjiid  , borrat márffi.
	#           L-1 L-1 L*-1        L-1 *L-2     L-1   L* -1         -1   0


# Infinitive in sentence fragments
# - - - - - - - - - - - - - - - - 

SELECT Inf IF (-1C Acc LINK *-1 BOS BARRIER NPNH)(1 EOS);
	## Ruđa juogadit.

SELECT Inf IF (-1C Ill LINK *-1 BOS BARRIER NPNH)(0 ILL-VERB)(1 EOS);
	## Mánáidgåardåai hárjánit.

SELECT Inf IF (-1 BOS)(*1C Acc BARRIER NPNH LINK 1 EOS);
	## Ruđa juogadit.

SELECT Inf IF (-1 BOS)(0 ILL-VERB)(*1C Ill BARRIER NPNH LINK 1 EOS);
	## Hárjánit mánáidgárdái.

SELECT Inf IF (-1C Ill LINK *-1 BOS BARRIER NPNH)(0 ILL-VERB)(1 EOS);
	## Mánáidgåardåai hárjánit.

SELECT Inf IF (-1 BOS)(0 LOC-VERB)(*1C Loc BARRIER NPNH LINK 1 EOS);

SELECT Inf IF (-1C Loc LINK *-1 BOS BARRIER NPNH)(0 LOC-VERB)(1 EOS);




# Indicative or imperative
# ========================

REMOVE Imprt IF (-1 BOS)(0 Adv)(*1 ("?") BARRIER CS); 
	## Gosa don dál?

REMOVE Imprt IF (*-1 CS OR CP BARRIER PUNCT)(NOT -1 CLB OR ("de") OR ("nu"))
	(NOT -1 CC LINK *-1 Imprt BARRIER CS);
	## Jus divššohas roasmmohuvvá dahje oažžu duođalaš váttisvuođaid,...

 # Very unlikely that an imperative should show up in an embedded clause, 
 # except in quotations (...de son celkkii: Manni eret!). But it can be 
 # preceded by an embedded clause, for example a relative clause, in which 
 # case we normally find a comma in front of the imperative.


REMOVE Imprt IF (-1 Adv)(NOT -1 ("nu") OR ("de"));
	## Dalle skáhppo buođđobiergasiid.

REMOVE Imprt IF (-1 Imprt)(NOT 0 ConNeg); 
	## Geahča lagat Hálddašanjoavkku čielggadeami.

SELECT Imprt IF (-2C (N Nom) LINK *-1 BOS OR PUNCT BARRIER NPNH)(-1 COMMA)
	(NOT 0 MOD-ASP OR Pass)(NOT 0 (N Sg Nom) LINK *1 V-SG3 BARRIER CS OR VFIN)
	(NOT 0 (N Pl Nom) LINK *1 V-PL3 BARRIER CS OR VFIN)(NOT *1 ("?"));
	## Ustiban, daga dan man várás don leat boahtán!.

SELECT Imprt IF (-2C (N Nom) LINK *-1 COMMA BARRIER NPNH LINK -1 (N Nom) 
	LINK *-1 BOS OR PUNCT BARRIER NPNH)(-1 COMMA)
	(NOT 0 AUX OR Pass OR (N Nom))(NOT *1 ("?"));
	## Hearra, Davveda bárdni, árpmit mu!

SELECT Imprt IF (*-2 ("dadjat") OR ("cealkit") OR ("vástidit") BARRIER VERB)
	(-1 PUNCT)(NOT 0 Pass)(NOT 0 MOD-ASP);
	## Jesus celkkii sutnje: "Várut dadjamis maidege geasage dan birra!" 

REMOVE Imprt IF (-1 Interr OR CP OR CS)(NOT -1 ("de"));
	## Rohkadallet sin ovddas geat doarrádallet din.

REMOVE Imprt IF (*-1 VERB BARRIER NOT-ADV OR CLB LINK NOT 0 Neg);
	## Eamiálbmotriektái gullá maiddái muhtun muddui njulget vahágiid,...

SELECT (Imprt Sg2) IF (*-1 BOS BARRIER NOT-CC)(NOT 0 ("manne"))(NOT 0 Gen LINK 1 N)
	(NOT 1 EOS)(NOT *1 VFIN BARRIER CS);
	## Várut beatnagis!
	
REMOVE Imprt IF (-1 ("go"));
    ## Stáda gáibida eambbo ahte eambbo suohkaniin go regulere ja lágaid rievdada ja suohkanat galget ovttatládje ođasmahttojuvvot.
    
REMOVE Imprt IF (*-2 BOS LINK 1 NP-MEMBER) (NOT *1 ("excl"));
    ## Sápmelaččat, geain lea dementiija, masset dábálaččat vuosttamužžan dárogielmáhtu, ja dasto maid sámegiela.
    # We assume that in order to avoid ambiguity there would have been an exclamationmark in a construction (with an NP in the first place), which is more likely to be a simple statement



# Some imperatives according to person and number.
# ------------------------------------------------

REMOVE (Imprt Sg2) IF (0 V-SG2)(NOT *1 COMMA OR CC BARRIER VFIN LINK 1C (Imprt Sg2));
	## Don gii gaikkodat tempela ja hukset dan fas ođđasit golmma beaivvis!

SELECT (Imprt Sg2) IF (NOT *-1 V-SG2 BARRIER VFIN)(*-1 PUNCT OR CLB 
	BARRIER NOT-ADV-PCLE)(1 (PxSg2));
	## Nahket miehkát fas dohppii.

REMOVE (V Ind Prs Sg3) IF (*-1 BOS BARRIER NOT-PCLE)(0 (V Imprt Prs Sg2))
	(NOT 0 A OR Foc)(NOT 1 ("."));
 	## Na attes munnje daid.

REMOVE (Imprt Du2) IF (*1 (Pron Pers Nom) BARRIER CS OR CP OR PUNCT LINK NOT 0 Du2);
REMOVE (Imprt Du2) IF (*1 Sg2 BARRIER CS OR CP OR PUNCT);
	## Dat guhte goargü eará báikki, son lea suola ja rievvár.
	## Velá okta váilu dus.

SELECT (Imprt Pl2) IF (*1 (PxPl2) BARRIER CC OR CS OR CP);
	## Váldet mu geassá badjelasadet.


# Imperative in coordination
# --------------------------
SELECT Imprt IF (NOT 0 V-SG3 LINK -1 COMMA LINK *-1 V-SG3 BARRIER VFIN OR CS)
	(-1 BOS OR COMMA)(*1 COMMA LINK 1 Imprt)(NOT 0 (A Attr) LINK 1 N);
	## Bohtet, goddot su, de mii oažžut su árbbi.

SELECT Imprt IF (NOT *-1 VFIN-NOT-IMP BARRIER CS OR CP)(NOT 0 PrfPrc)
	(*1 CC BARRIER VFIN LINK *1C Imprt BARRIER VFIN OR CS OR CP);
	## Čoavdi daid ja bukti mu lusa!

SELECT Imprt IF (-1 COMMA OR CC)(NOT 0 Pron)(*-2 Imprt BARRIER VFIN LINK 
	NOT *-1 CP BARRIER PUNCT OR REALCLB); 
	## Mannet dal ja guorahallet dán máná hárrái!

SELECT Imprt IF (*-2C Imprt)(-1 COMMA);
	## Allet almmi bokte, dasgo dat lea Ipmila truvdnu, allet ge eatnama bokte...

SELECT (Imprt Sg2) IF (*1 CC OR COMMA BARRIER VFIN LINK *1C (Imprt Sg2)
	BARRIER VFIN OR CS OR CP);
	## Gohčo bargiid boahtit ja atte dáidda bálkká!

SELECT (Imprt Sg2) IF (*-1 CC BARRIER NOT-ADV LINK *-1C (Imprt Sg2));
	## Váldde dan mii gullá dutnje ja mana!
 
SELECT (Imprt Pl2) IF (*1 CC OR COMMA BARRIER VFIN LINK *1C (Imprt Pl2)
	BARRIER VFIN OR CS OR CP);
	## Čatnet su gieđaid ja julggiid ja bálkestehket su olggobeallái..
 
REMOVE Imprt IF (*-1 CC BARRIER NOT-ADV-PCLE OR REALCLB LINK NOT *-1 Imprt)
	(NOT -1 CC LINK -1 BOS);
	## Sus lea goaivu gieđas ja áigu buhtistit gortniidis.


# Rule that finds a coordinated Imprt across a relative clause:
# - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
SELECT (Imprt Pl2) IF (-1 CC OR COMMA)(*-2 VFIN BARRIER CS OR CP OR CONTRA 
	LINK *-1 (Pron Interr) BARRIER CS LINK *-1 (Imprt Pl2) 
	BARRIER VFIN OR CS OR CP);
	## Buressivdnidehket sin geat garrudit din, dahket buriid sidjiide
	## geat vašuhit din, ja rohkadallet sin ovddas geat doarrádallet din.


# Negative imperative
# -------------------
  # Neg Imprt Pl2 = allet V Inf = Prs Pl1 = Prs Pl3

SELECT (Neg Imprt Pl2) IF (*1 ConNeg BARRIER Neg); 
	## Allet ge gátte ahte ...



# Verbs according to person and number
# ====================================

# Sg1 - First person singular
# ---------------------------
 # Prs Sg1 = PtcPrf (odd-syllables), and Prs Sg1 = V Actio (even-syllables)
 # Past Sg1 is unique.

SELECT Sg1 IF (0 LEAN) (*1 PrfPrc BARRIER CS);
	## Dál lean cealkán dan dudnuide.

SELECT Sg1 IF (NOT *-1 VFIN BARRIER CS OR PUNCT OR REALCLB)(0 LEAN);
	## Jesus celkkii: "Oanehis áiggi lean velá din luhtte."

SELECT V-SG1 IF (NOT *-1 VFIN OR PrfPrc BARRIER CS OR REALCLB)(NOT 0 Pass OR N)
	(NOT 0 (N Nom) LINK 0 LEX-N)(NOT *1 VFIN BARRIER CS);
	## Oađidettiin gullen olbmo boahtimin.

SELECT V-SG1 IF (*-1 CS BARRIER VFIN LINK -1 BOS)(*1 COMMA BARRIER VFIN);
	## Ovdal go čielggadan dan lagabui,...

SELECT V-SG1 IF (*-1 CC BARRIER VERB LINK *-1C V-SG1 BARRIER VERB)(NOT 0 Pass);
	## Das maŋŋá dajan juoidá álbmotrievtti mearkkašumi birra, ja geavahan
	## das veahkkin ođđaseamos riektegáldu.

REMOVE (V Sg1) IF (*-1 CC BARRIER VERB LINK NOT *-1 V-SG1);
	## Olbmot ledje ija dolastallan ja oggon.

REMOVE V-SG1 IF (0 PrfPrc OR Actio)(*1C VFIN BARRIER CC OR CLB OR CS OR CP);
	## Dálá fiinnuhan riektejurddašanvuogi mielde ii eahpiduvvo ahte 
	## dán livččii galgan dahkat.


# Sg2 - Second person singular
# ----------------------------
 # Sg2 = Inf (even syllables, copula)
 # Note the homonymy with derived verbs (jearat = jearadit + ...
 # Prt Sg2 = Prs Pl3 = Imprt Prs Pl2 (even syll)    vulget
 # Prt Sg2 = Prs Pl3 = Prs Pl1 = Inf (odd syll)   muitalit
 # Prt Sg2 = Prs Pl3                 (contracted) fertejit


SELECT V-SG2 IF (*-2C V-SG2 BARRIER VFIN OR CS OR CP OR PUNCT OR REALCLB)
	(-1 Interr)(NOT -1 (Pl Nom));
	## Don leat garra olmmái gii láddjet doppe gosa it leat gilván.

SELECT Prt IF (*1 IKTE BARRIER VFIN OR CS OR CP);
	## Vulget go ikte?

SELECT V-SG2 IF (*-1 CC BARRIER VFIN LINK *-1C V-SG2 BARRIER VFIN OR Inf OR CS);
	## Dasgo sániidat mielde don dahkkojuvvot friddjan, ja sániidat mielde
	## dubmejuvvot.

SELECT V-SG2 IF (*-1 CC BARRIER NOT-ADV LINK *-1 CP LINK *-1C V-SG2 BARRIER
	NOT-ADV-PCLE);
	## Don oainnát mo olbmot bahkkejit du birra, ja de jearat vel...

SELECT (V Sg2) IF (*-2C (V Sg2) BARRIER VFIN)(-1 CS)(NOT *1 PxPl3);
	## Don it beasa doppe olggos ovdal go leat máksán.

REMOVE ("leat" Pl1) IF (*1 (Sg Nom) BARRIER NOT-ADV-PCLE LINK NOT 0 NUMERALS);
	## Leatgo árgi bajándálkkis?
	## Leat go árgi bajándálkkis?

REMOVE ("leat" Pl3) IF (NOT *-1 (Pl Nom) OR (Pl3 Nom) BARRIER NOT-ADV-PCLE)
	(*1 (Sg Nom) BARRIER NOT-ADV-PCLE OR CLB LINK NOT 0 NUMERALS 
	LINK NOT *1 (Pl Nom) OR (Pl3 Nom) BARRIER NPNHA)
	(NOT *1 (Sg Nom) BARRIER NOT-ADV-PCLE OR CLB LINK 1 CC LINK *1 Nom 
	BARRIER NPNH);
	## Leatgo árgi bajándálkkis?
	## Leat go árgi bajándálkkis?

REMOVE (Prs Pl1) IF (-2 BOS)(-1 ("jus") OR ("jos"))(0 (Prs Sg2))(*1 ("de") BARRIER (Sg2 Nom));
	## Jus leat buohcamin, de fertet...

REMOVE (V Sg2) IF (1 CC)(2 VFIN)(NOT 2 Sg2);
	## Buođđomuoraid maid ráhkadit olbmot gárvvisin dassá go deatnu
	## leahkkasa, čuolddaid čohket ja ráhkadit oar-julggiid.

REMOVE (V Sg2) IF (0 COPULAS)(*1 (Pl Nom) BARRIER NOT-ADV-PCLE);
	## Leatgo doppe olu gávppit?

REMOVE (V Sg2) IF (*-1 (Num Nom) BARRIER NPNH);
	## Gitta guhtta hearggi sáhttet leat ovtta ráiddus.


# Sg3 - Third person singular
# ---------------------------
 # Prs Sg3 = ConNeg (even)
 # Prs Sg3 = ConNeg = Imprt Sg2 (contracted)
 # Prs Sg3 = VGen
 # Prt Sg3 = Sg Ill for certain words!!
 # Prt Sg3 = Imprt Prs Sg3 (leai)
 # leat Prs Sg3 Foc = leat Prs Du3 (leaba) 

SELECT V-SG3 IF (*-1 (Sg Nom) BARRIER NOT-ADV-PCLE)
	(*1 (Neg Sg3) BARRIER VFIN LINK 1 ("ge"));
	## Rakel čierru mánáidis dihtii ii ge beroš jeđđehusas.

SELECT V-SG3 IF (*-1 (Sg Nom) BARRIER NOT-ADV-PCLE)
	(*1 (Neg Sg3 Foc) BARRIER VFIN);
	## Rakel čierru mánáidis dihtii iige beroš jeđđehusas.

REMOVE (V Sg3) IF (0 (Sg Nom))(*1C (V Sg3) BARRIER CLB OR CC OR CS OR CP OR go
	LINK NOT 1 ("go" Pcle));
	## Man uhccán osku dus lea!

SELECT V-SG3 IF (*-1 (Sg Nom) BARRIER NOT-ADV-PCLE)(NOT 0 Inf OR (N Pl Nom))
	(NOT 0 Ill LINK *-1 Gen BARRIER NOT-ADV-PCLE);
	## ...ja de olmmoš suohppu fávleoalggi nu guhkás vulos go...

SELECT V-SG3 IF (*-1 (Sg Nom) BARRIER Nom OR CC OR CLB OR CS OR CP)(1 CC);
	##

SELECT V-SG3 IF (*-1 (Pron Sg3 Nom) BARRIER NOT-ADV-PCLE)
	(NOT 0 Du3 LINK *-1 ("dat" Pl3 Nom) BARRIER NOT-ADV-PCLE) ;
	## Dat áigu leat ruovttus.

SELECT V-SG3 IF (*-1 Loc BARRIER NOT-ADV-PCLE)(*1 NP-HEAD-SG-NOM BARRIER NPNHA);
	## Dát leat sii geat sirrejit olbmuid; sii leat máilmmálaččat 
	## ja sis váilu Vuoigŋa.

SELECT V-SG3 IF (*-1 ("gii" Pron Interr Sg Loc) BARRIER CS OR CP); 
	## Sus geas ii leat, váldojuvvo velá dat ge eret mii sus lea.

SELECT V-SG3 IF (*-1 BOS OR CS OR (":" CLB) BARRIER Nom)(NOT 0 Adv)
	(*1 (Sg3 Nom) OR (Pron Sg Nom) BARRIER NOT-ADV-PCLE LINK NOT 0 Refl);
	## ...ahte vuoigatvuhtii automáhtalaččat gullá dat mii guorrana...

SELECT (V Sg3) IF (*-1 BOS OR CS OR (":" CLB) BARRIER Nom)(NOT 0 Adv OR Attr)
	(*1 (N Sg Nom) OR (A Sg Nom) BARRIER NPNHA);
	## Dán guovtti báhkkoma alde orru oppa láhka ja profehtat.

REMOVE VFIN IF (-1 COMMA OR CC LINK *-1 N BARRIER VFIN)(0 Comp)(1 (N Nom));

SELECT V-SG3 IF (-1 CLB)(NOT 0 Adv)(*1 (N Sg Nom) BARRIER NPNH);

	## Earáide fas, geat eai máhte sámegiela, orru vástádus eahpečielggas. 

SELECT V-SG3 IF (*-1 (N Sg Nom) BARRIER NOT-ADV-PCLE)(0 (N Sg Ill))
	(*1 Acc BARRIER NPNH);
	## Dalle isit gohčui bálvaleaddji fas lusas.

SELECT V-SG3 IF (NOT *-1 V-SG3)(*1 CC BARRIER V-SG3 LINK 1 V-SG3);
	## De gohčui sisa oskkolaččaid oktan leaskkaiguin ja čájehii sidjiide...

REMOVE Ill IF (NOT *-1 ("seammá") OR ("eará") BARRIER NPNH)(1 ("go" Pcle));
	## Gos lei riegádan, ja máhtii go dárogiela?

#REMOVE (V Prt Pl3) IF (NOT *-1 (Pl Nom) OR (Pl3 Nom) BARRIER NOT-ADV-PCLE)
#	(0 AUX)(0 (V Prs Sg3));
	## Giellabargguid ferte viidásetfievrridit.
# Removed this rule because it made "orro" Pass in this sentence:
    ## Dán guovllus orro sullii 20.000 olbmo duopmoáigodagas.    
# It looks like the rule is unnecessary.


# Infinitive and clausal subject
# - - - - - - - - - - - - - - - -
SELECT V-SG3 IF (NOT *-1 Nom BARRIER CS OR REALCLB)(NOT 0 AUX)
	(NOT 0 (Pl Nom) LINK *1 V-PL3 BARRIER NOT-ADV-PCLE)
	(NOT 0 INF-VERB LINK 0 (Ind Sg2))(*1 Inf BARRIER NOT-ADV-PCLE);
	## Eamiálbmotrievtti guovddášosiide gullá maiddái sihkkarastit...

SELECT V-SG3 IF (*1 ("ahte") BARRIER NOT-ADV-PCLE);
	## Dearvvašvuođabargiidlága § 26 olis čuovvu ahte...
 # Verbs with nominal or pronominal subjects are picked earlier.


# Rules that look backwards for a subject across a relative clause:
# - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
SELECT V-SG3 IF (-1 CLB LINK *-1 (Pron Interr) OR MO BARRIER CS LINK *-1 (N Sg Nom) 
	OR (Pron Sg3 Nom) BARRIER N OR VFIN LINK NOT *-1 V-SG3 BARRIER CS OR CP 
	OR CLB)(NOT 0 Qst);
	## Dat guhte ii čohkke muinna, biđge. 

SELECT V-SG3 IF (*-1 VFIN BARRIER Nom OR CS OR CP LINK *-1 (Pron Interr)
	BARRIER CS LINK *-1 (N Sg Nom) OR (Pron Sg3 Nom) BARRIER VFIN OR Nom 
	LINK NOT *-1 V-SG3 BARRIER CS OR CP)(NOT 0 Loc OR Ill OR A OR Adv OR Qst);
	## Son gii lea váldán Jesusa gitta doalvu su dál Kaifasa lusa.
	
# Rules that look backwards for a subject across a subordinate clause (CP boundary):
# - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - 

SELECT Sg2 IF (*-1 CS BARRIER NOT-ADV LINK *-1 Sg2 BARRIER Pl3 OR Pl1)(0 V);
    ## Áhččán, mun rámidan du, almmi ja eatnama Hearrá, danne go leat čiehkan 
    ## dán viisáin ja oahppan olbmuin, muhto leat almmustahttán dan uhcahaččaide.
    # The point is: if the reference in the matrix-sentence was missing, we would 
    # not get a 0 subject.
    
SELECT Pl1 IF (*-1 CS BARRIER NOT-ADV LINK *-1 Sg2 BARRIER Pl3 OR Sg2)(0 V);
SELECT Pl3 IF (*-1 CS BARRIER NOT-ADV LINK *-1 Sg2 BARRIER Sg2 OR Pl1)(0 V);
    ## Ihttin rámidat mu, go leat ipmirdán buot, maid lean dutnje dahkán.
    ## Ikte don rámidit mu, go buohkaide muitalit mu bargu birra. 


# Extension possibilities: Coordination
# Son oaidná du ja mu ovdal go boahtit... 


# Coordinated Sg3 verbs
# - - - - - - - - - - -
SELECT V-SG3 IF (*-1 CC BARRIER NOT-ADV-PCLE LINK *-1 V-SG3 BARRIER VFIN OR CC)
	(NOT 0 Adv OR A)(NOT 0 N LINK *-1 CC BARRIER NPNH LINK -1 N);
	## Jus divššohas roasmmohuvvá dahje oažžu duođalaš váttisvuođaid...

SELECT V-SG3 IF (*-2 V-SG3 BARRIER VFIN OR CC OR REALCLB)(-1 COMMA)(NOT 0 Inf);
	## Son manai su lusa, dollii su gihtii ja čuoččáldahttii su.

SELECT V-SG3 IF (NOT *-1 V-SG3 BARRIER CS OR CP OR PUNCT LINK NOT *-1 CP 
	BARRIER VFIN)(1 CC)(*2 V-SG3 BARRIER CS OR CP OR VFIN)
	(NOT 0 N LINK 1 CC LINK *1 N BARRIER NPNH);
	## ...mo daid beliid berre dahje galgá áimmahuššat.

SELECT V-SG3 IF (-1 CC LINK *-1 V-SG3 BARRIER CC OR CLB OR CS OR CP OR VFIN)
	(NOT *1 V-SG3 BARRIER CC OR CLB OR CP OR CS)(NOT 0 (A Attr) LINK 1 N);
	## Dalle váldá lievssa ja bálkesta dan bodnái ja suohppu gáddeoalggi ja 
	## reasta fávleoalggis mohki rádjái.
 

# leage ("leat" or "leahki" N Sg Gen)
# - - - - - - - - - - - - - - - - - -
REMOVE ("leahki" N Sg Gen) IF (0 ("leat" Prs Sg3 Foc))(*1 (Sg Nom) BARRIER NPNH);
	## ...ja leage stuorradiggi mii ferte mearrádusa dulkot ja atnit.


# leai (Imprt Prs Sg3 and Ind Prt Sg3)
# - - - - - - - - - - - - - - - - - - 
SELECT Ind IF (*-1 N OR Pron OR Num BARRIER NOT-ADV-PCLE)(0 LEAI);
	## Dain leai seammalágan dálvesiidaortnet.
	## Guovvamánu 1. b. leai áigemearri.


# Not (V Sg3)
# - - - - - -
REMOVE V-SG3 IF (*1 NP-HEAD-PL-NOM BARRIER NOT-ADV-PCLE);
	## De leaba spagát giddalagaid bajil.

REMOVE V-SG3 IF (*-1 VFIN BARRIER CS OR CP)(0 (Sg Ill))(NOT 1 ("go" Pcle));
	## Sámekommišuvnna evttohus lei boaris ja báidnon dálá oidnui.


# Du1 - First person dual 
# -----------------------
 # Prs Du1 = Prt Pl3 (even syllable verbs); also similar to Prs Sg3 of other verbs.

SELECT (V Du1) IF (*-1 CC BARRIER VFIN LINK *-1C (V Du1) BARRIER VFIN);
	## Moai bohte su lusa ja orrugohte su luhtte.

REMOVE (V Du1) IF (NOT *-1 MOAI)(NOT *1 MOAI);
	## De bohte ruđat.
	## Nieiddat jerre.


# Du2 - Second person dual
# ------------------------

# Rules for leahppi = ("leahppi" N Sg Nom)

SELECT Du2 IF (*-1 Du2)(0 ("leat" V Ind Prs Du2));
	## Manni muitalit maid leahppi oaidnán ja gullan.

SELECT Du2 IF (0 ("leat" V Ind Prs Du2))(*1 PrfPrc BARRIER NOT-ADV);
	## Gean nammii leahppi dahkan dan?


# Du3 - Third person dual
# -----------------------
 # Du3 = PrsPrc Clt = Imprt Prs Du2 Clt
 # Du3 = Ind Prs Sg3 Clt "leaba, ferteba leat"

 # Most Du3 rules moved to earlier cycle, in order to hit before Sg3 + Foc "leaba".

SELECT V-DU3 IF (*-1 (Pl Nom) OR ("dat" Pl3 Nom) BARRIER NOT-ADV-PCLE);
	## Ja duot eará oskuvieljat leaba searvegotti áirasat.

REMOVE (V Sg3 Foc) IF (-1 CS OR CLB LINK *-1 (V Du3) BARRIER VFIN)(0 (V Du3));
	## Spahkágeažit šaddaba ruossalagaid geaigut bajás, go leaba boalo-
	## huvvon oktii.

REMOVE (V Sg3 Foc) IF (*-1 (Pl Nom) BARRIER CC OR CLB OR CS OR CP OR (Sg Nom))
	(0 (V Du3));
	## Geažit guovtte dakkár čoavjevuolisgorrojuvvoba oktii.
	
SELECT Du3 IF (*-3 Prop LINK 1 ("guovttos") LINK 1 Com BARRIER NOT-ADV)(0 VFIN);
    ## Biera guovttos Márehiin leaba daid mánáid váhnemat.

 
# Pl1 - First person plural
# -------------------------
 # Pl1 = Inf for all stem classes
 # Pl1 = Inf = Pl3 = Prt Sg2 for odd syll
 # Pl1 = Inf = Pl3 = Sg2 = ConNeg (copula)

SELECT (V Pl1) IF (*-1 CC BARRIER NOT-ADV LINK *-1C (V Pl1) BARRIER VFIN OR Inf 
	OR CS);
	## Mii bidjat vuođđun luonddurievtti oainnuid ja vuordnut... 

SELECT V-PL1 IF (*-2C (V Pl1) BARRIER VFIN OR CS)(-1 Interr)(NOT -1 (Pl Nom));
	## Mii sárdnidit dan maid diehtit. 

SELECT (V Pl1) IF (*-2C (V Pl1) BARRIER VFIN OR CS)(-1 CS);
	## Eat ge vuoitte maidege jos borrat.

REMOVE (V Pl1) IF (NOT -2 (V Pl1))(-1 CC);
	## Eamiálbmogiid luondduriggodagat leat leamaš ja leat duoguštuvvon.

REMOVE (V Pl1) IF (*-1 (V Pl3) BARRIER CC OR CS OR CP);
	## Bártnit ledje mannán meahccái ozažit meahcce-elliid.

SELECT V-PL1 IF (*-1 Nom BARRIER NOT-ADV-PCLE OR CLB LINK *-1 CC BARRIER NPNH
	LINK -1 (Pers Sg1 Nom) OR (Pers Du1 Nom) OR (Pers Pl1 Nom));
	## Mun ja Biera váhnemat leat boahtán.
 # Pl1 if a first person pronoun is coordinated with something else.

REMOVE Inf IF (-1 ("go" CS))(0 (V Pl1))(*1 ("de" Adv) BARRIER VFIN);
	## ... , muhto go geahččát guovttegielalašvuođa máilmmi perspektiivvas, de ...


REMOVE Inf IF (-2 (@SPRED) LINK NOT 0 Comp)(-1 ("go" CS))(0 VFIN);
	## Sohkabealiskkadeapmi lea dehálaš gelbbolašvuohta go hállat dáseárvvu birra.


# Pl2 - Second person plural
# --------------------------
 # Imprt Pl2 = Ind Prt Sg2 (even syllable verbs)
 # Imprt Pl2 = Imprt Pl1 (verbs ending in -ut)

SELECT (V Pl2) IF (*-2C (V Pl2))(-1 CS);
	## Go mieđihehpet, de dadjet dušše: Juo!

SELECT (V Pl2) IF (-1 BOS OR PUNCT)(*1 Pl2 OR PxPl2);
	## Rohkadallet, de oažžubehtet!
	## Gáidet eret mu luhtte, dii verrošeaddjit! 
	## Bissot olisin, nugo almmálaš Áhččádet ge lea olis.

SELECT (V Pl2) IF (*-1 CC BARRIER NOT-ADV LINK *-1C (V Pl2) BARRIER VFIN OR CS);
	## Váldet ja borret!


# Pl3 - Third person plural
# -------------------------
 # Prs Pl3 = Prt Sg2 = Prs Pl1 = Inf (odd syll)   muitalit
 # Prs Pl3 = Prt Sg2 = Imprt Prs Pl2 (even syll)    vulget
 # Prs Pl3 = Prt Sg2                 (contracted) fertejit
 # Prt Pl3 = Prs Du1

# Preverbal subject
# - - - - - - - - - 

 # Single noun

SELECT V-PL3 IF (*-1 (Pl Nom) BARRIER NOT-ADV-PCLE);
	## Muhto dorskit gal leat varrasat.
	## Divššohasaid birra geat leat uminddegin dahkkon.
	## Eatnagat bohtet nuortan ja oarjin.
	## Geafit gal leat álo din luhtte.

SELECT V-PL3 IF (*-1C NP-HEAD-PL-NOM BARRIER PPRON-NOT-SII OR (N Sg Nom) OR CS)
	(NOT *-1 (V Pl3) BARRIER CS OR CP OR PUNCT);
	## Makkár vuoigatvuođat dain leat riikavuložin?
	
SELECT V-PL3 IF (*-1C (N Pl Nom) BARRIER MII-PERS OR MOAI OR Nom OR VFIN OR CLB OR CS);
	## Dárbbašlaš dieđut dakkár mediaid vuođul sáhttet čállojuvvot journálii. 


 # Pronoun

SELECT V-PL3 IF (*-1 (Pron Pl3 Nom) BARRIER MII-PERS OR MOAI OR CLB OR CS);
	## Dathan gal leat buorit guolit.

SELECT Pl3 IF (*-1 (Pron Interr Pl Nom) BARRIER CP OR CS OR VFIN);
	## Mu diehto-olbmát geat munnje leat muitalan guotteheami birra,...

SELECT Pl3 IF (*-1 (Pron Indef Pl Nom) BARRIER CP OR CS OR VFIN);
	## ...man muhtimat Norgga bajimuš oapmeriektečehpiin leat čállán. 


 # Numeral

SELECT V-PL3 IF (*-1 NUMERALS BARRIER CS OR REALCLB OR PPRON-NOT-SII OR (N Sg Nom)
	LINK 0 Nom LINK NOT *-1 (V Pl3) BARRIER CC OR CS);
	## Máŋga Davviálbmogiid fakultehta studeantta leat oasi oahpus gazzan... 

SELECT V-PL3 IF (*-1 (N Num) BARRIER NP-HEAD-NOM OR VFIN OR ConNeg OR CS OR CP 
	LINK 0 Nom);
	## Viđas sis leat juo gahččan. 


 # Pl3 verbs that follow relative clauses:

SELECT V-PL3 IF (*-2 VFIN BARRIER CS OR CP LINK *-1 (Pron Rel Pl) BARRIER CS OR 
	CP)(-1 COMMA)(NOT 0 Inf LINK *-2 Inf BARRIER VFIN);
	## Dieđut mat leat dárbbašlaččat, galget liikká addojuvvot.  

SELECT V-PL3 IF (-1 CLB LINK *-1 (Pron Rel) OR MO BARRIER CS LINK *-1 (N Pl Nom) 
	OR (Pron Pl3 Nom) BARRIER N OR VFIN LINK NOT *-1 V-PL3 BARRIER CS OR CP)
	(NOT 0 Qst);
	## Dát duovdagat gos jogat ja ádjagat, jávrrit ja eará čáhcečoahkut
	## leat, gullet daid duovdagiid eaiggádiidda.

SELECT V-PL3 IF (*-1 VFIN BARRIER Nom OR CS OR CP LINK *-1 (Pron Rel)
	BARRIER CS LINK *-1 (N Pl Nom) OR (Pron Pl3 Nom) BARRIER N OR VFIN 
	LINK NOT *-1 V-PL3 BARRIER CS OR CP)(NOT 0 Qst OR Adv);
	## Olbmot, geat oassálastet kulturdoaimmaide vásihit sosiála oassá-
	## lastima.

 # Pl3 verbs that follow appositions:
 
REMOVE (V Sg2) IF (*-1 NUMERALS LINK 0 (@SUBJ-QH) BARRIER VFIN LINK 1 (@NQ<) BARRIER ("don"));

REMOVE (V Pl1) IF (*-1 NUMERALS LINK 0 (@SUBJ-QH) BARRIER VFIN LINK 1 (@NQ<) BARRIER ("mii"));
    ## Guokte beroštumiorganisašuvnna, Kirkens Familievern (KF, Girku bearašsuodjalus) ja Offentlige Familievernkontorers Organisasjon (OFO, Almmolaš bearašsuodjaluskantuvrraid organisašuvdna) leat dán rádjai fuolahan doaimmain ja prošeavttain, maid ulbmil lea ovddidit fágalaš bearašsuodjalusa.

 # Coordinated subjects

SELECT (V Pl3) IF (*-1 Nom BARRIER NOT-ADV-PCLE LINK *-1 CC 
	BARRIER NOT-NP LINK -1 Nom);        
	## Gielda ja fylkkagielda galget plánet.

SELECT (V Pl3) IF (*-1 Nom BARRIER NOT-ADV-PCLE LINK *-1 CC 
	BARRIER NOT-NP LINK -1 Po LINK -1 Gen LINK *-1 Nom BARRIER NPNH);

SELECT (V Pl3) IF (*-1 Nom BARRIER NOT-ADV-PCLE LINK *-1 CC 
	BARRIER NOT-NP LINK -1 Po LINK -1 Gen LINK *-1 CC 
	BARRIER NPNH LINK *-1 Nom BARRIER NPNH);


 # Coordinated Pl3 verbs

SELECT (V Pl3) IF (*-1 CC BARRIER NOT-ADV-PCLE LINK *-1C (V Pl3) BARRIER VERB OR CS);
	## Journála dieđut divvojuvvojit dahje sihkkojuvvojit njuolggadusaid mielde.

# Subordinate null subjects

SELECT (V Pl3) IF (-1 CS LINK -1C (V Pl3)) ;
	## Sii moaráhuvve go gulle dan.

SELECT (V Prs Pl3) IF (-1 CS LINK *-1 (V Prs Pl3) LINK -1 (Pl Nom));
	## Njuolggadusat leat váikkuhan Romssa ja Finnmárkku gielddaide nu, ahte leat váldigoahtán ovdan ássiid.

SELECT (V Prs Pl3) IF (-1 CS LINK *-1 (V Prs Pl3) LINK -1 CC LINK *-1 (V Prs Pl3) LINK -1 (Pl Nom));
	## Ii leat vealttakeahttá doarvái ahte ovttaskas olbmot máhttet sámegiela dahje dovdet sámii kultuvrra, go leat ordnemin ovttadássásaš bálbalusfálaldaga sápmelaččaide.

SELECT (V Prs Pl1) IF (-1 CS LINK *-1C (V Prs Pl1));
	## Mii leat dahkan ná, ahte leat oastán ođđa girjji.

# Postverbal subject
# - - - - - - - - - -

 # Single subject

SELECT V-PL3 IF (*1 (Pl Nom) BARRIER NPNHA)(NOT 0 ("leat" Imprt Pl2))
	(NOT 0 Inf LINK -1 ("mo"));
	## Ja dis leat velá buot oaivvi vuoktačalmmit lohkkojuvvon.

SELECT Pl3 IF (NOT *-1 VFIN BARRIER CS OR CP)(0C VERB)(*1 NP-HEAD-PL-NOM 
	BARRIER CS OR CP LINK NOT 0 Refl OR Interr);
	## Olggosaddin sáhttá liikká dáhpáhuvvat jos gávdnojit dasa beaktilis sivat.

SELECT V-PL3 IF (NOT *-1 INF-VERB OR ("leat") BARRIER CS OR CP)
	(*1 (Num Nom) BARRIER VFIN OR Inf OR PrfPrc OR Nom OR CLB OR CS OR CP
	LINK NOT 0 OKTA);
	## Dán guovllus orro sullii 20.000 olbmo duopmoáigodagas.

SELECT V-PL3 IF (*-1 CLB OR CC OR CS OR BOS BARRIER NOT-ADV-PCLE)
	(NOT 0 ("leat" Imprt Pl2))
	(*1 (Pl Nom) BARRIER VFIN OR Nom OR CS OR CP OR CLB);
	## ...de bohte muhtun viisát nuortan Jerusalemii ja jerre:..

SELECT (V Pl3) IF (0 INF-VERB)(*1 NP-HEAD-PL-NOM BARRIER NOT-ADV-PCLE LINK
	*1 Inf BARRIER CS OR CP);
	## Jus lea dárbu, fertejit lassidieđut vižžojuvvot.

SELECT V-PL3 IF (*-1 CLB OR CC OR CS OR BOS BARRIER NOT-ADV-PCLE)
	(*1 (Pron Pl3 Nom) BARRIER VFIN OR Nom OR CS OR CP OR CLB);
	## Jos dat ii leat vejolaš, searvvadit dat oktii geainna váimmut
	## buoremusat lávggadit.

SELECT V-PL3 IF (NOT *-1 VFIN BARRIER CS OR CP OR CC)
	(NOT -1 (N Nom))(NOT -1 CC LINK *-1 Inf BARRIER VERB)
	(*1 (PxPl3) BARRIER VFIN OR CS OR CP);
	## ...ja dasto leat massán hálddašeami iežaset guovlluid badjel. 

SELECT V-PL3 IF (NOT *-1 Nom BARRIER CS OR CONTRA)
	(NOT 0 Inf LINK *-1 ("leat") BARRIER CLB OR CC OR CS OR CP)
	(*1 (Num Nom) BARRIER V-TRANS-ACT OR CP OR CS);
	## Muhto ovtta čázis sáhttet leat máŋga buođđosaji.


 # Coordinated subjects

SELECT V-PL3 IF (NOT *-1 VFIN OR ConNeg BARRIER NOT-ADV-PCLE)
	(*1 Nom BARRIER NPNHA LINK 1 CC LINK *1 Nom BARRIER NPNH); 
	## Dá leat mu eadni ja mu vielljat.

SELECT V-PL3 IF (NOT *-1 VFIN OR ConNeg BARRIER NOT-ADV-PCLE)
	(*1 Nom BARRIER NPNHA LINK 1 COMMA LINK *1 Nom BARRIER NPNH
	LINK 1 CC OR COMMA); 
	## Bivdoearrái gullet dorski, diksu ja sáidi.


# Coordinated Pl3 verbs
# - - - - - - - - - - -

SELECT V-PL3 IF (*-1 CC OR COMMA BARRIER NOT-ADV LINK *-1C V-PL3 BARRIER VFIN OR CS);
	## Rivttiid háhkama eavttut leat heivehuvvon dahje heivehuvvojit ain.
	
# Pl3/Sg3 verbs that follow locative "subjects"
# - - - - - - - - - - - - - - - - - - - - - - -

REMOVE V-1-2 IF (*-1 ORGANIZATION LINK 1 Po LINK 0 ("bealis") OR ("beale") BARRIER NOT-ADV)(0 OPRED-VERB LINK 0 V-3)(*1 Ess BARRIER NPNHA);
	## Departemeantta bealis atnet dehálažžan, ahte ovdánahttin dievasmahttá ja doarju sámi gelbbolašvuođa fylkkagielddaid sullasaš bálvalusain nugo gárrenávnnassuodjalusas, mánáidsuodjalusas ja bearašsuodjalusas.
# The rule with the V-1-2 gives the same outcome as the one with Sg2. Thus, the latter
# may be removed.


# Rules for a special infinitive construction
# ===========================================
 # I made a separate rule for each form of the interrogative pronoun, 
 # in case different conditions are needed.

SELECT Inf IF (NOT -2 Pers OR REALCLB OR BOS)(-1 ("mii" Interr Sg Acc) OR 
	("gii" Sg Acc) LINK NOT *-1 (V Pl1) BARRIER VFIN)(NOT 0 AUX);
	## Sii eai diehtán maid vástidit sutnje.

SELECT Inf IF (NOT -3 Pers OR REALCLB OR BOS)(-2 ("mii" Interr Sg Acc) OR 
	("gii" Sg Acc) LINK NOT *-1 (V Pl1) BARRIER VFIN)(-1 ADVLCASE)
	(NOT 0 AUX);
	## Ii ge mus leat maid sutnje guossohit.

SELECT Inf IF (NOT -2 Pers OR REALCLB OR BOS)(-2 ("mii" Interr Sg Gen) OR 
	("gii" Sg Gen))(-1 Po)(NOT 0 AUX);
	## Ii Alimusrievttis leat mihkkege, man ovddas bivdit ándagassii.

SELECT Inf IF (NOT -2 Pers OR REALCLB OR BOS)(-1 ("mii" Interr Ill) OR 
	("gii" Ill))(NOT 0 AUX);
	## Máhtes ii lean šat oktage geasa dorvvastit.

SELECT Inf IF (NOT -2 Pers OR REALCLB OR BOS)(-1 ("mii" Interr Loc) OR 
	("gii" Loc))(NOT 0 AUX);
	## Son attii geafivuođastis buot dan mii sus lei mas eallit.
	## Máret ii gávdnan ovttage geas jearrat.

SELECT Inf IF (NOT -2 Pers OR REALCLB OR BOS)(-1 ("mii" Interr Com) OR ("gii" Com))
	(NOT 0 AUX);
	## Sus ii lean mihkkege mainna maksit.
	## Lei oktasaš riektegiella mainna gulahallat.


# More finite verbs
# =================

SELECT Prs IF (NOT *-1 V-IND-FIN BARRIER CS)(0 LEAT)(NOT *1 V-IND-FIN)  ;
	## De leat máksán.

REMOVE Inf IF (-2 CLB) (-1 CS); 
	## Dušše, juos leat doarvái...

REMOVE Inf IF (*-1 (Pron Interr) BARRIER VFIN)(NOT 1C VFIN)
	(NOT -1 PUNCT LINK 2 PUNCT);
	## ...maid Norgga bealde gohčodit "sámi guovddášguovlun".

REMOVE Inf IF (-1 Inf) ; 
	## No hits any more?


# Passive
# =======
REMOVE Pass IF (*1C Acc BARRIER VERB OR CLB OR CC OR CS LINK NOT 0 Interr);
	## Son han ii rihkkon dušše sábbáha.

REMOVE Pass IF (*-1 Neg BARRIER VERB)(0 ACT-PRFP)(NOT *1 N BARRIER NPNH);
	## Olbmot eai orron čadnon dušše iežaset mearragáddái bivdit.
	
REMOVE Pass IF (*-1 (@SUBJ) BARRIER CS OR CLB)(0 PrfPrc);
    ## Soai leigga orron doppe muhtun áiggi.	
    
REMOVE Pass IF (*1 (@SUBJ) BARRIER CS OR CLB)(0 PrfPrc);
    ## Doppe lea orron dušše okta álbmot.
    
SELECT Pass IF (*-1 ("leat" Sg3) BARRIER V)(*-1 (@ADVL))(NOT *-1 (@SUBJ) BARRIER CS OR CLB)(NOT *1 (V Sg3) BARRIER V LINK NOT 0 Pass);
SELECT Pass IF (*-1 ("leat" Sg3) BARRIER V)(*-1 (@ADVL))(NOT *-1 (@SUBJ) BARRIER CS OR CLB)(NOT *1 (V Sg3) BARRIER V LINK NOT 0 Pass);
    ## Son fuobmái ahte doppe lei orron.   
    ## Maŋŋil go badjel 20 jagi lea orron máddin, de máhccá ruovttoluotta.    
    
# Non-finite verb forms#
# ===================== #

# ================== #
    CONSTRAINTS #23  #
# ================== #


# Infinitive
# ----------

REMOVE VFIN IF (*-1C VFIN BARRIER NOT-ADV-PCLE OR CLB LINK NOT *-1 CP BARRIER VFIN);
	## Journálaovddasvástádus mearkkaša maiddái árvvoštallat divvun-
	## gáibádusaid.
	## Ehpet ge gallen mu.

REMOVE VFIN IF (*-1 VFIN-NOT-AUX OR ConNeg OR PrfPrc BARRIER MII-PERS 
	OR SII OR CP OR CS OR CC OR CLB)(0 Inf)(NOT 0 LEAT);
	## Dát guoská bargguide ásahit mastera ja bachelora.
	## Universitehta oaidná deaŧalažžan doarjut doaimmaid mat...

REMOVE (N Pl Nom) IF (*-1 VFIN-NOT-AUX OR ConNeg OR PrfPrc 
	BARRIER MII-PERS OR SII OR CS OR CLB)(0 Inf)(NOT 0 LEAT);
	## Maid mearkkaša čuovvut Jesusa.
	## Dat láhčá vejolašvuođaid oassálastit eamiálbmotkonferánssain.

REMOVE (V Ind Prs ConNeg) IF (*-1 VFIN-NOT-AUX OR ConNeg OR PrfPrc
	BARRIER MII-PERS OR SII OR CS OR CLB)(0 Inf)(NOT 0 LEAT);
	## Eai leamaš váttisvuođat soahpat eaiggádiiguin.

SELECT Inf IF (*-1 Inf BARRIER VERB);
	## Dáhtut go munno rávkat dola álmmis vulos loahpahit sin?

SELECT Inf IF (*-2 ("leat") BARRIER VERB)(-1 Ess);
	## ...lea Sámedikkiin dáhpin geavahit... 

SELECT Inf IF (-1 ("leat"))(1 ("dego"));
	##

SELECT Inf IF (-1 ("mo"));
	## Dakkár doaimmat berrejit ráhkadit njuolggadusaid, mo gieđahallat
 	## journálaid.


# Infinitive after predicative
# - - - - - - - - - - - - - - -
 # There are two rules in the beginning, conflate when I see the effect.
 # The barrier may be too weak.

SELECT Inf IF (-1 A BARRIER VFIN OR CP LINK *-1 ("leat") OR ("šaddat") 
	BARRIER NOT-ADV)(NOT 0 (N Pl Nom));
	## Eará guovlluin lei vejolaš nannet dárogiela.

SELECT Inf IF (*-1 N BARRIER VFIN OR CS OR CP LINK *-1 ("leat") OR ("šaddat") 
	BARRIER NPNHAI)(NOT 0 (N Pl Nom));
        ## Jos leat jaska, de lea dárbu singuin gulahaddat.


# Infinitive in coordination
# - - - - - - - - - - - - - -

SELECT Inf IF (*-2 Inf BARRIER VERB)(-1 COMMA)(*1 CC BARRIER VERB LINK 1 Inf);
	## No longer needed?

SELECT Inf IF (*-2 Inf BARRIER VFIN OR CC OR CP)(-1 CC)(*1 Acc BARRIER NPNHAII 
	LINK 1 NP-BORDER);
	## Son lea vuolggahan mu gulahit friddjavuođa fáŋggaide ja oainnu
	## čalmmehemiide, friddjan dahkat sordojuvvon olbmuid ja sárdnidit
	## Hearrá árpmu jagi.

SELECT Inf IF (*-1 CC BARRIER NOT-ADV-PCLE LINK *-1 Inf BARRIER VERB); 
	## No longer needed?

SELECT Inf IF (1 CC)(2C Inf); 
	## Golbma nuora ledje mielde golmma vahkkosaš gieddebarggus Svalbárddas, 
	## man ulbmilin lei lohkat ja juogustit bohccuid, ja iskat ráppiid 
	## Adventdalenis.
	

# Present Participle
# ------------------
 # PrsPrc = Imprt Prs Du2 (even-syll)
 # PrsPrc = V+N+Actor, and often LEX-N.

 # An example: Njuolggadusat divššohasovddasvástideaddji doaktára birra.
 #				N Sg Gen OR V PrsPrc
 # Very difficult to come up with any general rules!


SELECT PrsPrc IF (-2 Attr)(NOT -2 Prop)(-1 COMMA OR CC)(1 N);
	## Čáppa ja fillejeaddji sániiguin...

REMOVE Nom IF (*-1 V-PL3 BARRIER NPNHA)(0 PrsPrc)(*1 N BARRIER NPNH);
	## ...mat rievdaduvvojit dahje sihkkojuvvojit journálaovddasvástideaddji 
	## redigerenbarggu oktavuođas.

SELECT PrsPrc IF (1 TIME);
	## Oaččut dan boahtti sotnabeaivái.

REMOVE PrsPrc IF (0 N)(1 COMMA)(2 NP-BORDER OR CC);
	## Eai dearvvaš olbmot dárbbaš dálkkasteddji, muhto buohccit.

REMOVE PrsPrc IF (0 N)(1 Indef);
	## Ii dus leat gávdni masage.


# Actio/Perfect Participle
# ------------------------
 # Actio = Prs Sg1 = PrfPrc (odd-syll verbs, contracted verbs)
 # Perfect Participle is also treated in "Disambiguating verbs - part 1".
 # The order of rules is of some importance here.
 # We do not SELECT Actio, we remove PrfPrc instead, since there might be a 
 # non-derived N reading alongside Actio. 

SELECT (V Actio Gen) IF (1 ("veara"));
	## Dát sátni lea vuostáiváldin veara.

SELECT PrfPrc IF (*-1 BOS OR CS OR (":" CLB) BARRIER NPNH)(NOT 1 COMMA LINK 1 (@CC))(*1 N BARRIER NPNH LINK *1 VFIN
	BARRIER NOT-ADV-PCLE);
    ## Sajáiduvvan fágadoahpagiid sáhttá dieđusge geavahit.

SELECT PrfPrc IF (*-1 PrfPrc BARRIER NOT-ADV-PCLE LINK NOT 0 Pass)
	(*1 N BARRIER NPNH OR Dem);
	## Čuoččuhuvvon boasttoáddejupmi lea šaddan cieggan vuoigatvuođa-
	## dilálašvuohta.

REMOVE PrfPrc IF (1C Po);
	## No longer needed?

REMOVE PrfPrc IF (1 CC)(2 Actio)(*3 PrfPrc BARRIER NOT-ADV LINK NOT 0 (Actio Acc));
	## Dušše dihto riddoguovlluin lea monnen ja uvjen leamaš stuorát mahtodagas.
 
SELECT PrfPrc IF (*-1 LEAT-FIN-NOT-IMP BARRIER PrfPrc OR Inf OR VFIN OR CS)
	(NOT 0 Ess)(NOT 0 Actio LINK -1 PrsPrc)
	(NOT 0 Actio LINK 1 COMMA OR CC LINK 1 Actio)
	(NOT *1 PrfPrc BARRIER CC OR CP OR CS);
	## Dasto lea Sámediggi ovttasráđiid Finnmárku ja Tromssa fylka-
	## manniiguin doallan fágaseminára.

REMOVE PrfPrc IF (*-1 ("leat" PrfPrc) BARRIER NPNH)(NOT -1 CC LINK -1 PrfPrc);
	## Doaibmaplána mávssoleamoš áŋgiruššansuorgi lea leamaš dási ja 
	## gelbbolašvuođa lokten.

SELECT PrfPrc IF (-1 CC LINK *-1 PrfPrc BARRIER VERB);
	## Boazodoallolágat leat dievasmahttán ja dahkan dárbbašmeahttun...

SELECT PrfPrc IF (1 CC)(2C PrfPrc)(3 CS OR EOS);
	## Dat boahtá dohko ja gávdná dan guorusin, suohpalastojuvvon ja
	## čiŋahuvvon.

REMOVE PrfPrc IF (*-1 Gen BARRIER NPNH LINK -1 BOS OR CS OR CLB);
	## Divššohasjournálaid geahččan dálkkasgeahččalemiid oktavuođas...

REMOVE PrfPrc IF (NOT *-1 VFIN)(NOT 0 Pass OR ("leat" PrfPrc))(NOT *1 VFIN BARRIER CP OR CS);
	## Miehtan nuoraid ovddas geain ii leat miehtangealbu.
	## Doppe leamaš sámiin stuora deaddu.
	# It should not take the latter one.

REMOVE PrfPrc IF (*-1 PrfPrc OR ConNeg BARRIER NPNHA LINK NOT 0 ("leat"));
	## Ii ávžžuhuvvo dieđuid geahččan sivas olbmuid geat čužžot 
	## divššohasa lahka.

REMOVE V-SG1 IF (*-1 VFIN BARRIER CLB OR CS OR CP);
	## Báikkálaš PPB-deaddočuokkiskantuvrrat jotket stáhta ruhtadan 
	## bálvalusaideaset.

SELECT Nom IF (0 Actio)(NOT 0 Ess LINK 0 (@ADVL))(1 V-SG3);
	## Doarjjaruđaid hálddašeapmi lea leamaš mávssolaš lávki Sámediggái 
	## dearvvašvuođa- ja sosiálasuorggi gelbbolašvuođa háhkamis.


# Coordinated actios
# - - - - - - - - - -

REMOVE PrfPrc IF (NOT *-1 ("leat") BARRIER VERB OR CS)(*1 Actio BARRIER NPNH
	LINK 1 CC);
	## Eiseválddiid oainnu mielde lea okta Sámedikki deaŧaleamos rollain 
	## bidjat eavttuid servodat hábmemis seailluhan, nannen ja ovddidan dihte 
	## sámi servodaga.

REMOVE PrfPrc IF (*-1 COMMA BARRIER NPNH LINK -1 Actio)(1 CC OR COMMA)
	(*2 Actio BARRIER NPNH);
	## Eiseválddiid oainnu mielde lea okta Sámedikki deaŧaleamos rollain 
	## bidjat eavttuid servodat hábmemis seailluhan, nannen ja ovddidan dihte 
	## sámi servodaga.

REMOVE VFIN IF (*-1 COMMA BARRIER NPNH LINK -1 Actio)(1 CC OR COMMA)
	(*2 Actio BARRIER NPNH);
	## Eiseválddiid oainnu mielde lea okta Sámedikki deaŧaleamos rollain 
	## bidjat eavttuid servodat hábmemis seailluhan, nannen ja ovddidan dihte 
	## sámi servodaga.

REMOVE PrfPrc IF (NOT -1 LEAT-FIN-NOT-IMP LINK *-1 CP BARRIER VFIN)
	(1 CLB LINK *1 N BARRIER NPNH LINK *1 CC BARRIER VFIN 
	LINK *1 N BARRIER NPNH LINK NOT 0 Loc);
	## Sámedikki hálddahusa nannen, dearvvašvuođa- ja eallindili-
	## iskkadeami ruhtadeapmi ja Sámi dearvvašvuođadutkanguovddáža 
	## doaimmaid viiddideapmi váldojit ovdan 2002 bušeahta oktavuođas.

REMOVE PrfPrc IF (1 CC)(2 Actio)(3 ("veara"));
	## ...buot dan mii lea ráhkistan ja gudnejahttin veara.


# Verbal versus nominal actio
# - - - - - - - - - - - - - -

SELECT (V Actio Loc) IF (*-1 ("ádjánit") OR ("beassat") OR ("biehttalit") OR 
	("dolkat") OR ("eastadit") OR ("geargat") OR ("gieldit") OR ("heaitit") OR 
	("hehttet") OR ("jaska") OR ("máššat") OR ("váibat") OR ("vajálduhttit") 
	BARRIER VERB);
	## Doaktárii dahje advokáhttii ii sáhte biehttalit dieđuid geahččamis.

SELECT (V Actio Ess) IF (*-2 ("leat") BARRIER NOT-ADV)(-1 ("mielde"));
	## Doaibmaplána lea bidjan buori vuođđu ja leamaš mielde rievdadeame
	## miellagottuid sámegiela geavaheami ektui.

SELECT (V Actio Ess) IF (*-2 V-TRANS-ACT BARRIER NPNH)(-1 Acc)(NOT 0 (N Ess));
	## Mii gulaimet soapmása čoalkkuheame uvssa.
 # If necessary, list the higher verbs that appear in this frame.

SELECT (V Actio Nom) IF (-1 NP-HEAD-GEN)(1 N)(2 NP-BORDER OR Dem)(NOT 2 Po);
	## Nieida ii čájehan geasage irggi čállin reivve.

 # In the so-called agens constructions, like "irggi čállin reive", we probably
 # have the verbal actio. However, unless we have a tag for short actios, it is 
 # not possible to write rules that distinguish between this construction, where 
 # the actio is always shortened, from constructions with the long nominative form, 
 # such as "sámegiela geavaheapmi journálain". Now both types get the analysis 
 # V Actio Nom. 


SELECT (V Actio Gen) IF (1 ("dihte" Po) OR ("dihtii"))(*2 Acc BARRIER NPNH);
	## Mun dál boađán du lusa čállin dihte dan reivve.

REMOVE (Actio Pl Loc) IF (0 (Actio Sg Com));
	## In vel čilgemiinge ipmirdan.
	# Since this rule comes after the Com/Loc rules, we still get locative where the head demands it. There is a couple of competition-activities that can appear as plural events. Still until now, there are not so many problems since a lot of them are lexicalized.

REMOVE (V Actio) IF (0 (N Actio));
	## Dáid plánaid ollašuhttin gáibida lassi doarjagiid.

 # This rule will work fine when all exceptions have been taken care of by the
 # rules above.


# Imperative II
# -------------


# Verbgenitive
# ------------

REMOVE VGen IF (NOT *-1 VFIN BARRIER NOT-ADV-PCLE)(0 VFIN)(*1 N BARRIER NOT-ADV);
	## Ja de čugge goaraha bodnái.

REMOVE VGen IF (*-1 CS OR CP OR BOS BARRIER VFIN)(0 VFIN)
	( *1 CS OR CP OR EOS BARRIER VFIN);
	## Báldáhan ferte diŋgot, jos galgá fidnet.

 # This rule assumes that VGen comes parasitically upon a finite verb,
 # and removes VGen if this is not the case. More rules are needed in order
 # to remove more cases.

REMOVE VGen IF (-1 Attr);
	## Son geas ledje leamaš buot buhtismeahttun vuoiŋŋat,...

REMOVE VGen IF (0 Num)(*1 (N Gen) BARRIER NPNH);
	## Son lei sutnje velggolaš čuođi denara.

SELECT VGen IF (-2 VGen)(-1 CC);
	## Olbmot bohte sihke vuoji ja vácci.
 
# Derived vs. lexicalized noun
# ----------------------------

# Is this a real distinction or just a constructed one?
# As there is only one form, it seems that Sámi does not make a distinction.
# This is a set of rules, choosing carefully one form, only if both alternatives,
# which should be the only ones, have the same case.
# One might be able to generalize further later.

REMOVE (V*) IF (0C (N Sg Nom) OR (TV N Sg Nom));
    
    
REMOVE (V*) IF (0C (N Sg Gen) OR (TV N Sg Gen));    
    ## Ráđđehusa mielas báikkálaš iešstivrejupmi ja sámi geavaheaddjiid ovdanbuktin dárbbut sáhttet mannat ruossalassii .
    
REMOVE (V*) IF (0C (N Sg Ill) OR (TV N Sg Ill));
    ## Sámi kultuvrra galgá dahkat čalmmusin seammás go sápmelaččat ieža besset nannoseappot váikkuhit bálvalusaid ja eastadandoaimmaid hábmemii.   

REMOVE (V*) IF (0C (N Sg Acc) OR (TV N Sg Acc)); 
    ## Dakkár sadji livččii hui ávkkálaš , mii dovddašii buot sámegielat dearvvašvuođa- ja sosiálafágalaš diehtojuohkima ja fágagirjjálašvuođa , mii lea gávdnamis , ja mii maiddái sáhtášii muitalit , geas lea fágagelbbolašvuohta guđege bálvalusain .

REMOVE (V*) IF (0C (N Sg Ess) OR (TV N Sg Ess));   

REMOVE (V*) IF (0C (N Sg Com) OR (TV N Sg Com));

REMOVE (V*) IF (0C (N Sg Loc) OR (TV N Sg Loc));
    ## Ráđđehusas lea ovddasvástádus dakkár ruossalasvuođaid čoavdimis.        

# Aktio
# - - -

REMOVE (V*) IF (0C (N Sg Nom) OR (TV Actio Nom));
    # Ášši 10/00 Ráđđehusa dearvvasvuođa- ja sosiálabálvalusaid doaibmaplána sámi álbmogii Norggas - gulaskuddan.
    
    
REMOVE (V*) IF (0C (N Sg Gen) OR (TV Actio Gen));    
    ## 
    
REMOVE (V*) IF (0C (N Sg Ill) OR (TV Actio Ill));
    ##    

REMOVE (V*) IF (0C (N Sg Acc) OR (TV Actio Acc)); 
    ##

REMOVE (V*) IF (0C (N Sg Ess) OR (TV Actio Ess));   

REMOVE (V*) IF (0C (N Sg Com) OR (TV Actio Com));

REMOVE (V*) IF (0C (N Sg Loc) OR (TV Actio Loc));
    ##    
 

# --------------------------------------------------------------------
# Gerund, Verbabessive and Supine have unique forms and need no rules.
# --------------------------------------------------------------------


# Selecting some more finite verbs
# ================================

SELECT VFIN IF (NOT *-1 VFIN BARRIER CS OR CP)(NOT -1 BOS LINK 2 EOS)
	(NOT -1 ("mo"))(NOT 0 (Pass PrfPrc) OR N)(NOT 0 (A Attr) LINK 1 N)
	(NOT 0 Inf LINK *-1 BOS BARRIER NOT-NP)(NOT 1 ("§"))
	(NOT *1 VFIN BARRIER CS);
	## Dát máksá ahte buot universitehta oktavuođat sámegillii mannet Sámi 
	## dutkamiid guovddáža giellakonsuleantta bokte.
	## Gos don dál boađát? Siiddas.

 # BOS/EOS is there to allow for fragments like the "Siiddas" example.


SELECT VFIN IF (*-1 CP OR CS BARRIER VFIN)(NOT 1 (Pron Interr))
	(NOT *1 VERB BARRIER CS OR CP);
	## Son lea dan sápmelaš gean dovddan.

 # This rule finds the finite verbs in embedded finite clauses. 

SELECT VFIN IF (-2 CLB)(-1 ("nu"))(NOT *1 VERB BARRIER CS OR CP);
	## Nu go son lea bassi, nu šaddos bassin oppa din eallin ge.

SELECT VFIN IF (0 INF-VERB)(1 ("go" Pcle))(*2 Inf BARRIER VERB);
	## Lea go son vuolgimin min álbmotguimmiid lusa geat ásset 
	## greikalaččaid gaskkas, ja áigu go sárdnidit greikalaččaide ge?

# Present or Past
# ---------------

# Tense congruence within the same clause:

# Present tense in subordinate clause if present in matrix clause
REMOVE Prt IF (*-1 CS BARRIER Prt OR (N Sg Nom) LINK *-1C Prs BARRIER Prt);
	## Mánáidgárdi lea dehálaš báiki gos sáhttet kártet mánáid giellaovdáneami.

# Past tense in subordinate clause if past in matrix clause
REMOVE Prs IF (*-1 CS BARRIER Prs OR (N Sg Nom) LINK *-1C Prt BARRIER Prs);
	## Mánáidgárdi lei dehálaš báiki gos sáhttet kártet mánáid giellaovdáneami.

# ------------------------------- # 
# Lexical disambiguation of verbs #
# ------------------------------- #

# bearrat vs. berret
# ------------------
# bearrat = 'inherit; demand' etc., berret 'ought'

REMOVE ("bearrat") IF (0 ("berret"))(*1 Inf BARRIER VERB OR CS);
	## Válljedettiin journálaovddasvástideaddji berre árvvoštallat su
	## fágagealbbu.


# beassat vs. besset
# ------------------
# beassat = 'get out of; get into' etc., besset 'strip off bark'

REMOVE ("besset") IF (0 ("beassat"))(*1 (V Actio Loc) BARRIER VERB OR CS);
	## Mun bessen vuolgimis.


# boahtit vs. bohtat 
# ------------------
# boahtit 'come', bohtat 'swell'

# These verbs have 5 homonym forms, Prs Du1/Pl3 bohte, Prt Sg1 bohten,
# Prs Pl3/Prt Sg2 bohtet, Imprt Sg1 bohton, Imprt Pl1 bohtot
# In almost all cases, the correct choice will be "boahtit", the 
# challenge is to avoid removing the cases where it might be "bohtat"
# TODO: Have native speakers make good 'bohtat' sentences.
# This rule tries to disambiguate lexically, it is thus a new type of
# rule, whether it is a good idea remains to see.  

REMOVE ("bohtat" Sg1) IF (0 ("boahtit")); # Will hit 'bohten' and 'bohton'.
REMOVE ("bohtat" Sg2) IF (0 ("boahtit")); # Will hit 'bohtet'.
	## Go mun bohten du vissui,...
	## Journálat álmmolaš doaimmas bohtet arkiivalága vuollái.

REMOVE ("bohtat" Du1) IF (0 ("boahtit")); # Will hit 'bohte'.
REMOVE ("bohtat" Pl1) IF (0 ("boahtit")); # Will hit 'bohtot'.
	## Moai bohte su lusa ja orrugohte su luhtte.
	## 
# With a personal subject, we go for "boahtit"

REMOVE ("bohtat") IF (0 ("boahtit"))(*1 ADVLCASE BARRIER CS) ;
REMOVE ("bohtat") IF (0 ("boahtit"))(*-1 ADVLCASE BARRIER CC OR CS OR CP);
	## Bohtetgo ruđat munnje?
	## Dasgo váimmus bohtet bahás jurdagat.
# When it is coming to or from, go for "boahtit"
# These last two rules may be riskier than the first.


# caggat vs.caggát
# ----------------
# caggat = 'tighten; keep open; hold back', caggát = 'be wide open'

REMOVE ("caggát") IF (0 ("caggat"))(*1 NP-HEAD-ACC BARRIER NPNHA);
	## Olmmoš caggá čuolu rávdnjái fávlli guvlui.


# čuohcat vs. čuohcit
# -------------------
# Prs Sg3 čuohcá.


# dadjat vs. daddjat
# ------------------
# daddjat is !SUB
# We remove the possible sáhtit-infinitiv from the lex instead of making this rule.
# REMOVE ("daddjat") IF (0 ("dadjat")) ;


# duđđat vs. duhtat
# -----------------
# duđđat = 'mess', duhtat 'be satisfied'

REMOVE ("duđđat") IF (0 ("duhtat"))(*1 Ill BARRIER NPNHA);
 	## Mun duđan dien vástádussii.

REMOVE ("duđđat") IF (0 ("duhtat"))(1 go)(*2 Ill BARRIER NPNHA);
	## Duđat go mu čállimii?


# diehtit vs. dihttot
# -------------------
# diehtit is TV and takes an animate subject.
# dihttot (vise seg, forekomme) is Passive and intransitive

SELECT ("dihttot") IF (*-1 ("sátni") OR ("vearba") OR ("substantiiva") LINK 0 (@SUBJ));
	## Sátni dihtto buot oarjesámegielain.
	# Needed here is a set of speach act and POS words.


# dovdat vs. dovdát
# -----------------

#REMOVE ("dovdat") IF (-2 ("go"))(-1 ("vuos"))(0 ("dovdát" Sg3));
    ## Go vuos dovdá ovtta unnitlohkoálbmoga, lea álki fuobmát oahpes
    ## minstariid maiddái eará unnitloguid diliin.
    # ii. dat ii doaimma ok, leago dat buoret? lea go MII buoret? MII? ahh..ii leat buoret. dat ferte leat "dovdat", muhto sahttasii maid leat "dovdát", muhto goitge "dovdat" orru leahkime buoret: 1.Hvis man først blir kjent med x, så er det lettere, 2. hvis man først kjenner et x, så er det lettere, 1. doaibmá maid
    #maid doai jáhkabeahtti?
    # datgo lei evttohus?
    # vurddes:
    # L: doaibmágo du mielas dadjat "go vuos dovdá" - dovdát - bli kjent med??
    # D: JEPP
    # L: muhto ii "go vuos dovdá" - dovdát - kjenne igjen ?
    # D: DAT NAI
    # (Duomma)


# eallit vs. eallát
# -----------------
# These verbs have 2 homonym forms: Ind Prs Sg3 eallá, PrfPrc eallán
# eallát = 'to recover, become alive again', eallit = 'to live'

REMOVE ("eallát") IF (0 ("eallit"))(*1 ("eallin")) ;
	## Son eallá suttolaš eallima.

REMOVE ("eallát") IF (0 ("eallit"))(*1 Loc OR ("siste") OR (VAbess)) ;
        ## Mun dovddan muhtun olbmo guhte eallá Kristusis. 
        ## Son eallá naitalkeahttá.


# gieldit vs. gildit 
# ------------------
# gieldit = 'deny sb sth', gildit = 'shine'
# Here, we have a transitivity difference.
# Homonym in the diphtong simplification cases: Prs Du1 gilde, Prs Pl3 gildet
# all Prt forms, gilden, gildet, ..., all Pot forms gilddežan, ..., 
# some of the Imprt forms.  

REMOVE ("gildit") IF (0 ("gieldit"))(*1 Acc BARRIER VFIN) ;
        ## Jesus gilddii sudno garrasit:
        ## Muhto mađe eanet son gilddii, dađe eanet sii almmuhedje dan.
#  The second example is ambiguous.

SELECT ("gieldit") IF (*1 (V Actio Loc) BARRIER VERB OR CS OR CP);
	## Mun gilden Máhte boahtimis.


# goaddat vs. goddit 
# ------------------ 
# goaddat = 'stick to, tarrautua', goddit = 'kill'
# Homonym in the diphtong simplification cases.

REMOVE ("goaddat") IF (0 ("goddit"))(*1 Acc BARRIER VFIN) ;
        ## Ja sii goddet su.


# goddet vs. goddit
# -----------------
# goddet = 'catch wild reindeer', goddit = kill

REMOVE ("goddet") IF (0 ("goddit"))(*1C (@OBJ) BARRIER NPNHA);
	## Vuovdái godde dán guovžža.


# gođđat vs. gođđit 
# ----------------- 
# gođđat = 'spawn', gođđit = 'knit, weave'

REMOVE ("gođđat") IF (0 ("gođđit"))(*1 Acc BARRIER VFIN);
	## Ovddemus dáida oastit árppuid ja gođđá firpmiid.


# guođđit vs. guđđat
# ------------------
# guođđit = 'leave sth swh', guđđat = 'swarm' 

REMOVE ("guđđat") IF (0 ("guođđit"))(*1 Acc BARRIER VFIN) ;
        ## De sii guđđet dan viesu ja gávpoga.


# gullat vs. gullát vs. gullet
# ----------------------------
# gullat = 'hear; belong', gullát 'begin to hear', gullet 'fish'

 # Two rules that pick out 'gullat' in the 'belong' sense:

SELECT ("gullat") IF (0 ("gullát") OR ("gullet"))(*1 Ill BARRIER VERB);
	## Dat gullá lunddolaččat dakkár áššiide go Sámedikkiid válganjuolggadusaide.

SELECT ("gullat") IF (*-1 Ill BARRIER NOT-ADV-PCLE)(0 ("gullát") OR ("gullet"));
	## Doaimma divššohasjournálaide gullá persovdnaregisttar.
	## Eanetlohku, masa gulle Riektejoavkku 6 eará lahtu,...

REMOVE ("gullet") IF (*1 ("ahte") BARRIER NOT-ADV-PCLE);
	## Mun gullen, ahte vulget ikte.


# gárrat vs. gárrut 
# ----------------- 
# gárrat = 'bind sth to sth', gárrut = 'get sth done, +Inf'

REMOVE ("gárrat") IF (0 ("gárrut"))(*1 Inf BARRIER NOT-ADV-PCLE-NEG) ;
        ## Mii lávlluimet morašlávlagiid, ehpet ge dii gárron luoibmat.


# máhccat vs. máhccut 
# ------------------- #
# máhccat = 'return, retreat', máhccut = 'make folds in'


# orrut vs. orrot
# ---------------- #
# orrot = 'remain somewhere (not come back as expected), settle down'

SELECT ("orrot") IF (0 ("orrot"))(*-1 Ill BARRIER V OR CC OR CS OR Adv OR Po OR Pr OR Loc) ;
SELECT ("orrot") IF (0 ("orrot"))(*1 Ill BARRIER V OR CC OR CS OR Adv OR Po OR Pr OR Loc) ;
REMOVE ("orrut" Pass) IF (0 ("orrut"))(1 ("go")) ;
REMOVE ("orrut" Pass) IF (0 ("orrut"))(*1 (Actio Ess) BARRIER V) ;
REMOVE ("orrot") IF (0 ("orrut"))(NOT 1 ("go"))(NOT *1 (Actio Ess) BARRIER V)(NOT *-1 Ill BARRIER V OR CC OR CS OR Adv OR Po OR Pr OR Loc) ;
REMOVE ("orrot") IF (0 ("orrut"))(NOT 1 ("go"))(NOT *1 (Actio Ess) BARRIER V)(NOT *1 Ill BARRIER V OR CC OR CS OR Adv OR Po OR Pr OR Loc) ;
    ## Mii áigut dohko orrot idjii.
    ## Buot dilálašvuođat main Ipmil orro leamen jávkosis.
    ## Min Áigi jearrá orro go heivvolaš ahte son čállá bartnis birra.

# rihkkut vs. riehkkat
# ------------------- #
# rihkkut = 'break, transgress, infringe, offens against, forfeit', riehkkat = 'work hard, make great efforts to..'

SELECT ("rihkkut") IF (0 ("rihkkut" Pass))(*-1 BOS LINK *1C LAHKA OR LAHKA-NOUN) ;
    ## Láhka lea rihkkojuvvon.

	
# sáhttit vs. sáhtit
# ------------------ #
# sáhtit is !SUB 
# We remove the possible sáhtit-infinitiv from the lex instead of making this rule.
# REMOVE ("sáhtit") IF (0 ("sáhttit")) ;


# sihtat vs. sihtit
# -----------------
# sihtat = 'want +Acc/+Inf', sihtit = 'luoda karvansa, puhdistua (oravan-
# nahasta syystalvella'
# This will in almost all cases mean 'sihtat'.


# vuohttit vs. vuohttát
# ---------------------
# vuohttá is sg3 of both these verbs





# ===================== #
#    Residual cases
# ===================== #
       CONSTRAINTS #24  #
# ===================== #


# Case rules
# ==========

REMOVE Nom IF (*-1 Inf BARRIER NPNH)(*1 CC OR CS OR CP OR EOS BARRIER WORD
	LINK 1 NP-BORDER);
	## Gonagas sáhttá viidáseappot mearridit dárkilet mearrádusaid mo geavahit 
	## elektrovnnalaš journála, ja das maiddái bidjat gáibádusaid oahpahussii.

SELECT Nom IF (-1 ("okta" Nom))(0 ("eará"));
	## Ii oktage eará go báhpat oaččo borrat.

SELECT Acc IF (*-1 (V Actio Loc) BARRIER NPNHA)(NOT *1 N BARRIER NPNH);
	## Álbmotčeardan geaid eallindilli hehttii sin ráhkadeames 
	## makkárge sierra stáhta.

SELECT Gen IF (0C Dem)(1 TIME)(1C Gen);
	## Maid Biret-Elle áigu dán dálvve dahkat? 

SELECT Gen IF (-1C NP-HEAD-ACC)(0 NP-HEAD-GEN)(*1 N BARRIER NPNH OR PUNCT 
	LINK 0 ADVLCASE);
	## Mii leat gullan dan su iežas njálmmis.

SELECT Gen IF (-1 Po)(*1 N BARRIER NPNH LINK 0 ADVLCASE)(NOT 0 N LINK 1 Indef);
	## Mii gullat sin sárdnumin Ipmila stuora daguid birra min iežamet gillii.

REMOVE Acc IF (-1C (Pron Interr Acc));
	## Mii hálidit muđui váldit ovdan ahte mii geat leat bargan prošeavttain 
	## leat deattuhan bidjat fuomášumi dasa maid min iežamet sajádagat 
	## mearkkašit guorahallanfáttá ektui.

REMOVE (Dem Acc) IF (*1C (N Gen) BARRIER NPNH LINK 0 HNOUN);
	## Fertejitgo máksit daid vuoivasiid ovddas?

#SELECT Gen IF (NOT *-1 V-TRANS-ACT BARRIER CC OR CLB OR CS OR CP)
#	(NOT -1 Loc)(0 Num)(*1 (N Sg Gen) BARRIER NPNH LINK 1 N);
SELECT Gen IF (NOT *-1 TV BARRIER CC OR CLB OR CS OR CP)
	(NOT -1 Loc OR ("§") OR ("b") OR ("nr"))(0 Num)(*1 (N Sg Gen) BARRIER NPNH LINK 1 N);
	## ..., ahte unnimus 135 jagi geavahus lea doarvái.


# Going for the right dainna

REMOVE (Pl Loc) IF (0 ("diet" Com) OR ("dat" Com) OR ("dát" Com) OR ("gii" Com) OR ("mii" Com));
	## Jos dat ii leat vejolaš searvvadit dat oktii geainna váimmut buoremusat lávggadit.
    # While Nickel does not allow those forms to be locative at all, Nielsen does so.
    
REMOVE (Pl Loc) IF (*-1 ("diet" Sg Com) OR ("dat" Sg Com) OR ("dát" Sg Com) OR ("gii" Com) OR ("mii" Com) BARRIER NPNH); 


# Other rules for nouns and pronouns
# ==================================

SELECT (N Sg Nom) IF (*-1C V-SG3 BARRIER NPNHA OR PrfPrc LINK NOT *-1 (Sg Nom) 
	BARRIER CS)(NOT 0 Acc LINK *-1 V-TRANS-ACT BARRIER NPNHA)
	(NOT 0 Gen LINK *1 N BARRIER NPNH);
	#(NOT 0 Loc LINK *1 (Sg Nom) BARRIER NPNHA);
	## Nuppe dáfus lea sámi nissoniid eallin rievdán. 

REMOVE NP-HEAD-PL-NOM IF (0 (Inf) OR NP-HEAD-SG-NOM)(1C V-SG3);
	## Dát lei deháleamos ágga.

SELECT (Interr Sg) IF (-1 CC LINK *-1 VFIN LINK *-1 (Interr Sg) BARRIER VFIN);
	## ...maid addet dearvvašvuođaásahusain dahje maid almmolaš
	## dohkkehuvvon dearvvašvuođbargit addet.
 # Coordinated relative clauses! Also write a rule for Pl?

SELECT Pers IF (-1 ACC-ILL-VERB)(0 Acc)(1 Ill);
	## ..gii dasto attii dan eadnásis.

REMOVE (Interr Pl) IF (-2 Sg3 OR Sg)(-1 Po)(0 (Interr Sg));
	## Dan vuođul maid Tønnesen lea ovdandoallan...


# Px rules
# ========

SELECT (PxSg2) IF (-1 Inf LINK 0 V-TRANS-ACT)
	(*-2 (Imprt Sg2) BARRIER NOT-ADV-PCLE);
	## ...muhto boađe bidjat gieđat su ala,...

REMOVE (PxSg2);
	## ...ahte báhpat juohke sábbáha barget tempelis...
	## Dát máksá ahte buot universitehta oktavuođat sámegillii mannet 
	## Sámi dutkamiid guovddáža giellakonsuleantta bokte.


REMOVE (Acc PxSg3) IF (*-1 V-NOT-AUX BARRIER VERB OR CC OR CLB OR CS OR CP 
	LINK NOT 0 V-TRANS-ACT);
	## ...de Hearrá eŋgel almmustuvai sutnje niegus ja celkkii:...

REMOVE (PxSg3) IF (*-1 Pers BARRIER CS OR CP OR CLB LINK NOT 0 Sg3 LINK -1 Pron);
        ## Goabbá munnos viehká čázis?

REMOVE (PxSg3) IF (0 Loc)(*1 CC BARRIER WORD LINK *1C Loc BARRIER NPNH);
	## Olmmoš ii eale dušše láibbis, muhto juohke sánis mii boahtá...

REMOVE (PxSg3) IF (*-1 Gen BARRIER VFIN OR Adv OR Nom) ;
        ## Piera borrá áhči lanjas.
        ## Piera borrá leaibbis.
        #  Not Px if there is a Genitive possessor in the same NP
        
REMOVE (PxSg3) IF (NOT *1 (V Sg3) BARRIER VFIN)(NOT *-1 (V Sg3) BARRIER VFIN);
    ## Divššohasas lea vuoigatvuohta oažžut dearvvašvuođadilis árvvoštallojuvvot. # It shouldn`t take this example.


# Determiners
# ===========

REMOVE Sg IF (0 DEM-PL)(NOT *1 V-SG3);
	## Dákkárat dat dahket olbmo buhtismeahttumin.

SELECT Pl IF (0 (Dem Nom))(*1 V-DU3 BARRIER VFIN OR CS OR CP);
	## Ja duot eará oskuvieljat leaba searvegotti áirasat Kristusa gudnin.

REMOVE Pl IF (0 DEM-SG)(NOT *1 V-PL3);
	## Gahččan, gahččan lea stuora Babylon, duot fuorrá.

SELECT Dem IF (1 ("bodda"));
	## Mun finan gávppašeame dan botta.




# Adverbs and adjectives
# ======================

SELECT Adv IF (-1C N)(0 A)(1C Inf);
    ## Dáidet ráđđehearrát duođaid dohkkehit ahte son lea Messias?

 # The idea is that we between a noun an an infinitive will have an adverb, 
 # and not an adjective.


REMOVE Adv IF (-1 (Dem Nom));
	## Buot dát bahát bohtet siskkildasas ja dahket olbmo buhtismeahttumin.

REMOVE (A Nom) IF (-1 Nom)(1 VFIN);
	## Máŋgga fágasuorggi siskkobealde lea dárbbašlaš erenoamážit
	## vuoruhit sámi/eamiálbmotguoskevaš dutkamiid.

REMOVE (Pl Nom) IF (0 Adv)(1 V-SG3);
	## Vaikko dákkár vuoigatvuohta nappo vánit oažžu buorre buhtadan-
	## suodjaleami,...

REMOVE (A Attr) IF (1C V OR EOS OR ("nr"))(NOT 1 N OR PrfPrc);
	## Muhto son gilddii sin garrasit muitaleames geasage iežas birra.
  # When all rules are executed, remove the Attrs.
  # The double requirement is there because of the compounds.




# =============================== #
# Global disambiguation - cycle 3 #
# =============================== #
              CONSTRAINTS #25     #
# =============================== #

# This is the section for rules of more global scope.
# True global rules, stretching across the clause boundaries, are found in another
# file, sme-tdis.rle. Usage: The two cg files shall be applied one after the other:
# preprocess --abbr=bin/abbr.txt | lookup -flags mbTT -utf8 bin/sme.fst
# | vislcg --grammar=src/sme-dis.rle | vislcg --grammar/src/sme-tdis.rle --minimal
# Note that only the final round can have the flag --minimal.

# The rules found here are thus only late sentenceinternal rules.

# Nouns
# -----

 # These rules need to come late, allowing Actor and Actio readings to be 
 # picked out by earlier rules.

REMOVE (N Actor) IF (0 LEX-N); # The N reading should remain.
	## Mihttomearrin lea 5 jagi badjel buvttadit 2 sámegiel álmmuheami jáhkái.

REMOVE Actio IF (0 N)(NOT 0 Ess OR PrfPrc OR VFIN OR A); 
	## Journála sirdin, doaimma loahpaheapmi jna. # Same here.
 # NOT 0 A because e. g. "eavttuheapmi" is Actio or A. Not always correct to remove Actio.


# Verbs
# -----

REMOVE V-DER; # Removes only if there is another reading. 
	## Mánáin lea vuoigatvuohta oažžut dearvvašvuođaiskkadeami.
	## Dat ihte johtilit.

REMOVE (IV Pass) IF (*-1 (N Nom @SUBJ) OR (Num Nom) BARRIER VFIN)(0 (TV Pass)); 
REMOVE (IV Pass) IF (*1  (N Nom @SUBJ) OR (Num Nom) BARRIER VFIN)(0 (TV Pass)); 
	
REMOVE TV IF (0 IV)(NOT 0 Pass)(NOT *-1 Acc LINK *-1 TV)(NOT *-1 TV LINK *-1 Acc)
                   (NOT  *1 Acc LINK  *1 TV)(NOT  *1 TV LINK  *1 Acc); 
    ## Mannan dálvvi hárjánin čuoigat.               

REMOVE Inf IF (*-1 BOS BARRIER VFIN)(0 VFIN)(*1 EOS BARRIER VFIN);
	## Muhto baice geahččat ovddas guvlui.

# Genitive after preposition -- round 2
# - - - - - - - - - - - - - - - - - - - 

# Those rules (exactly the same ones) stand at an earlier place,
# but get another round because they are quite strict with regard
# to the preceeding preposition (should not be an adverb)

SELECT Gen IF (*-1C Pr BARRIER NPNH OR REALCLB OR CONTRA)(NOT 0 VFIN)
	(1 NP-BORDER OR CC OR COMMA OR ("ieš")); 
	## Earret mu ii giige lea boahtán.
	## Sáhka su birra viidánii miehtá Syria, ja ...
	## ...gaskal vejolaš ja dohkálaš iskkadan- ja dikšunvugiid.
	## ...ja vulggii rastá jávrri iežas gávpogii. 

SELECT Gen IF (*-1C Pr BARRIER NOT-NP OR Po)(*1 Interr BARRIER WORD);
	## Sáhttá dadjat, ahte jagi 1978 láhka balansere gaskkal sámi 
	## vuoigatvuođaid boarásmuvvan oainnu, mii lei ráđđejeaddjin 
	## "vigihis ávkkástallanrievtti áigodagas", ja eanet ođđaáigásat 
	## vuoigatvuođaid dovddasteaddji oainnu.

SELECT Gen IF (-1C Pr)(0 Num);
	## Jus divššohas lea badjel 16 jagi boaris... 



# =============================== #
# Disambiguating syntax - Cycle 4 #
# =============================== #
             CONSTRAINTS #26      #
# =============================== #

    # ----------------- #
    # Table of Contents #
    # ----------------- #

    # NP internal
    # Subject
    # Subject predicate
    # Subject or not
    # Fragments
    # Object and object predicate
    # Verbal functions
    # Other grammatical functions
  
  
  
  
# NP internal
# -----------

REMOVE (@NQ<) IF (NOT *-1 Num BARRIER NPNH OR COMMA);

REMOVE (@NQ<) IF (-1 (@NumN<));
    ## Finnmárkku fylkkagielda lea jagi 1988 rájes ovddidan doaimmaid sámi álbmoga várás spesialistadearvvašvuođabálvalusas, ja ráhkadii jagi 1991 fylkkagieldda sámi dearvvašvuođa- ja sosiálaplána (rievdaduvvon maŋimus jagi 1995).

REMOVE (@GN>) IF (NOT -1 ("nr"))(NOT *1 N OR Actio BARRIER (@NumN<) OR NPNH);  
REMOVE (@NQ<) IF (-2 ("nr") LINK 1 Num);
	## Dearvvašvuođaregisttarláhka lea sierraláhka heaggadieđuidlága ektui (cuoŋománu 14. b. 2000 láhka nr 31 heaggadieđuid gieđahallama birra ).
	
REMOVE (@NQ<) IF (0 ("nr") OR ("§"));
	## Máná iežas journála geahččanlobi ferte árvvoštallat divššohasvuoigatvuođalága § 5-1, § 4-4, § 3-4 ja § 3-5 ektui.
	
REMOVE (@NQ<) IF (-1 Ord);
	## Geahča § 12 vuosttas lađđasa mávssu birra máŋgosa / nuppádasa ovddas.    
	
REMOVE (@ADVL) IF (0 ("b" @GN>) OR ("beaivi" @GN>))
     (*1 N BARRIER NON-APP LINK NOT 0 HUMAN OR ORGANIZATION);
    ## Geassemánu 8.b. 2001 láhkaásahus nr 676 oktagaslaš plánaid birra dearvvašvuođalágaid vuođul lea addojuvvon láhkamearrádusaid láhkavuođuin.
# todo: 1. make the set non-app
#       2. restrict the number of N to the right





# Verbal functions
# ----------------

# Moved in from the end.

SELECT (@+FAUXV) IF (*1 Inf OR PrfPrc OR (Actio Ess) BARRIER CS OR CP OR VFIN);
	## Máhtte áigu heaitit vuola juhkamis.

SELECT (@+FAUXV) IF (*1 ConNeg BARRIER CS OR CP OR Neg);
	## Allos dattege šatta nu go mun dáhtun, muhto nugo don dáhtut.

REMOVE (@+FAUXV) IF (0 AUX-OR-MAIN)(NOT *1 Inf OR PrfPrc OR (Actio Ess) OR ConNeg 
	BARRIER CS OR CP);
	## Maid Máret áiggui goarrumis?

SELECT (@-FAUXV) IF (*1 Inf OR PrfPrc OR (Actio Ess) BARRIER CS OR CP);
	## In beassan dal deatnojohtima oaidnit.

REMOVE (@-FAUXV) IF (0 (@-FMAINV))(NOT *1 Inf BARRIER CS OR CLB);
	## Sápmelaččat ledje orron dáin guovlluin 1600-logu birrasiin. 

SELECT (@-FMAINV) IF (*-1C (@SUBJ) BARRIER CS OR CP LINK 0 Sg)(0 Inf);
	## Vuoigatvuohta journála geahččat.

SELECT (@+FMAINV) IF (*-1 (@OBJ) BARRIER NOT-ADV-PCLE LINK *-1 (@SUBJ) 
	BARRIER NOT-ADV-PCLE LINK 0 Sg)(0 Sg3);
	## Juohke háve go olmmoš joddosaji molsu...

SELECT (@+FMAINV) IF (NOT *-2 VFIN BARRIER CS)(-1 (@ADVL))(NOT 0 Inf)
	(NOT *1 VFIN BARRIER CS);
	## Dalle bidjet buođđogeaži.



# Subject
# -------

SELECT (@SUBJ) IF (0 MOAI)(*1 ("leat" Du1) BARRIER NOT-ADV-PCLE) ;
SELECT (@SUBJ) IF (0 DOAI)(*1 ("leat" Du2) BARRIER NOT-ADV-PCLE) ;
SELECT (@SUBJ) IF (0 MII-PERS)(*1 ("leat" Pl1) BARRIER NOT-ADV-PCLE) ;
SELECT (@SUBJ) IF (0 DII)(*1 ("leat" Pl2) BARRIER NOT-ADV-PCLE) ;
	## Moai letne studeanttat.

SELECT (@SUBJ) IF (0 (N Num))(1 (Sg Com))(*1 V-DU3 BARRIER VERB OR CS OR CP);
	## Máhte-guovttos Lássiin eaba boahtán.

SELECT (@SUBJ) IF (0 (Interr Sg))(*1 V-SG3 BARRIER NOT-ADV);
	## Márjá riegádahtii Jesusa, guhte gohčoduvvo Kristusin.

SELECT (@SUBJ) IF (0 (N Sg Nom))(*1 (Neg Sg3) BARRIER NOT-ADV-PCLE OR CLB);
	## Sámi dutkan ii leat dan dihte soaittáhagas válljejuvvon dutkansuorgi.

SELECT (@SUBJ) IF (0 (N Pl Nom))(*-1 COPULAS BARRIER NPNH)
	(*-1 V-PL3 BARRIER VFIN LINK *-1 (Sg Nom) BARRIER NOT-ADV-PCLE
	LINK NOT *-1 CC BARRIER NPNH LINK -1 Nom); 
	## Erenoamáš ulbmiljoavku leat doavttergrádastudeanttat.
 
SELECT (@SUBJ) IF (*-1 CC BARRIER NPNH LINK -1 Nom)
	(*1 V-PL3 BARRIER NOT-ADV-PCLE);
	## Muhto buođđogeažit ja giđđabuođut ráhkaduvvojit muđui seammá vugiid 
	## mielde.

SELECT (@SUBJ) IF (*-1 CC BARRIER NPNH LINK -1 Nom)
	(*1 PUNCT-LEFT BARRIER NOT-ADV-PCLE LINK *1 PUNCT-RIGHT LINK *1 V-PL3 BARRIER NOT-ADV-PCLE);
	## Molssaevttolaš dálkkasdiehtaga našunála dutkanguovddáš ja Sámi dearvvašvuođadutkanguovddáš (goappašat fágabirrasat gullet Romssa universitehta vuollái) berrejit sáhttit oassálastit dán bargui.	


SELECT (@SUBJ) IF (*-1 CC BARRIER NPNH LINK -1 (@SPRED))
	(*1 (@SPRED) BARRIER NPNHA OR REALCLB OR PUNCT LINK NOT 0 Interr LINK
	NOT *1 COPULAS BARRIER Nom OR CS OR CP OR CLB);
	## Vuosttaš vuojahaga fávllimus čuolda lea goaratčuoldan, ja 
	## nubbi čuolda fas gáddeoal-čuoldan.

SELECT (@SUBJ) IF (*-1 COPULAS BARRIER NPNHA)(NOT 0 Ess)(NOT 1 COMMA)
	(*1 (A Nom) BARRIER NOT-ADV-PCLE OR go);
	## De lea cicha gárvvis.

SELECT (@SUBJ) IF (*-1 COPULAS BARRIER NPNH LINK *-1 ("veara") BARRIER NOT-ADV-PCLE);
	## Man ollu eanet veara lea olmmoš go sávza!

SELECT (@SUBJ) IF (0 (N Sg Nom) LINK 0 TIME)(*1 V-SG3 BARRIER NOT-ADV-PCLE);
	## Sii eai diehtán ahte mánnu šaddá.

SELECT (@SUBJ) IF (0 ("guovttis" Nom))(*1 Du3 BARRIER VERB OR CLB OR CC OR CS OR CP);
	## Guovttis jámiiga dan bárttis.

SELECT (@SUBJ) IF (NOT *-1 (@SUBJ) BARRIER CS)(*-1 ("leat") BARRIER VERB OR CS)
	(0 Actio)(*1 PrfPrc BARRIER VERB OR CC OR CS OR CP OR CLB);
	## Leago miehtan vižžojuvvon divššohasas dahje sus geas lea...


# Subject predicate
# -----------------

REMOVE (@SPRED) IF (*-1 ("leat") BARRIER NOT-ADV-PCLE OR ("dego"))
	(*1 PrfPrc BARRIER NOT-ADV-PCLE LINK NOT 0 Pass);
	## Sámi vuoigatvuođalávdegotti čállin leat čuovvovaččat maid doaibman.
	
SELECT (@SPRED) IF (0 A)(1 go)(*2 COPULAS BARRIER V-NOT-AUX LINK *1 (@SUBJ) BARRIER 
	NPNHA);
	## Guhkki go lea du biila?

SELECT (@SPRED) IF (0 (A Qst))(*1 COPULAS BARRIER V-NOT-AUX LINK *1 (@SUBJ) BARRIER
	NPNHA);
	## Guhkkigo lea du biila?

SELECT (@SPRED) IF (*-1 COPULAS BARRIER (@-FMAINV) OR Loc OR go OR ("dego") OR CC 
	OR CLB OR CS OR CP LINK *-1 (@SUBJ) BARRIER (@OBJ) OR CC OR CS OR CP 
	LINK NOT 0 Ess)(NOT 0 Loc OR Pers OR Interr OR Prop)(1 NP-BORDER)
	(NOT *-1 VFIN BARRIER CS LINK *-1 VFIN BARRIER CS LINK *-1 Interr BARRIER VERB);
	## Gustovaš riekteteoriija gaskavuođat sihke kultuvrralaš ja ekonomalaš 
	## konteavstta dáfus leat eahpečielgasat.

 # The last condition prevents the rule from hitting "vuoigatvuohta" here:
	## Divššohasas, gii dárbbaša guhkit áiggi ja oktiiheivehuvvon dearvvašvuođa-
	## bálvalusaid, lea vuoigatvuohta oažžut ráhkaduvvot oktagaslaš plána.

SELECT (@SPRED) IF (*-1 COPULAS BARRIER VERB OR (@SPRED) OR CLB OR CS OR CP OR go 
	OR ("dego") OR (@OBJ) LINK *-1 (@SUBJ) OR (@SUBJ-QH) BARRIER V-NOT-AUX OR 
	CLB OR CC OR CS OR CP LINK NOT 0 Ess LINK NOT *-1 (@SPRED) BARRIER CC OR CS)
	(NOT 0 Loc OR Prop OR Interr)(NOT 0 Actio OR Actor OR Attr LINK 1 N);
	## Soai leaba goappašagat nu jallat.
	
SELECT (@SPRED) IF (*-1 COPULAS BARRIER VERB OR (@SPRED) OR CLB OR CS OR CP OR go 
	OR ("dego") OR (@OBJ) LINK *-1C (@PrcN>) LINK *-1 (@SUBJ) OR (@SUBJ-QH) BARRIER V-NOT-AUX OR 
	CLB OR CC OR CS OR CP LINK NOT 0 Ess LINK NOT *-1 (@SPRED) BARRIER CC OR CS)
	(NOT 0 Loc OR Prop OR Interr)(NOT 0 Actio OR Attr LINK 1 N);
    ## Gávcci obbalaččat 12 válljejuvvon áirasiin leat leamaš nissonolbmot.
	
#SELECT (@SPRED) IF (*-1 COPULAS LINK -1 Loc LINK -1 (@PrcN>) OR A BARRIER VERB OR (@SPRED) OR CLB OR CS OR CP OR go 
#	OR ("dego") OR (@OBJ) LINK *-1 (@SUBJ) OR (@SUBJ-QH) BARRIER V-NOT-AUX OR 
#	CLB OR CC OR CS OR CP LINK NOT 0 Ess LINK NOT *-1 (@SPRED) BARRIER CC OR CS)
#	(NOT 0 Loc OR Prop OR Interr)(NOT 0 Actio OR Attr LINK 1 N);
#	## Muhtin biirres leat gávcci obbalaččat 12 válljejuvvon áirasiin leamaš nissonolbmot, ja eará biirres leat buohkat geat leat válljejuvvon leamaš dievddut.

SELECT (@SPRED) IF (*-1 COPULAS BARRIER NPNH OR (@SPRED) OR COMMA LINK 0 PrfPrc 
	LINK *-1 ("leat") BARRIER NOT-ADV-PCLE LINK *-1 
	Nom BARRIER VERB OR CS OR CP);
	## Arkelaus lei šaddan Judea gonagassan áhčis Herodesa maŋŋil.

SELECT (@SPRED) IF (*-1 (@SUBJ) BARRIER NPNHA OR CC OR CLB OR CS OR CP OR ("/")
	LINK *-1 COPULAS BARRIER NOT-ADV-PCLE)(NOT 0 Interr OR Rel);
	## Dávjá lea riektevuogádat áidna mearridanvuogádat.

SELECT (@SPRED) IF (*-1 (N Nom) BARRIER NOT-ADV-PCLE OR go OR ("nugo") OR CLB OR 
	PUNCT OR CP LINK NOT 0 ("buorre") OR (@PROP>) LINK *-1 COPULAS BARRIER NPNHA)
	(NOT 0 Interr OR Pers)(NOT 0 ("nr") LINK 1 @NumN<); 
	#  After "NPHNA": LINK NOT *-1 Loc BARRIER NOT-ADV-PCLE
	## Ledjego guolit divrasat?

SELECT (@SPRED) IF (*-1 COPULAS BARRIER VERB LINK 0 Sg3 LINK NOT *-1 Loc BARRIER
	NOT-ADV-PCLE)(0 A OR ("imaš"))(NOT 0 Attr LINK 1 N)
	(NOT *1 PrfPrc BARRIER NOT-ADV-PCLE);
	## Dutnje lea buoret ahte don massát ovtta lahtu.

SELECT (@SPRED) IF (*-1 COPULAS BARRIER VFIN OR CONTRA LINK *-1 AUX 
	BARRIER NOT-ADV-PCLE LINK 0 Sg3)(0 A OR ("imaš"))(NOT 0 N)
	(NOT 0 ("veara") LINK 1 PrfPrc OR (Actio Ess) OR N);
	## Boazosápmelaččain ii leat bievlan vejolaš fievrridit gálvvu
	## gerresiiguin.

SELECT (@SPRED) IF (NOT *-1 VFIN)(0 Pers)(1 Interr LINK *1 VFIN BARRIER CS OR CP 
	LINK *1 COPULAS BARRIER CS OR CP OR CLB LINK *1 (@SUBJ) BARRIER CS OR CP 
	OR CLB LINK 0 Prop LINK NOT *1 V-NOT-AUX);
	## Son gii ovddimus boahtá lea Máhtte. 
 
SELECT (@SPRED) IF (*-2 COPULAS BARRIER NOT-ADV-PCLE)(-1 Superl)
	(*1 (@SUBJ) BARRIER CC OR CLB OR CS);
	## Nugo namuhuvvui, lea nannoseamos ágga dása guhkes áiggi geavahus.

SELECT (@SPRED) IF (*-1 COPULAS BARRIER NOT-ADV-PCLE LINK 0 PrfPrc OR ConNeg LINK 
	*-1 Neg BARRIER NOT-ADV-PCLE)(0 A);
	## Ii dál leat álki bargui beassat.

SELECT (@SPRED) IF (*-1 (Pers Nom) BARRIER NPNHA OR CLB LINK *-1 COPULAS 
	BARRIER NOT-ADV-PCLE)(NOT 0 Interr);
	## Leat go don árgi bajándálkkis?

SELECT (@SPRED) IF (*-2 COPULAS BARRIER NOT-ADV-PCLE LINK *-1 (@SUBJ) 
	BARRIER V-NOT-AUX OR CS)(-1 (PrfPrc))(NOT -1 Pass)(0C Nom)(1 NP-BORDER);
	## Son lea oahppan olmmoš.

SELECT (@SPRED) IF (*-1 COPULAS BARRIER V-NOT-COP LINK 0 Pl3)(0 (A Pl))
	(*1 (@SUBJ) BARRIER NPNH LINK 0 Pl);
	## Leatgo hilbadat manat?

SELECT (@SPRED) IF (*-1 COPULAS BARRIER NOT-ADV-PCLE)(0 A)(1 (Pers Nom));
	## Eamiálbmogiidda leat oktasaš dat, ahte ...

SELECT (@SPRED) IF (*-1 COPULAS BARRIER VERB OR CS)(1 ("dego"));
	## Lehket dan dihtii gávvilat dego gearbmašat ja vigiheamit dego duvvát!

SELECT (@SPRED) IF (0 Ess)(*1 (@SUBJ) OR (@SUBJ-QH) BARRIER VERB OR CLB OR CS OR CP 
	LINK *1 COPULAS BARRIER V-NOT-AUX OR CS OR CONTRA LINK NOT *1 V-NOT-AUX 
	BARRIER CS)(NOT *1 (Interr Nom) BARRIER VERB OR CS);
	## Ja stuorra buressivdnádussan dat gal buođđobivdu leamašge Sámieatnan 
	## stuorámus deanu alde ássiide.

SELECT (@SPRED) IF (*-1 (@SUBJ) OR (@SUBJ-QH) BARRIER NPNH LINK *-1 COPULAS 
	BARRIER NOT-ADV-PCLE)(0 Ess);
	## Mus lea Ánde-máhka mielbargin.

SELECT (@SPRED) IF (*-1 COPULAS BARRIER NOT-ADV-PCLE LINK 0 Inf 
	LINK *-1 (@SUBJ) OR (@SUBJ-QH) BARRIER NOT-ADV-PCLE LINK *-1 AUX BARRIER VERB);
	## De ferte joddu ge leat dađi mielde, oanehit go dábálaš.

SELECT (@SPRED) IF (0 (A Sg Nom))(NOT *-1 Nom BARRIER CS)(NOT *1 Nom BARRIER CS)
	(NOT *-1 V-NOT-AUX BARRIER CS)(NOT *1 V-NOT-AUX BARRIER CS);
	## Go beare vejolaš lea, de ráhkaduvvo buođđu čuolddaiguin.
	## Buorre lea go it fuobmán.

SELECT (@SPRED) IF (*-1 COPULAS BARRIER VERB OR CS)(0 ALU);
	## Dábálaččat leat joddut sullii ovtta guhku.

SELECT (@SPRED) IF (*-1 COPULAS BARRIER NPNH OR CC LINK -1 ("dego"));
	## Sii válde mu gitta, dego livččen rievvár.

SELECT (@SPRED) IF (-2 (@SUBJ) LINK 0 Sg)(-1 ("nu") OR ("hui"))(0 (A Sg));
	## Márehis lea oaivvi nu bávččas.

SELECT (@SPRED) IF (-2 (@SUBJ) LINK 0 Pl)(-1 ("nu") OR ("hui"))(0 (A Pl));
	## Mus leat gieđat nu galbmasat.

SELECT (@SPRED) IF (-1 ("mii" Pron Interr Sg Gen))(0 A);
	## Man guhkki lea skuvlii?
	## In dieđe man buorit dat leat.

SELECT (@SPRED) IF (*-1 COPULAS BARRIER (@+FMAINV) OR COMMA OR NPNHA OR go OR ("nugo") 
	LINK *-1 (Pers Nom) BARRIER V-NOT-AUX OR CS OR CP)(NOT 0 Interr)
	(NOT 0 Ess LINK 1 OPRED-VERB LINK NOT 0 Pass);
	## ...dan addejumis, man mielde dat leat eamiálbmotválljen orgánat ja ...
	## 

SELECT (@SPRED) IF (*-1 VFIN BARRIER Acc OR OPRED-VERB OR CS OR CP OR go OR ("dego") 
	OR (@SPRED) LINK NOT 0 ("dahkat") LINK *-1 Nom BARRIER NOT-ADV-PCLE LINK 
	NOT *-1 (@OBJ) BARRIER CS)(0 Ess)(NOT 1 Inf);
	## Máhtte álggii reaŋgan.

SELECT (@SPRED) IF (*-1 (@SUBJ) BARRIER NPNHA LINK *-1 VFIN BARRIER Nom OR CC OR 
	CS OR CP)(0 Ess)(NOT *-1 (@OBJ) BARRIER CS)(NOT *1 (@OBJ) BARRIER NPNHA);
	## ...mii muitala, doaibmago našunálastáhta siviliserejuvvon kulturservodahkan.

SELECT (@SPRED) IF (*-1 BOS OR CLB OR PUNCT BARRIER NOT-CC)(0 A)
	(1 (Pers Nom));
	## Heittogat dat leat maiddái.
	## Ávdugasat sii geat moraštit.

SELECT (@SPRED) IF (NOT 0 Loc)(*1 (Dem Nom) OR (Pers Nom) BARRIER NOT-ADV-PCLE 
	OR COMMA OR CP OR CC OR PUNCT LINK *1 COPULAS BARRIER V-NOT-AUX OR CS OR CP 
	LINK NOT *1 VERB BARRIER CS OR CP OR CC);
	## Gean ráhkadan beaskanáhkit dát leat?

SELECT (@SPRED) IF (NOT 0 ("namma"))(*1 (COPULAS V-3) BARRIER NOT-ADV-PCLE OR CLB 
	LINK *1 Prop BARRIER NOT-ADV-PCLE OR CLB LINK 0 Nom);
	## Salmai riegadii Boas, gean eadni lei Rahab.
	## Mun lean Trond.

SELECT (@SPRED) IF (NOT 0 ("namma"))(*1 COPULAS BARRIER NOT-ADV-PCLE OR CLB 
	LINK *1 Pers BARRIER NOT-ADV-PCLE OR CLB LINK 0 Nom LINK NOT 1 Interr);
	##

SELECT (@SPRED) IF (0 (Sg Nom))(*1 COPULAS BARRIER NOT-ADV-PCLE OR CLB LINK 0 (Pl3) 
	LINK *1 (Pl Nom) BARRIER NPNHA);
	## Erenoamáš ulbmiljoavku leat doavttergrádastudeanttat.

SELECT (@SPRED) IF (*-2C (@SPRED) BARRIER WORD)(-1 Neg)(NOT -1 Sup);
	## Máhtte lea dát iige duot.

SELECT (@SPRED) IF (*-1 ("leat" Imprt) BARRIER VERB OR Nom OR Ess OR PUNCT LINK 
	NOT *-1 (@SPRED))(NOT 0 Pers)(NOT *1 (A Nom) BARRIER VERB OR CS OR CP OR CC);
	## Leage buorre!

SELECT (@SPRED) IF (*-1 COPULAS BARRIER NOT-ADV-PCLE)(*-1 VFIN LINK NOT 0 Sg3 OR Pl3)
	(0 (A Sg Nom));
	## ...ahte áiggošin leat bahánihkkán, váttis dahje fuikolas.

SELECT (@SPRED) IF (0 ("veara"))(1 COPULAS LINK *1 (@SUBJ) OR (@SUBJ-QH) BARRIER NPNHA);
	## Man ollu eanet veara lea olmmoš go sávza!

SELECT (@SPRED) IF (-1 Actio LINK *-1 COPULAS BARRIER NPNHA)(0 ("veara"))
	(NOT *1 PrfPrc OR (Actio Ess) BARRIER NOT-ADV-PCLE)(NOT 1 N);
	## Lea fuopmášan veara, ahte...

SELECT (@SPRED) IF (-3 Actio LINK *-1 COPULAS BARRIER NOT-ADV-PCLE)(-2 CC)(-1 Actio)
	(0 ("veara"));
	## ...buot dan mii lea ráhkistan ja gudnejahttin veara.

SELECT (@SPRED) IF (*-1 COPULAS BARRIER NOT-ADV-PCLE)(0 ("veara"))(1 Inf);
	## Min mielas goit lea veara jurddašit nissongova birra mii boahtá ovdan 
	## jearahalamiin.

SELECT (@SPRED) IF (0 ("veara") OR ("mearkkašan#veara") OR ("namuhan#veara"))
	(*1 COPULAS BARRIER NOT-ADV-PCLE);
	## Mearkkašan veara lea ahte...
	## Erenomáš mearkkašanveara lea ahte...

SELECT (@SPRED) IF (*-1 COPULAS BARRIER NOT-ADV-PCLE)(0 A)(1 (N Nom));
	## Go sápmelaš lea alla váriid alde, de sus lea oppa čielggas jierbmi.

SELECT (@SPRED) IF (*-1 Interr BARRIER VFIN)(0 Ess)
	(*1 COPULAS BARRIER VERB LINK NOT *1 V-TRANS-ACT BARRIER CS);
	## ...mii dál min áiggi eanemus ávkin lea...

SELECT (@SPRED) IF (0 Ess)(*1 ("atnit") OR ("heivet") BARRIER V-NOT-AUX OR CS OR CP);
	## Maŋŋálasa gáddeoal-čuoldan heive dalle dábálaččat vuosttaš vuojahaga 
	## fávllimus čuolda.

SELECT (@SPRED) IF (*-1 ("gohčodit" Pass) OR ("dadjat" Pass) OR ("dahkat" Pass) 
	BARRIER NOT-ADV-PCLE)(0 Ess);
	## Son gohčoduvvo bassin ja Ipmila Bárdnin.
	
REMOVE (@SUBJ) IF (*-1 BOS LINK NOT *1 VFIN BARRIER EOS);
    ## Kárášjogas 31.5.2002 dbm. 10.00.
    
REMOVE (@SUBJ) IF (*-1 COPULAS BARRIER VERB OR CS OR CP)(0 ("buorre"));
	## Leat go buorre munnje?

SELECT (@SPRED) IF (-1 COPULAS LINK *-1 ("namma" Nom @SUBJ))(0 Prop) ;
	## Mu namma lea Søren.

SELECT (@SPRED) IF (-2 COPULAS)(-1 (":"))(0 Num);
	## Doaimmabiju jahkásaš bušeahttarámma lea: 380.000 ruvnno.

SELECT (@SPRED) IF (*-1 go BARRIER NOT-ADV LINK -1 COPULAS LINK *-1 BOS OR CC
	BARRIER NOT-CC)(0 A);
	## Leat go árgi bajándálkkis?

SELECT (@SPRED) IF (*-1 ("dahkat") BARRIER NOT-ADV-PCLE)(0 Ess)(NOT *-1 (@OBJ)
	BARRIER CS OR CP)(NOT *1 (@OBJ) BARRIER CS OR CP);
	## Ii dahkan gullin ge.
	## Problem: Son guhte dahká bassin.

SELECT (@SPRED) IF (*-1 ("leat" Sg1) BARRIER VERB OR CLB OR CS OR CP OR go OR ("dego") 
	OR (@OBJ))(NOT 0 MUN) ;
SELECT (@SPRED) IF (*-1 ("leat" Sg2) BARRIER VERB OR CLB OR CS OR CP OR go OR ("dego") 
	OR (@OBJ))(NOT 0 DON) ;
	## Mun lean Tarzan, don leat Jane.
 #  This rule did not work with COPULAS, for some reason.
 #  Idea: I am a predicate if I don't agree with the verb.

SELECT (@SPRED) IF (*-2 CC BARRIER N OR Loc OR Po OR V-NOT-AUX OR CP OR CS)(-1 COPULAS)
	(0 A)(1 CLB OR CC OR EOS);
	## Nugo dii bijaidet lahtuideattet buhtismeahttunvuođa ja lágahisvuođa háldui 
	## ja šattaidet lágaheapmin,...

SELECT (@SPRED) IF (*-1 COPULAS BARRIER NOT-ADV-PCLE LINK NOT *-1 V-NOT-AUX 
	BARRIER VFIN)(0 Ess)(*1 Qst BARRIER WORD);
	## Ruonáetnama hárrái šaddá váldoáššin, berrešiigo jienastuslohku ráddje-
	## juvvot nu, ahte...

SELECT (@SPRED) IF (0 A)(NOT 0 Adv)(*1 COPULAS BARRIER NOT-ADV-PCLE LINK *1 Inf 
	BARRIER NOT-ADV-PCLE);
	## Dárbbašlaš lea dikšut jávrriid ovdal go lea ekonomalaččat veara bivdit.

SELECT (@SPRED) IF (*-1 ("leat") BARRIER VERB)(0 Ess)(NOT 0 (@ADVL))
	(*1 Inf BARRIER VERB);
	## Universitehtii lea ain hástalussan jorgalit dárogielmateriála.

SELECT (@SPRED) IF (-2 Num)(-1 (@QN<))(0 A);
	## Arkiivaávdnasat galget sáddejuvvot go leat sullii 25-30 jagi boarrásat. 

SELECT (@SPRED) IF (-1 ("go" Pcle))(0 (A Sg Nom))(NOT 0 Attr LINK *1 N BARRIER NPNH);
	## Gielddat berrejit láhčit diliid nu ahte sámi boarrásat ja doaibmahehttejuvvon 
	## sápmelaččat sáhttet, nu guhká go vejolaš, orrut ruovttus.

SELECT (@SPRED) IF (NOT *-1 VERB BARRIER CS)(0 Ess)(*1 Pass BARRIER VERB);
	## Noađđeheargin dárbbašuvvojit sakka lojit hearggit go geresgeassin.

SELECT (@SPRED) IF (*-1C (@SUBJ) BARRIER go OR ("dego") OR CC OR CLB OR CS OR 
	CP OR PUNCT LINK NOT 0 Interr)(NOT -1 (@SUBJ))(0C (N Sg Nom));
	## Sámi dutkan ii leat dan dihte soaittáhagas válljejuvvon dutkansuorgi.
 # If -1 is (@SUBJ) we probably have apposition.
 
REMOVE (@SUBJ) IF (0 Ess)(*1C (@SUBJ) BARRIER CS OR CP LINK NOT 0 Interr);
	## Sámi vuoigatvuođalávdegotti čállin leat čuovvovaččat maid doaibman.
 
SELECT (@-FSUBJ) IF (NOT *-1 V-TRANS-ACT BARRIER CS)(1 VAbess);
	## Diet dáhpáhuvai mu dieđikeahttá.
	
SELECT (@SPRED) IF (-2 N)(-1 (":"))(0 CARDINALS)(1 CLB);
    ## Fáksa: 22242786.


# Coordinated SPRED
# - - - - - - - - -

SELECT (@SPRED) IF (-2C (@SPRED))(-1 CC)(NOT *1 VFIN BARRIER CS);
	## De adnojuvvojit dat vel čuollofirbmin ja buođo-al-fierbmin.

SELECT (@SPRED) IF (*-1 CC BARRIER NPNH LINK -1C (@SPRED))
	(NOT *1 VFIN BARRIER CS OR REALCLB)(NOT *1 (@SPRED) BARRIER NPNH)
	(NOT 1 (@ADVL));
	## Dalle ledje noaiddis erenoamáš rolla oaidnilin ja dálkkodeaddjin.

SELECT (@SPRED) IF (*-1 COMMA BARRIER NPNH LINK -1C (@SPRED))(0 A)
	(NOT *1 VFIN BARRIER CLB OR CS);
	## Guokte golbma čuoldabaji guhku, ja guhkitge, sáhttá vuojahat leat.


# Topicalised @SPRED
# - - - - - - - - - -
	# Revise these rules:
	# Make test sentences. Make rules who identify the subject in another position

#SELECT (Sg @SPRED) IF (*-1 BOS LINK *1 V-SG LINK 1 (@SUBJ) LINK 1 Inf);
    ## Guokte golbma čuoldabaji guhku, ja guhkitge, sáhttá vuojahat leat.    

#SELECT (Sg @SPRED) IF (*1 V-SG BARRIER VFIN LINK 1C @SUBJ);
#SELECT (Pl @SPRED) IF (*-1 BOS LINK *1 V-PL OR V-DU BARRIER VFIN LINK 1C @SUBJ);
#SELECT (Pl @SPRED) IF (*1 V-PL OR V-DU BARRIER VFIN LINK 1C @SUBJ);


# Subject or not
# --------------

REMOVE (@SUBJ) IF (NOT *-1 VERB BARRIER CS OR CP)(0 Gen)(*1C (@SPRED) BARRIER NPNH);
	## Riektemetoda vuođđun lea našunálastáhtalaš riektaárbevieruid áddejupmi.

REMOVE (@SUBJ) IF (*-1C V-1-2 BARRIER NOT-ADV-PCLE)(0 Sg OR Pl);
	## Mii leimmet viđas.
	## Mun lean árgi.

REMOVE (@SUBJ) IF (0 ("mearkkašan#veara") OR ("namuhan#veara"));
	## Datte lea mearkkašanveara ahte son, stáhta ektui, orru oaivvildeamen...

REMOVE (@SUBJ) IF (*-1 (@SUBJ) BARRIER CS OR CP)(0 (@TITLE));
	## Stuorra giitu maiddái professor Birger Stuevold lassenii.

REMOVE (@SUBJ) IF (0 (@TITLE))(*1 (@SUBJ) BARRIER CS OR CP);
	## Go gonagas Herodes gulai dan, de son suorganii.

REMOVE (@SUBJ) IF (*-1 (@SUBJ) BARRIER CS)(0 Attr)(*1 N BARRIER NPNH);
	## Dát lea ee. kultuvrralaš gaskka duohken.

SELECT (@SUBJ) IF (*1C (@SPRED) BARRIER NPNH OR CC OR CLB);
	## Dalle adno nannejuvvon dulkon gustojeaddji riektin.
	
REMOVE (@SUBJ) IF (-1 BOS)(*1 HNOUN BARRIER NPNH LINK 1 EOS);
    ## Stáhta ruđalaš ovddasvástádus.
    
REMOVE (@SUBJ) IF (NOT *-1 ("go") OR ("dego") LINK *1 VFIN)(0C (@CS-COMPL) OR (@SUBJ) LINK NOT *1 VFIN);
    ## Čuovvumuš sáhttá leat, ahte sápmelaččat eai oaččo seammá ávkki bálvalusain go eanaš dážat.

	
# Fragments
# -------------------

SELECT (@HNOUN) IF (NOT -2 (N Nom) OR (ACR Nom))(-1 ("nr") OR ("nummar") OR ("nummir"))(NOT *-1 VFIN OR ("leat" PrfPrc))(0 (Num Nom))(NOT *1 VFIN);
    ## Dieđáhusa nummar okta.

SELECT (@HNOUN) IF (NOT *-1 VFIN OR ("leat" PrfPrc) OR (N Nom) OR (ACR Nom))(0 (N Nom))(NOT *1 VFIN);
    ## Oassi II.

# Object and object predicate
# ---------------------------

SELECT (@OBJ) IF (NOT *-1 (@OBJ) BARRIER CS OR REALCLB)(0 ("hearrá"))
	(1 ("ipmil" Acc));
	
SELECT (@OBJ) IF (-1 ("hearrá") LINK 0 (@OBJ))(0 ("ipmil" Acc));
	## Don galggat ráhkistit Hearrá Ipmilat oppa váimmustat...

SELECT (@OPRED) IF (*-1 (@OBJ) BARRIER VERB OR CC OR CLB OR CP OR go OR PUNCT 
	LINK NOT 0 Dem)(*-1 OPRED-VERB BARRIER VERB OR CC OR CLB OR CS OR CP)
	(NOT 0 Gen LINK *1 N BARRIER NPNH)(NOT 0 Interr OR TIME OR ROUTE)
	(NOT 0 Num LINK 1 ROUTE OR TIME)(NOT 1 (TV Actio) LINK NOT *1 (@OBJ) BARRIER CS);
	## Divššohasbearráigeahčči galgá dahkat ortnega dovddusin.
 
SELECT (@OPRED) IF (*-1 (@OBJ) BARRIER NOT-NP OR Interr)(0 Ess)(NOT 1 V-SG3);
	## Doaibmaplána lea bidjan buori vuođu ja leamaš mielde rievdadeame 
	## miellaguottuid sámegiela geavaheami ja oainnusin dahkama ektui.

SELECT (@OPRED) IF (*-1 VFIN BARRIER Acc OR CS OR CP LINK *-1 Nom 
	BARRIER NOT-ADV-PCLE)(0 Ess)(1 Inf);
	## Fakultehtaid gáržžes ruhtadilli dahká váttisin lágidit dákkár kurssaid.

SELECT (@OPRED) IF (*-1 COPULAS BARRIER VERB LINK NOT 0 VFIN LINK *1 (@OBJ) 
	BARRIER NOT-ADV-PCLE)(NOT *1 V-TRANS-ACT BARRIER NP-BORDER);
	## Dat ii lohkan daid leat buriid.

SELECT (@OPRED) IF (*-1 (@OBJ) BARRIER CS OR CP OR CC LINK 0 N)(0 A OR Ess)
	(NOT 0 Adv)(NOT 0 Attr LINK *1 N BARRIER NPNH)
	(NOT *-1 COPULAS BARRIER VERB LINK NOT *1 VERB BARRIER CLB OR CC OR CS OR CP);
	## Gáhkuid son ráhkada hui buriid.

SELECT (@OPRED) IF (0 Ess)(NOT 0 Acc)(*1 OPRED-VERB BARRIER V-NOT-AUX OR CS OR CP 
	LINK NOT 0 Pass);
	## Buođđogeahčin gohčodit olbmot dakkár buođu mii dulvvi áiggi bidjaluvvo 
	## gáddegurii.

SELECT (@OPRED) IF (0 (A Acc))(*1 NP-HEAD-ACC BARRIER CS OR CP LINK *1 COPULAS 
	BARRIER NOT-ADV-PCLE);
	## Buriid dat ii lohkan daid leat.

REMOVE (@OPRED) IF (NOT *-1 V-TRANS-ACT BARRIER CS LINK NOT 0 AUX)
	(NOT *1 V-TRANS-ACT BARRIER CS LINK NOT 0 AUX);
	## Itgo boađáše munnje veahkkin?
	
REMOVE (@OBJ) IF (*-1 BOS LINK NOT *1 V-TRANS-ACT BARRIER EOS);
    ## Kárášjogas 31.5.2002 dbm. 10.00.
    ## láibbi borrat.

REMOVE (@OBJ) IF (0 (@GN>))(NOT 0 Interr)(*1 (@SUBJ) BARRIER NPNH LINK *1C (@OBJ) 
	BARRIER CLB OR CS OR CP)(NOT 1 VFIN);
	## ...go su iežas anistumit gesset ja giktalit su.

REMOVE (@OBJ) IF (*-1C (@OBJ) BARRIER NPNH OR CC OR CLB OR PUNCT)
	(NOT 0 Interr)(NOT 1 (TV Actio) LINK NOT *1 (@OBJ) BARRIER CS);
	## Das maŋŋá dajan juoidá álbmotrievtti mearkkašumi birra. 
 
REMOVE (@OBJ) IF (*-1C (@OBJ) BARRIER CS OR CP OR CC OR CLB OR Inf OR PUNCT 
	LINK NOT *-1 V-TRANS-ACT BARRIER NPNHA)(NOT 0 Interr OR Pers)(NOT 1 Inf);
	## ...dieđuid maid son dárbbaša iežas doaimmaheami dihtii.

REMOVE (@OBJ) IF (*-1 Inf BARRIER NPNHA LINK 0 V-TRANS-ACT LINK -1 (@OBJ)
	LINK NOT *-1 V-TRANS-ACT BARRIER NPNH)(0 Gen LINK *1 N BARRIER NPNH)
	(NOT 0 Interr);
	## Sámi studeanttaid oččodit Romssa universitehtii.

REMOVE (@OBJ) IF (*-1 V-TRANS-ACT BARRIER NPNHA LINK *-1 (@OBJ) BARRIER
	V-NOT-AUX OR CLB OR CC OR CS OR CP LINK NOT 0 (@GN>) LINK 
	NOT *-1 V-TRANS-ACT BARRIER NOT-ADV-PCLE)(0 (@GN>))(NOT 0 Interr);
	## Daid guovlluid sáhttit meroštallat sámi guovddášguovlun.

REMOVE (@OBJ) IF (*-1 V-NOT-AUX BARRIER NOT-ADV-PCLE LINK 0C IV LINK NOT 0 PrfPrc)
	(NOT *-1 CLB BARRIER NPNH)(NOT *1 V-TRANS-ACT BARRIER V-NOT-AUX OR CS OR CP);
	## Stuorát guolit mannet čađa dohko joddomohkkái.

REMOVE (@OBJ) IF (*-1 ("gullat") BARRIER VERB OR ("gč") OR CS LINK *-1 Ill 
	BARRIER NOT-ADV-PCLE);
	## Dasa gullet maiddái riekteásahusat.

REMOVE (@OBJ) IF (0 Dem)(*1C NP-HEAD-ACC BARRIER V-TRANS-ACT OR go OR CS OR CP OR 
	CC OR CLB LINK NOT 0 Interr);
	## Mun in leat vel borran dan guolis maidege.

REMOVE (@OBJ) IF (-1 V-TRANS-ACT BARRIER NOT-ADV-PCLE LINK *-1 (@OBJ) BARRIER
	NOT-ADV-PCLE)(0 Adv);
	## In sáhtege buohkaid biebmat gallás.

SELECT (@OBJ) IF (*-1 Inf BARRIER NPNHA LINK 0 V-TRANS-ACT LINK NOT *-1 (@OBJ)
	BARRIER V-NOT-AUX OR CS OR CP)(NOT 0 Loc LINK *-1 (@OBJ) BARRIER VERB)
	(1 (@OPRED) LINK 1 NP-BORDER);
	## Divššohasbearráigeahčči galgá dahkat ortnega dovddusin.	
   
SELECT (@OBJ) IF (*-1 ST-VERB BARRIER CS OR CP OR go OR (@OBJ) 
	LINK NOT *-1 (@OBJ) BARRIER CS OR CP)(NOT 0 Adv)
	(NOT 0 Nom LINK *-1 (TV N Gen) BARRIER NPNH)(NOT 0 TIME)
	(NOT 0 Dem LINK *1 N BARRIER NPNH)(NOT 0 Loc LINK *-1 (IV) BARRIER VERB)
	(NOT 0 (Acc PxSg3) LINK 0 Loc)(NOT 1 V-TRANS-ACT)
	(NOT *1 (@OBJ) BARRIER CC OR CS OR CP OR COMMA)
	(NOT 0 ("nr") LINK -1 N OR A OR ACR OR ABBR);
	## Mun oidnen guovžža golbma kilomehtara dábbelis.

SELECT (@OBJ) IF (-2 V-TRANS-ACT LINK 1 CS LINK 1 @CS-COMPL);	
	
	
SELECT (@OBJ) IF (*1 V-TRANS-ACT 
	LINK NOT *1 (@OBJ) BARRIER CS OR CP)(NOT 0 Adv)
	(NOT 0 Nom LINK *-1 (TV N Gen) BARRIER NPNH)(NOT 0 TIME)
	(NOT 0 Dem LINK *1 N BARRIER NPNH)(NOT 0 Loc LINK *-1 (IV) BARRIER VERB)
	(NOT 0 (Acc PxSg3) LINK 0 Loc)			   
	(NOT *1 (@OBJ) BARRIER CC OR CS OR CP)	   
	(NOT 0 ("nr") LINK -1 N OR A OR ACR OR ABBR);
	## SDD lea nammadan guovlluid dearvvašvuođafitnodagaid stivrraid, mat vástidit doaimmaineaset dušše stáhtii, mii daid eaiggáduššá.
	## ...juos sin livčče dikšumin bargit...
	# Removed BARRIER NOT-ADV from the first part. Perhaps there should be another barrier? 

REMOVE (@GN>) IF (0 VFIN LINK *1 @OBJ LINK NOT *1 V-TRANS-ACT);
    ## Áldu mii dálvvi miehtá njamaha miesi gohčoduvvo čearpmateadni.

REMOVE (@GN>) IF (*-1 V-TRANS-ACT BARRIER NOT-ADV-PCLE LINK 0 VERB 
	LINK NOT *-1 (@OBJ) BARRIER CS OR CP OR CC)(1 (@GP>));
	## ...ja maiddái muđui guorahallat soahpameahttunvuođa 
	## vuoigatvuođa ja duođalaš geavaheami gaskkas.

REMOVE (@GN>) IF (1 (@-FMAINV))(1 V-TRANS-ACT);
	## Divššohas dahje su ovddasteaddji galgá leat váidalusa vuolláičállán.

SELECT (@OBJ) IF (*-1 ST-VERB BARRIER VERB OR CS OR go)
	(NOT *-1 (@OBJ) BARRIER CS)(NOT 0 Loc)(NOT 0 Dem LINK *1 N BARRIER NPNH)
	(*1 ADVLCASE BARRIER NOT-ADV-PCLE LINK NOT *1 (@OBJ) BARRIER CS);
	## De olahit olbmot luosa buođđogežiin.

SELECT (@OBJ) IF (*-1 V-TRANS-ACT BARRIER NOT-ADV-PCLE)(1 Num)(2 TIME);
	## Go fylkkadoavttir lea seailluhan journála 10 jagi,...

SELECT (@OBJ) IF (NOT *-1 VFIN BARRIER CS)
	(1 COMMA LINK *1 COMMA LINK *1 V-TRANS-ACT BARRIER V-NOT-AUX);
	## Muhto gittabivdosa, nugo buođu ja njaŋgofierpmi, oažžu son atnit 
	## dušše dan čázis mii sutnje lea mearriduvvon.

SELECT (@OBJ) IF (*-1 ("deavdit") BARRIER NPNHA);
	## Go mánná lea deavdán 12 jagi,..

SELECT (@OBJ) IF (0 TIME)(*1 ("deavdit") BARRIER NOT-ADV-PCLE);
	## ...maŋŋá go lea 16 jagi deavdán.

SELECT (@OBJ) IF (1 (V-TRANS-ACT Sup) LINK NOT *1 (@OBJ) BARRIER CS OR CC);
	## Boađe sisa gáfe jugažit.

SELECT (@OBJ) IF (1 (TV Actio Loc))(NOT 1 N)(NOT *2 (@OBJ) BARRIER CS);
	## Manin don gielddát mu dan muitaleames?
 # This rule would work just fine if it were not for the fact that the 
 # verbal actio "-eames" (instead of "-eamis") is often used where we 
 # would expect a nominal actio.

SELECT (@OBJ) IF (-1 ("ollu") LINK *-1 V-TRANS-ACT BARRIER NOT-ADV-PCLE)
	(0 TIME)(1 NP-BORDER);
	## Dat gáibida ollu áiggi.

REMOVE (@GN>) IF (1 (PUNCT LEFT) LINK *1 (PUNCT RIGHT) LINK 1 NOT-NP);
	## Molssaevttolaš dálkkasdiehtaga našunála dutkanguovddáš ja Sámi 
	## dearvvašvuođadutkanguovddáš (goappašat fágabirrasat gullet Romssa 
	## universitehta vuollái) berrejit sáhttit oassálastit dán bargui.
	



# Coordinated objects
# - - - - - - - - - -

SELECT (@OBJ) IF (0 TIME)(1 CC)(*2 (@OBJ) BARRIER NPNH);
	## Dat gáibida almmatge sihke ollu áiggi ja resurssaid.

REMOVE (@GN>) IF (1 CC)(2C (@OBJ));
	## Ferte váldit vuhtii máná ági ja láttasvuođa ja muđui eará beliid.

SELECT (@OBJ) IF (-2C (@OBJ))(-1 CC)(1 Gen)(2 Po);
	## Jođiheaddjit galget čuovvolit mearrádusaid ja njuolggadusaid
	## giellageavaheame hárrái.
	
SELECT (@OBJ) IF (*-4 V-TRANS-ACT LINK *1C (@OBJ) LINK 1 CC OR COMMA)(*-1 V-TRANS-ACT BARRIER NPNHA)(0 Nom);
    ## Dákkár máhtuin sáhttá geahpedit guovttekultuvralaš bearrašiid mánáid dili, ja nannet sámi mánáid identitehta.


# Verbal functions were here, was moved to beginning.

# Other grammatical functions
# ---------------------------


# Genitive
# - - - - -

SELECT (@GP>) IF (0 Date)(1C Po);
    ## 01.01.02 rájes stáhta váldá badjelasas spesialistadearvvašvuođabálvalusaid eaiggátvuođa.
REMOVE (@GP>) IF (0 Date)(NOT 1C Po);

SELECT (@GP<) IF (-1C Pr)(0 Date);
REMOVE (@GP<) IF (NOT -1C Pr)(0 Date);
   
REMOVE (@GN>) IF (1 CC)(2 ("dat" Gen));
	## Romssa universitehta sámi strategiijaplána ja dan ruhtadeami...

SELECT (@GN>) IF (NOT 0 Dem OR Interr OR PrsPrc OR (@ADVL))(*1C (@SUBJ) BARRIER NPNH)
	(NOT 1 CC OR COMMA);
	## Romssa universitehta sámi studeanttaid mátkedoarjjaortnet lea 
	## máŋga jagi leamaš mielde ásaheame...		# 4 hits here!!

REMOVE (@GN>) IF (0 (@TITLE));
	## ...maid profehta Jeremia lei sárdnon.

REMOVE (@GP<) IF (*-1C Po BARRIER NPNH);
	## Son lea dat gean birra profehta Jesaja sardnu.

REMOVE (@GP<) IF (NOT *-1 Pr BARRIER NPNHA);
	## ...ovdalgo 1883 láhka ásahuvvui.
	
#REMOVE (@GP<) IF (0 (Pron Attr))(NOT -1 ("earet") LINK 0 ("eará") OR ("eara"));

SELECT (@GN>) IF (-1C (Num Gen))(NOT 0 Loc)(1 N);
	## Unnimus 135 jagi geavahus lea doarvái.

SELECT (@GN>) IF (NOT *-1 V-TRANS-ACT BARRIER CS)(0 N)(1 N)(*2 V-TRANS-ACT BARRIER
	V-NOT-AUX OR CC OR CS OR CP)(*2 (@OBJ) BARRIER CS OR CP);
	## Máŋga dehálaš servodatásahusa Norgga servodagas leat jitnosit dovddastan 
	## morálalaš ja politihkalaš ovddasvástádusaset duolbmamiin.

REMOVE (@OBJ) IF (-2 (A Attr) LINK NOT *-1 V-TRANS-ACT BARRIER CC OR CS OR CP)
	(-1 (N Actio));
	## ...ja nu leat mielde oppalaš gelbbolašvuođahuksemis
	## eamiálbmogiid saji ja vuoigatvuođaid ektui.
 # The Actio is nominal if preceded by (A Attr). Then a following noun is not 
 # the object of that Actio.

SELECT (@GN>) IF (NOT -1 NP-BORDER)(NOT -1 TIME)(0 TIME)(NOT 0 ("geardi"))(1 NOTIME);
	## Nugo namuhuvvui, lea nannoseamos ágga dása guhkes áiggi geavahus. 
	
REMOVE (@ADVL) IF (0 TIME OR ROUTE)(1 ("loahppa") OR ("álgu"));
    ## Jagi loahpas álggiimet ođđa turnusiin, bargit ledje fárus dán barggus ovttasráđiid luohttámušolbmuiguin.

SELECT (@GN>) IF (0 Gen)(1 ("veahkki" Sg Com));
    ## Sámi ássiin lea nana árbevierru veahkehit ieš iežas persovnnalaš fierpmádagaid vehkiin.

# Attributes and quantifiers
# - - - - - - - - - - - - - -

SELECT Attr IF (0 (Prop Mal) OR (Prop Fem) OR (Prop Sur))
	(1 (Prop Sur) OR (Prop Mal) OR (Prop Fem))(1 (@SPRED));
	## Mun lean Marit Julien. Mun lean Anne Marit Julien. Mun lean Anne Marit Julien Hansen.

SELECT (@QN>) IF (0 Num)(*1 (@OBJ) BARRIER NPNH);
	## Dat gáibidii mis 5000 ruvnno.

SELECT (@QN>) IF (1 ("jahkásaš"));
	## Stuoret mánná, viđa-guđa jahkásaš, gal riide.

SELECT (@AN>) IF (0 ("veara"))(1 N);
	## Dattetge ii leat muitaluvvon makkárge namahan veara geavahanriiddu 
	## birra dálolaččaid ja boazodoalu gaskkas.

REMOVE (@AN>) IF (NOT *1 N OR A BARRIER NPNH);
	## Sii eat sáhte čielgasit diehtán ahte... 

SELECT (@NumN<) IF (-2 (@NumN<))(-1 CC);
	## ...gč. dearvvašvuođabargiidlága kapihttal 6 ja 7.
	
REMOVE (@QN>) IF (NOT 0 ("okta" Ess) LINK *1 Ess BARRIER NPNH)(0 (@ADVL))
    (1 N OR A OR Pron) ;	
	## Ráđđehus oktii áigodagas galgá bidjat ovdan dieđáhusa Stuorradiggái

SELECT  (@QN>) IF (0 ("okta" Ess) LINK *1 Ess BARRIER NPNH) ;
    ## Son vuodjá oktan guollin.
    
REMOVE (@QN>) IF (0 Nom)(1 ("dbm") OR ("diibmu"))(2 Num);

SELECT (@NQ<) IF (*-1 Num BARRIER NOT-ADJ)(0 (N Gen Sg));
    ## Skuvlajagis 2001/2002 leat oktiibuot 469 oahppi suohkana vuođđoskuvllain.
    # We could get problems if the numeral wants to stand on its own.

REMOVE (@NQ<) IF (0 ("nubbi" A Ord Sg Gen @NQ<));
	## Artihkkal 13 vuosttaš ja nuppi lađđasa čuoggáin 14.

REMOVE @SUBJ-QH IF (-1 N)(0 (@NumN<))(NOT *1 N BARRIER NOT-ADJ-COMMA);
	## 


# Adverbials
# - - - - - -

SELECT (@ADVL) IF (0 (@SUBJ))(*-1C (@SUBJ) BARRIER CS OR CP OR CC);
	## Boazu dáidá buot ovddimus leat fievrun adnojuvvot dan láhkái ahte...
 
SELECT (@ADVL) IF (NOT 0 ("okta" Ess) LINK *1 Ess BARRIER NPNH)(0 Num OR Date)
    (*-1 V-NOT-AUX LINK 0 IV);
	## Fierbmi bistá guokte-golbma jagi.

SELECT (@ADVL) IF (*-2 (@OBJ) BARRIER CC OR CLB OR CS OR CP OR Pr)(-1 NUMERALS)
	(0 TIME OR MEASURE);
	## Mii doalvvuimet ealu guokte miilla dan beaivve. 

SELECT (@ADVL) IF (*-1 INF-VERB BARRIER NOT-ADV-PCLE)(0 Num)(1 TIME)
	(*2 Inf BARRIER NOT-ADV-PCLE);
	## ...mas sáhttá ain máŋga jagi addit gieldda ássiide boaldin...

SELECT (@ADVL) IF (*-1 (@OBJ) BARRIER NOT-ADV-PCLE OR go)(0 Num)(1 TIME);
	## Go fylkkadoavttir lea seailluhan journála 10 jagi, de...

SELECT (@ADVL) IF (0 ("diibmu") OR ("dbm"))(1 Num);
	## Mun boađán diibmu vihtta.

SELECT (@ADVL) IF (NOT *-1 ("deavdit") BARRIER VERB OR CC OR CLB OR CS OR CP)
	(-1 Num LINK NOT 0 (@GP<))(0 TIME LINK NOT 0 Nom)
	(NOT *1 ("deavdit") BARRIER NOT-ADV-PCLE)(1C NOT-NP)(NOT 1 ("dassá"));
	## Dábálaš noađđeráidduin vázzá olmmoš guokte- golbma tiimmu ovdal go
	## suhpoda.

SELECT (@ADVL) IF (0 TIME)(1 TIME);
	## Galggat golmma geardde beaivvis borrat ovtta tableahta.

SELECT (@ADVL) IF (*-1 VERB BARRIER NOT-ADV-PCLE)(0 ("jahki"))(1 Num);
	## ...ja ráhkadii jagi 1991 fylkkagieldda sámi dearvvašvuođa- ja 
	## sosiálaplána.

REMOVE (@SUBJ) IF (0 (@ADVL))(*1C (@SUBJ) BARRIER CC OR CLB OR CS OR CP);
	## Mo dal juo dilli leašge,...
	
#REMOVE (@GN>) IF (1C (@ADVL));
REMOVE (@GN>) IF (1C Adv);
    ## Sii manne eará luotta ruoktot eatnamasaset.	


# Various
# - - - -

REMOVE (@SUBJ) IF (*-1 (@SUBJ) BARRIER CC OR CS OR CLB LINK NOT 0 Gen)(0 Gen);
	## Salomo, gean eadni lei Uria áhkká,...

REMOVE (@OBJ) IF (0 Gen)(*1 Ill BARRIER NPNH LINK *1 V-TRANS-ACT BARRIER NOT-ADV-PCLE
	OR PUNCT OR CLB LINK 0 V-NOT-AUX LINK *1 Inf BARRIER NOT-ADV-PCLE);
	## Eamiálbmotrievtti guovddášosiide gullá maiddái sihkkarastit (dahje 
	## milloseappot jođálmahttit) kultuvrra earáláhkai.

REMOVE (@OBJ) IF (NOT *-1 V-TRANS-ACT BARRIER CC OR CLB OR CP OR CS)
	(*1 V-TRANS-ACT BARRIER CLB OR CS OR CP OR CC LINK 0 AUX 
	LINK *1 V-TRANS-ACT BARRIER NOT-ADV-PCLE LINK 0 V-MAIN LINK *1C (@OBJ) 
	BARRIER CLB OR CC OR CS OR CP);
	## Ii Finnmárkku eatnamiid oktasaš hálddašanortnegii sáhte laktit guovlluid, ...

REMOVE (@OBJ) IF (0 Gen)(*1 (@SUBJ) BARRIER NPNH LINK *1 ("gullat") BARRIER V-MAIN OR 
	CLB OR CC OR CS OR CP LINK *1 Ill BARRIER CLB OR CC OR CS OR CP);
	## ...maid oamastanvuoigatvuohta gullá konvenšuvnna vuođul sápmelaččaide.

SELECT (@GP<) IF (*-1 Pr BARRIER NPNH);
	## Dalle bisttii luossaguolli miehtá dálvvi bearaš atnui.

SELECT (@HNOUN) IF (*-1 BOS BARRIER NPNH)(1 EOS);
	## Riektejoavkku nammadeami duogáš.

REMOVE (@ADVL) IF (0 TITLE OR PROFESSION)(1 Mal OR Fem OR Sur);
	## maiddái professoriidda Asbjørn Kjønstad ja Kristian Andenæs.


# ----------- #
  CONSTRAINTS #27
# ----------- #

REMOVE (@+FAUXV) ;


REMOVE (@SPRED) ; #IF (NOT 0 PrfPrc);


REMOVE (@OPRED) ;

#REMOVE (@X) IF (0 (@+FAUXV) OR (@+FMAINV) OR (@-FAUXV) OR (@-FMAINV) OR (@-FSUBJ) OR
#                  (@ADV-A) OR (@ADV-ADV) OR (@ADVL) OR (@AN>) OR (@ActioN>) OR (@CC) OR
#                  (@CMPND) OR (@CS) OR (@DN>) OR (@GA>) OR (@GN>) OR (@GP<) OR (@GP>) OR
#                  (@HNOUN) OR (@INTERJ) OR (@NPron<) OR (@NQ<) OR 
#                  (@NUM-PRON) OR (@NumN<) OR (@OBJ) OR (@OPRED) OR (@PCLE) OR 
#                  (@CS-COMPL) OR (@PrcN>) OR (@PronN<) OR (@PronN>) OR 
#                  (@QN<) OR (@QN>) OR (@SPRED) OR
#                  (@SUBJ) OR (@SUBJ-QH) OR (@TITLE) OR (@VOC)) ;



# ===================================================== #
# Post-syntactic morphological disambiguation - Cycle 5 #
# ===================================================== #
                      CONSTRAINTS #28                   #
# ===================================================== #


# Demonstratives

SELECT (Dem Acc @DN>) IF (*1C (N Acc) BARRIER NOT-ADJ);
	## , man šaddet dán ulbmila ollašuhttimis.
    ## Berrejitgo sámi geavaheaddjit fgeavahit dáid fálaldagaid?
    
SELECT (Dem Gen @DN>) IF (*1C (N Gen) BARRIER NOT-ADJ);
SELECT (Dem Sg Nom @DN>) IF (*1C (N Sg Nom) BARRIER NOT-ADJ);
SELECT (Dem Pl Nom @DN>) IF (*1C (N Pl Nom) BARRIER NOT-ADJ);


# Case

REMOVE Nom IF (*1C V-NOT-AUX LINK NOT 0C (@PrcN>) BARRIER CLB OR CC OR CS OR CP OR PUNCT 
	LINK *1C NP-HEAD-NOM BARRIER ("dego") OR CC OR CS OR CP OR CLB 
	LINK NOT 0 Interr)(NOT -1 COMMA LINK -1 (@SPRED));
	## Dakkár buođus gáržo guolli juo nu ahte dat deaddil joddu sisa.


# Adverbs

#SELECT Adv IF (0C (@ADVL));
	## Dainna Máret lávlii čiŋadit.
    # Philisophy: An adverbial is preferably an adverb.

	
# Verbs

REMOVE Sg2 IF (0 Pl3)(*-1 (N Pl @SUBJ));
    ## Girkonjárgga buohcceviessu, sámi hálddašanguovllu gielddat ja Finnmárkku lášmmodahttinguovddáš, mii lea Álttás, galget ovttasbargat dan hárrái.
    
REMOVE Sg2 IF (0 Pl3)(NOT *-1 @SUBJ)(*1 (Pl @SUBJ) BARRIER (V Pl3) LINK NOT *1 (V Pl3));
    ## Ovddasvástádusa galget juohkit ieža sápmelaččat ja báikkálaš eiseválddit.
     
	

# ========================== #
# Tidying up some loose ends #
# ========================== #

REMOVE Gen IF (0 (ACR Nom));
REMOVE Acc IF (0 (ACR Nom));
    ## IT lea dehálaš.

REMOVE Gen IF (0 (Num Nom));
REMOVE Acc IF (0 (Num Nom))(NOT *1 TIME BARRIER NOT-A);
 # This in order to constrain the proliferation of case. If no context has hit, 
 # then be it a stray nominative.

REMOVE (@X) ; # Kein Spur...
 # @X is our "didn't find a syntactic tag -tag.
 # Here we remove it if we have any other suggestion.
 

#=====#
  END #
#=====#



# Notes for future thought:
# =========================

#Add a rule REMOVE (@X) at the end?
