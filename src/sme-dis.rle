# ==================================================================== #
# ==================================================================== #
#        N O R T H E R N   S Á M I   D I S A M B I G U A T O R         #
# ==================================================================== #
# ==================================================================== #

#      Sámi language technology project 2003, University of Tromsø     #



DELIMITERS = "<.>" "<!>" "<?>" ;


# ============= #
# Tags and sets #
# ============= #


SETS # (Remove all lines that start with SETS if you want to use mdis.)

# I define end of clause and beginning of clause in a way so that the file
# may be read both by the CG-2 and the vislcg formalisms.

LIST BOS = (>>>) (<s>); LIST EOS = (<<<) (</s>); # vislcg and CG-2 together.
#LIST BOS = (>>>); LIST EOS = (<<<); # end and beg. of sentence. for vislcg.


# Tags declared as single-membered LISTs
# ======================================

# Parts of speech

LIST N = N ; LIST A = A ; LIST Adv = Adv ; LIST V = V ; LIST Pron = Pron ;
LIST CS = CS ; LIST CC = CC ; LIST Adp = Adp ; LIST Po = Po ; LIST Pr = Pr ;
LIST Pcle = Pcle ; LIST Num = Num ; LIST Prop = Prop ;


# POS sub-categories

LIST Pers = Pers ; LIST Dem = Dem ; LIST Interr = Interr ;
LIST Indef = Indef ;
LIST Recipr = Recipr ;      # LIST Rel = Rel ; LIST Refl = Refl ;
LIST Interj = Interj ;      


# Morphosyntactic properties

LIST Ess = Ess ;
LIST Sg = Sg ; LIST Pl = Pl ;
LIST Nom = Nom ; LIST Acc = Acc ;
#LIST Ill = Ill ;
LIST Loc = Loc ; LIST Com = Com ;
LIST Gen = Gen ;

#LIST Cmpnd = Cmpnd ;

#LIST PxSg1 = PxSg1 ; LIST PxSg2 = PxSg2 ; LIST PxSg3 = PxSg3 ; LIST
#PxDu1 = PxDu1 ; LIST PxDu2 = PxDu2 ; LIST PxDu3 = PxDu3 ; LIST PxPl1 =
#PxPl1 ; LIST PxPl2 = PxPl2 ; LIST PxPl3 = PxPl3 ;

LIST Comp = Comp ; LIST Superl = Superl ;

LIST Attr = Attr ; LIST Clt = Clt ; LIST Card = Card ;

LIST Pass = Pass ;
LIST Prt = Prt; LIST Prs = Prs ; 

LIST Ind = Ind ; LIST Pot = Pot ; LIST Cond = Cond ;
LIST Imprt = Imprt ; LIST ImprtII = ImprtII ;

LIST Sg1 = Sg1 ;  LIST Sg2 = Sg2 ; LIST Sg3 = Sg3 ;
LIST Du1 = Du1 ;  # LIST Du2 = Du2 ; LIST Du3 = Du3 ;  # Not using the ones that
LIST Pl1 = Pl1 ;  LIST Pl3 = Pl3 ; #LIST Pl2 = Pl2 ;   # do not have hom. forms

LIST Inf = Inf ; LIST ConNeg = ConNeg ; LIST Neg = Neg ;
LIST PrfPrc = PrfPrc ; LIST VGen = VGen ;

LIST Act = Act ;
#LIST Ger = Ger ; 
#LIST PrsPrc = PrsPrc ; LIST Sup = Sup ; 
#LIST VAbess = VAbess ; LIST Actio = Actio ; LIST Actor = Actor ;

LIST ABBR = ABBR ; LIST CLB = CLB ;

#LIST PUNCT = PUNCT ;

# Derivational tags

#LIST adda = adda ; LIST alla = alla ; LIST d = d ; LIST eaddji = eaddji ;
#LIST eamos1 = eamos1 ; LIST eapmi = eapmi ; LIST g = g ;
#LIST geahtes = geahtes ; LIST h = h ; LIST asti = asti ; LIST heapmi = heapmi ;
#LIST hudda = hudda ; LIST huhtti = huhtti ; LIST huvva = huvva ; LIST j = j ;
#LIST l = l ; LIST las1 = las1 ; LIST ahtti = ahtti ; LIST s1 = s1 ;
#LIST meahttun = meahttun ; LIST mus1 = mus1 ; LIST st = st ;
#LIST stuvva = stuvva ; LIST us = us ; LIST vuohta = vuohta ;
#LIST goahti = goahti ; LIST lágan = lágan ; LIST Dimin = Dimin ;


LIST OKTA = "okta"; LIST go = "go" ; 
# These single-word sets do not work, BUT THEY SHOULD!
# Try again, they are changed now, and refer to inflected form, not to lexeme.

LIST COMMA = "<,>" ;
LIST MANGA = "mán1ga" "galle" ;





# Tag sets
# ========

SETS

LIST WORD = N A Adv V Pron CS CC Adp Po Pr Interj Pcle Num ; # any word


# Derivational affixes
# --------------------

LIST V-DER = adda alla d g h asti hudda huhtti huvva j l ahtti st
              stuvva goahti ;
LIST N-DER = eaddji eamos1 eapmi heapmi s1 mus1 us vuohta ;
LIST A-DER = geahtes lágan las1 meahttun ;

#SET DER = V-DER | N-DER | A-DER ;


# Case sets
# ---------

LIST ADVLCASE = Ill Loc Com Ess ;


# Noun sets
# ---------

LIST N-SG-NOM = (N Sg Nom); LIST N-PL-NOM = (N Pl Nom);
LIST N-NOM = (N Nom) ; LIST N-GEN = (N Gen);

SET LEX-N = N - N-DER;

LIST PROP = (N Prop);
SET CNOUN = N - Prop;
SET CNOUN-PL-NOM = (N Pl Nom) - Prop ;

SET N-NON-GEN = N - (N Gen) ;

# Verb sets
# ---------

# Finiteness and mood
# - - - - - - - - - -
SET V-IND-FIN = Prs | Prt ; # Problem: "In boahtán" is an invisible indicative

SET VFIN = Ind | Pot | Imprt | ImprtII | Cond ;

SET VFIN-NOT-IMP =  Ind | Pot | Cond ;

# Person
# - - - 

LIST V-SG1 = (V  Ind Prs Sg1) (V Ind Prt Sg1) (V Cond Prs Sg1) 
             (V Cond Prt Sg1) (V Pot Prs Sg1) (V  Neg Ind Sg1); 
LIST V-SG2 = (V  Ind Prs Sg2) (V Ind Prt Sg2) (V Cond Prs Sg2) 
             (V Cond Prt Sg2) (V Pot Prs Sg2) (V  Neg Ind Sg2); 
LIST V-SG3 = (V  Ind Prs Sg3) (V Ind Prt Sg3) (V Cond Prs Sg3) 
             (V Cond Prt Sg3) (V Pot Prs Sg3) (V  Neg Ind Sg3); 
LIST V-DU1 = (V  Ind Prs Du1) (V Ind Prt Du1) (V Cond Prs Du1) 
	     (V Cond Prt Du1) (V Pot Prs Du1) (V  Neg Ind Du1); 
LIST V-DU2 = (V  Ind Prs Du2) (V Ind Prt Du2) (V Cond Prs Du2) 
	     (V Cond Prt Du2) (V Pot Prs Du2) (V  Neg Ind Du2); 
LIST V-DU3 = (V  Ind Prs Du3) (V Ind Prt Du3) (V Cond Prs Du3) 
	     (V Cond Prt Du3) (V Pot Prs Du3) (V  Neg Ind Du3); 
LIST V-PL1 = (V  Ind Prs Pl1) (V Ind Prt Pl1) (V Cond Prs Pl1) 
             (V Cond Prt Pl1) (V Pot Prs Pl1) (V  Neg Ind Pl1); 
LIST V-PL2 = (V  Ind Prs Pl2) (V Ind Prt Pl2) (V Cond Prs Pl2) 
             (V Cond Prt Pl2) (V Pot Prs Pl2) (V  Neg Ind Pl2); 
LIST V-PL3 = (V  Ind Prs Pl3) (V Ind Prt Pl3) (V Cond Prs Pl3) 
             (V Cond Prt Pl3) (V Pot Prs Pl3) (V  Neg Ind Pl3); 

# Some subsets of the VFIN sets
# - - - - - - - - - - - - - - -

SET V-DU-PL = V-DU1 | V-DU2 | V-DU3 | V-PL1 | V-PL2 | V-PL3 ;

SET V-NOT-SG3 = V-SG1 | V-SG2 | V-DU1 | V-DU2 | V-DU3 |
                V-PL1 | V-PL2 | V-PL3 ;

SET V-1-2 = V-SG1 | V-SG2 | V-DU1 | V-DU2 | V-PL1 | V-PL2 ;

SET V-NOT-SG1 = VFIN-NOT-IMP - V-SG1 ;


# Copula sets (these ones need to be rewritten)

SET LEAN = ("leat" V Ind Prs Sg1)|("leat" V PrfPrc);
SET LEAI = ("leat" V Ind Prt Sg3)|("leat" V Imprt Prs Sg3);
SET LEAT = ("leat" V Ind Prs Pl1)|("leat" V Ind Prs Pl3)|
           ("leat" V Ind Prs Sg2)|("leat" V Inf)|("leat" V Ind Prs ConNeg);

SET LEAT-COP = ("leat" V );
SET LEAT-DU-PL = ("leat" V Du1)|("leat" V Du2)|("leat" V Du3)|
                 ("leat" V Pl1)|("leat" V Pl2)|("leat" V Pl3);
SET LEAT-DU3-PL3 = ("leat" V Du3) | ("leat" V Pl3);
LIST LEAT-FIN-NON-IMP = ("leat" V Ind) ("leat" V Imp) ("leat" V Cond)
                        ("leat" V Pot); # Check!

LIST COPULAS = "leat" "s1addat" ;

SET VERB = V - (V N);   # V is all readings with a V tag in them, VERB should
                        # be the ones withoud an N tag following the V

#SET V-IND-FIN-NON-COP =  V-IND-FIN - LEAT-COP  ;

SET V-NON-COP = V - LEAT-COP ;        # SET V-NON-COP = VERB - LEAT-COP ;
SET LEX-V = VERB - V-DER;             # VERB but not V-DER

SET AUX = LEAT-COP | Neg ;

SET V-NON-AUX = V - AUX ;

SET VFIN-NON-AUX = VFIN - AUX ;


SET V-DER-PASS = V + V-DER + Pass;    # + is intersection, this should work
#SET V-PASS = V + Pass - V-DER;       # hmm, what was the idea here?
SET V-PASS = (V Pass);                # better?


# Pronoun sets
# ------------
SETS

SET MUN = (Pron Pers Sg1 Nom);
SET DON = (Pron Pers Sg2 Nom);
SET SON = (Pron Pers Sg3 Nom);
SET MOAI = (Pron Pers Du1 Nom);
SET DOAI = (Pron Pers Du2 Nom);
SET SOAI = (Pron Pers Du3 Nom);
SET MII-PERS = (Pron Pers Pl1 Nom);
SET DII = (Pron Pers Pl2 Nom);
SET SII = (Pron Pers Pl3 Nom);

SET PPRON-DU-PL = MOAI | DOAI | SOAI | MII-PERS | DII | SII ;

#SET PRON-PL-LOC = (Pron Pers Pl1 Loc)|(Pron Pers Pl2 Loc)|(Pron Pers Pl3 Loc);

#SET MII-INTERR = ("mii" Pron Interr Sg Nom);




SET DEM-SG = (Pron Dem Sg Nom);
SET DEM-PL = (Pron Dem Pl Nom);



# Adjective sets
# --------------

SET A-ATTR = ( A Attr) ;

SET LEX-A = A - A-DER ;



# Sets for adverbials
# -------------------

SET LEX-ADV = Adv - A ;

LIST IKTE = "ikte" "dolin" "dovle" ;



# Sets for closed classes
# -----------------------
#SET PPOS = Adp | Po | Pr ;

LIST POST-A = "rájes" ;
LIST POST-B = "rádjái" ;

#LIST WACKERNAGEL = "go" "ge" ;



# Sets of specific words
# ----------------------

# Sets for verbs choosing oblique objects
# - - - - - - - - - - - - - - - - - - - - 

LIST LOC-VERB = "ballat" "beros1tit" "bihtit" "ceavzit" "c1uoc1c1ut"
                "dolkat" "fuollat" "geargat" "heaitit" "nohkkot" "váruhit" ;

LIST COM-VERB = "árvalit" "árvvohus1s1at" "riidalit" "bargat"
                "bártas1uvvat" "háladig" "humadit" "náitalit" "ságastaddat"
                "ságastallat" "hilbos1it" "meannudit" ;

LIST ILL-VERB = "áibbas1it" "dorvvastit" "duhtat" "jáhkkit" "liikot"
                "luohttit" "oskut" "suhttat" "álgit" "gullat" "guoskat"
                "heivet" "irggástallat" "riepmat" "soahpat" ;


# Sets for adverbs that have lookalikes
# - - - - - - - - - - - - - - - - - - -
# Here comes some adverbs that have identical twins in other POS. If these
# are found in Adv contexts, we treat them as adverbs.

LIST ADV-NOT-VERB = "dus1s1e"  ;

LIST ADV-NOT-NOUN = "easka" ;

LIST ADV-NOT-NAMES = "Ain" "Anne" "De" "Diego" "Dieppe" "Enge" "Galle" "Haga"
                      "Joba" "Johan" "Liikka" "Mai" "Mannes" "Mo" "Mot" "Naba"
                      "Nan" "Oktan" "Sierra" "Sokka" "Villa" ;

SET ADV-NOT-OTHER-POS = ADV-NOT-VERB | ADV-NOT-NOUN | ADV-NOT-NAMES ;



# Sets for nouns with common syntactic behaviour
# - - - - - - - - - - - - - - - - - - - - - - - 

LIST MANNU = "od1d1a/jage/mánnu" "od1d1a/jagi/mánnu" 
             "guovva/mánnu" 
             "njukc1a/mánnu" "njuvc1c1a/mánnu" "márs/mánnu" 
             "cuon1o/mánnu" "cuon1os/mánnu" "gáranas/mánnu" 
             "miesse/mánnu" "mái/mánnu" 
             "geasse/mánnu" 
             "suoidne/mánnu" 
             "borge/mánnu" "s1njilc1a/mánnu" "s1njilz1a/mánnu" 
             "c1akc1a/mánnu" 
             "golggot/mánnu" 
             "skábma/mánnu" "november/mánnu" "ritne/mánnu" 
             "juovla/mánnu" ;

LIST GUOVLU = "guovlu" "ássan/guovlu" "gávpot" "sádji" "viessu" "goahti" ;

LIST GEASSET = "dálvet" "dálvit" "gid1d1at" "geasset" "geassit" "c1akc1at" ;

LIST INITIAL =  "a" "b" "c" "d" "e" "f" "g" "h" "i" "j" "k" "l" "m"
                "n" "o" "p" "q" "r" "s" "t" "u" "v" "w" "x" "y" "z"
                "á" "æ" "ø" "å" "ö" "ä" ;

LIST PROSEANTA = "proseanta" "%" ;


# Miscellanious sets
# ------------------

SET NUMERALS = Num - OKTA ;
#SET COMP = (Pron Interr);


# Syntactic sets
# --------------

SETS

LIST NP-HEAD = Pron N ;
SET NP-HEAD-SG = (Pron Sg) | (N Sg);
SET NP-HEAD-PL = (Pron Pl) | (N Pl);
SET NP-HEAD-SG-NOM = (Pron Sg Nom) | (N Sg Nom);
SET NP-HEAD-PL-NOM = (Pron Pl Nom) | (N Pl Nom);

SET PRE-NP-HEAD = (Prop Attr) | A | (Pron Pers Gen) | (N Gen) |
                  (Pron Dem)  | (Pron Refl Gen) | CC ;

# There must be a sensible naming convention for these sets...
SET NPNH = WORD - PRE-NP-HEAD ;                 # NPNH = "NON-PRE-NP-HEAD". 

SET NPNHA = WORD - PRE-NP-HEAD - Adv ;     # NPNHA = "NON-PRE-NP-HEAD-OR-ADV"
SET NPNHAI = WORD - PRE-NP-HEAD - Adv - Indef ; # NPNHAI " "...-OR-INDEF"

SET NON-ADV = WORD - Adv;
SET NON-ADV-PCLE = WORD - Adv - Pcle ;
SET NON-ADV-INDEF = WORD - Adv - Indef ;
SET NON-ADV-PCLE-INDEF = WORD - Adv - Pcle - Indef ;

SET NON-A   = WORD - A ;
SET NON-DEM = WORD - Dem ;

SET NON-N = WORD - N ;
SET A-CASE = A - Attr - Adv ;

LIST A-CC = A CC ;

SET NON-ADJ-CC = WORD - A-CC ;

SET NON-NP = VFIN | Adv | Pcle | Interj | CS | CC ;

SET NP-BORDER = BOS | EOS | CLB | Adv | V | Pers | CS | Recipr | Dem |
                 Po | Pr  | Adp ;
   # too risky to use CLB, need more sophisticated way.
   # this allows for NP-internal commas to break NP.

LIST CONTRA = "muhto" ; # In lean muitalan, muhto dál muitalan.




# ==================== #
# ==================== #
# Disambiguation rules #
# ==================== #
# ==================== #

# ===================================
# One-cohort disambiguation - cycle 0
# ===================================

CONSTRAINTS


# Proper nouns
# ------------

REMOVE (Prop Pl) IF (0 (Prop Sg));                   ## Marit lea dál.
  # Marit N Sg, not Mari+t Nom Pl

REMOVE (Prop Pl) IF (-1 BOS)(0 CNOUN-PL-NOM);        ## Sámit leat álgoálbmot.

# Removing propernouns that are lookalikes
# ----------------------------------------

REMOVE ("Lea") IF (-1 BOS)(NOT 1 V-SG3); ## Lea dus1s1e okta Ipmil. Lea idja.

REMOVE ("Man") IF (-1 BOS)(0 (V Imprt));
  # Cannot think of sensible conditions

# Lexicalised adverbs
# -------------------
REMOVE (A Adv) IF (0 LEX-ADV) ;#REMOVE A-ADV IF ( 0 Adv ) ;


# Lexicalised derivations
# -----------------------

REMOVE N-DER IF (0 LEX-N); # SELECT instead of REMOVE if one wants to
REMOVE A-DER IF (0 LEX-A); # investigate word structure
REMOVE V-DER IF (0 LEX-V); # THIS IS PROBABLY TOO STRONG, MSC may differ

REMOVE V-DER-PASS IF (0 V-PASS);
# It might be that this one is no longer needed.


# Lexicalised p-positions
# -----------------------

REMOVE N IF (0 Pr OR Adp)(*1 (N Gen) OR (Pron Pers Gen) BARRIER NPNH);
REMOVE N IF (0 Po OR Adp)(-1 (N Gen) OR (Pron Pers Gen)); # dan várás, dan áigi



# ===================================== #
# Local disambiguation - cycles 1 and 2 #
# ===================================== #

# --------------------------------------------------------- #
# Cycle 1a: cases that will be needed in careful mode later #
# --------------------------------------------------------- #
                             CONSTRAINTS                    #
# --------------------------------------------------------- #
# The idea with cycle 1a is to disambiguate cases that are judged as easier and
# more secure than later cases, and that at the same may be used as context
# for other rules in later cycles.






# Personal pronouns
# =================

SELECT Pers IF (*-1 V-SG3)(0 (Pron Pers Sg3 Nom)); # leago son? (not Pcle)
SELECT Pers IF (*1 V-SG3)(0 (Pron Pers Sg3 Nom));  # de son lea.(not Pcle)

SELECT Pers IF (*-1 V-SG2)(0 (Pron Pers Sg2 Nom)); # leatgo don  (not dem)
SELECT Pers IF (*1 V-SG2)(0 (Pron Pers Sg2 Nom));  # de don leat (not dem)



# Px constraints
# ==============

# First select some clear Px cases
# Let these come directly after local disamb


SELECT (PxSg1) IF (-1 V-SG1)(NOT -1 ("leat"))(-2 MUN);
SELECT (PxSg2) IF (-1 V-SG2)(NOT -1 ("leat"))(-2 DON);
SELECT (PxSg3) IF (-1 V-SG3)(NOT -1 ("leat"))(-2 SON);

# Px for names for relatives

# Px for diminutives


# Certain verb forms
# ==================


# Pret Sg2
# --------

SELECT (V Ind Prt Sg2) IF (*-1 DON BARRIER V-SG2);
                                 ## Amma don áiggut guollegáhkuid ráhkadit.






# ---------------------------------------------------------------- #
# Cycle 1b: removing the readings that were left from the 1a rules #
# ---------------------------------------------------------------- #
                             CONSTRAINTS                           #
# ---------------------------------------------------------------- #

# Removing all remaining Px
REMOVE (PxSg1) ; # IF (0 Ess); # yes, kill'em all
REMOVE (PxSg2) ; # IF (0 Pl);
REMOVE (PxSg3) ; # IF (0 Loc);
# Note: Perhaps rather allow Px only for some key nouns (mother, father, etc).

# Removing remaining Prt Sg2 (philosophy: these are not pro-drop)
REMOVE (V Ind Prt Sg2) IF (NOT -1 BOS) (NOT 0 (Clt));
# Proseassas láhc1c1ojuvvojit dilálas1vuod1at nu, ahte 
# fertejitgo dahkat dan?




# --------------------------------------------
# Cycle 2: Other cases of local disambiguation
# --------------------------------------------


# This is the main part of the local disambiguation.
# Here we go through the grammar in a systematic way.
# Cases that should be handled before or after the main disambiguation, will be
# taken care of in cycoes before or after this one.




# Noun or not?
# ============

# Based on left context
# ---------------------

SELECT N IF (-1C (A Attr));                     ## Dat lea c1uovvovas1 sajis.

SELECT N IF (-2C N) (-1 CC) (NOT 1 N OR A);     ## Mánát ja nuorat leat dál.

SELECT N IF (1 COMMA)(2 (Pron Interr));         ## Doaimmat, maiguin mun barggan.
                                                 # too specific!

SELECT N IF (-2 BOS)(-1C Gen)(1 EOS);           ## Son lea Ruot1a c1álli.
                                                   #(titles)

REMOVE A IF (-1 Dem)(1 (Pron Interr));     ## Son lea dan sápmelas1 gean dovddan.


REMOVE (A Sg Nom) IF (NOT *-1 VFIN)(0 (N Sg Nom))(*1 V-SG3 BARRIER NON-ADV-PCLE);
REMOVE (A Pl Nom) IF (NOT *-1 VFIN)(0 (N Pl Nom))(*1 V-PL3 BARRIER NON-ADV-PCLE);
                     ## Dálá guovlluin sápmelac1c1at eai leat ássan nu guhká.

# Based on right context
# ----------------------

REMOVE A IF (1 Po);                           ## C1oahkkán dárolac1c1aid gaskkas.
REMOVE A IF (1 Adp)(NOT *2 N BARRIER NPNHAI); ## Human sápmelac1c1aid birra.
                                                #  problem with derived V > N


REMOVE V IF (-1 Dem)(1 V-IND-FIN);          ## Dán báhkis s1addá c1áppa gohppu.

REMOVE VFIN IF (-1 BOS)(0 NP-HEAD-SG-NOM)(1C V-SG3);## Lásse lea mus rean1gan.

REMOVE Adv IF (1C Po); 



REMOVE A-CASE IF (*-1 BOS BARRIER NON-DEM)(0 N)(NOT 0 ("buorre"))(1 N);
                                       ## Sápmelac1c1aid historjá lea boaris.
  # No adjective, except buorre, agrees with a head noun in case. The correct
  # reading for a case-inflected prenominal word must thus be N.
  # (Problem: we have not considered a string predicative adj + other NP)

REMOVE (A Gen) IF (0 N)(*1 N BARRIER NON-A);




# ======================= #
# Certain singleton words #
# ======================= #

SELECT Gen IF (0 ("nubbi"))(1 N);
                      ## Son oac1c1ui c1áda ás1s1i fas nuppi Stuorradikkis.





# ====================== #
# Disambiguating adverbs #
# ====================== #
        CONSTRAINTS      #
# ====================== #

SELECT Adv IF (*-1C VFIN-NON-AUX BARRIER NON-ADV)
              (0 A-ATTR)(NOT 1 N);                    # Adv if postverbally

REMOVE Adv IF (-1 (N Gen) OR (Pron Gen))(NOT -1 Adv)
              (0 Po OR Adp);                          # Po if after Gen

SELECT Adv IF (-2 V)(NOT -2 ("leat"))(-1 Adv)(1 EOS); # Too specific!!
                                                     ## Son c1állá hui c1ábbát.

SELECT Adv IF (-1 BOS OR CLB)(0C Adv OR Po);

SELECT Adv IF (0 (Pron Indef))(1 CS);              ## Dat lea nu ahte boad1án.
SELECT Adv IF (0 (Pron Indef))(1 COMMA)(2 CS);     ## Dat lea nu, ahte boad1án.

REMOVE Po IF (-1 NON-NP)(0 Adv)(1 NON-NP OR CLB);  ## Mun in leat mielde.

# Adverbs, not interjections
REMOVE Interj IF (NOT -1 Interj OR BOS)(0 Adv);      ## Dat dinii bures doppe.

SELECT Adv IF (*-1 VFIN) (0 GEASSET);            ## Son lei doppe geasset.
SELECT Adv IF (0 GEASSET)(*1 VFIN);              ## Dálvet son vulggii meahccái.


# Rules related to specific adverbs
# ---------------------------------

# ikte
SELECT Adv IF (0 ("ikte"))(*1 (V Prt));          ## Ikte lei hui buolas1.
SELECT Adv IF (*-1 (V Prt))(0 ("ikte"));         ## Ikte lei hui buolas1.

SELECT Adv IF (0 ("de"))(NOT 1 Prop);      ## De lea vissa ahte De Silva boahtá.

SELECT Adv IF (-1 VFIN)(0 ADV-NOT-OTHER-POS);    ## Lea dus1s1e okta Ipmil.
       # Cycle 1a?

SELECT Adv IF (0 ("goas"))(*1 ("<?>"));          ## Goas heajat leat?
# This is not an optimal rule for getting rid of the former Portuguese colony.

SELECT Adv IF (NOT -1 Dem or (A Attr))(0 ("duo")); ## Áddjá go duo lea boahtime?

SELECT Adv IF (-1 VFIN)(0 ("gusto"));            ## Diet lea gusto ávkkálas1.

#SELECT Adv IF (NOT -1 BOS)(0 ("died1usge"));    ## Mun lean died1usge oaddán.
SELECT Adv IF (0 ("died1usge"));                 ## Mun lean died1usge oaddán.

  # Todo: Make a set of these verb-like adverbs, and make a rule for them.
  # Eventually more sets, if they have different syctactic properties...

SELECT Adv IF (-1 BOS)(0 ("manne"))(1 VFIN);     ## Manne ii leat áviisa boahtán?
SELECT Adv IF (-1 BOS)(0 ("manne"))(1 N OR Pron)(2 VFIN); ## Manne don leat dál?

  # Need a rule for "Manne + NP + VFIN".

SELECT Adv IF (0 ("sullii"))(1 Num);    ## Dat lea sullii 20 kg.
  # This rule must be generalised on both conditions: "sullii" must be extended
  # to a set of adverbs denoting quantification, and Num must be extended to
  # more measures (or parallel rules must be written).

SELECT Adv IF (-1 BOS)(0 ADV-NOT-NAMES);             ## De fidnejin.
  # Evaluate this one. An additional condition
  # (NOT 1 VFIN) was removed, as it was too careful.

# álgu
SELECT N IF (-1 Gen OR (A Attr))(0 ("álgu"));   ## Pros1eavtta álgu ii lean álki.



# ========================= #
# Disambiguating adjectives #
# ========================= #
         CONSTRAINTS        #
# ========================= #

# Attribute disambiguation
# ------------------------


REMOVE (A Nom) IF (0 (A Attr))(*1 N BARRIER NON-A LINK NOT 1 Po OR Adp);
                                     ## Dat lea c1uovvovas1 sajis.


# Rule for predicative attributes
SELECT Attr IF (-3 BOS)(-2 Nom)(-1 ("leat"))(0 Prop)(1  Prop)(2 NP-BORDER);


SELECT (A Attr) IF (1 CC)(2 (A Attr))(3 N); ## Dat lea od1d1a ja vilges gákti.

# Coordinated attributive surnames
SELECT (Prop Attr) IF (NOT -1 Prop)(1 CC)(2 (Prop Attr))(3 (N Prop));
                                      ## Dál leaba Brita ja Ove Kåven.

SELECT A IF (*-1 ("leat" V) BARRIER NON-ADV-INDEF)(1 N OR EOS);
                                     ## Ikte lei hui buolas1.

REMOVE Attr IF (0 A)(NOT 1 A OR CC OR N OR COMMA); ## Vuolgit lea suohtas.
 # feil, feil!! comma?

REMOVE Attr IF (0 A-CASE OR Adv)(1 CC)(NOT *2 N BARRIER (A Attr) OR ("buorre"));
                    ## Guovlu lea 1500 km guhkkosas1 ja 300 km govdosas1.
  # Is there something wrong with this rule?


# special rules for 'buorre' (the only adjective showing case agreement)
# ----------------------------------------------------------------------

SELECT Gen IF (0 ("buorre"))(NOT 0 Comp OR Superl)
              (*1C (N Gen) BARRIER NON-ADJ-CC);

SELECT Acc IF (0 ("buorre"))(NOT 0 Comp OR Superl)
              (*1C (N Acc) BARRIER NON-ADJ-CC);


# Comparatives
# ------------

REMOVE VGen IF (-1 Gen OR Dem)(0 Attr OR Comp OR Superl)(1 N)(2 NP-BORDER);
REMOVE N    IF (-1 Gen OR Dem)(0 Attr OR Comp OR Superl)(1 N)(2 NP-BORDER);
REMOVE Adv  IF (-1 Gen OR Dem)(0 Attr OR Comp OR Superl)(1 N)(2 NP-BORDER);
REMOVE VFIN IF (-1 Gen OR Dem)(0 Attr OR Comp OR Superl)(1 N)(2 NP-BORDER);
## Dat lea died1áhus sámepolitihka bajit linjáid birra.

   # This is a highly specified context. The idea is that the Spec ___ N
   # position should have and Attr or some other adjective. Look out for probl.

REMOVE Gen IF (0 Comp)(1 N)(NOT 1 (N Gen));
REMOVE Acc IF (0 Comp)(1 N)(NOT 1 (N Acc));
REMOVE Nom IF (0 Comp)(1 N)(NOT 1 (N Nom));
## Dat lea died1áhus sámepolitihka bajit linjáid birra.

   # This block of rules is there to ensure case agreement for attributive
   # comparatives.

SELECT Comp IF (*1 ("go") BARRIER NON-N);
            ## Son lea buoret go mun.
            ## Dáz1at navde Finnmárkku buoret guovlun go iez1aset ruovttubáiki.







# ==================== #
# Disambiguating nouns #
# ==================== #
      CONSTRAINTS      #
# ==================== #




# Proper nouns
# ============

SELECT Prop IF (NOT *-1 BOS OR (":") OR (""") BARRIER WORD); # still does
# Capitalised nouns in the middle of a sentence are names.   # not work
# Jesus dajai: "Lea c1állojuvvon: ..."

REMOVE Attr IF (0 Prop)(NOT 1 Prop OR INITIAL);   ## Trond Trosterud lea dál.

# Note:  the rule for selecting attribute reading of surnames is given
#        in the section on adjectives.

SELECT Attr IF (0 Prop)(1 INITIAL)(2 Prop);   ## Mathis K. Sara.

SELECT Attr IF (0 Prop)(1C (N Prop)) ;        ## Rolf Arne Berg.



# Case disambiguation
# ===================

# Genitive or Accusative
# ======================

REMOVE Nom IF (*-1 (Pron Dem Acc) OR (Pron Dem Gen) BARRIER NON-A);
                                   ## Dan ás1s1i ferte dutkat.


# Genitive
# ========

# Genitive as complement
# ----------------------

# Genitive in front of postposition
SELECT Gen IF (NOT 0 Adv)(1 Po) ;    ## Dat lea nieidda dihte, nieidda birra.
  # As it stands, this rule has no conditions. So, what errors will we get?

# Genitive after preposition
SELECT Gen IF (-1 Pr); # Earret mu ii giige lea boahtán.

# Genitive in front of adposition
SELECT Gen IF (1 Adp)(NOT *2 Gen BARRIER NPNH);

# Genitive after adposition
SELECT Gen IF (NOT -2 Gen)(-1 Adp); 

# Genitive after quantifier
SELECT Gen IF (-1 NUMERALS OR MANGA); ## Dat leat golbma nieidda.
                                      ## Dál leat mán1ga nieidda

# Genitive as specifier
# ---------------------

# Genitive in front of noum in the beginning of a sentence
REMOVE (N Acc) IF (-1 NP-BORDER)(*1 N BARRIER NON-A LINK *1 VFIN);
REMOVE (N Nom) IF (-1 NP-BORDER)(*1 N BARRIER NON-A LINK *1 VFIN);
                                         ## Nieidda girji lea ruoksat.

REMOVE (Pron Acc) IF (-1 NP-BORDER)(*1 N BARRIER NON-A LINK *1 VFIN);
REMOVE (Pron Nom) IF (-1 NP-BORDER)(*1 N BARRIER NON-A LINK *1 VFIN);
                                         ## Din viessu s1attai oppa stuoris.

# The 'select' rule was too powerful, we do 'removeä instead.
#SELECT Gen IF (-1 NP-BORDER)(*1 N BARRIER NON-A LINK *1 VFIN);

REMOVE Gen IF (-1 CLB)(1 (Pron Pers))(*1 VFIN); ## Na, du mun lean oaidnán.
REMOVE Gen IF (-1 BOS)(1 (Pron Pers))(*1 VFIN); ## Du mun lean oaidnán.

# Genitive inside a two-part Po
SELECT Gen IF (-1 POST-A)(1 N)(2 POST-B);
## Nu leat 40 buolvva Dávveda rájes Babylonia fán1gavuod1a rádjái.

#REMOVE Acc IF (-1 BOS)(1 (N Gen) OR (Attr) OR PPOS) ;
                                            ## Mu birra don humat.
                        # This should be covered by the others... Check.

# Genitive in front of the final word in the sentence
SELECT Gen IF (0C CNOUN)(*1 (N Acc) BARRIER NPNH)(2 CLB);
                                            ## Oainnán nieidda biilla.
                                            ## Oainnán nieidda biillas.

SELECT Gen IF (0C (Pron Pers))(*1 (N Acc) BARRIER NPNH)(2 CLB);
                                            ## Oainnán su od1d1a biilla.
                    # Unify these? Look at the effect of each and one of them.

SELECT Gen IF (0C (N Prop) )(*1 CNOUN BARRIER NPNH)(2 CLB);
                                            ## Oainnán Uria biilla.

# Genitive relative pronouns
SELECT Gen IF (0 (Pron Interr))(1 N);
                               ## Oidnen mátkkos1teaddji, gean namma lei Keilhau.

# Genitive in time expressions
SELECT Gen IF (0 MANNU)(1 (Num Ord));    ##  Dál lea miessemánu 1. beaivvi.

# Genitive if cardinal number
SELECT Gen IF (0 Card)(1 (N Gen));       ## Dat lea duháhiid jagiid boaris.

# Genitive in proper name sequences
REMOVE Acc IF (0 Prop)(1 Prop);          ## Dat lea Norgga Röros ja Máhte Piera.
  # Hmm, very general indeed!

# Genitive sequences

SELECT Gen IF (NOT -1 V)(0C N)(1C CNOUN) (NOT 2 Po); ##Áhc1i viesu uksa lea alit.

# This is a bad rule. I need more constraints on the noun in pos 1.
# Today, this sentences gets a wrong analysis: ## Bijatgo don sohkkara dedjii?


# Accusative
# ==========


# Accusative between Aux and main verb
# ------------------------------------

# In front of an infinitive
SELECT Acc IF (*-1 V-IND-FIN BARRIER NPNHAI)(NOT 0 PrfPrc)(1 Inf);
                          ## Od1astus addá vejolas1vuod1a muitalit.
                          ## Son lea beassan bargat.

SELECT Acc IF (*-1 V-IND-FIN BARRIER NPNH)(1 CC)
              ( *2 NP-HEAD   BARRIER NPNH LINK 1 Inf);
SELECT Acc IF (*-1 CC BARRIER NPNH LINK -1 NP-HEAD LINK 
              *-1 V-IND-FIN BARRIER NPNH)(1 Inf);

# In front of a participle
SELECT Acc IF (*-1 LEAT-FIN-NON-IMP BARRIER NPNHAI) (1 PrfPrc);
                                                 ## Mun lean dan dahkan.



# Accusative postverbally
# -----------------------

# Object only.

SELECT Acc IF (*-1 V-NON-AUX BARRIER NPNHA)
              (0 NP-HEAD)(NOT 0 PrfPrc or Adv)
              (1 CLB OR Adv OR (A Comp) OR (A Superl) OR ("go") OR CS);
                                 ## Oainnán du.
                                 ## In mun sálten nu olu.
                                 ## Dázat eai atnán sámiid buoret go beanan.
                                 ## Sii badjelgehc1c1e olbmo jos sámiin náitala.

REMOVE Acc IF (NOT *-1 V-NON-AUX BARRIER CS)(NOT *1 V-NON-AUX BARRIER CS);
        ## Sápmelac1c1aid historjá álbmogin lea duháhiid jagiid boaris, muhto
        ## dálá ássanguovlluin sápmelac1c1at eai leat ássan nu guhká.
        ## Eai lean váldán go niestebohccu.
  # We need a verb for an obj. The barrier must be refined, though!
  # Cf the lat exambple, that gives the wrong result.

REMOVE Acc IF (NOT *-1 V-NON-AUX BARRIER CS)(*1 V-PASS BARRIER CS);
REMOVE Acc IF (*-1 V-PASS BARRIER VFIN)(NOT *1 V-NON-AUX);
                            ## Dál nannejuvvui girku sámeguovlluin.

# Coordinated object
  # First part
SELECT Acc IF (*-1 V-NON-AUX BARRIER NPNHA)(0 N)(1 CC)
              ( *2 NP-HEAD   BARRIER NPNHA LINK 1 CLB);
                                       ## Oainnán vilges biilla ja rukses viesu.
  # Second part
SELECT Acc IF (*-1 CC BARRIER NPNH LINK -1 NP-HEAD LINK 
              *-1 V-NON-AUX BARRIER NPNHA)(0 N)(1 CLB);
                                       ## Oainnán vilges biilla ja rukses viesu.

# Accusative for a topicalised verb
# ---------------------------------

REMOVE Gen IF (@1 V-NON-AUX + Clt)(NOT -1 (N Acc))(1 EOS); ## C1ilgego moai dan?
  # This is not a good rule. Have more looks at it.
  # On the other hand side: With the addition (1 EOS)
  # it has become too careful.


REMOVE Gen IF (-1 VFIN)(0 N)(1 CLB); ## Joramii riegádii Ussia, Ussiai riegádii
# The rule is too specific.          ## Jotam.

# Topicalised object
# ------------------

SELECT Acc IF (NOT *-1 VFIN)(NOT 0 Nom)
              (*1 VFIN BARRIER NON-ADV-PCLE LINK NOT *1 Acc);
                                     ## Dan lean mun váldán.


# Object predicatives
# -------------------

SELECT Acc IF (-1 Acc)(0 A)(1 CLB OR EOS OR CS);
                                     ## Dáz1at eai atnán sámiid buoret go beanan.



# Nominative
# ==========

# Nominative in titles and sentence fragments
# - - - - - - - - - - - - - - - - - - - - - -

# A single word is nominative
SELECT Nom IF (-1 BOS)(NOT 0 VFIN)(0 N OR A OR Pron)(1 EOS); ## Katja.

# An NP head with a genitive modifier is nominative
SELECT Nom IF (-2 BOS)(-1 Gen)(0C N)(1 EOS); # .

# Nominative subjects
# - - - - - - - - - -

# Find the subject, and assume it is nominative

SELECT Nom IF (-1 CS OR BOS)(0 Sg)(1C V-SG3); 
                                 ## Died1án, ahte plána movttiidahttá dan.

SELECT Nom IF (-1 CS OR BOS)(0 Pl)(1C V-PL3); 
                                 # ... and in pl..

SELECT Nom IF (-1 BOS)(0 Prop)(1 COMMA); 
                                 ## Biera, du sabehiid!

REMOVE Nom IF (-1 BOS)(NOT 0 (Pron Pers))(1C V-1-2);
                                 ## Dan oainnán viesu duohkin.  
                                 ## Moai c1ilge dan ás1s1i dutnje.

# Coordinated subjects
# --------------------

SELECT (Sg Nom) IF (*-1 CC BARRIER NPNHAI LINK -1C (Sg Nom))(1 NP-BORDER);
  ## Bror Berg ja Per Mikael Utsi leaba ruovttus.

SELECT (Sg Nom) IF (1 CC)(*2C (Sg Nom) BARRIER NPNHAI LINK 1 NP-BORDER);
  ## Per Mikael Utsi ja Bror Berg leaba ruovttus.

# Nominative predicatives
# - - - - - - - - - - - -

  # Here, we handle both nouns and adjectives.


# Case disambiguation of predicatives
# -----------------------------------


SELECT Nom IF (*-1 ("leat" V Sg1) BARRIER NPNH OR PrfPrc
               LINK *-1 MUN BARRIER NON-ADV-PCLE)
              (NOT 0 PrfPrc)(NOT *1 PrfPrc);

SELECT Nom IF (*-1 ("leat" V Sg2) BARRIER NPNH OR PrfPrc
               LINK *-1 DON BARRIER NON-ADV-PCLE)
              (NOT 0 PrfPrc)(NOT *1 PrfPrc);
                                      ## Mun lean Anna. Don leat Anna.

SELECT (Sg Nom) IF (*-1 ("leat" V Sg3) BARRIER NPNH
                LINK *-1 (Sg Nom) BARRIER NON-ADV-PCLE)(NOT 0 PrfPrc or Adv);
                                      ## Tor lea od1d1a oahpaheaddji.

SELECT (Pl Nom) IF (*-1 ("leat" V Pl3) BARRIER NPNH
                LINK *-1 (Pl Nom) BARRIER NON-ADV-PCLE)(NOT 0 PrfPrc);

SELECT Nom IF (-2 Nom)(-1 COPULAS)(0 A)(NOT 0 PrfPrc)(1 EOS OR ("go"));
                                      ## Biilla ivdni lea vielgat.
                                      ## Biilla ivdni lea vielgadeabbo go mielki.

SELECT Nom IF (-2 Nom)(-1 COPULAS)(0 A)(NOT 0 PrfPrc)(1 CC)(2 A);
                                      ## Viessu lea vielgat ja ruksat.

SELECT Nom IF (-4 Nom)(-3 COPULAS)(-2 A)(-1 CC)(0 A)(NOT 0 PrfPrc)(1 EOS);
                                      ## Viessu lea vielgat ja ruksat.


SELECT Nom IF (-3 BOS)(-2 Nom)(-1 ("leat"))(NOT 0 PrfPrc)(1 EOS);
                                           ## Mun lean Bond.
                                           ## Mun lean gánda.


SELECT Nom IF (-2 NP-HEAD-SG-NOM)(-1 ("leat" V Sg3))(NOT 0 PrfPrc or Adv);


SELECT Nom  IF (-4 BOS)(-3 Nom)(-2 ("leat"))(-1 Prop OR (ABBR))(NOT 0 PrfPrc)
               (1 NP-BORDER);

SELECT Nom  IF (*-5 BOS BARRIER NPNH)(-4 Nom)(-3 ("leat"))
                (-2 Prop OR (ABBR))(-1 Prop)(0 Prop)(1 NP-BORDER);

SELECT Nom  IF (*-6 BOS BARRIER NPNH)(-5 Nom)(-4 ("leat"))
                (-3 Prop OR (ABBR))(-2 Prop)(-1 Prop)(0 Prop)(1 NP-BORDER);

                                            ## Mun lean James Bond.
                                            ## Mun in leat James Bond.
                                            ## Mu namma lea Jo Ann Ba Doe.

# Again, these rules are too specific. Need empirical data to enlarge them.
# What is needed is linking both to copula and to clause border.

SELECT Nom IF (-2 NP-HEAD-SG-NOM)(-1 COPULAS + (V Sg))(1 EOS);
                                            ## Tor lea oahpaheaddji.

SELECT (N Sg Nom) IF (*-1 V-SG3 LINK NOT *-1 NP-HEAD-SG-NOM)
                                    (NOT  -1 NP-HEAD-SG-NOM)
                                    (NOT 0 PrfPrc)(1 EOS);


# Nominative as objects in existential clauses
# --------------------------------------------

REMOVE Acc IF (-2 Loc)(-1 ("leat"))(NOT *1 V); # Mus lea golli.
SELECT Nom IF (-2 Loc)(-1 ("leat"))(1 EOS); # Mus lea golli.


# Unclassified
# - - - - - -

SELECT Nom IF (-1 Gen)(NOT -1 (Pron Dem Gen))(0 N-SG-NOM)(1 V-SG3);
                            ## Du ás1s1i lea dehalas1.
                            ## Dan ás1s1i ferte dutkat.
# HMM, brave..
# Note that the tag is selected, the set is in the condition.

# Nominative if two names in a row
SELECT Nom IF (-1 PROP) (0 PROP) (1 V-SG3); # Biret Anne galgá mannat.


# Rules for disambiguation around conjunctions, in coordination
# -------------------------------------------------------------

SELECT Nom IF (-2C N-NOM)(-1 CC)(NOT 1 Gen OR (Prop Attr));
 ## Trond ja Kirsti leat dál. Bror Berg ja Per Mikael Utsi leat ruovttus.
SELECT Nom IF (1 CC)(2C N-NOM); # Kirsti  ja Trond leat dál.

REMOVE Gen IF (0 N)(1 CC)(2 (Pron Sg3 Gen)); ## Jos1iai riegádii.
# Peters and Pauls friend OK.                ## Jekonja ja su vieljat leat dál.

SELECT Nom IF (-2 (Nom Cmpnd)) (-1 CC);
SELECT Gen IF (-2 (Gen Cmpnd)) (-1 CC);
SELECT Acc IF (-2 (Acc Cmpnd)) (-1 CC);


# Nominative plural
# =================
CONSTRAINTS

SELECT (Pl Nom) IF  (*-1 LEAT-DU-PL BARRIER NPNHAI
                LINK *-1 PPRON-DU-PL BARRIER NON-ADV-PCLE);
                                  ## Doai leahppi goappas1agat nu jallat.

SELECT (Pl Nom) IF  (*-1 ("leat" Du3) BARRIER NPNHAI
                LINK *-1 (N Sg Nom) BARRIER NON-ADV-PCLE
                LINK *-1 CC BARRIER NPNH LINK -1 (N Sg Nom));
                                  ## Anne ja Biret leaba goappas1agat nu jallat.

SELECT (Pl Nom) IF  (*-1 ("leat" Pl3) BARRIER NPNHAI
                LINK *-1 (N Sg Nom) BARRIER NON-ADV-PCLE
                LINK *-1 CC BARRIER NPNH LINK -1 (N Sg Nom)
                LINK *-1 COMMA BARRIER NPNH LINK -1 (N Sg Nom));
                                  ## Anne, Biret ja Cathrin leat nu jallat.

SELECT (Pl Nom) IF  (*-1 ("leat" Pl3) BARRIER NPNHAI
                LINK *-1 (N Pl Nom) BARRIER NON-ADV-PCLE);
                                  ## Mánát ge leat buot nu jallat.

# Missing Du3 and Pl3 rules for coordinated subjects and pl subjects


#SELECT (Pl Nom) IF (NOT *-1 (N Pl Nom))(*1 V-PL3 BARRIER ()
#                LINK NOT *-1 CC);     



# Locative singular
# =================

SELECT Loc IF (*-1 BOS BARRIER NPNHAI)(1 ("leat"));
                                          ## Ádjásgo lea od1d1a fanas.
  # This rule is all too general, it must be sharpened in some way.
  # The rule does not handle ## Daningo sámiin lea iez1aset giella.

SELECT Loc IF (-2 BOS)(-1 ("leat" Clt)); ## Leago mus rud1aid?
# hmm...

# Comitative singular
# ===================

# NP-internal disambiguation of Com

SELECT Com IF (-1 (Dem Sg Com));         ## Vuolggán dainna biillain.
SELECT Com IF (-2 (Dem Sg Com))(-1 Adv); ## Vuolggán dainna biillain.


# Disambiguation based upon type of NP

SELECT Com IF (0 Prop); # assuming proper names come in sg


# Disambiguation based upon verb valency
#SELECT Com IF (*-1 COM-VERB)(0 (Sg)) ;
SELECT Com IF (*-1 COM-VERB BARRIER CS)(0 Sg);
                             ## Son náitali nieiddain.

SELECT Com IF (0 Sg)(*1 COM-VERB BARRIER CS);
                             ## Nieiddain son náitali.

# Locative plural
# ===============

# NP-internal disambiguation of Loc

SELECT Loc IF (-1 (Dem Pl Loc));         ## Mii c1ohkkát dain biillain.
SELECT Loc IF (-2 (Dem Pl Loc))(-1 Adv); ## Mii c1ohkkát dain biillain.
# Not too elegant, the rule set might be done more general with a barrier

SELECT (Pl Loc) IF (*-1 CC LINK *-1 (Sg Loc) BARRIER NPNH);
                                     ## Son lea skuvllas ja od1d1a mediain.

SELECT (Pl Loc) IF (1 CC)(*2 (Sg Loc) BARRIER NPNH);
                                     ## Son lea od1d1a mediain ja skuvllas.

SELECT (Pl Loc) IF (-2 Num)(-1 PROSEANTA); ## Dat lea 50 % rud1ain.

SELECT Loc IF (1 ("gos"));           ## Son lea guovlluin gos sámit ásset.
SELECT Loc IF (1 COMMA)(2 ("gos"));  ## Son lea guovlluin, gos sámit ásset.


# Disambiguation based upon type of NP

SELECT (Pl Loc) IF (0 GUOVLU);

# Disambiguation based upon verb valency

SELECT Loc IF (*-1 LOC-VERB BARRIER CS)(0 Pl);
                             ## Son balla nieiddain.

SELECT Loc IF (0 Pl)(*1 LOC-VERB BARRIER CS);
                             ## Nieiddain son balla.


# Loc Pl if NP-complement (A book about the wars)

SELECT Loc IF (*-1 N-NON-GEN BARRIER NPNH)(0 Pl);
        ## Ii leat dus1s1e sáhka olbmuid oktavuod1ain juohkebeaivválas1 eallimis.
# Note: There may be counter examples here, I cannot come up with any.


# ==================== #
# Disambiguating verbs #
# ==================== #
      CONSTRAINTS      #
# ==================== #


# Perfect Participle
# -------------------
# PrfPrc to the right of negated verb
SELECT PrfPrc IF (*-1 Neg BARRIER CONTRA);

SELECT PrfPrc IF (*-1 VFIN BARRIER CLB OR CS)
                 (NOT *-1 (V Ind Sg1) BARRIER CC); ## Mun lean muitalan.
  # Hmm: PrfPrc if there is not a Sg1 to the left, but if there is a CC to
  # the right of a possible Sg1, then we forget about the Sg1.

# Topicalised participle
# ???

# Old rules, remove asap.
#SELECT PrfPrc IF (0 LEAN) (-1 (Neg));
#SELECT PrfPrc IF (*-1 VFIN-NOT-IMP); # Too general!!
#SELECT PrfPrc IF (-1 NP-BORDER)(0 Pass)(1 CNOUN);
                                     



# Finite forms #
# ============ #
   CONSTRAINTS #
# ============ #


# Indicative Negative
# -------------------
  # Unique are Sg1, Sg3, Du1, Du2, Du3, Pl1, Pl2, Pl3
  # hom: "it", where Sg2 = ABBR.

# Select Neg Sg2 instead of ABBR
REMOVE ABBR IF (*-1 ConNeg OR PrfPrc)(0 Neg); ## Dáppe don leamas1 it.
REMOVE ABBR IF (0 Neg)(*1 ConNeg OR PrfPrc);  ## Don it leat dál.


# Indicative Present
# ------------------

# Sg1
# ---
  # Sg1 = PtcPrf (odd-syllables), and Sg1 = V Act (even-syllables)

SELECT Sg1 IF (0 LEAN) (*1 PrfPrc);           ## Mun lean boahtán.
  # So, what kind of barrier could we have here?

SELECT Sg1 IF (*-1 MUN BARRIER Neg OR LEAN);  ## Mun ferten lohkat.

SELECT Sg1 IF (-1 CC)(-2 (V Sg1));              ## Mun human ja human.

#SELECT Sg1 IF (NOT *-1 (V Neg))(NOT *-1 PrfPrc OR Act OR V-SG1);
SELECT Sg1 IF (NOT *-1 VFIN);                 ## Háliidan hupmat.
                                              ## In háliidan hupmat.

# Note: We will need a rule selecting Sg1. # Mun muitalan Suoma birra.






# Sg2
# ---
  # Sg2 = Inf (even syllables, copula)
  # Note the homonymy with derived verbs (jearat = jearadit + ...

SELECT Sg2 IF (0 LEAT) (*-1 DON BARRIER NON-ADV); # no hits

SELECT Sg2 IF (*-1C DON BARRIER Neg)(NOT 0 Imprt); ## fertetgo don vuolgit?

SELECT Sg2 IF (NOT 0 Imprt)(*1C DON BARRIER Neg); ## De, don fertet vuolgit.

# The NOT 0 Imprt part for this example:   ## Gosa don bidjet daid sáiddiid?

# Sg3
# ---
  # Sg3 = ConNeg (even)
  # Sg3 = ConNeg = Imprt Sg2 (contracted)
  # Sg3 = Sg Ill for certain words!!
  # Disambiguating Prs Sg3 is taken care of by rules removing the other 
  # readings whenever their conditions are not fulfilled.



REMOVE Sg3 IF (*-1 Neg)(0 (Sg Ill));    ## Iiba leat skuvlii álgán.






# Du1
# ---
  # Du1 = Prt Pl3

REMOVE Pl3 IF (0 Sg3) (-1 (N Sg Nom)); ## Dokumeanta c1ilge, mo mii eallit.

SELECT Du1 IF (*-1  MOAI BARRIER MII-PERS OR CLB);
SELECT Du1 IF (*1  MOAI BARRIER MII-PERS OR CLB);
SELECT Du1 IF (1 Pcle) (2 MOAI);               ## Jerre go moai?


# Du2
# ---
  # leahppi = ("leahppi" N Sg Nom)

REMOVE ("leahppi") IF (*-1 DOAI)(0 ("leat" V Ind Prs Du2));
REMOVE ("leahppi") IF           (0 ("leat" V Ind Prs Du2))(1 DOAI);
  # If there is a pronoun, then we have dual, and not the noun.

# Du3 
  # Du3 = PrsPrc Clt = Imprt Prs Du2 Clt
  # Du3 = Ind Prs Sg3 Clt "leaba, ferteba leat"

SELECT (Du3) IF (*-1 ("son" Pron Pers Du3 Nom)); ## Soai ferteba mannat.
SELECT (Du3) IF (*1 ("son" Pron Pers Du3 Nom));  ## fertebago soai mannat?
SELECT (Du3) IF (-1 (N Sg Nom))
                (*-2 CC BARRIER NPNH LINK -1 (N Sg Nom));
                           ## Per Berg ja Rolf Olsen leaba doppe.
                           ## Stuorra nieida ja unna gánda ferteba leat doppe.


# Pl1
# ---
  # Pl1 = Inf for all stem classes
  # Pl1 = Inf = Pl3 = Prt Sg2 for odd syll
  # Pl1 = Inf = Pl3 = Sg2 = ConNeg (copula)

SELECT Pl1 IF (*-1C MII-PERS BARRIER SII OR MOAI OR CLB
                    OR V-PL1);

SELECT Inf IF (-1 V-PL1)(*-2C MII-PERS BARRIER SII OR
                              MOAI OR CLB);

#SELECT Pl1 IF (-1 CC -2 V-PL1 LINK *-1C MII-PERS BARRIER SII OR
#                                  MOAI OR CLB);

SELECT Pl1 IF (NOT -2 (V Ind Prs Pl1 Clt))(-1 MII-PERS);
                                                ## Fertetgo mii fertet?

#SELECT Pl1 IF (NOT -3 (V Ind Prs Pl1)(-2 go)(-1 MII-PERS);
                                                ## Fertet go mii fertet?

SELECT Pl1 IF (-1 BOS)(0 (Clt))(1 MII-PERS);

SELECT Interr IF             (0 ("mii")) (*1 Sg3 BARRIER V-PL1 LINK NOT 0 Pl1);



  # Pl2 unique, no rules needed

# Pl3
# ---
  # Prs Pl3 = Prt Sg2 = Prs Pl1 = Inf (odd syll)   muitalit
  # Prs Pl3 = Prt Sg2 = Imprt Prs Pl2 (even syll)    vulget
  # Prs Pl3 = Prt Sg2                 (contracted) fertejit


# Preverbal subject

 # Single noun
SELECT Pl3 IF (*-1C NP-HEAD-PL-NOM BARRIER NON-ADV-PCLE);   ## Nieiddat jerre

 # Coordination
SELECT Pl3 IF (*-1 NP-HEAD-SG-NOM BARRIER NON-ADV
           LINK *-1 CC BARRIER NPNH
           LINK -1 NP-HEAD-SG-NOM);                      ## Nieida ja gánda jerre

 # Pronoun

SELECT Pl3 IF (*-1 SII BARRIER MII-PERS OR MOAI OR CLB); ## Sii jerre.

# Postverbal subject
# - - - - - - - - -

 # Sentence-initial verb with question particle and coordinated subject
SELECT Pl3 IF (*-1 BOS BARRIER NON-ADV-PCLE)
              (*1 (N Sg Nom) BARRIER NPNH OR NON-ADV-PCLE
              LINK 1 CC LINK *1 (N Sg Nom) BARRIER NPNH);  ## Jerrego Al ja Bo?

 # Sentence-initial verb with question particle and subject 'sii'
SELECT Pl3 IF (*-1 BOS BARRIER NON-ADV-PCLE)
              (*1 SII BARRIER NON-ADV-PCLE);                ## Jerrego sii?

 # Sentence-initial verb with question particle and ordinary plural subject
SELECT Pl3 IF (*-1 BOS BARRIER NON-ADV-PCLE)(*1C NP-HEAD-PL-NOM
              BARRIER NON-ADV-PCLE OR NPNH);                ## Jerrego nieiddat?


# Relative pronoun as subject
# - - - - - - - - - - - - - - 

SELECT Pl3 IF (-3 NP-HEAD-PL-NOM)(-2 COMMA)(-1 (Pron Interr Pl Nom)) ;
             #olbmot, geat fállet (too specific?)
SELECT Pl3 IF (-2 NP-HEAD-PL-NOM)(-1 (Pron Interr Pl Nom)) ;
             #olbmot geat fállet (too specific?)



# Existential sentences
# - - - - - - - - - - - 

SELECT ("leat" Pl3) IF (-1 (Loc))(1 NP-HEAD-PL-NOM); ## Mis leat beatnagat.
SELECT ("leat" Pl3) IF (-1 (Loc))(1 NUMERALS); ## Mis leat golbma beatnaga.



## Dá livc1c1e dutnje sabehat.


# Number phrases as subjects
# - - - - - - - - - - - - - 

SELECT Pl3 IF (-2 Num) (-1 N-GEN) ; # Golbma nieidda leat boahtán.
SELECT Pl3 IF (-1 Adv) (1 Num); # Nu jerre golbma nieidda


SELECT Pl3 IF (-1 Loc) (1 Attr OR Pron)(2 (N Pl Nom));## Mus leat od1d1a sabehat


# Selecting some present
# ----------------------

SELECT Prs IF (0 LEAT) (NOT *-1 V-IND-FIN); # one strange hit, revise!
# The same should be done ... hmm, for what?

SELECT Prs IF (-1 (Neg Prs)) ;

# Select some finite
REMOVE Inf IF (-2 CLB) (-1 CS); ## Dus1s1e, juos leat doarvái...
REMOVE Inf IF (-1 (Inf)) ; # 1 hit


# Indicative Past
# ---------------

  # Past Sg1 is unique

# Past Sg2
# --------
  # Prt Sg2 = Prs Pl3 = Imprt Prs Pl2 (even syll)    vulget
  # Prt Sg2 = Prs Pl3 = Prs Pl1 = Inf (odd syll)   muitalit
  # Prt Sg2 = Prs Pl3                 (contracted) fertejit

SELECT Sg2 IF (*-1 (Pron Pers Sg2 Nom))(0 Prt);             ## Don vulget ikte.
SELECT Sg2 IF (-1 BOS)(0 (Prt Clt))(1 (Pron Pers Sg2 Nom)); ## Vulgetgo don?

SELECT Sg2 IF (*-1 IKTE)(0 Prt);               ## Mun died1an, ahte vulget ikte.
SELECT Sg2 IF (-1 BOS)(0 (Prt Clt))(*1 IKTE);  ## Vulgetgo ikte?


# Past Sg3
# --------
  # Prt Sg3 = Imprt Prs Sg3 (leai)
 
# Past Du1
# --------
# Past Du2
# --------
# Past Du3
# --------
# Past Pl1
# --------
# Past Pl2
# --------
# Past Pl3
# --------
  # Prt Pl3 = Prs Du1

REMOVE (V Du1) IF (NOT *-1 MOAI or CC)(NOT *1 MOAI OR CC);


# leai disambiguation
SELECT Prt IF (0 LEAI)(-1 NP-HEAD-SG-NOM); # Introduced 0 LEAI



REMOVE (Prs Du1) IF (0 (Prt Sg3)) (-1 (N Pl Nom)); # Dokumeantat c1ilge, mo...


# Potential Present
# -----------------
# Potential Past
# --------------
# Conditional
# -----------

# Sg3
  # copula sg3 = pl3 livc1c1e (in the eastern dialect)
  # These rules may be generalised. Let's first see whether they work.

SELECT (Sg3) IF (-1 ("Son" Pron Pers Sg Nom))(0 ("leat"));
SELECT (Sg3) IF (NOT *-2 CC)(-1 (N Sg Nom) OR ("son" Pron Pers Sg Nom))
             (0 ("leat")); # strange rule, revise!
SELECT (Sg3) IF (-1 Loc)(1 (N Sg Nom)); # Mus livc1c1e viessu.

# Selecting Sg3 is not restricted to leat. This is not a rule for Conditional.

# Pl3
  # copula sg3 = pl3

SELECT (Pl3) IF (-1 (N Pl Nom) OR ("son" Pron Pers Pl Nom))(0 ("leat"));
SELECT (Pl3) IF (-1 Loc)(1 (N Pl Nom)); # Mus livc1c1e guolit.

# Imperative
# ----------

SELECT (Imprt ConNeg) IF (-1 (Neg Imprt)) ; ## Ale suhta munnje.

REMOVE Imprt IF (NOT -1 COMMA OR BOS or Pcle)(NOT 0 (ConNeg)) ;
# Imprt must be either a negative verb form, or in the middle of a sentencne.

# Negative imperative
  # Neg Imprt Pl2 = allet V Inf = Prs Pl1 = Prs Pl3

SELECT (Neg Imprt Pl2) IF (*1 ConNeg BARRIER Neg); ## Allet ge gátte ahte ...


REMOVE (V Ind Prs Sg3) IF (-2 BOS)(-1 Pcle)(0 (V Imprt Prs Sg2));
                           ## Na attes munnje daid.
                           # Note that this is dangerous, and kept specific in
                           # order to play safe. May need to be generalised.

# Infinite forms #
# ============== #
    CONSTRAINTS  #
# ============== #

# Infinitive
# ----------

REMOVE VFIN IF (*-1 VFIN-NON-AUX OR ConNeg OR PrfPrc
                    BARRIER ("mii" Pers Pron Nom Pl1) OR SII OR CS OR CLB)
               (0 Inf)(NOT 0 LEAT);
                           ## Mun háliidan bargat. Son ii háliidan bargat.
                           ## Stockfleth lea beassan bargat sámegiela buorrin.
                           # reintroduce (NOT 0 LEAT)? Yes.

SELECT Inf IF (*-1 VFIN)(0 LEAT);
                           ## Jos galggas1 leat doaivva kristtalas1vuod1a nannet,

REMOVE (Prs Pl1) IF (-1 BOS)(1C V-SG3);
                           ## Vuolgit lea suohtas


REMOVE NP-HEAD-PL-NOM IF (0 (Inf) OR NP-HEAD-SG-NOM)(1C V-SG3);


# ConNeg forms
# ------------

SELECT (ConNeg) IF (*-1 Neg BARRIER VFIN);
REMOVE (ConNeg) IF (NOT *-1 (Neg));

# Need more advanced rule to select ConNeg when more
# material between neg and conneg
# SELECT (ConNeg IF (0 LEAT) (*-1 (Neg));

REMOVE (VGen) IF (NOT *-1 V-IND-FIN)(Not *1 V-IND-FIN); # too sloppy!

REMOVE (ConNeg) IF (NOT *-1 (Neg) BARRIER CLB OR V-IND-FIN); 

REMOVE Imprt IF (*-1 (Neg Ind) BARRIER CLB OR V-IND-FIN)(0 (ConNeg));
REMOVE (Prs) IF (*-1 (Neg Imprt) BARRIER CLB OR V-IND-FIN)(0 (ConNeg));

# Act
# ---
# Act = Prs Sg1 = PrfPrc (odd-syll verbs, contracted verbs)
# => make rules for Act.


# Present Participle
# ------------------

# PrsPrc = Imprt Prs Du2 (even-syll)

# Perfect Participle is treated at the Ind Prs Sg1 chapter.




# ImprtII
# -------

# VGen
# ----

REMOVE VGen IF (0 VFIN)(NOT *-1 VFIN BARRIER CS)(NOT *1 VFIN BARRIER CS);
                              ## Báldáhan ferte din1got, jos galgá fidnet.

  # This rule assumes that VGen comes parasitically upon a finite verb,
  # and removes VGen if this is not the case. More rules are needed in order
  # to remove more cases.

# Gerund,  Verb abessive and Supine do have uniqe forms and need no rules


# Selecting some finite.
# ----------------------

SELECT VFIN IF (NOT *-1 VFIN BARRIER CS OR Interr)(NOT *1 VFIN);
                               ## Son lea dan sápmelas1 gean dovddan.
  # first approximation





# ======================= #
# Disambiguating pronouns #
# ======================= #
       CONSTRAINTS        # 
# ======================= #


# Personal pronouns
# -----------------

# Mon
# ---
  # = N Prop Ess Mo

# Don
# ---
  # Pron Dem Sg Acc / Gen

SELECT (Pron Pers Sg2 Nom) IF ( *1 V-SG2);     ## Don boad1át fargga.
SELECT (Pron Pers Sg2 Nom) IF (*-1 V-SG2);     ## Don boad1át fargga.

# Son
# ---
  # son Pcle

SELECT (Pron Pers Sg3 Nom) IF ( *1 V-SG3);     ## Son boahtá fargga.
SELECT (Pron Pers Sg3 Nom) IF (*-1 V-SG3);     ## Boahtágo son fargga?
  
# Moai
# ----
  # N Prop Ill Moa

SELECT (Pron Pers Du1 Nom) IF ( *1 V-DU1);     ## Moai c1ilge dan ás1s1i.
SELECT (Pron Pers Du1 Nom) IF (*-1 V-DU1);     ## C1ilgego moai dan?

# Mii
# ---


SELECT Interr IF (0 ("mii"))(-1 COMMA)(1 Sg3);

SELECT Interr IF (0 ("mii"))(*1 Sg3 BARRIER V-PL1);


SELECT Pers IF (0 ("mii"))(*1 V-PL1 BARRIER NON-ADV) ;
SELECT Pers IF (-3 BOS)(-2 V-PL1)(-1 go)(0 ("mii")); 
SELECT Pers IF (-2 BOS)(-1 (V Ind Prs Pl1 Clt))(0 ("mii"));


SELECT Pers IF (-1 (V Ind Prs Pl1 Clt))(0 ("mii"));

SELECT Pers IF (0 ("mii"))(*1 V-PL1 BARRIER NON-ADV) ;

SELECT Pers IF (-1 (V Neg Pl1))(0 ("mii"));     ## Eat mii sáhte vuolgit.

#SELECT Pron IF (0 Pron Pers Loc)(1 V-SG3)(*2 N BARRIER V-IND-FIN);
# Strange rule, causes error.


# Mis and sis
# ---
  # mun Pron Pers Pl1 Loc = mis Adv, corr. for son Pron Pers Pl3 Nom = sis Adv
  # This one is hard to resolve, as they have the same distribution.

# Other homonymies linked to personal pronouns, with non-obvious answers.
  # Munin = Pron Pers Sg1 Ess = Ess of N Prop Muni (!)
  # dudnon = Pron Per Du2 Ess = dudnu N Sg ... PxSg1 (covered by Px rule)
  # Sunin = Pron Pers Sg3 Ess = Ess of N Prop Sun  
  # son Pron Pers Sg3 Ill = suotnjat V Ind Prs Du1 = V Ind Prt Pl3


# Recipr and refl
# ---------------

SELECT Recipr IF (-1 Recipr); # ... guhtet
SELECT Recipr IF  (1 Recipr); # ... ...    guoimmiset

CONSTRAINTS

REMOVE Recipr ;


# Demonstrative pronouns
# ----------------------

# NP-internal concord

SELECT DEM-SG IF (-1 BOS)(0 Nom)(1 V-SG3); 
                            ## Dat lea duohta.

SELECT Pl IF (0 (Pron Dem))(*1 (N Pl) BARRIER NON-A);
                             ## Ja dát soad1it s1adde garrasit c1uohcat Supmii.


# Disambiguating against other POS

# Getting rid of problematic names.
# Problematic here are the names Dan, Dien, and the essive of the noun duo.

REMOVE (Prop) IF (-1 BOS)(0 Dem)(1 N)(NOT 1 Prop); ## Dan nieidda mun dovddan.

REMOVE (Dem Sg Nom) IF (NOT *-1 V-SG3)
                       (NOT *1 (N Sg Nom) OR V-SG3);
                                      ## Ii dat beassan.
  # The verb is there to capture intransitive Dem.

REMOVE (Dem Pl Nom) IF (NOT *1 (N Pl Nom) OR V-PL3 BARRIER NPNH);
  # These are not good, we may have topicalised verbs...

REMOVE (Dem Acc) IF (*1 (N Ill) OR (N Loc)
                    BARRIER NPNH);

REMOVE (Dem Gen) IF (*1 (N Acc) BARRIER NPNH LINK NOT 0 (N Gen));
REMOVE (Dem Acc) IF (*1 (N Gen) BARRIER NPNH LINK NOT 0 (N Acc));



# Indefinite pronouns
# ===================

SELECT Indef IF (0 ("seammá"))(*1 N BARRIER NPNH);
                   ## Dat lea dohkkehuvvon stáhtarád1is seammá beaivvi

SELECT Attr IF (0 Pron)(*1 N BARRIER NON-A);
                   ## Dan muitalii muhtin mátkkos1teaddji gean namma lei Keilhau.




# ========================== #
# Disambiguating adpositions #
# ========================== #
        CONSTRAINTS          #
# ========================== #


SELECT Po IF (*-1 V)(-1 Gen)(0 ("alde"));   ## Láibi lea beavddi alde.
SELECT Po IF (-1 Gen);                      ## Láibi lea beavddi alde.

# using it for all Po
REMOVE N IF (-1 Gen)(0 Po);
REMOVE V IF (-1 Gen)(0 Po);
REMOVE Adv IF (-1 Gen)(0 Po);
REMOVE N IF (-1 Gen)(0 Adp);
REMOVE V IF (-1 Gen)(0 Adp);
REMOVE Adv IF (-1 Gen)(0 Adp);



# =========================== #
# Disambiguating subjunctions #
# =========================== #
        CONSTRAINTS           #
# =========================== #

SELECT CS IF (-1 CLB); # this one too general, there must be separate
                         # rules for different groups of CS.
# go
# --

SELECT Pcle IF (-2 BOS)(NOT -1 CC OR Adv)(0 go);  ## Boad1át go?
                                                  ## Ja go beasai fas bargui.
SELECT Pcle IF (-3 BOS)(-2 CS)(0 go);             ## Muhto boad1át go?

SELECT Pcle IF (-1 (V Neg));                      ## Son jerre it go boad1e?


CONSTRAINTS # New cycle to get rid of Pcle "go"

SELECT CS IF (0 go); # All other go's are CS.
#REMOVE CS IF (0 )(NOT -1 V);                ## Nu guhkás go mun died1án,
                                             ## buot leat ortnegis.


# Numerals
# ========

REMOVE ("guovttes") IF (1 (Sg Gen));         ## Viesus leat guokte olbmo.

# other words
# ===========

SELECT CS IF (0 go)(-1 COMMA) ;

SELECT Pron IF (0 ("don" Nom Sg))(*1 V-SG2);

REMOVE Prop IF (0 ("Lassi"))(1 ("dát" Pron Dem Sg Ill));
                                             ## Lassin dása mun lean dál.
  # all too specific!




# =====================
# Global disambiguation
# =====================


CONSTRAINTS

REMOVE Sg IF (0 DEM-PL) (NOT *1 V-SG3);       ## Dat lea buorre.
REMOVE Pl IF (0 DEM-SG) (NOT *1 V-PL3);       ## Dat lea od1d1a niibbit.


# At least one finite verb in the sentence
# The rule scans to the left until it finds a finite verb
#SELECT VFIN IF (NOT *-1 VFIN)(NOT *1 VFIN);

# Stop at guhto to the left and comma to the right.
#SELECT VFIN IF (NOT *-1 VFIN BARRIER COMP)(NOT *1 VFIN BARRIER CLB);
# cut these two because of titles. They would be ok if we could
# recognise titles.

# Afterthought, get rid of Acc in specifier position.
# Move to logical place, but after the primary Acc cycle.
Remove Acc IF (0 Pron)(1C Acc); # Mun oainnán du vielja.

END

# Note for future thought:

# Soai leaba goappas1agat    nu jallat
# they are   both-Pron-Indef so stupid

# Here, the subject NP is spread around the finite verb. Either, all the
# rules must be duplicated with this feature in mind, or the possible
# discontinuous parts must be included in the complement to the
# NON-ADV-PCLE set.
# While thinking, let us try including Pron Indef into a NON-ADV-PCLE-INDEF,
# at least for predicative constructions.
