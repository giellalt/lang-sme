# ==================================================================== #
# ==================================================================== #
#        N O R T H E R N   S Á M I   D I S A M B I G U A T O R         #
# ==================================================================== #
# ==================================================================== #

#    Sámi language technology project 2003-2006, University of Tromsø  #


            # =========================================== #
            #               Table of contents             #
            # =========================================== #
                 
            # Delimiters
            # Tags and sets
            # Disambiguation rules
              # One-cohort disambiguation - cycle 0
              # Local disambiguation - cycles 1 and 2
                # Cycle 1a: Special cases 
                  # Personal pronouns
                  # Px
                  # Certain verbs
                # Cycle 1b: Cleaning up after the special cases
                  # Px
                  # Certain verbs
                # Cycle 2: Other local disambiguation
                  # Noun or not?
                  # Certain singleton words
                  # Disambiguating clitics
                  # Disambiguating determiners 
                  # Disambiguating adpositions
                  # Disambiguating subjunctions
                  # Disambiguating adverbs
                  # Disambiguating pronouns
                  # Disambiguating adjectives
                  # Disambiguating certain safe verbs
                  # Disambiguating nouns
                  # Disambiguating verbs
                  # Other words
                # Global disambiguation
             # Notes for future thought are put after the END symbol


# ========== #
# Delimiters #
# ========== #


DELIMITERS = "<.>" "<!>" "<?>" ;


# ============= #
# Tags and sets #
# ============= #


SETS # (Remove all lines that start with SETS if you want to use mdis.)

# I define end of clause and beginning of clause in a way so that the file
# may be read both by the CG-2 and the vislcg formalisms.

LIST BOS = (>>>) (<s>); LIST EOS = (<<<) (</s>); # vislcg and CG-2 together.
#LIST BOS = (>>>); LIST EOS = (<<<); # end and beg. of sentence. for vislcg.


# Tags declared as single-membered LISTs
# ======================================

# Parts of speech

LIST N = N ; LIST A = A ; LIST Adv = Adv ; LIST V = V ; LIST Pron = Pron ;
LIST CS = CS ; LIST CC = CC ; LIST Po = Po ; LIST Pr = Pr ;
LIST Pcle = Pcle ; LIST Num = Num ; LIST Prop = Prop ;
#LIST Adp = Adp ; 

# POS sub-categories

LIST Pers = Pers ; LIST Dem = Dem ; LIST Interr = Interr ;
LIST Indef = Indef ;
LIST Recipr = Recipr ; LIST Refl = Refl ;     # LIST Rel = Rel ; 
LIST Interj = Interj ;      


# Morphosyntactic properties

LIST Ess = Ess ;
LIST Sg = Sg ; LIST Pl = Pl ; LIST Du = Du ;
LIST Nom = Nom ; LIST Acc = Acc ;
LIST Ill = Ill ;
LIST Loc = Loc ; LIST Com = Com ;
LIST Gen = Gen ;

LIST Cmpnd = Cmpnd ;

#LIST PxSg1 = PxSg1 ; LIST PxSg2 = PxSg2 ; LIST PxSg3 = PxSg3 ; LIST
#PxDu1 = PxDu1 ; LIST PxDu2 = PxDu2 ; LIST PxDu3 = PxDu3 ; LIST PxPl1 =
#PxPl1 ; LIST PxPl2 = PxPl2 ; LIST PxPl3 = PxPl3 ;

LIST Comp = Comp ; LIST Superl = Superl ;

LIST Attr = Attr ; LIST Card = Card ; LIST Ord = Ord ;
LIST Qst = Qst ; LIST Foc = Foc ;

LIST Pass = Pass ;
LIST Prt = Prt; LIST Prs = Prs ; 

LIST Ind = Ind ; LIST Pot = Pot ; LIST Cond = Cond ;
LIST Imprt = Imprt ; LIST ImprtII = ImprtII ;

LIST Sg1 = Sg1 ;  LIST Sg2 = Sg2 ; LIST Sg3 = Sg3 ;
LIST Du1 = Du1 ;  # LIST Du2 = Du2 ; LIST Du3 = Du3 ;  # Not using the ones that
LIST Pl1 = Pl1 ;  LIST Pl3 = Pl3 ; #LIST Pl2 = Pl2 ;   # do not have hom. forms

LIST Inf = Inf ; LIST ConNeg = ConNeg ; LIST Neg = Neg ;
LIST PrfPrc = PrfPrc ; LIST VGen = VGen ;
LIST PrsPrc = PrsPrc ; 

LIST Ger = Ger ; LIST Sup = Sup ; LIST Actio = Actio ;
#LIST VAbess = VAbess ;  LIST Actor = Actor ;

# Derivation

LIST as1 = "as1" ;

# Other tags

LIST ABBR = ABBR ; LIST ACR = ACR ; LIST CLB = CLB ;
LIST PUNCT = PUNCT ;


# Single-word sets

LIST OKTA = "okta"; LIST go = "go" ; 
LIST COMMA = "<,>" ;
LIST MANGA = "mán1ga" "galle" ;



# Tag sets
# ========

SETS

LIST WORD = N A Adv V Pron CS CC Po Pr Interj Pcle Num ; # any word



# Derivational affixes
# --------------------

LIST V-DER = adda alla d g h asti hudda huhtti huvva j l ahtti st
              stuvva goahti ;
LIST N-DER = eaddji eamos1 eapmi heapmi s1 mus1 us vuohta ;
LIST A-DER = geahtes lágan las1 meahttun ;

#SET DER = V-DER | N-DER | A-DER ;


# Case sets
# ---------

LIST ADVLCASE = Ill Loc Com Ess ;

LIST CASE = Nom Acc Gen Ill Loc Com Ess ;

SET NON-NOM = CASE - Nom ;
SET NON-GEN = CASE - Gen ;
SET NON-ACC = CASE - Acc ;

# Noun sets
# ---------

LIST N-SG-NOM = (N Sg Nom); LIST N-PL-NOM = (N Pl Nom);
LIST N-NOM = (N Nom) ; LIST N-GEN = (N Gen);

SET LEX-N = N - N-DER;

LIST PROP = (N Prop);
SET CNOUN = N - Prop;
SET CNOUN-PL-NOM = (N Pl Nom) - Prop ;

SET N-NON-GEN = N - (N Gen) ;


# Verb sets
# ---------

# Finiteness and mood
# - - - - - - - - - -

SET V-IND-FIN = Prs | Prt ; # Problem: "In boahtán" is an invisible indicative

SET V-MOOD = Ind | Pot | Imprt | ImprtII | Cond ;

SET VFIN = V-MOOD - ConNeg ;

SET VFIN-POS = V-MOOD - ConNeg - Neg ;

SET VFIN-NOT-IMP =  Ind | Pot | Cond ;


# Person
# - - - 

LIST V-SG1 = (V  Ind Prs Sg1) (V Ind Prt Sg1) (V Cond Prs Sg1) 
             (V Cond Prt Sg1) (V Pot Prs Sg1) (V  Neg Ind Sg1); 
LIST V-SG2 = (V  Ind Prs Sg2) (V Ind Prt Sg2) (V Cond Prs Sg2) 
             (V Cond Prt Sg2) (V Pot Prs Sg2) (V  Neg Ind Sg2); 
LIST V-SG3 = (V  Ind Prs Sg3) (V Ind Prt Sg3) (V Cond Prs Sg3) 
             (V Cond Prt Sg3) (V Pot Prs Sg3) (V  Neg Ind Sg3); 
LIST V-DU1 = (V  Ind Prs Du1) (V Ind Prt Du1) (V Cond Prs Du1) 
	     (V Cond Prt Du1) (V Pot Prs Du1) (V  Neg Ind Du1); 
LIST V-DU2 = (V  Ind Prs Du2) (V Ind Prt Du2) (V Cond Prs Du2) 
	     (V Cond Prt Du2) (V Pot Prs Du2) (V  Neg Ind Du2); 
LIST V-DU3 = (V  Ind Prs Du3) (V Ind Prt Du3) (V Cond Prs Du3) 
	     (V Cond Prt Du3) (V Pot Prs Du3) (V  Neg Ind Du3); 
LIST V-PL1 = (V  Ind Prs Pl1) (V Ind Prt Pl1) (V Cond Prs Pl1) 
             (V Cond Prt Pl1) (V Pot Prs Pl1) (V  Neg Ind Pl1); 
LIST V-PL2 = (V  Ind Prs Pl2) (V Ind Prt Pl2) (V Cond Prs Pl2) 
             (V Cond Prt Pl2) (V Pot Prs Pl2) (V  Neg Ind Pl2); 
LIST V-PL3 = (V  Ind Prs Pl3) (V Ind Prt Pl3) (V Cond Prs Pl3) 
             (V Cond Prt Pl3) (V Pot Prs Pl3) (V  Neg Ind Pl3); 


# Some subsets of the VFIN sets
# - - - - - - - - - - - - - - -

SET V-DU = V-DU1 | V-DU2 | V-DU3 ;
SET V-PL = V-PL1 | V-PL2 | V-PL3 ;

SET V-DU-PL = V-DU1 | V-DU2 | V-DU3 | V-PL1 | V-PL2 | V-PL3 ;

SET V-NOT-SG3 = V-SG1 | V-SG2 | V-DU1 | V-DU2 | V-DU3 |
                V-PL1 | V-PL2 | V-PL3 ;

SET V-1-2 = V-SG1 | V-SG2 | V-DU1 | V-DU2 | V-PL1 | V-PL2 ;

SET V-NOT-SG1 = VFIN-NOT-IMP - V-SG1 ;


# Copula sets (these ones need to be rewritten)
# - - - - - - - - - - - - - - - - - - - - - - -

SET LEAN = ("leat" V Ind Prs Sg1)|("leat" V PrfPrc);
SET LEAI = ("leat" V Ind Prt Sg3)|("leat" V Imprt Prs Sg3);
SET LEAT = ("leat" V Ind Prs Pl1)|("leat" V Ind Prs Pl3)|
           ("leat" V Ind Prs Sg2)|("leat" V Inf)|("leat" V Ind Prs ConNeg);

SET LEAT-COP = ("leat" V );
SET LEAT-DU-PL = ("leat" V Du1)|("leat" V Du2)|("leat" V Du3)|
                 ("leat" V Pl1)|("leat" V Pl2)|("leat" V Pl3);
SET LEAT-DU3-PL3 = ("leat" V Du3) | ("leat" V Pl3);
LIST LEAT-FIN-NON-IMP = ("leat" V Ind) ("leat" V Imp) ("leat" V Cond)
                        ("leat" V Pot); # Check! NON-IMP??
LIST LEAN-PRC = ("leat" V PrfPrc);
LIST LEAN-SG1 = ("leat" V Ind Prs Sg1);

LIST COPULAS = "leat" "s1addat" ;

SET VERB = V - (V N);   # V is all readings with a V tag in them, VERB should
                        # be the ones without an N tag following the V

#SET V-IND-FIN-NON-COP =  V-IND-FIN - LEAT-COP  ;

SET V-NON-COP = V - LEAT-COP ;        # SET V-NON-COP = VERB - LEAT-COP ;
SET LEX-V = VERB - V-DER;             # VERB but not V-DER

LIST MOD-ASP = "áigot" "fertet" "galgat" "gillet" "lávet" "sáhttit" "viggat" ;
# Verbs that never have arguments of their own.

SET AUX = COPULAS | Neg | MOD-ASP;

SET V-NON-AUX = V - AUX ;

SET VFIN-NON-AUX = VFIN - AUX ;

SET V-DER-PASS = V + V-DER + Pass;    # + is intersection, this should work
#SET V-PASS = V + Pass - V-DER;       # hmm, what was the idea here?
SET V-PASS = (V Pass);                # better?
SET V-NON-AUX-PASS = V - AUX - V-PASS ;
SET ACT-PRFP = PrfPrc - V-PASS;


# Pronoun sets
# ------------
SETS

SET MUN = (Pron Pers Sg1 Nom);
SET DON = (Pron Pers Sg2 Nom);
SET SON = (Pron Pers Sg3 Nom);
SET MOAI = (Pron Pers Du1 Nom);
SET DOAI = (Pron Pers Du2 Nom);
SET SOAI = (Pron Pers Du3 Nom);
SET MII-PERS = (Pron Pers Pl1 Nom);
SET DII = (Pron Pers Pl2 Nom);
SET SII = (Pron Pers Pl3 Nom);

SET PPRON-DU-PL = MOAI | DOAI | SOAI | MII-PERS | DII | SII ;

SET PPRON-NOT-SII = MUN | DON | SON | MOAI | DOAI | SOAI | MII-PERS | DII ;

#SET PRON-PL-LOC = (Pron Pers Pl1 Loc)|(Pron Pers Pl2 Loc)|(Pron Pers Pl3 Loc);

#SET MII-INTERR = ("mii" Pron Interr Sg Nom);


SET DEM-SG = (Pron Dem Sg Nom);
SET DEM-PL = (Pron Dem Pl Nom);


# Adjective sets
# --------------

SET A-ATTR = ( A Attr) ; # Used in one rule. Not needed? 

SET LEX-A = A - A-DER ;


# Sets for adverbials
# -------------------

SET LEX-ADV = Adv - A ;

LIST IKTE = "ikte" "dolin" "dovle" ;

LIST MO = "danne" "goas" "gokko" "gos" "makkár" "mo" "nugo"; # Introduce finite clauses.


# Clitic sets
# -----------
LIST Clt = Qst Foc ; 


# Sets for closed classes
# -----------------------

#SET PPOS = Po | Pr ;

LIST POST-A = "rájes" ;   
LIST POST-B = "rádjái" ;

#LIST WACKERNAGEL = "go" "ba" "bat" "be" "bahan" "son" ;

LIST GASKAL = "gaskal" "gaskkas" ; # p-positions that like coordination


# Sets of specific words
# ----------------------

# Sets for verbs choosing oblique objects
# - - - - - - - - - - - - - - - - - - - - 

LIST LOC-VERB = "ballat" "beros1tit" "bihtit" "ceavzit" "c1uoc1c1ut" "c1uovvut"
                "dolkat" "fuollat" "geargat" "heaitit" "nohkkot" "váruhit"
                "ilbmat" ;

LIST COM-VERB = "árvalit" "árvvohus1s1at" "riidalit" "bargat"
                "bártas1uvvat" "háladig" "humadit" "náitalit" "ságastaddat"
                "ságastallat" "hilbos1it" "meannudit" ;

LIST ILL-VERB = "áibbas1it" "dorvvastit" "duhtat" "jáhkkit" "liikot"
                "luohttit" "oskut" "suhttat" "álgit" "gullat" "guoskat"
                "heivet" "irggástallat" "riepmat" "ráhkkanit" "soahpat" ;

LIST INF-VERB = "adnot" "astat" "ádjánit" "áigut" "álgit" "ásahit" "bávc1c1agit"
"beassat" "berret" "bivvat" "bistit" "boahtit" "bovdet" "c1ohkkát" "c1ohkkedit"
"c1ohkkánit" "c1uoc1c1ahit" "c1uoc1c1astit" "c1urggodit" "dáhttut"
"dáidit" "dárbbas1it" "doaivut" "doapmat" "duostat" "fertet" "fuobmát" "fuolahit" 
"galgat" "gáibidit" "gártat" "geahc1c1alit" "geatnegahttit" "gillet" "háhppehit" 
"hálidit" "háliidit" "hárjánit" "heivet" "lávet" "mannat" "máhttit" "navdit" "oahppat" 
"oaz1z1ut" "ollet" "ribahit" "riepmat" "sáhttit" "sihtat" "soaitit" "s1addat" 
"stad1d1at" "veadjit" "viggat" "vis1s1at" "vuogáiduvvat"
"vuolgit" "vuollánit" "vuordit";

LIST ACC-INF-VERB = "bivdit" "c1uorvut" "diktit" "gohc1c1ut" "movttiidahttit" 
		"sihtat" "suovvat" ;

## Ii suova nieiddas náitalit.
#  These are verbs taking accusative objects and infinitives.


LIST OPRED-VERB = "atnit" "dahkat" ;
# Verbs that often occur with secondary predicates.


# Sets for adverbs that have lookalikes
# - - - - - - - - - - - - - - - - - - -
# Here comes some adverbs that have identical twins in other POS. If these
# are found in Adv contexts, we treat them as adverbs.

LIST ADV-NOT-VERB = "dus1s1e" ;

LIST ADV-NOT-NOUN = "easka" "várra" ;

LIST ADV-NOT-NAMES = "Ain" "Anne" "De" "Diego" "Dieppe" "Enge" "Galle" "Haga"
                      "Joba" "Johan" "Liikka" "Mai" "Mannes" "Mo" "Mot" "Naba"
                      "Nan" "Oktan" "Sierra" "Sokka" "Villa" ;

SET ADV-NOT-OTHER-POS = ADV-NOT-VERB | ADV-NOT-NOUN | ADV-NOT-NAMES ;



# Sets for nouns with common syntactic behaviour
# - - - - - - - - - - - - - - - - - - - - - - - 

LIST MANNU = "od1d1a/jage/mánnu" "od1d1a/jagi/mánnu" 
             "guovva/mánnu" 
             "njukc1a/mánnu" "njuvc1c1a/mánnu" "márs/mánnu" 
             "cuon1o/mánnu" "cuon1os/mánnu" "gáranas/mánnu" 
             "miesse/mánnu" "mái/mánnu" 
             "geasse/mánnu" 
             "suoidne/mánnu" 
             "borge/mánnu" "s1njilc1a/mánnu" "s1njilz1a/mánnu" 
             "c1akc1a/mánnu" 
             "golggot/mánnu" 
             "skábma/mánnu" "november/mánnu" "ritne/mánnu" 
             "juovla/mánnu" ;

LIST VAHKKU = "áigi" "beaivi" "c1akc1a/geassi" "c1uohte/jahki" "geardi" "geassi" 
		"hávvi" "idja" "jahki" "mánnu" "vahkku" jahki ;
 # The quoteless jahki here is since the preprocessor is not fine-tuned
 # yet, it still gives '"1870-" # jahki N Sg Com'.

SET TIME = MANNU | VAHKKU ;
SET NOTIME = N - TIME ;


LIST GUOVLU = "guovlu" "ássan/guovlu" "gávpot" "sádji" "viessu" "goahti" ;

LIST GEASSET = "dálvet" "dálvit" "gid1d1at" "geasset" "geassit" "c1akc1at" ;

LIST INITIAL =  "a" "b" "c" "d" "e" "f" "g" "h" "i" "j" "k" "l" "m"
                "n" "o" "p" "q" "r" "s" "t" "u" "v" "w" "x" "y" "z"
                "á" "æ" "ø" "å" "ö" "ä" ;

LIST PROSEANTA = "proseanta" "%" ;


# Miscellaneous sets
# ------------------

SET NUMERALS = Num - OKTA ;

SET NON-CC = WORD - CC ;

LIST COMPAR = "dávjjit" "earalágan" "seammás" "seammalágan" "seammalágán" 
		"seammaláhkai"; # Combine with "go" Pcle.


# Syntactic sets
# --------------

SETS

# NP sets
# -------

LIST NP-HEAD = Pron N ;
SET NP-HEAD-SG = (Pron Sg) | (Pron Sg3) | (N Sg);
SET NP-HEAD-PL = (Pron Pl) | (Pron Pl3) | (N Pl);
SET NP-HEAD-SG-NOM = (Pron Sg Nom) | (Pron Sg3 Nom) | (N Sg Nom);
SET NP-HEAD-PL-NOM = (Pron Pl Nom) | (Pron Pl3 Nom) | (N Pl Nom);
SET NP-HEAD-ACC = (Pron Acc) | (N Acc);

SET PRE-NP-HEAD = (Prop Attr) | A | (Pron Pers Gen) | (N Gen) | Num |
                  (Pron Dem)  | (Pron Refl Gen) | (Pron Indef) | CC ; # PrfPrc?

# There must be a sensible naming convention for these sets...
SET NPNH = WORD - PRE-NP-HEAD ;                 # NPNH = "NON-PRE-NP-HEAD". 

SET NPNHA = WORD - PRE-NP-HEAD - Adv ;     # NPNHA = "NON-PRE-NP-HEAD-OR-ADV"
SET NPNHAI = WORD - PRE-NP-HEAD - Adv - Indef ; # NPNHAI " "...-OR-INDEF"
SET NPNHAII = WORD - PRE-NP-HEAD - Adv - Indef - Ill ; # Illative indir.obj.
SET NPNHAIIP = WORD - PRE-NP-HEAD - Adv - Indef - Ill - Pcle ;


# Other nominal sets
# ------------------

SET NON-ADV = WORD - Adv;
SET NON-ADV-PCLE = WORD - Adv - Pcle ;
SET NON-ADV-INDEF = WORD - Adv - Indef ;
SET NON-ADV-PCLE-ILL = WORD - Adv - Pcle - Ill ;
SET NON-ADV-PCLE-INDEF = WORD - Adv - Pcle - Indef ;
SET NON-ADV-NEG = WORD - Adv - Neg ;

SET NON-A   = WORD - A ;
SET NON-Attr = WORD - Attr ;
SET NON-A-PCLE = WORD - A - Pcle ;
SET NON-DEM = WORD - Dem ;

SET NON-N =  WORD - N ;
SET NON-NA = WORD - N - A ;
SET NON-NP = VFIN | Adv | Pcle | Interj | CS | CC ;


# Adjectival sets and their complements
# -------------------------------------

SET A-CASE = A - Attr - Adv ;

LIST A-CC = A CC ;

SET NON-ADJ = WORD - A ;

SET NON-ADJ-CC = WORD - A-CC ;


# Adverbial sets and their complements
# ------------------------------------

SET ADVERBIAL = Adv | Ill | Loc | Com | Ess ;

# SET NON-ADV = WORD - Adv ;


# Border sets and their complements
# ---------------------------------

SET CP = (Pron Interr) | MO ;

SET NP-BORDER = BOS | EOS | CLB | Adv | V | Pers | CS | CP | Recipr |# Refl |
                Po | Pr | Pcle ;
   # too risky to use CLB, need more sophisticated way.
   # this allows for NP-internal commas to break NP.
   # Refl is deactivated because of "sin iez1aset vieruid" etc.	


# Semantically defined sets
# -------------------------

LIST CONTRA = "muhto" ; # In lean muitalan, muhto dál muitalan.




# ==================== #
# ==================== #
# Disambiguation rules #
# ==================== #
# ==================== #

# =================================== #
# One-cohort disambiguation - cycle 0 #
# =================================== #
             CONSTRAINTS              #
# =================================== #

# Proper nouns
# ------------

REMOVE (Prop Pl) IF (0 (Prop Sg));                   ## Marit lea dál.
  # Marit N Sg, not Mari+t Nom Pl

REMOVE (Prop Pl) IF (-1 BOS)(0 CNOUN-PL-NOM);        ## Sámit leat álgoálbmot.

REMOVE (Prop Pl Nom) IF (-1 BOS)(NOT *1 V-PL3);


# Removing propernouns that are lookalikes
# ----------------------------------------

REMOVE ("Lea") IF (-1 BOS)(NOT 1 V-SG3); ## Lea dus1s1e okta Ipmil. Lea idja.

REMOVE ("Lea") IF (-1 BOS)(1 A); 	## Lea buolas1.

REMOVE ("Man") IF (-1 BOS)(0 (V Imprt));
  # Cannot think of sensible conditions

REMOVE ("Hu" Ill) IF (1 A or Adv);

REMOVE ("Dan" Attr) IF (NOT 1 Prop);

REMOVE ("Dan" Prop) IF (1 CNOUN); # Dan is Nom or Attr.


# Removing a spurious Pr reading
# ------------------------------
REMOVE (Pr Foc) IF (0 ("c1ad1a")); # To get rid of 'c1ad1ahan' Pr Foc. 

# Lexicalised adverbs
# -------------------
REMOVE (A Adv) IF (0 LEX-ADV) ;#REMOVE A-ADV IF ( 0 Adv ) ;


# Lexicalised derivations
# -----------------------

REMOVE N-DER IF (0 LEX-N); # SELECT instead of REMOVE if one wants to
REMOVE A-DER IF (0 LEX-A); # investigate word structure
REMOVE V-DER IF (0 LEX-V); # THIS IS PROBABLY TOO STRONG, MSC may differ

REMOVE V-DER-PASS IF (0 V-PASS);
# It might be that this one is no longer needed. - No hits in corpus!


# Lexicalised p-positions
# -----------------------

REMOVE N IF (0 Pr)(*1 (N Gen) OR (Pron Pers Gen) BARRIER NPNH);
REMOVE N IF (0 Po)(-1 (N Gen) OR (Pron Pers Gen)); # dan várás, dan áigi



# ===================================== #
# Local disambiguation - cycles 1 and 2 #
# ===================================== #

# --------------------------------------------------------- #
# Cycle 1a: cases that will be needed in careful mode later #
# --------------------------------------------------------- #
                             CONSTRAINTS                    #
# --------------------------------------------------------- #
# The idea with cycle 1a is to disambiguate cases that are judged as easier and
# more secure than later cases, and that at the same may be used as context
# for other rules in later cycles.



# Personal pronouns
# =================

SELECT Pers IF (*-1 V-SG3)(0 ("son" Pron Pers Sg3 Nom)); ## leago son?
# There is still a problem here since son might as well be a particle 
# in the example.

REMOVE Pcle IF (*1 V-SG3 BARRIER N)(0 (Pron Pers Sg3 Nom));  ## De son lea.

SELECT Pers IF (*-1 V-SG2)(0 (Pron Pers Sg2 Nom)); ## leatgo don  
SELECT Pers IF (*1 V-SG2)(0 (Pron Pers Sg2 Nom));  ## de don leat 
  # (Not dem)



# Px constraints
# ==============

# First select some clear Px cases
# Let these come directly after local disamb


SELECT (PxSg1) IF (-1 V-SG1)(NOT -1 ("leat"))(-2 MUN);
SELECT (PxSg2) IF (-1 V-SG2)(NOT -1 ("leat"))(-2 DON);
SELECT (PxSg3) IF (-1 V-SG3)(NOT -1 ("leat") OR Pass)(-2 SON);
	## Dat ráhkaduvvo gáttis gárvvisin.

# Px for names for relatives

# Px for diminutives


# Certain verb forms
# ==================


# Pret Sg2
# --------

SELECT (V Ind Prt Sg2) IF (*-1 DON BARRIER V-SG2);
                                 ## Don muitalit mu birra.

# Pret Pl3
# --------

REMOVE (V Ind Pl3) IF (1C VFIN)(NOT 1 Pl3) ;
		## Vihtta bivdovuogi mun gal dus1s1e namahin.  

REMOVE V IF (-1 CS)(0 ("dus1s1e"))(*1 Acc BARRIER NPNH); 
	# This rule had to precede particle rules.

# Imperatives
# -----------
REMOVE Imprt IF (0 (Clt Qst));     ## Heivego gávtti coggat?
REMOVE Imprt IF (1 ("go" Pcle));   ## Heive go gávtti coggat?



# ---------------------------------------------------------------- #
# Cycle 1b: removing the readings that were left from the 1a rules #
# ---------------------------------------------------------------- #
                             CONSTRAINTS                           #
# ---------------------------------------------------------------- #

# Removing all remaining Px
REMOVE (PxSg1) ; # IF (0 Ess); # yes, kill'em all
REMOVE (PxSg2) ; # IF (0 Pl);
REMOVE (PxSg3) ; # IF (0 Loc);
# Note: Perhaps rather allow Px only for some key nouns (mother, father, etc).

# Removing remaining Prt Sg2 (philosophy: these are not pro-drop (BUT THEY ARE!!))
# REMOVE (V Ind Prt Sg2) IF (NOT -1 BOS) (NOT 0 (Clt));
# Proseassas láhc1c1ojuvvojit dilálas1vuod1at nu, ahte 
# fertejitgo dahkat dan?




# -------------------------------------------- #
# Cycle 2: Other cases of local disambiguation #
# -------------------------------------------- #


# This is the main part of the local disambiguation.
# Here we go through the grammar in a systematic way.
# Cases that should be handled before or after the main disambiguation, will be
# taken care of in cycles before or after this one.



# Noun or not?
# ============

# Based on left context
# ---------------------

SELECT N IF (-1 (A Attr))(NOT -1 Adv)(1 NP-BORDER);   ## Dat lea c1uovvovas1 sajis.

SELECT N IF (-2C N)( -1 CC)(NOT 0 VERB)(NOT 1 N OR Pron OR Num OR A);  
						## Mánát ja nuorat leat dál.

SELECT N IF (-2 BOS)(-1C Gen)(1 EOS);        ## Son lea Ruot1a c1álli.
                                              # (titles)
REMOVE PrsPrc IF (-1 A)(NOT -1 Adv) ;


# Based on right context
# ----------------------

SELECT N IF (NOT 0 Adv OR Inf OR PrfPrc)(1 COMMA)(2 CP);   
				## Doaimmat, maiguin mun barggan.
                       		## Gaz1aldat lea dus1s1e, man ollu ...
				## Lea miellagiddevas1 lohkat, maid ...

REMOVE A IF (0 N)(1 Po);                ## C1ohkkán dárolac1c1aid gaskkas.

REMOVE A IF (NOT -1 ("mii" Sg Gen))(0 N)(1C VFIN) ; ## Man guhkki lea du biila?	
					## Dál go sápmelac1c1ain leat bohccot.

REMOVE VFIN IF (-1 BOS)(0 NP-HEAD-SG-NOM)(1C V-SG3); ## Lásse lea mus rean1gan.
  # Here I am careful, and require the V-SG3 to be a secure reading.

REMOVE VFIN IF (-1 BOS)(0 Prop)(1 V-SG3);	 ## Máhtte ferte leat vuolgime.

  # Here I am less careful, and accept also ambiguous V-SG3, since I take it
  # that when the left context is a name, it is a more secure subject-candidate.
  # Alternatively, I might drop the 1C in the previous rule, or include the
  # pronouns.

REMOVE Adv IF (1C Po)(NOT 1 Adv);   
                        ## Mun vulgen dus1s1e mielde.
  # NBN! This example clause still gets the wrong analysis, it removes
  # Adv of dus1s1e although 1 not is (only) Po, but also Adv.


REMOVE A-CASE IF (*-1 BOS BARRIER NON-DEM)(0 N)(NOT 0 ("buorre"))(1 N);
                                       ## Sápmelac1c1aid historjá lea boaris.
  # No adjective, except buorre, agrees with a head noun in case. The correct
  # reading for a case-inflected prenominal word must thus be N.
  # (Problem: we have not considered a string predicative adj + other NP)

REMOVE (A Gen) IF (0 N)(*1 N BARRIER NON-A);


# Preverbal subject
SELECT (Pl Nom) IF (NOT 0 Inf LINK -1 INF-VERB)(1C (V Pl3))(NOT 0 Adv);
	## Olu nuorat bohte naitalanahkái.
  # This is a brave rule, probably too strong. It comes this early in order to
  # precede other rules.
  # New: Modified because of
	## Pros1eavtta áigut c1ad1ahit. # Added C to avoid áigut as subject.
	## ... geainna váimmut buoremusat lávggadit. # Buoremusat = Adv!


# Based on circumscribed context
# ------------------------------

REMOVE A IF (-1 Dem)(1 (Pron Interr));    ## Son lea dan sápmelas1 gean dovddan.

REMOVE (A Sg Nom) IF (NOT *-1 VFIN OR ("mii" Sg Gen))(0 (N Sg Nom))
			(*1 V-SG3 BARRIER NON-ADV-PCLE);

REMOVE (A Pl Nom) IF (NOT *-1 VFIN)(0 (N Pl Nom))(*1 V-PL3 BARRIER NON-ADV-PCLE);
                     ## Dálá guovlluin sápmelac1c1at eai leat ássan nu guhká.

REMOVE V IF (-1C Dem)(1 V-IND-FIN);
#REMOVE (V Ind) IF (-1 Dem)(1 VFIN);      ## Dán báhkis s1addá c1áppa gohppu.

REMOVE V IF (-1 BOS)(0 N)(1 ("go"))(2 VFIN); ## Ádjás go lea od1d1a fanas?

REMOVE (V Ind) IF (-1 Dem)(0 N)(1C (V Ind));

REMOVE (V Ind) IF (-1 CS)(0 N)(NOT 0 (V Pl1)) (1 (Pron Interr) LINK *1 (V Ind)); 
		## Dasgo mánná mii lea su siste... 

# Adjectives and adverbs
# ======================

SELECT A IF (-2 BOS)(-1 ("mii" Interr Sg Gen));

REMOVE VERB IF (-1 ("mii" Interr Sg Gen)); 

REMOVE VERB IF (-1 ("mihá")); #(*1 go BARRIER NON-NA); 

REMOVE VERB IF (-1 ("seammá"))(*1 go BARRIER NON-NA LINK NOT 1 VFIN); 

REMOVE N IF (-1 ("nu") OR ("mihá") OR ("hui"));#(*1 go BARRIER NON-NA);

#SELECT A IF (-1 CC)(0 (N as1))(1 Cmpnd);
           ## Lea sahka oppalac1c1at eamiálbmot- ja unnitloguás1s1iid dutkamiin.


# ======================= #
# Certain singleton words #
# ======================= #

# dat
# ---
SELECT Pcle IF (-2 BOS)(-1 Nom)(NOT -1 A)(0 ("dat" Nom));  ## Mun dat lean boahtán.

# imas1
# -----
SELECT N IF (-1 VFIN OR ConNeg OR A OR Dem)(0 ("imas1"));
                      ## Lei imas1, go buvddas eai vuovdde albma varramárffiid.
                      ## dakkár imas1 ja ártegis dilli ahte ...

# nubbi
# -----
SELECT Gen IF (0 ("nubbi"))(1 N);
                      ## Son oac1c1ui c1áda ás1s1i fas nuppi Stuorradikkis.



# ========================== #
# Disambiguating clitics     #
# ========================== #

REMOVE Qst IF (0 Adv)(NOT *1 EOS LINK -1 ("?")); ## Nugo died1át, de lean dál.


# ========================== #
# Disambiguating determiners #
# ========================== #


SELECT Loc IF (*-1 Num BARRIER NON-A) ; 



# ========================== #
# Disambiguating adpositions #
# ========================== #
        CONSTRAINTS          #
# ========================== #


# The case of GASKAL

SELECT Pr IF (0 GASKAL)(1 (Pron Gen))(2 CC)(3 (Pron Gen));
SELECT Po IF (-3 (Pron Gen))(-2 CC)(-1 (Pron Gen))(0 GASKAL) ;

SELECT Pr IF (0 GASKAL)(1 Num)(2 CC)(3 Num);
SELECT Po IF (-3 Num)(-2 CC)(-1 Num)(0 GASKAL);

SELECT Pr IF (0 GASKAL)(*1 (N Gen) BARRIER NPNH 
          LINK 1 CC LINK *1 (N Gen) BARRIER NPNH);
SELECT Pr IF (0 GASKAL)(1 A)(2 CC)(3 A LINK *1 (N Gen)); # Barrier?
SELECT Po IF (-1 (N Gen) 
          LINK *1 CC BARRIER NPNH LINK -1 (N Gen))(0 GASKAL);


# ===========
  CONSTRAINTS
# ===========

# Po or Pr?
# =========

REMOVE Po IF (NOT -1 N OR A OR Num OR Pron);
REMOVE Po IF (-1 N)(NOT -1 Gen);   ## Leansmánni lea kommis1uvnnas fárus.
REMOVE Pr IF (NOT *1 N OR A OR Num OR Pron BARRIER NPNH);


SELECT Pr IF (NOT -1 Gen OR (Pron Interr Nom))(NOT 0 A)(*1 (N Gen) BARRIER NPNH);
                                ## Olbmuin bisttii luossaguolli miehtá dálvvi. 

SELECT Pr IF (NOT -1 Gen)(1 (Pron Gen));

REMOVE Pr IF (NOT *1 (N Gen) BARRIER NPNH);

SELECT Po IF (*-1 BOS OR CLB BARRIER NPNH)(-1 Gen);

SELECT Po IF (-1 Gen)(1 CC);

SELECT Pr IF (NOT 0 Attr)(*1 (N Gen) OR (Pron Gen) BARRIER PUNCT 
	LINK NOT *1 N OR Po BARRIER CP OR CS OR CLB);

REMOVE Pr IF (NOT 1 (Pron Gen));

SELECT Po IF (-1 Gen)(NOT -1 ("leat" PrfPrc))(NOT 0 Attr)(NOT 1C Po);
		       ## ... dan njealji beali ektui.
                
SELECT Po IF (-1 Num)(0 ("rádjai")); # make general rule!

REMOVE Po IF (NOT -1 Gen);

# using it for all Po
REMOVE N IF (-1 Gen)(0 Po)(NOT 1C Po);
REMOVE V IF (-1 Gen)(0 Po)(NOT 1C Po);
REMOVE Adv IF (-1 Gen)(NOT -1 ("leat" PrfPrc))(0 Po)(NOT 1C Po);




# =========================== #
# Disambiguating subjunctions #
# =========================== #
        CONSTRAINTS           #
# =========================== #

# Subjunctions are disambiguated in two subcycles. This is the first.

SELECT CS IF (-1 CLB)(0 ("de") OR ("vaikko"))(NOT 1 CS);

REMOVE CS IF (NOT *1 VFIN OR ("leapma" Dimin)); # The latter is 'leamas'.

# amas
# ----
# A, or Neg Sup, or CS (if followed by (nominative) subject and (infinitival) verb). 

SELECT A IF (0 ("amas"))(1 CC)(2 A);

SELECT A IF (*-1 ("leat") BARRIER NON-ADV OR CLB)(0 ("amas"));

SELECT CS IF (0 ("amas"))(1 Neg);

SELECT CS IF (0 ("amas"))(*1 Nom BARRIER NPNH); 

# daningo
# -------
SELECT CS IF (0 ("daningo"))(NOT *1 ("?"));

# dasgo
# -----
SELECT CS IF (0 ("dasgo"))(NOT *1 ("?"));

# de
# --

SELECT CS IF (*-1 ("jos") OR ("go") LINK -1 BOS OR CS)(-1 COMMA)(0 ("de"));
                     ## Jos died1ás1in, de dajas1in.
                     ## Go olbmot jaskkodit, de sáhpánat álget jursit.
                     ## Son diehtá, ahte jos galgá leat mielde, de ferte máksit.
 
# go
# --

#SELECT Pcle IF (-2 BOS)(NOT -1 CC OR CS OR PUNCT OR Adv)(0 go);  ## Boad1át go?

SELECT Pcle IF (-2 BOS)(0 go)(*1 ("?"));
                                                  
SELECT Pcle IF (-3 BOS)(-2 CS)(0 go);             ## Muhto boad1át go?

SELECT Pcle IF (-1 (V Neg))(0 go);                ## Son jerre it go boad1e?

SELECT Pcle IF (-2 CLB)(-1 V)(0 go);              ## Jus juo, lea go nu ahte..

SELECT Pcle IF (-3 BOS)(-2 CC)(-1 V)(0 go);      ## Dahje leat go aiddonassii...

SELECT Pcle IF (-2 ("vai"))(0 go);

SELECT Pcle IF (*-1 A OR Adv LINK *-1 ("nu") OR ("mihá") BARRIER CS)(0 go);

SELECT Pcle IF (*-1 ("seammá"))(NOT -1 VFIN)(0 go);  ## Seammá gusto go...

SELECT Pcle IF (-1 COMPAR)(0 go); 

SELECT Pcle IF (0 go)(*1 EOS BARRIER VFIN);       ## Mus ii leat go okta beana.

SELECT Pcle IF (-1 A OR Adv)(NOT -1 Po)(0 go)(*1 Interr BARRIER VFIN);

SELECT Pcle IF (0 go)(1 ("ahte"));

SELECT Pcle IF (-3 N OR Pron)(-2 ("ahte"))(-1 VFIN)(0 ("go"));
                  ## Politihkas lei 1866 rájes sáhka galggai go stivrregoahtit 
                  ## gii oaz1z1u eatnama láigohit stáhtas.
# Note! This is a tentative rule! (too specific (-2 N) and to general
# (will probably remove some correct CS).

# vai
# ---
# This one may be CS (vai = 'in order to') or CC (vai = 'or').

SELECT CC IF (0 ("vai"))(1 Neg)(2 CLB);
                   ## Leago sámeálbmogis vuoigatvuod1at vai eai, dahje...

SELECT CC IF (0 ("vai"))(1 Qst);	## ... vai leago ..

SELECT CC IF (0 ("vai"))(2 ("go"));

SELECT CC IF (0 ("vai"))(NOT *1 VFIN BARRIER CS OR CP); 


# vaikko

SELECT Pron IF (0 ("vaikko"))
               (1 ("mii") OR ("gii") OR ("guhte") OR ("goabbá") OR
                  ("guhtemus1") OR ("makkár"));
  # "vaikko mii", etc. is a pronoun. Other vaikko cases are CS.
  # This should probably rather be taken care of in the preprocessor.

#SELECT CS IF (-1 BOS OR CS)(0 ("vaikko"))(*1 ("de"));



# Subjunctions are disambiguated in two subcycles. This is the second.

# =========== #
  CONSTRAINTS # New cycle to get rid of unwanted CS readings
# =========== #

# CS selection for specific CS candidates

SELECT CS IF (0 go); # All other go's are CS.

SELECT CS IF (0 ("vaikko"));

SELECT CS IF (0 ("vai")); 

# CS removal for specific CS candidates
REMOVE CS IF (0 ("de"));


# Numerals
# ========

REMOVE ("guovttes") IF (1 (Sg Gen));         ## Viesus leat guokte olbmo.

# other words
# ===========

SELECT Pron IF (0 ("don" Nom Sg))(*1 V-SG2);

REMOVE Prop IF (0 ("Lassi"))(1 ("dát" Pron Dem Sg Ill));
                                             ## Lassin dása mun lean dál.
  # all too specific!




# ====================== #
# Disambiguating adverbs #
# ====================== #
        CONSTRAINTS      #
# ====================== #


# General adverb rules
# --------------------

SELECT Adv IF (-2 VFIN)(NOT -2 ("leat") OR ("gártat")) (-1 ("nu"))(1 ("ahte"));
SELECT Adv IF (-2 VFIN)(-1 ("nu"))(1 COMMA)(2 ("ahte"));
                                       ## Lohku lassánii nu johtilit ahte ...

SELECT Adv IF (*-1C VFIN-NON-AUX BARRIER NON-ADV)
              (0 A-ATTR)(NOT 0 Inf)(NOT 1 N);     # Adv if postverbally

REMOVE Adv IF (-1 (N Gen) OR (Pron Gen))(NOT -1 Adv OR ("leat" PrfPrc))
              (0 Po);                                 # Po if after Gen

REMOVE Adv IF (-1 ("leat"))(0 (A Attr))(1 N);  ## Bussás leat alit c1almmit.

SELECT Adv IF (-2 V)(NOT -2 COPULAS)(-1 Adv)(1 EOS); # Too specific!!
                                                     ## Son c1állá hui c1ábbát.

SELECT Adv IF (-1 BOS OR CLB)(0C Adv OR Po);

SELECT Adv IF (0 (Pron Indef))(1 CS);              ## Dat lea nu ahte boad1án.
SELECT Adv IF (0 (Pron Indef))(1 COMMA)(2 CS);     ## Dat lea nu, ahte boad1án.

REMOVE Po IF (-1 NON-NP)(0 Adv)(1 NON-NP OR CLB);  ## Mun in leat mielde.

REMOVE (N Gen) IF (-1 VFIN)(0 Adv)(NOT 1 N or Po); ## Diet lea beare unnán.

REMOVE (Pron Interr) IF (1 CLB);  ## Dá lea reivve maid.


# Adverbs, not interjections
# --------------------------

REMOVE Interj IF (NOT -1 Interj OR BOS)(0 Adv);      ## Dat dinii bures doppe.


# Adverbs, not comparative adjectives
# -----------------------------------

REMOVE Comp IF (-1 ("hui") OR ("nu") OR ("seammá"))(0 Adv);
                  ## Dat lea hui c1ielgasit.

REMOVE Comp IF (-1 VFIN)(0 Adv)(1 Inf);
                  ## Rád1d1ehus áigu erenomázit giddet fuopmás1umi sámegillii.

REMOVE (A Comp) IF (-1 N OR Pron)(1 VFIN);


# Adverbs, not verbs
# ------------------

REMOVE (V Ind) IF (-1 Nom LINK *-1 (V Ind) BARRIER NPNH)(0 ADV-NOT-VERB);

REMOVE VFIN IF (*1C VFIN BARRIER CLB OR CS OR CP); 


# Adverbs, not nouns
# ------------------

SELECT Adv IF (*-1 VFIN)(0 GEASSET)(NOT 1 Acc);            ## Son lei doppe geasset.
SELECT Adv IF (0 GEASSET)(*1 VFIN);              ## Dálvet son vulggii meahccái.


#SELECT Adv IF (-1 VFIN)(0 ADV-NOT-OTHER-POS);  
SELECT Adv IF (*-1 VFIN BARRIER NON-ADV)(0 ADV-NOT-OTHER-POS);
                                     ## Lea dus1s1e okta Ipmil.
                                     ## Spahká lea sullii dus1s1e suprbmabeali.
       # Cycle 1a?

SELECT Adv IF (-1 CS)(0 ADV-NOT-OTHER-POS)(1 Nom LINK 1 VFIN OR Interr);
                           ## Lahka mearridii, ahte dus1s1e sii gud1et máhte...
 # A bit half-hearted the previous LINK).

#SELECT Adv IF (-1 BOS)(0 ADV-NOT-NAMES);             ## De fidnejin.
  # Evaluate this one. An additional condition
  # (NOT 1 VFIN) was removed, as it was too careful.
REMOVE Interj IF (-1 BOS)(0 ADV-NOT-NAMES);             ## De fidnejin.
#The former one all to brutal, removed Num, ...

SELECT Adv IF (-2 BOS)(-1 ("leat" V Ind))(1 ("ahte"));
                     ## Lea duohta, ahte mán1ggas eai oz1z1on oastit eatnama.

SELECT Adv IF (-1 (N Nom))(0 ADV-NOT-NOUN);

SELECT Adv IF (-1 CC)(0 ADV-NOT-NOUN)(NOT *1 VFIN); 


# Nouns, not adverbs
# ------------------

Remove Adv IF (-1 Gen)(NOT -1 Interr)(0 (N Loc)) ;   ## Son bod1ii njukc1amánu loahpas.
   

# Rules related to specific adverbs
# ---------------------------------

# álgu
SELECT N IF (-1 Gen OR (A Attr))(0 ("álgu"));  ## Pros1eavtta álgu ii lean álki.

# beare
SELECT Adv IF (0 ("beare"))(1 ("ollu"));  ## Orohagas leat beare olu bohccot.

# beallái
  # XXX write rules for this as postposition as well!

# danin
SELECT Adv IF (0 ("danin"))(NOT *1 (N Ess) BARRIER NPNH)
		(NOT *-1 COPULAS BARRIER NON-ADV)(NOT 1 CLB)
		(NOT *-1 OPRED-VERB BARRIER NON-ADV)
		(NOT *1 OPRED-VERB BARRIER NON-ADV);
                                        ## Danin badjánii jurdda lága ráhkadit.
					## Návetruitu lávejit danin atnit.

SELECT Ess IF (0 ("danin"))(*-1 COPULAS BARRIER NON-ADV)(1 CLB); 
					## ...jos fierpmit leat danin,...

# dassá
REMOVE (V Ind Prs Sg3) IF (0 ("dassá"))( *1 VFIN BARRIER CS OR CLB);
REMOVE (V Ind Prs Sg3) IF (0 ("dassá"))(*-1 VFIN BARRIER CS OR CLB);
                                  ## Leago guhkes áigi dassá?
  # If this is to strict, IF (1 ("go")) is also possible.

# dákko
REMOVE (V Imprt Prs Sg2) IF (0 ("dákko"))(*1 VFIN);
REMOVE (V Imprt Prs Sg2) IF (*-1 VFIN)(0 ("dákko"));
                                        ## Dákko ii oac1c1o mannat ii oktage.
  # Alternatively, on could remove all imperatives. Here, I remove the
  # impertative reading of 'dággut' if there is some finite verb somewhere
  # else.

# de
REMOVE Prop IF (0 ("de"))(NOT 1 Prop);  ## De lea vissa ahte De Silva boahtá.

# died1usge
#SELECT Adv IF (NOT -1 BOS)(0 ("died1usge"));    ## Mun lean died1usge oaddán.
SELECT Adv IF (0 ("died1usge"));                 ## Mun lean died1usge oaddán.
  # Todo: Make a set of these verb-like adverbs, and make a rule for them.
  # Eventually more sets, if they have different syctactic properties...

# duo
SELECT Adv IF (NOT -1 Dem or (A Attr))(0 ("duo"));
                                           ## Áddjá go duo lea boahtime?

# dus1s1e
# There is a rule "Lea dus1s1e okta Ipmil" for the typical dus1s1e cases.

REMOVE N IF (0 ("dus1s1e")); # Brutal, but the noun is almost always "dus1s1i".
REMOVE A IF (0 ("dus1s1e"));
REMOVE VFIN IF (*-1 VFIN BARRIER CP OR CS)(0 ("dus1s1e"));
REMOVE VFIN IF (0 ("dus1s1e"))(*1 VFIN BARRIER CP OR CS);
REMOVE (Actor) IF (0 ("dus1s1at")); # Make a set for non-agentive verbs?

SELECT Adv IF (-1 BOS)(0 ADV-NOT-VERB);#(*1 VFIN BARRIER CC OR CS OR CLB);
SELECT Adv IF (-2 BOS)(-1 Pcle)(0 ADV-NOT-VERB);
                             ## Dus1s1e borrásat ledje girkus.

# eanet
SELECT Adv IF (-1 ("olu"))(0 ("eanet"));       ## Guovlu atnigoahtá olu eanet.
 # This is really a low-level rule. It may perhaps be extended to
 # SELECT Adv IF (-1 ("olu"));
 # but we would like to see some examples of a general pattern first.

SELECT Adv IF (0 ("eanet"))(1 ("go")) ;      ## Dat lea eanet go 10.

SELECT Adv IF (0 ("eanet"))(1 ("uhcit") OR ("unnit));
SELECT Adv IF (-1 ("uhcit") OR ("unnit"))(0 ("eanet"));

# easkka
REMOVE N IF (0 ("easkka"))(*1 TIME OR A);
                            ## Muhto easkka N. V. Stockfleth áiggis lei nu.
                            ## Dat lei boahtán ruot1agielain easkka j. 1863.
# goas
SELECT Adv IF (0 ("goas"))(*1 ("<?>"));         ## Goas heajat leat?
  # This is not an optimal rule for getting rid of the former Portuguese colony.

# goassige
SELECT Adv IF (*-1 Neg BARRIER VFIN)(0 ("goassige"));

# gosa
SELECT Adv IF (0 ("gosa"))(*1 VFIN BARRIER CLB OR CS);
                            ## Gosa don c1ihket dan?
                            ## Son oinnii báikkiid gosa juo ledje ásaiduvvan. 
  # Here, the rule giving the verbal reading of 'gosa' is missing. It must
  # be added.

# gusto
SELECT Adv IF (-1 VFIN)(0 ("gusto"));           ## Diet lea gusto ávkkálas1.

# ihttin
SELECT Adv IF (0 ("ihttin"));                   ## Mii vuolgit ihttin juo.
  # The competitor is '"ihtit" V N Actor Ess'. This reading will now never be
  # selected. Have a look at this when dealing with Actor nouns.

# ikte
SELECT Adv IF (0 ("ikte"))(*1 (V Prt));         ## Ikte lei hui buolas1.
SELECT Adv IF (*-1 (V Prt))(0 ("ikte"));        ## Ikte lei hui buolas1.

# maid
SELECT Adv IF (-1C VFIN)(0 ("maid"));            ## Mun háliidan maid mannat.
SELECT Adv IF (-1 N LINK *-1 BOS BARRIER NPNH)(0 ("maid"));
						  ## Suomas maid lei dilli váttis.
SELECT Adv IF (-2 BOS)(-1 (Pron Pers) OR (Pron Dem))(0 ("maid"));
						  ## Sidjide maid lei Ruija.

SELECT Adv IF (0 ("maid"))(*1 Inf BARRIER VERB);

SELECT Adv IF (*-1 CS BARRIER VFIN OR CLB)(NOT -1 go)(0 ("maid"))
	(*1 VFIN LINK NOT *1 VFIN);
	##... nu ahte dat maid siskkilda ávnnalas1 kultureavttuid.    

SELECT Interj IF (-1 BOS)(0 ("maid"))(1 CLB);

# manne
SELECT Adv IF (-1 BOS)(0 ("manne"))(1 VFIN);     ## Manne ii leat áviisa boahtán?
SELECT Adv IF (-1 BOS)(0 ("manne"))(1 N OR Pron)(2 VFIN); ## Manne don leat dál?
  # Need a rule for "Manne + NP + VFIN".

# mielas
REMOVE Adv IF (-1 Gen)(0 ("mielas"));   ## Mu mielas don sáhtát mielas boahtit.

# naba
SELECT Adv IF (0 ("naba"))(NOT 1 V-SG3);
                               ## Naba don, man ollu don leat dahkan.
  # "Naba" is a proper noun also, but ardly relevant in a Sami context.
  # Thus, it could perhaps be deleted in all contexts. The intended context
  # here is one of the subject, but it is somewhat halfheartedly written.

# rádjai 
SELECT Po IF (-1 Num)(0 ("rádjai") OR ("rádjái"));
 # this rule should be extended to all postpositions likely to occur after
 # number expresseions. Todo: Make a set.

# sullii
SELECT Adv IF (0 ("sullii"))(1 Num);    ## Dat lea sullii 20 kg.
  # This rule must be generalised on both conditions: "sullii" must be extended
  # to a set of adverbs denoting quantification, and Num must be extended to
  # more measures (or parallel rules must be written).

SELECT Adv IF (-1 ("leat"))(0 ("sullii"));
                                        ## Dat lea sullii 20 kg.
  # The idea is that 'leat' doesn't subcategorise for illatives.

# unnán

SELECT Adv IF (0 ("unnán"))(1 VFIN OR PrfPrc);

# uhcit/unnit

SELECT Adv IF (-1 ("eanet"))(0 ("uhcit") OR ("unnit"));
SELECT Adv IF (0 ("uhcit") OR ("unnit"))(1 ("eanet"));

# visot
SELECT Adv IF (0 ("viso"))(NOT 1 (N Pl));
   ## Visot dan maid NRK sádde digitála fierpmis sáhttá maid gullat Nettradios.
 # This is not a particularly good rule for 'visot'.



# ======================= #
# Disambiguating pronouns #
# ======================= #
       CONSTRAINTS        # 
# ======================= #


# Personal pronouns
# =================

# Mon
# ---
  # = N Prop Ess Mo

# Don
# ---
  # Pron Dem Sg Acc / Gen

SELECT (Pron Pers Sg2 Nom) IF ( *1 V-SG2);     ## Don boad1át fargga.
SELECT (Pron Pers Sg2 Nom) IF (*-1 V-SG2);     ## Don boad1át fargga.

# Son
# ---
  # son Pcle

SELECT (Pron Pers Sg3 Nom) IF (0 ("son"))(*1 V-SG3 BARRIER N); ## Son boahtá fargga.
#SELECT (Pron Pers Sg3 Nom) IF (*-1 V-SG3);     ## Boahtágo son fargga?
  
 # Sutnje (pronoun, not verb)
 # -------------------------- 

SELECT (Pron Ill) IF (*-1 (V Ind)) (0 ("son"));
SELECT (Pron Ill) IF (0 ("son"))(1 (Pron Interr Sg)); 

# Dat
# ---
 # Pron Pers Sg Nom
 # Pron Pers Pl Nom
 # Pron Dem Sg Nom
 # Pron Dem PL Nom

SELECT Pers IF (0 ("dat"))(NOT 1 A OR Num OR N); 
SELECT Ill IF (0 ("dat"))(1 ("lassi" Ess));

# More rules for "dat" are found after rules for demonstratives.

# Moai
# ----
  # N Prop Ill Moa

SELECT (Pron Pers Du1 Nom) IF ( *1 V-DU1);     ## Moai c1ilge dan ás1s1i.
SELECT (Pron Pers Du1 Nom) IF (*-1 V-DU1);     ## C1ilgego moai dan?


# Mii
# ---

# Left conditions
SELECT Pers IF (-3 BOS)(-2 V-PL1)(-1 go)(0 ("mii")); 
SELECT Pers IF (-2 BOS)(-1 (V Ind Prs Pl1 Clt))(0 ("mii"));

SELECT Pers IF (-1 (V Ind Prs Pl1 Clt))(0 ("mii"));
SELECT Pers IF (-1 (V Neg Pl1))(0 ("mii"));     ## Eat mii sáhte vuolgit.

SELECT Pers IF (*-1 V-PL1 BARRIER NON-ADV)(0 ("mii"))(0 (Pl1 Nom));

# Right conditions

SELECT Pers IF (0 ("mii"))(0 (Pl1 Nom))(*1 V-PL1 BARRIER NON-ADV) ;

SELECT Pers IF (0 ("mii"))(*1 V-PL1 BARRIER NON-ADV) ;

SELECT Pers IF (0 ("mii))(1C Interr);
SELECT Pers IF (0 ("mii))(1 COMMA)(2C Interr);


# Mis and sis
# -----------
  # mun Pron Pers Pl1 Loc = mis Adv, corr. for son Pron Pers Pl3 Nom = sis Adv
  # This one is hard to resolve, as they have the same distribution.

# Other homonymies linked to personal pronouns, with non-obvious answers.
  # Munin = Pron Pers Sg1 Ess = Ess of N Prop Muni (!)
  # dudnon = Pron Per Du2 Ess = dudnu N Sg ... PxSg1 (covered by Px rule)
  # Sunin = Pron Pers Sg3 Ess = Ess of N Prop Sun  
  # son Pron Pers Sg3 Ill = suotnjat V Ind Prs Du1 = V Ind Prt Pl3



# Interrogative pronouns
# ======================

REMOVE Interr IF (-1 Interr); 
	## Dat lea c1uonz1z1a, mii maid gorgn1u detnui c1akc1ageasi.

SELECT Sg IF (-1C (N Sg) OR (Pron Sg))(0 (Pron Interr));
SELECT Pl IF (-1C (N Pl) OR (Pron Pl))(0 (Pron Interr));
SELECT Sg IF (NOT -3 CC)(-2C (N Sg) OR (Pron Sg))(-1 COMMA)(0 (Pron Interr));
SELECT Pl IF (-2C (N Pl) OR (Pron Pl))(-1 COMMA)(0 (Pron Interr));
                                   ## Giella, maid dovdán.
                                   ## Gielat, maid dovdán.

REMOVE Pl IF (-1 BOS)(0 ("mii" Pron Interr))(NOT *1 (N Pl) BARRIER NPNH);

#SELECT Sg IF (-1C (N Sg))(0 (Pron Interr));

SELECT Interr IF (-1 BOS)(*1 ("?"));
SELECT Interr IF (-2 BOS)(-1 Pcle)(*1 ("?"));

SELECT Interr IF (NOT -1 (V Pl1))(0 ("mii"))
                 (*1 Sg3 BARRIER V-PL1 LINK NOT 0 Pl1);

SELECT Interr IF (0 ("mii" Sg Nom))(-1 COMMA)(1 Sg3);
SELECT Interr IF (NOT -1 (V Pl1))(0 ("mii" Sg Nom))(*1 Sg3 BARRIER V-PL1);

SELECT (Sg Gen) IF (0 ("mii" Interr))(1 A OR Adv)(NOT 1 Comp OR Superl);

SELECT Gen IF (*1 BOS)(0 (Pron Interr))(NOT *1 V-NON-AUX);# No hits!
 

# Reflexive pronouns
# ==================
  # iez1as is Gen or Acc
  # iez1a is Pl or Du

# reflexive iez1as
# ----------------

SELECT Gen IF (-1 Acc)(0 ("ies1" Gen) OR (Pron Pers Gen))(1 N);
                       ## , muhto it fuomás1 hirssa iez1at c1almmis?

# Emphatic ies1
# -------------
  # Sg ies1 is unique, there is homonymy Du/Pl for iez1a.

SELECT Du IF (*-1 V-DU)(0 ("ies1" Pron Refl Du Nom));
SELECT Du IF (*1  V-DU)(0 ("ies1" Pron Refl Du Nom));
SELECT Pl IF (*-1 V-PL)(0 ("ies1" Pron Refl Pl Nom));
SELECT Pl IF (*1  V-PL)(0 ("ies1" Pron Refl Pl Nom));
                       ## Dii lehpet iez1a dan oaidnán.
                       ## Iez1a dii lephet dan oaidnan.
                       ## Doai galgabeahtti iez1a mannat.

# Reciprocal pronouns
# ===================

SELECT Recipr IF (-1 Recipr); # ... guhtet
SELECT Recipr IF  (1 Recipr); # ... ...    guoimmiset

# =========== #
  CONSTRAINTS
# =========== #

REMOVE Recipr ;


# Demonstrative pronouns
# ======================

REMOVE Pcle IF (-1 Interr)(0 ("dat"));
REMOVE Pers IF (-1 CC)(0 Gen)(*1 N BARRIER NPNH LINK 1 CS OR CLB);

# NP-internal number concord
# --------------------------

SELECT Pl IF (0 (Pron Dem))(*1C (N Pl) BARRIER NON-A);
                             ## Ja dát soad1it s1adde garrasit c1uohcat Supmii.
SELECT Pl IF (0 (Pron Dem))(1 (Num Pl));

# NP-internal case concord
# ------------------------

# More rules for Dem in front of NUMERALS must be added.

SELECT (Dem Sg Nom) IF (0 ("dat"))(*1 (N Sg Nom) BARRIER NON-ADJ 
	LINK *1 V-SG3 BARRIER CS);
SELECT (Dem Sg Nom) IF (0 ("dat"))(*1C N BARRIER NON-ADJ LINK 0 (Sg Nom)
	LINK *-1 V-SG3 BARRIER CS OR CP);
SELECT (Dem Pl Nom) IF (*1 (N Pl Nom) BARRIER NON-ADJ LINK *1 V-PL3
	BARRIER CS);
SELECT (Dem Pl Nom) IF (*1 (N Pl Nom) BARRIER NON-ADJ LINK *-1 V-PL3 
	BARRIER CS OR CP);
SELECT (Dem Pl Nom) IF (*1 (N Pl Nom) BARRIER NON-ADJ LINK *1 V-DU3
	BARRIER CS);
SELECT (Dem Pl Nom) IF (*1 (N Pl Nom) BARRIER NON-ADJ LINK *-1 V-DU3 
	BARRIER CS OR CP);
SELECT (Dem Pl Nom) IF (0 ("dat"))(1 (Num Nom));

SELECT (Dem Acc) IF (0 ("dat"))(1C (Num Acc));
#SELECT (Dem Acc) IF (0 ("dat"))(*1C (N Acc) BARRIER NON-ADJ); # Could be Gen 
SELECT (Dem Gen) IF (0 ("dat"))(1C (Num Gen));
SELECT (Dem Gen) IF (0 ("dat"))(*1C (N Gen) BARRIER NON-ADJ);

SELECT (Dem Sg Gen) IF (0 ("dat"))(*1C (N Sg Ill) BARRIER NPNH);
SELECT (Dem Pl Ill) IF (0 ("dat"))(*1C (N Pl Ill) BARRIER NPNH);

SELECT (Dem Sg Gen) IF (0 ("dat"))(*1C (N Sg Loc) BARRIER NPNH);
SELECT (Dem Pl Loc) IF (0 ("dat"))(*1C (N Pl Loc) BARRIER NPNH);

SELECT (Dem Sg Com) IF (0 ("dat"))(*1C (N Sg Com) BARRIER NPNH);
SELECT (Dem Pl Com) IF (0 ("dat"))(*1C (N Pl Com) BARRIER NPNH);
SELECT (Dem Pl Gen) IF (0 ("dat"))(*1C (N Pl Com) BARRIER NPNH);

SELECT (Dem Ess) IF (0 ("dat"))(*1C (N Ess) BARRIER NPNH); # Not good. Danin=adv.

REMOVE (Dem Nom) IF (0 ("dat"))(NOT *1 (N Nom) BARRIER NPNH);
REMOVE (Dem Sg Loc) IF (0 ("dat"));
REMOVE (Dem Sg Ill) IF (0 ("dat"));


# Some rules for personal pronouns again (will hit "dat")
# ------------------------------------------------------- 

SELECT (Sg3 Nom) IF (*-1 (V Sg3) BARRIER CC OR CS OR CLB OR CP OR PUNCT); 
SELECT (Pl3 Nom) IF (*-1 (V Pl3) BARRIER CC OR CS OR CLB OR CP OR Nom);
SELECT (Sg3 Nom) IF (*1 (V Sg3) BARRIER VFIN OR CS OR CP LINK NOT 0 (N Pl));
SELECT (Pl3 Nom) IF (*1 (V Pl3) BARRIER VFIN OR CS OR CP);

SELECT (Pron Sg3) IF (1 (Pron Interr Sg)); ## Dat mas boallu lea,... 
SELECT (Pron Pl3) IF (1 (Pron Interr Pl));

REMOVE (Pers Acc) IF (*1C (N Acc) LINK NOT 0 TIME BARRIER CS OR CP);


# Disambiguating against other POS
# --------------------------------
# Getting rid of problematic names.
# Problematic here are the names Dan, Dien, and the essive of the noun duo.

REMOVE (Prop) IF (-1 BOS)(0 Dem)(1 N)(NOT 1 Prop); ## Dan nieidda mun dovddan.

REMOVE (Dem Sg Nom) IF (NOT *-1 V-SG3)
                       (NOT *1 (N Sg Nom) OR V-SG3);
  # The verb is there to capture intransitive Dem.


# dasa
  # The problem here is the VGen form of 'dassat', which is 
  # identical to the Illative form of 'dat' (both 'dasa').

REMOVE VGen IF (0 Pron);


# Earlier specific rules, to be reactivated if the previous one is too strong.
# ----------------------------------------------------------------------------
#SELECT Dem IF (0 ("dat" Pron Dem Sg Ill))(1 VFIN); ## Dasa lea guhkes historjá.
#SELECT Dem IF (0 ("dat" Pron Dem Sg Ill))(1 ("lassi" N Ess)); ## Dasa lassin.
#SELECT Ill IF (-1 ("sivva"))(0 ("dat")); ## Sivva dasa lea, ahte ...
#SELECT Ill IF (-1 PrfPrc)(0 ("dat")); ## Sii leat c1ujuhan dasa vuosttas jagi.



# Indefinite pronouns
# ===================

#SELECT Attr IF (-1 Pr)(*1 N BARRIER NON-A);

SELECT Attr IF (0 ("seamma"))(1 A);

REMOVE Attr IF (0 ("seammá"))(NOT *1 N OR A BARRIER NPNH);
                  ## Dat lea dohkkehuvvon stáhtarád1is seammá beaivvi.

SELECT Attr IF (0 Pron)(*1C N BARRIER NON-A-PCLE OR go);
                 ## Dan muitalii muhtin mátkkos1teaddji gean namma lei Keilhau.
                 ## Njuolggadusain ledje eará ge mearrádusat.

SELECT Indef IF (0 ("nubbi"))(*1 CS OR CC OR COMMA LINK *1 ("nubbi"));
                  ## Nubbi lei dáz1a, muhto nuppi bearras1is lei isit sápmelas1.

SELECT Indef IF (-1 CS OR CC OR COMMA LINK *-1 ("nubbi"))(0 ("nubbi"));
                  ## Nubbi lei dáz1a, muhto nuppi bearras1is lei isit sápmelas1.

SELECT Indef IF (*-1 COPULAS BARRIER V-NON-AUX)(0 ("unnán"));



# ========================= #
# Disambiguating adjectives #
# ========================= #
         CONSTRAINTS        #
# ========================= #

# Rules related to specific adjectives
# ====================================

# garra
SELECT Attr IF (NOT *-1 V-SG3)(0 ("garas" A Attr))(1 N)(NOT *1 V-SG3);
                            ## Beatnagat eai biva vis1s1at garra buollas1iid.
# This rule is here to remove "garra" N Sg Nom
# There are still instances of attr garra that it will fail to disambiguate.
# Wait for them to turn up.


# Attribute disambiguation
# ========================

REMOVE (A Nom) IF (0 (A Attr))(*1C N BARRIER NON-A LINK NOT 1 Po);
#REMOVE (A Nom) IF (0 (A Attr))(*1 N BARRIER NON-A LINK NOT 1 Po);
REMOVE (A Gen) IF (0 (A Attr))(*1 N BARRIER NON-A LINK NOT 1 Po);
REMOVE (A Acc) IF (0 (A Attr))(*1 N BARRIER NON-A LINK NOT 1 Po);
               ## Dat lea c1uovvovas1 sajis.
               ## Sámegiella ja dárogiella leat ovttaárvosas1 gielat.
	## Lea deat1alas1 movttiidahttit studenttaide gazzat doavttergrádaoahpu. 
 # The careful mode (*1C N) was removed because of the second example.
 # But reintroduced because of the third example.

REMOVE V IF (NOT -1 (N Nom))(0 (A Attr))(*1 N BARRIER NON-A LINK NOT 1 Po)
	(*2 VFIN BARRIER CC OR CLB OR CP OR CS);
REMOVE N IF (0 (A Attr))(NOT 1 VFIN OR PrfPrc OR CLB) 
	(*1 N BARRIER NON-A LINK NOT 1 Po);
               ## Olles doaibmaviessu lea sullii 2300 njealjehasmetera stuoru. 


REMOVE PrsPrc IF (0 (A Attr))(1 N)(NOT 1 Gen);
                  ## Sidjiide maid lei guollerikkis Ruija geasuheaddji guovlu.

REMOVE (A Loc) IF (0 Attr)(1 N);

SELECT (A Attr) IF (NOT -1 ("leat") LINK -1 Nom)(NOT -1 CS)(0 (N Nom) OR (A Nom))
	(NOT 0 VERB)(1 N)(NOT 1 VFIN OR TIME OR ("leapma" Dimin));


REMOVE VGen IF (-1 Gen OR Dem OR Indef)(0 Attr)(1 N)(2 NP-BORDER);
REMOVE N    IF (-1 Gen OR Dem OR Indef)(0 Attr)(1 N)(2 NP-BORDER);
REMOVE Adv  IF (-1 Gen OR Dem OR Indef)(0 Attr)(1 N)(2 NP-BORDER);
REMOVE VFIN IF (-1 Gen OR Dem OR Indef)(0 Attr)(1 N)(2 NP-BORDER);

	## Dat lea died1áhus sámepolitihka bajit linjáid birra.

   # This is a highly specified context. The idea is that the Spec ___ N
   # position should have and Attr or some other adjective. Look out for probl.
 

# Special rules for 'buorre' (the only adjective showing case agreement)
# ----------------------------------------------------------------------

SELECT (Pl Nom) IF (0 ("buorre"))(*1 (N Pl Nom) BARRIER NON-ADJ-CC);

SELECT Gen IF (0 ("buorre"))(NOT 0 Comp OR Superl)
              (*1C (N Gen) BARRIER NON-ADJ-CC);

SELECT Acc IF (0 ("buorre"))(NOT 0 Comp OR Superl)
              (*1C (N Acc) BARRIER NON-ADJ-CC);



# Rule for predicative attributes
# - - - - - - - - - - - - - - - - 

SELECT Attr IF (-3 BOS)(-2 Nom)(-1 ("leat"))(0 Prop)(1  Prop)(2 NP-BORDER);

SELECT (A Attr) IF (1 CC)(2 (A Attr))(3 N); ## Dat lea od1d1a ja vilges gákti.



# Coordinated attributive surnames
# - - - - - - - - - - - - - - - - 

SELECT (Prop Attr) IF (NOT -1 Prop)(1 CC)(2 (Prop Attr))(3 (N Prop));
                           ## Dál leaba Brita ja Ove Kåven.


# Adjectives as predicates
# ========================

# Two rules here to avoid removing infinitive:
REMOVE VGen IF (*-1 COPULAS BARRIER NON-ADV-INDEF)(0 Attr)(1 N OR EOS);
REMOVE VFIN IF (*-1 COPULAS BARRIER NON-ADV-INDEF)(0 Attr)(1 N OR EOS);

SELECT A IF (*-1 COPULAS BARRIER NON-ADV-INDEF)(NOT 0 VERB OR N)(1 N OR EOS);
                           ## Ikte lei hui buolas1.
#REMOVE Adv IF (*-1 ("leat" V) BARRIER NON-ADV-INDEF)(NOT *1 VERB BARRIER CS OR CP);

REMOVE Attr IF (0 A)(NOT 1 A OR CC OR N); ## Vuolgit lea suohtas.
 # Now, the former "OR COMMA" is moved to the next rule.
 # Perhaps the rule fares better now.

REMOVE Attr IF (0 A)(1 COMMA)(NOT 2 A);
                           ## Jos dat ii leat vejolas1, de mii fertet mannat.

REMOVE Attr IF (0 A-CASE OR Adv)(1 CC)(NOT *2 N BARRIER (A Attr) OR ("buorre"));
                    ## Guovlu lea 1500 km guhkkosas1 ja 300 km govdosas1.
  # Is there something wrong with this rule?

REMOVE (A Gen) IF (*-1 COPULAS BARRIER NON-ADV-PCLE)(1 Inf); ## Lea hávski oaz1z1ut reivve.

REMOVE (A Sg Nom) IF (0 (A Attr))(*1C N BARRIER NON-Attr);
  # When 1 = object predicative, this is a bad rule. Perhaps:
  # (-1 COPULAS)? Keep it in mind. - Also compare first rule of Attribute disambiguation.

# In the two next rules, an alternative would be to leave out the reference to the
# subject and simply refer to the number marking of the copula. 
#SELECT (A Sg Nom) IF (*-1 COPULAS BARRIER NON-ADV LINK *-1 (N Sg Nom) OR (Sg3 Nom) 
#	BARRIER V-NON-AUX)(NOT 0 Inf);
SELECT (A Sg Nom) IF (*-1 COPULAS BARRIER NON-ADV LINK 0 (Sg3))(NOT 0 Inf OR Attr)
	(NOT 1 PrfPrc);

SELECT (A Pl Nom) IF (*-1 COPULAS BARRIER NON-ADV LINK *-1 (N Pl Nom) OR (Pl3 Nom) 
	BARRIER V-NON-AUX);


# Comparatives
# ------------

REMOVE Gen IF (0 Comp)(1 N)(NOT 1 (N Gen));
REMOVE Acc IF (0 Comp)(1 N)(NOT 1 (N Acc));
REMOVE Nom IF (0 Comp)(1 N)(NOT 1 (N Nom));

	## Dat lea died1áhus sámepolitihka bajit linjáid birra.

   # This block of rules is there to ensure case agreement for attributive
   # comparatives.

SELECT Comp IF (NOT -1 ("nu") OR CC) (*1 ("go" Pcle) BARRIER NON-N);
            ## Son lea buoret go mun.
            ## Dáz1at navde Finnmárkku buoret guovlun go iez1aset ruovttubáiki.

SELECT Comp IF (-1 ("mihá")); 

REMOVE Comp IF (-1 ("nu"))(*1 ("go") BARRIER NON-N);
	    ## C1almmit leat aiddo nu baskit go láhka suovvá.	



# Cardinal numbers
# ----------------

SELECT Card IF (*-1 ("okta" Num))(-1 COMMA)(0 ("nubbi"));
SELECT Indef IF (*-1 ("nubbi"))(-1 COMMA)(0 ("nubbi"));
                             ## Okta ás1s1i lea mii lágas mearriduvvui, nubbi
                             ## lea man kommis1uvnnat c1uvvo mearrádusaid.
  # Hmm. remove or select Card? It depends on how we understand the situation.
  # If "okta" is to be seen as numeral, then it should have a numeral as com-
  # panion along the road. Better would perhaps be to see both "okta" and
  # "nubbi" as indefinite pronouns in this context.
  # Here in any case: okta ... nubbi => card and nubbi ... nubbi => indef.





# ==================== #
# Disambiguating verbs #
# ==================== #
      CONSTRAINTS      #
# ==================== #

# In this section, we include safe verbal rules, rules that should come before
# the nominal rules. Two things may be done to improve this:
# 1. These rules may be moved to the front of the cycle, before other POS
# 2. More safe V rules may be moved earlier in the rule file.


# ConNeg forms
# ------------

SELECT (Imprt ConNeg) IF (*-1 (Neg Imprt) BARRIER VFIN OR ConNeg OR CS OR CP);
SELECT (Ind ConNeg)   IF (*-1 (Neg Ind) BARRIER VFIN OR ConNeg OR CS OR CP);
SELECT (Cond ConNeg) IF  (*-1 (Neg Ind) BARRIER VFIN OR ConNeg OR CS OR CP);

REMOVE ConNeg IF (NOT *-1 Neg);


SELECT (Imprt ConNeg) IF (*-1 CC BARRIER NON-ADV LINK *-1 (Imprt ConNeg) BARRIER VERB);
SELECT (Ind ConNeg) IF (*-1 CC BARRIER NON-ADV LINK *-1 (Ind ConNeg) BARRIER VERB);

# Need more advanced rule to select ConNeg when more
# material between neg and conneg.
# SELECT (ConNeg IF (0 LEAT) (*-1 Neg);

REMOVE VGen IF (NOT *-1 V-IND-FIN BARRIER CLB OR CS)
               (Not *1 V-IND-FIN BARRIER CLB OR CS);
  # too sloppy, but a bit better with the barrier...

REMOVE ConNeg IF (NOT *-1 (Neg) BARRIER CLB OR V-IND-FIN); 

REMOVE Imprt IF (*-1 (Neg Ind) BARRIER CLB OR V-IND-FIN)(0 (ConNeg));
REMOVE Prs IF (*-1 (Neg Imprt) BARRIER CLB OR V-IND-FIN)(0 (ConNeg));


# Infinitive
# ----------

SELECT Inf IF (*-1 (Neg Sup) BARRIER CLB); 

SELECT Inf IF (*-1 ("amas" CS) BARRIER VERB);

# Rules that prevent later selection of Inf for a finite verb in the frame
# INF-VERB...CC... ____
 
REMOVE Inf IF (*-1 (N Nom) OR (Pron Nom) BARRIER NPNH LINK -1 CC)
	(NOT -1 INF-VERB OR A);

REMOVE Inf IF (*-1 (Num Nom) BARRIER NON-NA LINK -1 CC LINK 
		NOT *-1 Inf OR INF-VERB BARRIER VFIN);

REMOVE Inf IF (-1 CC LINK NOT *-1 Inf); # Barrier? 

#Now we select Inf (the rule seems much better without the Not 0 Adv) :

SELECT Inf IF (*-1 INF-VERB BARRIER CS OR CLB OR CP OR Neg);#(NOT 0 Adv);
                           ## Skuvla galggai oahpahit sámemánáide dárogiela.
                           ## Pros1eavtta áigut c1ad1ahit.

SELECT Inf IF (-2 Inf)(-1 CLB)(1 CLB OR CC);

# This rule looks across a relative clause:
SELECT Inf IF (*-1 VFIN BARRIER CS OR CP LINK *-1 (Pron Interr) BARRIER CS
	LINK *-1 INF-VERB BARRIER VFIN OR CS OR CP);

SELECT Inf IF (*-1 ACC-INF-VERB BARRIER CS OR CP OR Nom OR Neg);

SELECT Inf IF (-1 (A Nom))(*-2 COPULAS BARRIER NON-ADV-PCLE);

SELECT Inf IF (*-1 LEAT-COP BARRIER NON-ADV-PCLE OR CLB 
		LINK *-1 Loc BARRIER NON-ADV-PCLE);


# Perfect Participle
# -------------------
# PrfPrc to the right of negated verb

SELECT PrfPrc IF (*-1 Neg BARRIER CONTRA OR VFIN); 
	# This rule also hits "Ii leat boahtán", but that is OK.

SELECT PrfPrc IF (*-1 VFIN BARRIER CLB OR CS)(NOT 0 Actio)
                 (NOT *-1 CC BARRIER VFIN); ## Mun lean muitalan.
                                            ## Mun muitalan ja muitalan.
        
  # Hmm: PrfPrc if there is not a Sg1 to the left, but if there is a CC to
  # the right of a possible Sg1, then we forget about the Sg1.

SELECT PrfPrc IF (*-1 VFIN BARRIER Nom OR V OR CS OR CC OR CLB OR PUNCT)
		(NOT 0 Actio);
                                                   ## Mun lean muitalan dan.
REMOVE (V Sg1) IF (*-1 ("leat") BARRIER NON-ADV-PCLE);

REMOVE Foc IF (0 Actio);
# Further qualification may be needed. The idea is to avoid focus reading of 
# oahpahan etc. Seems best to have this rule early. 


# Old rules, remove asap.
#SELECT PrfPrc IF (0 LEAN) (-1 (Neg));
#SELECT PrfPrc IF (*-1 VFIN-NOT-IMP); # Too general!!
#SELECT PrfPrc IF (-1 NP-BORDER)(0 Pass)(1 CNOUN);
                                     


# ==================== #
# Disambiguating nouns #
# ==================== #
      CONSTRAINTS      #
# ==================== #


# Proper nouns
# ============

# Prop or not
# -----------

SELECT Prop IF (NOT *-1 BOS OR (":") OR (""") BARRIER WORD)(NOT 1 ("Romsa")); 
# Capitalised nouns in the middle of a sentence are names.
# Jesus dajai: "Lea c1állojuvvon: ..."
# The final part of the rule is an ad hoc solution to avoid "de Romsa".

# Prop as Attr or not
# -------------------

REMOVE Attr IF (0 Prop)(NOT 0 A)(NOT 1 Prop OR INITIAL);   
                 ## Trond Trosterud lea dál.
                 ## Olles doaibmaviessu lea sullii 2300 njealjehasmetera. 

# Note:  the rule for selecting attribute reading of surnames is given
#        in the section on adjectives.

SELECT Attr IF (0 Prop)(1 INITIAL)(2 Prop);   ## Mathis K. Sara.

SELECT Attr IF (0 Prop)(1C (N Prop))(NOT 1 ("Romsa")) ;  ## Rolf Arne Berg.


# Choosing between different proper nouns
# ---------------------------------------

# Norga vs. Norge
REMOVE ("Norge") IF (0 ("Norga"));            ## Mun vuolggán Norgii.

# Lassi
REMOVE Prop IF (-1 BOS)(0 ("Lassi"));
                               ## Lassin nammadii direktevra guokte bargi.

# Nilse vs. Nilsen
REMOVE ("Nilse") IF (0 ("Nilsen" N Prop Sg Nom));
                               ## Tor Nilsen lea boahtán.



# Case disambiguation
# ===================

# =========== #
# Early rules #
# =========== #
  CONSTRAINTS #
# =========== #

# Genitive or Accusative
# ----------------------

# Genitive after quantifier

SELECT Gen IF (*-1C NUMERALS BARRIER NPNH OR Gen LINK NOT 0 Ord);  
# Gen as barrier because of: ## Dan eatnamis oaz1z1u ovtta gusa fuod1d1ara.

# SELECT Gen IF (NOT -2 Pr)(-1 NUMERALS);
                    ## Dat leat golbma nieidda.
                    ## Dál leat mán1ga nieidda
                    ## Ovdal 1920 orru guovtti goalmmátoasis ohcamiin nu geavvan.

# Genitive in front of postposition

SELECT Gen IF (NOT 0 Adv OR PrfPrc)(1 Po);    ## Dat lea nieidda dihte, nieidda birra.
  # As it stands, this rule has no conditions. So, what errors will we get?

# Genitive after preposition

SELECT Gen IF (*-1 Pr BARRIER NPNH)(1 NP-BORDER OR CC); ## Earret mu ii giige lea boahtán.


# Various
# -------

REMOVE Nom IF (*-1 (Pron Dem Acc) OR (Pron Dem Gen) BARRIER NON-A);
                                   ## Dan ás1s1i ferte dutkat.

REMOVE Nom IF (*-1 Nom)(NOT 0 NUMERALS)(1 Actio);

REMOVE Nom IF (*-1 V-NOT-SG3 BARRIER CC)(NOT *-1 COPULAS OR Pass BARRIER V)
	      (0 (N Sg Acc) OR (N Sg Gen))(NOT *1 CC)(NOT *1 V-SG3);
        ## C1uovvovas1 seminára cealkagat govahallet seminára loahppabohtosa.
	## Dat leamas1 vissa gobmi.
	## Eai das leat go áibbas moadde c1uoldda.

REMOVE Acc IF (-1 ("leat"))(1 EOS); ## Dát lea áhc1i. 


# Accusative object with secondary predicate
# --------------------------------------

REMOVE Gen IF (*-1 OPRED-VERB BARRIER NPNH)(1 Ess);


# Nominative or Accusative
# ------------------------

REMOVE Gen IF (-1 BOS)(1 Adv) ; # Dan láhkai - Gen or Acc?
            ## Dan maid dalle dárbbas1a fievrridit.

REMOVE Gen IF (NOT *-1 Pr OR NUMERALS BARRIER NPNH)(NOT -1 ("mii"))(NOT 0 TIME)
		(1C NP-BORDER); 
        ## Eai lean váldán go niestebohcco.



# =========== #
  CONSTRAINTS #
# =========== #
# Nominative
# ==========

SELECT (N Nom) IF (*-1 (Dem Nom) BARRIER NON-A);
REMOVE (N Nom) IF (*-1 (Pers Nom) BARRIER NON-A);

# Nominative in titles and sentence fragments
# - - - - - - - - - - - - - - - - - - - - - -

# A single word is nominative
SELECT Nom IF (-1 BOS)(NOT 0 VFIN)(0 N OR A OR Pron)(1 EOS); ## Katja.

# An NP head with a genitive modifier is nominative
SELECT Nom IF (-2 BOS)(-1 Gen)(0C N)(1 EOS);

# An NP head with an Attr modifier is nominative.
SELECT Nom IF (-2 BOS)(-1 Attr)(1 EOS);

# An NP head with a nominative Pron Dem modifier is nominative
SELECT Nom IF (-2 BOS)(-1 (Pron Dem Sg Nom))(0 Sg)
              (*1 V-SG3 BARRIER NON-ADV);
                                       ## Dat died1us mielddisbuvtii ahte ...

# A numeral preceded by a Nom demonstrative is nominative

SELECT (Num Nom) IF (-1 (Dem Sg Nom));


# Nominative subjects
# -------------------

# Find the subject, and assume it is nominative

# Ordinary preverbal subjects
# - - - - - - - - - - - - - - 

SELECT Nom IF (-1 CS OR BOS OR (Interr Sg))(0 Sg)(1C V-SG3);
                                 ## Died1án, ahte plána movttiidahttá dan.

SELECT Nom IF (-1 CS OR BOS)(0 Pl)(NOT 0 Adv)(1C V-PL3); 
                                 # ... and the same in pl..

SELECT Nom IF (*-1 CS OR BOS BARRIER NON-ADV)(0 NUMERALS)
	(*1 (V Pl3) BARRIER CS)(NOT *1 Nom BARRIER CS);

SELECT Nom IF (0 NOTIME OR Pron)(NOT 0 ("leat")) 
		(*1C V-PASS BARRIER V-NON-AUX OR NP-HEAD OR Po OR CS OR CLB) ;
	## Váfistan dihte od1d1a bargovugiid lea komitea viiddiduvvon.
	## Bus1eahtas lea sullii 650.000 ruvnno leamas1 várrejuvvon dáidda.

REMOVE Nom IF (-1 BOS)(NOT 0 (Pron Pers))(1C V-1-2);
                                 ## Dan oainnán viesu duohkin.  
                                 ## Moai c1ilge dan ás1s1i dutnje.


# Vocatives, subjects of sentence fragments
# - - - - - - - - - - - - - - - - - - - - - 

SELECT Nom IF (-1 BOS)(0 Prop)(1 COMMA); 
                                 ## Biera, du sabehiid!


# Coordinated subjects
# - - - - - - - - - -

SELECT Nom IF (*-1 CC BARRIER NPNHAI LINK -1C Nom)(1 NP-BORDER);
  ## Bror Berg ja Per Mikael Utsi leaba ruovttus.
  ## Bivdoearrái gullet diksu ja dorski.

SELECT Nom IF (1 CC)(*2C Nom BARRIER NPNHAI LINK 1 NP-BORDER);
  ## Per Mikael Utsi ja Bror Berg leaba ruovttus.
  ## Bivdoearrái gullet dorski ja diksu.

SELECT Nom IF (*-1 CLB BARRIER NPNHAI LINK -1 Nom)(1 CC);
 

# Postverbal subjects
# - - - - - - - - - -

# Singular
SELECT (Sg Nom) IF (*-1 V-SG3 BARRIER NPNHAIIP LINK *-1 BOS OR CS OR CLB BARRIER Nom)
                (0 (N Sg) or (Pron Pers Sg) OR (Pron Interr Sg))(NOT 0 PrfPrc)
		(NOT 1 Po)(Not 0 Gen);
                    ## Dat lei 1827s, muitalii muhtin mátkkos1teaddji midjiide.
                    ## Riikkaid galgá Piera oaidnit.

# Plural
SELECT (Pl Nom) IF (*-1 V-PL3 BARRIER NPNHAIIP LINK *-1 BOS OR CLB BARRIER Nom)
                (0 (N Pl) or (Pron Pers Pl) OR (Pron Interr Pl))(NOT 0 PrfPrc);
                    ## 1995:s gárvánedje rádiobuvttadeapmái lanjat.

# Coordinated nominatives - Could we simplify somewhat - see above?
# - - - - - - - - - - - - 
SELECT Nom IF (*-1 (V Pl3) BARRIER NPNH LINK NOT *-1 (Pl Nom) OR (Pl3 Nom) 
		BARRIER CS OR CP)(1 CC)(*2 Nom BARRIER NPNH);

SELECT Nom IF (*-1 CC BARRIER NPNH OR PUNCT LINK -1 Nom)(0C N OR Num);


# Nominative predicatives
# - - - - - - - - - - - -

  # Here, we handle both nouns and adjectives.

REMOVE A IF (NOT *-1 COPULAS OR OPRED-VERB BARRIER V-NON-AUX)(0 N)(1 CLB) ;
	## Sii dahke sámi kultuvrra oainnusin.
	## De lea cihca gárvvis.
# This rule has to follow identification of subject.


# Case disambiguation of predicatives
# -----------------------------------

SELECT Nom IF (*-1 ("leat" V Sg1) BARRIER NPNH OR PrfPrc
               LINK *-1 MUN BARRIER NON-ADV-PCLE)
              (NOT 0 PrfPrc)(NOT *1 PrfPrc);

SELECT Nom IF (*-1 ("leat" V Sg2) BARRIER NPNH OR PrfPrc
               LINK *-1 DON BARRIER NON-ADV-PCLE)
              (NOT 0 PrfPrc)(NOT *1 PrfPrc);
                                      ## Mun lean Anna. Don leat Anna.

#SELECT (Sg Nom) IF (*-1 ("leat" V Sg3) BARRIER NPNH
#                LINK *-1 (Sg Nom) BARRIER NON-ADV-PCLE)(NOT 0 VERB OR Adv OR Ill )
#		(NOT 1 Po)(NOT *1 Nom OR TIME BARRIER NPNH);
# This rule creates more problems than it solves. ## Tor lea od1d1a oahpaheaddji.


SELECT (Pl Nom) IF (*-1 ("leat" V Pl3) BARRIER NPNH
                LINK *-1 (Pl Nom) BARRIER NON-ADV-PCLE)(NOT 0 PrfPrc);

SELECT Nom IF (-2 Nom)(-1 COPULAS)(0 A)(NOT 0 PrfPrc)(1 EOS OR ("go"));
                                      ## Biilla ivdni lea vielgat.
                                      ## Biilla ivdni lea vielgadeabbo go mielki.

SELECT Nom IF (-2 Nom)(-1 COPULAS)(0 A)(NOT 0 PrfPrc)(1 CC)(2 A);
                                      ## Viessu lea vielgat ja ruksat.

SELECT Nom IF (-4 Nom)(-3 COPULAS)(-2 A)(-1 CC)(0 A)(NOT 0 PrfPrc)(1 EOS);
                                      ## Viessu lea vielgat ja ruksat.


SELECT Nom IF (-3 BOS)(-2 Nom)(-1 ("leat"))(NOT 0 PrfPrc)(1 EOS);
                                           ## Mun lean Bond.
                                           ## Mun lean gánda.

SELECT Nom IF (-2 NP-HEAD-SG-NOM)(-1 ("leat" V Sg3))(NOT 0 VERB OR Adv OR Ill OR Num)
	(NOT *1 Nom BARRIER NPNH);


SELECT Nom  IF (-4 BOS)(-3 Nom)(-2 ("leat"))(-1 Prop OR (ABBR))(NOT 0 PrfPrc)
               (1 NP-BORDER);

SELECT Nom  IF (*-5 BOS BARRIER NPNH)(-4 Nom)(-3 ("leat"))
                (-2 Prop OR (ABBR))(-1 Prop)(0 Prop)(1 NP-BORDER);

SELECT Nom  IF (*-6 BOS BARRIER NPNH)(-5 Nom)(-4 ("leat"))
                (-3 Prop OR (ABBR))(-2 Prop)(-1 Prop)(0 Prop)(1 NP-BORDER);

                                            ## Mun lean James Bond.
                                            ## Mun in leat James Bond.
                                            ## Mu namma lea Jo Ann Ba Doe.

# Again, these rules are too specific. Need empirical data to enlarge them.
# What is needed is linking both to copula and to clause border.

SELECT Nom IF (-2 NP-HEAD-SG-NOM)(-1 COPULAS + (V Sg))(1 EOS);
                                            ## Tor lea oahpaheaddji.

#SELECT (N Sg Nom) IF (*-1 V-SG3 LINK NOT *-1 NP-HEAD-SG-NOM)
#                                    (NOT  -1 NP-HEAD-SG-NOM)
#                                    (NOT 0 PrfPrc)(1 EOS);

SELECT Nom IF (*-1 COPULAS BARRIER V-NON-AUX OR CS OR CC)(NOT 0 VERB OR Adv OR Ill)
		(*1 CLB OR CS BARRIER NP-HEAD) ;


# Nominative as objects in existential clauses
# --------------------------------------------

REMOVE Acc IF (-2 Loc)(-1 ("leat"))(NOT *1 V); # Mus lea golli.
SELECT Nom IF (-2 Loc)(-1 ("leat"))(1 EOS); # Mus lea golli.


# Unclassified
# - - - - - -

SELECT Nom IF (-1 Gen)(NOT -1 (Pron Dem Gen))(0 N-SG-NOM)(NOT 0 PrfPrc)(1 V-SG3);
                            ## Du ás1s1i lea dehalas1.
                            ## Dan ás1s1i ferte dutkat.
# HMM, brave.. - but it seems to work!
# Note that the tag is selected, the set is in the condition.

# Nominative if two names in a row
SELECT Nom IF (-1 PROP) (0 PROP) (1 V-SG3); # Biret Anne galgá mannat.


# Rules for disambiguation around conjunctions, in coordination
# -------------------------------------------------------------

SELECT Nom IF (-2C N-NOM)(-1 CC)(NOT 1 Gen OR (Prop Attr));
 ## Trond ja Kirsti leat dál. Bror Berg ja Per Mikael Utsi leat ruovttus.
SELECT Nom IF (1 CC)(2C N-NOM); # Kirsti  ja Trond leat dál.

REMOVE Gen IF (0 N)(1 CC)(2 (Pron Sg3 Gen)); ## Jos1iai riegádii.
# Peters and Pauls friend OK.                ## Jekonja ja su vieljat leat dál.

SELECT Nom IF (-2 (Nom Cmpnd)) (-1 CC);
SELECT Gen IF (-2 (Gen Cmpnd)) (-1 CC);
SELECT Acc IF (-2 (Acc Cmpnd)) (-1 CC);


# Nominative plural
# =================
    CONSTRAINTS
# =================

# Nominative plural is also covered by many of the rules found in the general 
# Nominative section above. Rules related to Nominative plural but #not# No-
# minative singular should go here.

# Postverbal predicative

SELECT (Pl Nom) IF  (*-1 LEAT-DU-PL BARRIER NPNHAI
                LINK *-1 PPRON-DU-PL BARRIER NON-ADV-PCLE);
                                  ## Doai leahppi goappas1agat nu jallat.

SELECT (Pl Nom) IF  (*-1 ("leat" Du3) BARRIER NPNHAI
                LINK *-1 (N Sg Nom) BARRIER NON-ADV-PCLE
                LINK *-1 CC BARRIER NPNH LINK -1 (N Sg Nom));
                                  ## Anne ja Biret leaba goappas1agat nu jallat.

SELECT (Pl Nom) IF  (*-1 ("leat" Pl3) BARRIER NPNHAI
                LINK *-1 (N Sg Nom) BARRIER NON-ADV-PCLE
                LINK *-1 CC BARRIER NPNH LINK -1 (N Sg Nom)
                LINK *-1 COMMA BARRIER NPNH LINK -1 (N Sg Nom));
                                  ## Anne, Biret ja Cathrin leat nu jallat.

SELECT (Pl Nom) IF  (*-1 ("leat" Pl3) BARRIER NPNHAI
                LINK *-1 (N Pl Nom) BARRIER NON-ADV-PCLE);
                                  ## Mánát ge leat buot nu jallat.

# Missing Du3 and Pl3 rules for coordinated subjects and pl subjects


#SELECT (Pl Nom) IF (NOT *-1 (N Pl Nom))(*1 V-PL3 BARRIER ()
#                LINK NOT *-1 CC);     



# Accusative
# =========== #
  CONSTRAINTS #
# =========== #


# Topicalised object
# ------------------

SELECT Acc IF (NOT *-1 VFIN BARRIER CS)(NOT -1 ("mii"))(NOT 0 Nom OR TIME OR ("leat"))
		(NOT 1 COMMA OR N)(*1 VFIN BARRIER NON-ADV-PCLE)
		(*1 V-NON-AUX-PASS BARRIER V-PASS OR CLB)(NOT 1 Actio) ;
		#check w/o care LINK NOT *1 Acc);
                                   ## Dan lean mun váldán.
				   ## Dat leamas1 vissa gobmi.	
				   ## Min áiggi lea dat gildojuvvon.
  	
SELECT Acc IF (-1 BOS)(0 ("mii"))(1 (N Pl Gen))(2 NP-BORDER); 

SELECT Acc IF (*-1 (V Neg))(NOT 0 Nom)
              (*1 ConNeg OR PrfPrc BARRIER NON-ADV-PCLE LINK NOT *1 Acc);
                                     ## Ii go mánáid sáhte oahpahit dárogillii?

REMOVE Gen IF (NOT -2 Gen)(-1 CC)(1C VFIN);
                     ## Mus lea doaivu ja dan sáhttá c1állit c1uovvovac1c1at.

REMOVE Gen IF (*-1 CLB BARRIER NON-CC)(1C VFIN);
                     ## Mus lea doaivu, ja dan sáhttá c1állit c1uovvovac1c1at.

REMOVE Gen IF (-1 CS)(1C VFIN);  # No hits?
                     ## Mun jáhkán, ahte dan sáhttá c1állit c1uovvovac1c1at.
  # The "1C" part is problematic, as this rule should have been placed
  # after the verb disambiguation. Keep in mind.

REMOVE Gen IF (NOT *-1 Pr OR Num BARRIER NPNH)(1 CC)(2 NP-BORDER);

REMOVE Nom IF (NOT *-1 (V Pl3) BARRIER CS)(0 NUMERALS)(NOT 1 CC)
		(NOT *1 (V Pl3) BARRIER CS);


# Accusative between Aux and main verb
# ------------------------------------

# In front of an infinitive
SELECT Acc IF (*-1 VFIN BARRIER NPNHAI)(NOT 0 VERB)(NOT 0 Nom OR Adv OR TIME)
		(*1 Inf BARRIER NON-ADV-PCLE);
                          ## Od1astus addá vejolas1vuod1a muitalit.
                          ## Son lea beassan bargat.
# changed V-IND-FIN to VFIN here and in subsequent rules.

SELECT Acc IF (*-1 Nom BARRIER CS)(*-1 VFIN)(NOT 0 PrfPrc OR Nom OR Adv OR TIME)
	(1 Inf);

SELECT Acc IF (*-1 VFIN BARRIER NPNH)(1 CC)
              ( *2 NP-HEAD   BARRIER NPNH LINK 1 Inf);
SELECT Acc IF (*-1 CC BARRIER NPNH LINK -1 NP-HEAD LINK 
              *-1 VFIN BARRIER NPNH)(1 Inf);

# In front of a participle
SELECT Acc IF (*-1 LEAT-FIN-NON-IMP BARRIER NPNHAI)(NOT 0 Adv OR Nom OR TIME)
		(*1 ACT-PRFP BARRIER NON-ADV LINK NOT *1 ("leat" PrfPrc) 
		BARRIER CS OR CP);
                        ## Mun lean dan dahkan.
			## Váfistan dihte bargovugiid lea komitea viiddiduvvon.
			## Ruot1as leat guhká jo ságastallan dán ás1s1is.

# Accusative postverbally
# -----------------------

# Object only.

SELECT Acc IF (*-1C V-NON-AUX BARRIER NPNHAIIP OR COMMA)
              (0 NP-HEAD)(NOT 0 PrfPrc or Adv or Dem or Nom)(NOT 0 TIME) 
              (1 NP-BORDER OR (Num Nom))(NOT 1 Po) ;
                     ## Oainnán du.
                     ## In mun sálten nu olu.
                     ## Dáz1at eai atnán sámiid buoret go beanan.
                     ## Sii badjelgehc1c1e olbmo jos sámiin náitala.
                     ## Son ráhkada gáhkuid hui buriid.
                     ## Mun manan daid mánáidguin.
                     ## Skuvla galggai oahpahit dárogiela vissis guovlluin.
                     ## Berit lei oz1z1on permis1uvnna guovvamánu 22. b.
                     ## Berit lei oz1z1on permis1uvnna 22.10.

# earlier formulation: (NP-BORDER OR ADVLCASE OR Adv), which gives the wrong
# result with ## Dat ii gal mearkkas1 ollu álbmoga ealáhussii.

# Removed  (1 NP-BORDER OR Adv OR TIME OR Attr) because of
		## Fylka mearrida olugo galgá atnit sámi od1d1a leavgga.

# The (Num Nom) is not optimal, we need to consider the Num Ord and Num Card
# tagging. Now, 22. is given Num Ord and 22.10. is given Num Nom, this gives
# the correct result, but is not satisfying, linguistically.

# look at the NP-BORDER concept here...
# NPNHAII there to include "indirect objects"
# Added C to *-1C, to avoid interference from derived nouns. The problem will
# become smaller as we improve the preprocessor.
# The (1 Adv or Adj) is there to take care of object predicatives
#              Here is a set ofrules that were dismissed
#              (1 CLB OR Adv OR (A Comp) OR (A Superl) OR (A Attr) OR
#              ("go") OR CS OR PUNCT);

# Extended copy of the previous rule:
# This rule is to tolerate a PP between the verb and the object.

SELECT Acc IF (*-1C Po BARRIER NPNHAIIP LINK -1 N OR Pron
              LINK *-1C V-NON-AUX BARRIER NPNHAIIP)
              (0 NP-HEAD)(NOT 0 PrfPrc or Adv OR TIME)
              (1 NP-BORDER OR ADVLCASE OR Adv) 
              (NOT 1 Po) ;
                  ## Ii olmmos1 gille guoddit, go beare deaivá dan mad1e bohcco
                  ## ahte lea niesteseahkkaguoddi.

SELECT Acc IF (*-1C V-NON-AUX BARRIER NPNHAIIP OR COMMA)
              (0 NP-HEAD)(NOT 0 PrfPrc OR Adv OR Dem OR Inf )
              (1 CC)(*2 VFIN BARRIER NON-ADV-NEG) ;  
		## Ovddemus dáidá oastit árppuid ja god1d1a firpmiid.	

# Could this rule be modified so that it accepts a subject between 0 and verb? 

REMOVE Gen IF (NOT *-1 Pr OR NUMERALS BARRIER NPNH)(0 N)(1 Dem);


# Accusative sentence-finally
# ---------------------------

REMOVE Gen IF (NOT *-1 Pr OR Num OR CC BARRIER NPNH)(0 NOTIME OR Pron)
 		(*1 CS OR CP OR EOS BARRIER NON-ADV-PCLE) ;
          ## Fylkka mearrida olugo galgá atnit sámi leavgga.
          ## C1uovvovas1 seminára cealkagat govahallet seminára loahppabohtosa.
          ## Man1n1il lea viessu viiddiduvvon guovtte geardde.

# CC in the preceding rule because of coordination after Pr. The next rule 
# compensates for it.

SELECT Acc IF (NOT *-1 Pr OR Num BARRIER NPNH)(0 NOTIME OR Pron)(NOT 0 Nom OR VERB)
		(*1 CLB BARRIER NON-ADV-PCLE);

REMOVE Gen IF (NOT *-1 Pr OR CC BARRIER NPNH)(-1 Gen)(0 NOTIME OR Pron)
		(*1 CLB BARRIER NON-ADV-PCLE);


# Not accusative
# --------------

REMOVE Acc IF (NOT *-1 V-NON-AUX BARRIER CS OR CP)(NOT *1 V-NON-AUX BARRIER CS OR CP);
        ## Sápmelac1c1aid historjá álbmogin lea duháhiid jagiid boaris, muhto
        ## dálá ássanguovlluin sápmelac1c1at eai leat ássan nu guhká.
  # We need a verb for an obj. 

REMOVE Acc IF (NOT *-1 V-NON-AUX BARRIER CS OR CP)
	(*1 V-NON-AUX BARRIER CS OR CP LINK *1C Acc BARRIER CS OR CP);


REMOVE Acc IF (*1 LEAT-COP BARRIER V-NON-AUX OR CS OR CP  LINK *1 Ger 
		BARRIER V-NON-AUX OR CS) ;
	## Vihtta bivdovuogi leat Deanus luosa bivdidettiin.  
	# An overly specific rule? And what about time expressions?

REMOVE Acc IF (NOT *-1 V-NON-AUX BARRIER CS)(NOT 0 TIME)
		(*1 V-PASS BARRIER V-NON-AUX OR CS OR CP);
REMOVE Acc IF (*-1 V-PASS BARRIER VFIN)(NOT 0 TIME)(NOT *1 V-NON-AUX BARRIER CS OR CP);
                            ## Dál nannejuvvui girku sámeguovlluin.


# Coordinated object
# - - - - - - - - -

# First part
# SELECT Acc IF (*-1 V-NON-AUX   BARRIER NPNHAII)(0 N)(1 CC)
#              (*2 NP-HEAD-ACC BARRIER NPNHA LINK 1 CLB);
                                       ## Oainnán vilges biilla ja rukses viesu.

SELECT Acc IF (0 N)(1 CC)(*2C NP-HEAD-ACC BARRIER NPNHA OR Gen LINK 1 CLB) ;
	## Dalle skáhppo biergasiid nugo firpmiid ja buod1d1omuoraid.
# Only one problem found: 
	## Romssa universitehta sámi(*ACC) ja eamiálbmotguoskevas1 dutkama.

SELECT Acc IF (*-1C V-NON-AUX BARRIER NPNHAII)(0 Num)(1 CC)
	(2 Num LINK *1 N BARRIER NON-A LINK 1 NP-BORDER) ;
		## Ja de ráhkadit ovtta dahje moadde s1luppoha vel.  

# Second part
SELECT Acc IF (*-1 CC BARRIER NPNH LINK -1 NP-HEAD-ACC LINK 
              *-1 V-NON-AUX BARRIER NPNHAII)(0 N)(1 CLB);
                                       ## Oainnán vilges biilla ja rukses viesu.

SELECT Acc IF (-1 CC LINK *-1C Acc BARRIER NON-NA OR CLB)(0 Num) ;
		## Ja de ráhkadit ovtta dahje moadde s1luppoha vel.    

# Three coordinated objects
  # First part
SELECT Acc IF (*-1 V-NON-AUX BARRIER NPNHAII)(0 N)(1 COMMA)
              ( *2 NP-HEAD-ACC   BARRIER NPNHA LINK 1 CC LINK
                 1 NP-HEAD-ACC   BARRIER NPNHA LINK 1 NP-BORDER);
         ## Jos juohká dáz1aid, sámiid ja láddelac1c1aid ies1 gud1et sadjái, ...

  # Second part
SELECT Acc IF (*-1 COMMA BARRIER NPNH LINK -1 NP-HEAD-ACC LINK 
               *-1 V-NON-AUX BARRIER NPNHAII)(0 N)(1 CC LINK
                 1 NP-HEAD-ACC   BARRIER NPNHA LINK 1 NP-BORDER);
         ## Jos juohká dáz1aid, sámiid ja láddelac1c1aid ies1 gud1et sadjái, ...

  # Third part
SELECT Acc IF (*-1 CC BARRIER NPNH LINK -1 NP-HEAD-ACC LINK -1 COMMA BARRIER
               NPNH LINK -1 NP-HEAD-ACC LINK *-1 V-NON-AUX BARRIER NPNHAII)
               (0 N)( 1 NP-BORDER);
         ## Jos juohká dáz1aid, sámiid ja láddelac1c1aid ies1 gud1et sadjái, ...


# Accusative for a topicalised verb
# ---------------------------------

REMOVE Gen IF (@1 V-NON-AUX + Clt)(NOT -1 (N Acc))
              (1 EOS OR CLB);             ## C1ilgego moai dan?
  # This is not a good rule. Have more looks at it.
  # On the other hand side: With the addition (1 EOS)
  # it has become too careful. Thus, let's try with CLB as well.


#REMOVE Gen IF (-1 VFIN)(0 N)(1 CLB); ## Joramii riegádii Ussia, Ussiai riegádii
# The rule is too specific.          ## Jotam.
# - And not needed any more for these examples. See rule under "Accusative sentence-finally"!


# Numeral as object after postverbal subject
# - - - - - - - - - - - - - - - - - - - - -

SELECT Acc IF (-1 (N Nom) LINK *-1 VFIN BARRIER NPNHAIIP)
              (0 Num)(NOT 1 Po);
       ## Lassin nammadii direktevra guokte bargi ja ozai c1álli lávdegoddái


# Accusative in time expressions
# ------------------------------

#SELECT Acc IF (0 VAHKKU)(NOT 1 Po);
 # Too general!
	## Min áiggi boaresolbmot muitaladdet ahte...


# Object predicatives
# -------------------

SELECT Acc IF (*-1 Acc BARRIER NPNHA)(0 A)(NOT 0 Adv)(1 CLB OR EOS OR CS);
                                     ## Dáz1at eai atnán sámiid buoret go beanan.


# Illative
# ========

REMOVE (V Ind Prt Sg3) IF (-1 (N Gen))(0 Ill);
                           ## Mud1ui gustojit seamma njuoggadusat sámi levgii.


REMOVE (V Ind Prt Sg3) IF (*-1 Acc BARRIER NPNH LINK *-1 VFIN);
                           ## Cokka láibbiid sehkkii.


#  Genitive
# ===========
  CONSTRAINTS
# ===========

# Genitives that are complements of adpositions are dealt with in an earlier
# cycle.


# Genitive as specifier
# ---------------------

# Genitive in front of noun in the beginning of a sentence
# - - - - - - - - - - - - - - - - - - - - - - - - - - - -

REMOVE (N Acc) IF (-1 NP-BORDER)(NOT -1 V)
                  (*1 N BARRIER NON-A LINK *1 VFIN BARRIER CS);

REMOVE (N Nom) IF (-1 NP-BORDER)(NOT 1 ("leat") OR Inf)
                  (*1 N BARRIER NON-A OR CLB LINK *1 VFIN BARRIER CS);
                                         ## Nieidda girji lea ruoksat.

REMOVE (Pron Pers Acc) IF (-1 NP-BORDER)(*1 N BARRIER NON-Attr LINK *1 VFIN);
#REMOVE (Pron Pers Nom) IF (-1 NP-BORDER)(*1C N BARRIER NON-Attr LINK *1 VFIN);
                                         ## Din viessu s1attai oppa stuoris.

#REMOVE (Dem Acc) IF (-1 NP-BORDER)(NOT -1 V)
#                  (*1 N BARRIER NON-A LINK *1 VFIN BARRIER CS);
                                         ## Dan girji lea ruoksat.
#REMOVE (Dem Nom) IF (-1 NP-BORDER)
#                  (*1 N BARRIER NON-A LINK *1 VFIN BARRIER CS);
# Desse to var for nokre akkusativ-pronomen som skulle vore Gen, men no
# finn eg dei ikkje att.


SELECT Gen IF (*-1 BOS BARRIER NPNH)(NOT 0 ("mii" Interr) OR Dem OR Attr)
	(1C (N Nom) OR (N Acc));

# The 'select' rule was too powerful, we do 'remove' instead.
#SELECT Gen IF (-1 NP-BORDER)(*1 N BARRIER NON-A LINK *1 VFIN);

REMOVE Gen IF (-1 CLB)(1 (Pron Pers))(*1 VFIN); ## Na, du mun lean oaidnán.
REMOVE Gen IF (-1 BOS)(1 (Pron Pers))(*1 VFIN); ## Du mun lean oaidnán.

REMOVE Gen IF (0 Adv)(NOT *-1 Pr BARRIER NPNH)
              (NOT *1 Po BARRIER NON-NP)
	      (NOT *1 N BARRIER NON-NP) ;  


# Genitive in nominal phrase between finite and nonfinite verb
# - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -  

SELECT Gen IF (*-1 VFIN BARRIER NON-ADV-PCLE)(NOT 0 Nom OR Dem OR VGen OR PrfPrc)
	(*1 (N Nom) OR (N Acc) BARRIER NPNH OR CC OR PUNCT
	LINK *1 Inf OR PrfPrc OR (Actio Ess) OR CLB BARRIER NON-ADV-PCLE);  


# Genitive in front of noun at the end of a sentence
# - - - - - - - - - - - - - - - - - - - - - - - - - 

SELECT Gen IF (NOT 0 Dem OR Attr OR VERB)(*1C Acc BARRIER NPNH OR CC LINK 1 EOS);

   ## Fylkka mearrida olugo galgá atnit sámi od1d1a leavgga.
   ## Ulbmiliin lea nannet sámegiela máhtolas1vuod1a Sámedikki organisas1uvnnas.
   ## Ulbmiliin lea duohtan dahkat sámegiela geavaheapmi Sámedikkis.


# Genitive in front of the final word in the sentence, or: NP-initially
# - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - 

SELECT Gen IF (0C CNOUN)(*1C (N Acc) BARRIER NPNH)
              (2 NP-BORDER OR ADVLCASE);  # earlier: (2 CLB);
                                            ## Oainnán nieidda biilla.
                                            ## Oainnán nieidda biillas.

SELECT Gen IF (0C (Pron Pers))(*1 Acc BARRIER NPNH LINK 1 CLB);
                                            ## Oainnán su od1d1a biilla.
                    # Unify these? Look at the effect of each and one of them.

SELECT Gen IF (0C (N Prop) )(*1C CNOUN BARRIER NPNH LINK 1 CLB);
                                            ## Oainnán Uria biilla.

SELECT Gen IF (NOT 0 A)(*1 (N Acc) BARRIER NPNH OR CC LINK 1 PrfPrc);
			## Mun lean du biilla oaidnán. 


# Specifier disambiguation in coordination constructions
# - - - - - - - - - - - - - - - - - - - - - - - - - - -

REMOVE Acc IF (-1 CC)(*1C (N Nom) BARRIER NPNH);

SELECT Gen IF (-2 Ill)(-1 CC)(*1 Ill BARRIER NPNH);
SELECT Gen IF (-2 Com)(-1 CC)(*1 Com BARRIER NPNH);
SELECT Gen IF (-2 Loc)(-1 CC)(*1 Loc BARRIER NPNH);
SELECT Gen IF (-2 Ess)(-1 CC)(*1 Ess BARRIER NPNH);
                        ## Suoma sisafárrejeaddjiide ja sin man1isboahttiide.


# Genitive inside a two-part Po
# -----------------------------

SELECT Gen IF (-1 POST-A)(1 N)(2 POST-B);
## Nu leat 40 buolvva Dávveda rájes Babylonia fán1gavuod1a rádjái.

#REMOVE Acc IF (-1 BOS)(1 (N Gen) OR (Attr) OR PPOS) ;
                                            ## Mu birra don humat.
                        # This should be covered by the others... Check.

# Genitive relative pronouns
# --------------------------

SELECT (Pron Interr Gen) IF (1 N) (*1 COPULAS BARRIER CLB)
	(NOT *1 V-NON-AUX-PASS BARRIER CLB);
                               ## Oidnen mátkkos1teaddji, gean namma lei Keilhau.

# Genitive as agent of Actio
# --------------------------

SELECT Gen IF (-1 BOS)(0 Interr)(1 Actio);


# Genitive in time expressions
# ----------------------------
# A 'measure' time adverbial is Acc, others are Gen. 

# Genitive if cardinal number
SELECT Gen IF (0 Card)(1 (N Gen));       ## Dat lea duháhiid jagiid boaris.

SELECT Gen IF (-1 ("seammá" Attr))(0 TIME); # Other attributes similar?

SELECT Gen IF (0 MANNU)(1 (Num Ord));    ##  Dál lea miessemánu 1. beaivvi.

SELECT Gen IF (-1 Ord)(0 ("beaivi")); 

SELECT Gen IF (0 ("jahki"))(1 Num); 

SELECT Gen IF (1 ("áigi"));

SELECT Gen IF (-1 Gen)(0 ("áigi")); 
# "áigi" can be an accusative object, but then there is in most cases no genitive 
# in front - cf. 'dárbbas1a guhkit áiggi', 'gáibida olu áiggi' vs. 'dulvvi áiggi'.

SELECT Gen IF (0 ("hávvi"))(*1C Acc BARRIER CS OR CP);
SELECT Gen IF (NOT *-1 V-NON-AUX BARRIER CS OR CP)(0 ("hávvi"))
		(*1 V-PASS BARRIER V-NON-AUX OR CS OR CP);
SELECT Gen IF (*-1 V-PASS BARRIER VFIN)(0 ("hávvi"))(NOT *1 V-NON-AUX BARRIER CS OR CP);
# The time expression hávvi is not Acc, but the problem is the homonymous noun. 


# Genitive in proper name sequences
# ---------------------------------

REMOVE Acc IF (0 Prop)(1 Prop);          ## Dat lea Norgga Röros ja Máhte Piera.
  # Hmm, very general indeed!


# Genitive after "mii"
# -------------------

SELECT (N Pl Gen) IF (-1 ("mii"))(1 NP-BORDER)(*1 ("?")); 
						## Maid ipmas1iid doppe dagat?
	# Embedded questions are also possible, but harder to formulate a rule. 
  

# Locative singular
# ================= #
      CONSTRAINTS
# ================= #

SELECT Loc IF (*-1 BOS OR CS BARRIER NPNHAI)
              (*1 ("leat") BARRIER NON-ADV-NEG);
                                          ## Ádjásgo lea od1d1a fanas.
                                          ## Suomas maid lei dilli váttis.
                                          ## Lágas eai leat mearrádusat.
					## Daningo sámiin lea iez1aset giella.

SELECT Loc IF (-2 BOS)(-1 ("leat" Clt)); ## Leago mus rud1aid?
# hmm...

SELECT Loc IF (0 VAHKKU);
           ## 1870-jagiin lea c1ielggas ahte láddelac1c1aide guske ortnegat.


# Comitative singular
# ===================

# NP-internal disambiguation of Com

SELECT Com IF (-1C (Dem Sg Com));         ## Vuolggán dainna biillain.
SELECT Com IF (-2C (Dem Sg Com))(-1 Adv); ## Vuolggán dainna biillain.


# Disambiguation based upon type of NP

SELECT Com IF (0 Prop); # assuming proper names come in sg


# Disambiguation based upon verb valency
#SELECT Com IF (*-1 COM-VERB)(0 (Sg)) ;
SELECT Com IF (*-1 COM-VERB BARRIER CS)(0 Sg);
                             ## Son náitali nieiddain.

SELECT Com IF (0 Sg)(*1 COM-VERB BARRIER CS);
                             ## Nieiddain son náitali.

# Locative plural
# ===============

# NP-internal disambiguation of Loc

SELECT Loc IF (-1 (Dem Pl Loc));         ## Mii c1ohkkát dain biillain.
SELECT Loc IF (-2 (Dem Pl Loc))(-1 Adv); ## Mii c1ohkkát dain biillain.
# Not too elegant, the rule set might be done more general with a barrier


SELECT (Pl Loc) IF (-2 Num)(-1 PROSEANTA); ## Dat lea 50 % rud1ain.

SELECT Loc IF (1 ("gos"));           ## Son lea guovlluin gos sámit ásset.
SELECT Loc IF (1 COMMA)(2 ("gos"));  ## Son lea guovlluin, gos sámit ásset.


# Disambiguation based upon type of NP

SELECT (Pl Loc) IF (0 GUOVLU);

# Disambiguation based upon verb valency

SELECT Loc IF (*-1 LOC-VERB BARRIER CS);
                             ## Son balla nieiddain.

SELECT Loc IF (*1 LOC-VERB BARRIER CS);
                             ## Nieiddain son balla.


# Loc Pl if NP-complement (A book about the wars)

#SELECT Loc IF (*-1 N-NON-GEN BARRIER NPNH)(0 Pl);
        ## Ii leat dus1s1e sáhka olbmuid oktavuod1ain juohkebeaivválas1 eallimis.
# BUT: ## Luossabivdin joddubuod1uin (i.e. with...).



# Disambiguation based upon coordination
# =========== #
  CONSTRAINTS
# =========== #
# The idea is to let the other rules work first, and then let the
# coordination follow suit.

# Simple coordination

SELECT Gen IF (1 CC)(*2 Po BARRIER NON-NP) ; # Overgenerates?
	# Váfistan ja áimmahus1s1an dihte od1d1a bargovugiid..

SELECT (Pl Loc) IF (*-1 CC LINK *-1 Loc BARRIER NPNH LINK NOT 0 Com);
                                     ## Son lea skuvllas ja od1d1a mediain.
SELECT (Pl Loc) IF (1 CC)(*2 Loc BARRIER NPNH LINK NOT 0 Com);
                                     ## Son lea od1d1a mediain ja skuvllas.

SELECT (Sg Com) IF (*-1 CC LINK *-1 Com BARRIER NPNH LINK NOT 0 Loc);
SELECT (Sg Com) IF (1 CC)(*2 Com BARRIER NPNH LINK NOT 0 Loc);


# Coordination within coordination
# Context to the left:
SELECT (Pl Loc) IF (*-1 CC BARRIER NPNH
                   LINK -1 Cmpnd LINK *-1 CC BARRIER NPNH 
                   LINK -1 Loc LINK NOT 0 Com);
                   ## Son lea skuvllas ja od1d1a fylkka- ja riikkamediain.

SELECT (Sg Com) IF (*-1 CC BARRIER NPNH
                   LINK -1 Cmpnd LINK *-1 CC BARRIER NPNH
                   LINK -1 Com LINK NOT 0 Loc);

# Context to the right:
SELECT (Pl Loc) IF (1 CC)
                   (*2 Cmpnd BARRIER NPNH LINK 1 CC
                   LINK *1 Loc LINK NOT 0 Com);
                 ## Son lea od1d1a fylkka- ja riikkamediain ja skuvllas.

# This part is the addition for right context checking of
# coordination within coordination:
# LINK -1 Cmpnd LINK *-1 CC BARRIER NPNH 

SELECT (Sg Com) IF (1 CC)(*2 Com BARRIER NPNH LINK NOT 0 Loc);


# Generalising the Cmpnd addition:
# Cmpnd in the left branch, checking to the left:




# ==================== #
# Disambiguating verbs #
# ==================== #

# Finite forms #
# ============ #
   CONSTRAINTS #
# ============ #

SELECT VFIN IF (*-1 BOS BARRIER VERB)(0 INF-VERB)(*1 Inf BARRIER CS OR CP);
 # Looks brave but works!
			## Áigugo skuvlii álgit? 
			## Sáhttit maid namahit ahte ...

SELECT VFIN IF (-1 Loc)(0 LOC-VERB); # Needs refinement?


# Indicative or imperative #
# ======================== #

REMOVE Imprt IF (0 Qst);     ## Bijatgo don sohkkara dedjii?

  # We could perhaps have had some restrictions here, like
  # (0 Ind) and/or (1 Nom) or something. But Both Imprt and Qst sounds
  # unlikely.

REMOVE Imprt IF (-1 BOS)(0 Adv)(*1 ("?") BARRIER CS); ## Gosa don dál?

REMOVE (Imprt Sg2) IF (*1 (Sg3 Nom) OR (N Nom)); # Looks brave but works.


# Indicative Negative
# -------------------
  # Unique are Sg1, Sg3, Du1, Du2, Du3, Pl1, Pl2, Pl3
  # hom: "it", where Sg2 = ABBR.

# Select Neg Sg2 instead of ABBR
REMOVE ABBR IF (*-1 ConNeg OR PrfPrc)(0 Neg); ## Dáppe don leamas1 it.
REMOVE ABBR IF (0 Neg)(*1 ConNeg OR PrfPrc);  ## Don it leat dál.


# Indicative Present
# ------------------

# Sg1
# ---
  # Sg1 = PtcPrf (odd-syllables), and Sg1 = V Actio (even-syllables)

SELECT Sg1 IF (0 LEAN) (*1 PrfPrc);           ## Mun lean boahtán.
  # So, what kind of barrier could we have here?

SELECT Sg1 IF (*-1 MUN BARRIER Neg OR LEAN-SG1 OR Inf);  ## Mun ferten lohkat.
                                                  ## Mun lean muitalan dan. 

SELECT Sg1 IF (-2 (V Sg1))(-1 CC);              ## Mun human ja human.

#SELECT Sg1 IF (NOT *-1 (V Neg))(NOT *-1 PrfPrc OR Actio OR V-SG1);
#SELECT Sg1 IF (NOT *-1 VFIN)(NOT 1 CC)(NOT 2 Actio) ; # many wrong results            
					 ## Háliidan hupmat.
                                       	 ## In háliidan hupmat.
			## Váfistan ja áimmuhus1s1an dihte...

# Note: We will need a rule selecting Sg1. # Mun muitalan Suoma birra.


# Sg2
# ---
  # Sg2 = Inf (even syllables, copula)
  # Note the homonymy with derived verbs (jearat = jearadit + ...

SELECT Sg2 IF (0 LEAT) (*-1 DON BARRIER NON-ADV); # no hits

SELECT Sg2 IF (*-1C DON BARRIER Neg)(NOT 0 Imprt); ## fertetgo don vuolgit?

SELECT Sg2 IF (NOT 0 Imprt)(*1C DON BARRIER Neg); ## De, don fertet vuolgit.

# The NOT 0 Imprt part for this example:   ## Gosa don bidjet daid sáiddiid?

# Sg3
# ---
  # Sg3 = ConNeg (even)
  # Sg3 = ConNeg = Imprt Sg2 (contracted)
  # Sg3 = Sg Ill for certain words!!
  # Sg3 = VGen
  # Disambiguating Prs Sg3 is taken care of by rules removing the other 
  # readings whenever their conditions are not fulfilled.

SELECT Sg3 IF (-2 CLB)(-1 (Sg3 Nom)); # Allow for phrasal subject?
SELECT Sg3 IF (-2 CLB)(-1 (N Sg Nom));

SELECT Sg3 IF (-1 CLB)(NOT 0 Adv)(1 (Sg3 Nom));
SELECT Sg3 IF (-1 CLB)(NOT 0 Adv)(1 (N Sg Nom));

SELECT V-SG3 IF (*-1 (N Sg Nom) OR (Pron Sg3 Nom) BARRIER VFIN)
		(NOT *-1 V-SG3 BARRIER CS OR CP)
		(NOT *1 V-SG3 BARRIER CC OR CS OR CP);

SELECT V-SG3 IF (-2 V-SG3)(-1 CC)(NOT 0 Adv OR A);

# Rules that look for a subject across a relative clause:

SELECT V-SG3 IF (-1 CLB LINK *-1 (Pron Interr) BARRIER CS LINK *-1 (N Sg Nom) OR
	(Pron Sg3 Nom) BARRIER N OR VFIN LINK NOT *-1 V-SG3 BARRIER CS OR CP)
	(NOT 0 Qst);

SELECT V-SG3 IF (*-1 VFIN BARRIER Nom OR CS OR CP LINK *-1 (Pron Interr)
	BARRIER CS LINK *-1 (N Sg Nom) OR (Pron Sg3 Nom) BARRIER VFIN 
	LINK NOT *-1 V-SG3 BARRIER CS OR CP)(NOT 0 Loc OR Ill OR A OR Adv OR Qst);   
  

# Du1
# ---
  # Du1 = Prt Pl3

REMOVE Pl3 IF (0 Sg3) (-1 (N Sg Nom)); ## Dokumeanta c1ilge, mo mii eallit.

SELECT Du1 IF (*-1  MOAI BARRIER MII-PERS OR CLB);
SELECT Du1 IF (*1  MOAI BARRIER MII-PERS OR CLB);
SELECT Du1 IF (1 Pcle) (2 MOAI);               ## Jerre go moai?

SELECT Du1 IF (*-1 (N Sg Nom) BARRIER NON-ADV LINK *-1 CC BARRIER NPNHAI 
              LINK -1 (N Sg Nom));
                              ## Anne ja Biret c1ilge, ahte dat lea duohta.

REMOVE (V Du1) IF (NOT *-1 MOAI)(NOT *1 MOAI);


# Du2
# ---

REMOVE (Du2) IF (*1 (Sg3)); # Add barriers, and a rule for preceding Sg3?

  # leahppi = ("leahppi" N Sg Nom)

REMOVE ("leahppi") IF (*-1 DOAI)(0 ("leat" V Ind Prs Du2));
REMOVE ("leahppi") IF           (0 ("leat" V Ind Prs Du2))(1 DOAI);
  # If there is a pronoun, then we have dual, and not the noun.

# Du3 
  # Du3 = PrsPrc Clt = Imprt Prs Du2 Clt
  # Du3 = Ind Prs Sg3 Clt "leaba, ferteba leat"

SELECT (Du3) IF (*-1 ("son" Pron Pers Du3 Nom)); ## Soai ferteba mannat.
SELECT (Du3) IF (*1 ("son" Pron Pers Du3 Nom));  ## fertebago soai mannat?
SELECT (Du3) IF (-1 (N Sg Nom))
                (*-2 CC BARRIER NPNH LINK -1 (N Sg Nom));
                           ## Per Berg ja Rolf Olsen leaba doppe.
                           ## Stuorra nieida ja unna gánda ferteba leat doppe.

SELECT (Du3) IF (*-1 ("guokte")) ; ## Guokte viesu leaba dál.
	# The competition is -ba Foc.


# Pl1
# ---
  # Pl1 = Inf for all stem classes
  # Pl1 = Inf = Pl3 = Prt Sg2 for odd syll
  # Pl1 = Inf = Pl3 = Sg2 = ConNeg (copula)

SELECT Pl1 IF (*-1C MII-PERS BARRIER SII OR MOAI OR CLB OR V-PL1);

#SELECT Pl1 IF (-1 CC -2 V-PL1 LINK *-1C MII-PERS BARRIER SII OR MOAI OR CLB);

SELECT Pl1 IF (NOT -2 (V Ind Prs Pl1 Clt))(-1 MII-PERS);
                                                ## Fertetgo mii fertet?

#SELECT Pl1 IF (NOT -3 (V Ind Prs Pl1)(-2 go)(-1 MII-PERS);
                                                ## Fertet go mii fertet?

SELECT Pl1 IF (-1 BOS)(0 (Clt))(1 MII-PERS);

SELECT Pl1 IF (1 MII-PERS)(NOT *2 V-PL1 BARRIER CLB);


  # Pl2 unique, no rules needed

# Pl3
# ---
  # Prs Pl3 = Prt Sg2 = Prs Pl1 = Inf (odd syll)   muitalit
  # Prs Pl3 = Prt Sg2 = Imprt Prs Pl2 (even syll)    vulget
  # Prs Pl3 = Prt Sg2                 (contracted) fertejit

REMOVE Pl1 IF (*-1C (N Pl Nom) BARRIER NON-ADV-PCLE);
REMOVE Pl1 IF (-1 Nom)(NOT -1 Pl1);
REMOVE Sg2 IF (-1 Nom)(NOT -1 Sg2);


# Preverbal subject
# - - - - - - - - - 
 # Single noun
SELECT Pl3 IF (*-1C NP-HEAD-PL-NOM BARRIER PPRON-NOT-SII OR (N Sg Nom))
		(NOT *-1 (V Pl3) BARRIER CS);
                            ## Nieiddat jerre.
                            ## Makkár vuoigatvuod1at dain leat riikavuloz1in?
			## Ciza atnet olbmot firpmiid várjalit.	

 # Coordination
SELECT Pl3 IF (*-1 NP-HEAD-SG-NOM BARRIER NON-ADV LINK *-1 CC BARRIER NPNH
           LINK -1 NP-HEAD-SG-NOM);        ## Nieida ja gánda jerre.

 # Pronoun
SELECT Pl3 IF (*-1 SII BARRIER MII-PERS OR MOAI OR CLB); ## Sii jerre.

 # Numeral
SELECT Pl3 IF (*-1 (Num Nom) BARRIER CS OR PPRON-NOT-SII OR (N Sg Nom))
		(NOT *-1 (V Pl3) BARRIER CS);

# Rules that look for a subject across a relative clause:

SELECT V-PL3 IF (-1 CLB LINK *-1 (Pron Interr) BARRIER CS LINK *-1 (N Pl Nom) OR
	(Pron Pl3 Nom) BARRIER N OR VFIN LINK NOT *-1 V-PL3 BARRIER CS OR CP)
	(NOT 0 Qst);

SELECT V-PL3 IF (*-1 VFIN BARRIER Nom OR CS OR CP LINK *-1 (Pron Interr)
	BARRIER CS LINK *-1 (N Pl Nom) OR (Pron Pl3 Nom) BARRIER N OR VFIN 
	LINK NOT *-1 V-PL3 BARRIER CS OR CP)(NOT 0 Qst OR Adv);


# Postverbal subject
# - - - - - - - - -

SELECT Pl3 IF (NOT *-1 VFIN BARRIER CS OR CP)(0C VERB)(*1 (N Pl Nom) OR (Pl3 Nom)
	OR (Pron Pl Nom) BARRIER CS OR CP);

#SELECT Pl3 IF (0C VERB)(*1 (N Pl Nom) OR (Pl3 Nom) OR (Pron Pl Nom) 
#		BARRIER NPNHAI OR CLB);

#SELECT Pl3 IF (0C VERB)(*1 (N Pl Nom) OR (Pl3 Nom) OR (Pron Pl Nom) BARRIER NON-ADV);

SELECT Pl3 IF (NOT *-1 INF-VERB)(NOT 0 Pron OR Adv) # Instead: (0C VERB)? 
		(*1 (Num Nom) BARRIER NON-ADV-PCLE LINK NOT 0 OKTA);

 # Sentence-initial verb with question particle and coordinated subject
SELECT Pl3 IF (*-1 BOS BARRIER NON-ADV-PCLE)
              (*1 (N Sg Nom) BARRIER NPNH OR NON-ADV-PCLE
              LINK 1 CC LINK *1 (N Sg Nom) BARRIER NPNH);  ## Jerrego Al ja Bo?

 # Sentence-initial verb with question particle and subject 'sii'
SELECT Pl3 IF (*-1 BOS BARRIER NON-ADV-PCLE)
              (*1 SII BARRIER NON-ADV-PCLE);          ## Jerrego sii?

 # Sentence-initial verb with question particle and ordinary plural subject
SELECT Pl3 IF (*-1 BOS BARRIER NON-ADV-PCLE)(*1C NP-HEAD-PL-NOM
              BARRIER NPNH);			          ## Jerrego nieiddat?

SELECT Pl3 IF (*-1 BOS BARRIER NON-ADV-PCLE)(1 ("go" Pcle))(*2C NP-HEAD-PL-NOM
              BARRIER NPNH);			          ## Jerre go nieiddat?

 # Post-adverbial verb with final subject
SELECT Pl3 IF (-1 Adv OR ADVERBIAL LINK *-1 BOS BARRIER NPNH)
              (*1 (N Pl) BARRIER NPNHAI);


# Relative pronoun as subject
# - - - - - - - - - - - - - - 

SELECT Pl3 IF (-3 NP-HEAD-PL-NOM)(-2 COMMA)(-1 (Pron Interr Pl Nom)) ;
             #olbmot, geat fállet (too specific?)
SELECT Pl3 IF (-2 NP-HEAD-PL-NOM)(-1 (Pron Interr Pl Nom)) ;
             #olbmot geat fállet (too specific?)


# Existential sentences
# - - - - - - - - - - - 

SELECT ("leat" Pl3) IF (-1 (Loc))(1 NP-HEAD-PL-NOM); ## Mis leat beatnagat.
SELECT ("leat" Pl3) IF (-1 (Loc))(1 NUMERALS); ## Mis leat golbma beatnaga.

## Dá livc1c1e dutnje sabehat.


# Number phrases as subjects
# - - - - - - - - - - - - - 

SELECT Pl3 IF (-2 Num) (-1 N-GEN) ; ## Golbma nieidda leat boahtán.
SELECT Pl3 IF (-1 Adv) (1 Num); ## Nu jerre golbma nieidda


SELECT Pl3 IF (-1 Loc) (1 Attr OR Pron)(2 (N Pl Nom));## Mus leat od1d1a sabehat.

# Coordination
# - - - - - - 

REMOVE V-SG2 IF (-1 CC)(*-2 V-PL3 BARRIER DON);  

 

# Selecting some present
# ----------------------

SELECT Prs IF (NOT *-1 V-IND-FIN BARRIER CS)(0 LEAT)(NOT *1 V-IND-FIN)  ;
 # More barriers? COLON?
 # one strange hit, revise!
 # The same should be done ... hmm, for what?

SELECT Prs IF (-1 (Neg Prs)) ;

# Select some finite
REMOVE Inf IF (-2 CLB) (-1 CS); ## Dus1s1e, juos leat doarvái...
REMOVE Inf IF (-1 Inf) ; # This will work when all INF-VERBs are identified!


# Indicative Past
# ---------------

  # Past Sg1 is unique

# Past Sg2
# --------
  # Prt Sg2 = Prs Pl3 = Imprt Prs Pl2 (even syll)    vulget
  # Prt Sg2 = Prs Pl3 = Prs Pl1 = Inf (odd syll)   muitalit
  # Prt Sg2 = Prs Pl3                 (contracted) fertejit

SELECT Sg2 IF (*-1 (Pron Pers Sg2 Nom))(0 Prt);             ## Don vulget ikte.
SELECT Sg2 IF (-1 BOS)(0 (Prt Clt))(1 (Pron Pers Sg2 Nom)); ## Vulgetgo don?

SELECT Sg2 IF (*-1 IKTE)(0 Prt);               ## Mun died1an, ahte vulget ikte.
SELECT Sg2 IF (-1 BOS)(0 (Prt Clt))(*1 IKTE);  ## Vulgetgo ikte?


# Past Sg3
# --------
  # Prt Sg3 = Imprt Prs Sg3 (leai)

REMOVE Sg3 IF (*-1 Neg)(0 (Sg Ill));    ## Iiba leat skuvlii álgán.

REMOVE Sg3 IF (-1 VFIN)(0 (N Sg Ill));   ## Mánná galggai skuvlii.

REMOVE Ill IF (*-1 (N Sg) OR (Pron Sg) BARRIER NON-ADV-PCLE
              LINK *-1 BOS OR CS OR CC OR Adv BARRIER NPNHAI)
              (0 (V Ind Sg3));
                               ## Muhto eanandoallu gillái rievdemiid bokte.

REMOVE Ill IF (-2 BOS)(-1 Adv OR ADVLCASE)(0 (V Ind Sg3));
                               ## Dattege loktii dáz1a sisafárren gitta 1850.

REMOVE Ill IF (NOT *-1 ("seammá") OR ("eará"))(1 ("go" Pcle));
           ## Gos lei riegádan, ja máhtii go dárogiela?
           ## Mud1ui gustojit seammá njuolggadusat sámi levgii go dáz1a levgii.



# Past Du1
# --------
# Past Du2
# --------
# Past Du3
# --------
# Past Pl1
# --------
# Past Pl2
# --------


# Past Pl3
# --------
  # Prt Pl3 = Prs Du1

# leai disambiguation
SELECT Prt IF (0 LEAI)(-1 NP-HEAD-SG-NOM); # Introduced 0 LEAI

REMOVE (Prs Du1) IF (0 (Prt Sg3)) (-1 (N Pl Nom)); ## Dokumeantat c1ilge, mo...

SELECT (Prt Pl3) IF (NOT *-1 (N Sg Nom) OR (Pron Sg Nom))
                    (*1 (Pl Nom) BARRIER VFIN);
                                    ## Maiddá siidaguovlluide bohte soapmásat.
  # REMOVE (Prs Du1 instead?

SELECT Pl3 IF (*-1 BOS LINK 1 ("go") LINK *1 (V Pl3))(-1 COMMA)
              (0 (V Ind Prt Pl3));



# Potential Present
# -----------------
# Potential Past
# --------------
# Conditional
# -----------

# Sg3
  # copula sg3 = pl3 livc1c1e (in the eastern dialect)

SELECT Sg3 IF (NOT *-1 (Pl Nom) OR CC)(0 ("leat V Cond Sg3))
              (NOT  *1 (Pl Nom) OR CC);         ## Dá livc1c1e dutnje gahpir

SELECT Pl3 IF (*-1 (Pl Nom))(0 ("leat V Cond Pl3));

SELECT Pl3 IF (NOT *-1 (Sg Nom))(0 ("leat" V Cond Pl3))(*1 (Pl Nom));
                                      ## Dá livc1c1e dutnje sabehat.

# These rules may be generalised. Let's first see whether they work.
SELECT (Sg3) IF (-1 ("Son" Pron Pers Sg Nom))(0 ("leat"));
SELECT (Sg3) IF (NOT *-2 CC)(-1 (N Sg Nom) OR ("son" Pron Pers Sg Nom))
             (0 ("leat")); # strange rule, revise!
SELECT (Sg3) IF (-1 Loc)(1 (N Sg Nom)); # Mus livc1c1e viessu.

# Selecting Sg3 is not restricted to leat. This is not a rule for Conditional.

# Pl3
  # copula sg3 = pl3

SELECT (Pl3) IF (-1 (N Pl Nom) OR ("son" Pron Pers Pl Nom))(0 ("leat"));
SELECT (Pl3) IF (-1 Loc)(1 (N Pl Nom)); # Mus livc1c1e guolit.


# Imperative
# ----------

SELECT (Imprt ConNeg) IF (-1 (Neg Imprt)) ; ## Ale suhta munnje.

#REMOVE Imprt IF (NOT -1 COMMA OR BOS or Pcle)(NOT 0 (ConNeg)) ;
REMOVE Imprt IF (NOT -1 COMMA OR BOS or Pcle)(NOT 0 (ConNeg)) ;
# Imprt must be either a negative verb form, or in the middle of a sentencne.

REMOVE Imprt IF (NOT -2 Nom)(-1 COMMA) ;
            ## Máret, boad1e deike!
            ## Dan maid dárbbas1a fievrridit, ferte olmmos1 noad1uhit.

# Negative imperative
  # Neg Imprt Pl2 = allet V Inf = Prs Pl1 = Prs Pl3

SELECT (Neg Imprt Pl2) IF (*1 ConNeg BARRIER Neg); ## Allet ge gátte ahte ...


REMOVE (V Ind Prs Sg3) IF (-2 BOS)(-1 Pcle)(0 (V Imprt Prs Sg2));
                           ## Na attes munnje daid.
                           # Note that this is dangerous, and kept specific in
                           # order to play safe. May need to be generalised.


# Infinite forms #
# ============== #
    CONSTRAINTS  #
# ============== #

REMOVE VFIN IF (-1 ("nu"))(*1 ("go" Pcle) BARRIER NON-NA); 

REMOVE Inf IF (-1 ("nu"))(*1 ("go" Pcle) BARRIER NON-NA); 


# Infinitive
# ----------

REMOVE VFIN IF (*-1C VFIN BARRIER NON-ADV-PCLE);

REMOVE VFIN IF (*-1 VFIN-NON-AUX OR ConNeg OR PrfPrc
	BARRIER ("mii" Pers Pron Nom Pl1) OR SII OR CP OR CS OR CC OR CLB)
               (0 Inf)(NOT 0 LEAT);
                           ## Mun háliidan bargat. Son ii háliidan bargat.
                           ## Stockfleth lea beassan bargat sámegiela buorrin.
                           # reintroduce (NOT 0 LEAT)? Yes.

# The following two rules extend the preceding one.
REMOVE (V N Actor Pl Nom) IF (*-1 VFIN-NON-AUX OR ConNeg OR PrfPrc
                    BARRIER ("mii" Pers Pron Nom Pl1) OR SII OR CS OR CLB)
               (0 Inf)(NOT 0 LEAT);
REMOVE (N Pl Nom) IF (*-1 VFIN-NON-AUX OR ConNeg OR PrfPrc
                    BARRIER ("mii" Pers Pron Nom Pl1) OR SII OR CS OR CLB)
               (0 Inf)(NOT 0 LEAT);
REMOVE (V Ind Prs ConNeg) IF (*-1 VFIN-NON-AUX OR ConNeg OR PrfPrc
                    BARRIER ("mii" Pers Pron Nom Pl1) OR SII OR CS OR CLB)
               (0 Inf)(NOT 0 LEAT);

REMOVE (V Pl3) IF (-1 (N Sg Nom))(*-1 V-SG3 BARRIER VFIN OR CS);
REMOVE (V Pl3) IF (-1 (N Sg Nom))(*1 V-SG3 BARRIER VFIN OR CS);

#SELECT Inf IF (*-1 VFIN BARRIER Neg)(0 LEAT);
#                           ## Jos galggas1 leat doaivva kristtalas1vuod1a nannet,
                            
SELECT Inf IF (-2 (Sg Nom) OR (Sg3 Nom))(-1 (Interr Sg Nom))(1 (V Sg3));
SELECT Inf IF (-3 (Sg Nom) OR (Sg3 Nom))(-1 (Interr Sg Nom))(-2 COMMA)
              (1 (V Sg3));
                           ## Son gii ohcat áiggui, fertii...
 # This rule is all to specific. It tries to capture topicalised objects
 # Within a subject relative clause.

SELECT Inf IF (-1 V-PL1)(NOT -1 Neg)(*-2C MII-PERS BARRIER SII OR MOAI OR CLB);


# Infinitive after predicative
# - - - - - - - - - - - - - - 

# There are two rules in the beginning, conflate when I see the effect.
# The barrier may be too weak.

SELECT Inf IF (-1 A BARRIER VFIN LINK *-1 ("leat") OR ("s1addat") 
              BARRIER NON-ADV)(NOT 0 (N Pl Nom));
                           ## Eará guovlluin lei vejolas1 nannet dárogiela.

SELECT Inf IF (*-1 N BARRIER VFIN LINK *-1 ("leat") OR ("s1addat") 
              BARRIER NPNHAI)(NOT 0 (N Pl Nom));
                           ## Jos leat jaska, de lea dárbu singuin gulahaddat.

# Infinitive as subject
# - - - - - - - - - - - 

REMOVE (Prs Pl1) IF (-1 BOS)(1C V-SG3);
                           ## Vuolgit lea suohtas.

REMOVE NP-HEAD-PL-NOM IF (0 (Inf) OR NP-HEAD-SG-NOM)(1C V-SG3);


# Infinitive in coordination
# - - - - - - - - - - - - -

SELECT Inf IF (1 CC)(2C Inf); ## Mus lea árvalusat mo skuvlla nannet ja buoridit.
SELECT Inf IF (-2C Inf)(-1 CC);

SELECT Inf IF (*-1 CC BARRIER NON-ADV-PCLE LINK *-1 Inf BARRIER VERB); 


# Present Participle
# ------------------

# PrsPrc = Imprt Prs Du2 (even-syll)


# Actio/PrfPrc
# ------------
# Actio = Prs Sg1 = PrfPrc (odd-syll verbs, contracted verbs)
# Perfect Participle is also treated at the Ind Prs Sg1 chapter.

SELECT PrfPrc IF (0 ("mannat"))(1 VAHKKU);
                     ## Mannan c1uohtejagi gaskkamuttos lei stuorra gilvu.

REMOVE (V Ind) IF (-1 NP-HEAD)(-1 Gen)(NOT -1 TIME)(1 N);
REMOVE PrfPrc IF (-1C NP-HEAD)(-1 Gen LINK NOT *-1 Num BARRIER NON-A)
	(NOT -1 TIME)(1 N);

# This rule is aimed at actio in coordination (I know it looks ugly): 
REMOVE PrfPrc IF (NOT -1 LEAT-FIN-NON-IMP LINK *-1 CP BARRIER VFIN)
		(1 CLB LINK *1 N BARRIER NPNH LINK *1 CC BARRIER VFIN 
		LINK *1 N BARRIER NPNH LINK NOT 0 Loc);

SELECT PrfPrc IF (*-1 LEAT-FIN-NON-IMP)(NOT *1 PrfPrc BARRIER CC OR CP OR CS);

#REMOVE PrfPrc IF (*1 PrfPrc BARRIER VFIN OR CC OR CP OR CS 
#		LINK NOT 0 Pass OR ("leapma" Dimin));

REMOVE PrfPrc IF (NOT *-1 VFIN)(NOT 0 Pass)(NOT *1 VFIN BARRIER CP OR CS);

SELECT Actio IF (1C Po);

#REMOVE V-SG1 IF (*-1 LEAT-FIN-NON-IMP BARRIER CS OR CP);

REMOVE V-SG1 IF (*-1 VFIN BARRIER CS OR CP);


# leamas1 without finite verb
# - - - - - - - - - - - - - -

SELECT PrfPrc IF (-1 NP-HEAD)(NOT -1 Gen)(0 ("leapma" Dimin)); 
SELECT PrfPrc IF (0 ("leapma" Dimin))(*1 A-CASE BARRIER NON-ADV-PCLE);
SELECT PrfPrc IF (NOT *-1 VFIN BARRIER CS)(0 ("leapma" Dimin))
		(NOT *1 VFIN BARRIER CP OR CS);



# ImprtII
# -------

# VGen
# ----

REMOVE VGen IF (*-1 CS OR CP OR BOS BARRIER VFIN)(0 VFIN)
               ( *1 CS OR CP OR EOS BARRIER VFIN);
                              ## Báldáhan ferte din1got, jos galgá fidnet.

  # This rule assumes that VGen comes parasitically upon a finite verb,
  # and removes VGen if this is not the case. More rules are needed in order
  # to remove more cases.

REMOVE VGen IF (0 Attr) (*1 N BARRIER NON-A);

REMOVE VGen IF (-1 ("nu") OR ("mihá"));

# Gerund,  Verb abessive and Supine do have unique forms and need no rules.


# Selecting some finite.
# ----------------------

SELECT VFIN IF (NOT *-1 VFIN BARRIER CS OR CP)(NOT 0 N)(NOT *1 VFIN BARRIER CS)
               (NOT -1 BOS LINK 2 EOS)(NOT -2 BOS LINK 1 N LINK 2 EOS);
                               ## Son lea dan sápmelas1 gean dovddan.
                               ## Gos don dál boad1át? Siiddas.
  # first approximation
  # BOS/EOS is there to allow for fragments like the "Siiddas" example.
  # and headlines like ## Giellabarggu organiseren.
  # But the rules goes wrong with lists consisting of infinitives.



# ===================== #
#    Residual cases
# ===================== #
       CONSTRAINTS      #
# ===================== #

# This is a section for local disambiguation that must come after the main POS


# Demonstrative pronouns
# ======================

#SELECT Acc IF (0 Dem)(*1C (N Acc) BARRIER NON-ADJ) ; # Could be Gen.
                                              ## Mun oainnán daid mánáid.

SELECT Gen IF (0 Dem)(*1C (N Gen) OR (N Sg Ill) OR (N Sg Loc) OR (N Pl Com)
                          BARRIER NON-ADJ) ;
                                              ## Mun manan daid mánáiguin.

# ===================== #
# Global disambiguation #
# ===================== #
       CONSTRAINTS      #
# ===================== #


# This is the section for rules of more global scope.
# It is also the section for rules that we would like to work in careful mode,
# i.e. after the other rules have removed as many false readings as possible.


# Adverbs
# -------

SELECT Adv IF (-1C N)(NOT 0 N)(1C Inf);
    ## Geain lea leamas1 vejolas1vuohta systemáhtalac1c1at guorahallat válggaid?
  # The idea is that we between a noun an an infinitive will have an adverb, and
  # not an adjective.
  # A weaker version (in case of trouble) will be to say:
  # REMOVE (A Sg) ...
  # REMOVE (A Pl) ...
  # i.e., this weaker version will keep readings that are neither adverbs not
  # adjectives.

REMOVE (A Nom) IF (-1 Nom)(1 VFIN);


# Adjectives
# ----------

REMOVE Attr IF (1C V OR EOS)(NOT 1 N);             ## Diet lea beare unnán.
  # When all rules are executed, remove the Attrs
  # The double requirement is there because of the compounds.


# Determiners
# -----------

REMOVE Sg IF (0 DEM-PL) (NOT *1 V-SG3);       ## Dat lea buorre.
REMOVE Pl IF (0 DEM-SG) (NOT *1 V-PL3);       ## Dat lea od1d1a niibbit.

REMOVE (Dem Gen) IF (*1 (N Nom) BARRIER NPNH);


# Finite verbs
# ------------

# At least one finite verb in the sentence (exception: leamas1).
# The rule scans to the left until it finds a finite verb.
#SELECT VFIN IF (NOT *-1 VFIN)(NOT *1 VFIN);

# Stop at guhto to the left and comma to the right.
#SELECT VFIN IF (NOT *-1 VFIN BARRIER COMP)(NOT *1 VFIN BARRIER CLB);
# cut these two because of titles. They would be ok if we could
# recognise titles.

# Accusative
# ----------

REMOVE Acc IF (0 Pron)(NOT 0 Dem)(1C Acc); # Mun oainnán du vielja.
  # Afterthought, get rid of Acc in specifier position.
  # Move to logical place, but after the primary Acc cycle.



#=====#
  END #
#=====#



# Notes for future thought:
# =========================

# Soai leaba goappas1agat    nu jallat
# they are   both-Pron-Indef so stupid

# Here, the subject NP is spread around the finite verb. Either, all the
# rules must be duplicated with this feature in mind, or the possible
# discontinuous parts must be included in the complement to the
# NON-ADV-PCLE set.
# While thinking, let us try including Pron Indef into a NON-ADV-PCLE-INDEF,
# at least for predicative constructions.
