! Divvun & Giellatekno - open source grammars for Sámi and other languages
! Copyright © 2000-2010 The University of Tromsø & the Norwegian Sámi Parliament
! http://giellatekno.uit.no & http://divvun.no
!
! This program is free software; you can redistribute and/or modify
! this file under the terms of the GNU General Public License as published by
! the Free Software Foundation, either version 3 of the License, or
! (at your option) any later version. The GNU General Public License
! is found at http://www.gnu.org/licenses/gpl.html. It is
! also available in the file $GTHOME/LICENSE.txt.
!
! Other licensing options are available upon request, please contact
! giellatekno@hum.uit.no or divvun@samediggi.no

! ############################## !
!This file contains the Pronouns !
! ############################## !



! Pronoun
! =======

!Pointing to all the subgrops, presented in this order below.
LEXICON Pronoun
Personal ;
Interrogative ;
Relative ;
Demonstrative ;
Reflexive ;
Reciprocal ;
Indefinite ;


! Personal pronouns
! -----------------

!Personal pronouns, splitting in 1st, 2nd, 3rd
LEXICON Personal
mun+Pron+Pers+Sg1:m firstperspronsg ;
mun+Pron+Pers+Du1:m firstpersprondu ;
mun+Pron+Pers+Pl1:m perspronpl ;
don+Pron+Pers+Sg2:d nonfirstperspronsg ;
don+Pron+Pers+Du2:d nonfirstpersprondu ;
don+Pron+Pers+Pl2:d perspronpl ;
son+Pron+Pers+Sg3:s nonfirstperspronsg ;
son+Pron+Pers+Du3:s nonfirstpersprondu ;
son+Pron+Pers+Pl3:s perspronpl ;
dat+Pron+Pers:da%> datpers ;                ! Dat lea ruovttus.

!Case suffixes for 1st Sg, pointing to K lexicon
LEXICON firstperspronsg
+Nom:u%>n K ;
+Nom+Use/NG:o%>n K ; ! 
+Acc:u%> K ;
+Gen:u%> K ;
+Gen:u»# NAMAT ;
+Ill:u%>nnje K ;
+Loc:u%>s K ;
+Com:u%>jnna K ;
+Ess:u%>nin K ;

!Case suffixes for noŋst Sg, pointing to K lexicon
LEXICON nonfirstperspronsg
+Nom:o%>n K ;
+Acc:u%> K ;
+Gen:u%> K ;
+Gen:u»# NAMAT ;
+Ill:u%>tnje K ;
+Loc:u%>s K ;
+Com:u%>jnna K ;
+Ess:u%>nin K ;

!Case suffixes for 1st Du, pointing to K lexicon
LEXICON firstpersprondu
+Nom:oaj%> K ;
+Acc:unno%> K ;
+Gen:unno%> K ;
+Gen:unno»# NAMAT ;
+Ill:unnu%>jde K ;
+Loc:unno%>s K ;
+Com:unnu%>jn K ;
+Ess:unno%>n K ;

!Case suffixes for noŋst Du, pointing to K lexicon
LEXICON nonfirstpersprondu
+Nom:oaj%> K ;
+Acc:udno%> K ;
+Gen:udno%> K ;
+Acc+Use/Sub:utno%> K ; !
+Gen+Use/Sub:utno%> K ; !
+Gen:udno»# NAMAT ;
+Ill:udnu%>jde K ;
+Ill+Use/Sub:utnu%>jde K ; !
+Loc:udno%>s K ;
+Loc+Use/Sub:utno%>s K ; !
+Com:udnu%>jn K ;
+Com+Use/Sub:utnu%>jn K ; !
+Ess:udno%>n K ;
+Ess+Use/Sub:utno%>n K ; !

!Case suffixes for all Pl forms, pointing to K lexicon
LEXICON perspronpl
+Nom:i%>j K ;
+Acc:i%>n K ;
+Gen:i%>n K ;
+Gen:i%>n»# NAMAT ;
+Ill+Use/Sub:i%>nsij K ; !
+Ill:i%>djijde K ;
+Ill+Use/Sub:i%>nsiidda K ; 
+Loc:i%>s K ;
+Loc+Use/Sub:i%>js K ; ! !SOUTH
+Com:i%>ngujn K ;
+Ess:i%>nin K ;

LEXICON datpers
+Sg3:0 demcassg ;
+Pl3:0 demcaspl ;
+Ess:nin K ;


! Interrogative pronouns
! ----------------------

!Giving ideosyncratic Sg Nom of gii, mii lexically
!Sending the oblique forms of gii, mii to an oblique sublexicon
!Giving the stem of guhte, guhtemuš, goabbá
LEXICON Interrogative
mii+Pron+Interr+Sg+Nom:mij%> K-son ;
mii+Pron+Interr+Sg+Acc+Use/Sub:májd9 K-son ; !
gii+Pron+Interr+Sg+Nom:gij%> K-son ;
gii+Pron+Interr+Sg+Ill+Foc:gea%>sagii # ;
mii+Pron+Interr:ma%> oblintercas ;
gii+Pron+Interr:gea%>X5 oblintercas ;
guh0te+Pron+Interr:guh'te GUHTECASE ;
guhte+Pron+Interr+Sg+Nom:guhte%>s K ;
guhtemuš+Pron+Interr:guđemužž GUHTEMUŠCASE ;
guhtemuš+Pron+Interr:guhtemuš%> K-son ; !deviating Nom alternant.
goabbá+Pron+Interr:goabbá GUHTECASE ;
makkár+Pron+Interr:makkár DAKKARInterr ;
man% láhkái+Pron+Interr+Sg+Com+Use/Sub:mainna#lágiin K-son ; !


! Relative pronouns
! -----------------

LEXICON Relative
mii+Pron+Rel+Sg+Nom:mij%> K-son ;
mii+Pron+Rel+Sg+Acc+Use/Sub:májd9 K-son ; !
gii+Pron+Rel+Sg+Nom:gij%> K-son ;
mii+Pron+Rel:ma%> oblintercas ;
gii+Pron+Rel:gea%>X5 oblintercas ;
guh0te+Pron+Rel:guh'te GUHTECASE ;
guhte+Pron+Rel+Sg+Nom:guhte%>s K ;
guhtemuš+Pron+Rel:guđemužž GUHTEMUŠCASE ;
guhtemuš+Pron+Rel:guhtemuš%> K-son ; !deviating Nom alternant.
goabbá+Pron+Rel:goabbá GUHTECASE ;
man% láhkái+Pron+Rel+Sg+Com:mainna#lági%>in K-son ;

LEXICON oblintercas
+Sg+Acc:jd9 K-son ;
+Pl+Nom:t K ;
+Ess:nin K ;
+Sg:0 obldemcassg ;
+Pl:0 obldemcaspl ;
!Then going directly to the case paradigm of the demonstratives

! guhte
LEXICON GUHTECASE
GUHTE-NE ;
GU-GU-OBL ;

! Nom and Ess of guhte
LEXICON GUHTE-NE
+Sg+Nom:%> K-son ;
+Ess:%>n K-son ;

! guhtemuš
LEXICON GUHTEMUŠCASE
GUHTEMUŠ-NE ;
:a GU-GU-OBL ;

! Nom and Ess of guhtemuš
LEXICON GUHTEMUŠ-NE
+Sg+Nom:%> K-son ;
+Ess:a%>n K-son ;

! Oblique paradigm for guhte and guhtemuš
LEXICON GU-GU-OBL
+Sg+Gen:%>X8 K-son ;
+Sg+Gen:%>X8»# NAMAT ;
+Sg+Acc:%>X8 K-son ;
+Sg+Ill:%>X3j K-son ;
!+Sg+Gen:%> K-son ; ! this should go out
+Sg+Loc:%>X4s K-son ;
+Sg+Loc+Use/Sub:%>X4n K-son ; ! !SOUTH
+Sg+Com:%>X5jn K-son ;
+Pl+Nom:%>X4t K-son ;
+Pl+Gen:%>X5jd9 K-son ;
+Pl+Gen:%>X5jd9»# NAMAT ;
+Pl+Acc:%>X5jd9 K-son ;
+Pl+Ill:%>X5jde K-son ;
+Pl+Com:%>X5jgujn K-son ;


! Demonstrative pronouns
! ----------------------

!Giving baseform + all demonstrative stems
!Pointing to case paradigms

LEXICON Demonstrative
dat+Pron+Dem:da%> datdemcas ;
die-dat+Pron+Dem:die-da%> demcas ;
diet+Pron+Dem:die%> demcas ;
die-diet+Pron+Dem:die-die%> demcas ;
dát+Pron+Dem:dá%> demcas ;
dá-dát+Pron+Dem:dá-dá%> demcas ;
duot+Pron+Dem:duo%> demcas ;
duo-duot+Pron+Dem:duo-duo%> demcas ;
dot+Pron+Dem:do%> demcas ;
do-dot+Pron+Dem:do-do%> demcas ;
doot+Pron+Dem+Use/Sub:doo demcas ; !

dahtanaga+Pron+Dem:da%> Dahtanaga-par ;
dakkár+Pron+Dem:dakkár DAKKAR ;
dákkar+Pron+Dem+Use/Sub:dákkar DAKKAR ; ! XXX
dakkar+Pron+Dem+Use/Sub:dakkar DAKKAR ; ! XXX
dákkár+Pron+Dem:dákkár DAKKAR ;
diekkár+Pron+Dem:diekkár DAKKAR ;
duokkár+Pron+Dem:duokkár DAKKAR ;
dokkár+Pron+Dem:dokkár DAKKAR ;
!makkár+Pron+Dem:makkár DAKKAR ; ! Give this one Interr

dakkáraš+Pron+Dem:dakkáražž DAKKARAS ;
dákkáraš+Pron+Dem:dákkáražž DAKKARAS ;
diekkáraš+Pron+Dem:diekkáražž DAKKARAS ;
duokkáraš+Pron+Dem:duokkáražž DAKKARAS ;
dokkáraš+Pron+Dem:dokkáražž DAKKARAS ;
makkáraš+Pron+Dem:makkáražž DAKKARAS ;


!Splitting into Sg, Pl and Ess, different paradigms
LEXICON demcas
+Sg:0 demcassg ;
+Pl:0 demcaspl ;
+Ess:%>nin K ;

!Splitting into Sg, Pl and Ess, different paradigms
LEXICON datdemcas
+Sg:0 datdemcassg ;
+Pl:0 demcaspl ;
+Ess:%>nin K ;

LEXICON datdemcassg
nomdemcassg ;
obldatdemcassg ;


!Splitting into oblique and nonoblique
!This is because of the interrogatives
LEXICON demcassg
nomdemcassg ;
obldemcassg ;
LEXICON demcaspl
nomdemcaspl ;
obldemcaspl ;

LEXICON nomdemcassg
!The nominative, then pointing to clitics
!This could be conflated, but look out for interr
+Nom:t K ;
LEXICON nomdemcaspl
+Nom:t K ;


LEXICON obldemcassg
!The other cases, then pointing to clitics
+Gen:n K ;
+Gen:n»# NAMAT ;
+Acc:n K ;
+Ill:sa K ;
+Loc:s K ;
+Com:jnna K ; !dáinna Sg Com

LEXICON obldatdemcassg
!The other cases, then pointing to clitics
+Gen:n K ;
+Gen:n»# NAMAT ;
+Acc:n K ;
+Com:jnna K ; !dainna Sg Com


LEXICON obldemcaspl
+Gen:jd9 K ;
+Gen:jd9»# NAMAT ;
+Acc:jd9 K ;
+Ill:jdda K ;
+Loc:jn K ;   !dain   Pl Loc
!+Loc:jnna K ; !dainna Pl Loc is deemed out in contemporary language.
+Com:jguin K ;

!This case paradigm is exceptional and partial.
LEXICON Dahtanaga-par
+Sg+Nom:%>htanaga # ;
+Sg+Gen:%>mmanaga # ;
+Sg+Gen:%>mmanaga»# NAMAT ;
+Sg+Acc:%>mmanaga # ;
+Sg+Ill:%>sanaga # ;
+Sg+Loc:%>sttánaga # ;
+Sg+Com:%>jnnanaga # ;
+Pl+Ill:%>jddanaga # ;
+Pl+Loc:%>jnnanaga # ;

LEXICON DAKKAR
+Sg+Nom:%> K ;
+Attr:%> K ;
+Attr:»# NAMAT ;
+Ess:%>in K ;
+Sg+Gen:%>a K ;
+Sg+Acc:%>a K ;
+Pl+Nom:%>at K ;
00:%>i DAKKARI- ;

LEXICON DAKKARI-
+Sg+Ill:j K ;
+Sg+Loc:s K ;
+Sg+Loc+Use/Sub:n K ; ! !SOUTH
+Sg+Com:jn K ;
+Pl+Gen:jd9 K ;
+Pl+Gen:jd9»# NAMAT ;
+Pl+Acc:jd9 K ;
+Pl+Ill:jdda K ;
+Pl+Loc:jn K ;
+Pl+Com:jgujn K ;

LEXICON DAKKARInterr
+Sg+Nom:%> K-son ;
+Attr:%> K-son ;
+Attr:»# NAMAT ;
+Ess:%>in K-son ;
+Sg+Gen:%>a K-son ;
+Sg+Acc:%>a K-son ;
+Pl+Nom:%>at K-son ;
00:%>i DAKKARI-Interr ;

LEXICON DAKKARI-Interr
+Sg+Ill:j K-son ;
+Sg+Loc:s K-son ;
+Sg+Loc+Use/Sub:n K-son ; ! !SOUTH
+Sg+Com:jn K-son ;
+Pl+Gen:jd9 K-son ;
+Pl+Gen:jd9»# NAMAT ;
+Pl+Acc:jd9 K-son ;
+Pl+Ill:jdda K-son ;
+Pl+Loc:jn K-son ;
+Pl+Com:jgujn K-son ;


LEXICON DAKKARAS
+Sg+Nom:%> K ;
+Sg+Nom:%>X7 R ;
+Ess:%>an K ;
:a%> DAKKARASSA- ;

LEXICON DAKKARASSA-
 +Sg+Gen:X4 K ;
 +Sg+Gen:X8 K ;
 +Sg+Gen:X4»# NAMAT ;
 +Sg+Gen:X8»# NAMAT ;
 +Sg+Acc:X4 K ;
 +Sg+Acc:X8 K ;
 +Sg+Ill:X3j K ;
 +Sg+Loc:X4s K ;
 +Sg+Loc:X8s K ;
+Sg+Loc+Use/Sub:X4n K ; ! !SOUTH
+Sg+Loc+Use/Sub:X8n K ; ! !SOUTH
 +Sg+Com:X5jn K ;
 +Pl+Nom:X4t K ;
 +Pl+Gen:X5jd9 K ;
 +Pl+Gen:X5jd9»# NAMAT ;
 +Pl+Acc:X5jd9 K ;
 +Pl+Ill:X5jde K ;
 +Pl+Loc:X5jn K ;
 +Pl+Com:X5jgujn K ;



! Reflexive pronouns
! ------------------

LEXICON Reflexive
!Two nominative reflexives, and pointer to the rest
!The Pl one is used for Du as well, here given two entries.
!Should one of them be removed?
ieš+Pron+Refl+Sg+Nom:ieš%> K ;
ieš+Pron+Refl+Sg+Nom:ieš»# NAMAT ;  ! <======= ok
ieš+Pron+Refl+Du+Nom:ieža%> K ;
ieš+Pron+Refl+Du+Nom+Use/Sub:ieža»# NAMAT ; !
ieš+Pron+Refl+Pl+Nom:ieža%> K ;
ieš+Pron+Refl+Pl+Nom+Use/Sub:ieža»# NAMAT ; !
ieš+Pron+Refl+Gen+PxSg3+Use/Sub:ald9 # ;   ! !SOUTH
ieš+Pron+Refl+Ess:iehča%>nassij K ;
ieš+Pron+Refl:0 reflobl ;
jieš+Pron+Refl+Sg+Nom:jieš%> K ;
jieš+Pron+Refl+Sg+Nom+Use/NG:jieš»# NAMAT ;  ! <======= ok
jieš+Pron+Refl+Du+Nom:jieža%> K ;
jieš+Pron+Refl+Du+Nom+Use/Sub:jieža»# NAMAT ; !
jieš+Pron+Refl+Pl+Nom:jieža%> K ;
jieš+Pron+Refl+Pl+Nom+Use/Sub:jieža»# NAMAT ; !
jieš+Pron+Refl+Ess:jiehča%>nassij K ;
jieš+Pron+Refl:j refloblalt ;

LEXICON reflobl
!The V/V2 alternation due to 3rd person vocalism
!Pointing directly to 1st and 3rd person sublexica for AG & Loc.
!Pointing to exceptional Pl sublexicon for AG and Loc.
+Acc:ieža%> PxV2 ;
+Gen:ieža%> PxV2 ;
+Gen+PxSg1+Use/NG:ieža%>n»# NAMAT ;
+Gen+PxSg1+Use/Sub:ieža%>m9»# NAMAT ;  ! !SOUTH
+Gen+PxSg2+Use/NG:ieža%>t»# NAMAT ;
+Gen+PxSg3+Use/NG:ieža%>s»# NAMAT ;     ! <========= ??
+Gen+PxDu1+Use/NG:ieža%>me»# NAMAT ;
+Gen+PxDu2+Use/NG:ieža%>de»# NAMAT ;
+Gen+PxDu3+Use/NG:ieža%>ska»# NAMAT ;
+Gen+PxPl1+Use/NG:ieža%>met»# NAMAT ;
+Gen+PxPl2+Use/NG:ieža%>det»# NAMAT ;
+Gen+PxPl3+Use/NG:ieža%>set»# NAMAT ;
+Acc+Use/NG:iehča%> Px1V ;
+Gen+Use/NG:iehča%> Px1V ;
+Gen+PxSg1+Use/NG:iehča%>n»# NAMAT ;
+Gen+PxDu1+Use/NG:iehča%>me»# NAMAT ;
+Gen+PxPl1+Use/NG:iehča%>met»# NAMAT ;
+Acc+Use/NG:ieža%>jd PxCPlstem ;
+Gen+Use/NG:ieža%>jd PxCPlstem ;
+Gen+PxDu1+Use/Sub:ieža%>jdeame»# NAMAT ;     !
+Gen+PxDu2+Use/Sub:ieža%>jdeatte»# NAMAT ;     !
+Gen+PxDu3+Use/Sub:ieža%>jdeaskka»# NAMAT ; !
+Gen+PxPl1+Use/Sub:ieža%>jdeamet»# NAMAT ;     !
+Gen+PxPl2+Use/Sub:ieža%>jdeattet»# NAMAT ; !
+Gen+PxPl3+Use/Sub:ieža%>jdeaset»# NAMAT ;     !
+Ill:alcce%>s PxC ; ! All generated Ill forms here
+Ill+Use/NG:alcca%> Px1V ;
+Ill+Use/NG:alcca%> Px2V ;
+Ill+Use/NG:alcci%> Px3V ;
+Ill+Use/NG:alcce%> PxV2 ;
+Ill+Use/NG:alla%>s PxC ;
+Ill+PxSg1+Use/Sub:allasim9# K ; ! !SOUTH
+Loc+Use/NG:alddi%> PxV ;
+Loc+Use/NG:aldde%> Px3V ;
+Loc:aldde%>st PxC ;
+Loc+Use/Sub:alddi%>n PxCPlstem ; !
+Loc+Use/NG:alddi%>jn PxCPlstem ;
+Loc+Use/NG:alddá%>n PxCPlstem ;
+Com+Use/NG:ieža%>jn PxC ;
+ComPxCPlCom+Use/NG:ieža%>jd PxCPlCom ;
+Ess:iehča%>n PxC ;

LEXICON refloblalt
!The V/V2 alternation due to 3rd person vocalism
!Pointing directly to 1st and 3rd person sublexica for AG & Loc.
!Pointing to exceptional Pl sublexicon for AG and Loc.
+Acc:ieža%> PxV2 ;
+Gen:ieža%> PxV2 ;
+Gen+PxSg1+Use/NG:ieža%>n»# NAMAT ;
+Gen+PxSg1+Use/Sub:ieža%>m9»# NAMAT ;  ! !SOUTH
+Gen+PxSg2+Use/NG:ieža%>t»# NAMAT ;
+Gen+PxSg3+Use/NG:ieža%>s»# NAMAT ;
+Gen+PxDu1+Use/NG:ieža%>me»# NAMAT ;
+Gen+PxDu2+Use/NG:ieža%>de»# NAMAT ;
+Gen+PxDu3+Use/NG:ieža%>ska»# NAMAT ;
+Gen+PxPl1+Use/NG:ieža%>met»# NAMAT ;
+Gen+PxPl2+Use/NG:ieža%>det»# NAMAT ;
+Gen+PxPl3+Use/NG:ieža%>set»# NAMAT ;
+Acc+Use/NG:iehča%> Px1V ;
+Gen+Use/NG:iehča%> Px1V ;
+Gen+PxSg1+Use/NG:iehča%>n»# NAMAT ;
+Gen+PxDu1+Use/NG:iehča%>me»# NAMAT ;
+Gen+PxPl1+Use/NG:iehča%>met»# NAMAT ;
+Acc+Use/NG:ieža%>jd PxCPlstem ;
+Gen+Use/NG:ieža%>jd PxCPlstem ;
+Gen+PxDu1+Use/Sub:ieža%>jdeame»# NAMAT ;
+Gen+PxDu2+Use/Sub:ieža%>jdeatte»# NAMAT ;
+Gen+PxDu3+Use/Sub:ieža%>jdeaskka»# NAMAT ;
+Gen+PxPl1+Use/Sub:ieža%>jdeamet»# NAMAT ;
+Gen+PxPl2+Use/Sub:ieža%>jdeattet»# NAMAT ;
+Gen+PxPl3+Use/Sub:ieža%>jdeaset»# NAMAT ;
+Com+Use/NG:ieža%>jn PxC ;
+Com+Use/NG:ieža%>jd PxCPlCom ;
+Ess+Use/NG:iehča%>n PxC ;


LEXICON PxCPlstem
!This lexicon is here since it only applies to Reflexives.
!It handles the exeptional AG and Loc DuPl plural stem forms of ieš.
+PxDu1:eame K ;
+PxDu2:eatte K ;
+PxDu3:easkka K ;
+PxPl1:eamet K ;
+PxPl2:eattet K ;
+PxPl3:easet K ;


LEXICON PxCPlCom
! This lexicon for archaic biblical forms like iežaideasetguin.
+PxDu1+Use/NG:eameguin K ;
+PxDu2+Use/NG:eatteguin K ;
+PxDu3+Use/NG:easkkaguin K ;
+PxPl1+Use/NG:eametguin K ;
+PxPl2+Use/NG:eattetguin K ;
+PxPl3+Use/NG:easetguin K ;


! Reciprocal pronouns
! -------------------

! The first 6 entries handle the first element of the recipr.
! The next 12 handle the 2nd part of the non-Px recipr.
! The members of the third section  point to Px lexica.

LEXICON Reciprocal
!nubbi+Pron+Recipr+Du:nubbi # ;     
!nubbi+Pron+Recipr+Pl:nuppit # ;    
goabbat+Pron+Recipr+Du:goabbat # ; 
goabbat+Pron+Recipr+Pl:guhtet # ;  
goabbat+Pron+Recipr+Du+Use/Sub:goappat # ; ! ! Dialectal forms. Norm?
goabbat+Pron+Recipr+Pl+Use/Sub:guđet # ;   ! ! Dialectal forms. Norm?


guoibmi+Pron+Recipr+Sg+Acc:guoibmá Px1VRecipr ;   
guoibmi+Pron+Recipr+Sg+Gen:guoibmá Px1VRecipr ;   
guoibmi+Pron+Recipr+Sg+Acc:guoimmi Px23VRecipr ;  
guoibmi+Pron+Recipr+Sg+Gen:guoimmi Px23VRecipr ;  
guoibmi+Pron+Recipr+Sg+Acc:guoimmi Px3VPlStem ;  
guoibmi+Pron+Recipr+Sg+Gen:guoimmi Px3VPlStem ;  
guoibmi+Pron+Recipr+Sg+Ill:guoibmás PxCPlstem ;   
guoibmi+Pron+Recipr+Sg+Loc:guoimmist PxCPlstem ;  
guoibmi+Pron+Recipr+Sg+Com:guimmiin PxCPlstem ;  

guoibmi+Pron+Recipr+Pl+Acc:guimmiid PxCPlstem ;   
guoibmi+Pron+Recipr+Pl+Gen:guimmiid PxCPlstem ;   
guoibmi+Pron+Recipr+Pl+Ill:guimmiidasa PxVPlStemRecipr ; 
guoibmi+Pron+Recipr+Pl+Loc:guimmiin PxCPlstem ;    
guoibmi+Pron+Recipr+Pl+Com:guimmiid PxCPlComRecipr ; 

nubbi+Pron+Recipr+Pl+Gen:nuppi%>id nubbiPxDu ;
nubbi+Pron+Recipr+Pl+Acc:nuppi%>id nubbiPxDu ;
nubbi+Pron+Recipr+Pl+Ill:nuppi%>idasa Px1VRecipr ;
nubbi+Pron+Recipr+Pl+Loc:nuppi%>in nubbiPxDu ;
nubbi+Pron+Recipr+Pl+Com:nuppi%>id nubbiPxDuCom ;

nubbi+Pron+Recipr+Pl+Gen:nuppi%>id nubbiPxPl ;
nubbi+Pron+Recipr+Pl+Acc:nuppi%>id nubbiPxPl ;
nubbi+Pron+Recipr+Pl+Ill:nuppi%>idasa Px23VRecipr ;
nubbi+Pron+Recipr+Pl+Ill:nuppi%>idasa Px3VPlStem ;
nubbi+Pron+Recipr+Pl+Loc:nuppi%>in nubbiPxPl ;
nubbi+Pron+Recipr+Pl+Com:nuppi%>id nubbiPxPlCom ;

LEXICON Px1VRecipr      
+PxDu1:me K ;          
+PxPl1:met K ;         

LEXICON Px23VRecipr 
!LEXICON Px2VPlStem     !SUB
+PxDu2:X2de K ;
+PxPl2:X2det K ;

LEXICON Px3VPlStem     
+PxDu3:ska K ;         
+PxPl3:set K ;         

LEXICON PxVPlStemRecipr 
+PxPl1:met K ;         
+PxPl2:det K ;         
+PxPl3:set K ;         

LEXICON PxCPlComRecipr  
+PxPl1:eametguin K ;   
+PxPl2:eadetguin K ;   
+PxPl3:easetguin K ;   

LEXICON nubbiPxDu
+PxDu1:eame K ;
+PxDu2:eatte K ;
+PxDu3:easkka K ;

LEXICON nubbiPxPl
+PxPl1:eamet K ;
+PxPl2:eattet K ;
+PxPl3:easet K ;

LEXICON nubbiPxDuCom
+PxDu1:eameguin K ;
+PxDu2:eatteguin K ;
+PxDu3:easkkaguin K ;

LEXICON nubbiPxPlCom
+PxPl1:eametguin K ;
+PxPl2:eattetguin K ;
+PxPl3:easetguin K ;


! Indefinite pronouns
! -------------------

! Dividing the indefinites in three groups
LEXICON Indefinite
declindef-cl ;
declindef ;
declindef-idiosync ;
indeclindef ;
+Use/Sub: indefmulti ; ! feara mii

! Declineable indefinite pronouns with case + clitic
! - - - - - - - - - - - - - - - - - - - - - - - - -

LEXICON declindef-cl

! Exceptional nominatives
mihkkege+Pron+Indef+Sg+Nom:mihkke%>ge # ;
mihkkege+Pron+Indef+Pl+Nom:mahkke%>ge # ;
mihkkege+Pron+Indef+Pl+Gen:maide%>ge # ;
mihkke+Pron+Indef+Sg+Nom:mihkke%> # ;
mihkket+Pron+Indef+Sg+Nom+Use/Sub:mihkke%>t # ; !
mihkkige+Pron+Indef+Sg+Nom:mihkki%>ge # ;
mihkkige+Pron+Indef+Pl+Nom:mahkki%>ge # ;
mihkkige+Pron+Indef+Pl+Gen:maidi%>ge # ;
giige+Pron+Indef+Sg+Nom:gii%>ge # ;
giinai+Pron+Indef+Sg+Nom:gii%>nai # ;
giijai+Pron+Indef+Sg+Nom:gii%>jai # ;
giiat+Pron+Indef+Sg+Nom:gii%>at # ;
dahtai+Pron+Indef+Sg+Nom:daht%>ai # ;
miinai+Pron+Indef+Sg+Nom:mii%>nai # ;
miijai+Pron+Indef+Sg+Nom:mii%>jai # ;
ieš#giige+Pron+Indef+Sg+Nom:ieš#gii%>ge # ;
ieš#giinai+Pron+Indef+Sg+Nom:ieš#gii%>nai # ;
ieš#giijai+Pron+Indef+Sg+Nom:ieš#gii%>jai # ;

! Pointers to obl sublexicon
mihkkege+Pron+Indef:ma%> oblindefcas ;
giige+Pron+Indef:gea%> oblindefcas ;
giinai+Pron+Indef:gea%> oblindefcas-giinai ;
miinai+Pron+Indef:ma%> oblindefcas-miinai ;
ieš#giige+Pron+Indef:ieš#gea%> oblindefcas ;
ieš#giinai+Pron+Indef:ieš#gea%> oblindefcas-giinai ;
guhtege+Pron+Indef:guh'te GUHTEGECASE ;
guhtenai+Pron+Indef:guh'te GUHTENAI ;
!ieš#guh0te+Pron+Indef:ieš#guh'te GUHTECASE ;
ieš#guhtege+Pron+Indef:ieš#guh'te GUHTEGECASE ;
ieš#guhtenai+Pron+Indef:ieš#guh'te GUHTENAI ;
guhtemušge+Pron+Indef:guđemužž GUHTEMUSGECASE ;
guhtemušge+Pron+Indef:guhtemuš%>ge # ; !deviating Nom altarnant.
goabbáge+Pron+Indef:goabbá GUHTEGECASE ;
goabbánai+Pron+Indef:goabbá GUHTENAI ;
!makkárge+Pron+Indef (have a look at this one)
!oktage+Pron+Indef (wait with this one until numerals are treated)

!The difference between mihkkege and mihkkige
!continues through declination (maidege-maidige etc.).
!Should they be separated in different lexicons to make it logical? IK

LEXICON oblindefcas
+Sg:0 oblindemcassg ;
+Pl:0 oblindemcaspl ;


LEXICON oblindemcassg
!The other cases, then pointing to clitics
+Gen:nge # ;
+Gen:nge»# NAMAT ;
+Acc:nge # ;
+Ill:sage # ;
+Loc:sge # ;
+Loc:stege # ;
+Com:jnnage # ;

LEXICON oblindemcaspl
+Gen:jde # ;
+Gen:jde»# NAMAT ;
+Acc:jde # ;
+Gen:jdege # ;
+Gen:jdege»# NAMAT ;
+Acc:jdege # ;
+Gen:jdige # ;
+Gen:jdige»# NAMAT ;
+Acc:jdige # ;
+Ill:jddage # ;
+Loc:jnge # ;
!Here we do not go to the case paradigm of the demonstratives,
!since the clitic path is different. ComPl lacks (intentionally) from paradigm.

LEXICON oblindefcas-giinai
+Gen:nnai # ;
+Gen:nnai»# NAMAT ;
+Acc:nnai # ;
+Gen:nai # ;
+Gen:nai»# NAMAT ;
+Acc:nai # ;
+Ill:sanai # ;
+Ill:sai # ;
+Loc:snai # ;
+Loc:sai # ;
+Loc:stenai # ;
+Loc:stai # ;
+Com:jnnanai # ;
+Com:jnnai # ;
+Gen:jdnai # ;
+Gen:jdnai»# NAMAT ;
+Acc:jdnai # ;
+Gen:jdai # ;
+Gen:jdai»# NAMAT ;
+Acc:jdai # ;
+Ill:jddanai # ;
+Ill:jddai # ;
+Loc:jnnai # ;
+Loc:jnai # ;

LEXICON oblindefcas-miinai
+Gen:nnai # ;
+Gen:nnai»# NAMAT ;
+Acc:nnai # ;
! +Gen:nai # ; !commented out disamb reasons
+Gen:nai»# NAMAT ;
! +Acc:nai # ; !commented out disamb reasons
+Ill:sanai # ;
+Ill:sai # ;
+Loc:snai # ;
+Loc:sai # ;
+Loc:stenai # ;
+Loc:stai # ;
+Com:jnnanai # ;
+Com:jnnai # ;
+Gen:jdnai # ;
+Gen:jdnai»# NAMAT ;
+Acc:jdnai # ; !commented out disamb reasons
! +Gen:jdai # ; !commented out disamb reasons
+Gen:jdai»# NAMAT ;
! +Acc:jdai # ; !commented out disamb reasons
+Ill:jddanai # ;
! +Ill:jddai # ; !commented out disamb reasons
+Loc:jnnai # ;
+Loc:jnai # ;




! guhtege
LEXICON GUHTEGECASE
GUHTEGE-NE ;
GUGE-GUGE-OBL ;


! Nom and Ess of guhtege
LEXICON GUHTEGE-NE
+Sg+Nom:%>ge # ;
+Ess:%>nge # ;

! guhtemušge
LEXICON GUHTEMUSGECASE
GUHTEMUSGE-NE ;
:a%> GUGE-GUGE-OBL ;

! Nom and Ess of guhtemušge
LEXICON GUHTEMUSGE-NE
+Sg+Nom:%>ge # ;
+Ess:a%>nge # ;

! Oblique paradigm for guhtege and guhtemušGE
LEXICON GUGE-GUGE-OBL
+Sg+Gen:X8ge # ;
+Sg+Gen:X8ge»# NAMAT ;
+Sg+Acc:X8ge # ;
+Sg+Ill:X3jge # ;
!+Sg+Gen:ge # ; ! this should go out
+Sg+Loc:X4sge # ;
+Sg+Loc+Use/Sub:X4nge K ; ! !SOUTH
+Sg+Com:X5jnge # ;
+Pl+Nom:X4tge # ;
+Pl+Gen:X5jdge # ;
+Pl+Gen:X5jdge»# NAMAT ;
+Pl+Acc:X5jdge # ;
+Pl+Ill:X5jdege # ;
+Pl+Loc+Use/Sub:X5jnge # ; ! because of speller
!+Pl+Com:X5jgujnge # ; !ComPl is out.

LEXICON GUHTENAI
+Sg+Nom:%>nai # ;
+Sg+Gen:%>X8nai # ;
+Sg+Gen:%>X8nai»# NAMAT ;
+Sg+Acc:%>X8nai # ;
+Sg+Ill:%>X3jnai # ;
+Sg+Loc:%>X4snai # ;
+Sg+Com:%>X5jnnai # ;
+Ess:%>nnai # ;
+Pl+Nom:X4tnai # ;
+Pl+Gen:%>X5jdnai # ;
+Pl+Gen:%>X5jdnai»# NAMAT ;
+Pl+Acc:%>X5jdnai # ;
+Pl+Ill:%>X5jdenai # ;
+Pl+Loc+Use/Sub:%>X5jnnai # ; ! because of speller



! Declineable indefinites with normal case paradigms
! - - - - - - - - - - - - - - - - - - - - - - - - - -

LEXICON declindef

eará+Pron+Indef:eará%>               indef-a-even-v ;
iežá+Pron+Indef:iežá%>                   indef-a-even-v ;
uhccánaš+Pron+Indef:uhccánažž       indef-a-even-c ;
unnánaš+Pron+Indef:unnánažž           indef-a-even-c ;

ea0nas+Pron+Indef:eatnas%>           indef-a-uneven ;
ea0naš+Pron+Indef:eatnaš%>               indef-a-uneven ;

muhtin+Pron+Indef:muhtim%>           indef-a-uneven-gahpir ;
muhtun+Pron+Indef:muhtum%>           indef-a-uneven-gahpir ;
muhtun+Pron+Indef+Use/Sub:muhtom%>           indef-a-uneven-gahpir ; !

muhtin+Pron+Indef+Sg+Gen:muhtin»#       NAMAT ;
muhtun+Pron+Indef+Sg+Gen:muhtun»#       NAMAT ;
muhtin+Pron+Indef+Pl+Gen:muhtim%>ijd9»#       NAMAT ;
muhtun+Pron+Indef+Pl+Gen:muhtum%>ijd9»#       NAMAT ;
muhtin+Pron+Indef+Pl+Loc+Use/Sub:muhttuin K ; !


uhccán+Pron+Indef:uhccán%>       indef-a-uneven-gahpir ;

unnán+Pron+Indef:unnán%>           indef-a-uneven-gahpir ;
veháš+Pron+Indef:veháž%>           indef-a-uneven-gahpir ;
váháš+Pron+Indef:váháž%>           indef-a-uneven-gahpir ;
veaháš+Pron+Indef:veaháž%>           indef-a-uneven-gahpir ;

soa0mes+Pron+Indef+Sg+Nom:soames%>   K ;
soa0mes+Pron+Indef+Ess:soapmás%>in   K ;
soa0mes+Pron+Indef:soapmás%>          indefattr ;
soa0mes+Pron+Indef:soapmás%>X2      indefdeclsguneven-ag ;
soa0mes+Pron+Indef:soapmás%>          indefdeclsguneven-ilc ;
soa0mes+Pron+Indef:soapmás%>X2      indefdeclpluneven-n ;
soa0mes+Pron+Indef:soapmás%>i          indefdeclpluneven-obl ;

nuppát+Pron+Indef:nuppád%>              indefsg-a-uneven ;

duobbá+Pron+Indef:duobbá%>              indef-a-even-v ;
dábbá+Pron+Indef:dábbá%>              indef-a-even-v ;
juobbá+Pron+Indef:juobbá%>              indef-a-even-v ;
juobbá#goabbá+Pron+Indef: JUOBBAGOABBA ;

nubbi+Pron+Indef:nubbi%>             nubbicase ;
nubbi+Pron+Recipr:nubbi%>             nubbicase ;

juohkehaš+Pron+Indef:juohkehažž         indef-a-even-c ;

ollut+Pron+Indef:ollu%>           indefdeclpleven ;
olut+Pron+Indef:olu%>           indefdeclpleven ;
moanat+Pron+Indef:moana%>           indefdeclpleven ;
gallát+Pron+Indef+Attr:gallát%>       K ;
buohkat+Pron+Indef:buohka%>       indefdeclpleven ;
gait#divnnat+Pron+Indef:gait#divnna%> indefdeclpleven ;
gait#diinnat+Pron+Indef:gait#dijnna%> indefdeclpleven ;
gait#tiinnat+Pron+Indef:gait#tijnna%> indefdeclpleven ;
gaikkat+Pron+Indef:gajkka%>       indefdeclpleven ;
goappašagat+Pron+Indef:goappašaga%>  indefdeclpleven ;

ollugat+Pron+Indef:ollug%>       indu-pl-gahpir ;
oallugat+Pron+Indef:oallug%>       indu-pl-gahpir ;
olugat+Pron+Indef:olug%>           indu-pl-gahpir ;
gal0lis+Pron+Indef:gal'lis%>       indefdecluneven ;
olus+Pron+Indef:olus%>           indefdecluneven ;
ol0lus+Pron+Indef+Use/Sub:ol'lus%>           indefdecluneven ; ! substandard
soamis+Pron+Indef:soamis%>       indefdecluneven ;

eambbosat+Pron+Indef:eambbos%>       indu-pl-gahpir ;
eambbogat+Pron+Indef:eambbog%>       indu-pl-gahpir ;
guoktásat+Pron+Indef:guoktás%>       indu-pl-gahpir ;
goappašat+Pron+Indef:goappaš%>       indu-pl-gahpir ;
goappašat+Pron+Indef+Pl+Gen:goappaš%>ijd9»# NAMAT ;
goappešat+Pron+Indef:goappeš%>        indu-pl-gahpir ;

eanebut+Pron+Indef+Nom:eaneb%>ut       K ;
eanebut+Pron+Indef:eaneb%>u       indefdeclpluneven-obl ;
eanemusat+Pron+Indef:eanemus%>X2       indefdeclpluneven-n ;
eanemusat+Pron+Indef:eanemus%>iX2       indefdeclpluneven-obl ;

eanebat+Pron+Indef+Nom:eaneb%>at       K ;
eanebat+Pron+Indef:eaneb%>i       indefdeclpluneven-obl ;

eatnagat+Pron+Indef:eatnag%>       indu-pl-gahpir ;
galládat+Pron+Indef:gallád%>       indu-pl-gahpir ;


! Declension lexica for the declineable indefinites
! - - - - - - - - - - - - - - - - - - - - - - - - -

!splitting v- and c-final evensyll in two for NomEss, as usual.
LEXICON indef-a-even-v
indefattr ;
indefdecleven-v-ne ;
indefdecleven-obl ;

LEXICON indef-a-even-c
:%> indefattr ;
:%> indefdecleven-c-ne ;
:a%> indefdecleven-obl ;

LEXICON indefdecleven-obl
indefdeclsgeven ;
indefdeclpleven ;

LEXICON indefdecleven-v-ne
+Sg+Nom: K ;
+Ess:n K ;

LEXICON indefdecleven-c-ne
+Sg+Nom: K ;
+Ess:an K ;

LEXICON indefdeclsgeven
+Sg+Gen:X4 K ;
+Sg+Gen:X4»# NAMAT ;
+Sg+Acc:X4 K ;
+Sg+Ill:X3j K ;
+Sg+Loc:X4s K ;
+Sg+Loc+Use/Sub:X4n K ; ! !SOUTH
+Sg+Com:X5jn K ;

LEXICON indefdeclpleven
+Pl+Nom:X4t K ;
+Pl+Gen:X5jd9 K ;
+Pl+Gen:X5jd9»# NAMAT ;
+Pl+Acc:X5jd9 K ;
+Pl+Ill:X5jde K ;
+Pl+Loc:X5jn K ;
+Pl+Com:X5jgujn K ;

LEXICON nubbicase
+Attr:0 K ;
+Sg+Nom: K ;
+Ess:n K ;
+Sg+Gen:X4 K ;
+Sg+Gen:X4X7 K ;
+Sg+Gen:X4X7»# NAMAT ;
+Sg+Acc:X4 K ;
+Sg+Acc:X4X7 K ;
+Sg+Ill:X3j K ;
+Sg+Loc:X4s K ;
+Sg+Loc+Use/Sub:X4n K ; ! !SOUTH
+Sg+Com:X5jn K ;
+Pl+Nom:X4t K ;
+Pl+Gen:X5jd9 K ;
+Pl+Gen:X5jd9»# NAMAT ;
+Pl+Acc:X5jd9 K ;
+Pl+Ill:X5jde K ;
+Pl+Loc:X5jn K ;
+Pl+Com:X5jgujn K ;


LEXICON JUOBBAGOABBA
+Sg+Nom:juobbá%>#goabbá%> K ;
+Sg+Gen:juoppá%>#goappá%> K ;
+Sg+Gen:juoppá%>#goappá»# NAMAT ;
+Sg+Acc:juoppá%>#goappá%> K ;
+Sg+Ill:juobbá%>i#goabbá%>i K ;
+Sg+Loc:juoppá%>s#goappá%>s K ;
+Sg+Com:juoppá%>in#goappá%>in K ;
+Ess:juobbá%>n#goabbá%>n K ;
+Pl+Nom:juobbá%>t#goabbá%>t K ;
+Pl+Gen:juoppá%>jd9#goappá%>jd9 K ;
+Pl+Gen:juoppá%>jd9#goappá%>jd9»# NAMAT ;
+Pl+Acc:juoppá%>jd9#goappá%>jd9 K ;
+Pl+Ill:juobbá%>jde#goabbá%>jde K ;
+Pl+Loc:juoppá%>jn#goappá%>jn K ;
+Pl+Com:juoppá%>jgujn#goappá%>jgujn K ;

! Intermediate lexica for uneven indefinite pronouns
! - - - - - - - - - - - - - - - - - - - - - - - - - -

LEXICON indef-a-uneven
indefattr ;
indefdecluneven ;

LEXICON indef-a-uneven-gahpir
indefattr ;
indefdecluneven-gahpir ;

LEXICON indefsg-a-uneven
indefattr ;
indu-sg ;

LEXICON indefdecluneven
indu-sg ;
indu-pl-gahpir ;

LEXICON indu-sg
:X4 indef-sg-u-n ;
indef-sg-u-e ;
:X2 indefdeclsguneven-ag ;
indefdeclsguneven-ilc ;

LEXICON indefdecluneven-gahpir
indu-sg-gahpir ;
indu-pl-gahpir ;

LEXICON indu-sg-gahpir
indefdeclsguneven-ne ;
indefdeclsguneven-ag ;
indefdeclsguneven-ilc ;

LEXICON indu-pl-gahpir
indefdeclpluneven-n ;
:i indefdeclpluneven-obl ;
indef-sg-u-e ; !For eatnagin

! Suffix lexica for uneven indefinite pronouns
! - - - - - - - - - - - - - - - - - - - - - - -

LEXICON indefdeclsguneven-ne
indef-sg-u-n ;
indef-sg-u-e ;

LEXICON indef-sg-u-n
+Sg+Nom: K ;

LEXICON indef-sg-u-e
+Ess:in K ;

LEXICON indefdeclsguneven-ag
+Sg+Gen:a K ;
+Sg+Acc:a K ;

LEXICON indefdeclsguneven-ilc
+Sg+Ill:ij K ;
+Sg+Loc:is K ;
+Sg+Loc+Use/Sub:in K ; ! !SOUTH
+Sg+Com:ijn K ;

LEXICON indefdeclpluneven-n
+Pl+Nom:at K ;

LEXICON indefdeclpluneven-obl
+Pl+Gen:jd9 K ;
+Pl+Acc:jd9 K ;
+Pl+Ill:jdda K ;
+Pl+Loc:jn K ;
+Pl+Com:jgujn K ;

LEXICON indefattr
+Attr:0 K ;


! Multiword indefinites
! - - - - - - - - - - -

LEXICON indefmulti !SUB added this lexicon, because otherwise these would be in indefattr
feara% mii+Pron+Indef+Sg+Nom+Use/Sub:feara% mij K-son ; ! spellera dihte
feara% mii+Pron+Indef+Sg+Acc+Use/Sub:feara% májd9 K-son ; ! spellera dihte
feara% gii+Pron+Indef+Sg+Nom+Use/Sub:feara% gij K-son ; ! spellera dihte
feara% mii+Pron+Indef+Use/Sub:feara% ma oblintercas ; ! spellera dihte
feara% gii+Pron+Indef+Use/Sub:feara% gea oblintercas ; ! spellera dihte

vaikko% mii+Pron+Indef+Sg+Nom+Use/Sub:vaikko% mij K-son ; ! spellera dihte
vaikko% mii+Pron+Indef+Sg+Acc+Use/Sub:vaikko% májd9 K-son ; ! spellera dihte
vaikko% gii+Pron+Indef+Sg+Nom+Use/Sub:vaikko% gij K-son ; ! spellera dihte
vaikko% mii+Pron+Indef+Use/Sub:vaikko% ma oblintercas ; ! spellera dihte
vaikko% gii+Pron+Indef+Use/Sub:vaikko% gea oblintercas ; ! spellera dihte
vaikko% makkár+Pron+Indef+Use/Sub:vaikko% makkár DAKKAR ;  ! spellera dihte
vaikko% guh0te+Pron+Interr+Use/Sub:vaikko% guh'te GUHTECASE ;  ! spellera dihte
vaikko% guhtemuš+Pron+Interr+Use/Sub:vaikko% guđemužž GUHTEMUŠCASE ; ! spellera dihte
vaikko% goabbá+Pron+Interr+Use/Sub:vaikko% goabbá GUHTECASE ; ! spellera dihte

! some multiword prons, according to Nickel
!feara% mii iind ;
!feara% gii iind ;
mii% nu+Use/Sub:mii% nu iind;  ! spellera
gii% nu+Use/Sub:gii% nu iind;  ! spellera
makkár% nu+Use/Sub:makkár% nu iind;  ! spellera
makkáraš% nu+Use/Sub:makkáraš% nu iind;  ! spellera


! The indeclineable indefinites
! - - - - - - - - - - - - - - -

LEXICON indeclindef
ald+Use/Sub:ald9 iind ; !
buot iind ;
buot:buot»# NAMAT ;
gait:gajt iind ;
gait:gajt»# NAMAT ;
eatnat iind ;
eatnat:eatnat»# NAMAT ;
!fal iind ; ! cf. Nickel s. 126.
gallát iind ;
goabbat iind ;
goabbat:goabbat»# NAMAT ;
goappaš iind ;
goappaš:goappaš»# NAMAT ;
!goappašat iind ; ! thus both Nom and non-Nom. XXX.
guhtet iind ;
ieš#guhtet iind ;
ieš#guđet iind ;
ieš#guđet:ieš#guđet»# NAMAT ;
ieš#guhtet:ieš#guhtet»# NAMAT ;
ihkinassii iind ;
juohke iind ;
juohke:juohke»# NAMAT ;
ollu iind ;
olu iind ;
!vaikko iind ; !This is CS, not pron


LEXICON iind
+Pron+Indef:%>0 K ;



! Separate lexica for exceptional entries
! - - - - - - - - - - - - - - - - - - - -

LEXICON declindef-idiosync
! separate lexica for these entries:
! oktat
oktahat:ovttaha OVTTAHAT ;
seammahat+Use/Sub:seammaha OVTTAHAT ; !
seammahat+Pron+Indef+Attr+Use/Sub:seammahat # ; !
seammahat+Pron+Indef+Nom+Sg+Use/Sub:seammahat # ; !
oktahat+Pron+Indef+Attr:oktahat%> K ;
aktahat+Pron+Indef+Attr:aktahat%> K ;
oktahat+Pron+Indef+Nom+Sg:oktahat%> K ;
aktahat+Pron+Indef+Nom+Sg:aktahat%> K ;

earrás+Pron+Indef:earrás EARRAS ;
ieš#guhtet+Pron+Indef+Nom:ieš#guhtet%> K ;
ieš#guhtet+Pron+Indef+Gen+Use/Sub:ieš#guđet%> K ; !
ieš#guhtet+Pron+Indef+Gen+Use/Sub:ieš#guđet»# NAMAT ; !
ieš#guhtet+Pron+Indef+Acc+Use/Sub:ieš#guđet%> K ; !
simme+Pron+Indef+Use/Sub:simme SIMME ; !
seamma+Pron+Indef+Use/Sub:seamma SEAMMA ; !
seammá+Pron+Indef:seammá SEAMMA ;
seamma+Pron+Indef+Attr:seamma»# NAMAT ;
seamma+Pron+Indef+Attr:seamma%> K ;
seammá+Pron+Indef+Attr+Use/Sub:seammá # ; !, dict says seamma ~ seammá (#seamma)
juoga+Pron+Indef:juo JUOGA ;
gal0le+Pron+Indef+Use/CircN:gal'l GUOKTE ; !
guktot+Pron+Indef:gukto indefdeclplcontr ;
juogamii+Pron+Indef+Sg+Nom:juogamij%> K-son ;
juogamii+Pron+Indef+Sg+Acc+Use/Sub:juogamájd9 K-son ; !
juogamii+Pron+Indef:juogama%> oblintercas ;

! okta
! juoga mii
!oktahat         +       no      OKTAHAT => indefeven / Nom=aktahat / OKTAttr
!        OKTattr:n=oktahat, aktahat, oktat, aktat
!                o=ovttahat, ovttat
!hárvvis        +       a       indef-a-      uneven  hárve, hárvves
!hárvvis        +       a       indef-a-      contr   hárve, hárvves
!Some problematic ones, one form for nsg, one for the rest
!oktat+Pron+Indef+Sg+Nom:oktat%> K ;
!oktat+Pron+Indef:ovttat%> K ;
!oktahat+Pron+Indef+Sg+Nom:oktahat%> K ;
!oktahat+Pron+Indef+:ovttahat%> K ;

LEXICON indefdeclplcontr ! so far guktot
+Pl+Nom:%>t K ;
+Pl+Gen:%>jd9 K ;
+Pl+Gen:%>jd9»# NAMAT ;
+Pl+Acc:%>jd9 K ;
+Pl+Ill:%>jde K ;
+Pl+Loc:%>jn K ;
+Pl+Com:%>jn K ;

LEXICON EARRAS
+Sg+Acc:%>a K ;
+Sg+Gen:%>a K ;
+Sg+Ill:%>ij K ;
+Sg+Loc:%>is K ;
+Sg+Loc+Use/Sub:%>in K ; ! !SOUTH
+Sg+Com:%>iin K ;
+Ess:%>in K ;
+Pl+Nom:%>at K ;
+Pl+Gen:%>ijd9 K ;
+Pl+Acc:%>ijd9 K ;
+Pl+Ill:%>ijdda K ;
+Pl+Loc:%>ijn K ;
+Pl+Com:%>ijgujn K ;


LEXICON SEAMMA
+Sg+Nom:%> K ;
+Sg+Acc:%> K ;
+Sg+Gen:%> K ;
+Sg+Gen+Use/Sub:»# NAMAT ; !
+Sg+Ill:%>j K ;  ! Let's await the verdict of the jury..
! +Sg+Loc:%>s K ; ! Let's settle for the Adv reading
+Sg+Loc+Use/Sub:%>n K ;  ! !SOUTH, well..., probably this one should Adv as well
+Sg+Com:%>jn K ; ! Let's await the verdict of the jury..
+Pl+Nom:%>t K ;
+Pl+Acc:%>jd9 K ;
+Pl+Gen:%>jd9 K ;
+Pl+Gen+Use/Sub:%>jd9»# NAMAT ; !

LEXICON SIMME !SUB
+Sg+Nom:%> K ;
+Sg+Acc:%> K ;
+Sg+Gen:%> K ;
+Sg+Ill:%>j K ;
+Sg+Loc:%>n K ;
+Sg+Com:%>jn K ;
+Pl+Nom:%>t K ;
+Pl+Acc:%>jd9 K ;
+Pl+Gen:%>jd9 K ;
+Pl+Gen:%>jd9»# NAMAT ;



LEXICON JUOGA
+Sg+Nom:ga%> K ;
+Sg+Acc:ga%> K ;
+Sg+Acc:jdá%> K ;
+Pl+Acc:jdá%>jd9 K ;
+Sg+Acc+Use/Sub:jddá K ; !
+Sg+Acc+Use/Sub:jaaájd9 K ; !
+Sg+Gen:ga%> K ;
+Sg+Gen:ga»# NAMAT ;
+Sg+Ill:sa%>t K ;
+Sg+Loc:stá%> K ;
+Sg+Com:jdá%>jn K ;
+Ess:jdá%>n K ;
+Ess:ni%>n K ;
+Attr+Use/Sub:da K ; !

LEXICON OVTTAHAT
+Sg+Acc:g%>a K ;
+Sg+Gen:g%>a K ;
+Sg+Gen:t»# NAMAT ;
+Sg+Ill:hk%>ii K ;
+Sg+Loc:g%>as K ;
+Sg+Com:g%>ajn K ;
+Ess:hk%>an K ;
+Pl+Nom:g%>at K ;
+Pl+Acc:g%>ajd9 K ;
+Pl+Gen:g%>ajd9 K ;
+Pl+Ill:g%>ajde K ;
+Pl+Loc:g%>ajn K ;
+Pl+Com:g%>ajgujn K ;
