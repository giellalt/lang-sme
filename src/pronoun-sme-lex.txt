!Distributed under the terms of the GNU General Public License version 2 
! or any later version.

! ############################## !
!This file contains the Pronouns !
! ############################## !



! Pronoun
! =======

!Pointing to all the subgrops, presented in this order below.
LEXICON Pronoun
 Personal ;
 Interrogative ;
 Relative ;
 Demonstrative ;
 Reflexive ;
 Reciprocal ;
 Indefinite ;


! Personal pronouns
! -----------------

!Personal pronouns, splitting in 1st, 2nd, 3rd
LEXICON Personal
mun+Pron+Pers+Sg1:m firstperspronsg ;
mun+Pron+Pers+Du1:m firstpersprondu ;
mun+Pron+Pers+Pl1:m perspronpl ;
don+Pron+Pers+Sg2:d nonfirstperspronsg ;
don+Pron+Pers+Du2:d nonfirstpersprondu ;
don+Pron+Pers+Pl2:d perspronpl ;
son+Pron+Pers+Sg3:s nonfirstperspronsg ;
son+Pron+Pers+Du3:s nonfirstpersprondu ;
son+Pron+Pers+Pl3:s perspronpl ;
dat+Pron+Pers:da datpers ;                ! Dat lea ruovttus.

!Case suffixes for 1st Sg, pointing to K lexicon
LEXICON firstperspronsg
 +Nom:un K ;
 +Nom:on K ;
 +Acc:u K ;
 +Gen:u K ;
 +Gen:u# NAMAT ;
 +Ill:unnje K ;
 +Loc:us K ;
 +Com:ujnna K ;
 +Ess:unin K ;

!Case suffixes for noŋst Sg, pointing to K lexicon
LEXICON nonfirstperspronsg
 +Nom:on K ;
 +Acc:u K ;
 +Gen:u K ;
 +Gen:u# NAMAT ;
 +Ill:utnje K ;
 +Loc:us K ;
 +Com:ujnna K ;
 +Ess:unin K ;

!Case suffixes for 1st Du, pointing to K lexicon
LEXICON firstpersprondu
 +Nom:oaj K ;
 +Acc:unno K ;
 +Gen:unno K ;
 +Gen:unno# NAMAT ;
 +Ill:unnujde K ;
 +Loc:unnos K ;
 +Com:unnujn K ;
 +Ess:unnon K ;

!Case suffixes for noŋst Du, pointing to K lexicon
LEXICON nonfirstpersprondu
 +Nom:oaj K ;
 +Acc:udno K ;
 +Gen:udno K ;
 +Acc:utno K ; !SUB
 +Gen:utno K ; !SUB
 +Gen:udno# NAMAT ;
 +Ill:udnujde K ;
 +Ill:utnujde K ; !SUB
 +Loc:udnos K ;
 +Loc:utnos K ; !SUB
 +Com:udnujn K ;
 +Com:utnujn K ; !SUB
 +Ess:udnon K ;
 +Ess:utnon K ; !SUB

!Case suffixes for all Pl forms, pointing to K lexicon
LEXICON perspronpl
 +Nom:ij K ;
 +Acc:in K ;
 +Gen:in K ;
 +Gen:in# NAMAT ;
 +Ill:insij K ; !SUB
 +Ill:idjijde K ;
 +Loc:is K ;
 +Loc:ijs K ; !SUB !SOUTH
 +Com:ingujn K ;
 +Ess:inin K ;

LEXICON datpers
 +Sg3:0 demcassg ;
 +Pl3:0 demcaspl ;
 +Ess:nin K ;


! Interrogative pronouns
! ----------------------

!Giving ideosyncratic Sg Nom of gii, mii lexically
!Sending the oblique forms of gii, mii to an oblique sublexicon
!Giving the stem of guhte, guhtemuš, goabbá
LEXICON Interrogative
mii+Pron+Interr+Sg+Nom:mij K-son ;
mii+Pron+Interr+Sg+Acc:májd9 K-son ; !SUB
gii+Pron+Interr+Sg+Nom:gij K-son ;
gii+Pron+Interr+Sg+Ill+Foc:geasagii # ;
mii+Pron+Interr:ma oblintercas ;
gii+Pron+Interr:geaX5 oblintercas ;
guh0te+Pron+Interr:guh'te GUHTECASE ;
guhte+Pron+Interr+Sg+Nom:guhtes K ;
guhtemuš+Pron+Interr:guđemužž GUHTEMUŠCASE ;
guhtemuš+Pron+Interr:guhtemuš K-son ; !deviating Nom alternant.
goabbá+Pron+Interr:goabbá GUHTECASE ;
makkár+Pron+Interr:makkár DAKKAR ;
man% láhkái+Pron+Interr+Sg+Com:mainna#lágiin K-son ; !SUB


! Relative pronouns
! -----------------

LEXICON Relative
mii+Pron+Rel+Sg+Nom:mij K-son ;
mii+Pron+Rel+Sg+Acc:májd9 K-son ;
gii+Pron+Rel+Sg+Nom:gij K-son ;
mii+Pron+Rel:ma oblintercas ;
gii+Pron+Rel:geaX5 oblintercas ;
guh0te+Pron+Rel:guh'te GUHTECASE ;
guhte+Pron+Rel+Sg+Nom:guhtes K ;
guhtemuš+Pron+Rel:guđemužž GUHTEMUŠCASE ;
guhtemuš+Pron+Rel:guhtemuš K-son ; !deviating Nom alternant.
goabbá+Pron+Rel:goabbá GUHTECASE ;
man% láhkái+Pron+Rel+Sg+Com:mainna#lágiin K-son ; ! XXX hmm...

LEXICON oblintercas
 +Sg+Acc:jd9 K-son ;
 +Pl+Nom:t K ;
 +Ess:nin K ;
 +Sg:0 obldemcassg ;
 +Pl:0 obldemcaspl ;
!Then going directly to the case paradigm of the demonstratives

! guhte
LEXICON GUHTECASE
 GUHTE-NE ;
 GU-GU-OBL ;

! Nom and Ess of guhte
LEXICON GUHTE-NE
 +Sg+Nom: K-son ;
 +Ess:n K-son ;

! guhtemuš
LEXICON GUHTEMUŠCASE
 GUHTEMUŠ-NE ;
 :a GU-GU-OBL ;

! Nom and Ess of guhtemuš
LEXICON GUHTEMUŠ-NE
 +Sg+Nom: K-son ;
 +Ess:an K-son ;

! Oblique paradigm for guhte and guhtemuš
LEXICON GU-GU-OBL
 +Sg+Gen:X8 K-son ;
 +Sg+Gen:X8# NAMAT ;
 +Sg+Acc:X8 K-son ;
 +Sg+Ill:X3j K-son ;
!+Sg+Gen: K-son ; ! this should go out
 +Sg+Loc:X4s K-son ;
 +Sg+Loc:X4n K-son ; !SUB !SOUTH
 +Sg+Com:X5jn K-son ;
 +Pl+Nom:X4t K-son ;
 +Pl+Gen:X5jd9 K-son ;
 +Pl+Gen:X5jd9# NAMAT ;
 +Pl+Acc:X5jd9 K-son ;
 +Pl+Ill:X5jde K-son ;
 +Pl+Com:X5jgujn K-son ;


! Demonstrative pronouns
! ----------------------

!Giving baseform + all demonstrative stems
!Pointing to case paradigms

LEXICON Demonstrative
dat+Pron+Dem:da demcas ;
diet+Pron+Dem:die demcas ;
dát+Pron+Dem:dá demcas ;
duot+Pron+Dem:duo demcas ;
dot+Pron+Dem:do demcas ;
doot+Pron+Dem:doo demcas ; !sub? dialectal

dahtanaga+Pron+Dem:da Dahtanaga-par ;
dakkár+Pron+Dem:dakkár DAKKAR ;
dákkar+Pron+Dem:dákkar DAKKAR ; !SUB XXX
dakkar+Pron+Dem:dakkar DAKKAR ; !SUB XXX
dákkár+Pron+Dem:dákkár DAKKAR ;
diekkár+Pron+Dem:diekkár DAKKAR ;
duokkár+Pron+Dem:duokkár DAKKAR ;
dokkár+Pron+Dem:dokkár DAKKAR ;
!makkár+Pron+Dem:makkár DAKKAR ; ! Give this one Interr

dakkáraš+Pron+Dem:dakkáražž DAKKARAS ;
dákkáraš+Pron+Dem:dákkáražž DAKKARAS ;
diekkáraš+Pron+Dem:diekkáražž DAKKARAS ;
duokkáraš+Pron+Dem:duokkáražž DAKKARAS ;
dokkáraš+Pron+Dem:dokkáražž DAKKARAS ;
makkáraš+Pron+Dem:makkáražž DAKKARAS ;


!Splitting into Sg, Pl and Ess, different paradigms
LEXICON demcas
 +Sg:0 demcassg ;
 +Pl:0 demcaspl ;
 +Ess:nin K ;

!Splitting into oblique and nonoblique
!This is because of the interrogatives
LEXICON demcassg
 nomdemcassg ;
 obldemcassg ;
LEXICON demcaspl
 nomdemcaspl ;
 obldemcaspl ;

LEXICON nomdemcassg
!The nominative, then pointing to clitics
!This could be conflated, but look out for interr
 +Nom:t K ;
LEXICON nomdemcaspl
 +Nom:t K ;


LEXICON obldemcassg
!The other cases, then pointing to clitics
 +Gen:n K ;
 +Gen:n# NAMAT ;
 +Acc:n K ;
 +Ill:sa K ;
 +Loc:s K ;
 +Com:jnna K ; !dainna Sg Com

LEXICON obldemcaspl
 +Gen:jd9 K ;
 +Gen:jd9# NAMAT ;
 +Acc:jd9 K ;
 +Ill:jdda K ;
 +Loc:jn K ;   !dain   Pl Loc
!+Loc:jnna K ; !dainna Pl Loc is deemed out in contemporary language.
 +Com:jguin K ;

!This case paradigm is exceptional and partial.
LEXICON Dahtanaga-par
 +Sg+Nom:htanaga # ;
 +Sg+Gen:mmanaga # ;
 +Sg+Gen:mmanaga# NAMAT ;
 +Sg+Acc:mmanaga # ;
 +Sg+Ill:sanaga # ;
 +Sg+Loc:sttánaga # ;
 +Sg+Com:jnnanaga # ;
 +Pl+Ill:jddanaga # ;
 +Pl+Loc:jnnanaga # ;

LEXICON DAKKAR
 +Sg+Nom: K ;
 +Attr: K ;
 +Attr:# NAMAT ;
 +Ess:in K ;
 +Sg+Gen:a K ;
 +Sg+Acc:a K ;
 +Pl+Nom:at K ;
 00:i DAKKARI- ;

LEXICON DAKKARI-
 +Sg+Ill:j K ;
 +Sg+Loc:s K ;
 +Sg+Loc:n K ; !SUB !SOUTH
 +Sg+Com:jn K ;
 +Pl+Gen:jd9 K ;
 +Pl+Gen:jd9# NAMAT ;
 +Pl+Acc:jd9 K ;
 +Pl+Ill:jdda K ;
 +Pl+Loc:jn K ;
 +Pl+Com:jgujn K ;

LEXICON DAKKARAS
 +Sg+Nom: K ;
 +Sg+Nom:X7 R ;
 +Ess:an K ;
 :a DAKKARASSA- ;

LEXICON DAKKARASSA-
  +Sg+Gen:X4 K ;
  +Sg+Gen:X8 K ;
  +Sg+Gen:X4# NAMAT ;
  +Sg+Gen:X8# NAMAT ;
  +Sg+Acc:X4 K ;
  +Sg+Acc:X8 K ;
  +Sg+Ill:X3j K ;
  +Sg+Loc:X4s K ;
  +Sg+Loc:X8s K ;
  +Sg+Loc:X4n K ; !SUB !SOUTH
  +Sg+Loc:X8n K ; !SUB !SOUTH
  +Sg+Com:X5jn K ;
  +Pl+Nom:X4t K ;
  +Pl+Gen:X5jd9 K ;
  +Pl+Gen:X5jd9# NAMAT ;
  +Pl+Acc:X5jd9 K ;
  +Pl+Ill:X5jde K ;
  +Pl+Loc:X5jn K ;
  +Pl+Com:X5jgujn K ;



! Reflexive pronouns
! ------------------

LEXICON Reflexive
!Two nominative reflexives, and pointer to the rest
!The Pl one is used for Du as well, here given two entries.
!Should one of them be removed?
ieš+Pron+Refl+Sg+Nom:ieš K ;
ieš+Pron+Refl+Sg+Nom:ieš# NAMAT ;  ! <======= ok
ieš+Pron+Refl+Du+Nom:ieža K ;
ieš+Pron+Refl+Du+Nom:ieža# NAMAT ; !SUB
ieš+Pron+Refl+Pl+Nom:ieža K ;
ieš+Pron+Refl+Pl+Nom:ieža# NAMAT ; !SUB
ieš+Pron+Refl+Gen+PxSg3:ald9 # ;   !SUB !SOUTH
ieš+Pron+Refl+Ess:iehčanassij K ;
ieš+Pron+Refl:0 reflobl ;

LEXICON reflobl
!The V/V2 alternation due to 3rd person vocalism
!Pointing directly to 1st and 3rd person sublexica for AG & Loc.
!Pointing to exceptional Pl sublexicon for AG and Loc.
+Acc:ieža PxV2 ;
+Gen:ieža PxV2 ;
+Gen+PxSg1:iežan# NAMAT ;
+Gen+PxSg1:iežam9# NAMAT ;  !SUB !SOUTH
+Gen+PxSg2:iežat# NAMAT ;
+Gen+PxSg3:iežas# NAMAT ;     ! <========= ??
+Gen+PxDu1:iežame# NAMAT ;
+Gen+PxDu2:iežade# NAMAT ;
+Gen+PxDu3:iežaska# NAMAT ;
+Gen+PxPl1:iežamet# NAMAT ;
+Gen+PxPl2:iežadet# NAMAT ;
+Gen+PxPl3:iežaset# NAMAT ;
+Acc:iehča Px1V ;
+Gen:iehča Px1V ;
+Gen+PxSg1:iehčan# NAMAT ;
+Gen+PxDu1:iehčame# NAMAT ;
+Gen+PxPl1:iehčamet# NAMAT ;
+Acc:iežajd PxCPlstem ;
+Gen:iežajd PxCPlstem ;
+Gen+PxDu1:iežajdeame# NAMAT ;     !SUB
+Gen+PxDu2:iežajdeatte# NAMAT ;     !SUB
+Gen+PxDu3:iežajdeaskka# NAMAT ; !SUB
+Gen+PxPl1:iežajdeamet# NAMAT ;     !SUB
+Gen+PxPl2:iežajdeattet# NAMAT ; !SUB
+Gen+PxPl3:iežajdeaset# NAMAT ;     !SUB
+Ill:alcces PxC ;
+Ill:alcca PxV ;
+Ill:alcce PxV2 ;
+Ill:allas PxC ;
+Ill+PxSg1:allasim9# K ; !SUB !SOUTH
+Loc:alddi PxV ;
+Loc:aldde Px3V ;
+Loc:alddest PxC ;
+Loc:alddin PxCPlstem ; !SUB
+Loc:alddijn PxCPlstem ;
+Loc:alddán PxCPlstem ;
+Com:iežajn PxC ;
+Com:iežajd PxCPlCom ;
+Ess:iehčan PxC ;

LEXICON PxCPlstem
!This lexicon is here since it only applies to Reflexives.
!It handles the exeptional AG and Loc DuPl plural stem forms of ieš.
 +PxDu1:eame K ;
 +PxDu2:eatte K ;
 +PxDu3:easkka K ;
 +PxPl1:eamet K ;
 +PxPl2:eattet K ;
 +PxPl3:easet K ;


LEXICON PxCPlCom
! This lexicon for archaic biblical forms like iežaideasetguin.
 +PxDu1:eameguin K ;
 +PxDu2:eatteguin K ;
 +PxDu3:easkkaguin K ;
 +PxPl1:eametguin K ;
 +PxPl2:eattetguin K ;
 +PxPl3:easetguin K ;


! Reciprocal pronouns
! -------------------

! The first 6 entries handle the first element of the recipr.
! The next 12 handle the 2nd part of the non-Px recipr.
! The members of the third section  point to Px lexica.

LEXICON Reciprocal
nubbi+Pron+Recipr+Du:nubbi # ;      !SUB
nubbi+Pron+Recipr+Pl:nuppit # ;     !SUB
goabbat+Pron+Recipr+Du:goabbat # ;  !SUB
goabbat+Pron+Recipr+Pl:guhtet # ;   !SUB
goabbat+Pron+Recipr+Du:goappat # ; !SUB ! Dialectal forms. Norm?
goabbat+Pron+Recipr+Pl:guđet # ;   !SUB ! Dialectal forms. Norm?

nuppi+Pron+Recipr+Du+Acc:nuppi K ;      !SUB
nuppi+Pron+Recipr+Du+Gen:nuppi K ;      !SUB
nuppi+Pron+Recipr+Du+Ill:nubbái K ;     !SUB
nuppi+Pron+Recipr+Du+Loc:nuppis K ;     !SUB
nuppi+Pron+Recipr+Du+Com:nuppiin K ;    !SUB
nuppi+Pron+Recipr+Du+Ess:nubbin K ;     !SUB
nuppi+Pron+Recipr+Pl+Acc:nuppiid K ;    !SUB
nuppi+Pron+Recipr+Pl+Gen:nuppiid K ;    !SUB
nuppi+Pron+Recipr+Pl+Ill:nuppiide K ;   !SUB
nuppi+Pron+Recipr+Pl+Loc:nuppiin K ;    !SUB
nuppi+Pron+Recipr+Pl+Com:nuppiiguin K ; !SUB
nuppi+Pron+Recipr+Pl+Ess:nubbin K ;     !SUB


guoibmi+Pron+Recipr+Sg+Acc:guoibmá Px1VRecipr ;     !SUB
guoibmi+Pron+Recipr+Sg+Gen:guoibmá Px1VRecipr ;     !SUB
!guoibmi+Pron+Recipr+Sg+Acc:guoimmá Px2VRecipr ;        !SUB
!guoibmi+Pron+Recipr+Sg+Gen:guoimmá Px2VRecipr ;        !SUB
guoibmi+Pron+Recipr+Sg+Acc:guoimmi Px23VRecipr ;        !SUB
guoibmi+Pron+Recipr+Sg+Gen:guoimmi Px23VRecipr ;        !SUB
guoibmi+Pron+Recipr+Sg+Ill:guoibmás PxCPlstem ;         !SUB
guoibmi+Pron+Recipr+Sg+Loc:guoimmist PxCPlstem ;        !SUB
guoibmi+Pron+Recipr+Sg+Com:guoimmiin PxCPlstem ;        !SUB

guoibmi+Pron+Recipr+Pl+Acc:guoimmiid PxCPlstem ;        !SUB
guoibmi+Pron+Recipr+Pl+Gen:guoimmiid PxCPlstem ;        !SUB
guoibmi+Pron+Recipr+Pl+Ill:guoimmiidasa PxVPlStemRecipr ;   !SUB
guoibmi+Pron+Recipr+Pl+Loc:guoimmiin PxCPlstem ;    !SUB
guoibmi+Pron+Recipr+Pl+Com:guoimmiid PxCPlComRecipr ;   !SUB

nubbi+Pron+Recipr+Pl+Gen:nuppiid nubbiPxDu ;
nubbi+Pron+Recipr+Pl+Acc:nuppiid nubbiPxDu ;
nubbi+Pron+Recipr+Pl+Ill:nuppiidasa Px1VRecipr ;
nubbi+Pron+Recipr+Pl+Loc:nuppiin nubbiPxDu ;
nubbi+Pron+Recipr+Pl+Com:nuppiid nubbiPxDuCom ;

nubbi+Pron+Recipr+Pl+Gen:nuppiid nubbiPxPl ;
nubbi+Pron+Recipr+Pl+Acc:nuppiid nubbiPxPl ;
nubbi+Pron+Recipr+Pl+Ill:nuppiidasa Px23VRecipr ;
nubbi+Pron+Recipr+Pl+Ill:nuppiidasa Px3VPlStem ;
nubbi+Pron+Recipr+Pl+Loc:nuppiin nubbiPxPl ;
nubbi+Pron+Recipr+Pl+Com:nuppiid nubbiPxPlCom ;

LEXICON Px1VRecipr      
 +PxDu1:me K ;          
 +PxPl1:met K ;         

LEXICON Px23VRecipr 
!LEXICON Px2VPlStem     !SUB
 +PxDu2:X2de K ;
 +PxPl2:X2det K ;

LEXICON Px3VPlStem     
 +PxDu3:ska K ;         
 +PxPl3:set K ;         

LEXICON PxVPlStemRecipr !SUB
 +PxPl1:met K ;         !SUB
 +PxPl2:det K ;         !SUB
 +PxPl3:set K ;         !SUB

LEXICON PxCPlComRecipr  !SUB
 +PxPl1:eametguin K ;   !SUB    
 +PxPl2:eadetguin K ;       !SUB
 +PxPl3:easetguin K ;       !SUB

LEXICON nubbiPxDu
 +PxDu1:eame K ;
 +PxDu2:eatte K ;
 +PxDu3:easkka K ;

LEXICON nubbiPxPl
 +PxPl1:eamet K ;
 +PxPl2:eattet K ;
 +PxPl3:easet K ;
 
LEXICON nubbiPxDuCom
 +PxDu1:eameguin K ;
 +PxDu2:eatteguin K ;
 +PxDu3:easkkaguin K ;

LEXICON nubbiPxPlCom
 +PxPl1:eametguin K ;
 +PxPl2:eattetguin K ;
 +PxPl3:easetguin K ;


! Indefinite pronouns
! -------------------

! Dividing the indefinites in three groups
LEXICON Indefinite
 declindef-cl ;
 declindef ;
 declindef-idiosync ;
 indeclindef ;
 indefmulti ; ! feara mii

! Declineable indefinite pronouns with case + clitic
! - - - - - - - - - - - - - - - - - - - - - - - - -

LEXICON declindef-cl

! Exceptional nominatives
mihkkege+Pron+Indef+Sg+Nom:mihkkege # ;
mihkkege+Pron+Indef+Pl+Nom:mahkkege # ;
mihkkege+Pron+Indef+Pl+Gen:maidege # ;
mihkke+Pron+Indef+Sg+Nom:mihkke # ;
mihkket+Pron+Indef+Sg+Nom:mihkket # ;
mihkkige+Pron+Indef+Sg+Nom:mihkkige # ;
mihkkige+Pron+Indef+Pl+Nom:mahkkige # ;
mihkkige+Pron+Indef+Pl+Gen:maidige # ;
giige+Pron+Indef+Sg+Nom:gijge # ;
giinai+Pron+Indef+Sg+Nom:gijnai # ;
giijai+Pron+Indef+Sg+Nom:gijjai # ;
miinai+Pron+Indef+Sg+Nom:mijnai # ;
miijai+Pron+Indef+Sg+Nom:mijjai # ;
ieš#giige+Pron+Indef+Sg+Nom:ieš#gijge # ;
ieš#giinai+Pron+Indef+Sg+Nom:ieš#gijnai # ;
ieš#giijai+Pron+Indef+Sg+Nom:ieš#gijjai # ;

! Pointers to obl sublexicon
mihkkege+Pron+Indef:ma oblindefcas ;
giige+Pron+Indef:gea oblindefcas ;
giinai+Pron+Indef:gea oblindefcas-giinai ;
miinai+Pron+Indef:ma oblindefcas-miinai ;
ieš#giige+Pron+Indef:ieš#gea oblindefcas ;
ieš#giinai+Pron+Indef:ieš#gea oblindefcas-giinai ;
guhtege+Pron+Indef:guh'te GUHTEGECASE ;
guhtenai+Pron+Indef:guh'te GUHTENAI ;
!ieš#guh0te+Pron+Indef:ieš#guh'te GUHTECASE ;
ieš#guhtege+Pron+Indef:ieš#guh'te GUHTEGECASE ;
ieš#guhtenai+Pron+Indef:ieš#guh'te GUHTENAI ;
guhtemušge+Pron+Indef:guđemužž GUHTEMUSGECASE ;
guhtemušge+Pron+Indef:guhtemušge # ; !deviating Nom altarnant.
goabbáge+Pron+Indef:goabbá GUHTEGECASE ;
goabbánai+Pron+Indef:goabbá GUHTENAI ;
!makkárge+Pron+Indef (have a look at this one)
!oktage+Pron+Indef (wait with this one until numerals are treated)

!The difference between mihkkege and mihkkige
!continues through declination (maidege-maidige etc.).
!Should they be separated in different lexicons to make it logical? IK

LEXICON oblindefcas
 +Sg:0 oblindemcassg ;
 +Pl:0 oblindemcaspl ;


LEXICON oblindemcassg
!The other cases, then pointing to clitics
 +Gen:nge # ;
 +Gen:nge# NAMAT ;
 +Acc:nge # ;
 +Ill:sage # ;
 +Loc:sge # ;
 +Loc:stege # ;
 +Com:jnnage # ;

LEXICON oblindemcaspl
 +Gen:jde # ;
 +Gen:jde# NAMAT ;
 +Acc:jde # ;
 +Gen:jdege # ;
 +Gen:jdege# NAMAT ;
 +Acc:jdege # ;
 +Gen:jdige # ;
 +Gen:jdige# NAMAT ;
 +Acc:jdige # ;
 +Ill:jddage # ;
 +Loc:jnge # ;
!Here we do not go to the case paradigm of the demonstratives,
!since the clitic path is different. ComPl lacks (intentionally) from paradigm.

LEXICON oblindefcas-giinai
 +Gen:nnai # ;
 +Gen:nnai# NAMAT ;
 +Acc:nnai # ;
 +Gen:nai # ;
 +Gen:nai# NAMAT ;
 +Acc:nai # ;
 +Ill:sanai # ;
 +Ill:sai # ;
 +Loc:snai # ;
 +Loc:sai # ;
 +Loc:stenai # ;
 +Loc:stai # ;
 +Com:jnnanai # ;
 +Com:jnnai # ;
 +Gen:jdnai # ;
 +Gen:jdnai# NAMAT ;
 +Acc:jdnai # ;
 +Gen:jdai # ;
 +Gen:jdai# NAMAT ;
 +Acc:jdai # ;
 +Ill:jddanai # ;
 +Ill:jddai # ;
 +Loc:jnnai # ;
 +Loc:jnai # ;
 
LEXICON oblindefcas-miinai
 +Gen:nnai # ;
 +Gen:nnai# NAMAT ;
 +Acc:nnai # ;
! +Gen:nai # ; !commented out disamb reasons
 +Gen:nai# NAMAT ;
! +Acc:nai # ; !commented out disamb reasons
 +Ill:sanai # ;
 +Ill:sai # ;
 +Loc:snai # ;
 +Loc:sai # ;
 +Loc:stenai # ;
 +Loc:stai # ;
 +Com:jnnanai # ;
 +Com:jnnai # ;
 +Gen:jdnai # ;
 +Gen:jdnai# NAMAT ;
 +Acc:jdnai # ; !commented out disamb reasons
! +Gen:jdai # ; !commented out disamb reasons
 +Gen:jdai# NAMAT ;
! +Acc:jdai # ; !commented out disamb reasons
 +Ill:jddanai # ;
! +Ill:jddai # ; !commented out disamb reasons
 +Loc:jnnai # ;
 +Loc:jnai # ;




! guhtege
LEXICON GUHTEGECASE
 GUHTEGE-NE ;
 GUGE-GUGE-OBL ;


! Nom and Ess of guhtege
LEXICON GUHTEGE-NE
 +Sg+Nom:ge # ;
 +Ess:nge # ;

! guhtemušge
LEXICON GUHTEMUSGECASE
 GUHTEMUSGE-NE ;
 :a GUGE-GUGE-OBL ;

! Nom and Ess of guhtemušge
LEXICON GUHTEMUSGE-NE
 +Sg+Nom:ge # ;
 +Ess:ange # ;

! Oblique paradigm for guhtege and guhtemušGE
LEXICON GUGE-GUGE-OBL
 +Sg+Gen:X8ge # ;
 +Sg+Gen:X8ge# NAMAT ;
 +Sg+Acc:X8ge # ;
 +Sg+Ill:X3jge # ;
!+Sg+Gen:ge # ; ! this should go out
 +Sg+Loc:X4sge # ;
 +Sg+Loc:X4nge K ; !SUB !SOUTH
 +Sg+Com:X5jnge # ;
 +Pl+Gen:X5jdge # ;
 +Pl+Gen:X5jdge# NAMAT ;
 +Pl+Acc:X5jdge # ;
 +Pl+Ill:X5jdege # ;
!+Pl+Com:X5jgujnge # ; !ComPl is out.

LEXICON GUHTENAI
 +Sg+Nom:nai # ;
 +Sg+Gen:X8nai # ;
 +Sg+Gen:X8nai# NAMAT ;
 +Sg+Acc:X8nai # ;
 +Sg+Ill:X3jnai # ;
 +Sg+Loc:X4snai # ;
 +Sg+Com:X5jnnai # ;
 +Ess:nnai # ;
 +Pl+Gen:X5jdnai # ;
 +Pl+Gen:X5jdnai# NAMAT ;
 +Pl+Acc:X5jdnai # ;
 +Pl+Ill:X5jdenai # ;



! Declineable indefinites with normal case paradigms
! - - - - - - - - - - - - - - - - - - - - - - - - - -

LEXICON declindef

eará+Pron+Indef:eará               indef-a-even-v ;
iežá+Pron+Indef:iežá                   indef-a-even-v ;
uhccánaš+Pron+Indef:uhccánažž       indef-a-even-c ;
unnánaš+Pron+Indef:unnánažž           indef-a-even-c ;

ea0nas+Pron+Indef:eatnas           indef-a-uneven ;
ea0naš+Pron+Indef:eatnaš               indef-a-uneven ;

muhtin+Pron+Indef:muhtim           indef-a-uneven-gahpir ;
muhtun+Pron+Indef:muhtum           indef-a-uneven-gahpir ;
muhtun+Pron+Indef:muhtom           indef-a-uneven-gahpir ; !SUB

muhtin+Pron+Indef+Sg+Gen:muhtin#       NAMAT ;
muhtun+Pron+Indef+Sg+Gen:muhtun#       NAMAT ;
muhtin+Pron+Indef+Pl+Gen:muhtimijd9#       NAMAT ;
muhtun+Pron+Indef+Pl+Gen:muhtumijd9#       NAMAT ;
muhtin+Pron+Indef+Pl+Loc:muhttuin K ;


uhccán+Pron+Indef:uhccán       indef-a-uneven-gahpir ;

unnán+Pron+Indef:unnán           indef-a-uneven-gahpir ;
veháš+Pron+Indef:veháž           indef-a-uneven-gahpir ;
váháš+Pron+Indef:váháž           indef-a-uneven-gahpir ;
veaháš+Pron+Indef:veaháž           indef-a-uneven-gahpir ;

soa0mes+Pron+Indef+Sg+Nom:soames   K ;
soa0mes+Pron+Indef+Ess:soapmásin   K ;
soa0mes+Pron+Indef:soapmás          indefattr ;
soa0mes+Pron+Indef:soapmásX2      indefdeclsguneven-ag ;
soa0mes+Pron+Indef:soapmás          indefdeclsguneven-ilc ;
soa0mes+Pron+Indef:soapmásX2      indefdeclpluneven-n ;
soa0mes+Pron+Indef:soapmási          indefdeclpluneven-obl ;

nuppát+Pron+Indef:nuppád              indefsg-a-uneven ;

duobbá+Pron+Indef:duobbá              indef-a-even-v ;
dábbá+Pron+Indef:dábbá              indef-a-even-v ;
juobbá+Pron+Indef:juobbá              indef-a-even-v ;
juobbá#goabbá+Pron+Indef: JUOBBAGOABBA ;

nubbi+Pron+Indef:nubbi             indef-a-even-v ;
nubbi+Pron+Indef+Acc:nuppe K ;
nubbi+Pron+Indef+Gen:nuppe K ;

juohkehaš+Pron+Indef:juohkehažž         indef-a-even-c ;

ollut+Pron+Indef:ollu           indefdeclpleven ;
olut+Pron+Indef:olu           indefdeclpleven ;
moanat+Pron+Indef:moana           indefdeclpleven ;
gallát+Pron+Indef+Attr:gallát       K ;
buohkat+Pron+Indef:buohka       indefdeclpleven ;
gait#divnnat+Pron+Indef:gait#divnna indefdeclpleven ;
gait#diinnat+Pron+Indef:gait#dijnna indefdeclpleven ;
gait#tiinnat+Pron+Indef:gait#tijnna indefdeclpleven ;
gaikkat+Pron+Indef:gajkka       indefdeclpleven ;
goappašagat+Pron+Indef:goappašaga  indefdeclpleven ;

ollugat+Pron+Indef:ollug       indu-pl-gahpir ;
oallugat+Pron+Indef:oallug       indu-pl-gahpir ;
olugat+Pron+Indef:olug           indu-pl-gahpir ;
gal0lis+Pron+Indef:gal'lis       indefdecluneven ;
olus+Pron+Indef:olus           indefdecluneven ;
ol0lus+Pron+Indef:ol'lus           indefdecluneven ; !SUB substandard
soamis+Pron+Indef:soamis       indefdecluneven ;

eambbosat+Pron+Indef:eambbos       indu-pl-gahpir ;
eambbogat+Pron+Indef:eambbog       indu-pl-gahpir ;
guoktásat+Pron+Indef:guoktás       indu-pl-gahpir ;
goappašat+Pron+Indef:goappaš       indu-pl-gahpir ;
goappašat+Pron+Indef+Pl+Gen:goappašijd9# NAMAT ;
goappešat+Pron+Indef:goappeš        indu-pl-gahpir ;

eanebut+Pron+Indef+Nom:eanebut       K ;
eanebut+Pron+Indef:eanebu       indefdeclpluneven-obl ;
eanemusat+Pron+Indef:eanemusX2       indefdeclpluneven-n ;
eanemusat+Pron+Indef:eanemusiX2       indefdeclpluneven-obl ;

eatnagat+Pron+Indef:eatnag       indu-pl-gahpir ;
galládat+Pron+Indef:gallád       indu-pl-gahpir ;


! Declension lexica for the declineable indefinites
! - - - - - - - - - - - - - - - - - - - - - - - - -

!splitting v- and c-final evensyll in two for NomEss, as usual.
LEXICON indef-a-even-v
 indefattr ;
 indefdecleven-v-ne ;
 indefdecleven-obl ;

LEXICON indef-a-even-c
 indefattr ;
 indefdecleven-c-ne ;
 :a indefdecleven-obl ;

LEXICON indefdecleven-obl
 indefdeclsgeven ;
 indefdeclpleven ;

LEXICON indefdecleven-v-ne
 +Sg+Nom: K ;
 +Ess:n K ;

LEXICON indefdecleven-c-ne
 +Sg+Nom: K ;
 +Ess:an K ;

LEXICON indefdeclsgeven
 +Sg+Gen:X4 K ;
 +Sg+Gen:X4# NAMAT ;
 +Sg+Acc:X4 K ;
 +Sg+Ill:X3j K ;
 +Sg+Loc:X4s K ;
 +Sg+Loc:X4n K ; !SUB !SOUTH
 +Sg+Com:X5jn K ;

LEXICON indefdeclpleven
 +Pl+Nom:X4t K ;
 +Pl+Gen:X5jd9 K ;
 +Pl+Gen:X5jd9# NAMAT ;
 +Pl+Acc:X5jd9 K ;
 +Pl+Ill:X5jde K ;
 +Pl+Loc:X5jn K ;
 +Pl+Com:X5jgujn K ;

LEXICON JUOBBAGOABBA
 +Sg+Nom:juobbá#goabbá K ;
 +Sg+Gen:juoppá#goappá K ;
 +Sg+Gen:juoppá#goappá# NAMAT ;
 +Sg+Acc:juoppá#goappá K ;
 +Sg+Ill:juobbái#goabbái K ;
 +Sg+Loc:juoppás#goappás K ;
 +Sg+Com:juoppáin#goappáin K ;
 +Ess:juobbán#goabbán K ;
 +Pl+Nom:juobbát#goabbát K ;
 +Pl+Gen:juoppájd9#goappájd9 K ;
 +Pl+Gen:juoppájd9#goappájd9# NAMAT ;
 +Pl+Acc:juoppájd9#goappájd9 K ;
 +Pl+Ill:juobbájde#goabbájde K ;
 +Pl+Loc:juoppájn#goappájn K ;
 +Pl+Com:juoppájgujn#goappájgujn K ;

! Intermediate lexica for uneven indefinite pronouns
! - - - - - - - - - - - - - - - - - - - - - - - - - -

LEXICON indef-a-uneven
 indefattr ;
 indefdecluneven ;

LEXICON indef-a-uneven-gahpir
 indefattr ;
 indefdecluneven-gahpir ;

LEXICON indefsg-a-uneven
 indefattr ;
 indu-sg ;

LEXICON indefdecluneven
 indu-sg ;
 indu-pl-gahpir ;

LEXICON indu-sg
 :X4 indef-sg-u-n ;
 indef-sg-u-e ;
 :X2 indefdeclsguneven-ag ;
 indefdeclsguneven-ilc ;

LEXICON indefdecluneven-gahpir
 indu-sg-gahpir ;
 indu-pl-gahpir ;

LEXICON indu-sg-gahpir
 indefdeclsguneven-ne ;
 indefdeclsguneven-ag ;
 indefdeclsguneven-ilc ;

LEXICON indu-pl-gahpir
 indefdeclpluneven-n ;
 :i indefdeclpluneven-obl ;
 indef-sg-u-e ; !For eatnagin

! Suffix lexica for uneven indefinite pronouns
! - - - - - - - - - - - - - - - - - - - - - - -

LEXICON indefdeclsguneven-ne
 indef-sg-u-n ;
 indef-sg-u-e ;

LEXICON indef-sg-u-n
 +Sg+Nom: K ;

LEXICON indef-sg-u-e
 +Ess:in K ;

LEXICON indefdeclsguneven-ag
 +Sg+Gen:a K ;
 +Sg+Acc:a K ;

LEXICON indefdeclsguneven-ilc
 +Sg+Ill:ij K ;
 +Sg+Loc:is K ;
 +Sg+Loc:in K ; !SUB !SOUTH
 +Sg+Com:ijn K ;

LEXICON indefdeclpluneven-n
 +Pl+Nom:at K ;

LEXICON indefdeclpluneven-obl
 +Pl+Gen:jd9 K ;
 +Pl+Acc:jd9 K ;
 +Pl+Ill:jdda K ;
 +Pl+Loc:jn K ;
 +Pl+Com:jgujn K ;

LEXICON indefattr
 +Attr:0 K ;


! Multiword indefinites
! - - - - - - - - - - -

LEXICON indefmulti ! added this lexicon, because otherwise these would be in indefattr
feara% mii+Pron+Indef+Sg+Nom:feara% mij K-son ; !SUB spellera dihte
feara% mii+Pron+Indef+Sg+Acc:feara% májd9 K-son ; !SUB spellera dihte
feara% gii+Pron+Indef+Sg+Nom:feara% gij K-son ; !SUB spellera dihte
feara% mii+Pron+Indef:feara% ma oblintercas ; !SUB spellera dihte
feara% gii+Pron+Indef:feara% gea oblintercas ; !SUB spellera dihte

vaikko% mii+Pron+Indef+Sg+Nom:vaikko% mij K-son ; !SUB spellera dihte
vaikko% mii+Pron+Indef+Sg+Acc:vaikko% májd9 K-son ; !SUB spellera dihte
vaikko% gii+Pron+Indef+Sg+Nom:vaikko% gij K-son ; !SUB spellera dihte
vaikko% mii+Pron+Indef:vaikko% ma oblintercas ; !SUB spellera dihte
vaikko% gii+Pron+Indef:vaikko% gea oblintercas ; !SUB spellera dihte
vaikko% makkár+Pron+Dem:vaikko% makkár DAKKAR ;  !SUB spellera dihte
vaikko% guh0te+Pron+Interr:vaikko% guh'te GUHTECASE ;  !SUB spellera dihte
vaikko% guhtemuš+Pron+Interr:vaikko% guđemužž GUHTEMUŠCASE ; !SUB spellera dihte
vaikko% goabbá+Pron+Interr:vaikko% goabbá GUHTECASE ; !SUB spellera dihte

! some multiword prons, according to Nickel
!feara% mii iind ;
!feara% gii iind ;
mii% nu iind ;
gii% nu iind ;
makkár% nu iind ;
makkáraš% nu iind ;


! The indeclineable indefinites
! - - - - - - - - - - - - - - -

LEXICON indeclindef
ald:ald9 iind ; !SUB
buot iind ;
buot:buot# NAMAT ;
gait:gajt iind ;
gait:gajt# NAMAT ;
eatnat iind ;
eatnat:eatnat# NAMAT ;
!fal iind ; ! cf. Nickel s. 126.
gallát iind ;
goabbat iind ;
goabbat:goabbat# NAMAT ;
goappaš iind ;
goappaš:goappaš# NAMAT ;
!goappašat iind ; ! thus both Nom and non-Nom. XXX.
guhtet iind ;
!ieš#guhtet iind ; !XXX Nom only
ieš#guhtet:ieš#guhtet# NAMAT ;
ihkinassii iind ;
juohke iind ;
juohke:juohke# NAMAT ;
ollu iind ;
olu iind ;
!vaikko iind ; !This is CS, not pron


LEXICON iind
 +Pron+Indef:0 K ;



! Separate lexica for exceptional entries
! - - - - - - - - - - - - - - - - - - - -

LEXICON declindef-idiosync
! separate lexica for these entries:
! oktat
oktahat:ovttaha OVTTAHAT ;
seammahat:seammaha OVTTAHAT ; !SUB
seammahat+Pron+Indef+Attr:seammahat # ; !SUB
seammahat+Pron+Indef+Nom+Sg:seammahat # ; !SUB
oktahat+Pron+Indef+Attr:oktahat K ;
aktahat+Pron+Indef+Attr:aktahat K ;
oktahat+Pron+Indef+Nom+Sg:oktahat K ;
aktahat+Pron+Indef+Nom+Sg:aktahat K ;

 earrás EARRAS ;
ieš#guhtet+Pron+Indef+Nom:ieš#guhtet K ;
ieš#guhtet+Pron+Indef+Gen:ieš#guđet K ;
ieš#guhtet+Pron+Indef+Gen:ieš#guđet# NAMAT ;
ieš#guhtet+Pron+Indef+Acc:ieš#guđet K ;
simme+Pron+Indef:simme SIMME ; !SUB
seamma+Pron+Indef:seamma SEAMMA ; !SUB
seammá+Pron+Indef:seammá SEAMMA ;
seamma+Pron+Indef+Attr:seamma K ;
seammá+Pron+Indef+Attr:seammá # ; !SUB, dict says seamma ~ seammá (#seamma)
juoga+Pron+Indef:juo JUOGA ;
gal0le+Pron+Indef:gal'l GUOKTE ; !^N^
guktot+Pron+Indef:gukto indefdeclplcontr ;
! okta
! juoga mii
!oktahat         +       no      OKTAHAT => indefeven / Nom=aktahat / OKTAttr
!        OKTattr:n=oktahat, aktahat, oktat, aktat
!                o=ovttahat, ovttat
!hárvvis        +       a       indef-a-      uneven  hárve, hárvves
!hárvvis        +       a       indef-a-      contr   hárve, hárvves
!Some problematic ones, one form for nsg, one for the rest
!oktat+Pron+Indef+Sg+Nom:oktat K ;
!oktat+Pron+Indef:ovttat K ;
!oktahat+Pron+Indef+Sg+Nom:oktahat K ;
!oktahat+Pron+Indef+:ovttahat K ;

LEXICON indefdeclplcontr ! so far guktot
 +Pl+Nom:t K ;
 +Pl+Gen:jd9 K ;
 +Pl+Gen:jd9# NAMAT ;
 +Pl+Acc:jd9 K ;
 +Pl+Ill:jde K ;
 +Pl+Loc:jn K ;
 +Pl+Com:jn K ;

LEXICON EARRAS
 +Sg+Acc:a K ;
 +Sg+Gen:a K ;
 +Sg+Ill:ij K ;
 +Sg+Loc:is K ;
 +Sg+Loc:in K ; !SUB !SOUTH
 +Sg+Com:in K ;
! Essive and plural ???

LEXICON SEAMMA
 +Sg+Nom: K ;
 +Sg+Acc: K ;
 +Sg+Gen: K ;
 +Sg+Gen:# NAMAT ;
 +Sg+Ill:j K ;  ! Let's await the verdict of the jury..
! +Sg+Loc:s K ; ! Let's settle for the Adv reading
 +Sg+Loc:n K ;  !SUB !SOUTH, well..., probably this one should Adv as well
 +Sg+Com:jn K ; ! Let's await the verdict of the jury..
 +Pl+Nom:t K ;
 +Pl+Acc:jd9 K ;
 +Pl+Gen:jd9 K ;
 +Pl+Gen:jd9# NAMAT ;

LEXICON SIMME !SUB
 +Sg+Nom: K ;
 +Sg+Acc: K ;
 +Sg+Gen: K ;
 +Sg+Ill:j K ;
 +Sg+Loc:n K ;
 +Sg+Com:jn K ;
 +Pl+Nom:t K ;
 +Pl+Acc:jd9 K ;
 +Pl+Gen:jd9 K ;
 +Pl+Gen:jd9# NAMAT ;



LEXICON JUOGA
 +Sg+Nom:ga K ;
 +Sg+Acc:ga K ;
 +Sg+Acc:jdá K ;
 +Sg+Acc:jdájd9 K ;
 +Sg+Acc:jddá K ; !SUB
 +Sg+Acc:jaaájd9 K ; !SUB
 +Sg+Gen:ga K ;
 +Sg+Gen:ga# NAMAT ;
 +Sg+Ill:sat K ;
 +Sg+Loc:stá K ;
 +Sg+Com:jdájn K ;
 +Ess:jdán K ;
 +Ess:nin K ;
 +Attr:da K ;
 
LEXICON OVTTAHAT
 +Sg+Acc:ga K ;
 +Sg+Gen:ga K ;
 +Sg+Gen:t# NAMAT ;
 +Sg+Ill:hkii K ;
 +Sg+Loc:gas K ;
 +Sg+Com:gajn K ;
 +Ess:hkan K ;
 +Pl+Nom:gat K ;
 +Pl+Acc:gajd9 K ;
 +Pl+Gen:gajd9 K ;
 +Pl+Ill:gajde K ;
 +Pl+Loc:gajn K ;
 +Pl+Com:gajgujn K ;



